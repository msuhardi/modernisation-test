      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C36S.                                                 
       AUTHOR.        ALWYN BENNY.                                              
       DATE-WRITTEN.  11/10/11.                                                 
                                                                                
      ************************************************************              
      *                                                          *              
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                    *              
      *                                                          *              
      *    SYSTEM ID   : BP13                                    *              
      *                                                          *              
      *    OBJECTIVE   :                                         *              
      *    TO PROCESS VALID KIV FILE FROM BP13K140 & WRITE TO    *              
      *    BP13K130 & BP13K350,BP13K352.                         *              
      *                                                          *              
      *     INPUT FILE :                                         *              
      *                  1. BP13F124                             *              
      *                  2. BP13K800 (SOC MASTER FILE)           *              
      *                                                          *              
      *     OUTPUT FILE:                                         *              
      *                  1. BP13K130 (SOCHIST   FILE)            *              
      *                  2. BP13K140 (KIVTRAN   FILE)            *              
      *                  3. BP13K350 (STUDIO APARTMENT FILE)     *              
      *                  4. BP13K352 (AMR APT DET FOR 2R/3R)     *              
      *                  5. BP13KD05                             *              
      *                                                          *              
      *  CHG-NO   BY   ON        DESCRIPTION                     *              
      *  -------- ---  --------  ------------------------------  *              
      *  BP134408 AB9  11/11/11  NEW PROGRAM                     *              
      *  BP134872 JGO1 02/08/13  READ BP13K410 IF NOT FOUND IN   *              
      *                          K800 FOR STUDIO UNITS           *              
      *  BP134917 JGO1 06/08/13  REMOVE CHECKING OF NUM-ALLO-CAT *              
      *                          FOR CASES NOT IN K800           *              
      *  BP134917 JGO1 12/08/13  MOVE CURR DATE TO K130-DTE-POST *              
      *  BP135037 JGO1 16/08/13  ALLOW CONTRA AS LONG AS SA      *              
      *  BP134969 ESD1 14/01/14  CATER TO FOR AMR NON SA WITH NO K800*          
      *  BP135291 ESD1 03/09/14  CATER FOR SUG CASE                             
      *  BP135621 ESD1 10/02/15  ADD CHECKING FOR OTHER CODES WHEN              
      *                          READING BP13K130                               
      *  BP135637 ESD1 25/02/15  USE THE CORRECT DTE-POST TO READ K130          
      *                          TO PREVENT LOOPING.                            
      *  BP136367 KSJ3 01/09/16  CATER FOR 2 ROOM FLEXI                         
      *  BP138668 LJL1 24/03/21  CATER FOR NEW RECEIPTING CODES  *              
      *                          6095I FOR CASES UNDER ALLOCATION*              
      *                                SCHEME SHS"              **              
      **************************************************************            
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F124 ASSIGN         TO BP13F124.                          
                                                                                
           SELECT BP13K130 ASSIGN         TO BP13K130                           
                           ACCESS MODE    IS DYNAMIC                            
                           ORGANIZATION   IS INDEXED                            
                           RECORD KEY     IS K130-KEY-FLD                       
                           FILE STATUS    IS WS-K130-STATUS.                    
                                                                                
                                                                                
           SELECT BP13K350 ASSIGN         TO BP13K350                           
                           ACCESS MODE       IS RANDOM                          
                           ORGANIZATION      IS INDEXED                         
                           RECORD KEY        IS K350-KEY-FLD                    
                           FILE STATUS       IS WS-K350-STATUS.                 
                                                                                
           SELECT BP13K352 ASSIGN         TO BP13K352                           
                           ACCESS MODE       IS RANDOM                          
                           ORGANIZATION      IS INDEXED                         
                           RECORD KEY        IS K352-KEY-FLD                    
                           FILE STATUS       IS WS-K352-STATUS.                 
                                                                                
                                                                                
           SELECT BP13KD05  ASSIGN        TO BP13KD05                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD05-KEY-FLD                       
                            FILE STATUS   IS WS-KD05-STATUS.                    
                                                                                
           SELECT BP13K140 ASSIGN         TO BP13K140                           
                           ACCESS MODE       IS RANDOM                          
                           ORGANIZATION      IS INDEXED                         
                           RECORD KEY        IS K140-KEY-FLD                    
                           FILE STATUS       IS WS-K140-STATUS.                 
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K410-NUM-REGN                      
                            FILE STATUS   IS WS-K410-STATUS.                    
                                                                                
           SELECT BP13F52A ASSIGN         TO BP13F52A.                          
           SELECT BP13F14A ASSIGN         TO BP13F14A.                          
           SELECT BP13L36S ASSIGN         TO BP13L36S.                          
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  BP13F124                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F124.                                                           
                                                                                
       FD  BP13K130                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K130.                                                           
                                                                                
       FD  BP13K350                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K350.                                                           
                                                                                
       FD  BP13K352                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K352.                                                           
                                                                                
       FD  BP13KD05                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13KD05.                                                           
                                                                                
       FD  BP13K140                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K140.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD  BP13F52A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 130 CHARACTERS.                                      
       COPY BP13F52A.                                                           
                                                                                
       FD  BP13F14A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       01  BP13F14A-REC                  PIC X(150).                            
                                                                                
       FD  BP13L36S                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC                      PIC X(132).                           
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
       01  WS-VARIABLES.                                                        
           05  WS-COUNT-HISTORY           PIC 9(3)       VALUE ZEROS.           
           05  WS-CDE-ERROR               PIC X          VALUE SPACES.          
       01  WS-NUM-NRIC1                   PIC X(9)       VALUE SPACES.          
       01  WS-STUDIO-TAG                  PIC X(2)       VALUE SPACES.          
       01  WS-LINE-CNT                    PIC 99         VALUE 66.              
       01  WS-PAGE-CNT                    PIC 9(5)       VALUE ZERO.            
       01  CNT-KIV-CASES                  PIC 9(4)       VALUE ZERO.            
       01  WS-AMT-KIV-TOTAL               PIC 9(6)V99    VALUE ZEROES.          
                                                                                
       01  WS-DTE-CUR.                                                          
           05  WS-CC               PIC 99.                                      
           05  WS-YY               PIC 99.                                      
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  WS-TRANS-DATE.                                                       
           05  WS-TRANS-YYYY       PIC 9(4).                                    
           05  WS-TRANS-MM         PIC 9(2).                                    
           05  WS-TRANS-DD         PIC 9(2).                                    
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K130-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K140-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K350-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-KD05-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K352-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K110-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K800-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K410-STATUS             PIC 9(2)       VALUE ZEROES.          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K130-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-K140-DELETE             PIC 9(5)       VALUE ZEROES.          
           05  WS-K350-REWRITE            PIC 9(5)       VALUE ZEROES.          
           05  WS-K352-REWRITE            PIC 9(5)       VALUE ZEROES.          
           05  WS-K350-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-K352-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-KD05-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-F52A-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-F14A-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-K110-READ               PIC 9(5)       VALUE ZEROES.          
           05  WS-K140-READ               PIC 9(5)       VALUE ZEROES.          
           05  WS-K110-NOTFD              PIC 9(5)       VALUE ZEROES.          
           05  WS-K140-NOTFND             PIC 9(5)       VALUE ZEROES.          
           05  WS-K800-READ               PIC 9(5)       VALUE ZEROES.          
           05  WS-KD05-READ               PIC 9(5)       VALUE ZEROES.          
           05  WS-K800-NOTFD              PIC 9(5)       VALUE ZEROES.          
           05  WS-F124-READ               PIC 9(5)       VALUE ZEROES.          
           05  WS-CNT-HIST                PIC 9(2)       VALUE ZEROES.          
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F124-EOF                PIC X          VALUE 'N'.             
           05  WS-KD05-EOF                PIC X          VALUE 'N'.             
           05  WS-KD05-FND                PIC X          VALUE 'N'.             
           05  WS-KD05-TAG                PIC X          VALUE 'N'.             
           05  WS-K130-WRITTEN            PIC X          VALUE SPACE.           
           05  WS-K140-WRITTEN            PIC X          VALUE SPACE.           
           05  WS-K350-WRITTEN            PIC X          VALUE SPACE.           
           05  WS-K352-WRITTEN            PIC X          VALUE SPACE.           
           05  WS-K800-REC-FND            PIC X          VALUE 'N'.             
                                                                                
       01  WS-AMT-TXN                     PIC 9(8)V99    VALUE ZEROS.           
       01  WS-BAL-TXN                     PIC 9(8)V99    VALUE ZEROS.           
                                                                                
       01  L003-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L36S'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  13S1-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  13S1-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L003-PR-HEAD-02.                                                     
           05  FILLER              PIC X(39)     VALUE SPACES .                 
           05  FILLER              PIC X(53)     VALUE                          
               ' VALID TRANSACTIONS NOT DELETED FROM THE KIV FILE '.            
           05  FILLER              PIC X(40)     VALUE                          
               '      (SALES - AGREEMENT/APPLICATION)'.                         
                                                                                
       01  L003-PR-HEAD-03.                                                     
           05  FILLER              PIC X(28)                                    
                  VALUE '   S/NO     SCH-ACCNT NO    '.                         
           05  FILLER              PIC X(34)                                    
                  VALUE 'REGN NO       AMOUNT    RECEIPT NO'.                   
           05  FILLER              PIC X(39)                                    
                  VALUE '      DATE POST   PAY TYPE       REMARK'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(6)     VALUE 'H-CNT '.                 
                                                                                
       01  L003-PR-DETAILS.                                                     
           05  13S1-SNO            PIC ZZZZZ9 .                                 
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  13S1-NUM-SCHACCNT   PIC X(11).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  13S1-ORIG-REG-NO    PIC X(8).                                    
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  13S1-AMT            PIC $$$$$$9.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  13S1-NUM-RECEIPT    PIC X(12) .                                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  13S1-DTE-POST.                                                   
               10  13S1-POST-DD    PIC X(2)      VALUE SPACES.                  
               10  13S1-POST-FIL1  PIC X         VALUE '/'.                     
               10  13S1-POST-MM    PIC X(2)      VALUE SPACES.                  
               10  13S1-POST-FIL2  PIC X         VALUE '/'.                     
               10  13S1-POST-YYYY  PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES .                 
           05  13S1-PAYMENT-TYPE   PIC X(03).                                   
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  13S1-REMARK         PIC X(35).                                   
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  13S1-CDE-HIST       PIC X(1)      VALUE SPACES.                  
           05  13S1-CNT-HIST       PIC X(2)      VALUE SPACES.                  
                                                                                
       01  L003-CONTROL-LINE.                                                   
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  CNTL-ITEM           PIC X(30)     VALUE SPACES.                  
           05  CNTL-CNT            PIC ZZZZ9 .                                  
           05  FILLER              PIC X(58)     VALUE SPACES.                  
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      ******************************************************************        
       000-MAIN.                                                                
      ******************************************************************        
                                                                                
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                          
           MOVE FUNCTION CURRENT-DATE   TO WS-DTE-CUR.                          
           PERFORM 200-READ-BP13F124    THRU 200-EXIT.                          
           PERFORM 300-PROCESS-BP13F124 THRU 300-EXIT                           
                   UNTIL WS-F124-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-FILES      THRU 900-EXIT.                          
                                                                                
           MOVE 1                       TO WS-LINE-CNT                          
           PERFORM 1100-ERROR-HEADING   THRU 1100-EXIT.                         
                                                                                
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES.                                                          
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F124                                                 
                       BP13K800                                                 
                       BP13K410                                                 
                I-O    BP13K130                                                 
                       BP13K350                                                 
                       BP13K352                                                 
                       BP13KD05                                                 
                       BP13K140                                                 
                OUTPUT BP13L36S                                                 
                       BP13F52A                                                 
                       BP13F14A.                                                
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K130. STATUS : ' WS-K130-STATUS        
              MOVE WS-K130-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K350-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K350. STATUS : ' WS-K350-STATUS        
              MOVE WS-K350-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K352-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K352. STATUS : ' WS-K352-STATUS        
              MOVE WS-K352-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13KD05. STATUS : ' WS-KD05-STATUS        
              MOVE WS-KD05-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K140-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K140. STATUS : ' WS-K140-STATUS        
              MOVE WS-K140-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K800. STATUS : ' WS-K800-STATUS        
              MOVE WS-K800-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K410. STATUS : ' WS-K410-STATUS        
              MOVE WS-K410-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F124.                                                       
      ******************************************************************        
                                                                                
           READ BP13F124    AT END                                              
                MOVE 'Y'    TO WS-F124-EOF                                      
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1            TO WS-F124-READ.                                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-BP13F124.                                                    
      ******************************************************************        
                                                                                
           MOVE ZEROS                   TO WS-BAL-TXN                           
                                           WS-AMT-TXN.                          
           MOVE 'N'                     TO WS-KD05-TAG                          
                                           WS-K800-REC-FND.                     
           MOVE SPACES                  TO WS-NUM-NRIC1 WS-CDE-ERROR.           
           IF F124-CDE-VALID = 'V'                                              
             IF F124-CDE-PAYMENT-TYPE = 'AHG' OR 'CHG' OR 'INT' OR 'SHG'        
                                         OR 'SUG'                               
                PERFORM 651-READ-KD05            THRU 651-EXIT                  
                PERFORM 655-UPD-K130-AHG         THRU 655-EXIT                  
             ELSE                                                               
               IF F124-CDE-PAYMENT-TYPE = 'ADV' OR 'ADP' OR 'AMR'               
                  PERFORM 470-READ-K800       THRU 470-EXIT                     
                END-IF                                                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F124    THRU 200-EXIT.                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       470-READ-K800.                                                           
      ******************************************************************        
                                                                                
             IF F124-NUM-REGN NOT = SPACES AND LOW-VALUES AND ALL 'X'           
                MOVE F124-NUM-REGN            TO    K800-NUM-REGN               
             ELSE                                                               
                GO TO 470-EXIT                                                  
             END-IF.                                                            
                                                                                
             READ BP13K800.                                                     
             EVALUATE WS-K800-STATUS                                            
               WHEN 00                                                          
                  MOVE 'Y'                      TO    WS-K800-REC-FND           
                  MOVE K800-NUM-NRIC1           TO    WS-NUM-NRIC1              
                  PERFORM 471-CHK-STUDIO        THRU  471-EXIT                  
               WHEN 23                                                          
                  PERFORM  477-READ-K410         THRU  477-EXIT                 
               WHEN OTHER                                                       
                  MOVE WS-K800-STATUS            TO RETURN-CODE                 
                  DISPLAY 'ERROR READING BP13K800. STATUS : '                   
                                                  WS-K800-STATUS                
                  PERFORM 900-CLOSE-FILES          THRU 900-EXIT                
             END-EVALUATE.                                                      
                                                                                
       470-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       471-CHK-STUDIO.                                                          
      ******************************************************************        
                                                                                
                IF (K800-NUM-IDP-SCHEME = 'SA ') OR                             
                   (K800-NUM-FLAT-TYPE  = '1A' OR '2A') OR                      
                   (K800-NUM-FLAT-TYPE  = '2F' AND                              
                    K800-NUM-LEASE-TENURE > ZEROES AND                          
                    K800-NUM-LEASE-TENURE <= 45)                                
                                                                                
      *             (K800-NUM-ALLO-CAT = 'NPL')                                 
                    PERFORM 472-READ-K350  THRU    472-EXIT                     
                ELSE                                                            
                    IF (K800-NUM-FLAT-TYPE = '2 ' OR '3 ' OR '2R'               
                        OR '3R')  AND                                           
                        (K800-NUM-ALLO-CAT = 'NPL' OR 'SHS')                    
                       PERFORM 475-READ-K352  THRU    475-EXIT                  
                    ELSE                                                        
                       DISPLAY 'NOT A STUDIO OR AMR CASE, REGN:'                
                                                      F124-NUM-REGN             
                       MOVE 'S'                    TO    WS-CDE-ERROR           
                       PERFORM  555-WRITE-BP13F52A    THRU  555-EXIT            
                       PERFORM  1000-ERROR-LIST       THRU  1000-EXIT           
                   END-IF                                                       
              END-IF.                                                           
                                                                                
       471-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       472-READ-K350.                                                           
      ******************************************************************        
                                                                                
             MOVE F124-NUM-REGN TO K350-NUM-REGN.                               
                                                                                
             READ BP13K350.                                                     
             EVALUATE WS-K350-STATUS                                            
               WHEN 00                                                          
                  PERFORM 485-MOVE-FIELDS       THRU  485-EXIT                  
                  REWRITE BP13K350-REC                                          
                  DISPLAY 'REWRITE REC IN K350: ' K350-NUM-REGN                 
                  IF WS-K350-STATUS = '00'                                      
                    ADD 1                         TO WS-K350-REWRITE            
                    MOVE  1                       TO WS-COUNT-HISTORY           
                    MOVE  '01'                    TO   WS-STUDIO-TAG            
                    PERFORM 600-WRITE-BP13K130    THRU   600-EXIT               
                  ELSE                                                          
                    MOVE WS-K350-STATUS           TO RETURN-CODE                
                    DISPLAY 'ERROR UPDATING BP13K350. STATUS : '                
                                                  WS-K350-STATUS                
                    PERFORM 900-CLOSE-FILES          THRU 900-EXIT              
                  END-IF                                                        
               WHEN 23                                                          
                   IF F124-CDE-CRDR = 'C'                                       
                      PERFORM 473-WRITE-K350        THRU  473-EXIT              
                   ELSE                                                         
                     IF F124-CDE-PAYMENT-TYPE = 'ADV'                           
                      PERFORM 473-WRITE-K350        THRU  473-EXIT              
                     ELSE                                                       
                       DISPLAY 'RECORD NOT FOUND IN K350 FOR REFUND:'           
                                                     F124-NUM-REGN              
                     END-IF                                                     
                   END-IF                                                       
               WHEN OTHER                                                       
                  MOVE WS-K350-STATUS            TO RETURN-CODE                 
                  DISPLAY 'ERROR READING BP13K350. STATUS : '                   
                                                  WS-K350-STATUS                
                  PERFORM 900-CLOSE-FILES          THRU 900-EXIT                
             END-EVALUATE.                                                      
                                                                                
       472-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       473-WRITE-K350.                                                          
      ******************************************************************        
                                                                                
               MOVE   SPACES                  TO    BP13K350-REC.               
               INITIALIZE                           BP13K350-REC.               
               MOVE F124-NUM-REGN TO K350-NUM-REGN.                             
               PERFORM 485-MOVE-FIELDS    THRU    485-EXIT.                     
               WRITE BP13K350-REC.                                              
               IF WS-K350-STATUS  = '00'                                        
                  ADD 1 TO WS-K350-WRITE                                        
                  MOVE  1                       TO WS-COUNT-HISTORY             
                  MOVE  '01'                    TO   WS-STUDIO-TAG              
                    PERFORM 600-WRITE-BP13K130    THRU   600-EXIT               
               ELSE                                                             
                  MOVE WS-K350-STATUS            TO RETURN-CODE                 
                  DISPLAY 'ERROR WRITING BP13K350. STATUS : '                   
                                                  WS-K350-STATUS                
                  PERFORM 900-CLOSE-FILES          THRU 900-EXIT                
                END-IF.                                                         
                                                                                
       473-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       475-READ-K352.                                                           
      ******************************************************************        
                                                                                
             MOVE F124-NUM-REGN          TO    K352-NUM-REGN.                   
             READ BP13K352.                                                     
             EVALUATE  WS-K352-STATUS                                           
             WHEN 00                                                            
                PERFORM 495-MOVE-FIELDS    THRU  495-EXIT                       
                REWRITE BP13K352-REC                                            
                IF WS-K352-STATUS = '00'                                        
                   ADD 1 TO WS-K352-REWRITE                                     
                   MOVE  1  TO WS-COUNT-HISTORY                                 
                   MOVE  '02'                     TO  WS-STUDIO-TAG             
                   PERFORM 600-WRITE-BP13K130     THRU   600-EXIT               
                ELSE                                                            
                    MOVE WS-K352-STATUS    TO RETURN-CODE                       
                    DISPLAY 'ERROR WRITING BP13K352. STATUS : '                 
                                                     WS-K352-STATUS             
                    PERFORM 900-CLOSE-FILES THRU 900-EXIT                       
                END-IF                                                          
             WHEN 23                                                            
                IF F124-CDE-CRDR = 'C'                                          
                   PERFORM 476-WRITE-K352          THRU   476-EXIT              
                ELSE                                                            
                   IF F124-CDE-PAYMENT-TYPE = 'ADV'                             
                      PERFORM 476-WRITE-K352          THRU   476-EXIT           
                  ELSE                                                          
                    DISPLAY 'RECORD NOT FOUND IN K352 FOR REFUND:'              
                                                     F124-NUM-REGN              
                  END-IF                                                        
                END-IF                                                          
             WHEN OTHER                                                         
                  MOVE WS-K352-STATUS            TO RETURN-CODE                 
                  DISPLAY 'ERROR READING BP13K352. STATUS : '                   
                                                  WS-K350-STATUS                
                  PERFORM 900-CLOSE-FILES          THRU 900-EXIT                
             END-EVALUATE.                                                      
                                                                                
                                                                                
       475-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       476-WRITE-K352.                                                          
      ******************************************************************        
                                                                                
               MOVE   SPACES                  TO    BP13K352-REC.               
               INITIALIZE                           BP13K352-REC.               
               MOVE F124-NUM-REGN TO K352-NUM-REGN.                             
               PERFORM 495-MOVE-FIELDS    THRU  495-EXIT.                       
               WRITE BP13K352-REC.                                              
               IF WS-K352-STATUS  = '00'                                        
                  ADD 1 TO WS-K352-WRITE                                        
                  MOVE  1                       TO WS-COUNT-HISTORY             
                  MOVE  '02'                    TO   WS-STUDIO-TAG              
                  PERFORM 600-WRITE-BP13K130    THRU   600-EXIT                 
               ELSE                                                             
                  MOVE WS-K352-STATUS            TO RETURN-CODE                 
                  DISPLAY 'ERROR WRITING BP13K352. STATUS : '                   
                                                  WS-K352-STATUS                
                  PERFORM 900-CLOSE-FILES          THRU 900-EXIT                
                END-IF.                                                         
                                                                                
       476-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       477-READ-K410.                                                           
      ******************************************************************        
                                                                                
             MOVE F124-NUM-REGN            TO    K410-NUM-REGN.                 
                                                                                
             READ BP13K410.                                                     
             EVALUATE WS-K410-STATUS                                            
               WHEN 00                                                          
                  MOVE 'Y'              TO    WS-K800-REC-FND                   
                  MOVE K410-NRIC1       TO    WS-NUM-NRIC1                      
                  MOVE K410-NUM-SCH-ACC  TO K800-NUM-SCH-ACC                    
                  PERFORM 478-CHK-STUDIO-K410   THRU  478-EXIT                  
               WHEN 23                                                          
                  PERFORM 475-READ-K352  THRU    475-EXIT                       
                                                                                
               WHEN OTHER                                                       
                  MOVE WS-K410-STATUS            TO RETURN-CODE                 
                  DISPLAY 'ERROR READING BP13K410. STATUS : '                   
                                                  WS-K410-STATUS                
                  PERFORM 900-CLOSE-FILES          THRU 900-EXIT                
             END-EVALUATE.                                                      
                                                                                
       477-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       478-CHK-STUDIO-K410.                                                     
      ******************************************************************        
                                                                                
                IF K410-CDE-IDP-SCH    = 'SA '                                  
      *             K410-NUM-ALLO-CAT = 'NPL'                                   
                    PERFORM 472-READ-K350  THRU    472-EXIT                     
                ELSE                                                            
                    PERFORM 475-READ-K352  THRU    475-EXIT                     
                END-IF.                                                         
                                                                                
       478-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       485-MOVE-FIELDS.                                                         
      ******************************************************************        
                                                                                
             MOVE F124-NUM-REGN          TO    K350-NUM-REGN.                   
             IF K350-NUM-SCH-ACC  =  LOW-VALUES OR SPACES                       
                MOVE K800-NUM-SCH-ACC    TO    K350-NUM-SCH-ACC                 
             END-IF.                                                            
             IF F124-AMT-RECIEPT         IS    NOT NUMERIC                      
                MOVE ZEROS               TO    F124-AMT-RECIEPT                 
             END-IF.                                                            
             EVALUATE F124-CDE-PAYMENT-TYPE                                     
                 WHEN 'ADV'                                                     
                   COMPUTE K350-AMT-ADV-COLLECTION = F124-AMT-RECIEPT +         
                                               K350-AMT-ADV-COLLECTION          
                 WHEN 'ADP'                                                     
                   PERFORM 500-COMPUTE-ADV-STUDIO  THRU  500-EXIT               
                   MOVE 'A'                    TO    K350-CDE-PRINT             
             END-EVALUATE.                                                      
      *      MOVE F124-DTE-TRANS         TO    K350-DTE-POST.                   
             MOVE F124-DTE-POST          TO    K350-DTE-POST.                   
             MOVE F124-NUM-RECPT-JRNO    TO    K350-NUM-RECPT-JRNO.             
             MOVE WS-DTE-CUR             TO    K350-DTE-UPDATE.                 
             MOVE 'BP13C36S'             TO    K350-NUM-USERID.                 
                                                                                
       485-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       495-MOVE-FIELDS.                                                         
      ******************************************************************        
                                                                                
             MOVE F124-NUM-REGN          TO    K352-NUM-REGN.                   
             IF K352-NUM-SCH-ACC  =  LOW-VALUES OR SPACES                       
                MOVE K800-NUM-SCH-ACC    TO    K352-NUM-SCH-ACC                 
             END-IF.                                                            
             IF F124-AMT-RECIEPT         IS NOT NUMERIC                         
                MOVE ZEROS               TO    F124-AMT-RECIEPT                 
             END-IF.                                                            
             EVALUATE F124-CDE-PAYMENT-TYPE                                     
                 WHEN 'ADV'                                                     
                   COMPUTE K352-AMT-ADV-COLLECTION = F124-AMT-RECIEPT +         
                                               K352-AMT-ADV-COLLECTION          
                 WHEN 'AMR'                                                     
                      PERFORM 510-COMPUTE-ADV-NON-STUDIO THRU 510-EXIT          
                      MOVE 'A'                    TO    K352-CDE-PRINT          
             END-EVALUATE.                                                      
      *      MOVE F124-DTE-TRANS         TO    K352-DTE-POST.                   
             MOVE F124-DTE-POST          TO    K352-DTE-POST.                   
             MOVE F124-NUM-RECPT-JRNO    TO    K352-NUM-RECPT-JRNO.             
             MOVE WS-DTE-CUR             TO    K352-DTE-UPDATE.                 
             MOVE 'BP13C36S'             TO    K352-NUM-USERID.                 
                                                                                
       495-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-COMPUTE-ADV-STUDIO.                                                  
      ******************************************************************        
                IF F124-CDE-CRDR = 'D'                                          
                    COMPUTE K350-AMT-REFUND-ADV-COL =                           
                                          K350-AMT-REFUND-ADV-COL +             
                                          ( F124-AMT-RECIEPT * -1)              
                 ELSE                                                           
                    COMPUTE K350-AMT-REFUND-ADV-COL =                           
                                           K350-AMT-REFUND-ADV-COL +            
                                           F124-AMT-RECIEPT                     
                 END-IF.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       510-COMPUTE-ADV-NON-STUDIO.                                              
      ******************************************************************        
              EVALUATE  F124-NUM-RECPT-JRNO(1:1)                                
                 WHEN 'B'                                                       
                     IF F124-CDE-CRDR = 'D'                                     
                        COMPUTE K352-AMT-REFUND-ADV-COL =                       
                                               K352-AMT-REFUND-ADV-COL +        
                                             ( F124-AMT-RECIEPT * -1)           
                     ELSE                                                       
                         COMPUTE K352-AMT-ADV-COLLECTION =                      
                             K352-AMT-ADV-COLLECTION + F124-AMT-RECIEPT         
                     END-IF                                                     
                WHEN 'P'                                                        
                     IF F124-CDE-CRDR = 'D'                                     
                        COMPUTE K352-AMT-REFUND-ADV-COL =                       
                                               K352-AMT-REFUND-ADV-COL +        
                                             ( F124-AMT-RECIEPT * -1)           
                     ELSE                                                       
                         COMPUTE K352-AMT-ADV-COLLECTION =                      
                             K352-AMT-ADV-COLLECTION + F124-AMT-RECIEPT         
                     END-IF                                                     
                WHEN 'J'                                                        
                      IF F124-CDE-CRDR = 'D'                                    
                         COMPUTE K352-AMT-ADV-COLLECTION-USED =                 
                                 K352-AMT-ADV-COLLECTION-USED  +                
                                       ( F124-AMT-RECIEPT * -1)                 
                      ELSE                                                      
                         COMPUTE K352-AMT-ADV-COLLECTION =                      
                             K352-AMT-ADV-COLLECTION + F124-AMT-RECIEPT         
                      END-IF                                                    
             END-EVALUATE.                                                      
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       555-WRITE-BP13F52A.                                                      
      ******************************************************************        
                                                                                
           PERFORM 565-MOVE-VALUES-F52A    THRU 565-EXIT.                       
           WRITE BP13F52A-REC.                                                  
           ADD 1 TO WS-F52A-WRITE.                                              
                                                                                
       555-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       565-MOVE-VALUES-F52A.                                                    
      ******************************************************************        
                                                                                
           MOVE  SPACES                TO    BP13F52A-REC.                      
           INITIALIZE                        BP13F52A-REC.                      
           IF F124-NUM-SCH-ACCT = LOW-VALUES                                    
              MOVE SPACES TO F124-NUM-SCH-ACCT                                  
           END-IF.                                                              
           MOVE F124-NUM-SCH-ACCT      TO    F52A-NUM-HDBREF.                   
           MOVE F124-DTE-TRANS         TO    F52A-DTE-TXN.                      
           MOVE F124-NUM-RECPT-JRNO    TO    F52A-NUM-RCPT.                     
           MOVE F124-AMT-RECIEPT       TO    F52A-AMT-TXN.                      
           MOVE F124-CDE-CRDR          TO    F52A-NUM-CRDR.                     
           MOVE F124-NUM-REGN          TO    F52A-NUM-REF.                      
                                                                                
       565-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       600-WRITE-BP13K130.                                                      
      ******************************************************************        
                                                                                
           PERFORM 700-MOVE-VALUES-TO-K130 THRU 700-EXIT.                       
                                                                                
           WRITE BP13K130-CDHIST.                                               
                                                                                
           IF WS-K130-STATUS = '00'                                             
              ADD 1                        TO   WS-K130-WRITE                   
              PERFORM  690-DELETE-K140     THRU  690-EXIT                       
                                                                                
           ELSE                                                                 
              IF WS-K130-STATUS = '22'                                          
                 MOVE 'N' TO WS-K130-WRITTEN                                    
                 PERFORM 800-READ-BP13K130 THRU  800-EXIT UNTIL                 
                             WS-K130-WRITTEN = 'Y'                              
              ELSE                                                              
                 MOVE WS-K130-STATUS       TO    RETURN-CODE                    
                 DISPLAY 'ERROR READING BP13K130. STATUS : '                    
                                                  WS-K130-STATUS                
                 PERFORM 900-CLOSE-FILES   THRU  900-EXIT.                      
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       651-READ-KD05.                                                           
      ******************************************************************        
                                                                                
           IF F124-AMT-RECIEPT IS NOT NUMERIC                                   
              MOVE ZEROS          TO   F124-AMT-RECIEPT                         
           END-IF.                                                              
                                                                                
           IF F124-CDE-PAYMENT-TYPE = 'INT'                                     
              MOVE  1                        TO WS-COUNT-HISTORY                
              PERFORM 600-WRITE-BP13K130     THRU 600-EXIT                      
           GO TO 651-EXIT.                                                      
                                                                                
           MOVE SPACES                       TO KD05-KEY-FLD.                   
           INITIALIZE                           KD05-KEY-FLD.                   
           MOVE F124-NUM-REGN                TO KD05-NUM-REGN.                  
           MOVE F124-NUM-NRIC                TO KD05-NUM-NRIC.                  
           MOVE 'N'                          TO WS-KD05-EOF                     
                                                WS-KD05-FND.                    
           READ BP13KD05 KEY IS KD05-KEY-FLD.                                   
                                                                                
           EVALUATE WS-KD05-STATUS                                              
                WHEN 00                                                         
                     PERFORM 652-READUPD-KD05   THRU 652-EXIT                   
                WHEN 10                                                         
                WHEN 23                                                         
                   DISPLAY 'RECORD NOT FOUND IN BP13KD05 ' KD05-KEY-FLD         
                WHEN OTHER                                                      
                      DISPLAY 'ERROR READING BP13KD05, STATUS '                 
                      DISPLAY WS-KD05-STATUS                                    
                      PERFORM 900-CLOSE-FILES THRU 900-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       651-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       652-READUPD-KD05.                                                        
      ******************************************************************        
                                                                                
           IF F124-AMT-RECIEPT IS NOT NUMERIC                                   
              MOVE ZERO            TO F124-AMT-RECIEPT                          
           END-IF.                                                              
           MOVE F124-AMT-RECIEPT   TO  WS-BAL-TXN.                              
           MOVE 'Y'                TO      WS-KD05-FND.                         
           ADD 1                   TO      WS-KD05-READ.                        
                                                                                
           PERFORM 653-UPD-KD05         THRU 653-EXIT.                          
                                                                                
       652-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       653-UPD-KD05.                                                            
      ******************************************************************        
                                                                                
           IF KD05-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZERO                    TO KD05-AMT-AHG-GRANT                
           END-IF.                                                              
           IF KD05-AMT-HSGNT IS NOT NUMERIC                                     
              MOVE ZERO                    TO KD05-AMT-HSGNT                    
           END-IF.                                                              
           IF KD05-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZERO                    TO KD05-AMT-SHG-GRANT                
           END-IF.                                                              
           IF KD05-AMT-SUG-GRANT IS NOT NUMERIC                                 
              MOVE ZERO                    TO KD05-AMT-SUG-GRANT                
           END-IF.                                                              
                                                                                
           EVALUATE F124-CDE-PAYMENT-TYPE                                       
             WHEN 'AHG'                                                         
                 PERFORM 65A-CHK-AHG-RECOVERY  THRU   65A-EXIT                  
             WHEN 'CHG'                                                         
                 PERFORM 65B-CHK-CHG-RECOVERY  THRU   65B-EXIT                  
             WHEN 'SHG'                                                         
                 PERFORM 65C-CHK-SHG-RECOVERY  THRU   65C-EXIT                  
             WHEN 'SUG'                                                         
                 PERFORM 65D-CHK-SUG-RECOVERY  THRU   65D-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       653-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       65A-CHK-AHG-RECOVERY.                                                    
      ******************************************************************        
                                                                                
           MOVE ZEROS                          TO  WS-AMT-TXN.                  
           IF KD05-AMT-AHG-GRANT > 0 AND                                        
              WS-BAL-TXN         > 0                                            
              COMPUTE  WS-AMT-TXN = KD05-AMT-AHG-GRANT-RECOVER                  
                                  + WS-BAL-TXN                                  
              IF (KD05-AMT-AHG-GRANT-RECOVER  =  KD05-AMT-AHG-GRANT)            
                                              OR                                
                  (  WS-AMT-TXN   > KD05-AMT-AHG-GRANT )                        
                  DISPLAY 'RECOVERY > AHG GRANT :' KD05-KEY-FLD                 
              ELSE                                                              
                 COMPUTE  KD05-AMT-AHG-GRANT-RECOVER =                          
                          KD05-AMT-AHG-GRANT-RECOVER +                          
                          WS-BAL-TXN                                            
                  MOVE  F124-DTE-TRANS   TO KD05-DTE-AHG-GRANT-RECOVER          
                  PERFORM  654-REWRITE-KD05   THRU  654-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              DISPLAY  ' NO AHG GRANT FOR THIS:  ' KD05-KEY-FLD                 
           END-IF.                                                              
                                                                                
       65A-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       65B-CHK-CHG-RECOVERY.                                                    
      ******************************************************************        
                                                                                
           MOVE ZEROS                          TO  WS-AMT-TXN.                  
           IF KD05-AMT-HSGNT     > 0 AND                                        
              WS-BAL-TXN         > 0                                            
              COMPUTE  WS-AMT-TXN = KD05-AMT-HSGNT-RECOVER                      
                                  + WS-BAL-TXN                                  
              IF (KD05-AMT-HSGNT-RECOVER      =  KD05-AMT-HSGNT    )            
                                              OR                                
                  (  WS-AMT-TXN   > KD05-AMT-HSGNT     )                        
                  DISPLAY 'RECOVERY > CHG GRANT :' KD05-KEY-FLD                 
              ELSE                                                              
                 COMPUTE  KD05-AMT-HSGNT-RECOVER     =                          
                          KD05-AMT-HSGNT-RECOVER     +                          
                          WS-BAL-TXN                                            
                  MOVE  F124-DTE-TRANS   TO KD05-DTE-HSGNT-RECOVER              
                  PERFORM  654-REWRITE-KD05   THRU  654-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              DISPLAY  ' NO CHG GRANT FOR THIS:  ' KD05-KEY-FLD                 
           END-IF.                                                              
                                                                                
       65B-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       65C-CHK-SHG-RECOVERY.                                                    
      ******************************************************************        
                                                                                
           MOVE ZEROS                          TO  WS-AMT-TXN.                  
           IF KD05-AMT-SHG-GRANT > 0 AND                                        
              WS-BAL-TXN         > 0                                            
              COMPUTE  WS-AMT-TXN = KD05-AMT-SHG-GRANT-RECOVER                  
                                  + WS-BAL-TXN                                  
              IF (KD05-AMT-SHG-GRANT-RECOVER  =  KD05-AMT-SHG-GRANT)            
                                              OR                                
                   (  WS-AMT-TXN   > KD05-AMT-SHG-GRANT )                       
                  DISPLAY 'RECOVERY > SHG GRANT :' KD05-KEY-FLD                 
              ELSE                                                              
                 COMPUTE  KD05-AMT-SHG-GRANT-RECOVER =                          
                          KD05-AMT-SHG-GRANT-RECOVER +                          
                          WS-BAL-TXN                                            
                  MOVE  F124-DTE-TRANS   TO KD05-DTE-SHG-GRANT-RECOVER          
                  PERFORM  654-REWRITE-KD05   THRU  654-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              DISPLAY  ' NO SHG GRANT FOR THIS:  ' KD05-KEY-FLD                 
           END-IF.                                                              
                                                                                
       65C-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       65D-CHK-SUG-RECOVERY.                                                    
      ******************************************************************        
                                                                                
           MOVE ZEROS                          TO  WS-AMT-TXN.                  
           IF KD05-AMT-SUG-GRANT > 0 AND                                        
              WS-BAL-TXN         > 0                                            
              COMPUTE  WS-AMT-TXN = KD05-AMT-SUG-GRANT-RECOVER                  
                                  + WS-BAL-TXN                                  
              IF (KD05-AMT-SUG-GRANT-RECOVER  =  KD05-AMT-SUG-GRANT)            
                                              OR                                
                   (  WS-AMT-TXN   > KD05-AMT-SUG-GRANT )                       
                  DISPLAY 'RECOVERY > SUG GRANT :' KD05-KEY-FLD                 
              ELSE                                                              
                 COMPUTE  KD05-AMT-SUG-GRANT-RECOVER =                          
                          KD05-AMT-SUG-GRANT-RECOVER +                          
                          WS-BAL-TXN                                            
                  MOVE  F124-DTE-TRANS   TO KD05-DTE-SUG-GRANT-RECOVER          
                  PERFORM  654-REWRITE-KD05   THRU  654-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              DISPLAY  ' NO SUG GRANT FOR THIS:  ' KD05-KEY-FLD                 
           END-IF.                                                              
                                                                                
       65D-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       654-REWRITE-KD05.                                                        
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO KD05-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD05-TME-UPDATE.                  
           MOVE 'P13C36S'                  TO KD05-NUM-USERID.                  
           REWRITE BP13KD05-REC.                                                
                                                                                
           IF WS-KD05-STATUS = 00                                               
              ADD             1              TO WS-KD05-WRITE                   
              MOVE 'Y'                       TO WS-KD05-TAG                     
              MOVE KD05-NUM-NRIC             TO WS-NUM-NRIC1                    
           ELSE                                                                 
              MOVE 'N'                       TO WS-KD05-TAG                     
              MOVE WS-KD05-STATUS    TO RETURN-CODE                             
              DISPLAY 'ERROR WRITING BP13KD05. STATUS : '                       
                                               WS-KD05-STATUS                   
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
       654-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       655-UPD-K130-AHG.                                                        
      ******************************************************************        
                                                                                
               IF WS-KD05-TAG = 'Y'                                             
                  MOVE  1  TO WS-COUNT-HISTORY                                  
                  PERFORM 600-WRITE-BP13K130     THRU  600-EXIT                 
               ELSE                                                             
                  IF F124-CDE-PAYMENT-TYPE NOT = 'INT'                          
                   DISPLAY 'RECORD NOT FOUND IN KD05  : ' F124-NUM-REGN         
                                                          F124-NUM-NRIC         
                   MOVE 'D'                       TO    WS-CDE-ERROR            
                   PERFORM  555-WRITE-BP13F52A    THRU  555-EXIT                
                   PERFORM  1000-ERROR-LIST       THRU  1000-EXIT               
                  END-IF                                                        
               END-IF.                                                          
                                                                                
       655-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       690-DELETE-K140.                                                         
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO K140-KEY-FLD.               
           INITIALIZE                               K140-KEY-FLD.               
                                                                                
           MOVE F124-KEY-FLD                     TO K140-KEY-FLD.               
                                                                                
           READ BP13K140.                                                       
                                                                                
           EVALUATE  WS-K140-STATUS                                             
                                                                                
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                              TO WS-K140-READ                
              PERFORM 691-WRITE-DEL-REC       THRU 691-EXIT                     
              PERFORM 692-DELETE-BP13K140     THRU 692-EXIT                     
              ADD 1                           TO WS-K140-NOTFND                 
           WHEN OTHER                                                           
                 DISPLAY 'BP13K140 - READ ERROR     : ' WS-K140-STATUS          
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       690-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       691-WRITE-DEL-REC.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO   BP13F14A-REC.                     
           INITIALIZE                         BP13F14A-REC.                     
           MOVE BP13K140-KIVTRAN          TO  BP13F14A-REC.                     
                                                                                
       691-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       692-DELETE-BP13K140.                                                     
      ******************************************************************        
                                                                                
           DELETE BP13K140.                                                     
                                                                                
           IF WS-K140-STATUS = 00                                               
              ADD 1                            TO WS-K140-DELETE                
              WRITE BP13F14A-REC                                                
              ADD 1                            TO WS-F14A-WRITE                 
           ELSE                                                                 
              DISPLAY 'BP13K140 - ERROR DELETING : ' WS-K140-STATUS             
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
                                                                                
       692-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       700-MOVE-VALUES-TO-K130.                                                 
      ******************************************************************        
                                                                                
           MOVE  SPACES                TO    BP13K130-CDHIST.                   
           INITIALIZE                        BP13K130-CDHIST.                   
           MOVE F124-CDE-TRANS-TYPE    TO    K130-TRANS-TYPE.                   
           MOVE F124-NUM-REGN          TO    K130-NUM-ORIG-REGN.                
           IF F124-NUM-NRIC NOT = SPACES AND LOW-VALUES                         
              MOVE F124-NUM-NRIC          TO    K130-NUM-NRIC                   
           ELSE                                                                 
              MOVE WS-NUM-NRIC1           TO    K130-NUM-NRIC                   
           END-IF.                                                              
           MOVE F124-DTE-POST          TO    K130-DTE-POST.                     
           MOVE F124-DTE-TRANS         TO    K130-DTE-TRANS.                    
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
           IF F124-AMT-RECIEPT IS NOT NUMERIC                                   
              MOVE ZEROS               TO    F124-AMT-RECIEPT                   
           END-IF.                                                              
           MOVE F124-AMT-RECIEPT       TO    K130-AMT-CASH.                     
           MOVE F124-CDE-CRDR          TO    K130-AMT-TYP.                      
           IF WS-K800-REC-FND = 'Y'                                             
              IF F124-NUM-SCH-ACCT = LOW-VALUES OR SPACES                       
                 MOVE  K800-NUM-SCH-ACC TO F124-NUM-SCH-ACCT                    
              ELSE                                                              
                 IF F124-NUM-SCH-ACCT NOT = K800-NUM-SCH-ACC                    
                    MOVE K800-NUM-SCH-ACC TO F124-NUM-SCH-ACCT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           MOVE F124-NUM-SCH-ACCT      TO    K130-NUM-SCH-ACCT.                 
           MOVE F124-NUM-RECPT-JRNO    TO    K130-NUM-RECPT-JRNO.               
                                                                                
           EVALUATE F124-CDE-PAYMENT-TYPE                                       
                                                                                
           WHEN 'ADV'                                                           
                 IF WS-STUDIO-TAG = '01'                                        
                   MOVE  '6154A'           TO    K130-NUM-RECEIPT-CODE          
                 ELSE                                                           
                   IF WS-STUDIO-TAG = '02'                                      
                      MOVE  '6088Z'        TO    K130-NUM-RECEIPT-CODE          
                   END-IF                                                       
                 END-IF                                                         
                   MOVE  'ADV'             TO    K130-PAYMENT-TYPE              
           WHEN 'AHG'                                                           
                 IF F124-NUM-REGN (1:1) = '5'                                   
                    MOVE  '1277G'          TO    K130-NUM-RECEIPT-CODE          
                 ELSE                                                           
                    MOVE  '6020H'          TO    K130-NUM-RECEIPT-CODE          
                 END-IF                                                         
                 MOVE  'AHG'               TO    K130-PAYMENT-TYPE              
           WHEN 'SHG'                                                           
                 MOVE  '6011E'             TO    K130-NUM-RECEIPT-CODE          
                 MOVE  'SHG'               TO    K130-PAYMENT-TYPE              
           WHEN 'SUG'                                                           
                 MOVE  '6015B'             TO    K130-NUM-RECEIPT-CODE          
                 MOVE  'SUG'               TO    K130-PAYMENT-TYPE              
           WHEN 'CHG'                                                           
                 MOVE  '1279J'           TO    K130-NUM-RECEIPT-CODE            
                 MOVE  'CHG'             TO    K130-PAYMENT-TYPE                
           WHEN 'INT'                                                           
                 MOVE  '0520C'           TO    K130-NUM-RECEIPT-CODE            
                 MOVE  'INT'             TO    K130-PAYMENT-TYPE                
           WHEN 'ADP'                                                           
                 MOVE  'ADP'             TO    K130-PAYMENT-TYPE                
           WHEN 'AMR'                                                           
                 MOVE  'AMR'             TO    K130-PAYMENT-TYPE                
           END-EVALUATE.                                                        
                                                                                
           MOVE SPACES                   TO  K130-NME-NAME                      
                                             K130-CDE-ERROR                     
                                             K130-CDE-VALID                     
                                             K130-CDE-CANCEL                    
                                             K130-CDE-BO                        
                                             K130-NUM-FLAT-TYPE-SA              
                                             K130-NUM-FLAT-TYPE-RSL             
                                             K130-NUM-CPF-APPL.                 
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-READ-BP13K130.                                                       
      ******************************************************************        
                                                                                
           MOVE F124-NUM-REGN          TO    K130-NUM-ORIG-REGN.                
           MOVE F124-DTE-POST          TO    K130-DTE-POST.                     
           ADD 1                       TO    WS-COUNT-HISTORY.                  
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
                                                                                
           READ BP13K130.                                                       
                                                                                
           IF WS-K130-STATUS = '00' OR '22'                                     
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF  WS-K130-STATUS = '23'                                         
                  MOVE 'Y'  TO WS-K130-WRITTEN                                  
                  PERFORM 600-WRITE-BP13K130       THRU  600-EXIT               
              ELSE                                                              
                  MOVE WS-K130-STATUS            TO RETURN-CODE                 
                  DISPLAY 'ERROR READING BP13K130. STATUS : '                   
                                                  WS-K130-STATUS                
                  DISPLAY 'BP13K130 KEYFIELD : ' K130-KEY-FLD                   
                  PERFORM 900-CLOSE-FILES          THRU 900-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-ERROR-LIST.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CNT > 46                                                  
               MOVE 1 TO WS-LINE-CNT                                            
               PERFORM 1100-ERROR-HEADING THRU 1100-EXIT                        
           ELSE                                                                 
               ADD  1 TO WS-LINE-CNT.                                           
           IF F124-AMT-RECIEPT   IS   NOT NUMERIC                               
              MOVE  ZEROS        TO   F124-AMT-RECIEPT                          
           END-IF.                                                              
           ADD F124-AMT-RECIEPT  TO   WS-AMT-KIV-TOTAL.                         
           ADD 1                 TO   CNT-KIV-CASES.                            
                                                                                
           EVALUATE WS-CDE-ERROR                                                
           WHEN 'S'                                                             
                 MOVE 'NOT A STUDIO OR AMR CASE'   TO 13S1-REMARK               
           WHEN 'D'                                                             
                 MOVE 'NOT IN BP13KD05 FILE'       TO 13S1-REMARK               
           WHEN 'R'                                                             
                 MOVE 'REC NOT IN K800 FILE'       TO 13S1-REMARK               
           END-EVALUATE.                                                        
                                                                                
           MOVE  CNT-KIV-CASES       TO 13S1-SNO.                               
           MOVE  F124-NUM-SCH-ACCT   TO 13S1-NUM-SCHACCNT.                      
           MOVE  F124-NUM-REGN       TO 13S1-ORIG-REG-NO.                       
           MOVE  F124-COUNT-HISTORY(1:1)  TO 13S1-CDE-HIST.                     
           MOVE  F124-COUNT-HISTORY(2:2)  TO 13S1-CNT-HIST.                     
           MOVE  F124-AMT-RECIEPT    TO 13S1-AMT.                               
           EVALUATE F124-CDE-PAYMENT-TYPE                                       
                                                                                
           WHEN 'ADV'                                                           
                 MOVE  'ADV'             TO    13S1-PAYMENT-TYPE                
           WHEN 'AHG'                                                           
                 MOVE  'AHG'             TO    13S1-PAYMENT-TYPE                
           WHEN 'CHG'                                                           
                 MOVE  'CHG'             TO    13S1-PAYMENT-TYPE                
           WHEN 'SHG'                                                           
                 MOVE  'SHG'             TO    13S1-PAYMENT-TYPE                
           WHEN 'SUG'                                                           
                 MOVE  'SUG'             TO    13S1-PAYMENT-TYPE                
           WHEN 'INT'                                                           
                 MOVE  'INT'             TO    13S1-PAYMENT-TYPE                
           WHEN 'ADP'                                                           
                 MOVE  'ADP'             TO    13S1-PAYMENT-TYPE                
           WHEN 'AMR'                                                           
                 MOVE  'AMR'             TO    13S1-PAYMENT-TYPE                
           END-EVALUATE.                                                        
                                                                                
                                                                                
           MOVE   F124-DTE-TRANS(1:4)TO    13S1-POST-YYYY.                      
           MOVE   F124-DTE-TRANS(5:2)TO    13S1-POST-MM.                        
           MOVE   F124-DTE-TRANS(7:2)TO    13S1-POST-DD.                        
                                                                                
           MOVE F124-NUM-RECPT-JRNO  TO 13S1-NUM-RECEIPT.                       
           WRITE PRINT-REC FROM L003-PR-DETAILS.                                
                                                                                
           MOVE SPACES               TO L003-PR-DETAILS.                        
           MOVE '/'                  TO 13S1-POST-FIL1                          
                                        13S1-POST-FIL2.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-ERROR-HEADING.                                                      
      *-------------------------------------------------------------            
                                                                                
            ADD 1               TO WS-PAGE-CNT.                                 
            MOVE WS-PAGE-CNT    TO 13S1-PAGENO.                                 
            STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                              
                   DELIMITED BY SIZE INTO 13S1-DATE.                            
            MOVE SPACES         TO PRINT-REC                                    
            WRITE PRINT-REC FROM L003-PR-HEAD-01 AFTER PAGE.                    
            WRITE PRINT-REC FROM L003-PR-HEAD-02 AFTER 2.                       
            WRITE PRINT-REC FROM L003-PR-HEAD-03 AFTER 2.                       
            INSPECT PRINT-REC REPLACING ALL SPACES BY LOW-VALUES.               
            INSPECT PRINT-REC REPLACING CHARACTERS BY '-'.                      
            INSPECT PRINT-REC REPLACING ALL LOW-VALUES BY SPACES.               
            WRITE PRINT-REC .                                                   
            MOVE SPACES TO PRINT-REC.                                           
            WRITE PRINT-REC AFTER 2.                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       900-CLOSE-FILES.                                                         
      ***************************************************************           
                                                                                
           DISPLAY '********* BP13C36S *********'.                              
           DISPLAY 'NO OF RECS PROCESSED (BP13F124) = ' WS-F124-READ.           
           DISPLAY 'NO OF RECS WRITTEN   (BP13K130) = ' WS-K130-WRITE.          
           DISPLAY 'NO OF RECS CREATED   (BP13K350) = ' WS-K350-WRITE.          
           DISPLAY 'NO OF RECS CREATED   (BP13K352) = ' WS-K352-WRITE.          
           DISPLAY 'NO OF RECS UPDATED   (BP13K350) = ' WS-K350-REWRITE.        
           DISPLAY 'NO OF RECS UPDATED   (BP13K352) = ' WS-K352-REWRITE.        
           DISPLAY 'NO OF RECS WRITTEN   (BP13F52A) = ' WS-F52A-WRITE.          
           DISPLAY 'NO OF RECS READ      (BP13KD05) = ' WS-KD05-READ.           
           DISPLAY 'NO OF RECS UPDATED   (BP13KD05) = ' WS-KD05-WRITE.          
           DISPLAY 'NO OF RECS READ      (BP13K140) = ' WS-K140-READ.           
           DISPLAY 'NO OF RECS DELETED   (BP13K140) = ' WS-K140-DELETE.         
           DISPLAY 'NO OF RECS WRITTEN   (BP13F14A) = ' WS-F14A-WRITE.          
                                                                                
           CLOSE BP13F124                                                       
                 BP13K130                                                       
                 BP13K350                                                       
                 BP13K352                                                       
                 BP13KD05                                                       
                 BP13K140                                                       
                 BP13K800                                                       
                 BP13K410                                                       
                 BP13F52A                                                       
                 BP13F14A                                                       
                 BP13L36S.                                                      
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K130. STATUS : ' WS-K130-STATUS        
              MOVE WS-K130-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-K350-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K350. STATUS : ' WS-K350-STATUS        
              MOVE WS-K350-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-K352-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K352. STATUS : ' WS-K352-STATUS        
              MOVE WS-K352-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13KD05. STATUS : ' WS-KD05-STATUS        
              MOVE WS-KD05-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-K140-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K140. STATUS : ' WS-K140-STATUS        
              MOVE WS-K140-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K800. STATUS : ' WS-K800-STATUS        
              MOVE WS-K800-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K410. STATUS : ' WS-K410-STATUS        
              MOVE WS-K410-STATUS       TO   RETURN-CODE.                       
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
