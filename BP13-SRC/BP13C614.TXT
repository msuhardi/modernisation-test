      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13C614.                                                  
      *AUTHOR.       SONNY A.                                                   
      *DATE-WRITTEN. 01/10/96.                                                  
                                                                                
      ***********************************************************               
      *  PURPOSE : TO DELETE THE CPF BALANCE ENQUIRY FILE FOR   *               
      *            CASES ALREADY TP OR ALREADY NOT FOUND IN     *               
      *            APPOINTMENT FILE (SA CASES).                 *               
      ***********************************************************               
      *  INPUT FILE :- BP13F680                                 *               
      *                BP13F300                                 *               
      *                BP13F400                                 *               
      *  OUTPUT FILE:- BP13K680                                 *               
      *                                                         *               
      ***********************************************************               
      * CHG-NO   BY  DATE   DESCRIPTION                         *               
      * -------- --- ------ -----------                         *               
      * BP130154 SIA 011096 NEW PGM                             *               
      * BP130154 RHB 041196 DELETE K680-REC IF FOUND IN K410,   *               
      *                     ELSE READ K454 USING ALT1-KEY       *               
      *                     IF NOT FOUND DELETE.                *               
      * BP130418 RHB 091698 Y2K;FILE LENGTH                     *               
      * BP130839 JMS 090500 REVAMP OF APPT MODULE.              *               
      * BP130929 CLT 301200 TO REPLACE BP13F680 WITH BP13F680.  *               
      * BP133429 RB12 170908 ADD CHECK FOR BOOKING APPMT        *               
      * BP133574 LMS1 270309 TO KEEP DELETE RECORDS.            *               
      * BP133674 LMS1 040809 ADD CHECK FOR SNP APPMT.           *               
      * BP133707 RB12 301009 ADD CHECK FOR NUM GRANT CREDIT TAG *               
      * BP136460 KSJ3 020317 EXPAND BP13F680/K680 (1500-1600)   *               
      *                      CHANGE AB02F875 TO BP13F680        *               
      ***********************************************************               
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F680 ASSIGN TO BP13F680.                                  
                                                                                
           SELECT BP13F68A ASSIGN TO BP13F68A.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K680 ASSIGN TO BP13K680                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K680-NUM-REGN                              
                  FILE STATUS     IS WS-K680-STATUS.                            
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS WS-K410-STATUS.                            
                                                                                
           SELECT BP13K454 ASSIGN TO BP13K454                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K454-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K454-ALT1-KEY                         
                  FILE STATUS     IS WS-K454-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS KD00-KEY-FLD                               
                  FILE STATUS     IS WS-KD00-STATUS.                            
                                                                                
                                                                                
      *----------------*                                                        
       DATA DIVISION.                                                           
      *----------------*                                                        
       FILE SECTION.                                                            
                                                                                
       FD  BP13F680   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 1600 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F680.                                                           
                                                                                
       FD  BP13F68A   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 1600 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       01  BP13F68A-REC                  PIC X(1600).                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K680                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13K680.                                                           
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K410.                                                           
                                                                                
       FD  BP13K454                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K454.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-SWITCHES.                                                         
           05  WS-F680-EOF               PIC X    VALUE 'N'.                    
               88 F680-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
           05  WS-K800-FND               PIC X    VALUE SPACES.                 
           05  WS-KD00-FND               PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F875          PIC 9(6) VALUE ZEROS.                  
           05  WS-K680-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K680-NOTFD             PIC 9(6) VALUE ZEROS.                  
           05  WS-K680-DELETED           PIC 9(6) VALUE ZEROS.                  
           05  WS-F68A-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-K410-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K454-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-KD00-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-CTR-CURDTE             PIC 9(6) VALUE ZEROS.                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K680-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K410-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K454-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K800-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-KD00-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC                 PIC 9(02).                             
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY              PIC 9(02).                             
              10 WS-CURR-MM              PIC 9(02).                             
              10 WS-CURR-DD              PIC 9(02).                             
                                                                                
       01  WS-DATE                       PIC X(8).                              
       01  WS-DATE9 REDEFINES WS-DATE    PIC 9(8).                              
       01  WS-INTEGER-CUR-DATE2          PIC S9(9) VALUE ZEROES.                
       01  WS-DATE2-N                    PIC  9(8) VALUE ZEROES.                
       01  WS-DATE2 REDEFINES WS-DATE2-N PIC  X(8).                             
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *-------------------*                                                     
       000-MAIN-LOGIC.                                                          
      *-------------------*                                                     
            PERFORM 100-OPEN-FILES       THRU 100-EXIT.                         
            PERFORM 150-READ-SY02F001    THRU 150-EXIT.                         
            PERFORM 200-READ-BP13F680    THRU 200-EXIT.                         
            PERFORM 300-PROCESS-BP13K680 THRU 300-EXIT                          
                    UNTIL F680-EOF.                                             
            PERFORM 999-CLOSE-FILES      THRU 999-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BP13F680                                                 
                       BP13K410                                                 
                       BP13K454                                                 
                       BP13K800                                                 
                       BP13KD00                                                 
                       SY02F001                                                 
                I-O    BP13K680                                                 
                OUTPUT BP13F68A.                                                
                                                                                
           IF WS-K680-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K680-STATUS ' WS-K680-STATUS              
              MOVE WS-K680-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K410-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K410-STATUS ' WS-K410-STATUS              
              MOVE WS-K410-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K454-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K454-STATUS ' WS-K454-STATUS              
              MOVE WS-K454-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-KD00-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, KD00-STATUS ' WS-KD00-STATUS              
              MOVE WS-KD00-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-READ-SY02F001.                                                       
      *-------------------------------------------------------------            
           READ SY02F001                                                        
                AT END                                                          
                   GO TO 150-EXIT.                                              
                                                                                
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           COMPUTE WS-INTEGER-CUR-DATE2 =                                       
              FUNCTION INTEGER-OF-DATE(WS-DATE9) - 2.                           
                                                                                
           COMPUTE WS-DATE2-N =                                                 
              FUNCTION DATE-OF-INTEGER(WS-INTEGER-CUR-DATE2).                   
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F680.                                                       
      *-----------------*                                                       
           READ BP13F680 AT END                                                 
           MOVE 'Y'                      TO WS-F680-EOF                         
           GO TO 200-EXIT.                                                      
           ADD 1                         TO WS-READ-CNT-F875.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       300-PROCESS-BP13K680.                                                    
      *---------------------*                                                   
      * DELETE CASE FROM BP13K680 IF :                                          
      * (A) CASE ALREADY TP                                                     
      * (B) CASE NOT FOUND IN BP13K454 ANYMORE                                  
                                                                                
           MOVE 'Y'           TO WS-FOUND.                                      
                                                                                
           MOVE F680-NUM-REGN TO K410-NUM-REGN.                                 
           READ BP13K410         NOT INVALID KEY                                
                                 ADD 1 TO WS-K410-READ.                         
                                                                                
           IF WS-K410-STATUS = 00                                               
              DISPLAY 'TP K410 FOUND CASE'                                      
              PERFORM 400-BP13K680-DELETE  THRU 400-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00                                           
              MOVE 'SA'          TO K454-CDE-APPT-TYPE                          
              MOVE F680-NUM-REGN TO K454-NUM-REGN                               
              READ BP13K454      KEY IS K454-ALT1-KEY                           
                                 NOT INVALID KEY                                
                                 ADD 1 TO WS-K454-READ                          
              END-READ                                                          
              IF WS-K454-STATUS     = 00                                        
                 DISPLAY 'SA CASE - APPT EXISTS'                                
                 CONTINUE                                                       
              ELSE                                                              
                 IF WS-K454-STATUS NOT = 00                                     
                    MOVE 'TP'          TO K454-CDE-APPT-TYPE                    
                    MOVE F680-NUM-REGN TO K454-NUM-REGN                         
                    READ BP13K454 KEY IS K454-ALT1-KEY                          
                                       NOT INVALID KEY                          
                                       ADD 1 TO WS-K454-READ                    
                    END-READ                                                    
                    IF WS-K454-STATUS = 00                                      
                       DISPLAY 'TP CASE - APPT EXISTS'                          
                       CONTINUE                                                 
                    ELSE                                                        
                       IF WS-K454-STATUS NOT = 00                               
                          MOVE 'SP'          TO K454-CDE-APPT-TYPE              
                          MOVE F680-NUM-REGN TO K454-NUM-REGN                   
                          READ BP13K454 KEY IS K454-ALT1-KEY                    
                                             NOT INVALID KEY                    
                                             ADD 1 TO WS-K454-READ              
                          END-READ                                              
                          IF WS-K454-STATUS = 00                                
                             DISPLAY 'SP CASE - APPT EXISTS'                    
                             CONTINUE                                           
                          ELSE                                                  
                             IF WS-K454-STATUS NOT = 00                         
                                MOVE 'SR'         TO K454-CDE-APPT-TYPE         
                                MOVE F680-NUM-REGN TO K454-NUM-REGN             
                                READ BP13K454 KEY IS K454-ALT1-KEY              
                                                   NOT INVALID KEY              
                                                   ADD 1 TO WS-K454-READ        
                                END-READ                                        
                                IF WS-K454-STATUS NOT = 00                      
                                   PERFORM 350-READ-K800 THRU 350-EXIT          
                                   PERFORM 360-READ-KD00 THRU 360-EXIT          
                                   IF (WS-KD00-FND = 'Y') AND                   
                                      (KD00-DTE-SNP-APPT < WS-DATE2)            
                                      PERFORM 400-BP13K680-DELETE               
                                         THRU 400-EXIT                          
                                   ELSE                                         
                                      IF (WS-KD00-FND NOT = 'Y') AND            
                                         (WS-K800-FND = 'Y') AND                
                                         (K800-DTE-BK-APPT < WS-DATE2)          
                                         PERFORM 400-BP13K680-DELETE            
                                            THRU 400-EXIT                       
                                      ELSE                                      
                                         IF K800-NUM-GRANT-CREDIT-TAG           
                                            = 'Y'                               
                                            PERFORM 400-BP13K680-DELETE         
                                               THRU 400-EXIT                    
                                         END-IF                                 
                                      END-IF                                    
                                   END-IF                                       
                                ELSE                                            
                                   DISPLAY 'SR CASE - APPT EXISTS'              
                                   CONTINUE                                     
                                END-IF                                          
                             END-IF                                             
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F680 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       350-READ-K800.                                                           
      *-------------------*                                                     
                                                                                
           MOVE SPACE         TO WS-K800-FND.                                   
           MOVE F680-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800 KEY  IS K800-NUM-REGN.                                 
                                                                                
           IF WS-K800-STATUS = 00                                               
              MOVE 'Y'        TO WS-K800-FND                                    
              ADD 1           TO WS-K800-READ                                   
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 MOVE 'N'     TO WS-K800-FND                                    
              ELSE                                                              
                 DISPLAY 'BP13K800 ERROR - ' WS-K800-STATUS                     
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       360-READ-KD00.                                                           
      *-------------------*                                                     
                                                                                
           MOVE SPACE         TO WS-KD00-FND.                                   
           MOVE F680-NUM-REGN TO KD00-KEY-FLD.                                  
                                                                                
           READ BP13KD00 KEY  IS KD00-KEY-FLD.                                  
                                                                                
           IF WS-KD00-STATUS = 00                                               
              MOVE 'Y'        TO WS-KD00-FND                                    
              ADD 1           TO WS-KD00-READ                                   
           ELSE                                                                 
              IF WS-KD00-STATUS = 23                                            
                 MOVE 'N'     TO WS-KD00-FND                                    
              ELSE                                                              
                 DISPLAY 'BP13KD00 ERROR - ' WS-KD00-STATUS                     
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       400-BP13K680-DELETE.                                                     
      *-------------------*                                                     
           MOVE F680-NUM-REGN TO K680-NUM-REGN.                                 
           DISPLAY 'REGN DELETED = ' F680-NUM-REGN.                             
                                                                                
           READ BP13K680                                                        
                INVALID KEY                                                     
                DISPLAY 'RECORD NOT FOUND IN BP13K680 : ' F680-NUM-REGN         
                DISPLAY '             BP13K680 STATUS : ' WS-K680-STATUS        
                ADD 1 TO WS-K680-NOTFD                                          
                GO TO 400-EXIT                                                  
           END-READ.                                                            
           ADD  1  TO WS-K680-READ.                                             
                                                                                
           IF WS-K410-STATUS = 00                                               
              MOVE K410-DTE-KEY-ISSUE TO K680-DTE-TP                            
              MOVE BP13K680-REC TO BP13F68A-REC                                 
              WRITE BP13F68A-REC                                                
              ADD 1 TO WS-F68A-WRITE                                            
           ELSE                                                                 
              IF K800-DTE-SALE-AGMT NOT = ZEROES AND LOW-VALUES AND             
                                          SPACES                                
                 MOVE K800-DTE-SALE-AGMT TO K680-DTE-SOCA                       
                 MOVE BP13K680-REC TO BP13F68A-REC                              
                 WRITE BP13F68A-REC                                             
                 ADD 1 TO WS-F68A-WRITE                                         
              ELSE                                                              
                 IF KD00-DTE-SNP-APPT NOT = ZEROES AND LOW-VALUES AND           
                                            SPACES                              
                    MOVE KD00-DTE-SNP-APPT TO K680-DTE-SOCA                     
                    MOVE BP13K680-REC TO BP13F68A-REC                           
                    WRITE BP13F68A-REC                                          
                    ADD 1 TO WS-F68A-WRITE                                      
                 ELSE                                                           
                    IF (K800-DTE-SO-APPT = ZEROES OR LOW-VALUES OR              
                                           SPACES) AND                          
                       (K800-DTE-AO-APPT = ZEROES OR LOW-VALUES OR              
                                           SPACES) AND                          
                       (K800-NUM-SCH-ACC NOT = ZEROES AND LOW-VALUES AND        
                                           SPACES)                              
                       MOVE K800-NUM-SCH-ACC    TO K680-NUM-HDB-REF             
                       MOVE K800-NUM-ALLOC-DATE TO K680-DTE-ACCEPT              
                       MOVE BP13K680-REC TO BP13F68A-REC                        
                       WRITE BP13F68A-REC                                       
                       ADD 1 TO WS-F68A-WRITE                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           DELETE BP13K680                                                      
                INVALID KEY                                                     
                DISPLAY '  ERROR IN DELETING BP13K680 : ' F680-NUM-REGN         
                DISPLAY '             BP13K680 STATUS : ' WS-K680-STATUS        
                GO TO 400-EXIT.                                                 
                                                                                
           ADD  1  TO WS-K680-DELETED.                                          
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       999-CLOSE-FILES.                                                         
      *----------------*                                                        
           CLOSE BP13F680                                                       
                 BP13F68A                                                       
                 SY02F001                                                       
                 BP13K410                                                       
                 BP13K454                                                       
                 BP13K680                                                       
                 BP13KD00                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K680-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K680, STATUS ' WS-K680-STATUS          
              MOVE WS-K680-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K410, STATUS ' WS-K410-STATUS          
              MOVE WS-K410-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K454, STATUS ' WS-K454-STATUS          
              MOVE WS-K454-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KD00, STATUS ' WS-KD00-STATUS          
              MOVE WS-KD00-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY '*------------------ BP13C614 -------------------'.          
           DISPLAY 'DATE RUN :        ' WS-CURR-DATE                            
           DISPLAY 'NO OF REC READ        BP13F680   = '                        
                    WS-READ-CNT-F875.                                           
           DISPLAY 'NO OF REC READ        BP13K410   = '                        
                    WS-K410-READ                                                
           DISPLAY 'NO OF REC READ        BP13K454   = '                        
                    WS-K454-READ                                                
           DISPLAY 'NO OF REC READ        BP13K800   = '                        
                    WS-K800-READ                                                
           DISPLAY 'NO OF REC READ        BP13KD00   = '                        
                    WS-KD00-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP13F68A   = '                        
                    WS-F68A-WRITE                                               
           DISPLAY 'NO OF REC DELETED     BP13K680   = '                        
                    WS-K680-DELETED                                             
           DISPLAY 'NO OF REC NOTFOUND    BP13K680   = '                        
                    WS-K680-NOTFD.                                              
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
