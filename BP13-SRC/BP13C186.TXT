      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C186.                                                 
       AUTHOR.        TEREZA SALONGA.                                           
      *-----------------------------------------------------------*             
                                                                                
      *===========================================================*             
      *        SOC - S Y S T E M  O F  C O M M I T M E N T        *             
      *===========================================================*             
      *  OBJECTIVES :                                             *             
      *                                                           *             
      *    1.  TO UPDATE BP13KB00 EXCEPTION TAGS IF REGISTRATION  *             
      *        NUMBER MATCHED BETWEEN BP13KB00 AND P13F730A       *             
      *    2.  TO UPDATE KB00-NUM-PRINT WHEN REGISTRATION NUMBER  *             
      *        MATCHED BETWEEN BP13KB00 AND BP13F730              *             
      *                                                           *             
      *    INPUT FILES : P13F730A                                 *             
      *                  BP13F730                                 *             
      *                                                           *             
      *    INPUT-OUTPUT: BP13KB00 - BE/BTO REQUEST FILE           *             
      *                                                           *             
      *    SUBROUTINES : NONE                                     *             
      *                                                           *             
      *---------------------------------------------------------- *             
      *===========================================================*             
      *  REVISION HISTORY :                                       *             
      *  CHG-REQ#  BY    DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP132886  MTSR1 23/07/06    NEW PROGRAM                  *             
      *  BP133050  CKK7  24/04/07    INCLUDE CLEARING OF LOCK ID  *             
      ************************************************************              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13F730A  ASSIGN   TO P13F730A.                               
           SELECT BP13F730  ASSIGN   TO BP13F730.                               
           SELECT BP13FB00  ASSIGN   TO BP13FB00.                               
                                                                                
           SELECT BP13KB00 ASSIGN    TO BP13KB00                                
                ACCESS MODE          IS RANDOM                                  
                ORGANIZATION         IS INDEXED                                 
                RECORD KEY           IS KB00-KEY-FLD                            
                FILE STATUS          IS WS-KB00-STATUS.                         
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  P13F730A                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13F730A-REC.                                                        
           05 F730A-NUM-10YR            PIC X(01).                              
           05 F730A-NUM-PORT            PIC X(01).                              
           05 F730A-NUM-PPO             PIC X(01).                              
           05 F730A-NUM-MCP             PIC X(01).                              
           05 F730A-NUM-TCP             PIC X(01).                              
           05 F730A-NUM-DUAL            PIC X(01).                              
           05 F730A-NUM-CODE33          PIC X(01).                              
           05 F730A-NUM-CODE71          PIC X(01).                              
           05 F730A-NUM-CODE77          PIC X(01).                              
           05 F730A-NUM-TRF             PIC X(01).                              
           05 F730A-NUM-SLS             PIC X(01).                              
           05 F730A-NUM-MAM             PIC X(01).                              
           05 F730A-NUM-PC              PIC X(01).                              
           05 F730A-NUM-BLANK           PIC X(01).                              
           05 F730A-NUM-HDB01           PIC X(01).                              
           05 F730A-NUM-SPOUSE          PIC X(01).                              
           05 F730A-NUM-DEBAR           PIC X(01).                              
           05 F730A-NUM-INCOME          PIC X(01).                              
           05 FILLER                    PIC X(104).                             
           05 F730A-NUM-REGN            PIC X(08).                              
           05 FILLER                    PIC X(370).                             
                                                                                
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F730.                                                           
                                                                                
       FD  BP13FB00                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13FB00.                                                           
                                                                                
                                                                                
       FD   BP13KB00                                                            
            RECORD     CONTAINS 200 CHARACTERS.                                 
       COPY BP13KB00.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-KB00-STATUS             PIC 9(2)    VALUE  0.                 
           05  WS-FB00-EOF-SW             PIC X(1)    VALUE 'N'.                
           05  WS-F730-EOF-SW             PIC X(1)    VALUE 'N'.                
           05  WS-F730A-EOF-SW            PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F730A-READ-CTR          PIC 9(5)    VALUE 0.                  
           05  WS-F730-READ-CTR           PIC 9(5)    VALUE 0.                  
           05  WS-FB00-READ-CTR           PIC 9(5)    VALUE 0.                  
           05  WS-KB00-READ-CTR           PIC 9(5)    VALUE 0.                  
           05  WS-KB00-NOTF-CTR           PIC 9(5)    VALUE 0.                  
           05  WS-KB00-UPDT-CTR           PIC 9(5)    VALUE 0.                  
           05  WS-KB00-UPDT2-CTR          PIC 9(5)    VALUE 0.                  
           05  WS-KB00-UPDT3-CTR          PIC 9(5)    VALUE 0.                  
           05  WS-KB00-F730A-CTR          PIC 9(5)    VALUE 0.                  
           05  WS-KB00-F730A-NOF          PIC 9(5)    VALUE 0.                  
                                                                                
       01  WS-FOUND-IND                   PIC X(1).                             
           88  WS-FOUND-Y                             VALUE 'Y'.                
           88  WS-FOUND-N                             VALUE 'N'.                
                                                                                
       01  WS-SYSTEM-DATE                 PIC X(8)    VALUE SPACES.             
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *-------------*                                                           
       0000-CONTROL.                                                            
      *-------------*                                                           
           PERFORM  1000-OPEN-FILES         THRU 1000-EXIT.                     
           PERFORM  2000-MAIN-ROUTINE       THRU 2000-EXIT                      
              UNTIL WS-F730A-EOF-SW = 'Y'.                                      
           PERFORM  2500-INITIALIZATION     THRU 2500-EXIT                      
           PERFORM  2600-PROCESS-BP13F730   THRU 2600-EXIT                      
              UNTIL WS-F730-EOF-SW = 'Y'.                                       
           PERFORM  3200-READ-FB00          THRU 3200-EXIT.                     
           PERFORM  2700-PROCESS-BP13FB00   THRU 2700-EXIT                      
              UNTIL WS-FB00-EOF-SW = 'Y'.                                       
           PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
           OPEN INPUT  P13F730A                                                 
                       BP13F730                                                 
                       BP13FB00                                                 
                I-O    BP13KB00.                                                
                                                                                
           IF WS-KB00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KB00 OPEN ERROR, STATUS IS ' WS-KB00-STATUS          
              MOVE     WS-KB00-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-SYSTEM-DATE.                 
           PERFORM 3000-READ-F730A         THRU 3000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       2000-MAIN-ROUTINE.                                                       
      *------------------*                                                      
           SET WS-FOUND-N               TO   TRUE                               
           MOVE F730A-NUM-REGN          TO   KB00-NUM-REGN                      
           PERFORM 3500-READ-KB00       THRU 3500-EXIT.                         
                                                                                
           IF WS-FOUND-Y                                                        
              PERFORM 4000-UPDATE-KB00  THRU 4000-EXIT                          
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           PERFORM  3000-READ-F730A     THRU 3000-EXIT.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       2500-INITIALIZATION.                                                     
      *--------------------*                                                    
           MOVE WS-KB00-READ-CTR            TO   WS-KB00-F730A-CTR              
           MOVE WS-KB00-NOTF-CTR            TO   WS-KB00-F730A-NOF              
           INITIALIZE                            WS-KB00-READ-CTR               
                                                 WS-KB00-NOTF-CTR.              
                                                                                
           PERFORM  3100-READ-F730        THRU   3100-EXIT.                     
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       2600-PROCESS-BP13F730.                                                   
      *----------------------*                                                  
           SET WS-FOUND-N                 TO   TRUE                             
           MOVE F730-NUM-REGN             TO   KB00-NUM-REGN                    
           PERFORM 3500-READ-KB00         THRU 3500-EXIT.                       
                                                                                
           IF WS-FOUND-Y                                                        
              PERFORM 4500-UPDATE-KB00-2  THRU 4500-EXIT                        
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           PERFORM  3100-READ-F730        THRU 3100-EXIT.                       
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       2700-PROCESS-BP13FB00.                                                   
      *----------------------*                                                  
           SET WS-FOUND-N                 TO   TRUE                             
           MOVE FB00-NUM-REGN             TO   KB00-NUM-REGN                    
           PERFORM 3500-READ-KB00         THRU 3500-EXIT.                       
                                                                                
           IF WS-FOUND-Y                                                        
              PERFORM 4600-UPDATE-KB00-3  THRU 4600-EXIT                        
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           PERFORM  3200-READ-FB00        THRU 3200-EXIT.                       
                                                                                
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       3000-READ-F730A.                                                         
      *----------------*                                                        
           READ P13F730A                                                        
              AT END MOVE 'Y' TO WS-F730A-EOF-SW                                
              GO TO  3000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F730A-READ-CTR.                                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       3100-READ-F730.                                                          
      *---------------*                                                         
           READ BP13F730                                                        
              AT END MOVE 'Y' TO WS-F730-EOF-SW                                 
              GO TO  3100-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F730-READ-CTR.                                           
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       3200-READ-FB00.                                                          
      *---------------*                                                         
           READ BP13FB00                                                        
              AT END MOVE 'Y' TO WS-FB00-EOF-SW                                 
              GO TO  3200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-FB00-READ-CTR.                                           
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       3500-READ-KB00.                                                          
      *---------------*                                                         
           READ BP13KB00.                                                       
                                                                                
           EVALUATE WS-KB00-STATUS                                              
           WHEN 00                                                              
              SET WS-FOUND-Y  TO TRUE                                           
              ADD 1           TO WS-KB00-READ-CTR                               
           WHEN 23                                                              
              SET WS-FOUND-N  TO TRUE                                           
              ADD 1           TO WS-KB00-NOTF-CTR                               
           WHEN OTHER                                                           
              DISPLAY 'BP13KB00 READ ERROR, STATUS IS ' WS-KB00-STATUS          
              MOVE  WS-KB00-STATUS      TO   RETURN-CODE                        
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       4000-UPDATE-KB00.                                                        
      *-----------------*                                                       
           MOVE P13F730A-REC(1:18)    TO    KB00-EXCEPTION-TAG                  
           MOVE 'Y'                   TO    KB00-NUM-EXCEPTION-STATUS           
           MOVE 'BP13C18A'            TO    KB00-NUM-USER-ID                    
           MOVE WS-SYSTEM-DATE        TO    KB00-DTE-UPDATE.                    
                                                                                
           REWRITE BP13KB00-REC.                                                
                                                                                
           IF WS-KB00-STATUS NOT = 00                                           
              DISPLAY 'BP13KB00 UPDATE ERROR, STATUS IS ' WS-KB00-STATUS        
              DISPLAY 'KB00-NUM-REGN = '    KB00-NUM-REGN                       
              MOVE WS-KB00-STATUS      TO   RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           ELSE                                                                 
              ADD 1                    TO   WS-KB00-UPDT-CTR                    
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       4500-UPDATE-KB00-2.                                                      
      *-------------------*                                                     
           MOVE 'Y'                    TO   KB00-NUM-PRINT                      
           MOVE 'BP13C18B'             TO   KB00-NUM-USER-ID                    
           MOVE WS-SYSTEM-DATE         TO   KB00-DTE-UPDATE.                    
                                                                                
           REWRITE BP13KB00-REC.                                                
                                                                                
           IF WS-KB00-STATUS NOT = 00                                           
              DISPLAY 'BP13KB00 UPDATE ERROR, STATUS IS ' WS-KB00-STATUS        
              DISPLAY 'KB00-NUM-REGN = ' KB00-NUM-REGN                          
              MOVE WS-KB00-STATUS      TO   RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           ELSE                                                                 
              ADD 1                    TO   WS-KB00-UPDT2-CTR                   
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4600-UPDATE-KB00-3.                                                      
      *-------------------*                                                     
           MOVE SPACES                 TO   KB00-NUM-LOCK-ID.                   
                                                                                
           REWRITE BP13KB00-REC.                                                
                                                                                
           IF WS-KB00-STATUS NOT = 00                                           
              DISPLAY 'BP13KB00 LOCK UPD ERR, STATUS IS ' WS-KB00-STATUS        
              DISPLAY 'KB00-NUM-REGN = ' KB00-NUM-REGN                          
           ELSE                                                                 
              ADD 1                    TO   WS-KB00-UPDT3-CTR                   
           END-IF.                                                              
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           CLOSE P13F730A                                                       
                 BP13F730                                                       
                 BP13FB00                                                       
                 BP13KB00.                                                      
                                                                                
           IF WS-KB00-STATUS NOT = 00                                           
              DISPLAY 'BP13KB00 CLOSE ERROR, STATUS IS ' WS-KB00-STATUS         
              MOVE WS-KB00-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           DISPLAY '                           '                                
           DISPLAY 'PROGRAM         : BP13C186 '                                
           DISPLAY 'PROCESSING DATE : ' WS-SYSTEM-DATE                          
           DISPLAY '*******************************************'                
           DISPLAY 'CONTROL TOTALS FOR F730A RECORD PROCESSING'                 
           DISPLAY '*******************************************'                
           DISPLAY 'NO. OF RECS READ IN F730A     :' WS-F730A-READ-CTR          
           DISPLAY 'NO. OF RECS MATCHED           :' WS-KB00-F730A-CTR          
           DISPLAY 'NO. OF RECS NOT FOUND IN KB00 :' WS-KB00-F730A-NOF          
           DISPLAY 'NO. OF RECS UPD IN KB00       :' WS-KB00-UPDT-CTR           
           DISPLAY '*******************************************'                
           DISPLAY 'CONTROL TOTALS FOR F730 RECORD PROCESSING'                  
           DISPLAY '*******************************************'                
           DISPLAY 'NO. OF RECS READ IN F730      :' WS-F730-READ-CTR           
           DISPLAY 'NO. OF RECS MATCHED           :' WS-KB00-READ-CTR           
           DISPLAY 'NO. OF RECS NOT FOUND IN KB00 :' WS-KB00-NOTF-CTR           
           DISPLAY 'NO. OF RECS UPD IN KB00       :' WS-KB00-UPDT2-CTR          
           DISPLAY '*******************************************'                
           DISPLAY 'CONTROL TOTALS FOR FB00 RECORD PROCESSING'                  
           DISPLAY '*******************************************'                
           DISPLAY 'NO. OF RECS READ IN FB00      :' WS-FB00-READ-CTR           
           DISPLAY 'NO. OF RECS UPD IN KB00       :' WS-KB00-UPDT3-CTR          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
