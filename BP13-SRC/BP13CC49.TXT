      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CC49.                                                 
       AUTHOR.        ARIEL HERNANDEZ.                                          
       DATE-WRITTEN.  30/01/2007.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  TO CREATE INTEREST REBATE FOR MORTGAGE LOAN   *        
      *                  - NEW LOAN                                    *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13FD30                                  *        
      *                  2.  SY02F001                                  *        
      *                  3.  BP13KD38                                  *        
      *                  4.  BP13KD00                                  *        
      *                                                                *        
      *   OUTPUT FILES:  1.  BP13F125                                  *        
      *                  2.  BP13LC49                                  *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132838  BA3  30/01/2007 NEW PROGRAM                        *        
      *   BP133052  BA3  07/06/2007 CHANGE ROUNDING OF REBATE AMOUNT   *        
      *   BP133115  BA3  16/10/2007 ADD NUM-LOAN-SCH                   *        
      *   BP133173  BA3  26/10/2007 UPDATE KD00-AMT-ARR-INTR           *        
      *   BP133091  RB12 16/11/2007 EXPAND KD00 LENGTH FR 1200 TO 1600 *        
      *   BP133314  JB8  30/06/2008 PRINT REPORT AFTER UPDATE          *        
      *   BP134089  JB8  31/05/2011 TO CATER FOR RIGHT PP SCH          *        
      *   BP134709  IMC1 31/10/2012 FIXED DIVIDE-BY-ZERO ERROR         *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD30  ASSIGN        TO BP13FD30.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13KD38  ASSIGN        TO BP13KD38                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD38-KEY-FLD                       
                            FILE STATUS   IS WS-KD38-STATUS.                    
                                                                                
           SELECT BP13KD00  ASSIGN        TO BP13KD00                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD00-KEY-FLD                       
                            FILE STATUS   IS WS-KD00-STATUS.                    
                                                                                
           SELECT BP13F125  ASSIGN        TO BP13F125.                          
           SELECT BP13LC49  ASSIGN        TO BP13LC49.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD30                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13FD30.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13KD38                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
                                                                                
       COPY BP13KD38.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
                                                                                
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13F125                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13F125.                                                           
                                                                                
       FD  BP13LC49                                                             
           BLOCK  CONTAINS    0 RECORDS                                         
           RECORD CONTAINS  132 CHARACTERS                                      
           LABEL  RECORD   IS   STANDARD                                        
           RECORDING MODE  IS   F.                                              
                                                                                
       01  BP13LC49-REC      PIC X(132).                                        
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-AMT-REBATE                PIC 9(5)V99 VALUE ZEROES.               
       01  WS-AMT-REBATE-6DEC           PIC 9(5)V9(6)     VALUE ZEROES.         
       01  WS-INT-PER-DAY               PIC 9(5)V9(6)     VALUE ZEROES.         
       01  WS-DAY                       PIC 9(03)   VALUE ZEROES.               
       01  WS-VALID-REC                 PIC X(01)   VALUE SPACES.               
                                                                                
       01  WS-KD38-FD                   PIC X(01)   VALUE SPACES.               
       01  WS-KD00-FD                   PIC X(01)   VALUE SPACES.               
       01  WS-EOF-FD30                  PIC X(01)   VALUE SPACES.               
       01  CNT-FD30-READ                PIC 9(05)   VALUE ZEROES.               
       01  CNT-KD38-UPDATE              PIC 9(05)   VALUE ZEROES.               
       01  WS-KD38-STATUS               PIC 9(2)    VALUE ZEROES.               
       01  WS-KD00-STATUS               PIC 9(2)    VALUE ZEROES.               
       01  CNT-KD38-READ                PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD00-READ                PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD38-NOTFOUND            PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD00-NOTFOUND            PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD00-UPDATE              PIC 9(8)    VALUE ZEROES.               
       01  CNT-F125-WRITE               PIC 9(8)    VALUE ZEROES.               
       01  WS-AMT-LOAN-BAL-REBATE       PIC S9(5)V99      VALUE ZEROES.         
       01  WS-PAGE                      PIC 9(4)    VALUE ZEROES.               
       01  WS-SNO                       PIC 9(4)    VALUE ZEROES.               
       01  WS-LINE                      PIC 9(2)    VALUE 60.                   
       01  WS-CTR                       PIC 9(2)    VALUE ZEROES.               
       01  WS-TMP-INTRATE               PIC 99V99         VALUE ZEROES.         
       01  WS-INT-FORM REDEFINES WS-TMP-INTRATE.                                
           05 WS-PCT1                   PIC X.                                  
           05 WS-PCT2                   PIC X.                                  
           05 WS-PCT3                   PIC X(2).                               
       01  WS-DTE-CHEQUE-SENT-ALPHA     PIC X(8)    VALUE SPACES.               
       01  REDEFINES WS-DTE-CHEQUE-SENT-ALPHA.                                  
           05  WS-DTE-CHEQUE-SENT-NUM   PIC 9(6).                               
           05  WS-DTE-CHEQUE-SENT-DD    PIC 9(2).                               
       01  WS-DTE-PP-DUE-ALPHA          PIC X(8)    VALUE SPACES.               
       01  REDEFINES WS-DTE-PP-DUE-ALPHA.                                       
           05  WS-DTE-PP-DUE-NUM        PIC 9(6).                               
           05  WS-DTE-PP-DUE-DD         PIC 9(2).                               
                                                                                
       01  WS-FD30-PP-DATA.                                                     
           05  WS-PP-SCH                PIC X(2).                               
           05  WS-FD30-AMT-PP-PAID      PIC S9(6)V99.                           
           05  WS-FD30-AMT-PP-LOAN      PIC S9(6)V99.                           
           05  WS-FD30-DTE-FIN-PROCESS  PIC X(8).                               
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  LC49-PR-HEAD-01.                                                     
           05  LC49-RPTNAME        PIC X(8)      VALUE 'BP13LC49'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC49-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC49-PRT-PAGE       PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  LC49-PR-HEAD-02.                                                     
           05  FILLER              PIC X(43)     VALUE SPACES .                 
           05  FILLER              PIC X(43)     VALUE                          
               'REPORT ON INTEREST REBATE FOR MORTGAGE LOAN'.                   
                                                                                
       01  LC49-PR-HEAD-03.                                                     
           05  FILLER              PIC X(14)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE                          
               'SNO'.                                                           
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE                          
               'REGN NO'.                                                       
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE                          
               'SCH'.                                                           
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE                          
               '   LOAN   '.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               'IT-REBATE'.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE                          
               ' DUE-DATE '.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE                          
               ' FIN-DATE '.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE                          
               ' CHQ-DATE '.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE                          
               '  OUTS-LOAN '.                                                  
                                                                                
       01  LC49-PR-HEAD-04.                                                     
           05  FILLER              PIC X(14)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE ALL '-'.                 
                                                                                
       01  LC49-PR-DTL.                                                         
           05  FILLER              PIC X(14)     VALUE SPACES.                  
           05  LC49-PRT-SNO        PIC ZZZ9      VALUE ZEROES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LC49-REGN-NO        PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LC49-SCH            PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC49-LOAN           PIC ZZZ,ZZ9.99      VALUE ZEROES.            
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC49-IT-REBATE      PIC ZZ,ZZ9.99 VALUE ZEROES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC49-DUE-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC49-FIN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC49-CHQ-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC49-LOAN-OUTS      PIC Z,ZZZ,ZZ9.99    VALUE ZEROES.            
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES        THRU    1000-EXIT.                   
           PERFORM  2000-READ-FD30         THRU    2000-EXIT.                   
           PERFORM  3000-PROCESS           THRU    3000-EXIT                    
                    UNTIL WS-EOF-FD30 = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE     THRU    9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13FD30                                                 
                       SY02F001                                                 
                I-O    BP13KD38                                                 
                       BP13KD00                                                 
                OUTPUT BP13F125                                                 
                       BP13LC49.                                                
                                                                                
           IF WS-KD38-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD38-STATUS ' WS-KD38-STATUS         
               MOVE     WS-KD38-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD00-STATUS ' WS-KD00-STATUS         
               MOVE     WS-KD00-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1500-READ-SY02F001   THRU 1500-EXIT.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1500-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ SY02F001.                                                       
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LC49-DATE.                             
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-FD30.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13FD30 AT END MOVE 'Y' TO WS-EOF-FD30                         
                                GO TO 2000-EXIT                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO CNT-FD30-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE ZEROES TO       WS-DAY   WS-INT-PER-DAY                         
                                         WS-AMT-REBATE                          
                                         WS-AMT-REBATE-6DEC.                    
           MOVE SPACES TO       WS-DTE-CHEQUE-SENT-ALPHA                        
                                WS-DTE-PP-DUE-ALPHA                             
                                WS-FD30-PP-DATA.                                
           INITIALIZE           WS-FD30-PP-DATA.                                
           MOVE 'N'    TO       WS-VALID-REC.                                   
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 15                 
              PERFORM 3500-CHECK-DTE-SENT    THRU 3500-EXIT                     
           END-PERFORM.                                                         
                                                                                
           IF WS-VALID-REC = 'Y'                                                
              PERFORM 3300-COMPUTE-REBATE       THRU 3300-EXIT                  
              IF WS-AMT-REBATE > 0                                              
                 CONTINUE                                                       
              ELSE                                                              
                 PERFORM 2000-READ-FD30      THRU 2000-EXIT                     
                 GO TO 3000-EXIT                                                
              END-IF                                                            
           ELSE                                                                 
              PERFORM 2000-READ-FD30      THRU 2000-EXIT                        
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 7000-READ-KD00            THRU 7000-EXIT.                    
           IF WS-KD00-FD = 'Y'                                                  
                                                                                
              IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                               
                 MOVE ZEROES                 TO KD00-AMT-LOAN-BAL               
              END-IF                                                            
                                                                                
              PERFORM 5000-WRITE-F125        THRU 5000-EXIT                     
                                                                                
              PERFORM 4500-REWRITE-KD38      THRU 4500-EXIT                     
              PERFORM 8000-REWRITE-KD00      THRU 8000-EXIT                     
                                                                                
              PERFORM 6000-PRINT-REPORT      THRU 6000-EXIT                     
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FD30            THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3300-COMPUTE-REBATE.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF KD38-AMT-INTEREST-NEWLOAN IS NOT NUMERIC                          
              MOVE ZEROES            TO KD38-AMT-INTEREST-NEWLOAN               
           END-IF                                                               
           IF KD38-NUM-INTEREST-DAY IS NOT NUMERIC                              
              MOVE ZEROES            TO KD38-NUM-INTEREST-DAY                   
           END-IF                                                               
                                                                                
           IF WS-DTE-CHEQUE-SENT-ALPHA NOT = '00000000' AND SPACES              
              COMPUTE WS-DAY =                                                  
                      WS-DTE-CHEQUE-SENT-DD - WS-DTE-PP-DUE-DD                  
              IF KD38-AMT-INTEREST-NEWLOAN NOT = ZEROES AND                     
                 KD38-NUM-INTEREST-DAY     NOT = ZEROES                         
                  COMPUTE WS-INT-PER-DAY =                                      
                      KD38-AMT-INTEREST-NEWLOAN / KD38-NUM-INTEREST-DAY         
              END-IF                                                            
                                                                                
              COMPUTE WS-AMT-REBATE-6DEC =                                      
                      WS-INT-PER-DAY * WS-DAY                                   
                                                                                
      ***     ROUND TO TWO DECIMAL PLACES                                       
              COMPUTE WS-AMT-REBATE ROUNDED = WS-AMT-REBATE-6DEC                
                                                                                
           ELSE                                                                 
              MOVE KD38-AMT-INTEREST-NEWLOAN TO WS-AMT-REBATE                   
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-CHECK-DTE-SENT.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF FD30-DTE-PP-DUE(WS-CTR)(1:6) = F001-DTE-CURRENT(1:6)              
              IF FD30-DTE-CHEQUE-SENT(WS-CTR)(7:2)                              
                                         = FD30-DTE-PP-DUE(WS-CTR)(7:2)         
                 CONTINUE                                                       
              ELSE                                                              
                 PERFORM 4000-READ-KD38         THRU 4000-EXIT                  
                 IF WS-KD38-FD = 'Y'                                            
                    MOVE 'Y'                       TO WS-VALID-REC              
                    PERFORM 3600-MOVE-PP-DATA    THRU 3600-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3600-MOVE-PP-DATA.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD30-NUM-PP-SCHDL(WS-CTR)  TO WS-PP-SCH.                        
                                                                                
           IF FD30-DTE-CHEQUE-SENT(WS-CTR) = SPACES OR LOW-VALUES               
              MOVE '00000000'              TO WS-DTE-CHEQUE-SENT-ALPHA          
           ELSE                                                                 
           MOVE FD30-DTE-CHEQUE-SENT(WS-CTR)                                    
                                           TO WS-DTE-CHEQUE-SENT-ALPHA          
           END-IF.                                                              
           IF FD30-DTE-PP-DUE(WS-CTR) = SPACES OR LOW-VALUES                    
              MOVE '00000000'              TO WS-DTE-PP-DUE-ALPHA               
           ELSE                                                                 
              MOVE FD30-DTE-PP-DUE(WS-CTR) TO WS-DTE-PP-DUE-ALPHA               
           END-IF.                                                              
                                                                                
           MOVE FD30-AMT-PP-LOAN(WS-CTR)   TO WS-FD30-AMT-PP-LOAN.              
           MOVE FD30-DTE-FIN-PROCESS(WS-CTR) TO WS-FD30-DTE-FIN-PROCESS.        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-READ-KD38.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD30-NUM-REGN TO KD38-NUM-REGN.                                 
           MOVE 'DUE'         TO KD38-NUM-TRANS.                                
           MOVE FD30-NUM-PP-SCHDL(WS-CTR)   TO KD38-NUM-SCHEDULE.               
                                                                                
           READ BP13KD38                                                        
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD38-FD                                         
                 ADD 1 TO CNT-KD38-READ                                         
              WHEN 23                                                           
                 DISPLAY KD38-KEY-FLD                                           
                 MOVE 'N' TO WS-KD38-FD                                         
                 ADD 1 TO CNT-KD38-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD38 - STATUS '                  
                             WS-KD38-STATUS                                     
                 MOVE        WS-KD38-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4500-REWRITE-KD38.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE F001-DTE-CURRENT           TO KD38-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD38-TME-UPDATE.                  
           MOVE 'BP13CC49'                 TO KD38-NUM-USERID.                  
                                                                                
           IF KD38-AMT-OPEN-BAL2 IS NOT NUMERIC                                 
              MOVE ZEROES                  TO KD38-AMT-OPEN-BAL2                
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-REBATE              TO KD38-AMT-INT-REBATE-1.            
                                                                                
           COMPUTE WS-AMT-LOAN-BAL-REBATE =                                     
                   KD38-AMT-OPEN-BAL2 - WS-AMT-REBATE.                          
           MOVE WS-AMT-LOAN-BAL-REBATE  TO KD38-AMT-OPEN-BAL1-REBATE.           
                                                                                
           REWRITE BP13KD38-TRANS-REC.                                          
                                                                                
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13KD38 - STATUS '                 
                             WS-KD38-STATUS                                     
                 MOVE        WS-KD38-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO CNT-KD38-UPDATE.                                            
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-F125.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES              TO BP13F125-CASHTRAN.                       
           INITIALIZE                  BP13F125-CASHTRAN.                       
                                                                                
           MOVE FD30-NUM-REGN       TO F125-NUM-REGN.                           
           MOVE '68'                TO F125-CDE-TRANS-TYPE.                     
           MOVE 'IT'                TO F125-CDE-PAYMENT-TYPE.                   
           MOVE F001-DTE-CURRENT    TO F125-DTE-POST                            
                                       F125-DTE-TRANS.                          
           MOVE ZEROES              TO F125-COUNT-HISTORY.                      
           MOVE FD30-NUM-SCH-ACC    TO F125-NUM-SCH-ACCT.                       
                                                                                
           MOVE KD38-CDE-INTEREST-RATE TO WS-TMP-INTRATE.                       
           IF  WS-PCT1 = 0                                                      
               STRING 'I'               DELIMITED BY SIZE                       
                      KD00-CDE-LOAN-SCH DELIMITED BY SIZE                       
                      '-'               DELIMITED BY SIZE                       
                      WS-PCT2           DELIMITED BY SIZE                       
                      '.'               DELIMITED BY SIZE                       
                      WS-PCT3           DELIMITED BY SIZE                       
                      INTO              F125-NUM-RECPT-JRNO                     
           ELSE                                                                 
               STRING 'I'               DELIMITED BY SIZE                       
                      KD00-CDE-LOAN-SCH DELIMITED BY SIZE                       
                      '-'               DELIMITED BY SIZE                       
                      WS-PCT1           DELIMITED BY SIZE                       
                      WS-PCT2           DELIMITED BY SIZE                       
                      '.'               DELIMITED BY SIZE                       
                      WS-PCT3           DELIMITED BY SIZE                       
                      INTO              F125-NUM-RECPT-JRNO                     
           END-IF.                                                              
                                                                                
                                                                                
           MOVE WS-AMT-REBATE           TO F125-AMT-RECEIPT                     
                                           F125-AMT-INTEREST.                   
           MOVE WS-DTE-PP-DUE-ALPHA     TO F125-DTE-PP-DUE.                     
           MOVE 'C'                     TO F125-CDE-CRDR.                       
           MOVE KD00-CDE-LOAN-SCH       TO F125-NUM-LOAN-SCH.                   
                                                                                
           MOVE WS-AMT-REBATE           TO F125-AMT-INTEREST                    
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1 TO CNT-F125-WRITE.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-PRINT-REPORT.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-LINE > 50                                                      
              ADD 1 TO WS-PAGE                                                  
              MOVE WS-PAGE TO LC49-PRT-PAGE                                     
              WRITE BP13LC49-REC FROM LC49-PR-HEAD-01  AFTER PAGE               
              WRITE BP13LC49-REC FROM LC49-PR-HEAD-02  AFTER 2                  
              WRITE BP13LC49-REC FROM LC49-PR-HEAD-03  AFTER 2                  
              WRITE BP13LC49-REC FROM LC49-PR-HEAD-04                           
              MOVE SPACES TO BP13LC49-REC                                       
              WRITE BP13LC49-REC                                                
              MOVE  7 TO WS-LINE                                                
           END-IF.                                                              
                                                                                
           ADD 1                             TO WS-SNO                          
                                                WS-LINE.                        
                                                                                
           MOVE SPACES TO LC49-PR-DTL.                                          
           MOVE WS-SNO                       TO LC49-PRT-SNO.                   
           MOVE FD30-NUM-REGN                TO LC49-REGN-NO.                   
           MOVE WS-PP-SCH                    TO LC49-SCH.                       
           MOVE WS-FD30-AMT-PP-LOAN          TO LC49-LOAN.                      
           MOVE WS-AMT-REBATE                TO LC49-IT-REBATE.                 
           STRING WS-DTE-PP-DUE-ALPHA(7:2) '/'                                  
                  WS-DTE-PP-DUE-ALPHA(5:2) '/'                                  
                  WS-DTE-PP-DUE-ALPHA(1:4)                                      
                  DELIMITED BY SIZE INTO        LC49-DUE-DATE.                  
           STRING WS-FD30-DTE-FIN-PROCESS(7:2) '/'                              
                  WS-FD30-DTE-FIN-PROCESS(5:2) '/'                              
                  WS-FD30-DTE-FIN-PROCESS(1:4)                                  
                  DELIMITED BY SIZE INTO        LC49-FIN-DATE.                  
           IF WS-DTE-CHEQUE-SENT-ALPHA  NOT = SPACES AND '00000000'             
              STRING WS-DTE-CHEQUE-SENT-ALPHA(7:2) '/'                          
                     WS-DTE-CHEQUE-SENT-ALPHA(5:2) '/'                          
                     WS-DTE-CHEQUE-SENT-ALPHA(1:4)                              
                     DELIMITED BY SIZE INTO     LC49-CHQ-DATE                   
           END-IF.                                                              
           MOVE KD00-AMT-LOAN-BAL            TO LC49-LOAN-OUTS.                 
                                                                                
           WRITE BP13LC49-REC          FROM LC49-PR-DTL.                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       7000-READ-KD00.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD30-NUM-REGN TO KD00-NUM-REGN.                                 
                                                                                
           READ BP13KD00                                                        
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD00-FD                                         
                 ADD 1 TO CNT-KD00-READ                                         
              WHEN 23                                                           
                 MOVE 'N' TO WS-KD00-FD                                         
                 ADD 1 TO CNT-KD00-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD00 - STATUS '                  
                             WS-KD00-STATUS                                     
                 MOVE        WS-KD00-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8000-REWRITE-KD00.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE F001-DTE-CURRENT           TO KD00-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD00-TME-UPDATE.                  
           MOVE 'BP13CC49'                 TO KD00-NUM-USERID.                  
                                                                                
           COMPUTE KD00-AMT-LOAN-BAL =                                          
                   KD00-AMT-LOAN-BAL - WS-AMT-REBATE.                           
                                                                                
      *** COMPUTATION OF ARR-INTR (INTEREST REBATE)                             
           COMPUTE KD00-AMT-ARR-INTR =                                          
                   KD00-AMT-ARR-INTR - WS-AMT-REBATE                            
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD00-UPDATE                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13KD00 - STATUS '                 
                             WS-KD00-STATUS                                     
                 MOVE        WS-KD00-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13FD30                                                       
                 SY02F001                                                       
                 BP13KD38                                                       
                 BP13KD00                                                       
                 BP13F125                                                       
                 BP13LC49.                                                      
                                                                                
           IF WS-KD38-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD38-STATUS ' WS-KD38-STATUS.         
              MOVE WS-KD38-STATUS TO RETURN-CODE.                               
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD00-STATUS ' WS-KD00-STATUS.         
              MOVE WS-KD00-STATUS TO RETURN-CODE.                               
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CC49 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BP13FD30) = '                     
                    CNT-FD30-READ.                                              
           DISPLAY 'NO OF RECORDS FOUND FROM (BP13KD38) = '                     
                    CNT-KD38-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD38) = '                     
                    CNT-KD38-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS UPDATED    (BP13KD38) = '                     
                    CNT-KD38-UPDATE.                                            
           DISPLAY 'NO OF RECORDS FOUND FROM (BP13KD00) = '                     
                    CNT-KD00-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD00) = '                     
                    CNT-KD00-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS UPDATED    (BP13KD00) = '                     
                    CNT-KD00-UPDATE.                                            
           DISPLAY 'NO OF RECORDS WRITTEN    (BP13F125) = '                     
                    CNT-F125-WRITE.                                             
           DISPLAY 'NO OF RECORDS WRITTEN    (BP13LC49) = '                     
                    WS-SNO.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
