       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ15.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 15 MAR 2012                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CREATE 2 FILES FROM BP13F730 WITH    *              
      *                   : FOUND AND NOT FOUND CASES IN         *              
      *                   : AB03K080 AND AB03K030 FILES          *              
      *                                                          *              
      * 2. INPUT          : BP13F730 - BP13.F730.XXXXXXXXXXXXX   *              
      *                   : AB03K080 - AB03.K080.UINACC          *              
      *                   : AB03K030 - AB03.K030.SALEMAST        *              
      *                                                          *              
      * 3. OUTPUT         : P13F730F - BP13.F730.CZ15.FOUND      *              
      *                   : P13F730N - BP13.F730.CZ15.NOTFOUND   *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP134482 15/03/2012 ZDD1 NEW PROGRAM.                   *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730     ASSIGN        TO BP13F730.                       
                                                                                
           SELECT AB03K080     ASSIGN        TO AB03K080                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K080-KEY                        
                               FILE STATUS   IS WS-K080-STATUS.                 
                                                                                
           SELECT AB03K030     ASSIGN        TO AB03K030                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K030-NUM-HDB-REF                
                               FILE STATUS   IS WS-K030-STATUS.                 
                                                                                
           SELECT BP13K730     ASSIGN        TO BP13K730                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K730-KEY-NEW                    
                               FILE STATUS   IS WS-K730-STATUS.                 
                                                                                
           SELECT AB03K700     ASSIGN        TO AB03K700                        
                               ORGANIZATION  IS INDEXED                         
                               ACCESS MODE   IS RANDOM                          
                               RECORD KEY    IS K700-NUM-HDB-REF                
                               FILE STATUS   IS WS-K700-STATUS.                 
                                                                                
           SELECT P13F730F     ASSIGN        TO P13F730F.                       
                                                                                
           SELECT P13F730N     ASSIGN        TO P13F730N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F730.                                                           
                                                                                
       FD  AB03K080                                                             
                               RECORD CONTAINS 50 CHARACTERS.                   
       COPY AB03K080.                                                           
                                                                                
       FD  AB03K030                                                             
                               RECORD CONTAINS 800 CHARACTERS.                  
       COPY AB03K030.                                                           
                                                                                
       FD  AB03K700                                                             
                               RECORD CONTAINS 500 CHARACTERS.                  
       COPY AB03K700.                                                           
                                                                                
       FD  BP13K730                                                             
                               RECORD CONTAINS 500 CHARACTERS.                  
       COPY BP13K730.                                                           
                                                                                
       FD  P13F730F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F730F-REC                  PIC X(500).                            
                                                                                
       FD  P13F730N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F730N-REC                  PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K080-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K030-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K730-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K700-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F730-EOF               PIC X     VALUE 'N'.                   
           05  WS-K080-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F730-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595A-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595T-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-K730-UPD               PIC 9(6)  VALUE ZEROES.                
           05  WS-K730-NFND              PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 4 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K080-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K080-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F730                                                 
                       AB03K080                                                 
                       AB03K030                                                 
                       AB03K700                                                 
                I-O    BP13K730                                                 
                OUTPUT P13F730F                                                 
                       P13F730N.                                                
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 OPEN ERROR ' WS-K080-STATUS                     
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 OPEN ERROR ' WS-K030-STATUS                     
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K700-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K700 OPEN ERROR ' WS-K700-STATUS                     
              MOVE WS-K700-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K730 OPEN ERROR ' WS-K730-STATUS                     
              MOVE WS-K730-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F730 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F730.                                                      
      *************************************************************             
           READ BP13F730                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F730-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F730-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F730-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F730-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F730-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND                    
              F730-NUM-NRIC3(1:1) NOT = '#'                                     
              MOVE F730-NUM-NRIC3        TO WS-NRIC-LIST(3)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND                    
              F730-NUM-NRIC4(1:1) NOT = '#'                                     
              MOVE F730-NUM-NRIC4        TO WS-NRIC-LIST(4)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO P13F730F-REC                        
                                            P13F730N-REC                        
                                                                                
                   PERFORM 3000-START-AB03K080 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              PERFORM 3700-PROCESS-NOT-FOUND THRU 3700-EXIT                     
           ELSE                                                                 
              PERFORM 3800-PROCESS-FOUND THRU 3800-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F730 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-AB03K080.                                                     
      *************************************************************             
           MOVE SPACES                   TO AB03K080-REC.                       
           INITIALIZE                       AB03K080-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K080-NUM-UIN                        
                                            WS-K080-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K080-EOF                         
                                            WS-K080-REC-PROCESS.                
                                                                                
           START AB03K080 KEY >= K080-KEY.                                      
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-AB03K080 THRU 3100-EXIT               
                      UNTIL K080-NUM-UIN NOT = WS-K080-NRIC-PREV                
                         OR WS-K080-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 START ERROR ' WS-K080-STATUS              
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-AB03K080.                                                  
      *************************************************************             
           READ AB03K080 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K080-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K080-NUM-UIN = WS-K080-NRIC-PREV                         
                       PERFORM 3500-READ-AB03K030 THRU 3500-EXIT                
                                                                                
                       MOVE 'Y'          TO WS-K080-REC-PROCESS                 
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 READ NEXT ERROR ' WS-K080-STATUS          
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3500-READ-AB03K030.                                                      
      *************************************************************             
           MOVE SPACES                   TO AB03K030-REC.                       
           INITIALIZE                       AB03K030-REC.                       
                                                                                
           MOVE K080-NUM-HDB-REF         TO K030-NUM-HDB-REF.                   
                                                                                
           READ AB03K030.                                                       
                                                                                
           EVALUATE WS-K030-STATUS                                              
               WHEN 00                                                          
                    IF K030-CDE-ACCT-STAT = 'C'                                 
                       PERFORM 4000-READ-AB03K700    THRU 4000-EXIT             
                    ELSE                                                        
                       ADD 1 TO WS-595N-WRITE-CNT                               
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-595A-WRITE-CNT                                  
               WHEN OTHER                                                       
                    DISPLAY 'AB03K030 READ ERROR ' WS-K030-STATUS               
                    DISPLAY 'K030 KEY: ' K030-NUM-HDB-REF                       
                    MOVE WS-K030-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3600-UPDATE-BP13K730.                                                    
      *************************************************************             
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
               WHEN 00                                                          
                MOVE 'P'                 TO K730-NUM-APPS-ELIG-TAG              
                    REWRITE BP13K730-REC                                        
                       IF WS-K730-STATUS NOT = 00                               
                         DISPLAY 'BP13K730 READ ERROR ' WS-K730-STATUS          
                         DISPLAY 'K730 KEY: ' K730-NUM-REGN                     
                         MOVE WS-K730-STATUS  TO RETURN-CODE                    
                                                                                
                         PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT              
                       END-IF                                                   
                     ADD 1 TO WS-K730-UPD                                       
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-K730-NFND                                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K730 READ ERROR ' WS-K730-STATUS               
                    DISPLAY 'K730 KEY: ' K730-NUM-REGN                          
                    MOVE WS-K730-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3700-PROCESS-NOT-FOUND.                                                  
      *************************************************************             
                                                                                
           IF  F730-NUM-APPS = 'Y'                                              
             MOVE 'P'                      TO F730-NUM-APPS-ELIG-TAG            
             MOVE SPACES                   TO BP13K730-REC                      
             MOVE F730-NUM-REGN            TO K730-NUM-REGN                     
             PERFORM 3600-UPDATE-BP13K730  THRU 3600-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE BP13F730-REC TO P13F730N-REC                                    
                                                                                
           WRITE P13F730N-REC.                                                  
           ADD 1 TO WS-595T-WRITE-CNT.                                          
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3800-PROCESS-FOUND.                                                      
      *************************************************************             
                                                                                
           MOVE K030-CDE-SALES-TYP                 TO F730-NUM-SMS-FT           
           MOVE K030-CDE-ALLOC-SCH                 TO F730-NUM-PREM-CDE         
           MOVE BP13F730-REC TO P13F730F-REC                                    
           MOVE SPACES                   TO P13F730F-REC(493:8)                 
           MOVE 'S'                      TO P13F730F-REC(500:1)                 
           MOVE K080-NUM-SCH-ACCNT TO P13F730F-REC(1:11)                        
           WRITE P13F730F-REC                                                   
                                                                                
           ADD 1                         TO WS-595F-WRITE-CNT.                  
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-READ-AB03K700.                                                      
      *************************************************************             
           MOVE SPACES                   TO AB03K700-REC.                       
           INITIALIZE                       AB03K700-REC.                       
                                                                                
           MOVE K080-NUM-HDB-REF         TO K700-NUM-HDB-REF.                   
                                                                                
           READ AB03K700.                                                       
                                                                                
           EVALUATE WS-K700-STATUS                                              
               WHEN 00                                                          
                 DISPLAY K080-NUM-UIN '=' K700-NUM-UIN1                         
                 IF K080-NUM-UIN = K700-NUM-UIN1                                
                    IF  K700-CDE-OWNER-STATUS1 = 'L'                            
                       DISPLAY 'OWNER-STATUS1 =' K700-CDE-OWNER-STATUS1         
                       MOVE 'Y'  TO WS-NRIC-FOUND                               
                    ELSE                                                        
                       ADD 1 TO WS-595N-WRITE-CNT                               
                    END-IF                                                      
                 ELSE                                                           
                       ADD 1 TO WS-595N-WRITE-CNT                               
                 END-IF                                                         
                 IF K080-NUM-UIN = K700-NUM-UIN2                                
                    IF  K700-CDE-OWNER-STATUS2 = 'L'                            
                       MOVE 'Y'  TO WS-NRIC-FOUND                               
                    ELSE                                                        
                       ADD 1 TO WS-595N-WRITE-CNT                               
                    END-IF                                                      
                 ELSE                                                           
                       ADD 1 TO WS-595N-WRITE-CNT                               
                 END-IF                                                         
                 IF K080-NUM-UIN = K700-NUM-UIN3                                
                    IF  K700-CDE-OWNER-STATUS3 = 'L'                            
                       MOVE 'Y'  TO WS-NRIC-FOUND                               
                    ELSE                                                        
                       ADD 1 TO WS-595N-WRITE-CNT                               
                    END-IF                                                      
                 ELSE                                                           
                       ADD 1 TO WS-595N-WRITE-CNT                               
                 END-IF                                                         
                 IF K080-NUM-UIN = K700-NUM-UIN4                                
                    IF  K700-CDE-OWNER-STATUS4 = 'L'                            
                       MOVE 'Y'  TO WS-NRIC-FOUND                               
                    ELSE                                                        
                       ADD 1 TO WS-595N-WRITE-CNT                               
                    END-IF                                                      
                 ELSE                                                           
                       ADD 1 TO WS-595N-WRITE-CNT                               
                 END-IF                                                         
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-595A-WRITE-CNT                                  
               WHEN OTHER                                                       
                    DISPLAY 'AB03K700 READ ERROR ' WS-K700-STATUS               
                    DISPLAY 'K700 KEY: ' K700-NUM-HDB-REF                       
                    MOVE WS-K700-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CZ15 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F730        :' WS-F730-READ-CNT.          
           DISPLAY 'RECS WRT P13F730F             :' WS-595F-WRITE-CNT.         
           DISPLAY 'TOTAL RECS WRITTEN P13F730N   :' WS-595T-WRITE-CNT.         
           DISPLAY '                  STAT NOT C  :' WS-595N-WRITE-CNT.         
           DISPLAY '                  NOT FND AB03:' WS-595A-WRITE-CNT.         
           DISPLAY 'RECS UPD BP13K730             :' WS-K730-UPD.               
           DISPLAY 'RECS NFND TO BP13K730         :' WS-K730-NFND.              
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F730                                                       
                 AB03K080                                                       
                 AB03K030                                                       
                 BP13K730                                                       
                 P13F730F                                                       
                 P13F730N.                                                      
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 CLOSE ERROR ' WS-K080-STATUS                    
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 CLOSE ERROR ' WS-K030-STATUS                    
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K730 CLOSE ERROR ' WS-K730-STATUS                    
              MOVE WS-K730-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
