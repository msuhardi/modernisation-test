       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD15.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  16/10/2015.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : OUTPUT CASES WHERE NRIC EXISTS IN EC        *            
      *                APPLICATION                                 *            
      *                                                            *            
      * INPUT FILES  : BP13F730 - CASES PER REGN                   *            
      *                BP18K310 - BP18.K310.OCCUPIER               *            
      * OUTPUT FILE  : P13F730A - CASES THAT COMPLY WITH THE       *            
      *                           CONDITIONS ABOVE                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135944  SMR2  16102015  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP18K310 ASSIGN       TO BP18K310                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K310-KEY-FLD                         
                           FILE STATUS  IS WS-K310-STATUS.                      
                                                                                
           SELECT P13F730A ASSIGN       TO P13F730A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F.                                              
       COPY BP13F730.                                                           
                                                                                
       FD   BP18K310                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP18K310.                                                           
                                                                                
       FD   P13F730A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F.                                              
       01   P13F730A-REC           PIC X(500).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K310-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-F730A-WRITE              PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-FOUND                    PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-SYSTEM-DATE              PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP18K310                                                 
                OUTPUT P13F730A.                                                
                                                                                
           IF WS-K310-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP18K310 FILE ERROR ' WS-K310-STATUS             
              MOVE WS-K310-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-READ-BP13F730.                                                      
      ****************************                                              
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                    TO WS-F730-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F730-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-PROCESS-RECORDS.                                                    
      ****************************                                              
                                                                                
           MOVE 'N'                         TO WS-FOUND.                        
                                                                                
           IF F730-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'                         
              PERFORM 4000-CHECK-HA         THRU 4000-EXIT                      
              IF WS-FOUND = 'Y'                                                 
                 WRITE P13F730A-REC         FROM BP13F730-REC                   
                 ADD 1                      TO WS-F730A-WRITE                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-CHECK-HA.                                                           
      ****************************                                              
                                                                                
           MOVE F730-NUM-NRIC1              TO WS-NRIC.                         
           PERFORM 5000-READ-BP18K310       THRU 5000-EXIT.                     
                                                                                
           IF WS-FOUND = 'N' AND                                                
             (F730-NUM-NRIC2(1:1) NOT = '#' AND SPACES AND LOW-VALUES)          
              MOVE F730-NUM-NRIC2           TO WS-NRIC                          
              PERFORM 5000-READ-BP18K310    THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-FOUND = 'N' AND                                                
             (F730-NUM-NRIC3(1:1) NOT = '#' AND SPACES AND LOW-VALUES)          
              MOVE F730-NUM-NRIC3           TO WS-NRIC                          
              PERFORM 5000-READ-BP18K310    THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-FOUND = 'N' AND                                                
             (F730-NUM-NRIC4(1:1) NOT = '#' AND SPACES AND LOW-VALUES)          
              MOVE F730-NUM-NRIC4           TO WS-NRIC                          
              PERFORM 5000-READ-BP18K310    THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       5000-READ-BP18K310.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES                      TO BP18K310-REC.                    
           INITIALIZE                          BP18K310-REC.                    
                                                                                
           MOVE F730-NUM-REGN               TO K310-NUM-REGN.                   
           MOVE WS-NRIC                     TO K310-NUM-NRIC.                   
                                                                                
           READ BP18K310.                                                       
                                                                                
           EVALUATE WS-K310-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                      TO WS-FOUND                         
                                                                                
           WHEN 23                                                              
              MOVE 'N'                      TO WS-FOUND                         
              DISPLAY 'BP18K310 REC NOT FND: ' K310-KEY-FLD                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP18K310'                               
              MOVE WS-K310-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9000-CLOSE-FILES.                                                        
      ****************************                                              
                                                                                
           DISPLAY 'RUN DATE : ' WS-SYSTEM-DATE.                                
           DISPLAY '*================ BP13CD15 ================*'.              
           DISPLAY '  NO OF F730 RECS READ       : ' WS-F730-READ.              
           DISPLAY '  NO OF F730 RECS WRITTEN    : ' WS-F730A-WRITE.            
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP18K310                                                       
                 P13F730A.                                                      
                                                                                
           IF WS-K310-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP18K310 FILE ERROR ' WS-K310-STATUS             
              MOVE WS-K310-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
