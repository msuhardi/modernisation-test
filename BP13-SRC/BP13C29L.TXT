       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C29L.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  12 NOV 2009.                                              
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: SUBROUTINE TO PRINT PRICELIST OF FLATS WITH      *           
      *            F999-NUM-POD-DUE = 'N' AND SPACES(FORM-TYPE ='A')*           
      *-------------------------------------------------------------*           
      * INPUT FILES: BP13F999                                       *           
      *              BP13K813                                       *           
      *              BM06K110                                       *           
      *              BM06K100                                       *           
      *                                                             *           
      * OUTPUT FILE: BP13L29L (FOR F999-NUM-POD-DUE = 'N' OR SPACES)*           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---- -------- ------------------------------------ *           
      * BP136066 ESA1 12112009 NEW PROGRAM(MODELLED AFTER BP13C29H) *           
      * BP136836 ESA1 16082017 TO CATER FOR ROF SELECTION TYPE      *           
      * BP137117 ESA1 20170111 TO CATER FOR 'UPDATED AS AT' ON THE  *           
      *                        HEADER PRINTING                      *           
      *                        TO CATER FOR PPVC BLOCKS DISPLAY     *           
      * BP137117 ESA1 20180228 TO MOVE '-' IN '99999999' PRICES     *           
      * BP137588 ESA1 20181211 TO CATER FOR SWT BLOCKS DISPLAY      *           
      * BP13XXXX CC37 20190521 TO CATER FOR SWT AND PPVC DISPLAY    *           
      * BP13XXXX CC37 20190607 TO CATER FOR OBF SELECTION TYPE      *           
      * BP138018 CC37 20191211 TO CATER FOR PPVC PROJECTS WITH      *           
      *                        SHORTER WAITING TIME                 *           
      * BP138613 MRR5 20210312 CATER FOR CCA FLAT NEW FORMID BP130O *           
      * BP138641 MRR5 20210511 REPLACE 'HARMONY VILLAGE @ BUKIT BATOK'          
      *                        WITH 'HARMONY VILLAGE @ BT BATOK'    *           
      * BP139344 MRR5 20221103 PRINT DPD AS EDPD IF SBF OR OBF      *           
      * BP138905 MRR5 20221220 CHANGED LENGTH OF L29L-BLK-DETAILS   *           
      * BP139436 MRR5 20230125 CALL NEW SUBR BP13CRG3 TO READ THE   *           
      *                        BLDG_FTR TABLE FOR CAR-LITE TEXT     *           
      *                        CHECK MODL DESGN 75/76 FOR PLH TEXT  *           
      * BP139436 MRR5 20230224 ADJUSTED ALIGNMENT OF L29L-NUM-UNIT  *           
      * BP139436 MRR5 20230306 FIX REMARKS ISSUE                    *           
      * BP139685 MRR5 20230929 CATER FOR SER 50YR                   *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F999  ASSIGN TO BP13F999.                                 
                                                                                
           SELECT BM06K100  ASSIGN TO BM06K100                                  
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13L29L  ASSIGN TO BP13L29L.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13F999                                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F999.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L29L                                                             
           RECORD CONTAINS 137 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  L29L-PRTREC                    PIC X(137).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER                 PIC X(13) VALUE SPACES.               
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-MM             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-CCYY           PIC X(04) VALUE SPACES.               
           05  WS-NUM-MONTH               PIC X(02) VALUE SPACES.               
           05  WS-BLOCK-INFO              PIC X(30) OCCURS 8.                   
           05  WS-REMARKS                 PIC X(30) OCCURS 14.                  
           05  WS-PREV-CDE-NT             PIC X(03) VALUE SPACES.               
           05  WS-PREV-FT                 PIC X(03) VALUE SPACES.               
           05  WS-PREV-CNTRCT-BLOCK.                                            
               10  WS-PREV-NEIGHBOURHOOD  PIC X(04) VALUE SPACES.               
               10  WS-PREV-CONTRACT-NO    PIC X(04) VALUE SPACES.               
               10  WS-PREV-BLOCK-NO       PIC X(05) VALUE SPACES.               
           05  WS-NME-ESTATE              PIC X(20) VALUE SPACES.               
           05  WS-NUM-EST                 PIC X(03) VALUE SPACES.               
           05  WS-TMP-CNTRCT-TYP          PIC X(30) VALUE SPACES.               
           05  WS-SUB                     PIC 9(02) VALUE ZEROES.               
           05  WS-MAX-REM                 PIC 9(02) VALUE ZEROES.               
           05  WS-CTR                     PIC 9(03) VALUE ZEROES.               
           05  WS-NAME-MONTH              PIC X(03) VALUE SPACES.               
           05  WS-ALLOC-SEL-DESC          PIC X(50) VALUE SPACES.               
           05  WS-RETURN-CODE             PIC 9(02) VALUE ZEROES.               
           05  WS-NUM-MAX-CAT.                                                  
               10  WS-NUM-MAX-CAT1        PIC ZZ9.                              
               10  WS-NUM-MAX-CAT2        PIC ZZ9.                              
               10  WS-NUM-MAX-CAT3        PIC ZZ9.                              
               10  WS-NUM-MAX-SPR         PIC ZZ9.                              
           05  WS-PREV-FLAT-TYPE          PIC X(03) VALUE SPACES.               
           05  WS-FLAT-TYPE               PIC X(14) VALUE SPACES.               
           05  WS-HEADING-LINE            PIC X(78) VALUE SPACES.               
           05  WS-ALLOC-DATE.                                                   
               10  WS-ALLOC-DATE-MONTH    PIC X(03).                            
               10  FILLER                 PIC X(01).                            
               10  WS-ALLOC-DATE-YEAR     PIC X(04).                            
           05  WS-TXT-QTR                 PIC X(07) VALUE SPACES.               
           05  WS-SELTYPE                 PIC X(55) VALUE SPACES.               
           05  WS-GEN3-TAG                PIC X(01) VALUE SPACES.               
           05  WS-CAR-LITE-TAG            PIC X(01) VALUE SPACES.               
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(125).                         
           05 WS-PARSED-STRING              PIC X(125).                         
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  WS-TABLES.                                                           
           05  WS-MONTH-TABLE.                                                  
               10  FILLER                 PIC X(05) VALUE '01JAN'.              
               10  FILLER                 PIC X(05) VALUE '02FEB'.              
               10  FILLER                 PIC X(05) VALUE '03MAR'.              
               10  FILLER                 PIC X(05) VALUE '04APR'.              
               10  FILLER                 PIC X(05) VALUE '05MAY'.              
               10  FILLER                 PIC X(05) VALUE '06JUN'.              
               10  FILLER                 PIC X(05) VALUE '07JUL'.              
               10  FILLER                 PIC X(05) VALUE '08AUG'.              
               10  FILLER                 PIC X(05) VALUE '09SEP'.              
               10  FILLER                 PIC X(05) VALUE '10OCT'.              
               10  FILLER                 PIC X(05) VALUE '11NOV'.              
               10  FILLER                 PIC X(05) VALUE '12DEC'.              
           05  FILLER  REDEFINES WS-MONTH-TABLE.                                
               10  WS-MONTH-TBL  OCCURS 12 INDEXED BY WS-MNT-NDX.               
                   15  WS-MONTH-NUM       PIC X(02).                            
                   15  WS-MONTH-NAME      PIC X(03).                            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K853-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K820-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K010-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K110-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K100-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F999-SW         PIC X(01) VALUE SPACES.                
               88  C-END-OF-F999         VALUE 'Y'.                             
           05  WS-BLKINFO-PRINTED-SW     PIC X(01) VALUE SPACES.                
               88  C-BLKINFO-PRINTED     VALUE 'Y'.                             
           05  WS-REMARKS-PRINTED-SW     PIC X(01) VALUE SPACES.                
               88  C-REMARKS-PRINTED     VALUE 'Y'.                             
           05  WS-FIRST-REC-READ-SW      PIC X(01) VALUE SPACES.                
               88  C-FIRST-REC-READ      VALUE 'Y'.                             
           05  WS-CHANGE-BLK-SW          PIC X(01) VALUE SPACES.                
               88  C-CHANGE-BLK          VALUE 'Y'.                             
           05  WS-K110-FND               PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F999-READ-CTR          PIC 9(06) VALUE ZEROES.                
           05  WS-LINE-CTR               PIC 9(02) VALUE ZEROES.                
           05  WS-LINE-CTR-A             PIC 9(02) VALUE ZEROES.                
           05  WS-PAGE-CTR               PIC 9(05) VALUE ZEROES.                
           05  WS-PRT-CTR                PIC 9(06) VALUE ZEROES.                
                                                                                
      *-------------------------------                                          
      *  FOR BP13C91J                *                                          
      *-------------------------------                                          
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-ZONE        PIC X(03).                               
           05  WS-LINK-NUM-FT          PIC X(02).                               
           05  WS-LINK-DTE-BALLOT      PIC X(06).                               
       01  WS-BP13K813-REC             PIC X(1000).                             
       01  WS-ZONE-FND-FLAG            PIC X(01).                               
                                                                                
      *-------------------------------                                          
      *  FOR BP13CRG3                *                                          
      *-------------------------------                                          
       01  WS-CRG3-REC.                                                         
           05  WS-CRG3-BLDG-GL        PIC X(05).                                
           05  WS-CRG3-BLDGFTR-DTLS.                                            
               10 WS-CRG3-DTLS        OCCURS 100 TIMES.                         
                  15  WS-CRG3-TYPE-FTR     PIC X(15).                           
                  15  WS-CRG3-NUM-FTR      PIC X(8).                            
                  15  WS-CRG3-NUM-SQNC     PIC X(2).                            
                  15  WS-CRG3-QTY-FTR      PIC S9(4) USAGE COMP.                
                  15  WS-CRG3-QTY-MSRMNT   PIC S9(6)V9(2) USAGE COMP-3.         
                  15  WS-CRG3-DTE-LASTUPD  PIC X(26).                           
           05  WS-CRG3-CDE-SYSERR     PIC S9(3).                                
           05  WS-CRG3-CDE-ERROR      PIC X(04).                                
           05  WS-CRG3-ERR-MSG        PIC X(50).                                
                                                                                
       COPY BP13K813.                                                           
       COPY M06COM31.                                                           
                                                                                
      *-------------------------------                                          
      *  REPORT LAYOUT               *                                          
      *-------------------------------                                          
      *-----------------------------------------------------------              
      *LAY-OUT FOR SALE OF FLATS FOR F999-NUM-POD-DUE = 'N'                     
      *-----------------------------------------------------------              
       01  L29L-HEADER-01.                                                      
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-TYP1      PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(15) VALUE ' APARTMENT (SA)'.           
           05  L29L-LEASE-TXT1     PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-02.                                                      
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-TYP2      PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE ' FLATS '.                   
           05  L29L-LEASE-TXT2     PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-2B.                                                      
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-TYP2B     PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE ' FLATS '.                   
           05  L29L-LEASE-TXT2B    PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-1A.                                                      
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-TYP1A     PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(15) VALUE ' APARTMENT (SA)'.           
           05  L29L-ALLOC-DATE1A   PIC X(08) VALUE SPACES.                      
           05  L29L-LEASE-TXT1A    PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-2A.                                                      
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-TYP2A     PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(09) VALUE ' FLATS - '.                 
           05  L29L-ALLOC-DATE2A   PIC X(08) VALUE SPACES.                      
           05  L29L-LEASE-TXT2A    PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-GRO-01.                                                  
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-GRO1      PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(70) VALUE ' APARTMENT (SA) UNDE        
      -        'R THE GRASSROOTS ORGANIZATIONS SCHEME'.                         
           05  L29L-LEASE-GRO1     PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-GRO-02.                                                  
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-GRO2      PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(50) VALUE                              
               ' FLATS UNDER THE GRASSROOTS ORGANISATIONS SCHEME'.              
           05  L29L-LEASE-GRO2     PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-GRO-2A.                                                  
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-GRO2A     PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(50) VALUE                              
               ' FLATS UNDER THE GRASSROOTS ORGANISATIONS SCHEME'.              
           05  L29L-LEASE-GRO2A    PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-MGP-01.                                                  
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-MGP1      PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(55) VALUE                              
             ' APARTMENT UNDER THE MULTI-GENERATION PRIORITY SCHEME  '.         
           05  L29L-PARENT-FT1     PIC X(04) VALUE SPACES.                      
           05  L29L-LEASE-FT1      PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-MGP-02.                                                  
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-FLAT-MGP2      PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(51) VALUE                              
               ' FLATS UNDER THE MULTI-GENERATION PRIORITY SCHEME  '.           
           05  L29L-PARENT-FT2     PIC X(04) VALUE SPACES.                      
           05  L29L-LEASE-FT2      PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-3R-HEADER-01.                                                   
           05  FILLER              PIC X(18) VALUE 'QUARTERLY SALE OF '.        
           05  L29L-3R-FLAT-TYP1   PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(13) VALUE ' APARTMENT   '.             
           05  L29L-3R-LEASE-TXT1  PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-3R-HEADER-02.                                                   
           05  FILLER              PIC X(18) VALUE 'QUARTERLY SALE OF '.        
           05  L29L-3R-FLAT-TYP2   PIC X(12) VALUE SPACES.                      
           05  FILLER              PIC X(09) VALUE ' FLATS   '.                 
           05  L29L-3R-LEASE-TXT2  PIC X(14) VALUE SPACES.                      
                                                                                
       01  L29L-1F-HEADER-01.                                                   
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29L-1F-FLAT-TYP    PIC X(14) VALUE SPACES.                      
           05  FILLER              PIC X(13) VALUE ' APARTMENTS  '.             
           05  FILLER              PIC X(50) VALUE SPACES.                      
                                                                                
       01  L29L-HEADER-03.                                                      
           05  L29L-NME-ESTATE     PIC X(90) VALUE SPACES.                      
           05  FILLER              PIC X(19) VALUE SPACES.                      
           05  FILLER              PIC X(11) VALUE 'PAGE: '.                    
           05  L29L-PAGE           PIC ZZZZZ.                                   
                                                                                
       01  L29L-HEADER-04.                                                      
           05  FILLER              PIC X(14)  VALUE 'UPDATED AS AT '.           
           05  L29L-DTE-PRINTED    PIC X(11)  VALUE SPACES.                     
           05  FILLER              PIC X(108) VALUE SPACES.                     
                                                                                
       01  L29L-DETAIL-REC.                                                     
           05  L29L-BLK-DETAILS        PIC X(30) VALUE SPACES.                  
           05  L29L-NUM-UNIT           PIC X(08) VALUE SPACES.                  
           05  L29L-NUM-FLAT-CODE      PIC X(04) VALUE SPACES.                  
           05  L29L-FLEXI-CDE          PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29L-FLOOR-AREA         PIC ZZ.                                  
           05  L29L-FLOOR-AREA-R REDEFINES L29L-FLOOR-AREA PIC X(02).           
           05  L29L-SLASH              PIC X(01) VALUE SPACES.                  
           05  L29L-IN-FLOOR-AREA      PIC ZZ.                                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-99   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-99-R REDEFINES                                      
               L29L-SELLING-PRICE-99   PIC X(07).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-45   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-45-R REDEFINES                                      
               L29L-SELLING-PRICE-45   PIC X(07).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-40   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-40-R REDEFINES                                      
               L29L-SELLING-PRICE-40   PIC X(07).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-35   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-35-R REDEFINES                                      
               L29L-SELLING-PRICE-35   PIC X(07).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-30   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-30-R REDEFINES                                      
               L29L-SELLING-PRICE-30   PIC X(07).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-25   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-25-R REDEFINES                                      
               L29L-SELLING-PRICE-25   PIC X(07).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-20   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-20-R REDEFINES                                      
               L29L-SELLING-PRICE-20   PIC X(07).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L29L-SELLING-PRICE-15   PIC ZZZ,ZZZ.                             
           05  L29L-SELLING-15-R REDEFINES                                      
               L29L-SELLING-PRICE-15   PIC X(07).                               
                                                                                
       01  L29L-LINE.                                                           
           05  FILLER                  PIC X(137) VALUE ALL '_'.                
                                                                                
      *-------------------------------                                          
      *  DJDE FOR FORM TYPE = 'A'    *                                          
      *-------------------------------                                          
       01  DJDE-FORM-BP130O              PIC X(94)  VALUE                       
           '$DJDE$ JDE=13C26E,FORMS=BP130O,COPIES=3,JDL=BP13,PMODE=LANDS        
      -    'CAPE,FONT=(R112BL),DUPLEX=YES,END;'.                                
       01  DJDE-FORM-BP13UD-A            PIC X(94)  VALUE                       
           '$DJDE$ JDE=13C26E,FORMS=BP13HF,COPIES=3,JDL=BP13,PMODE=LANDS        
      -    'CAPE,FONT=(R112BL),DUPLEX=YES,END;'.                                
       01  DJDE-FORM-BP13HT              PIC X(94)  VALUE                       
           '$DJDE$ JDE=13C26E,FORMS=BP13HT,COPIES=3,JDL=BP13,PMODE=LANDS        
      -    'CAPE,FONT=(R112BL),DUPLEX=YES,END;'.                                
       01  DJDE-FORM-BP13UD-B            PIC X(90)  VALUE                       
           '$DJDE$ BEGIN=(0.50,0.30),END;'.                                     
      *-----------------------------------------------------------              
       LINKAGE SECTION.                                                         
      *-----------------------------------------------------------              
       01  WS-C290-PARAMETERS.                                                  
           05  WS-C290-FORM-TYPE         PIC X(01).                             
           05  WS-C290-FLAT-TYPE         PIC X(02).                             
           05  WS-C290-NUM-GRO           PIC X(01).                             
           05  WS-C290-RETURN-CODE       PIC 9(04).                             
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION USING WS-C290-PARAMETERS.                             
      *--------------------------------------------------------------           
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           IF WS-C290-FLAT-TYPE = '1F'                                          
              MOVE SPACES         TO L29L-PRTREC                                
              WRITE L29L-PRTREC FROM DJDE-FORM-BP130O AFTER PAGE                
           ELSE                                                                 
              IF WS-C290-NUM-GRO = 'P' OR 'C'                                   
                 MOVE SPACES         TO L29L-PRTREC                             
                 WRITE L29L-PRTREC FROM DJDE-FORM-BP13HT AFTER PAGE             
              ELSE                                                              
                 MOVE SPACES         TO L29L-PRTREC                             
                 WRITE L29L-PRTREC FROM DJDE-FORM-BP13UD-A AFTER PAGE           
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *    WRITE L29L-PRTREC FROM DJDE-FORM-BP13UD-B.                           
                                                                                
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
             UNTIL C-END-OF-F999.                                               
                                                                                
           IF NOT C-BLKINFO-PRINTED OR                                          
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3330-PRINT-REMAINING-INFO    THRU 3330-EXIT               
           END-IF.                                                              
                                                                                
           WRITE L29L-PRTREC     FROM L29L-LINE AFTER 1.                        
                                                                                
           MOVE ZEROES        TO WS-RETURN-CODE.                                
                                                                                
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           GOBACK.                                                              
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F999                                               
                         BM06K110                                               
                         BM06K100                                               
                OUTPUT   BP13L29L.                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 OPEN ERROR, STATUS ' WS-K110-STATUS             
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 OPEN ERROR, STATUS ' WS-K100-STATUS             
              MOVE WS-K100-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
           CALL 'O13K813A'         USING WS-RETURN-CODE.                        
                                                                                
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'OPEN ERROR, BP13K813 STATUS ' WS-RETURN-CODE             
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE 'Y'       TO WS-FIRST-REC-READ-SW.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F999 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F999-SW                               
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F999-READ-CTR.                                         
                                                                                
           PERFORM 3100-PRINT-DETAILS    THRU 3100-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-PRINT-DETAILS.                                                      
      *--------------------------------*                                        
                                                                                
           IF (F999-NEIGHBOURHOOD = WS-PREV-NEIGHBOURHOOD) AND                  
              (F999-CONTRACT-NO = WS-PREV-CONTRACT-NO) AND                      
              (F999-BLOCK-NO = WS-PREV-BLOCK-NO)                                
              PERFORM 3300-WRITE-DETAILS    THRU 3300-EXIT                      
           ELSE                                                                 
              MOVE 'Y'     TO WS-CHANGE-BLK-SW                                  
              IF NOT C-BLKINFO-PRINTED OR                                       
                 NOT C-REMARKS-PRINTED                                          
                 PERFORM 3330-PRINT-REMAINING-INFO THRU 3330-EXIT               
              END-IF                                                            
              PERFORM 3200-GET-CNTRCT-BLOCK-INFO   THRU 3200-EXIT               
              IF NOT C-FIRST-REC-READ                                           
                 WRITE L29L-PRTREC        FROM L29L-LINE AFTER 1                
              END-IF                                                            
              IF F999-CDE-NT NOT = WS-PREV-CDE-NT                               
                 OR F999-NUM-FLAT-TYPE NOT = WS-PREV-FT                         
                 PERFORM 4100-PRINT-PAGE-BREAK        THRU 4100-EXIT            
              END-IF                                                            
              MOVE F999-CDE-NT        TO WS-PREV-CDE-NT                         
              MOVE F999-NUM-FLAT-TYPE TO WS-PREV-FT                             
              PERFORM 3300-WRITE-DETAILS           THRU 3300-EXIT               
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-GET-CNTRCT-BLOCK-INFO.                                              
      *--------------------------------*                                        
                                                                                
           MOVE F999-NEIGHBOURHOOD    TO WS-PREV-NEIGHBOURHOOD.                 
           MOVE F999-CONTRACT-NO      TO WS-PREV-CONTRACT-NO.                   
           MOVE F999-BLOCK-NO         TO WS-PREV-BLOCK-NO.                      
                                                                                
           PERFORM 3210-GET-BLK-DETAILS     THRU 3210-EXIT.                     
           PERFORM 3220-GET-REMARKS         THRU 3220-EXIT.                     
           MOVE ZEROES   TO WS-SUB.                                             
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3210-GET-BLK-DETAILS.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE SPACES     TO WS-BLKINFO-PRINTED-SW.                            
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 8                  
              MOVE SPACES         TO WS-BLOCK-INFO (WS-SUB)                     
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES      TO WS-SUB.                                          
                                                                                
           IF F999-NUM-ZONE = SPACES OR LOW-VALUES                              
              MOVE F999-NME-ESTATE        TO WS-NME-ESTATE                      
           ELSE                                                                 
              IF F999-NUM-ZONE = 'RF '                                          
                 MOVE F999-NME-ESTATE     TO WS-NME-ESTATE                      
              ELSE                                                              
                 PERFORM 3212-GET-ESTATE-NAME   THRU 3212-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-NUM-SELTY = 'BTO'                                            
              IF WS-NME-ESTATE(1:9) = 'TOA PAYOH' AND                           
                 WS-NUM-EST = '749'                                             
                 MOVE 'TOA PAYOH(BIDADARI)'  TO WS-NME-ESTATE                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           SET WS-MNT-NDX TO 1.                                                 
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES       TO WS-ALLOC-DATE                             
              WHEN F999-DTE-BALLOT(5:2) = WS-MONTH-NUM (WS-MNT-NDX)             
                 MOVE WS-MONTH-NAME (WS-MNT-NDX) TO WS-ALLOC-DATE-MONTH         
                 MOVE F999-DTE-BALLOT(1:4)       TO WS-ALLOC-DATE-YEAR          
           END-SEARCH.                                                          
                                                                                
           MOVE SPACES            TO WS-HEADING-LINE                            
                                     WS-SELTYPE.                                
                                                                                
           EVALUATE F999-NUM-SELTY                                              
              WHEN 'BTO'                                                        
                   STRING 'BUILD-TO-ORDER ' WS-ALLOC-DATE                       
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
              WHEN 'SBF'                                                        
                   STRING 'SALE OF BALANCE FLATS ' WS-ALLOC-DATE                
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
              WHEN 'OBF'                                                        
                 STRING 'OPEN BOOKING OF FLATS'                                 
                        DELIMITED BY SIZE INTO WS-SELTYPE                       
                 END-STRING                                                     
              WHEN 'ROF'                                                        
                 STRING 'RE-OFFER OF BALANCE FLATS ' WS-ALLOC-DATE              
                        DELIMITED BY SIZE INTO WS-SELTYPE                       
                 END-STRING                                                     
              WHEN 'SER'                                                        
                   STRING 'SELECTIVE EN BLOC REDEVELOPMENT SCHEME '             
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
              WHEN 'REN'                                                        
                   STRING 'RELOCATION EXERCISE '                                
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
           END-EVALUATE.                                                        
           IF F999-NUM-GRO = 'Y'                                                
              STRING WS-SELTYPE ' - ' WS-NME-ESTATE                             
                     DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                  
              END-STRING                                                        
              MOVE LENGTH OF WS-STRING-TO-PARSE                                 
                                        TO WS-STRING-LENGTH                     
              MOVE 'Y'                     TO WS-INSERT-SPACE-FLAG              
              PERFORM 4200-PARSE-STRING    THRU 4200-EXIT                       
              MOVE WS-PARSED-STRING        TO  L29L-NME-ESTATE                  
           END-IF.                                                              
                                                                                
           IF F999-NUM-GRO = 'P'                                                
              STRING WS-SELTYPE ' - ' WS-NME-ESTATE                             
                     ' (PARENT APPLICATION)'                                    
                     DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                  
              END-STRING                                                        
              MOVE LENGTH OF WS-STRING-TO-PARSE                                 
                                        TO WS-STRING-LENGTH                     
              MOVE 'Y'                     TO WS-INSERT-SPACE-FLAG              
              PERFORM 4200-PARSE-STRING    THRU 4200-EXIT                       
              MOVE WS-PARSED-STRING        TO  L29L-NME-ESTATE                  
           ELSE                                                                 
              IF F999-NUM-GRO = 'C'                                             
                 STRING WS-SELTYPE ' - ' WS-NME-ESTATE                          
                        ' (MARRIED CHILD APPLICATION)'                          
                       DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                
                 END-STRING                                                     
                 MOVE LENGTH OF WS-STRING-TO-PARSE                              
                                        TO WS-STRING-LENGTH                     
                 MOVE 'Y'                     TO WS-INSERT-SPACE-FLAG           
                 PERFORM 4200-PARSE-STRING    THRU 4200-EXIT                    
                 MOVE WS-PARSED-STRING        TO  L29L-NME-ESTATE               
              ELSE                                                              
                 IF F999-NUM-SELTY = 'NPL'                                      
                    MOVE WS-NME-ESTATE  TO L29L-NME-ESTATE                      
                 ELSE                                                           
                    STRING WS-SELTYPE ' - ' WS-NME-ESTATE                       
                      DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                 
                    MOVE LENGTH OF WS-STRING-TO-PARSE                           
                                        TO WS-STRING-LENGTH                     
                    MOVE 'Y'               TO WS-INSERT-SPACE-FLAG              
                    PERFORM 4200-PARSE-STRING    THRU 4200-EXIT                 
                    MOVE WS-PARSED-STRING  TO  L29L-NME-ESTATE                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES                    TO WS-BLOCK-INFO (1).                 
                                                                                
           IF F999-NME-LOCAL-PRCNCT = 'HARMONY VILLAGE @ BUKIT BATOK'           
              MOVE 'HARMONY VILLAGE @ BT BATOK' TO WS-BLOCK-INFO (2)            
           ELSE                                                                 
              MOVE F999-NME-LOCAL-PRCNCT        TO WS-BLOCK-INFO (2)            
           END-IF.                                                              
                                                                                
           STRING 'BLK ' F999-BLOCK-NO                                          
                   DELIMITED BY SIZE    INTO WS-BLOCK-INFO (3).                 
                                                                                
           MOVE F999-NME-STREET           TO WS-BLOCK-INFO (4).                 
                                                                                
           MOVE SPACES                    TO WS-BLOCK-INFO (5).                 
                                                                                
           MOVE 'MAXIMUM UNITS ALLOWED FOR: ' TO WS-BLOCK-INFO (6).             
                                                                                
           IF F999-NUM-MAX-CAT1  = ZEROES AND                                   
              F999-NUM-CAT1-FULL = SPACES                                       
              MOVE F999-NUM-MAX-CAT1  TO WS-NUM-MAX-CAT1                        
           ELSE                                                                 
              MOVE F999-NUM-ADJ-CAT1  TO WS-NUM-MAX-CAT1                        
           END-IF.                                                              
                                                                                
           IF F999-NUM-MAX-CAT2  = ZEROES AND                                   
              F999-NUM-CAT2-FULL = SPACES                                       
              MOVE F999-NUM-MAX-CAT2  TO WS-NUM-MAX-CAT2                        
           ELSE                                                                 
              MOVE F999-NUM-ADJ-CAT2  TO WS-NUM-MAX-CAT2                        
           END-IF.                                                              
                                                                                
           IF F999-NUM-MAX-CAT3  = ZEROES AND                                   
              F999-NUM-CAT3-FULL = SPACES                                       
              MOVE F999-NUM-MAX-CAT3  TO WS-NUM-MAX-CAT3                        
           ELSE                                                                 
              MOVE F999-NUM-ADJ-CAT3  TO WS-NUM-MAX-CAT3                        
           END-IF.                                                              
                                                                                
           STRING 'M - '   WS-NUM-MAX-CAT1 ', '                                 
                  'C - '   WS-NUM-MAX-CAT2 ', '                                 
                  'I/O - ' WS-NUM-MAX-CAT3                                      
                  DELIMITED BY SIZE     INTO WS-BLOCK-INFO (7).                 
                                                                                
           IF F999-NUM-SELTY = 'SER'                                            
              MOVE F999-NUM-MAX-SPR    TO WS-NUM-MAX-SPR                        
                                                                                
              STRING 'SPR - ' WS-NUM-MAX-SPR                                    
                    DELIMITED BY SIZE  INTO WS-BLOCK-INFO (8)                   
           ELSE                                                                 
              MOVE SPACES              TO WS-BLOCK-INFO (8)                     
           END-IF.                                                              
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3212-GET-ESTATE-NAME.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE SPACES               TO WS-LINK-REC.                            
           IF F999-NUM-SELTY = 'BTO'                                            
              MOVE F999-NUM-ZONE     TO WS-LINK-NUM-ZONE                        
           ELSE                                                                 
              MOVE F999-CDE-NT       TO WS-LINK-NUM-ZONE                        
           END-IF.                                                              
           MOVE F999-NUM-FLAT-TYPE   TO WS-LINK-NUM-FT.                         
           MOVE F999-DTE-BALLOT      TO WS-LINK-DTE-BALLOT.                     
           CALL 'BP13C91J' USING  WS-LINK-REC,                                  
                                  WS-BP13K813-REC,                              
                                  WS-ZONE-FND-FLAG.                             
                                                                                
           IF WS-ZONE-FND-FLAG = 'Y'                                            
              MOVE WS-BP13K813-REC   TO BP13K813-REC                            
              IF F999-NUM-ZONE = '194'                                          
                 MOVE 'JURONG EAST/WEST' TO WS-NME-ESTATE                       
              ELSE                                                              
                 MOVE K813-NME-SITE      TO WS-NME-ESTATE                       
              END-IF                                                            
              MOVE K813-NUM-GEN3-TAG TO WS-GEN3-TAG                             
              MOVE K813-NUM-EST      TO WS-NUM-EST                              
           ELSE                                                                 
              MOVE SPACES            TO WS-NME-ESTATE                           
                                        WS-GEN3-TAG                             
                                        WS-NUM-EST                              
           END-IF.                                                              
                                                                                
       3212-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3213-CHANGE-CNTRCT-TYP.                                                  
      *--------------------------------*                                        
                                                                                
           MOVE SPACES               TO WS-TMP-CNTRCT-TYP.                      
                                                                                
           IF F999-CDE-MODL-DESG NOT = SPACES                                   
              EVALUATE F999-CDE-MODL-DESG                                       
                 WHEN '1IMP'                                                    
                    MOVE 'IMPROVED'         TO WS-TMP-CNTRCT-TYP                
                 WHEN '1PA '                                                    
                    MOVE 'PREMIUM APARTMENTS' TO WS-TMP-CNTRCT-TYP              
                 WHEN '2DBP'                                                    
                    MOVE 'DESIGN & BUILD/DESIGN PLUS'                           
                                            TO WS-TMP-CNTRCT-TYP                
                 WHEN '2MOA'                                                    
                    MOVE 'MODEL A'          TO WS-TMP-CNTRCT-TYP                
                 WHEN '3NG'                                                     
                    MOVE 'NEW GENERATION'   TO WS-TMP-CNTRCT-TYP                
                 WHEN '1STD'                                                    
                 WHEN '3STD'                                                    
                 WHEN '4STD'                                                    
                    MOVE 'STANDARD'      TO WS-TMP-CNTRCT-TYP                   
                 WHEN '4SIM'                                                    
                    MOVE 'SIMPLIFIED'       TO WS-TMP-CNTRCT-TYP                
                 WHEN '3STB'                                                    
                    MOVE 'STANDARD (BUDGET)'  TO WS-TMP-CNTRCT-TYP              
              END-EVALUATE                                                      
           ELSE                                                                 
              EVALUATE F999-NUM-CNTRCT-TYP                                      
                WHEN '1PA '                                                     
                   MOVE 'PREMIUM APARTMENTS' TO WS-TMP-CNTRCT-TYP               
                WHEN '2DBP'                                                     
                   MOVE 'DESIGN & BUILD/DESIGN PLUS'                            
                                             TO WS-TMP-CNTRCT-TYP               
                WHEN '3STD'                                                     
                   MOVE 'STANDARD'           TO WS-TMP-CNTRCT-TYP               
                WHEN '3STB'                                                     
                   MOVE 'STANDARD (BUDGET)'  TO WS-TMP-CNTRCT-TYP               
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '83' OR '84'                                 
              MOVE SPACES TO WS-TMP-CNTRCT-TYP                                  
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '28'                                         
              MOVE 'TERRACE'      TO WS-TMP-CNTRCT-TYP                          
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '85'                                         
              MOVE 'DBSS'          TO WS-TMP-CNTRCT-TYP                         
           END-IF.                                                              
                                                                                
       3213-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3214-DECODE-ROOM-ZONE.                                                   
      *--------------------------------*                                        
           MOVE SPACES    TO WS-GEN3-TAG.                                       
           PERFORM 3212-GET-ESTATE-NAME      THRU 3212-EXIT.                    
                                                                                
           MOVE SPACES                       TO WS-FLAT-TYPE.                   
           IF F999-NUM-FLAT-TYPE = '1F'                                         
              MOVE 'COMMUNITY CARE'   TO WS-FLAT-TYPE                           
           ELSE                                                                 
              MOVE '2-ROOM FLEXI'     TO WS-FLAT-TYPE                           
           END-IF.                                                              
                                                                                
           IF F999-NUM-FLAT-TYPE = '1A' OR '2A'                                 
              MOVE '(SHORT LEASE)'           TO L29L-LEASE-TXT1                 
                                                L29L-LEASE-TXT2                 
                                                L29L-LEASE-TXT2B                
                                                L29L-LEASE-TXT1A                
                                                L29L-LEASE-TXT2A                
                                                L29L-LEASE-GRO1                 
                                                L29L-LEASE-GRO2                 
                                                L29L-LEASE-GRO2A                
                                                L29L-LEASE-FT1                  
                                                L29L-LEASE-FT2                  
                                                L29L-3R-LEASE-TXT1              
                                                L29L-3R-LEASE-TXT2              
           END-IF.                                                              
                                                                                
           SET WS-MNT-NDX TO 1.                                                 
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES       TO WS-ALLOC-DATE                             
              WHEN F999-DTE-BALLOT(5:2) = WS-MONTH-NUM (WS-MNT-NDX)             
                 MOVE WS-MONTH-NAME (WS-MNT-NDX) TO WS-ALLOC-DATE-MONTH         
                 MOVE F999-DTE-BALLOT(1:4)       TO WS-ALLOC-DATE-YEAR          
           END-SEARCH.                                                          
                                                                                
           IF F999-NUM-FLAT-TYPE = '1F'                                         
              MOVE WS-FLAT-TYPE           TO L29L-1F-FLAT-TYP                   
              MOVE L29L-1F-HEADER-01      TO WS-HEADING-LINE                    
              WRITE L29L-PRTREC FROM L29L-1F-HEADER-01 AFTER PAGE               
              GO TO 3214-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F999-NUM-SELTY = '3R'                                             
              IF F999-NUM-FLAT-TYPE = '1A' OR '2A'                              
                 MOVE WS-FLAT-TYPE           TO L29L-3R-FLAT-TYP1               
                 MOVE L29L-3R-HEADER-01      TO WS-HEADING-LINE                 
                 WRITE L29L-PRTREC FROM L29L-3R-HEADER-01 AFTER PAGE            
              ELSE                                                              
                 MOVE WS-FLAT-TYPE           TO L29L-3R-FLAT-TYP2               
                 MOVE L29L-3R-HEADER-02      TO WS-HEADING-LINE                 
                 WRITE L29L-PRTREC FROM L29L-3R-HEADER-02 AFTER PAGE            
              END-IF                                                            
           ELSE                                                                 
              IF F999-NUM-SELTY = 'NPL'                                         
                 IF F999-NUM-FLAT-TYPE = '1A' OR '2A'                           
                    MOVE WS-FLAT-TYPE           TO L29L-FLAT-TYP1A              
                    MOVE WS-ALLOC-DATE          TO L29L-ALLOC-DATE1A            
                    MOVE L29L-HEADER-1A         TO WS-HEADING-LINE              
                    WRITE L29L-PRTREC FROM L29L-HEADER-1A AFTER PAGE            
                 ELSE                                                           
                    MOVE WS-FLAT-TYPE           TO L29L-FLAT-TYP2A              
                    MOVE WS-ALLOC-DATE          TO L29L-ALLOC-DATE2A            
                    MOVE L29L-HEADER-2A         TO WS-HEADING-LINE              
                    WRITE L29L-PRTREC FROM L29L-HEADER-2A AFTER PAGE            
                 END-IF                                                         
              ELSE                                                              
                 IF F999-NUM-GRO = 'Y'                                          
                    IF F999-NUM-FLAT-TYPE = '1A' OR '2A'                        
                       MOVE WS-FLAT-TYPE       TO L29L-FLAT-GRO1                
                       MOVE L29L-HEADER-GRO-01 TO WS-HEADING-LINE               
                       WRITE L29L-PRTREC FROM                                   
                             L29L-HEADER-GRO-01 AFTER PAGE                      
                    ELSE                                                        
                       IF WS-GEN3-TAG = 'Y' AND                                 
                          F999-NUM-GEN3 = ' '                                   
                          MOVE WS-FLAT-TYPE    TO L29L-FLAT-GRO2A               
                          MOVE L29L-HEADER-GRO-2A TO WS-HEADING-LINE            
                          WRITE L29L-PRTREC FROM                                
                               L29L-HEADER-GRO-2A AFTER PAGE                    
                       ELSE                                                     
                          MOVE WS-FLAT-TYPE  TO L29L-FLAT-GRO2                  
                          MOVE L29L-HEADER-GRO-02  TO WS-HEADING-LINE           
                          WRITE L29L-PRTREC FROM                                
                                L29L-HEADER-GRO-02 AFTER PAGE                   
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    IF F999-NUM-GRO = 'P' OR 'C'                                
                       IF F999-NUM-FLAT-TYPE = '1A' OR '2A'                     
                          IF F999-NUM-PARENT-FT NOT = SPACES                    
                                                   AND LOW-VALUES               
                             STRING '(' F999-NUM-PARENT-FT ')'                  
                               DELIMITED BY SIZE INTO L29L-PARENT-FT1           
                             END-STRING                                         
                          ELSE                                                  
                             MOVE SPACES TO L29L-PARENT-FT1                     
                          END-IF                                                
                          MOVE WS-FLAT-TYPE TO L29L-FLAT-MGP1                   
                          MOVE L29L-HEADER-MGP-01 TO WS-HEADING-LINE            
                          WRITE L29L-PRTREC FROM                                
                               L29L-HEADER-MGP-01  AFTER PAGE                   
                       ELSE                                                     
                          IF F999-NUM-PARENT-FT NOT = SPACES                    
                                                   AND LOW-VALUES               
                             STRING '(' F999-NUM-PARENT-FT ')'                  
                                DELIMITED BY SIZE INTO L29L-PARENT-FT2          
                             END-STRING                                         
                          ELSE                                                  
                             MOVE SPACES TO L29L-PARENT-FT2                     
                          END-IF                                                
                          MOVE WS-FLAT-TYPE  TO L29L-FLAT-MGP2                  
                          MOVE L29L-HEADER-MGP-02 TO WS-HEADING-LINE            
                          WRITE L29L-PRTREC FROM                                
                               L29L-HEADER-MGP-02 AFTER PAGE                    
                       END-IF                                                   
                    ELSE                                                        
                       IF F999-NUM-FLAT-TYPE = '1A' OR '2A'                     
                          MOVE WS-FLAT-TYPE   TO L29L-FLAT-TYP1                 
                          MOVE L29L-HEADER-01 TO WS-HEADING-LINE                
                          WRITE L29L-PRTREC FROM                                
                                L29L-HEADER-01 AFTER PAGE                       
                       ELSE                                                     
                          IF WS-GEN3-TAG = 'Y' AND                              
                             F999-NUM-GEN3 = ' '                                
                             MOVE WS-FLAT-TYPE   TO L29L-FLAT-TYP2B             
                             MOVE L29L-HEADER-2B TO WS-HEADING-LINE             
                             WRITE L29L-PRTREC FROM                             
                             L29L-HEADER-2B  AFTER PAGE                         
                          ELSE                                                  
                             MOVE WS-FLAT-TYPE   TO L29L-FLAT-TYP2              
                             MOVE L29L-HEADER-02 TO WS-HEADING-LINE             
                             WRITE L29L-PRTREC FROM                             
                             L29L-HEADER-02  AFTER PAGE                         
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       3214-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3220-GET-REMARKS.                                                        
      *--------------------------------*                                        
                                                                                
           MOVE SPACES     TO WS-REMARKS-PRINTED-SW.                            
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 14                 
              MOVE SPACES       TO WS-REMARKS (WS-SUB)                          
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES          TO WS-SUB.                                      
                                                                                
           IF (F999-CDE-KEY-AVAIL = 'Y') AND                                    
              (F999-DTE-LEASE-COMM NOT = SPACES AND LOW-VALUES)                 
              ADD 1 TO WS-SUB                                                   
              MOVE 'COMPLETED FLAT,'      TO WS-REMARKS(WS-SUB)                 
           ELSE                                                                 
              IF (F999-CDE-KEY-AVAIL = 'Y') AND                                 
                 (F999-DTE-LEASE-COMM = SPACES OR LOW-VALUES)                   
                 ADD 1 TO WS-SUB                                                
                 MOVE 'COMPLETED FLAT'    TO WS-REMARKS(WS-SUB)                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-NUM-SELTY = 'SBF' OR 'NPL' OR 'ROF' OR 'OBF'                 
              IF F999-DTE-PCD NOT = SPACES AND LOW-VALUES                       
                  PERFORM 3223-MOVE-PCD-QTR              THRU 3223-EXIT         
              END-IF                                                            
           ELSE                                                                 
              IF F999-DTE-ECD NOT = SPACES AND LOW-VALUES                       
                  PERFORM 3221-MOVE-ECD-QTR              THRU 3221-EXIT         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-DTE-DLVRY-POSSESSION NOT = SPACES AND LOW-VALUES             
              PERFORM 3222-MOVE-DLVRY-POSSESSION     THRU 3222-EXIT             
           END-IF.                                                              
                                                                                
           IF ((F999-CDE-KEY-AVAIL = 'Y') AND                                   
               (F999-NUM-MODL-DESG = '97' OR '98'))                             
              ADD 2 TO WS-SUB                                                   
              STRING 'LCD: '                                                    
                      'FROM KEYS COLLECTION '                                   
                      DELIMITED BY SIZE    INTO WS-REMARKS(WS-SUB)              
           ELSE                                                                 
              IF F999-DTE-LEASE-COMM NOT = SPACES AND LOW-VALUES                
                 ADD 2 TO WS-SUB                                                
                 MOVE F999-DTE-LEASE-COMM(5:2) TO WS-NUM-MONTH                  
                 PERFORM 3400-GET-MONTH-NAME       THRU 3400-EXIT               
                 IF F999-NUM-REMAIN-LEASE = 'Y' AND                             
                    F999-DTE-BALLOT < '201905'                                  
                    STRING 'LCD: '                                              
                           F999-DTE-LEASE-COMM(7:2) ' '                         
                           WS-NAME-MONTH ' '                                    
                           F999-DTE-LEASE-COMM(1:4) ' (RL)'                     
                           DELIMITED BY SIZE    INTO WS-REMARKS(WS-SUB)         
                 ELSE                                                           
                    STRING 'LCD: '                                              
                           F999-DTE-LEASE-COMM(7:2) ' '                         
                           WS-NAME-MONTH ' '                                    
                           F999-DTE-LEASE-COMM(1:4)                             
                           DELIMITED BY SIZE    INTO WS-REMARKS(WS-SUB)         
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-CDE-POF-TAG = 'P' OR 'S' OR 'C'                              
              IF F999-CDE-OCS-TAG = 'Y'                                         
                 IF F999-CDE-POF-TAG = 'P'                                      
                    ADD 2 TO WS-SUB                                             
                    MOVE 'PPVC'             TO WS-REMARKS(WS-SUB)               
                    ADD 1 TO WS-SUB                                             
                    MOVE '(OCS OPTION FOR ELDERLY'                              
                                            TO WS-REMARKS(WS-SUB)               
                    ADD 1 TO WS-SUB                                             
                    MOVE ' FITTINGS)'       TO WS-REMARKS(WS-SUB)               
                 ELSE                                                           
                    IF F999-CDE-POF-TAG = 'S'                                   
                       ADD 2 TO WS-SUB                                          
                       MOVE 'PROJECT WITH SHORTER'                              
                                                TO WS-REMARKS(WS-SUB)           
                       ADD 1 TO WS-SUB                                          
                       MOVE 'WAITING TIME (OCS OPTION'                          
                                                TO WS-REMARKS(WS-SUB)           
                       ADD 1 TO WS-SUB                                          
                       MOVE 'FOR ELDERLY FITTINGS)'                             
                                                TO WS-REMARKS(WS-SUB)           
                    ELSE                                                        
                       ADD 2 TO WS-SUB                                          
                       MOVE 'PPVC PROJECT WITH SHORTER'                         
                                                TO WS-REMARKS(WS-SUB)           
                       ADD 1 TO WS-SUB                                          
                       MOVE 'WAITING TIME (OCS OPTION'                          
                                                TO WS-REMARKS(WS-SUB)           
                       ADD 1 TO WS-SUB                                          
                       MOVE 'FOR ELDERLY FITTINGS)'                             
                                                TO WS-REMARKS(WS-SUB)           
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF F999-CDE-POF-TAG = 'P'                                      
                    ADD 2 TO WS-SUB                                             
                    MOVE 'PPVC'             TO WS-REMARKS(WS-SUB)               
                 ELSE                                                           
                    IF F999-CDE-POF-TAG = 'S'                                   
                       ADD 2 TO WS-SUB                                          
                       MOVE 'PROJECT WITH SHORTER'                              
                                            TO WS-REMARKS(WS-SUB)               
                       ADD 1 TO WS-SUB                                          
                       MOVE 'WAITING TIME'  TO WS-REMARKS(WS-SUB)               
                    ELSE                                                        
                       ADD 2 TO WS-SUB                                          
                       MOVE 'PPVC PROJECT WITH SHORTER'                         
                                            TO WS-REMARKS(WS-SUB)               
                       ADD 1 TO WS-SUB                                          
                       MOVE 'WAITING TIME'  TO WS-REMARKS(WS-SUB)               
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
            IF (F999-CDE-OCS-TAG = 'Y') AND                                     
               (F999-NUM-FLAT-TYPE NOT = '1A' AND '2A' AND '1F') AND            
               (F999-DTE-EXPIRY NOT = SPACES AND LOW-VALUES AND ZEROES)         
               ADD 2 TO WS-SUB                                                  
               MOVE 'WITH OCS OPTION'     TO WS-REMARKS(WS-SUB)                 
            END-IF                                                              
           END-IF.                                                              
                                                                                
      **---> GET UPGRADING STATUS                                               
           IF F999-NUM-UPG-STATUS = 'PUG' OR 'TND' OR 'CON'                     
              ADD 1    TO WS-SUB                                                
              MOVE 'MUP - COMMENCING/'   TO WS-REMARKS (WS-SUB)                 
              ADD 1    TO WS-SUB                                                
              MOVE '      IN PROGRESS'   TO WS-REMARKS (WS-SUB)                 
           ELSE                                                                 
              IF F999-NUM-UPG-STATUS = 'CPL' OR 'APP' OR                        
                                       'SEG' OR 'BIL'                           
                 ADD 1    TO WS-SUB                                             
                 MOVE 'MUP - COMPLETED'     TO WS-REMARKS (WS-SUB)              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '75' OR '76'                                 
              ADD 2    TO WS-SUB                                                
              MOVE 'PRIME LOCATION PUBLIC' TO WS-REMARKS(WS-SUB)                
              ADD 1    TO WS-SUB                                                
              MOVE 'HOUSING'               TO WS-REMARKS(WS-SUB)                
           END-IF.                                                              
                                                                                
           PERFORM 322A-GET-BLDG-FTR THRU 322A-EXIT.                            
           IF WS-CAR-LITE-TAG = 'Y'                                             
              ADD 2    TO WS-SUB                                                
              MOVE 'CAR-LITE PRECINCT' TO WS-REMARKS(WS-SUB)                    
           END-IF.                                                              
                                                                                
      *** MOVE THE COUNTER OF THE LAST REMARKS LINE TO WS-MAX-REM               
                                                                                
           MOVE WS-SUB TO WS-MAX-REM.                                           
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       322A-GET-BLDG-FTR.                                                       
      ******************************************************************        
           MOVE SPACES                 TO WS-CRG3-REC.                          
           INITIALIZE                     WS-CRG3-REC.                          
           MOVE SPACES                 TO WS-CAR-LITE-TAG.                      
                                                                                
           PERFORM 322B-GET-BLDG-GL    THRU 322B-EXIT.                          
           MOVE K100-CDE-BLK           TO WS-CRG3-BLDG-GL.                      
                                                                                
           CALL 'BP13CRG3' USING WS-CRG3-REC.                                   
                                                                                
           IF (WS-CRG3-CDE-SYSERR = 0)                                          
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 100 OR          
                                                 WS-CAR-LITE-TAG = 'Y'          
                 IF (WS-CRG3-TYPE-FTR(WS-CTR) = 'CAR-LITE'                      
                                             OR 'CAR LITE')                     
                    AND (WS-CRG3-NUM-FTR (WS-CTR) = 'Y')                        
                    MOVE 'Y' TO WS-CAR-LITE-TAG                                 
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              DISPLAY 'ERROR CALLING BP13CRG3 '                                 
              DISPLAY 'SQL CODE = ' WS-CRG3-CDE-SYSERR                          
              MOVE SPACES                TO WS-CAR-LITE-TAG                     
           END-IF.                                                              
                                                                                
       322A-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       322B-GET-BLDG-GL.                                                        
      *-----------------------------------------------------------              
            MOVE SPACES           TO K110-SCH-ACC-NO.                           
            MOVE F999-NUM-HDB-REF TO K110-SCH-ACC-NO.                           
            READ BM06K110.                                                      
                                                                                
            EVALUATE WS-K110-STATUS                                             
               WHEN 00                                                          
                    PERFORM 322C-READ-BM06K100 THRU 322C-EXIT                   
               WHEN 23                                                          
                    MOVE SPACES TO K100-CDE-BLK                                 
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR : ' WS-K110-STATUS             
                    DISPLAY 'BM06K110 KEY-FLD    : ' K110-SCH-ACC-NO            
                    MOVE WS-K110-STATUS          TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT               
            END-EVALUATE.                                                       
                                                                                
       322B-EXIT.                                                               
           EXIT.                                                                
                                                                                
       322C-READ-BM06K100.                                                      
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K100-KEY-FLD.                           
           MOVE K110-ESTATE          TO K100-ESTATE.                            
           MOVE K110-NEIGHBOURHOOD   TO K100-NEIGHBOURHOOD.                     
           MOVE K110-CONTRACT-NO     TO K100-CONTRACT-NO.                       
           MOVE K110-BLK-NO          TO K100-BLK-NO.                            
                                                                                
           READ BM06K100.                                                       
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE SPACES                TO K100-CDE-BLK                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' WS-K100-STATUS                
              MOVE WS-K100-STATUS             TO  RETURN-CODE                   
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       322C-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3221-MOVE-ECD-QTR.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 2 TO WS-SUB.                                                     
                                                                                
           EVALUATE F999-DTE-ECD(5:2)                                           
              WHEN '01'                                                         
              WHEN '02'                                                         
              WHEN '03'                                                         
                 MOVE '1ST QTR' TO WS-TXT-QTR                                   
              WHEN '04'                                                         
              WHEN '05'                                                         
              WHEN '06'                                                         
                 MOVE '2ND QTR' TO WS-TXT-QTR                                   
              WHEN '07'                                                         
              WHEN '08'                                                         
              WHEN '09'                                                         
                 MOVE '3RD QTR' TO WS-TXT-QTR                                   
              WHEN '10'                                                         
              WHEN '11'                                                         
              WHEN '12'                                                         
                 MOVE '4TH QTR' TO WS-TXT-QTR                                   
           END-EVALUATE.                                                        
                                                                                
           STRING 'ECD: '                                                       
                   WS-TXT-QTR ' '                                               
                   F999-DTE-ECD(1:4)                                            
                   DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB).                 
                                                                                
       3221-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3222-MOVE-DLVRY-POSSESSION.                                              
      *--------------------------------*                                        
                                                                                
           ADD 2 TO WS-SUB.                                                     
           MOVE F999-DTE-DLVRY-POSSESSION(5:2)  TO WS-NUM-MONTH.                
           PERFORM 3400-GET-MONTH-NAME       THRU 3400-EXIT.                    
                                                                                
           IF F999-NUM-BTO-NEW-CONTRACT = 'Y' OR                                
             (F999-NUM-SELTY = 'SBF' OR 'OBF' OR 'SER')                         
              STRING 'EDPD: '                                                   
                     F999-DTE-DLVRY-POSSESSION(7:2) ' '                         
                     WS-NAME-MONTH ' '                                          
                     F999-DTE-DLVRY-POSSESSION(1:4)                             
                     DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB)                
           ELSE                                                                 
              STRING 'DPD: '                                                    
                     F999-DTE-DLVRY-POSSESSION(7:2) ' '                         
                     WS-NAME-MONTH ' '                                          
                     F999-DTE-DLVRY-POSSESSION(1:4)                             
                     DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB)                
           END-IF.                                                              
                                                                                
       3222-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3223-MOVE-PCD-QTR.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 2 TO WS-SUB.                                                     
                                                                                
           EVALUATE F999-DTE-PCD(5:2)                                           
              WHEN '01'                                                         
              WHEN '02'                                                         
              WHEN '03'                                                         
                 MOVE '1ST QTR' TO WS-TXT-QTR                                   
              WHEN '04'                                                         
              WHEN '05'                                                         
              WHEN '06'                                                         
                 MOVE '2ND QTR' TO WS-TXT-QTR                                   
              WHEN '07'                                                         
              WHEN '08'                                                         
              WHEN '09'                                                         
                 MOVE '3RD QTR' TO WS-TXT-QTR                                   
              WHEN '10'                                                         
              WHEN '11'                                                         
              WHEN '12'                                                         
                 MOVE '4TH QTR' TO WS-TXT-QTR                                   
           END-EVALUATE.                                                        
                                                                                
           STRING 'PCD: '                                                       
                   WS-TXT-QTR ' '                                               
                   F999-DTE-PCD(1:4)                                            
                   DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB).                 
                                                                                
       3223-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-WRITE-DETAILS.                                                      
      *--------------------------------*                                        
                                                                                
           IF C-FIRST-REC-READ                                                  
              PERFORM 3310-PRINT-FIRST-RECORD       THRU 3310-EXIT              
              GO TO 3300-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF C-CHANGE-BLK                                                      
              MOVE SPACES   TO WS-CHANGE-BLK-SW                                 
              MOVE ZEROES   TO WS-SUB                                           
              ADD  1        TO WS-LINE-CTR                                      
              IF WS-LINE-CTR < 52                                               
                 MOVE ZEROES              TO WS-LINE-CTR-A                      
                 IF F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                  
                    MOVE F999-NUM-FLAT-TYPE     TO WS-PREV-FLAT-TYPE            
                 END-IF                                                         
              END-IF                                                            
              IF WS-LINE-CTR > 38                                               
                 PERFORM 4100-PRINT-PAGE-BREAK      THRU 4100-EXIT              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1   TO WS-LINE-CTR.                                              
           PERFORM 4000-CHECK-LINE-CTR           THRU 4000-EXIT.                
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
           END-IF.                                                              
                                                                                
           IF WS-LINE-CTR-A = 5 OR                                              
              F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                        
              PERFORM 3360-PRINT-SPACES          THRU 3360-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 3320-MOVE-OTHER-DETAIL        THRU 3320-EXIT.                
                                                                                
           WRITE L29L-PRTREC        FROM L29L-DETAIL-REC AFTER 1.               
           ADD   1  TO WS-PRT-CTR                                               
                       WS-LINE-CTR-A.                                           
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3310-PRINT-FIRST-RECORD.                                                 
      *--------------------------------*                                        
                                                                                
           MOVE 1        TO WS-SUB.                                             
           MOVE WS-BLOCK-INFO (WS-SUB)       TO L29L-BLK-DETAILS.               
           PERFORM 3320-MOVE-OTHER-DETAIL       THRU 3320-EXIT.                 
           WRITE L29L-PRTREC       FROM L29L-DETAIL-REC AFTER 1.                
           ADD  1        TO WS-PRT-CTR.                                         
                                                                                
           ADD  1        TO WS-LINE-CTR                                         
                            WS-LINE-CTR-A.                                      
                                                                                
           MOVE SPACES   TO WS-FIRST-REC-READ-SW                                
                            WS-CHANGE-BLK-SW.                                   
                                                                                
           MOVE F999-NUM-FLAT-TYPE     TO WS-PREV-FLAT-TYPE.                    
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3320-MOVE-OTHER-DETAIL.                                                  
      *--------------------------------*                                        
                                                                                
           STRING ' ' F999-LEVEL-NO '-'                                         
                  F999-MAIN-UNIT                                                
                  DELIMITED BY SIZE       INTO L29L-NUM-UNIT.                   
                                                                                
           EVALUATE F999-NUM-FLAT-CODE                                          
           WHEN 'SF'                                                            
              MOVE '*'                 TO L29L-NUM-FLAT-CODE                    
           WHEN 'SH'                                                            
              MOVE '(H)'               TO L29L-NUM-FLAT-CODE                    
           WHEN SPACES                                                          
              MOVE SPACES              TO L29L-NUM-FLAT-CODE                    
           WHEN OTHER                                                           
              STRING '(' F999-NUM-FLAT-CODE ')'                                 
                   DELIMITED BY SIZE INTO L29L-NUM-FLAT-CODE                    
           END-EVALUATE.                                                        
                                                                                
           IF ((F999-NUM-MODL-DESG = '97' OR '98') AND                          
               (F999-NUM-FLAT-CODE = 'FF'))                                     
              MOVE SPACES              TO L29L-NUM-FLAT-CODE                    
           END-IF.                                                              
                                                                                
           IF F999-CDE-FLEXI-TAG = 'L'                                          
              STRING  '(' F999-CDE-FLEXI-TAG ')'                                
                DELIMITED BY SIZE INTO L29L-FLEXI-CDE                           
           ELSE                                                                 
              MOVE SPACES           TO L29L-FLEXI-CDE                           
           END-IF.                                                              
                                                                                
           MOVE F999-QTY-AREA-TOTAL      TO L29L-FLOOR-AREA                     
           MOVE F999-QTY-AREA-ORGNL      TO L29L-IN-FLOOR-AREA                  
           MOVE '/'                         TO L29L-SLASH.                      
                                                                                
           PERFORM 5000-LINK-BM06CCFL       THRU 5000-EXIT.                     
                                                                                
       3320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3330-PRINT-REMAINING-INFO.                                               
      *--------------------------------*                                        
                                                                                
           IF C-FIRST-REC-READ                                                  
              GO TO 3330-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES           TO L29L-DETAIL-REC.                            
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
                UNTIL C-BLKINFO-PRINTED                                         
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
                UNTIL C-REMARKS-PRINTED                                         
           END-IF.                                                              
                                                                                
       3330-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3340-PRINT-BLOCK-INFO.                                                   
      *--------------------------------*                                        
                                                                                
           ADD  1   TO WS-SUB.                                                  
                                                                                
           IF WS-SUB > 8                                                        
              MOVE 'Y'      TO WS-BLKINFO-PRINTED-SW                            
              MOVE ZEROES   TO WS-SUB                                           
              GO TO 3340-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-BLOCK-INFO (WS-SUB)     TO L29L-BLK-DETAILS.                 
                                                                                
           IF C-CHANGE-BLK OR                                                   
              C-END-OF-F999                                                     
              ADD  1   TO WS-LINE-CTR                                           
              PERFORM 4000-CHECK-LINE-CTR      THRU 4000-EXIT                   
              WRITE L29L-PRTREC        FROM L29L-DETAIL-REC AFTER 1             
           END-IF.                                                              
                                                                                
       3340-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3350-PRINT-REMARKS.                                                      
      *--------------------------------*                                        
                                                                                
           ADD  1  TO WS-SUB.                                                   
                                                                                
           IF (WS-SUB > WS-MAX-REM)                                             
              MOVE SPACES         TO L29L-BLK-DETAILS                           
              MOVE 'Y'            TO WS-REMARKS-PRINTED-SW                      
              MOVE ZEROES         TO WS-SUB                                     
              GO TO 3350-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-REMARKS (WS-SUB)        TO L29L-BLK-DETAILS.                 
                                                                                
           IF C-CHANGE-BLK OR                                                   
              C-END-OF-F999                                                     
              ADD  1   TO WS-LINE-CTR                                           
              PERFORM 4000-CHECK-LINE-CTR           THRU 4000-EXIT              
              WRITE L29L-PRTREC        FROM L29L-DETAIL-REC AFTER 1             
           END-IF.                                                              
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3360-PRINT-SPACES.                                                       
      *--------------------------------*                                        
                                                                                
           MOVE ZEROES        TO WS-LINE-CTR-A.                                 
                                                                                
           MOVE SPACES        TO L29L-NUM-UNIT                                  
                                 L29L-SLASH                                     
                                 L29L-NUM-FLAT-CODE                             
                                 L29L-FLEXI-CDE.                                
                                                                                
           MOVE ZEROES        TO L29L-FLOOR-AREA                                
                                 L29L-IN-FLOOR-AREA                             
                                 L29L-SELLING-PRICE-99                          
                                 L29L-SELLING-PRICE-45                          
                                 L29L-SELLING-PRICE-40                          
                                 L29L-SELLING-PRICE-35                          
                                 L29L-SELLING-PRICE-30                          
                                 L29L-SELLING-PRICE-25                          
                                 L29L-SELLING-PRICE-20                          
                                 L29L-SELLING-PRICE-15.                         
                                                                                
           WRITE L29L-PRTREC     FROM L29L-DETAIL-REC AFTER 1                   
                                                                                
           ADD 1       TO WS-LINE-CTR                                           
           PERFORM 4000-CHECK-LINE-CTR        THRU 4000-EXIT                    
           MOVE SPACES        TO L29L-BLK-DETAILS                               
                                                                                
           IF F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                        
              MOVE F999-NUM-FLAT-TYPE     TO WS-PREV-FLAT-TYPE                  
           END-IF.                                                              
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
           END-IF.                                                              
                                                                                
       3360-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3400-GET-MONTH-NAME.                                                     
      *--------------------------------*                                        
                                                                                
           SET WS-MNT-NDX TO 1.                                                 
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES     TO WS-NAME-MONTH                               
              WHEN WS-NUM-MONTH = WS-MONTH-NUM (WS-MNT-NDX)                     
                 MOVE WS-MONTH-NAME (WS-MNT-NDX)   TO WS-NAME-MONTH             
           END-SEARCH.                                                          
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 52  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 4100-PRINT-PAGE-BREAK       THRU 4100-EXIT                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4100-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           PERFORM 3214-DECODE-ROOM-ZONE THRU 3214-EXIT.                        
                                                                                
           MOVE SPACES        TO L29L-PRTREC.                                   
           ADD  1             TO WS-PAGE-CTR.                                   
           MOVE WS-PAGE-CTR   TO L29L-PAGE.                                     
           IF L29L-NME-ESTATE = SPACES                                          
              MOVE F999-NME-ESTATE    TO L29L-NME-ESTATE                        
           END-IF.                                                              
           WRITE L29L-PRTREC     FROM L29L-HEADER-03.                           
           MOVE WS-DISP-MM       TO WS-NUM-MONTH.                               
           PERFORM 3400-GET-MONTH-NAME THRU 3400-EXIT.                          
           STRING WS-DISP-DD ' ' WS-NAME-MONTH ' ' WS-DISP-CCYY                 
                  DELIMITED BY SIZE INTO                                        
                  L29L-DTE-PRINTED                                              
           END-STRING.                                                          
           WRITE L29L-PRTREC     FROM L29L-HEADER-04.                           
           MOVE SPACES        TO L29L-PRTREC.                                   
           WRITE L29L-PRTREC     AFTER 5.                                       
           MOVE 7             TO WS-LINE-CTR.                                   
           MOVE ZEROES        TO WS-LINE-CTR-A.                                 
                                                                                
           IF F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                        
              MOVE F999-NUM-FLAT-TYPE  TO WS-PREV-FLAT-TYPE                     
           END-IF.                                                              
                                                                                
           IF (F999-NEIGHBOURHOOD = WS-PREV-NEIGHBOURHOOD) AND                  
              (F999-CONTRACT-NO = WS-PREV-CONTRACT-NO) AND                      
              (F999-BLOCK-NO = WS-PREV-BLOCK-NO)                                
              MOVE SPACES     TO WS-BLKINFO-PRINTED-SW                          
                                 WS-REMARKS-PRINTED-SW                          
              MOVE ZEROES     TO WS-SUB                                         
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4200-PARSE-STRING.                                                       
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 4300-GET-CHAR          THRU 4300-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------          
       4300-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-LINK-BM06CCFL.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE F999-NUM-HDB-REF  TO COM31-NUM-SCH-ACCNT.                       
           CALL 'BM06CCFL' USING BM06COM31-REC.                                 
                                                                                
           IF COM31-CDE-RETURN NOT = SPACES AND LOW-VALUES                      
              MOVE ZEROES  TO  L29L-SELLING-PRICE-99                            
                               L29L-SELLING-PRICE-45                            
                               L29L-SELLING-PRICE-40                            
                               L29L-SELLING-PRICE-35                            
                               L29L-SELLING-PRICE-30                            
                               L29L-SELLING-PRICE-25                            
                               L29L-SELLING-PRICE-20                            
                               L29L-SELLING-PRICE-15                            
           ELSE                                                                 
              IF COM31-AMT-SELL-PRICE-OFFER-99 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-99               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-45 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-45               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-40 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-40               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-35 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-35               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-30 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-30               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-25 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-25               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-20 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-20               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-15 NOT NUMERIC                      
                 MOVE ZEROES    TO  COM31-AMT-SELL-PRICE-OFFER-15               
              END-IF                                                            
              IF COM31-AMT-SELL-PRICE-OFFER-15 = ZEROES AND                     
                 COM31-AMT-SELL-PRICE-OFFER-20 = ZEROES AND                     
                 COM31-AMT-SELL-PRICE-OFFER-25 = ZEROES AND                     
                 COM31-AMT-SELL-PRICE-OFFER-30 = ZEROES AND                     
                 COM31-AMT-SELL-PRICE-OFFER-35 = ZEROES AND                     
                 COM31-AMT-SELL-PRICE-OFFER-40 = ZEROES AND                     
                 COM31-AMT-SELL-PRICE-OFFER-45 = ZEROES AND                     
                 COM31-AMT-SELL-PRICE-OFFER-99 = ZEROES                         
                 MOVE F999-AMT-SELLING-PRICE                                    
                                  TO L29L-SELLING-PRICE-99                      
                 MOVE '   -   '   TO L29L-SELLING-45-R                          
                 MOVE '   -   '   TO L29L-SELLING-40-R                          
                 MOVE '   -   '   TO L29L-SELLING-35-R                          
                 MOVE '   -   '   TO L29L-SELLING-30-R                          
                 MOVE '   -   '   TO L29L-SELLING-25-R                          
                 MOVE '   -   '   TO L29L-SELLING-20-R                          
                 MOVE '   -   '   TO L29L-SELLING-15-R                          
              ELSE                                                              
                 IF COM31-AMT-SELL-PRICE-OFFER-99 =  SPACES OR ZEROES OR        
                                                     99999999                   
                    MOVE '   -   '   TO L29L-SELLING-99-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-99                          
                                   TO L29L-SELLING-PRICE-99                     
                 END-IF                                                         
                 IF (COM31-AMT-SELL-PRICE-OFFER-45 =                            
                                    SPACES OR ZEROES OR 99999999) OR            
                    (F999-NUM-FLAT-TYPE = '1F')                                 
                    MOVE '   -   '   TO L29L-SELLING-45-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-45                          
                                     TO L29L-SELLING-PRICE-45                   
                 END-IF                                                         
                 IF (COM31-AMT-SELL-PRICE-OFFER-40 =                            
                                    SPACES OR ZEROES OR 99999999) OR            
                    (F999-NUM-FLAT-TYPE = '1F')                                 
                    MOVE '   -   '   TO L29L-SELLING-40-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-40                          
                                     TO L29L-SELLING-PRICE-40                   
                 END-IF                                                         
                 IF COM31-AMT-SELL-PRICE-OFFER-35 =  SPACES OR ZEROES OR        
                                                     99999999                   
                    MOVE '   -   '   TO L29L-SELLING-35-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-35                          
                                     TO L29L-SELLING-PRICE-35                   
                 END-IF                                                         
                 IF COM31-AMT-SELL-PRICE-OFFER-30 =  SPACES OR ZEROES OR        
                                                     99999999                   
                    MOVE '   -   '   TO L29L-SELLING-30-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-30                          
                                     TO L29L-SELLING-PRICE-30                   
                 END-IF                                                         
                 IF COM31-AMT-SELL-PRICE-OFFER-25 =  SPACES OR ZEROES OR        
                                                     99999999                   
                    MOVE '   -   '   TO L29L-SELLING-25-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-25                          
                                     TO L29L-SELLING-PRICE-25                   
                 END-IF                                                         
                 IF COM31-AMT-SELL-PRICE-OFFER-20 =  SPACES OR ZEROES OR        
                                                     99999999                   
                    MOVE '   -   '   TO L29L-SELLING-20-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-20                          
                                     TO L29L-SELLING-PRICE-20                   
                 END-IF                                                         
                 IF COM31-AMT-SELL-PRICE-OFFER-15 =  SPACES OR ZEROES OR        
                                                     99999999                   
                    MOVE '   -   '   TO L29L-SELLING-15-R                       
                 ELSE                                                           
                    MOVE COM31-AMT-SELL-PRICE-OFFER-15                          
                                     TO L29L-SELLING-PRICE-15                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP1329L'.                                    
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F999 RECORDS READ       : ' WS-F999-READ-CTR.           
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-PRT-CTR.                 
                                                                                
           CALL 'C13K813A'         USING WS-RETURN-CODE.                        
                                                                                
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSE ERROR, BP13K813 STATUS ' WS-RETURN-CODE            
           END-IF.                                                              
                                                                                
           MOVE WS-RETURN-CODE         TO WS-C290-RETURN-CODE.                  
                                                                                
           CLOSE BP13F999                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13L29L.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR, WS-K110-STATUS ' WS-K110-STATUS             
              MOVE WS-K110-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR, WS-K100-STATUS ' WS-K100-STATUS             
              MOVE WS-K100-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
