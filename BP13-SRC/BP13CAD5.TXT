       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CAD5.                                                 
      *AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
      *DATE-WRITTEN.  06/10/2023.                                               
      * ========================================================== *            
      *    THIS PROGRAM WILL READ FECA FILE, MATCH WITH K800 AND   *            
      *         OUTPUT TO BP13F800.                                *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13T.FECA.HFE.MAYBTO (LEN:500)        *            
      *              :  2.) BP13K800                               *            
      * OUTPUT FILE  :  3.) BP13F800                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP139365 KAC1 06/10/2023 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FECA  ASSIGN        TO BP13FECA.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FECA                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13FECA.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000  CHARACTERS.                                    
           COPY BP13K800.                                                       
                                                                                
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F800.                                                       
                                                                                
      *FD  BP13F002                                                             
      *    RECORDING MODE IS F                                                  
      *    RECORD CONTAINS 60 CHARACTERS.                                       
      *01  BP13F002-REC.                                                        
      *    05 WS-F002-REGN                 PIC X(08).                           
      *    05 FILLER                       PIC X(22).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                      PIC 9(2)   VALUE ZEROES.               
           05  WS-CNT-FECA-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K800-MATCH         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F800-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FECA-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
      *01  WS-OTHERS.                                                           
      *    05  WS-FILLER                 PIC X(8)   VALUE SPACES.               
      *    05  WS-NUM-HLA                PIC X(9)   VALUE SPACES.               
      *                                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13FECA-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FECA-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FECA                                                 
                       BP13K800                                                 
                OUTPUT BP13F800.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS        TO   RETURN-CODE                   
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-BP13FECA-READ.                                                      
      *=============================================================            
           READ BP13FECA                                                        
              AT END MOVE 'Y'                TO WS-FECA-EOF                     
                 GO TO 2000-EXIT.                                               
                                                                                
           ADD 1                             TO WS-CNT-FECA-READ.               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
           MOVE 'N'                          TO WS-K800-FND.                    
           MOVE FECA-NUM-REGN                TO K800-NUM-REGN.                  
           PERFORM 3100-READ-BP13K800      THRU 3100-EXIT.                      
                                                                                
           IF WS-K800-FND = 'Y'                                                 
             MOVE SPACES                     TO BP13F800-MASTER                 
             MOVE BP13K800-MASTER            TO BP13F800-MASTER                 
             MOVE FECA-HFE-CASE-STATUS       TO F800-NUM-HLA-STATUS             
             WRITE BP13F800-MASTER                                              
             ADD 1                           TO WS-CNT-F800-WRITE               
           ELSE                                                                 
             DISPLAY 'K800 NOT FOUND KEY: ' K800-NUM-REGN                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13FECA-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K800.                                                      
      *=============================================================            
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                     TO WS-K800-FND                 
                    ADD   1                      TO WS-CNT-K800-MATCH           
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY ' '                                                 
                    DISPLAY '**BP13K800 - READ FILE ERROR, KEY:'                
                                                  K800-NUM-REGN                 
                    DISPLAY ' '                                                 
                    MOVE WS-K800-STATUS          TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FECA                                                       
                 BP13K800                                                       
                 BP13F800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE WS-K800-STATUS          TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13CAD5 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FECA READ      : ' WS-CNT-FECA-READ.          
           DISPLAY ' 2. NO OF BP13K800 MATCH     : ' WS-CNT-K800-MATCH.         
           DISPLAY ' 3. NO OF BP13F800 WRITTEN   : ' WS-CNT-F800-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
       9000-EXIT.                                                               
           EXIT.                                                                
