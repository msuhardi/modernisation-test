       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH06.                                                 
      *AUTHOR.        MICHAEL ARRIOLA.                                          
      *DATE-WRITTEN.  04/07/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                             
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO EXTRACT....BP13F268                         *            
      *             MATCH BY SYSTEM DATE.                          *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133367  MSD1  20080704  NEW PGM                          *            
      * BP135037  IMC1  20130912  MOVE BF BOOK STATUS              *            
      * BP136467  FNP1  20161020  DO NOT BLANK OFF ROOM-TYPE       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F268  ASSIGN        TO BP13F268.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT P13F268A  ASSIGN        TO P13F268A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F268                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F268.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13F268A                                                            
            RECORDING MODE IS F.                                                
       01 P13F268A-REC            PIC X(200).                                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F268-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F268-WRITE        PIC 9(08) VALUE ZEROES.                       
          05 WS-REJECTED          PIC 9(08) VALUE ZEROES.                       
          05 WS-NOT-TODAY         PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-DTE-ACCEPT        PIC X(8).                                     
          05 WS-ACCEPT-FMT REDEFINES WS-DTE-ACCEPT.                             
             10 WS-DTE-ACCEPT-FMT PIC 9(8).                                     
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F268-KEY-FLD = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F268                                                 
                       SY02F001                                                 
               OUTPUT  P13F268A.                                                
                                                                                
           READ SY02F001.                                                       
           PERFORM 2000-READ-BP13F268  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F268.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F268 AT END                                                 
                MOVE HIGH-VALUES TO F268-KEY-FLD                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F268-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F268-DTE-ACCEPT         TO WS-DTE-ACCEPT.                       
                                                                                
           IF WS-DTE-ACCEPT-FMT = F001-DTE-CURRENT                              
              IF F268-NUM-REJECT = 'Y'                                          
                 ADD  1   TO WS-REJECTED                                        
                                                                                
              ELSE                                                              
                 MOVE  'Y'            TO F268-NUM-BOOK                          
                 MOVE  'BF'           TO F268-NUM-BOOK-STATUS                   
      *          MOVE SPACES          TO F268-NUM-FT(2:1)                       
                 WRITE P13F268A-REC   FROM BP13F268-REC                         
                 ADD  1   TO WS-F268-WRITE                                      
              END-IF                                                            
                                                                                
           ELSE                                                                 
              ADD  1   TO WS-NOT-TODAY                                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F268 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH06 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F268 : ' WS-F268-READ.            
           DISPLAY 'NO OF RECS WRITTEN    P13F268A : ' WS-F268-WRITE.           
           DISPLAY 'NO OF RECS NOT WRITE  BP13F268 : ' WS-NOT-TODAY.            
           DISPLAY 'NO OF RECS REJECTED   BP13F268 : ' WS-REJECTED.             
                                                                                
           CLOSE BP13F268                                                       
                 P13F268A                                                       
                 SY02F001.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
