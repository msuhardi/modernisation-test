       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C517.                                                 
      *AUTHOR.        ARIEL HERNANDEZ.                                          
      *DATE-WRITTEN.  17/09/2007.                                               
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE:  TO UPDATE NT,FT AND ROLE ON BP13K820.   *                
      *                                                        *                
      *    INPUT FILE  : BP13F716                              *                
      *    I-O   FILE  : BP13K820                              *                
      *========================================================*                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP133145 BA3  170907 NEW PROGRAM                       *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13F716   ASSIGN       TO BP13F716.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
                                                                                
       COPY BP13K820.                                                           
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F716.                                                           
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-F716-READ         PIC 9(08)    VALUE ZEROES.                   
           05 WS-K820-STATUS       PIC 9(2)     VALUE ZERO.                     
           05 CNT-K820-FOUND       PIC 9(8)     VALUE ZERO.                     
           05 CNT-K820-UPDATE      PIC 9(8)     VALUE ZERO.                     
           05 CNT-K820-NOTFOUND    PIC 9(8)     VALUE ZERO.                     
           05 WS-K820-FD           PIC X(1)     VALUE SPACES.                   
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YY                PIC 9(02).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
       01  WS-CURR-DATE            PIC X(10).                                   
                                                                                
      *================================================================*        
       PROCEDURE DIVISION.                                                      
      *================================================================*        
                                                                                
      *-----------*                                                             
       0000-CONTROL.                                                            
      *-----------*                                                             
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F716    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL F716-NUM-REGN = HIGH-VALUES.                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       1000-OPEN-FILES.                                                         
      *--------------*                                                          
           OPEN INPUT  BP13F716                                                 
                  I-O  BP13K820.                                                
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13K820-STATUS ' WS-K820-STATUS         
               MOVE     WS-K820-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE.                           
                                                                                
           STRING WS-DD '/'                                                     
                  WS-MM '/'                                                     
                  WS-CURR-CC                                                    
                  WS-YY DELIMITED BY SIZE INTO WS-CURR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-F716.                                                          
      *-----------------*                                                       
           READ BP13F716 AT END                                                 
                MOVE HIGH-VALUES TO F716-NUM-REGN                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F716-READ.                                               
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           MOVE 'N'                        TO WS-K820-FD.                       
           MOVE SPACES                     TO K820-KEY-FLD.                     
           MOVE F716-NUM-REGN              TO K820-NUM-REGN.                    
           MOVE F716-NUM-NRIC              TO K820-NUM-NRIC.                    
           PERFORM 4000-READ-K820        THRU 4000-EXIT                         
           IF WS-K820-FD = 'Y'                                                  
              PERFORM 5000-MOVE-DATA     THRU 5000-EXIT                         
              PERFORM 6000-UPDATE-K820   THRU 6000-EXIT                         
           END-IF.                                                              
           PERFORM 2000-READ-F716        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-READ-K820.                                                          
      *-----------------*                                                       
           READ BP13K820                                                        
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-K820-FD                                         
                 ADD   1  TO CNT-K820-FOUND                                     
              WHEN 23                                                           
                 MOVE 'N' TO WS-K820-FD                                         
                 ADD   1  TO CNT-K820-NOTFOUND                                  
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13K820 - STATUS '                  
                             WS-K820-STATUS                                     
                 MOVE        WS-K820-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-MOVE-DATA.                                                          
      *-----------------*                                                       
           MOVE F716-CDE-NT-RESIDE         TO K820-NUM-EXIST-NT.                
           MOVE F716-CDE-FLAT-TYPE-RESIDE  TO K820-NUM-EXIST-FT.                
           MOVE F716-NUM-ROLE              TO K820-NUM-EXIST-ROLE.              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       6000-UPDATE-K820.                                                        
      *-----------------*                                                       
           REWRITE BP13K820-REC.                                                
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13K820 - STATUS '                 
                             WS-K820-STATUS                                     
                 MOVE        WS-K820-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO CNT-K820-UPDATE.                                            
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-ROUTINE.                                                      
      *-----------------*                                                       
                                                                                
           DISPLAY '         PROGRAM-ID :  BP13C517'.                           
           DISPLAY '         ~~~~~~~~~~             '.                          
           DISPLAY '       CURRENT-DATE : ' WS-CURR-DATE.                       
           DISPLAY ' '.                                                         
           DISPLAY '1. NO OF RECS READ FROM F716  : ' WS-F716-READ.             
           DISPLAY '2. NO OF RECS FOUND (K820)    : ' CNT-K820-FOUND.           
           DISPLAY '3. NO OF RECS NOT FOUND (K820): ' CNT-K820-NOTFOUND.        
           DISPLAY '4. NO OF RECS UPDATED (K820)  : ' CNT-K820-UPDATE.          
           DISPLAY '  '                                                         
                                                                                
           CLOSE BP13F716                                                       
                 BP13K820.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13K820-STATUS ' WS-K820-STATUS.         
              MOVE WS-K820-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------- END OF PROGRAM  ---------------------*              
                                                                                
                                                                                
