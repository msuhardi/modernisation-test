       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C460.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  27 DEC 2004.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *  OBJECTIVES : SEND EMAIL ALERT TO CSM FOR CASES WITH 1 MTH  *           
      *               FROM 2ND PAYMENT DUE DATE                     *           
      *                                                             *           
      *  INPUT FILES  - BP13F326                                    *           
      *  OUTPUT FILES - BP13MAIL                                    *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY    DATE       DESCRIPTION                       *           
      * -------- ----  ---------- -----------                       *           
      * BP132605 JF4   04/02/2004 NEW PROGRAM                       *           
      * BP132907 SSS2  15/09/2006 REMOVE EXTRA LINE FOR EMAIL       *           
      * BP135537 RJB1  15/10/2015 CHANGE THE EMAIL RECIPIENT FROM   *           
      *                           NICKNAME INTO COMPLETE NAME       *           
      * BP139030 ZAR7  07/01/2022 REPLACE CHUA YEE MIEN TO LIM KOK CHUN         
      * BP139276 KAC1  05/04/2024 REPLACE LIM KOK CHUN BY           *           
      *                                   KATHIJAH_BEE_ALI_MOHAMED  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F326  ASSIGN        TO BP13F326.                          
                                                                                
           SELECT BP13MAIL  ASSIGN        TO BP13MAIL.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F326                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  100 CHARACTERS.                                     
       01  BP13F326-REC.                                                        
           05  F326-NUM-REGN          PIC X(08).                                
           05  F326-NUM-OIC           PIC X(05).                                
           05  F326-NUM-2ND-PYMT      PIC 9(02).                                
           05  F326-DTE-SDS-DUE       PIC X(08).                                
           05  FILLER                 PIC X(77).                                
                                                                                
       FD  BP13MAIL                                                             
           RECORDING  MODE  IS F                                                
           RECORD CONTAINS   80 CHARACTERS.                                     
       01  MAIL-PRTREC                PIC X(80).                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  BP13F326-EOF-TAG        PIC X(01).                               
               88  BP13F326-EOF        VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F326-READ-CNT        PIC 9(06)     VALUE ZEROES.              
           05  WS-MAIL-SEND-CNT        PIC 9(06)     VALUE ZEROES.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-DATE                 PIC X(08)     VALUE SPACES.              
           05  WS-EDIT-DATE            PIC X(10)     VALUE SPACES.              
           05  WS-NUM-OIC              PIC X(05)     VALUE SPACES.              
           05  WS-SNO                  PIC 9(02)     VALUE ZEROES.              
           05  WS-EDIT-CNT             PIC Z(05)9.                              
                                                                                
      *------------------------------------------------------------*            
      * EMAIL LAYOUT                                               *            
      *------------------------------------------------------------*            
       01  MAIL-HEADER-1.                                                       
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-2.                                                       
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  SEND-ID         PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-3.                                                       
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  TO-MAIL-ID-A    PIC X(31)  VALUE SPACES.                         
           05  FILLER          PIC X(40)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-3A.                                                      
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(24)  VALUE                                 
                               'KATHIJAH_BEE_ALI_MOHAMED'.                      
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-HEADER-4.                                                       
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-5.                                                       
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  FROM-MAIL-ID    PIC X(10)  VALUE                                 
               'SOC System'.                                                    
           05  FILLER          PIC X(65)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-6.                                                       
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID      PIC X(31)  VALUE SPACES.                         
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-7.                                                       
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID      PIC X(24)  VALUE                                 
                              'KATHIJAH_BEE_ALI_MOHAMED'.                       
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(51)  VALUE SPACES.                         
                                                                                
       01  MAIL-HEADER-8.                                                       
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  SUBJECT-1       PIC X(15)  VALUE                                 
               'SDS 2nd Payment'.                                               
           05  FILLER          PIC X(55)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY-TXT-1.                                                     
           05 FILLER          PIC X(31)  VALUE                                  
              'Attn : Customer Service Manager'.                                
           05 FILLER          PIC X(49)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-TXT-2.                                                     
           05 FILLER          PIC X(08)  VALUE                                  
              'Dear CSM'.                                                       
           05 FILLER          PIC X(72)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-TXT-3.                                                     
           05 FILLER          PIC X(40)  VALUE                                  
              'List of Regn No(s). where topup for SDS '.                       
           05 FILLER          PIC X(17)  VALUE                                  
              '2nd Payment due :'.                                              
           05 FILLER          PIC X(23)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-TXT-4.                                                     
           05 FILLER          PIC X(48)  VALUE                                  
              'S/No    Regn No      2nd Payment(%)     Due Date'.               
           05 FILLER          PIC X(32)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-TXT-5.                                                     
           05 FILLER          PIC X(48)  VALUE                                  
              '----    -------      --------------     --------'.               
           05 FILLER          PIC X(32)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-DTL-1.                                                     
           05 BODY-SNO        PIC Z(04)  VALUE SPACES.                          
           05 FILLER          PIC X(04)  VALUE SPACES.                          
           05 BODY-REGNO      PIC X(08)  VALUE SPACES.                          
           05 FILLER          PIC X(10)  VALUE SPACES.                          
           05 BODY-2ND-PYMT   PIC Z(02)  VALUE SPACES.                          
           05 FILLER          PIC X(12)  VALUE SPACES.                          
           05 BODY-DUE-DATE   PIC X(10)  VALUE SPACES.                          
           05 FILLER          PIC X(30)  VALUE SPACES.                          
                                                                                
       01  MAIL-BODY-BLANK-LINE.                                                
           05 FILLER          PIC X(80)  VALUE SPACES.                          
                                                                                
       01  L002-FOOTER-1.                                                       
           05 FILLER          PIC X(31)  VALUE                                  
              'Agreement Team                 '.                                
           05 FILLER          PIC X(49)  VALUE SPACES.                          
                                                                                
       01  L002-FOOTER-2.                                                       
           05 FILLER          PIC X(30)  VALUE                                  
              'Sales Operations Unit         '.                                 
           05 FILLER          PIC X(59)  VALUE SPACES.                          
                                                                                
       01  L002-FOOTER-3.                                                       
           05 FILLER          PIC X(28)  VALUE                                  
              'Estate Admin & Property Dept'.                                   
           05 FILLER          PIC X(52)  VALUE SPACES.                          
                                                                                
       01  L002-FOOTER-END.                                                     
           05 FILLER          PIC X      VALUE '.'.                             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F326     THRU 2000-EXIT.                       
           PERFORM 3000-START-PROCESS     THRU 3000-EXIT                        
                                          UNTIL BP13F326-EOF.                   
           PERFORM 6000-END-PROCESS       THRU 6000-EXIT.                       
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F326                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DATE.                        
           STRING WS-DATE(7:2) '/'                                              
                  WS-DATE(5:2) '/'                                              
                  WS-DATE(1:4)                                                  
                  DELIMITED BY SIZE        INTO WS-EDIT-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F326.                                                      
      *-------------------------------------------------------------            
           READ BP13F326 AT END                                                 
                MOVE    'Y'       TO   BP13F326-EOF-TAG                         
                NOT AT END                                                      
                ADD      1        TO   WS-F326-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-START-PROCESS.                                                      
      *-------------------------------------------------------------            
           IF F326-NUM-OIC NOT = WS-NUM-OIC                                     
              IF WS-F326-READ-CNT NOT = 1                                       
                 PERFORM 4500-SETUP-FOOTER   THRU 4500-EXIT                     
              END-IF                                                            
                                                                                
              MOVE F326-NUM-OIC              TO   WS-NUM-OIC                    
              MOVE ZEROES                    TO   WS-SNO                        
              PERFORM 4100-SETUP-HEADER      THRU 4100-EXIT                     
              PERFORM 4300-SETUP-DETAIL      THRU 4300-EXIT                     
              ADD  1                         TO   WS-MAIL-SEND-CNT              
           ELSE                                                                 
              PERFORM 4300-SETUP-DETAIL      THRU 4300-EXIT                     
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F326        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-SETUP-HEADER.                                                       
      *-------------------------------------------------------------            
           STRING F326-NUM-OIC   DELIMITED BY SPACES                            
                  '@HDB.GOV.SG>' DELIMITED BY SIZE                              
                                   INTO TO-MAIL-ID                              
           END-STRING.                                                          
           MOVE TO-MAIL-ID         TO   TO-MAIL-ID-A.                           
                                                                                
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-1.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-2.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-3A.                         
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-4.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-5.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-6.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-7.                          
           WRITE MAIL-PRTREC       FROM MAIL-HEADER-8.                          
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-1.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-2.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-3.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-BLANK-LINE.                   
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-4.                        
           WRITE MAIL-PRTREC       FROM MAIL-BODY-TXT-5.                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4300-SETUP-DETAIL.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO   MAIL-BODY-DTL-1.                      
           ADD 1                     TO   WS-SNO.                               
           MOVE WS-SNO               TO   BODY-SNO.                             
           MOVE F326-NUM-REGN        TO   BODY-REGNO.                           
           MOVE F326-NUM-2ND-PYMT    TO   BODY-2ND-PYMT.                        
                                                                                
           STRING F326-DTE-SDS-DUE(7:2) '/'                                     
                  F326-DTE-SDS-DUE(5:2) '/'                                     
                  F326-DTE-SDS-DUE(1:4)                                         
                  DELIMITED BY SIZE  INTO BODY-DUE-DATE                         
           END-STRING.                                                          
                                                                                
           WRITE MAIL-PRTREC         FROM MAIL-BODY-DTL-1.                      
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4500-SETUP-FOOTER.                                                       
      *-------------------------------------------------------------            
           WRITE MAIL-PRTREC         FROM MAIL-BODY-BLANK-LINE.                 
           WRITE MAIL-PRTREC         FROM MAIL-BODY-BLANK-LINE.                 
                                                                                
           WRITE MAIL-PRTREC         FROM L002-FOOTER-1.                        
           WRITE MAIL-PRTREC         FROM L002-FOOTER-2.                        
           WRITE MAIL-PRTREC         FROM L002-FOOTER-3.                        
                                                                                
           WRITE MAIL-PRTREC         FROM L002-FOOTER-END.                      
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-END-PROCESS.                                                        
      *-------------------------------------------------------------            
           IF WS-MAIL-SEND-CNT > 0                                              
              PERFORM 4500-SETUP-FOOTER    THRU 4500-EXIT                       
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' BP13C460 CONTROL LIST    DATE : ' WS-EDIT-DATE.            
           DISPLAY '*------------------------------------------*'.              
           DISPLAY ' '.                                                         
           MOVE WS-F326-READ-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES READ FROM BP13F326    : ' WS-EDIT-CNT.                
           MOVE WS-MAIL-SEND-CNT     TO WS-EDIT-CNT.                            
           DISPLAY 'CASES WRITTEN TO EMAIL FILE : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           DISPLAY '*------------------------------------------*'.              
           DISPLAY ' '.                                                         
                                                                                
           CLOSE  BP13F326                                                      
                  BP13MAIL.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
