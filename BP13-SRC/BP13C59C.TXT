      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C59C.                                                 
      *AUTHOR.        ZAR7.                                                     
      *DATE-WRITTEN.  AUG 8 2021.                                               
                                                                                
      ******************************************************************        
      * OBJECTIVE  - TO IDENTIFY CASES TO SEND TO CPF BOARD FOR        *        
      *              WITHDRAWAL                                        *        
      ******************************************************************        
      *REF NO   DATE      BY   AMENDMENTS/ENHANCEMENTS                 *        
      *-------  ----     ---   ----------------------                  *        
      *BP138702 04/08/21 ZAR7   GENERATE ADHOC REPORT                  *        
      *BP138702 04/08/21 ZAR7   RESTAGING                              *        
      ******************************************************************        
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
      *-----------------------*                                                 
                                                                                
           SELECT BP13F600 ASSIGN       TO BP13F600.                            
                                                                                
           SELECT BP13OUT1 ASSIGN       TO BP13OUT1.                            
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
       FD  BP13OUT1                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 74 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       01  BP13OUT1-REC.                                                        
           05  OUT1-HDB-REF   PIC X(11).                                        
           05  OUT1-FILLER    PIC X.                                            
           05  OUT1-ADDRESS.                                                    
               10 OUT1-BLK    PIC X(10).                                        
               10 OUT1-ST     PIC X(32).                                        
               10 OUT1-HS-LV  PIC X(03).                                        
               10 OUT1-HS-UN  PIC X(05).                                        
               10 OUT1-POST   PIC X(06).                                        
           05  OUT1-ST-CDE    PIC X(06).                                        
                                                                                
       FD  BP13F600                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 1800 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       COPY BP13K600.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-F600-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-BP13C913                   PIC X(08) VALUE 'BP13C913'.            
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F600-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-F860-WRT            PIC 9(07) VALUE 0.                     
                                                                                
      *=======================================================*                 
      *    LINKAGE VARIABLES                                  *                 
      *=======================================================*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *************                                                             
       0000-CONTROL.                                                            
      *************                                                             
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
             UNTIL WS-F600-EOF = 'Y'.                                           
           PERFORM 9999-WRAP-UP          THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***************                                                           
       1000-START-UP.                                                           
      ***************                                                           
           OPEN INPUT  BP13F600                                                 
                OUTPUT BP13OUT1.                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *******************                                                       
       2000-MAIN-ROUTINE.                                                       
      *******************                                                       
      *---------------------------------------------------------------*         
      * READS BP13K600 AND PROCESS EACH RECORD UNTIL END OF FILE      *         
      *---------------------------------------------------------------*         
           READ BP13F600 AT END                                                 
                MOVE 'Y' TO WS-F600-EOF                                         
                GO       TO 2000-EXIT                                           
                END-READ.                                                       
                                                                                
           ADD   1            TO WS-CNT-F600-READ.                              
                                                                                
           MOVE K600-NUM-SCH-ACCNT      TO WS-LINK-REC.                         
           PERFORM 5400-GET-CDE-STREET  THRU 5400-EXIT                          
                                                                                
           PERFORM 3000-WRITE-BP13OUT1  THRU 3000-EXIT.                         
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
                                                                                
      *********************                                                     
       3000-WRITE-BP13OUT1.                                                     
      *********************                                                     
           PERFORM 4000-MOVE-VALUES  THRU 4000-EXIT.                            
                                                                                
           WRITE BP13OUT1-REC.                                                  
                                                                                
           ADD  1 TO  WS-CNT-F860-WRT.                                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
                                                                                
      *****************                                                         
       4000-MOVE-VALUES.                                                        
      *****************                                                         
           MOVE SPACES                   TO  BP13OUT1-REC.                      
                                                                                
           MOVE K600-NUM-HDB-REF         TO  OUT1-HDB-REF.                      
           MOVE ' '                      TO  OUT1-FILLER.                       
           MOVE K600-NUM-BLK             TO  OUT1-BLK.                          
           MOVE K600-NME-STREET          TO  OUT1-ST.                           
           MOVE K600-NUM-HSE-LVL         TO  OUT1-HS-LV.                        
           MOVE K600-NUM-HSE-UNIT(1:5)   TO  OUT1-HS-UN.                        
           MOVE K600-CDE-POST-CDE        TO  OUT1-POST.                         
           MOVE COMM8-CDE-ST             TO  OUT1-ST-CDE.                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
                                                                                
      **********************                                                    
       5400-GET-CDE-STREET.                                                     
      **********************                                                    
           CALL WS-BP13C913 USING                                               
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **************                                                            
       9999-WRAP-UP.                                                            
      **************                                                            
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13ADH1'.                                     
           DISPLAY '*********************************'.                         
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F600 RECS READ     = ' WS-CNT-F600-READ.          
           DISPLAY 'TOTAL AB02F860 RECS WRITTEN  = ' WS-CNT-F860-WRT.           
                                                                                
           CLOSE BP13F600.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
