       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB09.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN : 02 MAY 2007.                                              
      *===============================================================*         
      *  SYSTEM OF COMMITMENT (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE   : READ BP13F203 AND CHECK FROM BP13K800 IF       *         
      *                K800-NUM-STATUS = 'C', BYPASS RECORD.          *         
      *                                                               *         
      *  INPUT : BP13F203                                             *         
      *          BP13K764                                             *         
      *          BP13K800                                             *         
      *---------------------------------------------------------------*         
      *  CHG-NO   BY    DATE   DESCRIPTION                            *         
      * -------- ---- -------- ---------------------------------------*         
      * BP133062 SD10 20070502 NEW PROGRAM                            *         
      *          SD10 20070507 READ BP13K764 TO CHECK FOR DTE-BALLOT  *         
      *          SD10 20070605 DISPLAY REGN NO OF CANCELLED CASES     *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
           SELECT P13F203A ASSIGN TO P13F203A.                                  
                                                                                
           SELECT BP13K764 ASSIGN       TO BP13K764                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K764-KEY-FLD                         
                           FILE STATUS  IS K764-STATUS.                         
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  BP13F203                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 200 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F203.                                                           
                                                                                
       FD  P13F203A                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 200 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       01  P13F203A-REC            PIC X(200).                                  
                                                                                
       FD   BP13K764.                                                           
       COPY BP13K764.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-F203-READ            PIC 9(5)     VALUE ZEROS.                
           05  WS-F203-WRITE           PIC 9(5)     VALUE ZEROS.                
           05  WS-K764-BYPASS          PIC 9(5)     VALUE ZEROS.                
           05  WS-K800-BYPASS          PIC 9(5)     VALUE ZEROS.                
           05  WS-K764-NOTFND          PIC 9(5)     VALUE ZEROS.                
           05  WS-K800-NOTFND          PIC 9(5)     VALUE ZEROS.                
           05  WS-K764-READ            PIC 9(5)     VALUE ZEROS.                
           05  WS-K800-READ            PIC 9(5)     VALUE ZEROS.                
           05  K764-STATUS             PIC 9(2)     VALUE ZEROS.                
           05  K800-STATUS             PIC 9(2)     VALUE ZEROS.                
           05  WS-EOF-F203             PIC X(3).                                
               88  EOF-OF-F203                      VALUE 'YES'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ***********************                                                   
       000-MAIN-LOGIC.                                                          
      ***********************                                                   
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 300-READ-F203      THRU 300-EXIT.                            
           PERFORM 400-PROCESS        THRU 400-EXIT                             
                   UNTIL EOF-OF-F203.                                           
           PERFORM 900-FINALIZATION   THRU 900-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************                                                   
       100-INITIALIZATION.                                                      
      ***********************                                                   
           OPEN INPUT  BP13F203                                                 
                       BP13K764                                                 
                       BP13K800                                                 
                OUTPUT P13F203A.                                                
           MOVE 'NO' TO WS-EOF-F203.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************                                                   
       300-READ-F203.                                                           
      ***********************                                                   
           READ BP13F203                                                        
             AT END MOVE 'YES' TO WS-EOF-F203                                   
                    MOVE HIGH-VALUE TO F203-KEY-FLD                             
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F203-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************                                                   
       400-PROCESS.                                                             
      ***********************                                                   
           PERFORM 500-READ-BP13K800  THRU 500-EXIT.                            
           PERFORM 300-READ-F203      THRU 300-EXIT.                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************                                                   
       500-READ-BP13K800.                                                       
      ***********************                                                   
           MOVE F203-NUM-REGN      TO K800-NUM-REGN.                            
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = ZEROS                                               
              ADD 1 TO WS-K800-READ                                             
              IF K800-NUM-STATUS = 'C'                                          
                 ADD  1 TO WS-K800-BYPASS                                       
                 DISPLAY 'CANCELLED CASE: ' K800-NUM-REGN                       
              ELSE                                                              
                 PERFORM 600-READ-BP13K764   THRU 600-EXIT                      
              END-IF                                                            
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 ADD  1 TO WS-K800-NOTFND                                       
                 DISPLAY 'RECORD NOTFND IN BP13K800 : KEY IS '                  
                          K800-NUM-REGN                                         
              ELSE                                                              
                 MOVE K800-STATUS   TO RETURN-CODE                              
                 DISPLAY 'ERROR READING BP13K800, STATUS = '                    
                         K800-STATUS ' KEY IS ' K800-NUM-REGN                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
            EXIT.                                                               
                                                                                
      ***********************                                                   
       600-READ-BP13K764.                                                       
      ***********************                                                   
           MOVE F203-NUM-ALLO-MODE   TO K764-NUM-SALES-MODE.                    
           READ BP13K764.                                                       
                                                                                
           IF K764-STATUS = ZEROS                                               
              ADD 1 TO WS-K764-READ                                             
              IF K764-DTE-BALLOT = F203-DTE-BALLOT                              
                 MOVE BP13F203-REC   TO P13F203A-REC                            
                 WRITE P13F203A-REC                                             
                 ADD  1 TO WS-F203-WRITE                                        
              ELSE                                                              
                 ADD  1 TO WS-K764-BYPASS                                       
              END-IF                                                            
           ELSE                                                                 
              IF K764-STATUS = 23                                               
                 ADD  1 TO WS-K764-NOTFND                                       
                 DISPLAY 'RECORD NOTFND IN BP13K764 : KEY IS '                  
                          K764-NUM-SALES-MODE                                   
              ELSE                                                              
                 MOVE K764-STATUS   TO RETURN-CODE                              
                 DISPLAY 'ERROR READING BP13K764, STATUS = '                    
                         K764-STATUS ' KEY IS ' K764-NUM-SALES-MODE             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
            EXIT.                                                               
                                                                                
      ***********************                                                   
       900-FINALIZATION.                                                        
      ***********************                                                   
             DISPLAY '*-------- BP13CB09 -----------*'.                         
             DISPLAY ' F203  RECS READ        : ' WS-F203-READ.                 
             DISPLAY ' K800  RECS READ        : ' WS-K800-READ.                 
             DISPLAY ' K800  RECS NOTFND      : ' WS-K800-NOTFND.               
             DISPLAY ' K764  RECS READ        : ' WS-K764-READ.                 
             DISPLAY ' K764  RECS NOTFND      : ' WS-K764-NOTFND.               
             DISPLAY ' F203A RECS WRITTEN     : ' WS-F203-WRITE.                
             DISPLAY ' F203  RECS BYPASSED    : '.                              
             DISPLAY '   1) NUM-STATUS = "C"  : ' WS-K800-BYPASS.               
             DISPLAY '   2) SYS-DTE > RSLT-DTE: ' WS-K764-BYPASS.               
                                                                                
           CLOSE      BP13F203                                                  
                      BP13K764                                                  
                      BP13K800                                                  
                      P13F203A.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
