      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CC27.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  17/07/06.                                                 
      *****************************************************************         
      * OBJECTIVE - MONTHLY NEW DBSS CASE REPORT                                
      *                                                                         
      *     INPUT FILE :                                                        
      *                  1. BP13FD40                                            
      *                  2. BP13K800                                            
      *                  3. BP13K820                                            
      *                  4. BP13KD00                                            
      *                  5. SY02F001                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. NIL                                                 
      *                                                                         
      *     LISTING:                                                            
      *                  1. BP13LC27 (REPORT)                                   
      *                                                                         
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP132888  17072006  BA2  NEW PROGRAM.                                   
      * BP132973  02022007  BA2  TO INCREASE SNO LENGTH AND ONLY ALLOW          
      *                          TO PRINT PREVIOUSE MONTH PROG PAYMENT.         
      * BP133175  28112007  BA3  INCLUDE CDE-LOAN-SCHEME                        
      * BP133091  17102007  RB12 EXPAND KD00 LENGTH FROM 1200 TO 1600           
      * BP133573  25052009  JB8  TO CLEAR ADDRESS BF MOVE NEW ADDRESS           
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FD40  ASSIGN      TO BP13FD40.                           
           SELECT  SY02F001  ASSIGN      TO SY02F001.                           
           SELECT  BP13K800  ASSIGN      TO BP13K800                            
                   ACCESS MODE  IS RANDOM                                       
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY   IS K800-NUM-REGN                                
                   FILE STATUS  IS WS-K800-STATUS.                              
           SELECT  BP13K820  ASSIGN      TO BP13K820                            
                   ACCESS MODE  IS RANDOM                                       
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY   IS K820-KEY-FLD                                 
                   FILE STATUS  IS WS-K820-STATUS.                              
           SELECT  BP13KD00  ASSIGN      TO BP13KD00                            
                   ACCESS MODE  IS RANDOM                                       
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY   IS KD00-KEY-FLD                                 
                   FILE STATUS  IS WS-KD00-STATUS.                              
           SELECT  BP13LC27  ASSIGN  TO   BP13LC27.                             
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13FD40                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 1500 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FD40.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400  CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13LC27                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  PRINT-REC                   PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-FD40-EOF                 PIC X     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FD40             PIC 9(7) COMP VALUE ZEROES.              
           05  WS-PAGE-CNT             PIC 9(5) COMP VALUE ZEROES.              
           05  WS-LINE-CNT             PIC 9(2)  VALUE 60.                      
           05  CNT-CASES               PIC 9(5)  VALUE ZEROES.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY            PIC X(4)  VALUE SPACES.                  
           05  WS-CURR-MM              PIC X(2)  VALUE SPACES.                  
           05  WS-CURR-DD              PIC X(2)  VALUE SPACES.                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS          PIC 99    VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 99    VALUE ZEROES.                  
           05  WS-KD00-STATUS          PIC 99    VALUE ZEROES.                  
                                                                                
       01  WS-DTE-LAST-DAY-PREV.                                                
           05 WS-LST-PREV-CC         PIC 9(2).                                  
           05 WS-LST-PREV-YY         PIC 9(2).                                  
           05 WS-LST-PREV-MM         PIC 9(2).                                  
       01  WS-DTE-LAST-DAY-PREV-CHR REDEFINES WS-DTE-LAST-DAY-PREV              
                                     PIC X(6).                                  
                                                                                
       01  LC27-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC27'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'HDBCAT3'.               
           05  FILLER                  PIC X(25) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LC27-DATE-HD1           PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LC27-PAGENO             PIC ZZZZ9 .                              
                                                                                
       01  LC27-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(44) VALUE SPACES.                  
           05  FILLER                  PIC X(32) VALUE                          
               'NEW DBSS CASES FOR THE MONTH OF '.                              
           05  LC27-DATE-HD2           PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(45) VALUE SPACES.                  
                                                                                
       01  LC27-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE 'S/N'.                   
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'REG-NO'.                
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'HDB-REF'.               
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'FLAT-TYPE'.             
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE 'SELLING-PRICE'.         
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                            'LOAN-DISBURSED'.                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(16) VALUE                          
                                             'REPAYMENT-PERIOD'.                
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(12) VALUE                          
                                             'MTHLY-INSTAL'.                    
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE                          
                                             'LOAN-SCH'.                        
      *    05  FILLER                  PIC X(22) VALUE SPACES.                  
                                                                                
       01  LC27-PR-HEAD-03A.                                                    
           05  FILLER                  PIC X(66)  VALUE SPACES.                 
           05  FILLER                  PIC X(07)  VALUE ALL                     
                                             'TO DATE'.                         
           05  FILLER                  PIC X(59)  VALUE SPACES.                 
                                                                                
       01  LC27-PR-HEAD-04.                                                     
           05  FILLER                  PIC X(11)  VALUE SPACES.                 
           05  FILLER                  PIC X(03)  VALUE 'UIN'.                  
           05  FILLER                  PIC X(06)  VALUE SPACES.                 
           05  FILLER                  PIC X(04)  VALUE 'NAME'.                 
           05  FILLER                  PIC X(46)  VALUE SPACES.                 
           05  FILLER                  PIC X(07)  VALUE 'ADDRESS'.              
           05  FILLER                  PIC X(55)  VALUE SPACES.                 
                                                                                
       01  LC27-PR-ROLL.                                                        
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(118) VALUE ALL '-'.                
      *    05  FILLER                  PIC X(21)  VALUE SPACES.                 
                                                                                
       01  LC27-PR-ROLL1.                                                       
           05  FILLER                  PIC X(11)  VALUE SPACES.                 
           05  FILLER                  PIC X(71)  VALUE ALL '-'.                
           05  FILLER                  PIC X(50)  VALUE SPACES.                 
                                                                                
       01  LC27-PR-DETAILS-01.                                                  
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LC27-SNO                PIC ZZZZ9       VALUE ZEROES.            
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  LC27-REG-NO             PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LC27-HDB-REF            PIC X(13)       VALUE SPACES.            
           05  FILLER                  PIC X(8)        VALUE SPACES.            
           05  LC27-FLAT-TYPE          PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE SPACES.            
           05  LC27-SELL-PRICE         PIC $$,$$$,$$9                           
                                                       VALUE ZEROES.            
           05  FILLER                  PIC X(9)        VALUE SPACES.            
           05  LC27-LOAN-AMT           PIC $,$$$,$$9.99                         
                                                       VALUE ZEROES.            
           05  FILLER                  PIC X(9)        VALUE SPACES.            
           05  LC27-REPAY-PRD          PIC Z9          VALUE ZEROES.            
           05  FILLER                  PIC X(11)       VALUE SPACES.            
           05  LC27-MTH-INSTAL         PIC $$$,$$9.99                           
                                                       VALUE ZEROES.            
           05  FILLER                  PIC X(05)       VALUE SPACES.            
           05  LC27-LOAN-SCHEME        PIC X           VALUE SPACES.            
      *    05  FILLER                  PIC X(29)       VALUE SPACES.            
                                                                                
       01  LC27-PR-DETAILS-02.                                                  
           05  FILLER                  PIC X(10)  VALUE SPACES.                 
           05  LC27-UIN                PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC27-NAME               PIC X(50) VALUE SPACES.                  
           05  LC27-ADDRESS            PIC X(60) VALUE SPACES.                  
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
                                                                                
       01  LC27-PR-SPACES.                                                      
           05  FILLER                  PIC X(132) VALUE SPACES.                 
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN-PARA.                                                          
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP           THRU  1000-EXIT.                     
                                                                                
           PERFORM 1500-READ-SY02F001      THRU  1500-EXIT.                     
                                                                                
           PERFORM 2000-READ-FD40          THRU  2000-EXIT.                     
                                                                                
           PERFORM 3000-PROCESS-RECORD     THRU  3000-EXIT                      
                   UNTIL WS-FD40-EOF       =     'Y'.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD40                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13KD00                                                 
                       SY02F001                                                 
                OUTPUT BP13LC27.                                                
                                                                                
           IF WS-K800-STATUS NOT = 0  AND                                       
              WS-K800-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13K800 STATUS ' WS-K800-STATUS         
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                  
                                                                                
           IF WS-K820-STATUS NOT = 0  AND                                       
              WS-K820-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13K820 STATUS ' WS-K820-STATUS         
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                  
                                                                                
           IF WS-KD00-STATUS NOT = 0  AND                                       
              WS-KD00-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13KD00 STATUS ' WS-KD00-STATUS         
              MOVE WS-KD00-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                  
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1500-READ-SY02F001.                                                      
      ******************************************************************        
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT       TO WS-CURR-DATE.                         
           MOVE F001-DTE-LAST-DAY-PREV TO WS-DTE-LAST-DAY-PREV.                 
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                    
                  DELIMITED BY SIZE INTO LC27-DATE-HD1.                         
                                                                                
           STRING F001-TXT-PREV-MTH(1:3) ' '                                    
                  F001-DTE-LAST-DAY-PREV(1:4)                                   
                  DELIMITED BY SIZE INTO LC27-DATE-HD2.                         
                                                                                
       1500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-FD40.                                                          
      ******************************************************************        
                                                                                
           READ BP13FD40           AT   END                                     
                MOVE 'Y'           TO   WS-FD40-EOF                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-CNT-FD40.                            
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORD.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO   BP13K800-MASTER                 
                                                BP13K820-REC                    
                                                BP13KD00-REC                    
                                                BP13COMM8-REC.                  
                                                                                
           IF FD40-AMT-PP-LOAN            NOT NUMERIC                           
              MOVE ZEROES                 TO  FD40-AMT-PP-LOAN                  
           END-IF.                                                              
                                                                                
           IF FD40-NUM-PP-SCHDL = '01' AND                                      
              WS-DTE-LAST-DAY-PREV-CHR = FD40-DTE-PROGRESS-PYMT(1:6)            
              PERFORM 4000-PRINT-DETAILS   THRU 4000-EXIT                       
           END-IF.                                                              
           PERFORM 2000-READ-FD40          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4000-PRINT-DETAILS.                                                      
      ******************************************************************        
                                                                                
           IF WS-LINE-CNT > 54                                                  
              PERFORM 7000-PRINT-HEADING THRU 7000-EXIT                         
           END-IF.                                                              
                                                                                
           ADD   1                       TO CNT-CASES.                          
           MOVE CNT-CASES                TO LC27-SNO.                           
           MOVE FD40-NUM-REGN            TO LC27-REG-NO.                        
           STRING FD40-NUM-SCH-ACC (1:4) '-'                                    
                  FD40-NUM-SCH-ACC (5:4) '-'                                    
                  FD40-NUM-SCH-ACC (09:3)                                       
                  DELIMITED BY SIZE      INTO LC27-HDB-REF.                     
                                                                                
      *  TO CALL SUBROUTINE BP13C913,GETTING ADDRESS                            
           MOVE FD40-NUM-SCH-ACC(1:4)    TO WS-LINK-NUM-SCH.                    
           MOVE FD40-NUM-SCH-ACC(5:4)    TO WS-LINK-NUM-ACC.                    
           MOVE FD40-NUM-SCH-ACC(9:1)    TO WS-LINK-NUM-CHK.                    
                                                                                
           PERFORM 5000-LINK-BP13C913    THRU 5000-EXIT.                        
      *    STRING COMM8-PROP-TYPE ' '                                           
      *           COMM8-RM-TYPE                                                 
      *           DELIMITED BY SIZE      INTO LC27-FLAT-TYPE.                   
           MOVE COMM8-CDE-TYP-ACTLUSE   TO LC27-FLAT-TYPE.                      
                                                                                
           MOVE SPACES                  TO LC27-ADDRESS.                        
                                                                                
           STRING 'BLK '                 DELIMITED BY SIZE                      
                  COMM8-NUM-BLK          DELIMITED BY SIZE                      
                  ' #'                   DELIMITED BY SIZE                      
                  COMM8-NUM-LEVEL        DELIMITED BY SIZE                      
                  '-'                    DELIMITED BY SPACES                    
                  COMM8-NUM-UNIT-MAIN    DELIMITED BY SIZE                      
                  ' '                    DELIMITED BY SIZE                      
                  COMM8-NME-STREET       DELIMITED BY "   "                     
                  ' S('                  DELIMITED BY SIZE                      
                  COMM8-NUM-POSTAL-CODE  DELIMITED BY SIZE                      
                  ')'                    DELIMITED BY SIZE                      
                  INTO LC27-ADDRESS.                                            
                                                                                
      *  TO GETTING NAME                                                        
           MOVE   FD40-NUM-REGN          TO  K800-NUM-REGN                      
                                             K820-NUM-REGN                      
                                             KD00-NUM-REGN.                     
           PERFORM 6000-READ-BP13K800    THRU 6000-EXIT.                        
           IF K800-NUM-NRIC1 NOT = LOW-VALUES AND ZEROES AND SPACES             
              MOVE   K800-NUM-NRIC1      TO  K820-NUM-NRIC                      
                                             LC27-UIN                           
              PERFORM 6100-READ-BP13K820 THRU 6100-EXIT                         
              MOVE K820-NME-OCCP         TO  LC27-NAME                          
           ELSE                                                                 
              MOVE SPACES                TO  LC27-UIN                           
                                             LC27-NAME                          
           END-IF.                                                              
                                                                                
      *  TO GETTING SELLING PRICE AND REPAYMENT PERIOD.                         
           PERFORM 6200-READ-BP13KD00    THRU 6200-EXIT                         
           IF KD00-AMT-SELL-PRICE         NOT NUMERIC                           
           MOVE ZEROES                   TO  KD00-AMT-SELL-PRICE                
           END-IF.                                                              
           IF KD00-NUM-LOAN-REPYMT-PERIOD NOT NUMERIC                           
           MOVE ZEROES                   TO  KD00-NUM-LOAN-REPYMT-PERIOD        
           END-IF.                                                              
                                                                                
           MOVE KD00-AMT-SELL-PRICE         TO   LC27-SELL-PRICE.               
           MOVE KD00-NUM-LOAN-REPYMT-PERIOD TO  LC27-REPAY-PRD.                 
           MOVE KD00-CDE-LOAN-SCH           TO  LC27-LOAN-SCHEME.               
                                                                                
           IF FD40-AMT-PP-INSTAL          NOT NUMERIC                           
           MOVE ZEROES                   TO  FD40-AMT-PP-INSTAL                 
           END-IF.                                                              
                                                                                
           MOVE FD40-AMT-PP-LOAN            TO  LC27-LOAN-AMT.                  
           MOVE FD40-AMT-PP-INSTAL          TO  LC27-MTH-INSTAL.                
                                                                                
           WRITE PRINT-REC                 FROM LC27-PR-DETAILS-01.             
           WRITE PRINT-REC                 FROM LC27-PR-DETAILS-02.             
           WRITE PRINT-REC                 FROM LC27-PR-SPACES.                 
                                                                                
           ADD +3                          TO WS-LINE-CNT.                      
                                                                                
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       5000-LINK-BP13C913.                                                      
      ******************************************************************        
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       6000-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                DISPLAY 'REG NUMBER NOT FOUND-BP13K800 - '                      
                         K800-NUM-REGN                                          
             ELSE                                                               
                DISPLAY 'ERROR WITH READ FILE-BP13K800. STATUS IS '             
                         WS-K800-STATUS                                         
                MOVE WS-K800-STATUS       TO RETURN-CODE                        
                PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       6000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       6100-READ-BP13K820.                                                      
      ******************************************************************        
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K820-STATUS = 23                                             
                DISPLAY 'REG NUMBER NOT FOUND-BP13K820 - '                      
                         K820-NUM-REGN                                          
             ELSE                                                               
                DISPLAY 'ERROR WITH READ FILE-BP13K820. STATUS IS '             
                         WS-K820-STATUS                                         
                MOVE WS-K820-STATUS       TO RETURN-CODE                        
                PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT                 
             END-IF                                                             
           END-IF.                                                              
       6100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       6200-READ-BP13KD00.                                                      
      ******************************************************************        
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-KD00-STATUS = 23                                             
                DISPLAY 'REG NUMBER NOT FOUND-BP13KD00 - '                      
                         KD00-NUM-REGN                                          
             ELSE                                                               
                DISPLAY 'ERROR WITH READ FILE-BP13KD00. STATUS IS '             
                         WS-KD00-STATUS                                         
                MOVE WS-KD00-STATUS       TO RETURN-CODE                        
                PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       6200-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADING.                                                      
      ******************************************************************        
                                                                                
           ADD 1               TO   WS-PAGE-CNT.                                
           MOVE WS-PAGE-CNT    TO   LC27-PAGENO.                                
           WRITE PRINT-REC     FROM LC27-PR-SPACES AFTER PAGE.                  
           WRITE PRINT-REC     FROM LC27-PR-SPACES.                             
           WRITE PRINT-REC     FROM LC27-PR-SPACES.                             
           WRITE PRINT-REC     FROM LC27-PR-HEAD-01.                            
           WRITE PRINT-REC     FROM LC27-PR-HEAD-02.                            
           WRITE PRINT-REC     FROM LC27-PR-SPACES.                             
           WRITE PRINT-REC     FROM LC27-PR-HEAD-03.                            
           WRITE PRINT-REC     FROM LC27-PR-HEAD-03A.                           
           WRITE PRINT-REC     FROM LC27-PR-ROLL.                               
           WRITE PRINT-REC     FROM LC27-PR-HEAD-04.                            
           WRITE PRINT-REC     FROM LC27-PR-ROLL1.                              
           WRITE PRINT-REC     FROM LC27-PR-SPACES.                             
           MOVE 12             TO   WS-LINE-CNT.                                
                                                                                
       7000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC27'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FD40 RECORDS READ..........: '                    
                    WS-CNT-FD40.                                                
           DISPLAY 'NO OF BP13LC27 RECORDS WRITTEN.......: '                    
                    CNT-CASES.                                                  
                                                                                
           CLOSE BP13FD40                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13KD00                                                       
                 SY02F001                                                       
                 BP13LC27.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 0 AND WS-K800-STATUS NOT = 97                
              DISPLAY ' CLOSING ERROR, BP13K800 STATUS ' WS-K800-STATUS         
              MOVE WS-K800-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K820-STATUS NOT = 0 AND WS-K820-STATUS NOT = 97                
              DISPLAY ' CLOSING ERROR, BP13K820 STATUS ' WS-K820-STATUS         
              MOVE WS-K820-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-KD00-STATUS NOT = 0 AND WS-KD00-STATUS NOT = 97                
              DISPLAY ' CLOSING ERROR, BP13KD00 STATUS ' WS-KD00-STATUS         
              MOVE WS-KD00-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
