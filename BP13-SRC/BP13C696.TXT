       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C696.                                                    
      *AUTHOR. RHB.                                                             
      *DATE-WRITTEN. 14/07/97.                                                  
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  TO DELETE BP13K810 RECORD FOR CASES THAT      *                
      *          TAKE POSSESION OR CANCELLED (BOT FOUND IN     *                
      *          SOC MASTER BP13K800)                          *                
      *                                                        *                
      * INPUT : BP13F800, BP13F810                             *                
      *                                                        *                
      * OUTPUT   : P13F810A                                    *                
      *          : P13F810B                                    *                
      *                                                        *                
      * CHG-NO   BY   ON        DESCRIPTION                    *                
      * ------------------------------------------------------ *                
      * BP130450 RHB 26/03/98  NEW PGM                         *                
      * BP130418 SGK 31/08/98  Y2K CHANGES.                    *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800   ASSIGN       TO BP13F800.                          
                                                                                
           SELECT BP13F810   ASSIGN       TO BP13F810.                          
                                                                                
           SELECT P13F810A   ASSIGN       TO P13F810A.                          
           SELECT P13F810B   ASSIGN       TO P13F810B.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F810                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
                                                                                
       COPY BP13F810.                                                           
                                                                                
       FD   P13F810A                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
                                                                                
       01   P13F810A-REC                      PIC X(400).                       
                                                                                
       FD   P13F810B                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
                                                                                
       01   P13F810B-REC                      PIC X(400).                       
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-F810-EOF             PIC X     VALUE SPACE.                       
       01  WS-F800-EOF             PIC X     VALUE SPACE.                       
       01  WS-F800-READ            PIC 9(08) VALUE ZEROES.                      
       01  WS-F810-READ            PIC 9(08) VALUE ZEROES.                      
       01  WS-F810-DELETE          PIC 9(08) VALUE ZEROES.                      
       01  WS-F810-WRITE           PIC 9(08) VALUE ZEROES.                      
       01  WS-F810-NOT-FOUND       PIC 9(08) VALUE ZEROES.                      
       01  WS-MATCH                PIC 9(08) VALUE ZEROES.                      
       01  WS-F810-REGN            PIC X(08) VALUE SPACES.                      
                                                                                
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-YYYY              PIC 9(04).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F800     THRU 200-EXIT.                             
           PERFORM 400-READ-F810     THRU 400-EXIT.                             
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL F800-NUM-REGN = HIGH-VALUES                            
                   AND   F810-NUM-REGN = HIGH-VALUES.                           
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F810                                                 
               OUTPUT  P13F810A                                                 
                       P13F810B.                                                
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-F800.                                                           
      *-----------------*                                                       
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-F800-EOF                                         
                MOVE HIGH-VALUES TO F800-NUM-REGN                               
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================*                          
      *       DOCUMENTATION                          *                          
      *==============================================*                          
                                                                                
      *---------------*                                                         
       300-PROCESS-REC.                                                         
      *---------------*                                                         
                                                                                
           IF F800-NUM-REGN = F810-NUM-REGN                                     
              ADD 1 TO WS-MATCH                                                 
              PERFORM 500-OUTPUT-F810 THRU 500-EXIT                             
                      UNTIL F810-NUM-REGN NOT = F800-NUM-REGN                   
                      OR    WS-F810-EOF = 'Y'                                   
              PERFORM 200-READ-F800 THRU 200-EXIT                               
           ELSE                                                                 
              IF F800-NUM-REGN < F810-NUM-REGN                                  
                 ADD 1 TO WS-F810-NOT-FOUND                                     
                 PERFORM 200-READ-F800       THRU 200-EXIT                      
              ELSE                                                              
                 MOVE SPACES        TO WS-F810-REGN                             
                 MOVE F810-NUM-REGN TO WS-F810-REGN                             
                 PERFORM 510-DELETE-F810 THRU 510-EXIT                          
                         UNTIL F810-NUM-REGN NOT = WS-F810-REGN                 
                            OR WS-F810-EOF       = 'Y'                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       400-READ-F810.                                                           
      *-----------------*                                                       
                                                                                
           READ BP13F810 AT END                                                 
                MOVE 'Y' TO WS-F810-EOF                                         
                MOVE HIGH-VALUES TO F810-NUM-REGN                               
                GO TO 400-EXIT.                                                 
           ADD 1 TO WS-F810-READ.                                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       500-OUTPUT-F810.                                                         
      *-----------------*                                                       
                                                                                
           MOVE  BP13F810-MASTER TO P13F810A-REC.                               
           WRITE P13F810A-REC.                                                  
           ADD   1               TO WS-F810-WRITE.                              
                                                                                
           PERFORM 400-READ-F810 THRU 400-EXIT.                                 
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       510-DELETE-F810.                                                         
      *-------------------*                                                     
                                                                                
           IF F810-NUM-REGN =  WS-F810-REGN                                     
              MOVE  BP13F810-MASTER TO P13F810B-REC                             
              WRITE P13F810B-REC                                                
              ADD   1               TO WS-F810-DELETE                           
           END-IF.                                                              
                                                                                
           PERFORM 400-READ-F810 THRU 400-EXIT.                                 
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                    
                                                                                
           DISPLAY 'PROGRAM-ID : MATF800  '.                                    
           DISPLAY '**********************'.                                    
           DISPLAY ' '.                                                         
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F800   ' WS-F800-READ.              
           DISPLAY 'NO OF RECORDS READ FROM F810   ' WS-F810-READ.              
           DISPLAY 'NO OF RECORDS MATCH            ' WS-MATCH.                  
           DISPLAY 'NO FOUND IN K800 NOTFD K810    ' WS-F810-NOT-FOUND.         
           DISPLAY 'NO OF RECORDS OUTPUT    F810A  ' WS-F810-WRITE.             
           DISPLAY 'NO OF RECORDS DELETE    F810B  ' WS-F810-DELETE.            
                                                                                
           CLOSE BP13F810                                                       
                 P13F810A                                                       
                 P13F810B                                                       
                 BP13F800.                                                      
                                                                                
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
