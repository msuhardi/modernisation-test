      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C326.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  03/12/2004.                                               
                                                                                
      *---------------------------------------------------------------*         
      *              SYSTEM OF COMMITMENT (BP13)                      *         
      *---------------------------------------------------------------*         
      *                                                               *         
      *  OBJECTIVE : TO PRINT MONTHLY REPORT FOR SDS TOP-UP WHERE     *         
      *              HALF IDP = 'Y' & 2ND PAYMENT DUE                 *         
      *                                                               *         
      *  INPUT FILES:                                                 *         
      *      1. BP13F800                                              *         
      *      2. BM06K110                                              *         
      *      3. BP13K022                                              *         
      *                                                               *         
      *  OUTPUT FILES:                                                *         
      *      1. BP13F326                                              *         
      *      2. BP13L326                                              *         
      *                                                               *         
      * ------------------------------------------------------------- *         
      * CHG REF   BY    DATE        DESCRIPTION                       *         
      * --------  ----  ----------  ------------                      *         
      * BP132605  JF4   03/12/2004  NEW PROGRAM.                      *         
      *                                                               *         
      *---------------------------------------------------------------*         
                                                                                
      *----------------------*                                                  
       ENVIRONMENT DIVISION.                                                    
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
                                                                                
      *----------------------*                                                  
       FILE-CONTROL.                                                            
      *----------------------*                                                  
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13F326  ASSIGN        TO BP13F326.                          
                                                                                
           SELECT BP13L326  ASSIGN        TO BP13L326.                          
                                                                                
                                                                                
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
      *--------------*                                                          
                                                                                
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           BLOCK  CONTAINS    0 RECORDS                                         
           LABEL  RECORDS   ARE STANDARD                                        
           RECORDING MODE    IS F.                                              
       COPY BP13F800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS  100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  BP13F326                                                             
           BLOCK  CONTAINS    0 RECORDS                                         
           RECORD CONTAINS  100 CHARACTERS                                      
           LABEL  RECORDS   ARE STANDARD                                        
           RECORDING MODE    IS F.                                              
       01  BP13F326-REC.                                                        
           05  F326-NUM-REGN          PIC X(08).                                
           05  F326-NUM-OIC           PIC X(05).                                
           05  F326-NUM-2ND-PYMT      PIC 9(02).                                
           05  F326-DTE-SDS-DUE       PIC X(08).                                
           05  FILLER                 PIC X(77).                                
                                                                                
       FD  BP13L326                                                             
           BLOCK  CONTAINS    0 RECORDS                                         
           RECORD CONTAINS  132 CHARACTERS                                      
           LABEL  RECORDS   ARE STANDARD                                        
           RECORDING MODE    IS F.                                              
       01  BP13L326-REC               PIC  X(132).                              
                                                                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BM06K110-STATUS          PIC  9(02)  VALUE  ZEROES.                  
       01  BP13K022-STATUS          PIC  9(02)  VALUE  ZEROES.                  
                                                                                
       01  WS-F800-READ-CNT         PIC  9(08)  VALUE  ZEROES.                  
                                                                                
       01  WS-K110-NOTFND-CNT       PIC  9(08)  VALUE  ZEROES.                  
       01  WS-K022-NOTFND-CNT       PIC  9(08)  VALUE  ZEROES.                  
                                                                                
       01  WS-PYMNT-NOT-DUE-CNT     PIC  9(08)  VALUE  ZEROES.                  
       01  WS-L326-WRITE-CNT        PIC  9(08)  VALUE  ZEROES.                  
       01  WS-L326-LINE             PIC  9(08)  VALUE  70.                      
       01  WS-L326-PAGE             PIC  9(08)  VALUE  ZEROES.                  
       01  WS-L326-SLNO             PIC  9(08)  VALUE  ZEROES.                  
                                                                                
       01  WS-AMT-TOPUP-CD          PIC  9(08)V99 VALUE  ZEROES.                
                                                                                
       01  BP13F800-EOF-TAG         PIC  X(01).                                 
           88  BP13F800-EOF         VALUE 'Y'.                                  
                                                                                
       01  BM06K110-FND-TAG         PIC  X(01).                                 
           88  BM06K110-FND         VALUE 'Y'.                                  
                                                                                
       01  WS-EDIT-DATE             PIC  X(10)  VALUE SPACES.                   
       01  WS-CURR-DATE             PIC  9(08)  VALUE ZEROES.                   
       01  WS-CHK-DATE              PIC  9(08)  VALUE ZEROES.                   
       01  WS-INT-DATE              PIC  9(09)  VALUE ZEROES.                   
                                                                                
       01  WS-EDIT-CNT              PIC  Z(07)9.                                
                                                                                
      *------------------------------------*                                    
      **     BP13L326   REPORT LAYOUT     **                                    
      *------------------------------------*                                    
       01  L326-HEADER1.                                                        
           05  L326-LIST-ID         PIC X(08)   VALUE 'BP13L326'.               
           05  FILLER               PIC X(05)   VALUE SPACES .                  
           05  FILLER               PIC X(08)   VALUE 'HDBCAT 3'.               
           05  FILLER               PIC X(23)   VALUE SPACES.                   
           05  FILLER               PIC X(39)   VALUE                           
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER               PIC X(17)   VALUE SPACES.                   
           05  FILLER               PIC X(06) VALUE                             
               'DATE :'.                                                        
           05  L326-PRT-DATE        PIC X(10) VALUE 'DD/MM/CCYY'.               
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(09) VALUE 'PAGE NO :'.                
           05  L326-PAGE-NO         PIC Z(03)9.                                 
                                                                                
       01  L326-HEADER2.                                                        
           05  FILLER               PIC X(46) VALUE SPACES.                     
           05  FILLER               PIC X(45) VALUE                             
               'MONTHLY REPORT FOR SDS TOP-UP WHERE'.                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
                                                                                
       01  L326-HEADER2A.                                                       
           05  FILLER               PIC X(37) VALUE SPACES.                     
           05  FILLER               PIC X(52) VALUE                             
               'HALF IDP = Y & 2ND PAYMENT DUE (AGREEMENT TEAM -CYM)'.          
           05  FILLER               PIC X(20) VALUE SPACES.                     
                                                                                
       01  L326-HEADER3.                                                        
           05  FILLER               PIC X(03) VALUE SPACES.                     
           05  FILLER               PIC X(03) VALUE 'S/N'.                      
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'REGN NO'.                  
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'AO DATE'.                  
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(16) VALUE 'PAYMENT DUE DATE'.         
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(13) VALUE 'SELLING PRICE'.            
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(13) VALUE 'TOP-UP CD AMT'.            
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(03) VALUE 'CSM'.                      
                                                                                
       01  L326-UNDERLINE.                                                      
           05  FILLER               PIC X(132) VALUE ALL '-'.                   
                                                                                
       01  L326-DETAIL1.                                                        
           05  L326-SNO             PIC Z(05)9.                                 
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  L326-NUM-REGN        PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE SPACES.                     
           05  L326-DTE-AO          PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE SPACES.                     
           05  L326-DTE-PYMNT-DUE   PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(06) VALUE SPACES.                     
           05  L326-SELL-PRICE      PIC $$,$$$,$$9.99.                          
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L326-AMT-TOPUP       PIC $$,$$$,$$9.99.                          
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L326-NUM-OIC         PIC X(05) VALUE SPACES.                     
                                                                                
       01  L326-DETAIL2.                                                        
           05  FILLER               PIC X(33) VALUE SPACES.                     
           05  L326-DESCRIPTION     PIC X(60) VALUE                             
               ' ** NO CASES WITH SDS 2ND PAYMENT DUE **'.                      
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *--------------------------------------------------------------*          
       0000-CONTROL.                                                            
      *--------------------------------------------------------------*          
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 1500-COMP-CHKDATE        THRU 1500-EXIT.                     
           PERFORM 2000-READ-BP13F800       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-DATA        THRU 3000-EXIT                      
                                            UNTIL BP13F800-EOF.                 
           PERFORM 9000-END-ROUTINE         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------------*          
            OPEN INPUT  BP13F800                                                
                        BM06K110                                                
                        BP13K022                                                
                 OUTPUT BP13F326                                                
                        BP13L326.                                               
                                                                                
           IF BM06K110-STATUS  NOT =  ZEROES AND 97                             
              DISPLAY 'BM06K110 OPEN ERROR, STATUS ' BM06K110-STATUS            
              MOVE  BM06K110-STATUS        TO   RETURN-CODE                     
              PERFORM 9000-END-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS  NOT =  ZEROES AND 97                             
              DISPLAY 'BP13K022 OPEN ERROR, STATUS ' BP13K022-STATUS            
              MOVE  BP13K022-STATUS        TO   RETURN-CODE                     
              PERFORM 9000-END-ROUTINE                                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-CURR-DATE.                   
           STRING WS-CURR-DATE(7:2), '/',                                       
                  WS-CURR-DATE(5:2), '/',                                       
                  WS-CURR-DATE(1:4)  DELIMITED BY SIZE                          
                  INTO WS-EDIT-DATE.                                            
                                                                                
           MOVE WS-EDIT-DATE               TO   L326-PRT-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       1500-COMP-CHKDATE.                                                       
      *---------------------------------------------------------------*         
           COMPUTE WS-INT-DATE =                                                
                   FUNCTION INTEGER-OF-DATE(WS-CURR-DATE).                      
           ADD  30     TO   WS-INT-DATE.                                        
           COMPUTE WS-CHK-DATE =                                                
                   FUNCTION DATE-OF-INTEGER(WS-INT-DATE).                       
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-READ-BP13F800.                                                      
      *--------------------------------------------------------------*          
           READ BP13F800 AT END                                                 
                MOVE    'Y'       TO   BP13F800-EOF-TAG                         
                NOT AT END                                                      
                ADD      1        TO   WS-F800-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-PROCESS-DATA.                                                       
      *--------------------------------------------------------------*          
      **ONLY AO RAISED, CUMULATIVE                                              
           IF F800-PCT-SDS-2ND-PYMT NOT NUMERIC                                 
              MOVE ZEROES         TO   F800-PCT-SDS-2ND-PYMT                    
           END-IF.                                                              
                                                                                
           IF (F800-DTE-RAISE-AO NOT = ZEROES AND SPACES AND                    
                                       LOW-VALUES)  AND                         
              (F800-PCT-SDS-2ND-PYMT > 0)                                       
              IF (F800-DTE-SDS-PYMT-DUE <= WS-CHK-DATE)    AND                  
                 (F800-NUM-SDS-2ND-PYMT NOT = 'Y')                              
                 MOVE F800-NUM-SCH-ACC           TO   K110-KEY-FLD              
                 PERFORM 4000-READ-BM06K110      THRU 4000-EXIT                 
                                                                                
                 IF BM06K110-FND                                                
                    MOVE F800-NUM-REGN           TO   K022-NUM-REGN             
                    PERFORM 4500-READ-BP13K022   THRU 4500-EXIT                 
                                                                                
                    PERFORM 5000-WRITE-BP13F326  THRU 5000-EXIT                 
                    PERFORM 6000-PRINT-REPORT    THRU 6000-EXIT                 
                 ELSE                                                           
                    ADD 1 TO WS-K110-NOTFND-CNT                                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800         THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4000-READ-BM06K110.                                                      
      *--------------------------------------------------------------*          
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
               MOVE  'Y'               TO   BM06K110-FND-TAG                    
             WHEN 23                                                            
               MOVE  SPACE             TO   BM06K110-FND-TAG                    
               DISPLAY 'BM06K110 REC NOT FOUND, KEY : ' K110-KEY-FLD            
             WHEN OTHER                                                         
               DISPLAY ' BM06K110 READ ERROR, STATUS ' BM06K110-STATUS          
                       '  KEY : ' K110-KEY-FLD                                  
               MOVE BM06K110-STATUS    TO   RETURN-CODE                         
               PERFORM 9000-END-ROUTINE                                         
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4500-READ-BP13K022.                                                      
      *--------------------------------------------------------------*          
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
               CONTINUE                                                         
             WHEN 23                                                            
               MOVE  SPACES            TO   K022-NUM-OIC                        
               DISPLAY 'BP13K022 REC NOT FOUND, KEY : ' K110-KEY-FLD            
             WHEN OTHER                                                         
               DISPLAY ' BM06K110 READ ERROR, STATUS ' BM06K110-STATUS          
                       '  KEY : ' K110-KEY-FLD                                  
               MOVE BM06K110-STATUS    TO   RETURN-CODE                         
               PERFORM 9000-END-ROUTINE                                         
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-BP13F326.                                                     
      *--------------------------------------------------------------*          
           MOVE   SPACES                TO   BP13F326-REC.                      
                                                                                
           MOVE F800-NUM-REGN           TO   F326-NUM-REGN.                     
           MOVE K022-NUM-OIC            TO   F326-NUM-OIC.                      
           MOVE F800-PCT-SDS-2ND-PYMT   TO   F326-NUM-2ND-PYMT.                 
           MOVE F800-DTE-SDS-PYMT-DUE   TO   F326-DTE-SDS-DUE.                  
                                                                                
           WRITE BP13F326-REC.                                                  
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       6000-PRINT-REPORT.                                                       
      *--------------------------------------------------------------*          
           IF WS-L326-LINE > 55                                                 
              PERFORM 6100-PRINT-HEADER     THRU 6100-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 6300-PRINT-DETAIL        THRU 6300-EXIT.                     
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           ADD   1                     TO    WS-L326-PAGE.                      
           MOVE  WS-L326-PAGE          TO    L326-PAGE-NO.                      
           WRITE BP13L326-REC          FROM  L326-HEADER1   AFTER PAGE.         
           WRITE BP13L326-REC          FROM  L326-HEADER2.                      
           WRITE BP13L326-REC          FROM  L326-HEADER2A.                     
           WRITE BP13L326-REC          FROM  L326-UNDERLINE AFTER 2.            
           WRITE BP13L326-REC          FROM  L326-HEADER3.                      
           WRITE BP13L326-REC          FROM  L326-UNDERLINE.                    
           MOVE  ALL SPACES            TO    BP13L326-REC.                      
           WRITE BP13L326-REC.                                                  
           MOVE  8                     TO    WS-L326-LINE.                      
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6300-PRINT-DETAIL.                                                       
      *-------------------------------------------------------------            
           ADD     1                     TO   WS-L326-SLNO.                     
           MOVE  WS-L326-SLNO            TO   L326-SNO.                         
                                                                                
           MOVE  F800-NUM-REGN           TO   L326-NUM-REGN.                    
                                                                                
           STRING F800-DTE-RAISE-AO(7:2)  '/'                                   
                  F800-DTE-RAISE-AO(5:2)  '/'                                   
                  F800-DTE-RAISE-AO(1:4)                                        
                  DELIMITED BY SIZE      INTO L326-DTE-AO.                      
                                                                                
           STRING F800-DTE-SDS-PYMT-DUE(7:2)  '/'                               
                  F800-DTE-SDS-PYMT-DUE(5:2)  '/'                               
                  F800-DTE-SDS-PYMT-DUE(1:4)                                    
                  DELIMITED BY SIZE      INTO L326-DTE-PYMNT-DUE.               
                                                                                
           IF F800-PCT-SDS-2ND-PYMT NOT NUMERIC                                 
              MOVE ZEROES TO WS-AMT-TOPUP-CD                                    
           ELSE                                                                 
              COMPUTE WS-AMT-TOPUP-CD = K110-CURR-SELLING-PRICE *               
                                        (F800-PCT-SDS-2ND-PYMT / 100)           
           END-IF.                                                              
                                                                                
           MOVE  WS-AMT-TOPUP-CD         TO   L326-AMT-TOPUP.                   
           MOVE  K110-CURR-SELLING-PRICE TO   L326-SELL-PRICE.                  
                                                                                
           MOVE  K022-NUM-OIC            TO   L326-NUM-OIC.                     
                                                                                
           WRITE BP13L326-REC            FROM L326-DETAIL1.                     
           ADD      1    TO WS-L326-WRITE-CNT                                   
                            WS-L326-LINE.                                       
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-END-ROUTINE.                                                        
      *-------------------------------------------------------------            
           IF WS-L326-WRITE-CNT = ZEROES                                        
              PERFORM 6100-PRINT-HEADER    THRU 6100-EXIT                       
              WRITE BP13L326-REC           FROM L326-DETAIL2 AFTER 7            
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' BP13C326 - CONTROL LIST    DATE : ' WS-EDIT-DATE.          
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE  WS-F800-READ-CNT    TO WS-EDIT-CNT.                            
           DISPLAY 'RECORDS READ FROM BP13F800  : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           MOVE  WS-K110-NOTFND-CNT  TO WS-EDIT-CNT.                            
           DISPLAY 'RECORDS NOT FND IN BM06K110 : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           MOVE  WS-L326-WRITE-CNT   TO WS-EDIT-CNT.                            
           DISPLAY 'RECORDS WRITTEN TO BP13F326 : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           MOVE  WS-L326-WRITE-CNT   TO WS-EDIT-CNT.                            
           DISPLAY 'RECORDS PRINTED IN LISTING  : ' WS-EDIT-CNT.                
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 BP13K022                                                       
                 BP13F326                                                       
                 BP13L326.                                                      
                                                                                
           IF BM06K110-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BM06K110 CLOSE ERROR, STATUS ' BM06K110-STATUS            
             MOVE BM06K110-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K022 CLOSE ERROR, STATUS ' BP13K022-STATUS            
             MOVE BP13K022-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
      *                 END OF BP13C326                              *          
      *--------------------------------------------------------------*          
