       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C94G.                                                  
      *DATE-WRITTEN. 19 JUN 2017.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO UPDATE BP13K48K FILE WITH KEY ISSUE DATE *            
      *                AND CASH PROCEEDS.                          *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136743 20170417 KVL1  NEW PROGRAM                        *            
      * BP136743 20170818 KVL1  ALLOW NOT FOUND RECORD IN BP13K480 *            
      * BP137044 20171116 RJB1  TO CHECK FOR NUM-CONTRA-TYPE DT/TU *            
      * BP137087 20171120 RJB1  TO FIX LOOPING STATEMENT           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410  ASSIGN        TO BP13F410.                          
                                                                                
           SELECT BP13K48K  ASSIGN        TO BP13K48K                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K48K-KEY-FLD                       
                            FILE STATUS   IS BP13K48K-STATUS.                   
                                                                                
           SELECT BP13K480  ASSIGN        TO BP13K480                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K480-KEY-FLD                       
                            FILE STATUS   IS BP13K480-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F410                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F410.                                                           
                                                                                
       FD  BP13K480                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K480.                                                           
                                                                                
       FD  BP13K48K                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K48K.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-DATE             PIC 9(08)  VALUE ZEROES.                         
       01  BP13K480-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K48K-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-S                PIC 9(06)  VALUE ZEROES.                         
       01  WS-READ-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(06)  VALUE ZEROES.                         
       01  WS-BP13F410-EOF     PIC X(01)  VALUE 'N'.                            
       01  WS-K48K-FND         PIC X(01)  VALUE 'N'.                            
       01  WS-K480-FND         PIC X(01)  VALUE 'N'.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F410       THRU 2000-EXIT.                     
           IF WS-BP13F410-EOF = 'Y'                                             
              DISPLAY 'FILE BP13F410 IS  EMPTY'                                 
           ELSE                                                                 
              PERFORM 3000-PROCESS-DATA     THRU 3000-EXIT                      
                UNTIL WS-BP13F410-EOF = 'Y'                                     
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K480                                                 
                I-O    BP13K48K.                                                
                                                                                
           IF BP13K480-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K480 OPEN ERROR, STATUS : ' BP13K480-STATUS          
              MOVE     BP13K480-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K48K-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K48K OPEN ERROR, STATUS : ' BP13K48K-STATUS          
              MOVE     BP13K48K-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F410.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F410 AT END                                                 
                         MOVE 'Y'              TO WS-BP13F410-EOF               
                         NOT AT END                                             
                         ADD  1                TO WS-READ-CNT                   
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13K480.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K480.                                                       
                                                                                
           EVALUATE BP13K480-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'             TO WS-K480-FND                            
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 0               TO K48K-CASH-PROCEED                      
                 MOVE 'N'             TO WS-K480-FND                            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K480. STATUS IS '                   
                            BP13K480-STATUS                                     
                 MOVE BP13K480-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READ-BP13K48K.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K48K.                                                       
                                                                                
           EVALUATE BP13K48K-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'                TO WS-K48K-FND                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'N'                TO WS-K48K-FND                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K48K. STATUS IS '                   
                            BP13K48K-STATUS                                     
                 MOVE BP13K48K-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-REWRITE-BP13K48K.                                                   
      *-------------------------------------------------------------            
           REWRITE  BP13K48K-REC.                                               
                                                                                
           EVALUATE BP13K48K-STATUS                                             
              WHEN 00                                                           
                 ADD 1                TO WS-WRITE-CNT                           
              WHEN OTHER                                                        
                 DISPLAY 'ERROR REWRITING BP13K48K FOR KEY '                    
                          K48K-NUM-REGN                                         
                 MOVE BP13K48K-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                      TO   BP13K48K-REC.                  
           MOVE F410-NUM-REGN               TO   K48K-NUM-REGN.                 
           PERFORM 2200-READ-BP13K48K       THRU 2200-EXIT.                     
           IF WS-K48K-FND = 'Y'                                                 
              MOVE SPACES                   TO   BP13K480-REC                   
              MOVE F410-NUM-REGN            TO   K480-NUM-REGN                  
              PERFORM 2100-READ-BP13K480    THRU 2100-EXIT                      
              IF WS-K480-FND = 'Y'                                              
              IF K48K-CASE-ELIGBLTY = 'C'                                       
                 MOVE K480-AMT-CONTRA-CASH  TO   K48K-CASH-PROCEED              
              ELSE                                                              
                 IF K48K-CASE-ELIGBLTY = 'T'                                    
                    MOVE K480-AMT-TEMP-LOAN-CASH                                
                                            TO   K48K-CASH-PROCEED              
                 ELSE                                                           
                 IF F410-NUM-CONTRA-TYPE = 'DT' OR 'TU'                         
                    CONTINUE                                                    
                 ELSE                                                           
                    DISPLAY 'CASE NOT YET ELIGIBILITY TAGGED, KEY IS '          
                             F410-NUM-REGN                                      
                    MOVE '03'               TO   RETURN-CODE                    
                    PERFORM 9000-CLOSE-ROUTINE                                  
                       THRU 9000-EXIT                                           
                 END-IF                                                         
                 END-IF                                                         
              END-IF                                                            
              END-IF                                                            
              MOVE F410-DTE-KEY-ISSUE       TO   K48K-DTE-KEY-ISS               
              PERFORM 2300-REWRITE-BP13K48K THRU 2300-EXIT                      
           END-IF                                                               
           PERFORM 2000-READ-BP13F410       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '------ BP13C94G CONTROL TOTAL -------'.                     
           DISPLAY ' NO OF REC READ FROM BP13F410      = '                      
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF REC WRITTEN TO BP13K48K     = '                      
                     WS-WRITE-CNT,                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F410                                                 
                       BP13K480                                                 
                       BP13K48K.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
