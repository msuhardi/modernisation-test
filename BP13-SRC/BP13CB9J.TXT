       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB9J.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  26 JUL 2016.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  MATCH BP13F730 (8AM) WITH BP13F55A(11AM),     *                
      *          OUTPUT CASES NOT FOUND IN 8AM FILE            *                
      *          OUTPUT CASES FOUND IN 8AM FILE WITH CHANGES   *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136363 FNP1 260716  NEW PROGRAM                      *                
      * BP136363 FNP1 090916  MATCH WITH F730 8AM FILE INSTEAD *                
      *                       OF F55A                          *                
      * BP137358 FNP1 300518  EXPAND BP13F55A FROM 800 TO 1200 *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT P13F55A  ASSIGN TO P13F55A.                                   
           SELECT P13F55B  ASSIGN TO P13F55B.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   P13F55A                                                             
            RECORD CONTAINS 1200 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F55A.                                                           
                                                                                
       FD   P13F55B                                                             
            RECORD CONTAINS 1200 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       01   P13F55B-REC              PIC X(1200).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F55A-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F730-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F55A-WRITE         PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF          PIC X       VALUE 'N'.                      
           05  WS-F55A-EOF          PIC X       VALUE 'N'.                      
           05  WS-DIFF              PIC X       VALUE 'N'.                      
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-F730-MATCHED      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F730-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F55A-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F730   THRU 1100-EXIT.                         
           PERFORM 1200-READ-BP13F55A   THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL F730-NUM-REGN = HIGH-VALUES AND                              
                   F55A-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F730                                                 
                       P13F55A                                                  
                OUTPUT P13F55B.                                                 
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F730.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F730 AT END                                                 
              MOVE HIGH-VALUES TO F730-NUM-REGN                                 
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1200-READ-BP13F55A.                                                      
      *------------------------------------------------------------             
                                                                                
           READ P13F55A AT END                                                  
              MOVE HIGH-VALUES TO F55A-NUM-REGN                                 
                GO TO 1200-EXIT.                                                
                                                                                
           ADD 1 TO WS-F55A-READ.                                               
                                                                                
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           IF F730-NUM-REGN = F55A-NUM-REGN                                     
              ADD 1 TO WS-F730-MATCHED                                          
              IF F55A-NUM-BAL-HH  = SPACES OR LOW-VALUES                        
                 IF F55A-NUM-FT2T = 'Y'                                         
                    MOVE 'F'   TO F55A-NUM-BAL-HH                               
                 ELSE                                                           
                    MOVE F55A-NUM-HH   TO F55A-NUM-BAL-HH                       
                 END-IF                                                         
              END-IF                                                            
              WRITE P13F55B-REC  FROM BP13F55A-REC                              
              ADD 1 TO WS-F55A-WRITE                                            
              PERFORM 1100-READ-BP13F730    THRU 1100-EXIT                      
              PERFORM 1200-READ-BP13F55A    THRU 1200-EXIT                      
           ELSE                                                                 
              IF F730-NUM-REGN <  F55A-NUM-REGN                                 
                 ADD 1 TO WS-F730-NOMATCH                                       
                 PERFORM 1100-READ-BP13F730    THRU 1100-EXIT                   
              ELSE                                                              
                 ADD 1 TO WS-F55A-NOMATCH                                       
                 PERFORM 1200-READ-BP13F55A    THRU 1200-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13CB9J END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM BP13F730   ' WS-F730-READ.          
           DISPLAY 'NO OF RECORDS READ FROM BP13F55A   ' WS-F55A-READ.          
           DISPLAY 'NO OF RECORDS MATCHED W/ F55A   ' WS-F730-MATCHED.          
           DISPLAY 'FOUND F730     NOT MTCH W/ F55A  ' WS-F730-NOMATCH.         
           DISPLAY 'FOUND F55A    NOT MTCH W/ F730  ' WS-F55A-NOMATCH.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF OUPUT                     ' WS-F55A-WRITE.            
                                                                                
                                                                                
           CLOSE BP13F730                                                       
                 P13F55A                                                        
                 P13F55B.                                                       
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
