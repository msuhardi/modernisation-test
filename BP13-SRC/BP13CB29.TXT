      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CB29.                                                 
       AUTHOR.        ARIEL HERNANDEZ.                                          
       DATE-WRITTEN.  19/01/2007.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  TO COMPARE 2 FILES                            *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP133111  BA3  22/08/2007 NEW PROGRAM                        *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F593  ASSIGN        TO BP13F593.                          
                                                                                
           SELECT BP13K593  ASSIGN        TO BP13K593.                          
                                                                                
           SELECT P13F593B  ASSIGN        TO P13F593B.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F593                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       COPY BP13F593.                                                           
                                                                                
                                                                                
       FD  BP13K593                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13K593.                                                           
                                                                                
       FD  P13F593B                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       01  P13F593B-REC                  PIC X(2000).                           
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-EOF-F593                  PIC X(01)    VALUE SPACES.              
       01  WS-EOF-K593                  PIC X(01)    VALUE SPACES.              
       01  CNT-F593-READ                PIC 9(05)    VALUE ZEROES.              
       01  CNT-K593-READ                PIC 9(08)    VALUE ZEROES.              
       01  CNT-F593-NOTFOUND            PIC 9(08)    VALUE ZEROES.              
       01  CNT-K593-NOTFOUND            PIC 9(08)    VALUE ZEROES.              
       01  CNT-F593B-WRITE              PIC 9(08)    VALUE ZEROES.              
       01  CNT-REC-MATCH                PIC 9(08)    VALUE ZEROES.              
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES        THRU    1000-EXIT.                   
           PERFORM  2000-READ-F593         THRU    2000-EXIT.                   
           PERFORM  2500-READ-K593         THRU    2500-EXIT.                   
           PERFORM  3000-PROCESS           THRU    3000-EXIT                    
                    UNTIL WS-EOF-F593 = 'Y' AND                                 
                          WS-EOF-K593 = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE     THRU    9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F593                                                 
                       BP13K593                                                 
                OUTPUT P13F593B.                                                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-F593.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F593                                                        
                AT END MOVE 'Y' TO WS-EOF-F593                                  
                       MOVE HIGH-VALUES TO F593-NUM-NRIC                        
                       GO TO 2000-EXIT                                          
           END-READ.                                                            
                                                                                
           ADD 1 TO CNT-F593-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2500-READ-K593.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K593                                                        
                AT END MOVE 'Y' TO WS-EOF-K593                                  
                       MOVE HIGH-VALUES TO K593-NUM-LAST-NRIC                   
                       GO TO 2500-EXIT                                          
           END-READ.                                                            
                                                                                
           ADD 1 TO CNT-K593-READ.                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           IF F593-NUM-NRIC = K593-NUM-LAST-NRIC                                
              ADD 1 TO CNT-REC-MATCH                                            
                                                                                
              PERFORM 4000-WRITE-K593-FND THRU 4000-EXIT                        
                                                                                
              PERFORM 2000-READ-F593 THRU 2000-EXIT                             
              PERFORM 2500-READ-K593 THRU 2500-EXIT                             
           ELSE                                                                 
              IF F593-NUM-NRIC < K593-NUM-LAST-NRIC                             
                 ADD 1 TO CNT-F593-NOTFOUND                                     
                 PERFORM 2000-READ-F593 THRU 2000-EXIT                          
              ELSE                                                              
                 ADD 1 TO CNT-K593-NOTFOUND                                     
                 PERFORM 2500-READ-K593 THRU 2500-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-WRITE-K593-FND.                                                     
      *----------------------------------------------------------------*        
           ADD  1                  TO CNT-F593B-WRITE.                          
           MOVE SPACES             TO P13F593B-REC.                             
           INITIALIZE                 P13F593B-REC.                             
                                                                                
           MOVE BP13F593-REC       TO P13F593B-REC.                             
           WRITE P13F593B-REC.                                                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13F593                                                       
                 BP13K593                                                       
                 P13F593B.                                                      
                                                                                
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** MTCHF593 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BBP13F593) = '                    
                    CNT-F593-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM  (B04K593) = '                      
                    CNT-K593-READ.                                              
           DISPLAY 'NO OF RECORDS MATCH                 = '                     
                    CNT-REC-MATCH.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13F593) = '                     
                    CNT-F593-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13K593) = '                     
                    CNT-K593-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS WRITE FND (F593B) = '                         
                    CNT-F593B-WRITE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
