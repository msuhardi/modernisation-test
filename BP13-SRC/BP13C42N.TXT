       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42N.                                                 
      *AUTHOR.        KSJ3                                                      
      *DATE-WRITTEN.  08/09/2016.                                               
      * ========================================================== *            
      *                SYSTEM OF COMMITMENT (BP13)                 *            
      * ========================================================== *            
      *  OBJECTIVE : PROCESS AG07 VOUCHER RETURN FILE (AG07F225)   *            
      *              FOR FORFEITURE AND REFUND                     *            
      *                                                            *            
      *    1. IT IS ASSUMED THAT RECORDS IN THE INPUT FILE ARE:    *            
      *       A. REJECTED: F225-REC(536:2) != SPACES               *            
      *       B. RESALE LEVY VOUCHERS: F225-REC(150:3) = FOR/REF   *            
      *       C. HEADER RECORDS: F225-REC(12:2) = '00'             *            
      *    2. READ BP13K62A. IF FOUND, CLEAR VOUCHER NO AND DATE.  *            
      *                                                            *            
      *  INPUT   :  AG07F225 - AG07 INTERFACE FILE                 *            
      *                                                            *            
      *  I-O     :  BP13K62A - SOC STAMP FEE REFUND FILE           *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP136483  KSJ3  080916  NEW PROGRAM                        *            
      * BP138865  KR13  281021  REMOVED REPORT P13L42NA            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F225 ASSIGN        TO AG07F225.                           
                                                                                
           SELECT BP13K62A ASSIGN        TO BP13K62A                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K62A-KEY-FLD                        
                           ALTERNATE KEY IS K62A-ALT-KEY2                       
                           FILE STATUS   IS WS-K62A-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AG07F225                                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
           COPY AG07F225.                                                       
                                                                                
       FD  BP13K62A                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
           COPY BP13K62A.                                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K62A-STATUS            PIC X(2)    VALUE SPACES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F225-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F225-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K62A-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE-A             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-PAGE-B             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE-A             PIC 9(2)   VALUE 60.                   
           05  WS-CNT-LINE-B             PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO-A              PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-SNO-B              PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F225-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K62A-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K62A-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DDD-FILE-REF.                                             
               10  WS-NUM-REGN           PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE SPACES.               
               10  WS-NUM-TRANS          PIC X(3)   VALUE SPACES.               
               10  FILLER                PIC X(3)   VALUE SPACES.               
           05  WS-NUM-VR.                                                       
               10  WS-NUM-DDD            PIC X(3).                              
               10  WS-NUM-FIN-YEAR       PIC X(2).                              
               10  WS-NUM-VR-SERIAL      PIC X(6).                              
           05  WS-DTE-SUB                PIC X(8).                              
                                                                                
      *-------------------------------------------------------------            
      *    WS VARIABLES PASSED FROM SUB-PROGRAM "AG07C907"                      
      *-------------------------------------------------------------            
       COPY AG7C907C.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-AG07F225-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F225-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  AG07F225                                                 
                I-O    BP13K62A.                                                
                                                                                
           IF WS-K62A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K62A ERROR ' WS-K62A-STATUS                  
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-AG07F225-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07F225 AT END                                                 
                MOVE 'Y'                   TO   WS-F225-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F225-READ.               
           MOVE F225-NUM-DDD-FILE-REF      TO   WS-NUM-DDD-FILE-REF.            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE 'N'                        TO   WS-K62A-FND                     
                                                WS-K62A-EOF.                    
                                                                                
           MOVE SPACES                     TO   BP13K62A-REC.                   
           MOVE WS-NUM-REGN                TO   K62A-NUM-REGN.                  
           PERFORM 3100-BP13K62A-STARTBR   THRU 3100-EXIT.                      
                                                                                
           IF WS-K62A-FND = 'Y'                                                 
      *-------------------------------------------------------------            
      *       PROCESS FORFEIT                                                   
      *-------------------------------------------------------------            
              IF F225-NUM-VR = K62A-FORFEIT-VR                                  
                 MOVE K62A-DTE-FORFEIT-VR  TO   WS-DTE-SUB                      
                 PERFORM 4000-UPD-FORFEIT  THRU 4000-EXIT                       
              END-IF                                                            
                                                                                
      *-------------------------------------------------------------            
      *       PROCESS REFUND                                                    
      *-------------------------------------------------------------            
              IF F225-NUM-VR = K62A-REFUND-VR                                   
                 MOVE K62A-DTE-REFUND-VR   TO   WS-DTE-SUB                      
                 PERFORM 4001-UPD-REFUND   THRU 4001-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-AG07F225-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13K62A-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13K62A KEY = K62A-ALT-KEY2.                                  
                                                                                
           EVALUATE WS-K62A-STATUS                                              
           WHEN '00'                                                            
              PERFORM 3200-BP13K62A-READNEXT    THRU 3200-EXIT                  
                      UNTIL WS-K62A-EOF = 'Y'                                   
                         OR WS-K62A-FND = 'Y'                                   
                                                                                
           WHEN '10'                                                            
           WHEN '23'                                                            
              MOVE 'N'                     TO   WS-K62A-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13K62A ERROR ' WS-K62A-STATUS                    
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-BP13K62A-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13K62A NEXT                                                   
                AT END MOVE 'Y'            TO   WS-K62A-EOF                     
                GO TO 3200-EXIT.                                                
                                                                                
           IF K62A-NUM-REGN NOT = WS-NUM-REGN                                   
              MOVE 'Y'                     TO   WS-K62A-EOF                     
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K62A-NUM-DOC     = 'ALE' AND                                      
              K62A-NUM-DOC-TYP = 'O'   AND                                      
              K62A-NUM-DOC-CNT =  1                                             
              MOVE 'Y'                     TO   WS-K62A-FND                     
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-UPD-FORFEIT.                                                        
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   K62A-DTE-FORFEIT-VR             
                                                K62A-FORFEIT-VR.                
                                                                                
           PERFORM 4010-REWRITE-BP13K62A   THRU 4010-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4001-UPD-REFUND.                                                         
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   K62A-DTE-REFUND-VR              
                                                K62A-REFUND-VR.                 
                                                                                
           PERFORM 4010-REWRITE-BP13K62A   THRU 4010-EXIT.                      
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-REWRITE-BP13K62A.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K62A-REC.                                                
                                                                                
           EVALUATE WS-K62A-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K62A-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K62A ERROR ' WS-K62A-STATUS                  
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C42N CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF AG07F225 READ      : ' WS-CNT-F225-READ.          
           DISPLAY ' 2. NO OF AG07F225 SKIPPED   : ' WS-CNT-F225-SKIP.          
           DISPLAY ' 3. NO OF BP13K62A UPDATED   : ' WS-CNT-K62A-UPD.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE AG07F225                                                       
                 BP13K62A.                                                      
                                                                                
           IF WS-K62A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K62A ERROR ' WS-K62A-STATUS                  
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
