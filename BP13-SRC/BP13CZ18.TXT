       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CZ18.                                                  
      *AUTHOR.       ZARITA DANE SARMIENTO.                                     
      *DATE-WRITTEN. 23/03/2012.                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:  TO GENERATE REPORT THAT SHOWS THE UPDATED     *            
      *              ELIGIBILITY TAG                               *            
      *                                                            *            
      *  INPUT:  BP13F730                                          *            
      *          BP13K730                                          *            
      *  OUTPUT: BP13LZ18                                          *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-NO   CHG-DATE  BY   DESCRIPTION                        *            
      * ------   --------  --   -----------                        *            
      * BP134482 23032012 ZDD1  NEW PGM.                           *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K730-KEY-NEW                               
                  FILE STATUS     IS BP13K730-STATUS.                           
                                                                                
           SELECT BP13LZ18 ASSIGN TO BP13LZ18.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F730                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730                                                            
                                   RECORD CONTAINS 500 CHARACTERS.              
       COPY BP13K730.                                                           
                                                                                
       FD  BP13LZ18                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 132 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       01  BP13LZ18-REC            PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K730-STATUS             PIC 9(2)    VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F520-READ            PIC 9(8)    VALUE ZEROES.                
           05  WS-F730-READ            PIC 9(8)    VALUE ZEROES.                
           05  WS-MATCH                PIC 9(8)    VALUE ZEROES.                
           05  WS-F730-NOTMATCH        PIC 9(8)    VALUE ZEROES.                
           05  WS-K730-WRITE           PIC 9(8)    VALUE ZEROES.                
           05  WS-K730-UPDATE          PIC 9(8)    VALUE ZEROES.                
           05 WS-CNT-LINE-ON-PAGE      PIC 9(8)    VALUE 66.                    
           05 WS-CNT-PRINT-LZ18        PIC 9(8)    VALUE 1.                     
           05 WS-CNT-WRITE-LZ18        PIC 9(8)    VALUE 0.                     
           05 WS-NO-OF-PAGE            PIC 9(8)    VALUE 0.                     
           05 F730-EOF                 PIC X(1)    VALUE 'N'.                   
           05  WS-CUR-DATE             PIC X(8)    VALUE SPACES.                
           05 WS-WRITE-REPORT          PIC X(1)    VALUE 'N'.                   
                                                                                
      *-----------------------------------------------------------*             
      *    BP13LZ18 - LAYOUT                                      *             
      *-----------------------------------------------------------*             
                                                                                
       01  LZ18-HEAD-01.                                                        
           05  FILLER                   PIC X(14) VALUE 'BP13LZ18'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LZ18-PRINT-DATE          PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  LZ18-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  LZ18-HEAD-02.                                                        
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
                '     AFTER UPDATE OF THE ELIGIBILITY TAG'.                     
                                                                                
       01  LZ18-HEAD-03.                                                        
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(05)  VALUE ' S-NO'.                       
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  FILLER           PIC X(10)  VALUE 'REGN NO'.                     
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  FILLER           PIC X(09)  VALUE 'NRIC     '.                   
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  FILLER           PIC X(04)  VALUE 'APPS'.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  FILLER           PIC X(04)  VALUE 'ELIG'.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  FILLER           PIC X(04)  VALUE 'MCPS'.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  FILLER           PIC X(04)  VALUE 'ELIG'.                        
                                                                                
       01  LZ18-HEAD-04.                                                        
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  LZ18-DETAILS.                                                        
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  LZ18-SNO         PIC ZZZZ9.                                      
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  LZ18-REGNO       PIC X(10)  VALUE SPACES.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  LZ18-NRIC        PIC X(09)  VALUE SPACES.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  LZ18-APPS        PIC X(04)  VALUE SPACES.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  LZ18-ELIG-APPS   PIC X(04)  VALUE SPACES.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  LZ18-MCPS        PIC X(04)  VALUE SPACES.                        
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  LZ18-ELIG-MCPS   PIC X(04)  VALUE SPACES.                        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
           PERFORM 1000-INITIALIZATION  THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F730   THRU 2000-EXIT.                         
           PERFORM 4000-PROCESS-RECORD  THRU 4000-EXIT                          
              UNTIL F730-EOF = 'Y'                                              
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F730                                                 
                       BP13K730                                                 
               OUTPUT  BP13LZ18.                                                
                                                                                
           IF BP13K730-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K730 - OPEN FILE ERROR, ' BP13K730-STATUS            
              MOVE BP13K730-STATUS     TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
           STRING WS-CUR-DATE(7:2) '/'                                          
                  WS-CUR-DATE(5:2) '/'                                          
                  WS-CUR-DATE(1:4)   DELIMITED BY SIZE INTO                     
                  LZ18-PRINT-DATE.                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F730.                                                      
      *-------------------------------------------------------------            
           READ BP13F730 AT END                                                 
               MOVE 'Y'         TO F730-EOF                                     
               GO TO 2000-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 5000-READ-K730   THRU 5000-EXIT.                             
                                                                                
           IF ((K730-NUM-APPS NOT  = SPACES AND LOW-VALUES) OR                  
               (K730-NUM-APPS-ELIG-TAG NOT  = SPACES AND LOW-VALUES))           
              PERFORM 6000-PRINT-REPORT  THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730   THRU 2000-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-READ-K730.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES           TO K730-NUM-REGN.                              
           MOVE F730-NUM-REGN    TO K730-NUM-REGN.                              
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE BP13K730-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 DISPLAY 'REG NO NOT FOUND : ' K730-NUM-REGN                    
              WHEN OTHER                                                        
                 DISPLAY ' ERROR READING BP13K730 TRANS REC '                   
                         ' STATUS ' BP13K730-STATUS                             
                 DISPLAY ' KEY IS ' K730-NUM-REGN                               
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-PRINT-REPORT.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE K730-NUM-REGN                TO LZ18-REGNO.                     
           MOVE K730-NUM-NRIC1               TO LZ18-NRIC.                      
                                                                                
           MOVE K730-NUM-APPS                TO LZ18-APPS.                      
           MOVE K730-NUM-APPS-ELIG-TAG       TO LZ18-ELIG-APPS.                 
           MOVE K730-NUM-MCPS                TO LZ18-MCPS.                      
           MOVE K730-CDE-RESIDENT-HA1        TO LZ18-ELIG-MCPS.                 
           PERFORM 6300-WRITE-BP13LZ18     THRU 6300-EXIT.                      
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6300-WRITE-BP13LZ18.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 53                                          
              PERFORM 6400-PRINT-HEADING THRU 6400-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-LZ18  TO    LZ18-SNO.                              
                                                                                
           WRITE BP13LZ18-REC      FROM  LZ18-DETAILS   AFTER 1.                
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE                    
                                         WS-CNT-PRINT-LZ18                      
                                         WS-CNT-WRITE-LZ18.                     
                                                                                
           MOVE  SPACES            TO    LZ18-DETAILS.                          
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6400-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     LZ18-PRINT-PAGE.                        
                                                                                
           MOVE SPACES           TO     BP13LZ18-REC.                           
                                                                                
           WRITE BP13LZ18-REC    AFTER  PAGE.                                   
           WRITE BP13LZ18-REC    FROM   LZ18-HEAD-01 AFTER 1.                   
           WRITE BP13LZ18-REC    FROM   LZ18-HEAD-02 AFTER 1.                   
           WRITE BP13LZ18-REC    FROM   LZ18-HEAD-03 AFTER 2.                   
           WRITE BP13LZ18-REC    FROM   LZ18-HEAD-04 AFTER 1.                   
                                                                                
           MOVE 4  TO WS-CNT-LINE-ON-PAGE.                                      
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C520 ------------- * '.                  
           DISPLAY 'NO OF REC READ BP13F730     = ' WS-F730-READ.               
           DISPLAY 'NO OF REC WRITTEN IN LZ18   = ' WS-CNT-WRITE-LZ18.          
                                                                                
           CLOSE BP13F730                                                       
                 BP13K730                                                       
                 BP13LZ18.                                                      
                                                                                
           IF BP13K730-STATUS NOT = 00                                          
              DISPLAY 'BP13K730 - CLOSE FILE ERROR, ' BP13K730-STATUS           
              MOVE BP13K730-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
