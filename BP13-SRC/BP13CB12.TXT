       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB12.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  06/06/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES   : UPDATE ALLOC-SCH, PPS-SUCC AND AST-SUCC    *            
      *                 FOR ELIGIBLE APPLICATIONS SHORTLISTED      *            
      *                 UNDER BP13APPZ                             *            
      *                                                            *            
      *  INPUT FILE   : BP13F740 - SUCCESSFUL CASES                *            
      *  I-O   FILE   : BP13K740 - BP13.K740.MATUREBA              *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP136291 SMR2 06062016 NEW PROGRAM                         *            
      * BP139670 AM25 25082023 CATER FOR FT(PMC)                   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
                                                                                
           SELECT BP13K740  ASSIGN       TO BP13K740                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K740-KEY-FLD                        
                            FILE STATUS  IS WS-K740-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K740-STATUS              PIC 9(02)  VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-FND                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-NTFND               PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-SAP                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-ELD                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-FTS                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-AST                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-MCP                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-UPG                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-PPS                 PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-NH                  PIC 9(06)  VALUE ZEROES.             
           05  WS-K740-UPD                 PIC 9(06)  VALUE ZEROES.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                 PIC X(01)  VALUE 'N'.                
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                     PIC X(08)  VALUE SPACES.             
           05  WS-DATE-EDIT                PIC X(10)  VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
                I-O   BP13K740.                                                 
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K740 ERROR ' WS-K740-STATUS                  
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-DATE.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F740-READ.                     
           PERFORM 3000-READ-BP13K740      THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K740.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13K740-REC.                     
           INITIALIZE                         BP13K740-REC.                     
                                                                                
           MOVE F740-KEY-FLD               TO K740-KEY-FLD.                     
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K740-FND                       
              PERFORM 4000-PROCESS-RECS    THRU 4000-EXIT                       
                                                                                
           WHEN 23                                                              
              ADD 1                        TO WS-K740-NTFND                     
              DISPLAY 'RECORD NOT FOUND IN BP13K740 : ' F740-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K740   : ' WS-K740-STATUS              
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-RECS.                                                       
      *-------------------------------------------------------------            
           IF K740-NUM-ELDERLY = 'F' OR 'S'                                     
              IF ((K740-NUM-APPS NOT = SPACES AND LOW-VALUES) AND               
                  (K740-NUM-APPS-ELIG-TAG = '1' OR 'T')) OR                     
                 ((K740-NUM-MCPS = 'P' OR 'C' OR 'M') AND                       
                  (K740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'))                  
                 MOVE K740-CDE-ALLOC-SCH   TO K740-CDE-REQ-ALLOC-SCH            
                 MOVE 'SAP'                TO K740-CDE-ALLOC-SCH                
                 ADD 1                     TO WS-K740-SAP                       
                 PERFORM 5000-REWRITE-BP13K740 THRU 5000-EXIT                   
              ELSE                                                              
                 ADD 1                     TO WS-K740-ELD                       
              END-IF                                                            
           ELSE                                                                 
              IF K740-CDE-ALLOC-SCH = 'FTS'                                     
                 ADD 1                     TO WS-K740-FTS                       
              ELSE                                                              
                 IF K740-CDE-BALLOT-HOUSEHOLD = 'G'                             
                    IF K740-NUM-AST = 'Y'                                       
                       MOVE 'Y'            TO K740-NUM-AST-SUCC                 
                       ADD 1               TO WS-K740-AST                       
                       PERFORM 5000-REWRITE-BP13K740 THRU 5000-EXIT             
                    ELSE                                                        
                       IF K740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'             
                          IF K740-NUM-MCPS = 'M' OR 'B' OR 'C' OR 'P'           
                             MOVE K740-CDE-ALLOC-SCH                            
                                           TO K740-CDE-REQ-ALLOC-SCH            
                             MOVE 'MCP'    TO K740-CDE-ALLOC-SCH                
                             ADD 1         TO WS-K740-MCP                       
                             PERFORM 5000-REWRITE-BP13K740                      
                                           THRU 5000-EXIT                       
                          END-IF                                                
                       ELSE                                                     
                          ADD 1            TO WS-K740-UPG                       
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    IF K740-NUM-PPS-ELIG-TAG = 'Y' OR 'E' OR 'F'                
                       MOVE 'Y'            TO K740-NUM-PPS-SUCC                 
                       ADD 1               TO WS-K740-PPS                       
                       PERFORM 5000-REWRITE-BP13K740 THRU 5000-EXIT             
                    ELSE                                                        
                       IF K740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'             
                          IF K740-NUM-MCPS = 'M' OR 'B' OR 'C' OR 'P'           
                             MOVE K740-CDE-ALLOC-SCH                            
                                           TO K740-CDE-REQ-ALLOC-SCH            
                             MOVE 'MCP'    TO K740-CDE-ALLOC-SCH                
                             ADD 1         TO WS-K740-MCP                       
                             PERFORM 5000-REWRITE-BP13K740                      
                                           THRU 5000-EXIT                       
                          END-IF                                                
                       ELSE                                                     
                          ADD 1            TO WS-K740-NH                        
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-REWRITE-BP13K740.                                                   
      *-------------------------------------------------------------            
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K740-UPD                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K740. STATUS IS - '                 
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '============================================='.             
           DISPLAY '   BP13CB12 CONTROL TOTAL   : ' WS-DATE-EDIT.               
           DISPLAY '---------------------------------------------'.             
           DISPLAY ' NO OF RECS READ FROM BP13F740    : ' WS-F740-READ.         
           DISPLAY ' NO OF RECS FOUND IN BP13K740     : ' WS-K740-FND.          
           DISPLAY ' NO OF RECS NOT FOUND IN BP13K740 : ' WS-K740-NTFND.        
           DISPLAY ' NO OF SAP - ELD RECS READ        : ' WS-K740-SAP.          
           DISPLAY ' NO OF NON-SAP - ELD RECS READ    : ' WS-K740-ELD.          
           DISPLAY ' NO OF FTS RECS READ              : ' WS-K740-FTS.          
           DISPLAY ' NO OF AST RECS READ              : ' WS-K740-AST.          
           DISPLAY ' NO OF MCP RECS READ              : ' WS-K740-MCP.          
           DISPLAY ' NO OF SECOND TIMER RECS READ     : ' WS-K740-UPG.          
           DISPLAY ' NO OF PPS RECS READ              : ' WS-K740-PPS.          
           DISPLAY ' NO OF FIRST TIMER RECS READ      : ' WS-K740-NH.           
           DISPLAY ' NO OF RECS UPDATED TO BP13K740   : ' WS-K740-UPD.          
           DISPLAY '============================================='.             
                                                                                
           CLOSE  BP13F740                                                      
                  BP13K740.                                                     
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K740 ERROR ' WS-K740-STATUS                  
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
