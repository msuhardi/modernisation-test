       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC71.                                                 
       AUTHOR.        ARIEL HERNANDEZ.                                          
       DATE-WRITTEN.  15/03/2007.                                               
      *****************************************************************         
      * OBJECTIVE  - TO CREATE AC08F582 INTERFACE FILE.                         
      *              (CREATE GIRO FILE)                                         
      *                                                                         
      * INPUT FILES  : SY02F001                                                 
      *                BP13KD00                                                 
      *                BP13KD63                                                 
      *                                                                         
      * REPORT OUTPUT: BP13LC71                                                 
      *                                                                         
      * SUBROUTINE    - NIL                                                     
      *                                                                         
      * IDENTICAL PGM - NIL                                                     
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO   DATE     BY   AMENDMENTS/ENHANCEMENTS                          
      * -------- ----     ---  -----------------------                          
      * BP132888 15032007 BA3  NEW PROGRAM.                                     
      * BP133052 22052007 BA3  INCLUDE REPORT AND READING TO SY02F001           
      * BP133091 17102007 RB12 EXPAND BP13KD00 FROM 1200 TO 1600 BYTES          
      * BP133209 18032008 JB8  ADD CUR-CPF-ARR,AMT-BILL-GIRO,CPF-STATUS         
      *                        IN BP13LC71,DELETE OUTPUT FILE AC08F582          
      * BP133360 27082008 JB8  OMIT 'CASH' 'GIRO' 'CASH-L'                      
      * BP133367 01042009 JB8  CATER FOR 'CASH-L' WITH 'INT-R'                  
      * BP135339 17022015 ZDD1 USE KD00-NUM-GIRO FOR LC71-GIRO-REF-NO           
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13KD00  ASSIGN        TO BP13KD00                          
                             ACCESS MODE   IS SEQUENTIAL                        
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD00-KEY-FLD                      
                             FILE STATUS   IS WS-KD00-STATUS.                   
                                                                                
           SELECT  BP13KD63  ASSIGN        TO BP13KD63                          
                             ACCESS MODE   IS SEQUENTIAL                        
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD63-KEY-FLD                      
                             FILE STATUS   IS WS-KD63-STATUS.                   
                                                                                
           SELECT  SY02F001  ASSIGN TO SY02F001.                                
      *    SELECT  AC08F582  ASSIGN TO AC08F582.                                
           SELECT  BP13LC71  ASSIGN TO BP13LC71.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD63                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13KD63.                                                           
                                                                                
      *FD  AC08F582                                                             
      *    BLOCK CONTAINS 0 RECORDS                                             
      *    RECORD CONTAINS 100 CHARACTERS                                       
      *    LABEL RECORDS ARE STANDARD.                                          
      *COPY AC08F582.                                                           
                                                                                
       FD  BP13LC71                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE  IS  F                                                
           DATA RECORD     IS  PRINT-REC.                                       
       01  PRINT-REC                    PIC X(132).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SN                             PIC 9(05) VALUE 0.              
           05 WS-LC71-WRITE                     PIC 9(05) VALUE 0.              
           05 WS-AMT-ARR-INSTAL-KD00            PIC 9(8)V99 VALUE 0.            
           05 WS-CPF-ARR-KD63                   PIC 9(8)V99 VALUE 0.            
                                                                                
       01  WS-AMTS.                                                             
           05  WS-AMT-LOAN OCCURS 40 TIMES      PIC 9(8)V99 VALUE 0.            
           05  WS-DTE-PROCESS1 OCCURS 40 TIMES  PIC X(08) VALUE SPACES.         
           05  WS-DTE-PROCESS2 OCCURS 20 TIMES  PIC X(08) VALUE SPACES.         
           05  WS-CTR1                          PIC 9(02) VALUE 0.              
           05  WS-CTR2                          PIC 9(02) VALUE 0.              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KD00-STATUS                   PIC 99    VALUE 0.              
           05  WS-KD63-STATUS                   PIC 99    VALUE 0.              
                                                                                
       01  WS-SWITCH.                                                           
           05  WS-END-KD00                      PIC X(1)  VALUE 'N'.            
               88 WS-EOF-KD00                   VALUE  'Y'.                     
           05  WS-EOF-KD63                      PIC X(1)  VALUE 'N'.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-KD00-READ                 PIC 9(8)  VALUE 0.              
           05  WS-CNT-F530-WRITE                PIC 9(8)  VALUE 0.              
           05  WS-CNT-BYPASS                    PIC 9(8)  VALUE 0.              
           05  WS-LINE-CNT                      PIC 9(2)  VALUE 60.             
           05  WS-PAGE-CNT                      PIC 9(5)  VALUE 0.              
                                                                                
       01  WS-DATES.                                                            
           05 WS-SYS-DATE                       PIC X(8)  VALUE SPACES.         
                                                                                
       01  LC71-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC71'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LC71-DATE               PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LC71-PAGENO             PIC ZZZZ9 VALUE ZEROES.                  
                                                                                
       01  LC71-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(38) VALUE SPACES.                  
           05  FILLER                  PIC X(50) VALUE                          
               ' REPORT ON CREATING GIRO INTERFACE FILE - AC08F582'.            
                                                                                
       01  LC71-PR-HEAD-03A.                                                    
           05  FILLER                  PIC X(76) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'CURRENT'.               
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'CURRENT'.               
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE 'AMT'.                   
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE 'CPF'.                   
       01  LC71-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE 'S/N'.                   
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE ' REG-NO '.              
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE ' SCH-A/C-NO'.           
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'GIRO-TAG   '.           
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                           'AMT-ARR-INSTAL'.                    
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE                          
                                           'CPF-ARR'.                           
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                           'BILLED-BY-GIRO'.                    
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE                          
                                           'STATUS'.                            
       01  LC71-PR-DETAILS.                                                     
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LC71-SN                 PIC ZZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  LC71-REG-NO             PIC X(8)  VALUE SPACES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  LC71-SCHEME             PIC X(14) VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE SPACES.                  
           05  LC71-GIRO-TAG           PIC X(21) VALUE SPACES.                  
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  LC71-CURR-ARR-INSTAL    PIC $,$$$,$$9.99                         
                                                 VALUE ZEROES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  LC71-CURR-CPF-ARR       PIC $,$$$,$$9.99                         
                                                 VALUE ZEROES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  LC71-AMT-BILLED-GIRO    PIC $,$$$,$$9.99                         
                                                 VALUE ZEROES.                  
           05  FILLER                  PIC X(7)  VALUE SPACES.                  
           05  LC71-CDE-CPF-STATUS     PIC X(1)  VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ********************************************************                  
       0000-MAIN-LOGIC.                                                         
      ********************************************************                  
                                                                                
           PERFORM 1000-START-UP                THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13KD00           THRU 2000-EXIT.                 
           PERFORM 3000-MAIN-PROCESS            THRU 3000-EXIT                  
                   UNTIL WS-EOF-KD00.                                           
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
      ********************************************************                  
       1000-START-UP.                                                           
      ********************************************************                  
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13KD00                                                 
                       BP13KD63                                                 
                OUTPUT BP13LC71.                                                
      *                AC08F582.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR OPENING BP13KD00 ' WS-KD00-STATUS                 
              MOVE WS-KD00-STATUS               TO RETURN-CODE                  
              GO TO 9999-WRAP-UP.                                               
                                                                                
           IF WS-KD63-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR OPENING BP13KD63 ' WS-KD63-STATUS                 
              MOVE WS-KD63-STATUS               TO RETURN-CODE                  
              GO TO 9999-WRAP-UP.                                               
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-SYS-DATE.                           
                                                                                
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE INTO LC71-DATE.                             
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       2000-READ-BP13KD00.                                                      
      ********************************************************                  
                                                                                
           READ BP13KD00 AT END MOVE 'Y'   TO  WS-END-KD00                      
                         GO TO 2000-EXIT.                                       
           ADD 1         TO WS-CNT-KD00-READ.                                   
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       3000-MAIN-PROCESS.                                                       
      ********************************************************                  
                                                                                
           IF KD00-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE ZEROS TO KD00-AMT-ARR-INSTAL                                 
           END-IF.                                                              
                                                                                
           IF ((KD00-AMT-ARR-INSTAL > ZEROES) AND                               
              ((KD00-NUM-GIRO = 'Y') OR                                         
               (KD00-NUM-GIRO-INFO NOT = SPACES AND                             
                                       '000000000000000000')))                  
              COMPUTE WS-AMT-ARR-INSTAL-KD00 = KD00-AMT-INSTAL-CPF1 +           
                                               KD00-AMT-INSTAL-CPF2 +           
                                               KD00-AMT-INSTAL-CPF3 +           
                                               KD00-AMT-INSTAL-CPF4             
              PERFORM 3500-BROWSE-BP13KD63 THRU 3500-EXIT                       
      *       PERFORM 4000-WRITE-AC08F582 THRU 4000-EXIT                        
              PERFORM 5000-PRINT-REPORT   THRU 5000-EXIT                        
           ELSE                                                                 
              ADD +1  TO WS-CNT-BYPASS                                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13KD00     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       3500-BROWSE-BP13KD63.                                                    
      ********************************************************                  
                                                                                
           MOVE SPACES                  TO BP13KD63-REC                         
           INITIALIZE                      BP13KD63-REC.                        
                                                                                
           MOVE KD00-NUM-REGN           TO KD63-NUM-REGN.                       
           MOVE WS-SYS-DATE(1:6)        TO KD63-DTE-PROCESS(1:6)                
           MOVE 'N'                     TO WS-EOF-KD63.                         
           MOVE ZEROES                  TO WS-CPF-ARR-KD63.                     
           INITIALIZE WS-AMTS.                                                  
                                                                                
           START BP13KD63 KEY >= KD63-KEY-FLD.                                  
                                                                                
           IF WS-KD63-STATUS = 00 OR 02                                         
              PERFORM UNTIL WS-EOF-KD63 = 'Y'                                   
                 PERFORM 3550-READNEXT-BP13KD63  THRU 3550-EXIT                 
              END-PERFORM                                                       
              PERFORM 3600-CHECK-INTR            THRU 3600-EXIT                 
           ELSE                                                                 
              IF WS-KD63-STATUS = 23                                            
                 GO TO 3500-EXIT                                                
              ELSE                                                              
                 DISPLAY 'BP13KD63 : START ERROR' WS-KD63-STATUS                
                 MOVE WS-KD63-STATUS           TO RETURN-CODE                   
                 GO TO 9999-WRAP-UP                                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       3550-READNEXT-BP13KD63.                                                  
      ********************************************************                  
                                                                                
           READ BP13KD63 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF-KD63                                  
                GO TO 3550-EXIT.                                                
                                                                                
           IF WS-KD63-STATUS EQUAL 00 OR 02                                     
              IF KD63-NUM-REGN = KD00-NUM-REGN                                  
                 IF KD63-DTE-PROCESS(1:6) = WS-SYS-DATE(1:6)                    
                    IF KD63-TXT-DESC = 'CPF' OR 'CPF-A' OR 'CPF-L'              
                       ADD KD63-AMT-LOAN       TO WS-CPF-ARR-KD63               
                    END-IF                                                      
                    IF KD63-TXT-DESC = 'CPF-L'                                  
                       ADD 1                 TO WS-CTR1                         
                       MOVE KD63-AMT-LOAN    TO WS-AMT-LOAN(WS-CTR1)            
                       MOVE KD63-DTE-PROCESS TO WS-DTE-PROCESS1(WS-CTR1)        
                    END-IF                                                      
                    IF KD63-TXT-DESC = 'INT-R'                                  
                       ADD 1                 TO WS-CTR2                         
                       MOVE KD63-DTE-PROCESS TO WS-DTE-PROCESS2(WS-CTR2)        
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'                   TO WS-EOF-KD63                   
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                      TO WS-EOF-KD63                   
              END-IF                                                            
           ELSE                                                                 
              IF WS-KD63-STATUS EQUAL 23                                        
                 MOVE 'Y'                      TO WS-EOF-KD63                   
              ELSE                                                              
                 DISPLAY 'BP13KD63 : READ NEXT ERROR BY REGN :'                 
                                                  WS-KD63-STATUS                
                 MOVE WS-KD63-STATUS           TO RETURN-CODE                   
                 GO TO 9999-WRAP-UP                                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3550-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       3600-CHECK-INTR.                                                         
      ********************************************************                  
                                                                                
           PERFORM VARYING WS-CTR2 FROM 1 BY 1                                  
                   UNTIL WS-DTE-PROCESS2(WS-CTR2) = SPACES                      
                      OR WS-CTR2 > 20                                           
              PERFORM VARYING WS-CTR1 FROM 1 BY 1                               
                      UNTIL WS-DTE-PROCESS1(WS-CTR1) = SPACES                   
                         OR WS-CTR1 > 40                                        
                 IF WS-DTE-PROCESS1(WS-CTR1) = WS-DTE-PROCESS2(WS-CTR2)         
                    COMPUTE WS-CPF-ARR-KD63 = WS-CPF-ARR-KD63 -                 
                                              WS-AMT-LOAN(WS-CTR1)              
                 END-IF                                                         
              END-PERFORM                                                       
           END-PERFORM.                                                         
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************************************************                  
      *4000-WRITE-AC08F582.                                                     
      ********************************************************                  
                                                                                
      *    IF KD00-AMT-ARR-INSTAL IS NOT NUMERIC                                
      *       MOVE ZEROES  TO  KD00-AMT-ARR-INSTAL                              
      *    END-IF.                                                              
                                                                                
      *    MOVE SPACES                          TO AC08F582-REC.                
      *    INITIALIZE AC08F582-REC.                                             
                                                                                
      *    MOVE KD00-NUM-SCH-ACCNT              TO F582-NUM-HDB-REF.            
      *    MOVE 'A'                             TO F582-NUM-DEDN-TYP.           
                                                                                
      *    MOVE KD00-AMT-ARR-INSTAL             TO F582-AMT-DEDN-LN.            
      *    MOVE ZEROES                          TO F582-AMT-DEDN-PF.            
                                                                                
      *    MOVE KD00-NUM-GIRO-INFO              TO F582-NUM-BANK-REF.           
                                                                                
      *    WRITE AC08F582-REC.                                                  
      *    ADD 1 TO WS-CNT-F530-WRITE.                                          
                                                                                
      *4000-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       5000-PRINT-REPORT.                                                       
      ********************************************************                  
           MOVE SPACES                    TO LC71-PR-DETAILS.                   
           IF WS-LINE-CNT > 50                                                  
              PERFORM 5500-WRITE-HEADING  THRU 5500-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1                          TO WS-SN                              
                                             WS-LC71-WRITE.                     
                                                                                
           MOVE WS-SN                     TO LC71-SN.                           
           MOVE KD00-NUM-REGN             TO LC71-REG-NO.                       
           STRING KD00-NUM-SCH-ACCNT(1:4) '-'                                   
                  KD00-NUM-SCH-ACCNT(5:4) '-'                                   
                  KD00-NUM-SCH-ACCNT(9:3)                                       
                  DELIMITED BY SIZE     INTO LC71-SCHEME                        
                  END-STRING.                                                   
                                                                                
           MOVE KD00-NUM-GIRO  TO LC71-GIRO-TAG.                                
                                                                                
      *    STRING KD00-CDE-GIRO-BANK   '-'                                      
      *           KD00-CDE-GIRO-BRANCH '-'                                      
      *           KD00-NUM-GIRO-REF                                             
      *           DELIMITED BY SIZE     INTO LC71-GIRO-REF-NO                   
      *           END-STRING.                                                   
                                                                                
           MOVE KD00-AMT-ARR-INSTAL       TO LC71-CURR-ARR-INSTAL               
           MOVE KD00-CDE-CPF-STATUS       TO LC71-CDE-CPF-STATUS                
           COMPUTE WS-CPF-ARR-KD63 = WS-AMT-ARR-INSTAL-KD00 -                   
                                     WS-CPF-ARR-KD63                            
           MOVE WS-CPF-ARR-KD63           TO LC71-CURR-CPF-ARR                  
           COMPUTE WS-CPF-ARR-KD63 = KD00-AMT-ARR-INSTAL -                      
                                     WS-CPF-ARR-KD63                            
           MOVE WS-CPF-ARR-KD63           TO LC71-AMT-BILLED-GIRO.              
                                                                                
           ADD 2                          TO WS-LINE-CNT.                       
           WRITE PRINT-REC                FROM LC71-PR-DETAILS AFTER 2.         
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       5500-WRITE-HEADING.                                                      
      ********************************************************                  
           ADD 1                          TO WS-PAGE-CNT.                       
           MOVE WS-PAGE-CNT               TO LC71-PAGENO.                       
                                                                                
           MOVE  SPACES                   TO PRINT-REC.                         
           WRITE PRINT-REC                       AFTER PAGE.                    
           WRITE PRINT-REC FROM LC71-PR-HEAD-01  AFTER 3.                       
           WRITE PRINT-REC FROM LC71-PR-HEAD-02  AFTER 1.                       
           WRITE PRINT-REC FROM LC71-PR-HEAD-03A AFTER 2.                       
           WRITE PRINT-REC FROM LC71-PR-HEAD-03  AFTER 1.                       
           MOVE 8                         TO WS-LINE-CNT.                       
                                                                                
                                                                                
       5500-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       9999-WRAP-UP.                                                            
      ********************************************************                  
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '******* CONTROL   TOTALS *******'.                          
           DISPLAY 'PROGRAM-ID : BP13CC71'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY '                         '.                                 
           DISPLAY ' NUMBER OF BP13KD00 RECORDS READ           : '              
                   WS-CNT-KD00-READ.                                            
           DISPLAY ' NUMBER OF BP13KD00 RECORDS BYPASS         : '              
                   WS-CNT-BYPASS.                                               
      *    DISPLAY ' NUMBER OF AC08F582 RECS WRITTEN           : '              
      *            WS-CNT-F530-WRITE.                                           
           DISPLAY ' NUMBER OF BP13LC71 RECS WRITTEN           : '              
                   WS-LC71-WRITE.                                               
                                                                                
           CLOSE SY02F001                                                       
                 BP13KD00                                                       
                 BP13KD63                                                       
      *          AC08F582                                                       
                 BP13LC71.                                                      
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY ' ERROR CLOSING BP13KD00 ' WS-KD00-STATUS                 
              MOVE WS-KD00-STATUS               TO RETURN-CODE.                 
                                                                                
           IF WS-KD63-STATUS NOT = 00                                           
              DISPLAY ' ERROR CLOSING BP13KD63 ' WS-KD63-STATUS                 
              MOVE WS-KD63-STATUS               TO RETURN-CODE.                 
                                                                                
           STOP RUN.                                                            
