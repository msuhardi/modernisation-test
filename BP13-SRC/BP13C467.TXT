      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C467.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  JANUARY 18, 2017.                                         
      *===============================================================*         
      * OBJECTIVE  :  TO MATCH BP13F410 AND WRITE TO AB20F302         *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. AB20F302                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136598 18/01/2017 PCL4 NEW PROGRAM                          *         
      * BP136598 24/01/2017 PCL4 TO ADD BUC CODE                      *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  AB20F302  ASSIGN        TO AB20F302.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F410.                                                           
                                                                                
       FD   AB20F302                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AB20F302.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01  WS-LINK-BP13C913.                                                    
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH        PIC X(4).                                
               10  LINK-NUM-ACC        PIC X(5).                                
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
               OUTPUT  AB20F302.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F410-DTE-FI-EFFECTIVE NOT = SPACES AND LOW-VALUES                 
                                                 AND ZEROES                     
               PERFORM 4000-PROCESS-AB20F302     THRU 4000-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-AB20F302.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO AB20F302-REC.                   
           INITIALIZE                           AB20F302-REC.                   
                                                                                
           PERFORM 5000-CALL-BP13C913      THRU 5000-EXIT.                      
                                                                                
           MOVE F410-NUM-SCH-ACC             TO F302-NUM-HDB-REF.               
                                                                                
           MOVE COMM8-NUM-BLK                TO F302-NUM-BLK.                   
           MOVE COMM8-NUM-LEVEL              TO F302-NUM-LVL.                   
           MOVE COMM8-NUM-UNIT-MAIN          TO F302-NUM-UNIT.                  
           MOVE COMM8-NME-STREET             TO F302-NME-STFULL.                
           MOVE COMM8-NUM-POSTAL-CODE        TO F302-NUM-POST-CODE.             
           MOVE COMM8-CDE-BUC                TO F302-CDE-BUC.                   
                                                                                
           MOVE F410-CDE-BO                  TO F302-NUM-BO.                    
           MOVE SPACES                       TO F302-CDE-COMPULSORY.            
           MOVE F410-DTE-FI-EFFECTIVE        TO F302-DTE-FI-EFF-OLD.            
                                                                                
           IF F410-NUM-FI-PERIOD NOT NUMERIC                                    
              MOVE ZEROES                    TO F410-NUM-FI-PERIOD              
           END-IF.                                                              
                                                                                
           MOVE F410-NUM-FI-PERIOD           TO F302-NUM-TERM-OLD.              
                                                                                
           WRITE AB20F302-REC.                                                  
           ADD 1                             TO WS-CNT-WRITE.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-CALL-BP13C913.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO BP13COMM8-REC.                  
           INITIALIZE                           BP13COMM8-REC.                  
                                                                                
           MOVE F410-NUM-SCH-ACC             TO LINK-NUM-SCH-ACC.               
                                                                                
           CALL 'BP13C913' USING WS-LINK-BP13C913,                              
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C467'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ (SA/TP)..... : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF AB20F302 RECORDS WRITTEN.......... : '            
                    WS-CNT-WRITE.                                               
                                                                                
           CLOSE    BP13F410                                                    
                    AB20F302.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
