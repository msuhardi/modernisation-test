       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13CAA4.                                                    
      *AUTHOR. RJB1.                                                            
      *DATE-WRITTEN. 08/11/16.                                                  
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (SOC)                             *                
      * ====================================================== *                
      *  - HOUSE KEEPING OF RECORDS AFTER KEY ISSUE            *                
      *                                                        *                
      *  INPUT FILES:                                          *                
      *    1. BP13.F410.DAILYSO                                *                
      *                                                        *                
      *  INPUT OUTPUT FILES:                                   *                
      *    1. BP13.KH50.UPD.FP                                 *                
      *    2. BP13.K859.PCD.EMAIL                              *                
      *                                                        *                
      *  OUTPUT FILES:                                         *                
      *    1. BP13FH50                                         *                
      *    2. BP13F859                                         *                
      *--------------------------------------------------------*                
      * CHG REF  BY   DATE      DESCRIPTION                    *                
      * -------- ---- --------  -----------                    *                
      * BP136539 RJB1 23/11/16  NEW PROGRAM                    *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410  ASSIGN TO BP13F410.                                 
                                                                                
           SELECT BP13K859  ASSIGN TO BP13K859                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K859-KEY-FLD                              
                  FILE STATUS      IS BP13K859-STATUS.                          
                                                                                
           SELECT BP13KH50  ASSIGN TO BP13KH50                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS KH50-KEY-FLD                              
                  FILE STATUS      IS BP13KH50-STATUS.                          
                                                                                
           SELECT BP13FH50  ASSIGN TO BP13FH50                                  
                  FILE STATUS      IS BP13FH50-STATUS.                          
           SELECT BP13F859  ASSIGN TO BP13F859                                  
                  FILE STATUS      IS BP13F859-STATUS.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F410                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F410.                                                           
                                                                                
       FD   BP13KH50                                                            
            RECORD CONTAINS 3000 CHARACTERS.                                    
       COPY BP13KH50.                                                           
                                                                                
       FD   BP13K859                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13K859.                                                           
                                                                                
       FD   BP13FH50                                                            
            RECORD CONTAINS 3000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13FH50.                                                           
                                                                                
       FD   BP13F859                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F859.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-F410-READ            PIC 9(08)    VALUE ZERO.                     
       01  F410-EOF                PIC X        VALUE 'N'.                      
       01  WS-KH50-FND             PIC X        VALUE 'N'.                      
       01  WS-K859-FND             PIC X        VALUE 'N'.                      
       01  BP13KH50-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BP13K859-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BP13FH50-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BP13F859-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  WS-FH50-WRITE           PIC 9(08)    VALUE ZEROES.                   
       01  WS-F859-WRITE           PIC 9(08)    VALUE ZEROES.                   
       01  WS-KH50-DELETE          PIC 9(08)    VALUE ZEROES.                   
       01  WS-K859-DELETE          PIC 9(08)    VALUE ZEROES.                   
                                                                                
       01  WS-DATE.                                                             
           05  WS-DATE-CCYY        PIC 9(4)     VALUE ZEROES.                   
           05  WS-DATE-MM          PIC 9(2)     VALUE ZEROES.                   
           05  WS-DATE-DD          PIC 9(2)     VALUE ZEROES.                   
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-READ-F410 THRU 2000-EXIT.                               
           PERFORM 3000-PROCESS-ROUTINE THRU 3000-EXIT                          
                   UNTIL F410-EOF = 'Y'.                                        
           PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                         
                                                                                
       0000-FINISH.                                                             
            EXIT.                                                               
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F410                                                
                    I-O BP13KH50                                                
                        BP13K859                                                
                 OUTPUT BP13FH50                                                
                        BP13F859.                                               
                                                                                
            IF BP13KH50-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, KH50-STATUS ' BP13KH50-STATUS            
               MOVE BP13KH50-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BP13K859-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K859-STATUS ' BP13K859-STATUS            
               MOVE BP13K859-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-READ-F410.                                                          
      *-------------------------------------------------------------            
            READ BP13F410  AT END MOVE 'Y' TO F410-EOF                          
                 GO TO 2000-EXIT.                                               
            ADD 1 TO WS-F410-READ.                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-PROCESS-ROUTINE.                                                    
      *-------------------------------------------------------------            
            PERFORM 4000-READ-KH50 THRU 4000-EXIT.                              
            IF WS-KH50-FND = 'Y'                                                
               PERFORM 4500-WRITE-FH50 THRU 4500-EXIT                           
               PERFORM 4600-DEL-KH50   THRU 4600-EXIT                           
            END-IF.                                                             
                                                                                
            PERFORM 6000-READ-K859 THRU 6000-EXIT.                              
            IF WS-K859-FND = 'Y'                                                
               PERFORM 6500-WRITE-F859 THRU 6500-EXIT                           
               PERFORM 6600-DEL-K859   THRU 6600-EXIT                           
            END-IF.                                                             
                                                                                
            PERFORM 2000-READ-F410 THRU 2000-EXIT.                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-READ-KH50.                                                          
      *-------------------------------------------------------------            
            MOVE 'N'             TO WS-KH50-FND.                                
            MOVE SPACES          TO KH50-KEY-FLD.                               
            MOVE F410-NUM-REGN   TO KH50-KEY-FLD.                               
                                                                                
            READ BP13KH50.                                                      
                                                                                
            EVALUATE BP13KH50-STATUS                                            
            WHEN 00                                                             
               MOVE 'Y'  TO WS-KH50-FND                                         
               DISPLAY 'RECORD FOUND IN BP13KH50 : '                            
                       KH50-KEY-FLD                                             
            WHEN 23                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
               DISPLAY 'READ ERROR, KH50-STATUS ' BP13KH50-STATUS               
               MOVE BP13KH50-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4500-WRITE-FH50.                                                         
      *-------------------------------------------------------------            
            MOVE SPACES         TO BP13FH50-REC.                                
            MOVE BP13KH50-REC   TO BP13FH50-REC.                                
            WRITE BP13FH50-REC.                                                 
            IF BP13FH50-STATUS NOT = ZEROES                                     
               DISPLAY 'BP13FH50 - WRITE ERROR: ' BP13FH50-STATUS               
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            ELSE                                                                
               ADD 1            TO WS-FH50-WRITE                                
            END-IF.                                                             
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4600-DEL-KH50.                                                           
      *-------------------------------------------------------------            
            DELETE BP13KH50.                                                    
                                                                                
            IF BP13KH50-STATUS NOT = ZEROES                                     
               DISPLAY 'BP13KH50 - DELETE ERROR: ' BP13KH50-STATUS              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            ELSE                                                                
               ADD 1            TO WS-KH50-DELETE                               
               DISPLAY 'KH50 RECORD DELETED : ' KH50-KEY-FLD                    
            END-IF.                                                             
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6000-READ-K859.                                                          
      *-------------------------------------------------------------            
            MOVE 'N'             TO WS-K859-FND.                                
            MOVE SPACES          TO K859-KEY-FLD.                               
            MOVE F410-NUM-REGN   TO K859-KEY-FLD.                               
                                                                                
            READ BP13K859.                                                      
                                                                                
            EVALUATE BP13K859-STATUS                                            
            WHEN 00                                                             
               MOVE 'Y'          TO WS-K859-FND                                 
               DISPLAY 'RECORD FOUND IN BP13K859 : '                            
                       K859-KEY-FLD                                             
            WHEN 23                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
               DISPLAY 'READ ERROR, K859-STATUS ' BP13K859-STATUS               
               MOVE BP13K859-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-EVALUATE.                                                       
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6500-WRITE-F859.                                                         
      *-------------------------------------------------------------            
            MOVE SPACES         TO BP13F859-REC.                                
            MOVE BP13K859-REC   TO BP13F859-REC.                                
            WRITE BP13F859-REC.                                                 
            IF BP13F859-STATUS NOT = ZEROES                                     
               DISPLAY 'BP13F859 - WRITE ERROR: ' BP13F859-STATUS               
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            ELSE                                                                
               ADD 1            TO WS-F859-WRITE                                
            END-IF.                                                             
                                                                                
       6500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6600-DEL-K859.                                                           
      *-------------------------------------------------------------            
            DELETE BP13K859.                                                    
                                                                                
            IF BP13K859-STATUS NOT = ZEROES                                     
               DISPLAY 'BP13K859 - DELETE ERROR: ' BP13K859-STATUS              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            ELSE                                                                
               ADD 1            TO WS-K859-DELETE                               
               DISPLAY 'K859 RECORD DELETED : ' K859-KEY-FLD                    
            END-IF.                                                             
                                                                                
       6600-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
             DISPLAY '- BP13CAA4 CONTROL TOTAL ----------'.                     
             DISPLAY ' F410-READ      ' WS-F410-READ.                           
             DISPLAY ' FH50-WRITE     ' WS-FH50-WRITE.                          
             DISPLAY ' F859-WRITE     ' WS-F859-WRITE.                          
             DISPLAY ' KH50-DELETE    ' WS-KH50-DELETE.                         
             DISPLAY ' K859-DELETE    ' WS-K859-DELETE.                         
             DISPLAY ' '.                                                       
             DISPLAY ' '.                                                       
                                                                                
           CLOSE      BP13F410                                                  
                      BP13KH50                                                  
                      BP13K859                                                  
                      BP13FH50                                                  
                      BP13F859.                                                 
                                                                                
           IF BP13KH50-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, KH50-STATUS '                            
                        BP13KH50-STATUS.                                        
                                                                                
           IF BP13K859-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K859-STATUS '                            
                        BP13K859-STATUS.                                        
                                                                                
             STOP RUN.                                                          
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
