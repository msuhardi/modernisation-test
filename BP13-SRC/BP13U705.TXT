       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13U705.                                                  
      *AUTHOR.       EVM.                                                       
      *DATE-WRITTEN. 14/11/1996.                                                
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  PROGRAM TO MATCH BP13F500 WITH BP13F558 BY    *                
      *          REGISTRATION NO. AND IF FOUND MATCH, VALIDATE *                
      *          REASON-CODE THRU P04K010 AND IF FOUND, WITH   *                
      *          DEBARMENT REASON-CODE = 62, 64 AND 66, CHANGE *                
      *          HOUSEHOLD CODE FORM 'H' TO 'G', PRINT REPORT  *                
      *          AND WRITE BP13F500-REC TO OUTPUT FILE.        *                
      *                                                        *                
      *      2.  MOVE 'DB' TO F500-CDE-EXIST-FLAT-TYPE, SO THAT*                
      *          THE CASES WILL BE EXCLUDED FROM 5-YR CHECK    *                
      * ------------------------------------------------------ *                
      *  CHG ID  OIC   DATE   DESCRIPTION                      *                
      *  ------  ---   ----   -----------                      *                
      *BP130214  EVM  141196  NEW PROGRAM                      *                
      * BP130445 SSS  050298  EXPAND F558 RECL FROM 150 TO 400 *                
      * BP130573 LSB  041198  ADD DEBARMENT CODE ON THE REPORT *                
      * BP130623 LSB  140499  CHECK PARENT/CHILD=Y, REMAIN AS G*                
      *                       DEBAR-CODE KEPT IN DEBARMENT-3   *                
      * BP130652 NOR  190299  EXPAND F500 & F520 RECL 300 - 500*                
      * BP130897 AKT  040800  EXPAND F558 RECL FROM 400 TO 600 *                
      * BP130925 ANC1 040701  CHGE HHTY TO 'G' ONLY IF DEBAR   *                
      *                       SCH ACCT = SMS SCH ACCT.         *                
      * BP131015 ANC1 121201  CHECK ONLY FOR THE 1ST 9 BYTES OF*                
      *                       SMS SCH ACCT AND DEBAR SCH ACCT. *                
      * ====================================================== *                
           SKIP1                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558   ASSIGN TO BP13F558.                                
           SELECT BP13F500   ASSIGN TO BP13F500.                                
                                                                                
           SELECT BP13F520   ASSIGN TO BP13F520.                                
           SELECT BP13L705   ASSIGN TO BP13L705.                                
                                                                                
           EJECT                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F500                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F500.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13L705                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED.                                          
                                                                                
       01   BP13L705-REC             PIC X(132).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K010-STATUS          PIC 9(2)    VALUE 0.                         
       01  WS-F500-READ            PIC 9(8)    VALUE 0.                         
       01  WS-F558-READ            PIC 9(8)    VALUE 0.                         
       01  WS-VALID-CNT            PIC 9(8)    VALUE 0.                         
       01  WS-OUTPUT-CNT           PIC 9(8)    VALUE 0.                         
       01  WS-NOTFND-F500-CNT      PIC 9(8)    VALUE 0.                         
       01  WS-NOTFND-F558-CNT      PIC 9(8)    VALUE 0.                         
       01  WS-MATCH-CNT            PIC 9(8)    VALUE 0.                         
       01  WS-REC-CNT              PIC 9(8)    VALUE 0.                         
       01  WS-PAGE-CTR             PIC 9(5)    VALUE 0.                         
       01  WS-LINE-CTR             PIC 9(2)    VALUE 65.                        
                                                                                
       01  WS-DATE.                                                             
           05  WS-YY               PIC 9999.                                    
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  WS-CC                   PIC X(2).                                    
                                                                                
       01  WS-F500-EOF             PIC X(1).                                    
           88  F500-EOF                          VALUE 'Y'.                     
       01  WS-F558-EOF             PIC X(1).                                    
           88  F558-EOF                          VALUE 'Y'.                     
       01  WS-K010-EOF             PIC X(1).                                    
           88  K010-EOF                          VALUE 'Y'.                     
       01  WS-VALID-REASON         PIC X(1).                                    
           88  VALID-REASON                      VALUE 'Y'.                     
                                                                                
      *================================================================*        
      *            BP13L705  REPORT  LAYOUT                            *        
      *================================================================*        
       01  L705-HEAD01.                                                         
           05  FILLER              PIC X(8)      VALUE 'BP13L705'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L705-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
                                                                                
       01  L705-HEAD02.                                                         
           05  FILLER              PIC X(36)     VALUE SPACES.                  
           05  FILLER              PIC X(46)     VALUE                          
               'SOC - REPORT ON CASES FOUND IN DEBARMENT WITH '.                
           05  FILLER              PIC X(24)     VALUE                          
               'CODE 62, 64, 66, GA, GB'.                                       
           05  FILLER              PIC X(14)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE :'.                
           05  L705-PAGENO         PIC ZZZZ9.                                   
                                                                                
       01  L705-HEAD03.                                                         
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'S/N'.                   
           05  FILLER              PIC X(10)     VALUE 'REF/REG NO'.            
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'NRIC1'.                 
           05  FILLER              PIC X(10)     VALUE 'NRIC2'.                 
           05  FILLER              PIC X(8)      VALUE 'BF-HH'.                 
           05  FILLER              PIC X(8)      VALUE 'AF-HH'.                 
           05  FILLER              PIC X(9)      VALUE 'ALO-SCH'.               
           05  FILLER              PIC X(12)     VALUE 'DEBAR-CD'.              
                                                                                
       01  L705-DETAIL.                                                         
           05  FILLER                PIC X(02)     VALUE SPACES.                
           05  L705-REC-CNT          PIC ZZZZ9.                                 
           05  FILLER                PIC X(01)     VALUE SPACES.                
           05  L705-NUM-REGN         PIC X(8)      VALUE SPACES.                
           05  FILLER                PIC X(4)      VALUE SPACES.                
           05  L705-NUM-NRIC1        PIC X(9)      VALUE SPACES.                
           05  FILLER                PIC X(2)      VALUE SPACES.                
           05  L705-NUM-NRIC2        PIC X(9)      VALUE SPACES.                
           05  FILLER                PIC X(5)      VALUE SPACES.                
           05  L705-CDE-HOUSEHOLD-BF PIC X(1)      VALUE SPACES.                
           05  FILLER                PIC X(7)      VALUE SPACES.                
           05  L705-CDE-HOUSEHOLD-AF PIC X(1)      VALUE SPACES.                
           05  FILLER                PIC X(8)      VALUE SPACES.                
           05  L705-CDE-ALLOC-SCH    PIC X(3)      VALUE SPACES.                
           05  FILLER                PIC X(06)     VALUE SPACES.                
           05  L705-CDE-DEBAR        PIC X(2)      VALUE SPACES.                
           05  FILLER                PIC X(13)     VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
      *================================================================*        
       000-MAIN-ROUTINE.                                                        
      *================================================================*        
           OPEN INPUT  BP13F500                                                 
                       BP13F558                                                 
                OUTPUT BP13F520                                                 
                       BP13L705.                                                
                                                                                
           ACCEPT WS-DATE FROM DATE YYYYMMDD.                                   
           STRING WS-DD '/' WS-MM '/' WS-YY                                     
                DELIMITED BY SIZE INTO L705-DATE.                               
                                                                                
           PERFORM 100-READ-F500-REC THRU 100-EXIT.                             
           PERFORM 110-READ-F558-REC THRU 110-EXIT.                             
           PERFORM 200-PROCESS-MATCH THRU 200-EXIT                              
                   UNTIL F500-EOF AND F558-EOF.                                 
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       100-READ-F500-REC.                                                       
      *================================================================*        
           READ BP13F500 AT END                                                 
                MOVE HIGH-VALUE TO F500-NUM-REGN                                
                MOVE 'Y'        TO WS-F500-EOF                                  
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F500-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       110-READ-F558-REC.                                                       
      *================================================================*        
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE TO F558-NUM-REGN                                
                MOVE 'Y'        TO WS-F558-EOF                                  
                GO TO 110-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       200-PROCESS-MATCH.                                                       
      *================================================================*        
           IF F500-NUM-REGN = F558-NUM-REGN                                     
              ADD 1                       TO WS-MATCH-CNT                       
              PERFORM 250-PROCESS-MATCHED THRU 250-EXIT                         
              PERFORM 100-READ-F500-REC   THRU 100-EXIT                         
              PERFORM 110-READ-F558-REC   THRU 110-EXIT                         
           ELSE                                                                 
              IF F500-NUM-REGN < F558-NUM-REGN                                  
                 WRITE BP13F520-REC FROM BP13F500-REC                           
                 ADD 1              TO WS-NOTFND-F500-CNT                       
                                       WS-OUTPUT-CNT                            
                 PERFORM 100-READ-F500-REC THRU 100-EXIT                        
              ELSE                                                              
                 ADD 1 TO WS-NOTFND-F558-CNT                                    
                 PERFORM 110-READ-F558-REC THRU 110-EXIT.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       250-PROCESS-MATCHED.                                                     
      *================================================================*        
           IF F500-CDE-HOUSEHOLD = 'H' AND                                      
                                 F500-CDE-PARENT-CHILD NOT = 'Y'                
              PERFORM 500-PROCESS-PRINT  THRU 500-EXIT                          
              IF F558-NUM-HDB-REF-SMS NOT = SPACES AND                          
                 F558-NUM-HDB-REF-SMS(1:9) = F558-NUM-HDB-REF-DEBAR(1:9)        
                 MOVE 'G'           TO F500-CDE-HOUSEHOLD                       
                 MOVE 'DB'          TO F500-CDE-EXIST-FLAT-TYPE                 
              END-IF                                                            
              WRITE BP13F520-REC FROM BP13F500-REC                              
              ADD 1              TO WS-VALID-CNT                                
                                    WS-OUTPUT-CNT                               
           ELSE                                                                 
              WRITE BP13F520-REC FROM BP13F500-REC                              
              ADD 1              TO WS-NOTFND-F500-CNT                          
                                    WS-OUTPUT-CNT.                              
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       500-PROCESS-PRINT.                                                       
      *================================================================*        
           IF WS-LINE-CTR > 53                                                  
              MOVE 0             TO WS-LINE-CTR                                 
              ADD 1              TO WS-PAGE-CTR                                 
              MOVE WS-PAGE-CTR   TO L705-PAGENO                                 
              WRITE BP13L705-REC FROM L705-HEAD01 AFTER PAGE                    
              WRITE BP13L705-REC FROM L705-HEAD02 AFTER 1                       
              WRITE BP13L705-REC FROM L705-HEAD03 AFTER 3                       
              MOVE SPACES TO BP13L705-REC                                       
              WRITE BP13L705-REC AFTER 1.                                       
                                                                                
           ADD 1 TO WS-REC-CNT.                                                 
           MOVE WS-REC-CNT            TO L705-REC-CNT.                          
           MOVE F500-NUM-REGN         TO L705-NUM-REGN.                         
           MOVE F500-NUM-NRIC1        TO L705-NUM-NRIC1.                        
           MOVE F500-NUM-NRIC2        TO L705-NUM-NRIC2.                        
           MOVE F500-CDE-HOUSEHOLD    TO L705-CDE-HOUSEHOLD-BF.                 
           MOVE 'G'                   TO L705-CDE-HOUSEHOLD-AF.                 
           MOVE F500-CDE-ALLOC-SCH    TO L705-CDE-ALLOC-SCH.                    
           MOVE F558-NUM-DEBARMENT-3  TO L705-CDE-DEBAR.                        
                                                                                
           WRITE BP13L705-REC FROM L705-DETAIL.                                 
           ADD 1 TO WS-LINE-CTR.                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================================================================*        
       900-CLOSE-ROUTINE.                                                       
      *================================================================*        
           DISPLAY 'BP13U705                 DATE : ' WS-DATE.                  
           DISPLAY 'UPDATE  HOUSEHOLD TYPE TO REQUEST FILE'.                    
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F500 :' WS-F500-READ.               
           DISPLAY 'NO OF RECORDS READ FROM F558 :' WS-F558-READ.               
           DISPLAY 'NO OF REORDS  W/ VALID REASON:' WS-VALID-CNT.               
           DISPLAY 'NO OF REORDS  WRITE OUT      :' WS-OUTPUT-CNT.              
           DISPLAY 'NO OF RECORDS MATCH          :' WS-MATCH-CNT.               
           DISPLAY 'NO OF RECORDS NOT MATCH-F500 :' WS-NOTFND-F500-CNT.         
           DISPLAY 'NO OF RECORDS NOT MATCH-F558 :' WS-NOTFND-F558-CNT.         
                                                                                
           CLOSE BP13F558                                                       
                 BP13F520                                                       
                 BP13F500                                                       
                 BP13L705.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
