      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C25L.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  25 JUN 2007.                                              
                                                                                
      *============================================================*            
      * BP13 - LOAN ELIGIBILITY AND ASSESSMENT SYSTEM              *            
      *============================================================*            
      * OBJECTIVES       : RPOGRAM TO CHECK FLAT AVAILABLE FOR     *            
      *                    SELECTION BY NT/N/C/FT, DETERMINE THE   *            
      *                    SALES STATUS. EVERYDAY PUSH THE DATA TO *            
      *                    SERVER FRO E-SALES DISPLAY.             *            
      *                    A= ALL SOLD                             *            
      *                    S= SELLING FAST IF LEFT HALF OF THE TOT *            
      *                    L= IF LEFT < 5 UNITS                    *            
      *                                                            *            
      * FILES            : BP13IPUT - BP13.F230.HEADFLAT           *            
      *============================================================*            
      * REVISION HISTORY :                                         *            
      * CHG-REQ#  BY    DATE        DESCRIPTION                    *            
      * --------  ----  ----------  ------------------------------ *            
      * BP133086  BA2   25/06/2007  NEW PROGRAM.                   *            
      *============================================================*            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13IPUT ASSIGN TO BP13IPUT.                                  
                                                                                
           SELECT BP13F299 ASSIGN TO BP13F299.                                  
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
       FD  BP13IPUT                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 60  CHARACTERS                                       
           DATA RECORDS IS IPUT-REC                                             
           LABEL RECORDS ARE STANDARD.                                          
       01 IPUT-REC PIC X(60).                                                   
                                                                                
       FD  BP13F299                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE OMITTED.                                           
       COPY BP13F299.                                                           
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
      *-------------------------*                                               
      *    DATE - VARIABLES     *                                               
      *-------------------------*                                               
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-DATE              PIC X(08)  VALUE SPACES.               
           05  WS-CURRENT-TIME           PIC X(09)  VALUE SPACES.               
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-CURR-TIME      PIC X(08).                             
                   10  WS-GM             PIC X(01).                             
           05  FILLER                    PIC X(04)  VALUE SPACES.               
                                                                                
      *-------------------------*                                               
      *  COUNT - VARIABLES.     *                                               
      *-------------------------*                                               
       01  WS-COUNT.                                                            
           05  WS-READ-IPUT              PIC 9(05) COMP VALUE ZEROES.           
           05  WS-WRIT-F299              PIC 9(05) COMP VALUE ZEROES.           
                                                                                
      *-------------------------*                                               
      *  SWITCH - FLAG          *                                               
      *-------------------------*                                               
       01  WS-SWITCH.                                                           
           05  WS-IPUT-END               PIC X(01)   VALUE 'N'.                 
               88  CS-IPUT-END                       VALUE 'Y'.                 
                                                                                
      *----------------------------*                                            
      *  COPY-BOOK FOR INPUT-FILE  *                                            
      *----------------------------*                                            
       01 CP-IPUT-REC.                                                          
          05 CP-REC.                                                            
             10 CP-IPUT-NUM-SEL            PIC X(03)  VALUE SPACES.             
             10 CP-IPUT-NUM-NT             PIC X(20)  VALUE SPACES.             
             10 CP-CHECK-KEY.                                                   
                15 CP-IPUT-NUM-FT          PIC X(09)  VALUE SPACES.             
                15 CP-IPUT-NUM-NEIGH       PIC X(04)  VALUE SPACES.             
                15 CP-IPUT-NUM-CNTRCT      PIC X(04)  VALUE SPACES.             
          05 CP-IPUT-NUM-ACCEPT            PIC X(01)  VALUE ZEROES.             
          05 FILLER                        PIC X(19)  VALUE SPACES.             
                                                                                
      *-------------------------------*                                         
      *  PREVIOUSE  - VARIABLES.      *                                         
      *-------------------------------*                                         
       01  WS-PREV-REC.                                                         
           05 WS-PREV-IPUT-NUM-SEL           PIC X(03)  VALUE SPACES.           
           05 WS-PREV-IPUT-NUM-NT            PIC X(20)  VALUE SPACES.           
           05 WS-PREV-KEY.                                                      
               10 WS-PREV-IPUT-NUM-FT        PIC X(09)  VALUE SPACES.           
               10 WS-PREV-IPUT-NUM-NEIGH     PIC X(04)  VALUE SPACES.           
               10 WS-PREV-IPUT-NUM-CNTRCT    PIC X(04)  VALUE SPACES.           
                                                                                
      *-------------------------*                                               
      *  OTHER - VARIABLES.     *                                               
      *-------------------------*                                               
       01  WS-VARIABLES.                                                        
           05  WS-NUM-ROOM-TOTAL         PIC 9(05)  VALUE ZEROES.               
           05  WS-NUM-ACCP-TOTAL         PIC 9(05)  VALUE ZEROES.               
           05  WS-HALF-TOTAL             PIC 9(05)  VALUE ZEROES.               
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-INIT-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-IPUT     THRU 2000-EXIT.                           
           PERFORM 3000-MAIN-PROCESS  THRU 3000-EXIT                            
                   UNTIL CS-IPUT-END.                                           
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13IPUT                                                  
               OUTPUT BP13F299.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-IPUT.                                                          
      *-------------------------------------------------------------            
           READ BP13IPUT AT END MOVE 'Y' TO WS-IPUT-END                         
                                GO TO 2000-EXIT.                                
                                                                                
           MOVE  IPUT-REC TO CP-IPUT-REC.                                       
           ADD 1 TO WS-READ-IPUT.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
           MOVE ZEROES                   TO WS-NUM-ROOM-TOTAL                   
                                            WS-NUM-ACCP-TOTAL                   
                                            WS-HALF-TOTAL.                      
                                                                                
           MOVE CP-REC                   TO WS-PREV-REC.                        
                                                                                
           PERFORM 3100-ACCUMULATE-FIELDS  THRU 3100-EXIT                       
                   UNTIL CP-CHECK-KEY NOT = WS-PREV-KEY OR CS-IPUT-END.         
                                                                                
           PERFORM 3300-MOVE-VALUES        THRU 3300-EXIT.                      
                                                                                
           PERFORM 4000-WRITE-F299         THRU 4000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-ACCUMULATE-FIELDS.                                                  
      *------------------------------------------------------------*            
                                                                                
           ADD +1                        TO WS-NUM-ROOM-TOTAL.                  
                                                                                
           IF CP-IPUT-NUM-ACCEPT  = 'A'                                         
              ADD +1                     TO WS-NUM-ACCP-TOTAL                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-IPUT        THRU 2000-EXIT.                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3200-DETERMINE-REMARKS.                                                  
      *------------------------------------------------------------*            
           MOVE SPACES                   TO F299-NUM-REMARKS.                   
                                                                                
           DIVIDE WS-NUM-ROOM-TOTAL BY 2 GIVING WS-HALF-TOTAL.                  
                                                                                
           COMPUTE  F299-NUM-UNITS =                                            
                                WS-NUM-ROOM-TOTAL - WS-NUM-ACCP-TOTAL.          
                                                                                
           IF WS-NUM-ROOM-TOTAL - WS-NUM-ACCP-TOTAL = ZEROES                    
              MOVE 'A'                   TO F299-NUM-REMARKS                    
           ELSE                                                                 
              IF WS-NUM-ROOM-TOTAL - WS-NUM-ACCP-TOTAL < 5                      
                 MOVE 'L'                TO F299-NUM-REMARKS                    
              ELSE                                                              
                 IF WS-NUM-ACCP-TOTAL > WS-HALF-TOTAL                           
                    MOVE 'S'             TO F299-NUM-REMARKS                    
                 ELSE                                                           
                    MOVE 'X'             TO F299-NUM-REMARKS                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3300-MOVE-VALUES.                                                        
      *------------------------------------------------------------*            
           MOVE SPACES                    TO BP13F299-REC.                      
           MOVE  WS-PREV-IPUT-NUM-SEL     TO F299-NUM-SELECTION.                
           MOVE  WS-PREV-IPUT-NUM-NT      TO F299-NUM-NEW-TOWN.                 
           MOVE  WS-PREV-IPUT-NUM-NEIGH   TO F299-NUM-NEIGH.                    
           MOVE  WS-PREV-IPUT-NUM-CNTRCT  TO F299-NUM-CNTRCT.                   
           MOVE  WS-PREV-IPUT-NUM-FT(1:1) TO F299-NUM-FLAT-TYPE.                
      *FOR MOVING REMARKS                                                       
           PERFORM 3200-DETERMINE-REMARKS  THRU 3200-EXIT.                      
                                                                                
           MOVE  WS-CURR-DATE             TO F299-DTE-UPDATE.                   
           MOVE  WS-CURR-TIME             TO F299-TME-UPDATE.                   
                                                                                
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-F299.                                                         
      *-------------------------------------------------------------            
           WRITE   BP13F299-REC.                                                
           ADD +1                  TO WS-WRIT-F299.                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '------- BP13C25L -------------'.                            
           DISPLAY 'NO OF RECORDS READ    (BP13IPUT) : '                        
                                               WS-READ-IPUT.                    
           DISPLAY 'NO OF RECORDS WRITTEN (BP13F299) : '                        
                                               WS-WRIT-F299.                    
                                                                                
           CLOSE BP13IPUT                                                       
                 BP13F299.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
      *-------------------------------------------------------------            
      *             END-PROGRAM.                                   *            
      *-------------------------------------------------------------            
