      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CZ04.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  31 AUG 2007.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : CREATE RECORDS IN BP13KZ04                  *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13K230                                              *           
      *   2.  BM06K110                                              *           
      *   3.  BP13KZ10                                              *           
      *   4.  BP13K240                                              *           
      *                                                             *           
      *   INPUT/OUTPUT:                                             *           
      *   1.  BP13KZ04                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133152 29/08/2007 RB12 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K230 ASSIGN       TO BP13K230                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K230-KEY-FLD                         
                           FILE STATUS  IS WS-K230-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K110-KEY-FLD                         
                           FILE STATUS  IS WS-K110-STATUS                       
                           ACCESS MODE  IS RANDOM.                              
                                                                                
           SELECT BP13KZ10 ASSIGN       TO BP13KZ10                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KZ10-KEY-FLD                         
                           FILE STATUS  IS WS-KZ10-STATUS                       
                           ACCESS MODE  IS RANDOM.                              
                                                                                
           SELECT BP13K240 ASSIGN       TO BP13K240                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K240-KEY-FLD                         
                           FILE STATUS  IS WS-K240-STATUS                       
                           ACCESS MODE  IS RANDOM.                              
                                                                                
           SELECT BP13KZ04 ASSIGN       TO BP13KZ04                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KZ04-KEY-FLD                         
                           FILE STATUS  IS WS-KZ04-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K230                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K230.                                                           
                                                                                
       FD   BP13KZ10                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       COPY BP13KZ10.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K240                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K240.                                                           
                                                                                
       FD   BP13KZ04                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13KZ04.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-K230-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-KZ10-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K110-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K240-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-KZ04-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-KZ10-FND            PIC X(01) VALUE SPACES.                       
       01  WS-K110-FND            PIC X(01) VALUE SPACES.                       
       01  WS-K240-FND            PIC X(01) VALUE SPACES.                       
       01  WS-K230-EOF            PIC X     VALUE SPACES.                       
       01  WS-K230-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K110-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-KZ10-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K240-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K110-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-KZ10-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-K240-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-K110-NOT-FND        PIC 9(08) VALUE ZEROES.                       
       01  WS-KZ10-NOT-FND        PIC 9(08) VALUE ZEROES.                       
       01  WS-K240-NOT-FND        PIC 9(08) VALUE ZEROES.                       
       01  WS-KZ04-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-KZ04-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-NUM-FT              PIC X(02) VALUE SPACES.                       
       01  WS-FLAT-TYPE           PIC X(09) VALUE SPACES.                       
       01  WS-CUR-DATE            PIC 9(08) VALUE ZEROES.                       
       01  WS-COMPLETION-DATE     PIC 9(08) VALUE ZEROES.                       
       01  WS-INTEGER1            PIC S9(9) VALUE ZEROES.                       
       01  WS-INTEGER2            PIC S9(9) VALUE ZEROES.                       
       01  WS-DATE-DIFF           PIC 9(05) VALUE ZEROES.                       
       01  WS-K230-AVAIL          PIC X(01) VALUE SPACES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS      THRU 1000-EXIT.                        
           PERFORM 2000-READ-K230        THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-K230-REC THRU 3000-EXIT                         
                   UNTIL WS-K230-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13K230                                                 
                       BP13KZ10                                                 
                       BM06K110                                                 
                       BP13K240                                                 
                I-O    BP13KZ04.                                                
                                                                                
           IF WS-K230-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K230 - OPEN ERROR: ' WS-K230-STATUS                  
              MOVE WS-K230-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-KZ10-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13KZ10 - OPEN ERROR: ' WS-KZ10-STATUS                  
              MOVE WS-KZ10-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BM06K110 - OPEN ERROR: ' WS-K110-STATUS                  
              MOVE WS-K110-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K240-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K240 - OPEN ERROR: ' WS-K240-STATUS                  
              MOVE WS-K240-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-KZ04-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13KZ04 - OPEN ERROR: ' WS-KZ04-STATUS                  
              MOVE WS-KZ04-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CUR-DATE FROM DATE YYYYMMDD.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-K230.                                                          
      *---------------*                                                         
                                                                                
           READ BP13K230 NEXT RECORD AT END                                     
                MOVE HIGH-VALUES TO K230-DETAIL-REC                             
                MOVE 'Y' TO WS-K230-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1      TO WS-K230-READ-CNT.                                     
           MOVE SPACES TO WS-K230-AVAIL.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3000-PROCESS-K230-REC.                                                   
      *---------------------*                                                   
                                                                                
           MOVE SPACES                     TO BP13KZ04-REC.                     
           INITIALIZE                         BP13KZ04-REC.                     
                                                                                
           PERFORM 3100-MOVE-FROM-K230        THRU 3100-EXIT.                   
           IF WS-K230-AVAIL = 'Y'                                               
              PERFORM 3200-READ-BP13KZ10         THRU 3200-EXIT                 
              PERFORM 3300-READ-BM06K110         THRU 3300-EXIT                 
              PERFORM 3400-READ-BP13K240         THRU 3400-EXIT                 
              PERFORM 3600-DECODE-STOREY-HEIGHT  THRU 3600-EXIT                 
                                                                                
              IF (WS-KZ10-FND = 'Y') AND (WS-K110-FND = 'Y') AND                
                 (WS-K240-FND = 'Y')                                            
                                                                                
                 WRITE BP13KZ04-REC                                             
                                                                                
                 IF WS-KZ04-STATUS = 00 OR 02                                   
                    ADD 1 TO WS-KZ04-WRITE-CNT                                  
                 ELSE                                                           
                    DISPLAY 'ERROR WRITING BP13KZ04, KEY: '                     
                    DISPLAY 'BP13KZ04 STATUS: ' WS-KZ04-STATUS                  
                    ADD 1 TO WS-KZ04-WRITE-ERR                                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-K230             THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3100-MOVE-FROM-K230.                                                     
      *--------------------*                                                    
                                                                                
           IF K230-NUM-ACCEPT = 'A'                                             
              MOVE 'N' TO WS-K230-AVAIL                                         
           ELSE                                                                 
              IF K230-NUM-SELECTION NOT = 'WIA' AND 'WIB' AND                   
                                          'WIC' AND '3R '                       
                 MOVE 'N' TO WS-K230-AVAIL                                      
              ELSE                                                              
                 MOVE 'Y'                 TO WS-K230-AVAIL                      
                 MOVE K230-NUM-NEW-TOWN   TO KZ04-NUM-NEW-TOWN                  
                 MOVE K230-NUM-CNTRCT4    TO KZ04-NUM-CNTRCT                    
                 MOVE K230-NUM-BLK        TO KZ04-NUM-BLK                       
                 MOVE K230-NUM-FLAT-TYPE2 TO KZ04-NUM-FLAT-TYPE                 
                 MOVE K230-NUM-FLAT-TYPE  TO KZ04-NUM-FLAT-TYPE2                
                 MOVE K230-NUM-SCH-ACC    TO KZ04-NUM-SCH-ACC                   
                 MOVE K230-NUM-SELECTION  TO KZ04-NUM-SELECTION                 
                 MOVE K230-DTE-ALLOC      TO KZ04-DTE-ALLOC                     
                 MOVE K230-NUM-ACCEPT     TO KZ04-NUM-ACCEPT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13KZ10.                                                      
      *-------------------*                                                     
           MOVE SPACES              TO KZ10-KEY-FLD                             
                                       WS-KZ10-FND.                             
           MOVE KZ04-NUM-NEW-TOWN   TO KZ10-NUM-NEW-TOWN.                       
                                                                                
           READ BP13KZ10 KEY IS KZ10-KEY-FLD.                                   
                                                                                
           EVALUATE WS-KZ10-STATUS                                              
             WHEN 00                                                            
                MOVE KZ10-LOCATION TO KZ04-LOCATION                             
                MOVE 'Y'           TO WS-KZ10-FND                               
                ADD 1 TO WS-KZ10-READ-CNT                                       
             WHEN 23                                                            
                DISPLAY 'BP13KZ10 REC NOT FOUND: ' KZ10-KEY-FLD                 
                ADD 1 TO WS-KZ10-NOT-FND                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13KZ10 - READ ERROR: ' WS-KZ10-STATUS                
                ADD 1 TO WS-KZ10-READ-ERR                                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3300-READ-BM06K110.                                                      
      *-------------------*                                                     
           MOVE SPACES              TO K110-SCH-ACC-NO                          
                                       WS-K110-FND.                             
           MOVE KZ04-NUM-SCH-ACC    TO K110-SCH-ACC-NO.                         
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K110-STATUS                                              
             WHEN 00                                                            
                MOVE K110-CURR-SELLING-PRICE TO KZ04-NUM-SP                     
                MOVE 'Y'            TO WS-K110-FND                              
                ADD 1 TO WS-K110-READ-CNT                                       
                PERFORM 3500-DECODE-PRICE-RANGE THRU 3500-EXIT                  
             WHEN 23                                                            
                DISPLAY 'BM06K110 REC NOT FOUND: ' K110-KEY-FLD                 
                ADD 1 TO WS-K110-NOT-FND                                        
             WHEN OTHER                                                         
                DISPLAY 'BM06K110 - READ ERROR: ' WS-K110-STATUS                
                ADD 1 TO WS-K110-READ-ERR                                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3400-READ-BP13K240.                                                      
      *-------------------*                                                     
           MOVE SPACES              TO K240-HEADER-REC                          
                                       WS-K240-FND.                             
           MOVE '01'                TO K240-CDE-HEADER.                         
           MOVE KZ04-DTE-ALLOC      TO K240-DTE-ALLOC.                          
           MOVE KZ04-NUM-NEW-TOWN   TO K240-NUM-NEW-TOWN.                       
           MOVE K230-NUM-FLAT-TYPE  TO K240-NUM-FLAT-TYPE.                      
           MOVE KZ04-NUM-BLK        TO K240-NUM-BLK.                            
                                                                                
           READ BP13K240 KEY IS K240-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K240-STATUS                                              
             WHEN 00                                                            
                MOVE K240-DTE-EST-CMPLTN TO WS-COMPLETION-DATE                  
                MOVE 'Y'                 TO WS-K240-FND                         
                ADD 1 TO WS-K240-READ-CNT                                       
                PERFORM 3700-DECODE-COMPLETION-DTE THRU 3700-EXIT               
             WHEN 23                                                            
                DISPLAY 'BP13K240 REC NOT FOUND: ' K240-KEY-FLD                 
                ADD 1 TO WS-K240-NOT-FND                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K240 - READ ERROR: ' WS-K240-STATUS                
                ADD 1 TO WS-K240-READ-ERR                                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3500-DECODE-PRICE-RANGE.                                                 
      *------------------------*                                                
                                                                                
           EVALUATE TRUE                                                        
              WHEN KZ04-NUM-SP <= 100000                                        
                 MOVE '1' TO KZ04-CDE-PRICE-RANGE                               
              WHEN (KZ04-NUM-SP >= 100001) AND (KZ04-NUM-SP <= 150000)          
                 MOVE '2' TO KZ04-CDE-PRICE-RANGE                               
              WHEN (KZ04-NUM-SP >= 150001) AND (KZ04-NUM-SP <= 200000)          
                 MOVE '3' TO KZ04-CDE-PRICE-RANGE                               
              WHEN (KZ04-NUM-SP >= 200001) AND (KZ04-NUM-SP <= 250000)          
                 MOVE '4' TO KZ04-CDE-PRICE-RANGE                               
              WHEN (KZ04-NUM-SP >= 250001) AND (KZ04-NUM-SP <= 300000)          
                 MOVE '5' TO KZ04-CDE-PRICE-RANGE                               
              WHEN KZ04-NUM-SP > 300000                                         
                 MOVE '6' TO KZ04-CDE-PRICE-RANGE                               
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------*                                              
       3600-DECODE-STOREY-HEIGHT.                                               
      *--------------------------*                                              
                                                                                
           EVALUATE TRUE                                                        
              WHEN K230-NUM-LEVEL <= '10'                                       
                 MOVE '1' TO KZ04-CDE-STOREY-HEIGHT                             
              WHEN (K230-NUM-LEVEL >= '11') AND (K230-NUM-LEVEL <= '20')        
                 MOVE '2' TO KZ04-CDE-STOREY-HEIGHT                             
              WHEN (K230-NUM-LEVEL >= '21') AND (K230-NUM-LEVEL <= '30')        
                 MOVE '3' TO KZ04-CDE-STOREY-HEIGHT                             
              WHEN K230-NUM-LEVEL > '30'                                        
                 MOVE '4' TO KZ04-CDE-STOREY-HEIGHT                             
           END-EVALUATE.                                                        
                                                                                
           MOVE K230-NUM-LEVEL TO KZ04-NUM-STOREY.                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------*                                              
       3700-DECODE-COMPLETION-DTE.                                              
      *--------------------------*                                              
                                                                                
           IF K240-DTE-KEY-AVAIL NOT = SPACES AND ZEROES AND LOW-VALUES         
              MOVE '1'    TO KZ04-CDE-COMPLETION-DATE                           
              MOVE K240-DTE-KEY-AVAIL TO KZ04-DTE-COMPLETION                    
           ELSE                                                                 
              COMPUTE WS-INTEGER1 =                                             
                      FUNCTION INTEGER-OF-DATE(WS-CUR-DATE)                     
              COMPUTE WS-INTEGER2 =                                             
                      FUNCTION INTEGER-OF-DATE(WS-COMPLETION-DATE)              
              COMPUTE WS-DATE-DIFF = WS-INTEGER2 - WS-INTEGER1                  
              EVALUATE TRUE                                                     
                 WHEN WS-DATE-DIFF <= 183                                       
                    MOVE '2' TO KZ04-CDE-COMPLETION-DATE                        
                 WHEN (WS-DATE-DIFF >= 184) AND (WS-DATE-DIFF <= 365)           
                    MOVE '3' TO KZ04-CDE-COMPLETION-DATE                        
                 WHEN (WS-DATE-DIFF >= 366) AND (WS-DATE-DIFF <= 730)           
                    MOVE '4' TO KZ04-CDE-COMPLETION-DATE                        
                 WHEN (WS-DATE-DIFF >= 731) AND (WS-DATE-DIFF <= 1095)          
                    MOVE '5' TO KZ04-CDE-COMPLETION-DATE                        
                 WHEN WS-DATE-DIFF >= 1096                                      
                    MOVE '6' TO KZ04-CDE-COMPLETION-DATE                        
              END-EVALUATE                                                      
              MOVE WS-COMPLETION-DATE TO KZ04-DTE-COMPLETION                    
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CZ04  *************'.                           
           DISPLAY 'RECS READ    FROM BP13K230 : ' WS-K230-READ-CNT.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13KZ10 : ' WS-KZ10-READ-CNT.            
           DISPLAY 'RECS NOT FOUND IN BP13KZ10 : ' WS-KZ10-NOT-FND.             
           DISPLAY 'READ ERRORS  IN   BP13KZ10 : ' WS-KZ10-READ-ERR.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BM06K110 : ' WS-K110-READ-CNT.            
           DISPLAY 'RECS NOT FOUND IN BM06K110 : ' WS-K110-NOT-FND.             
           DISPLAY 'READ ERRORS  IN   BM06K110 : ' WS-K110-READ-ERR.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13K240 : ' WS-K240-READ-CNT.            
           DISPLAY 'RECS NOT FOUND IN BP13K240 : ' WS-K240-NOT-FND.             
           DISPLAY 'READ ERRORS  IN   BP13K240 : ' WS-K240-READ-ERR.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN BP13KZ04   : ' WS-KZ04-WRITE-CNT.           
           DISPLAY 'WRITE ERRORS IN BP13KZ04   : ' WS-KZ04-WRITE-ERR.           
                                                                                
           CLOSE BP13K230                                                       
                 BM06K110                                                       
                 BP13K240                                                       
                 BP13KZ04.                                                      
                                                                                
           IF WS-K230-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K230 - CLOSE ERROR: ' WS-K230-STATUS                 
              MOVE WS-K230-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KZ10-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13KZ10 - CLOSE ERROR: ' WS-KZ10-STATUS                 
              MOVE WS-KZ10-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT EQUAL 00                                       
              DISPLAY 'BM06K110 - CLOSE ERROR: ' WS-K110-STATUS                 
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K240-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K240 - CLOSE ERROR: ' WS-K240-STATUS                 
              MOVE WS-K240-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KZ04-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13KZ04 - CLOSE ERROR: ' WS-KZ04-STATUS                 
              MOVE WS-KZ04-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
