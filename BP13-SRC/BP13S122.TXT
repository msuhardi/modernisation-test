 /*********************************************************************/        
 /*                                                                  **/        
 /*   PROGRAM ID         : BP13S122                                  **/        
 /*                                                                  **/        
 /*   DATE WRITTEN       : 12/06/98                                  **/        
 /*                                                                  **/        
 /*   AUTHOR             : MANU M S                                  **/        
 /*                                                                  **/        
 /*   SPECIFICATION      : REPORT ON FS APPLICATION CANCELLED        **/        
 /*                        WITH OUSTANDING REGISTRATION DEPOSIT      **/        
 /*                                                                  **/        
 /*                                                                  **/        
 /* CHG-NO   BY   ON      DESCRIPTION                                **/        
 /* ------   --   --      -----------                                **/        
 /* BP130498 MANU 980617 - NEW PGM                                   **/        
 /* BP130583 RMDU 981111 - ADD ZONE,FLAT TYPE AND FILE RECEIVED DATE **/        
 /*********************************************************************/        
OPTION NONUMBER NODATE;                                                         
                                                                                
  DATA BP13F800;                                                                
  INFILE BP13F800;                                                              
  INPUT  @04  REGNO     $CHAR8.                                                 
         @30  FLAT_TYP  $CHAR2.                                                 
         @49  ZONE      $CHAR2.                                                 
         @60  ELI_SH    $CHAR3.                                                 
         @88  NRIC1     $CHAR9.                                                 
         @185 DTE_CNCL  $CHAR8.                                                 
         @284 DTE_RECV  $CHAR8.                                                 
         @791 REG_CASH  ZD7.2                                                   
         @798 REG_CPF1  ZD7.2                                                   
         @805 REG_CPF2  ZD7.2                                                   
         @812 REG_CPF3  ZD7.2                                                   
         @819 REG_CPF4  ZD7.2;                                                  
                                                                                
          LENGTH DTECANL $10.;                                                  
             DTECANL=SUBSTR(DTE_CNCL,7,2)||'/'||SUBSTR(DTE_CNCL,5,2)||          
                     '/'||SUBSTR(DTE_CNCL,1,4);                                 
                                                                                
          LENGTH DTERECV $10.;                                                  
             DTERECV=SUBSTR(DTE_RECV,7,2)||'/'||SUBSTR(DTE_RECV,5,2)||          
                     '/'||SUBSTR(DTE_RECV,1,4);                                 
                                                                                
  PROC SORT;                                                                    
    BY DTE_CNCL REGNO;                                                          
                                                                                
   DATA _NULLS_;                                                                
   SET BP13F800 END=EOF;                                                        
      BY DTE_CNCL REGNO;                                                        
                                                                                
        IF REGNO^='  ';                                                         
                                                                                
        DEP_PAID=SUM(REG_CASH,REG_CPF1,REG_CPF2,REG_CPF3,REG_CPF4);             
                                                                                
        IF DEP_PAID>0000;                                                       
                                                                                
   OPTIONS PAGESIZE=55 LS=132 NODATE;                                           
                                                                                
       LENGTH K820KEY $17.;                                                     
       K820KEY=REGNO||NRIC1;                                                    
                                                                                
   INFILE BP13K820 VSAM KEY=K820KEY;                                            
   INPUT @;                                                                     
                                                                                
   RC=_IORC_; FBC=_FDBK_;                                                       
   IF RC=0 THEN                                                                 
      DO;                                                                       
         INPUT @28  K820NME $CHAR20.;                                           
         NAME = K820NME;                                                        
      END;                                                                      
   ELSE                                                                         
      DO;                                                                       
        _ERROR_ = 0;                                                            
         NAME=' ';                                                              
      END;                                                                      
                                                                                
   FILE BP13L122 HEADER=HH NOTITLES;                                            
                                                                                
   SNO+1;                                                                       
   PUT @1   SNO  3.                                                             
       @10  REGNO                                                               
       @24  ZONE                                                                
       @33  FLAT_TYP                                                            
       @44  ELI_SH                                                              
       @52  NRIC1                                                               
       @64  NAME                                                                
       @87  DTECANL                                                             
       @102 DEP_PAID                                                            
       @115 DTERECV;                                                            
                                                                                
   RETURN;                                                                      
                                                                                
 HH:                                                                            
    CURRENT=PUT(TODAY(),DDMMYY10.);                                             
    PAGE+1;                                                                     
    PUT   @1   'BP13L122  HDB3'                                                 
          @50  'SYSTEM OF COMMITMENT (SOC)'                                     
          @110 'DATE : ' CURRENT;                                               
                                                                                
    PUT   @30  'REPORT ON FS APPLICATION CANCELLED WITH OUTSTANDING'            
          @82  'REGISTRATION DEPOSIT'                                           
          @110 'PAGE : ' PAGE 5.;                                               
    PUT /;                                                                      
    PUT @1   'SER NO'                                                           
        @10  'REGNO'                                                            
        @23  'ZONE '                                                            
        @30  'FLAT TYPE'                                                        
        @42  'ELG-SCH'                                                          
        @52  'NRIC1'                                                            
        @64  'NAME'                                                             
        @87  'DTE-CANCEL'                                                       
        @100 'DEPOSIT-PAID'                                                     
        @115 'FILE RECEIVED DATE';                                              
                                                                                
     RETURN;                                                                    
                                                                                
