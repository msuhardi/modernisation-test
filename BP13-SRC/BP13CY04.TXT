      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY04.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  25/04/08.                                                 
      *****************************************************************         
      * OBJECTIVE -                                                   *         
      *   MATCH WITH BP13F730 WITH BP13F595, OMIT EXTRAL CANCELLED    *         
      *   OMIT EXTRA REC (CASES CANCELLED RE-INSTATED BCAK TO K730)   *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F730                                  *         
      *                  2. BP13F595                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. P13F730A                                  *         
      *                  1. P13F730B (CANCEL CASES)                   *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP133340  14042008  JB8  NEW PROGRAM.                         *         
      * BP133614  25062009  PCL3 ADD BOOK-STATUS TO BP13F730.         *         
      * BP133921  31052010  PCL3 ADD 'NS' TO COULD NOT BOOK CODE.     *         
      * BP133900  27082010  PCL3 TO PASS F595-CDE-FLAT-TYPE TO        *         
      *                                  F730-CDE-FLAT-TYPE.          *         
      * BP135039  02122013  IMC1 CATER FOR NEW BOOK-STATUS CODES      *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F730  ASSIGN        TO BP13F730.                         
           SELECT  BP13F595  ASSIGN        TO BP13F595.                         
           SELECT  P13F730A  ASSIGN        TO P13F730A.                         
           SELECT  P13F730B  ASSIGN        TO P13F730B.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F730                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F595                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F595.                                                           
                                                                                
       FD   P13F730A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F730A-REC                PIC X(500).                              
                                                                                
       FD   P13F730B                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F730B-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  VARIABLES.                                                           
           05  WS-NUM-REGN             PIC X(8)  VALUE SPACES.                  
           05  WS-MATCH                PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F730-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-BP13F595-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-F730             PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-F595             PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-TOT              PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2100-READ-BP13F730      THRU 2100-EXIT.                      
           PERFORM 2200-READ-BP13F595      THRU 2200-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
                   UNTIL F730-NUM-REGN = HIGH-VALUES                            
                     AND F595-NUM-REGN = HIGH-VALUES.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F595                                                 
                OUTPUT P13F730A                                                 
                       P13F730B.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F730.                                                      
      ******************************************************************        
                                                                                
           READ BP13F730           AT   END                                     
                MOVE HIGH-VALUES   TO   F730-NUM-REGN                           
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F730-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2200-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE HIGH-VALUES   TO   F595-NUM-REGN                           
                GO                 TO   2200-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F595-READ.                       
                                                                                
       2200-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE 'N'                                  TO WS-MATCH.               
                                                                                
           IF F730-NUM-REGN = F595-NUM-REGN                                     
                                                                                
              MOVE 'Y'                               TO WS-MATCH                
              PERFORM 4100-WRITE-P13F730A          THRU 4100-EXIT               
              PERFORM 2200-READ-BP13F595           THRU 2200-EXIT               
                UNTIL F595-NUM-REGN > F730-NUM-REGN                             
                   OR F595-NUM-REGN = HIGH-VALUES                               
              PERFORM 2100-READ-BP13F730           THRU 2100-EXIT               
           ELSE                                                                 
              IF F730-NUM-REGN < F595-NUM-REGN                                  
                 PERFORM 4100-WRITE-P13F730A       THRU 4100-EXIT               
                 PERFORM 2100-READ-BP13F730        THRU 2100-EXIT               
              ELSE                                                              
                 MOVE F595-NUM-REGN                  TO WS-NUM-REGN             
                 PERFORM 4200-WRITE-P13F730B       THRU 4200-EXIT               
                 PERFORM 2200-READ-BP13F595        THRU 2200-EXIT               
                   UNTIL F595-NUM-REGN > WS-NUM-REGN                            
                      OR F595-NUM-REGN = HIGH-VALUES                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4100-WRITE-P13F730A.                                                     
      ******************************************************************        
                                                                                
           IF F730-NUM-MCPS NOT = SPACES AND LOW-VALUES                         
              MOVE 'MCP'                 TO F730-CDE-ALLOC-SCH                  
           END-IF                                                               
                                                                                
           IF WS-MATCH = 'Y'                                                    
              IF F595-NUM-BOOK-STATUS = 'BF' OR 'LF' OR 'WF' OR                 
                                         'NC' OR 'SC' OR 'NV' OR                
                                         'NF' OR 'SF' OR 'NS' OR                
                                         'NF' OR 'SF' OR 'NS' OR                
                                         'LA' OR 'WA' OR 'LG' OR                
                                         'WG' OR 'LS' OR 'WS'                   
                 MOVE F595-NUM-BOOK-STATUS   TO F730-NUM-BOOK-STATUS            
              END-IF                                                            
                                                                                
              MOVE F595-CDE-NT1              TO F730-CDE-ZONE                   
              MOVE F595-CDE-FLAT-TYPE        TO F730-CDE-FLAT-TYPE              
           END-IF.                                                              
                                                                                
           WRITE P13F730A-REC          FROM BP13F730-REC.                       
           ADD 1                         TO WS-CNT-F730.                        
           ADD 1                         TO WS-CNT-TOT.                         
                                                                                
       4100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4200-WRITE-P13F730B.                                                     
      ******************************************************************        
                                                                                
           IF F595-NUM-MCPS NOT = SPACES AND LOW-VALUES                         
              MOVE 'MCP'                 TO F595-CDE-ALLOC-SCH                  
           END-IF.                                                              
                                                                                
           MOVE F595-CDE-NT1             TO F595-CDE-ZONE.                      
                                                                                
           WRITE P13F730B-REC          FROM BP13F595-REC.                       
           ADD 1                         TO WS-CNT-F595.                        
           ADD 1                         TO WS-CNT-TOT.                         
                                                                                
       4200-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CY04'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY 'NO OF BP13F730 RECORDS READ................. : '            
                    WS-BP13F730-READ.                                           
           DISPLAY 'NO OF BP13F595 RECORDS READ................. : '            
                    WS-BP13F595-READ.                                           
           DISPLAY 'NO OF P13F730A RECORDS WRITTEN ............. : '            
                    WS-CNT-F730.                                                
           DISPLAY 'NO OF P13F730B RECORDS WRITTEN ............. : '            
                    WS-CNT-F595.                                                
           DISPLAY 'TOTAL NO OF P13F730A RECORDS WRITTEN ........: '            
                    WS-CNT-TOT.                                                 
                                                                                
           CLOSE BP13F730                                                       
                 BP13F595                                                       
                 P13F730A                                                       
                 P13F730B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
