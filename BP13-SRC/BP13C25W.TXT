       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C25W.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  20 APRIL 2016.                                            
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  TO PRINT SMS LETTER FOR APPLICANT1 WITHOUT  *           
      *                 VALID HAND PHONE NUMBER                     *           
      *                                                             *           
      * OUTPUT       :  BP13L25W - INVITATION LETTER                *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      * -------- ---  ------  ------------------------------------- *           
      * BP136192 ESA1 200415  NEW PROGRAM                           *           
      * BP136467 ESA1 041016  ADD READING OF BP13F205 FOR LETTER DTE*           
      * BP136836 ESA1 170817  TO CATER FOR 'ROF' SELECTION TYPE     *           
      * BP137001 ESA1 290917  TO ADD BP139V FOR CHECKLIST           *           
      * BP137208 FNP1 150318  TO ADD BP13VN/BP13VP FOR SERS         *           
      * BP137013 ESA1 110418  TO ADD READING OF BP13K200 FOR APPMT  *           
      * BP137047 ESA1 130618  TO ADD CREATION OF P13F800A           *           
      * BP138023 CC37 071119  TO CHANGE THE LETTER DATE FOR SERS    *           
      * BP138201 MRR5 010720  CHECK IF WS-DTE-LETTER-2 IS VALID DATE*           
      * BP138721 MRR5 030621  SUBTR 14 DAYS FR APPT DTE INSTD OF 31 *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS BP13K820-STATUS.                           
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K830-KEY-FLD                               
                  ALTERNATE RECORD KEY IS  K830-NUM-REGN                        
                  FILE STATUS     IS BP13K830-STATUS.                           
                                                                                
           SELECT BP13K200 ASSIGN TO BP13K200                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K200-KEY-FLD                               
                  ALTERNATE RECORD KEY IS  K200-NUM-REGN                        
                  FILE STATUS     IS BP13K200-STATUS.                           
                                                                                
           SELECT BP13L25W ASSIGN TO BP13L25W.                                  
                                                                                
           SELECT P13F800A ASSIGN TO P13F800A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE  IS  F.                                              
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS IS STANDARD                                           
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K200                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13K200.                                                           
                                                                                
       FD   BP13L25W                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   BP13L25W-REC                PIC X(132).                             
                                                                                
       FD   P13F800A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE  IS  F.                                              
       01   P13F800A-MASTER    PIC X(2000).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K820-STATUS          PIC 9(02)  VALUE ZEROES.                
           05  BP13K830-STATUS          PIC 9(02)  VALUE ZEROES.                
           05  BP13K200-STATUS          PIC 9(02)  VALUE ZEROES.                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-MONTH                 PIC X(02)  VALUE SPACES.                
           05  WS-MONTH-DESC            PIC X(03)  VALUE SPACES.                
           05  WS-F800-END              PIC X      VALUE 'N'.                   
           05  WS-F205-END              PIC X      VALUE 'N'.                   
           05  WS-READ-F800             PIC 9(05)  VALUE ZEROES.                
           05  WS-WRITE-F800A           PIC 9(05)  VALUE ZEROES.                
           05  WS-STREET-NAME           PIC X(65)  VALUE SPACES.                
           05  WS-BLK-NO                PIC X(10)  VALUE SPACES.                
           05  WS-AGE                   PIC 9(03)  VALUE ZEROES.                
           05  WS-DTE-LETTER            PIC 9(08)  VALUE ZEROES.                
           05  WS-DTE-LETTER-2          PIC 9(08)  VALUE ZEROES.                
           05  WS-NUM-DTE-LETTER        PIC 9(07)  VALUE ZEROES.                
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURRENT-CCYY      PIC X(4)   VALUE SPACES.                
               10  WS-CURRENT-MM        PIC X(2)   VALUE SPACES.                
               10  WS-CURRENT-DD        PIC X(2)   VALUE SPACES.                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD         PIC 99     VALUE 00.                    
               10  FILLER               PIC X      VALUE '/'.                   
               10  WS-EDITED-MM         PIC 99     VALUE 00.                    
               10  FILLER               PIC X      VALUE '/'.                   
               10  WS-EDITED-CCYY       PIC 9999   VALUE 00.                    
           05  WS-COMP-DATE.                                                    
               10  WS-COMP-CCYY         PIC 9999   VALUE 00.                    
               10  WS-COMP-MM           PIC 99     VALUE 00.                    
               10  WS-COMP-DD           PIC 99     VALUE 00.                    
           05  WS-APPMT-TIME.                                                   
               10  WS-APPMT-HR          PIC 9(2)   VALUE 00.                    
               10  WS-APPMT-MIN         PIC 9(2)   VALUE 00.                    
                                                                                
           05  WS-F800-DTE-REGN.                                                
               10  WS-F800-DTE-REGN-YR  PIC 9(4)   VALUE ZEROES.                
               10  WS-F800-DTE-REGN-MM  PIC 9(2)   VALUE ZEROES.                
               10  WS-F800-DTE-REGN-DD  PIC 9(2)   VALUE ZEROES.                
                                                                                
           05  WS-K820-DTE-BIRTH.                                               
               10  WS-K820-DTE-BIRTH-YR  PIC 9(4)  VALUE ZEROES.                
               10  WS-K820-DTE-BIRTH-MM  PIC 9(2)  VALUE ZEROES.                
               10  WS-K820-DTE-BIRTH-DD  PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(77).                          
           05 WS-PARSED-STRING              PIC X(77).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  WS-L25W-PRT-VARS.                                                    
           05  L25W-PRT-HDG-1.                                                  
               10  FILLER               PIC X(27)  VALUE SPACES.                
               10  L25W-REGN-NO         PIC X(08)  VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-2.                                                  
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L25W-APPLN-NAME      PIC X(66)  VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-3.                                                  
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L25W-BLK-NO          PIC X(77)  VALUE SPACES.                
               10  FILLER               PIC X(2)   VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-4.                                                  
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L25W-NUM-FLAT.                                               
                   15  L25W-CHAR        PIC X      VALUE SPACE.                 
                   15  L25W-LEVEL       PIC X(2)   VALUE SPACES.                
                   15  L25W-DASH        PIC X      VALUE SPACE.                 
                   15  L25W-HSE-MAIN    PIC X(4)   VALUE SPACES.                
                   15  L25W-HSE-SUB     PIC X(3)   VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-5.                                                  
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L25W-CODE            PIC X(16)  VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-6.                                                  
               10  FILLER               PIC X(10)  VALUE SPACES.                
               10  L25W-DATE            PIC X(11)  VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-6A.                                                 
               10  FILLER               PIC X(61)  VALUE SPACES.                
               10  L25W-EXERCISE        PIC X(12)  VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-7.                                                  
               10  FILLER               PIC X(30)  VALUE SPACES.                
               10  L25W-APPMT-DATE      PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(04)  VALUE SPACES.                
               10  L25W-APPMT-HR        PIC Z9.                                 
               10  L25W-APPMT-HR-R       REDEFINES L25W-APPMT-HR                
                                        PIC X(2).                               
               10  FILLER               PIC X      VALUE ':'.                   
               10  L25W-APPMT-MIN       PIC X(2)   VALUE SPACES.                
               10  FILLER               PIC X      VALUE SPACES.                
               10  L25W-AM-PM           PIC X(2)   VALUE SPACES.                
               10  FILLER               PIC X(31)  VALUE SPACES.                
                                                                                
           05  L25W-PRT-HDG-7A.                                                 
               10  FILLER               PIC X(66)  VALUE SPACES.                
               10  L25W-APPMT-DATE-S    PIC X(10)  VALUE SPACES.                
               10  FILLER               PIC X(03)  VALUE SPACES.                
               10  L25W-APPMT-HR-S      PIC Z9.                                 
               10  L25W-APPMT-HR-R-S     REDEFINES L25W-APPMT-HR-S              
                                        PIC X(2).                               
               10  FILLER               PIC X      VALUE ':'.                   
               10  L25W-APPMT-MIN-S     PIC X(2)   VALUE SPACES.                
               10  FILLER               PIC X      VALUE SPACES.                
               10  L25W-AM-PM-S         PIC X(2)   VALUE SPACES.                
      *        10  FILLER               PIC X(31)  VALUE SPACES.                
                                                                                
      *------------------------------------------------------------*            
      *  DJDE LINE FOR SMS LETTER                                  *            
      *------------------------------------------------------------*            
       01  DJDE-LINE-SMS                  PIC X(90)  VALUE                      
           '$DJDE$ JDE=13C399,FORMS=BP13AL,COPIES=1,COLLATE=YES,DUPLEX=Y        
      -    'ES,JDL=BP13,NUMBER=NO,END;'.                                        
                                                                                
       01  DJDE-LINE-SMS2                 PIC X(90)  VALUE                      
           '$DJDE$ FORMS=BP139V,NUMBER=NO,END;'.                                
                                                                                
       01  DJDE-LINE-SMS-SERS             PIC X(90)  VALUE                      
           '$DJDE$ JDE=13C399,FORMS=BP13VN,COPIES=1,COLLATE=YES,DUPLEX=Y        
      -    'ES,JDL=BP13,NUMBER=NO,END;'.                                        
                                                                                
       01  DJDE-LINE-SMS2-SERS            PIC X(90)  VALUE                      
           '$DJDE$ FORMS=BP13VP,NUMBER=NO,END;'.                                
                                                                                
        01  DJDE-LINE-BLANKF               PIC X(90)  VALUE                     
            '$DJDE$ FORMS=BLANKF,NUMBER=NO,END;'.                               
                                                                                
      *------------------------------------------------------------*            
      *  AV02C001 PARAMETERS                                       *            
      *------------------------------------------------------------*            
       COPY AV02COMM.                                                           
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 1500-READ-BP13F205        THRU  1500-EXIT.                  
            PERFORM 2000-READ-BP13F800        THRU  2000-EXIT                   
                    UNTIL WS-F800-END = 'Y'.                                    
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           GOBACK.                                                              
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
           OPEN  INPUT  BP13F800                                                
                        BP13F205                                                
                        BP13K820                                                
                        BP13K830                                                
                        BP13K200                                                
                 OUTPUT BP13L25W                                                
                        P13F800A.                                               
                                                                                
            IF BP13K820-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K820-STATUS IS ' BP13K820-STATUS          
               MOVE BP13K820-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K830-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K830-STATUS IS ' BP13K830-STATUS          
               MOVE BP13K830-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K200-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K200-STATUS IS ' BP13K200-STATUS          
               MOVE BP13K200-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                 
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1500-READ-BP13F205.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F205                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F205-END                         
           END-READ.                                                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-F800-END                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-READ-F800.                                               
                                                                                
           IF WS-READ-F800 = 1                                                  
              MOVE SPACES TO BP13L25W-REC                                       
              WRITE BP13L25W-REC AFTER PAGE                                     
           END-IF.                                                              
                                                                                
           PERFORM 3000-MOVE-DETAILS    THRU  3000-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MOVE-DETAILS.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                  TO BP13L25W-REC.                        
                                                                                
           PERFORM 8200-READ-BP13K200  THRU 8200-EXIT.                          
                                                                                
      *--  LETTER PRINT DATE                                                    
           MOVE SPACES                  TO WS-MONTH-DESC.                       
           MOVE F205-DTE-REQUEST       TO WS-DTE-LETTER                         
           IF F800-NUM-MGPS = 'P' OR 'C'                                        
              IF K200-DTE-BKAPPMT-DATE-MGPS NOT = SPACES AND                    
                                                  LOW-VALUES                    
                 MOVE K200-DTE-BKAPPMT-DATE-MGPS                                
                                            TO WS-DTE-LETTER-2                  
              ELSE                                                              
                 MOVE F800-DTE-BK-APPT-MGPS TO WS-DTE-LETTER-2                  
              END-IF                                                            
           ELSE                                                                 
              IF K200-DTE-BKAPPMT-DATE NOT = SPACES AND LOW-VALUES              
                 MOVE K200-DTE-BKAPPMT-DATE                                     
                                            TO WS-DTE-LETTER-2                  
              ELSE                                                              
                 MOVE F800-DTE-BK-APPT      TO WS-DTE-LETTER-2                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-LETTER-2           TO WS-IN-DATE.                        
           MOVE 'CD'                      TO WS-IN-OPTION.                      
           CALL 'AV02C001' USING LINK-REC.                                      
                                                                                
           IF WS-RETURN = '000'                                                 
              COMPUTE WS-NUM-DTE-LETTER =                                       
                      FUNCTION INTEGER-OF-DATE(WS-DTE-LETTER-2)                 
      *       SUBTRACT 31         FROM   WS-NUM-DTE-LETTER                      
              SUBTRACT 14         FROM   WS-NUM-DTE-LETTER                      
              COMPUTE WS-DTE-LETTER-2 =                                         
                      FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-LETTER)               
           ELSE                                                                 
              MOVE ZEROES TO WS-DTE-LETTER-2                                    
           END-IF.                                                              
                                                                                
           IF F800-NUM-ALLO-CAT = 'SER'                                         
              MOVE SPACES                  TO WS-MONTH-DESC                     
              MOVE F205-DTE-REQUEST(5:2)   TO WS-MONTH                          
              PERFORM 4000-DECODE-MONTH    THRU 4000-EXIT                       
              STRING F205-DTE-REQUEST(7:2) ' '  WS-MONTH-DESC ' '               
                     F205-DTE-REQUEST(1:4) DELIMITED BY SIZE                    
                    INTO L25W-DATE                                              
              END-STRING                                                        
           ELSE                                                                 
              IF WS-DTE-LETTER-2 > F205-DTE-REQUEST                             
                 MOVE SPACES                 TO WS-MONTH-DESC                   
                 MOVE WS-DTE-LETTER-2(5:2)   TO WS-MONTH                        
                 PERFORM 4000-DECODE-MONTH    THRU 4000-EXIT                    
                 IF WS-DTE-LETTER-2 = ZEROES                                    
                    MOVE SPACES TO L25W-DATE                                    
                 ELSE                                                           
                    STRING WS-DTE-LETTER-2(7:2) ' '  WS-MONTH-DESC ' '          
                           WS-DTE-LETTER-2(1:4) DELIMITED BY SIZE               
                           INTO L25W-DATE                                       
                    END-STRING                                                  
                 END-IF                                                         
              ELSE                                                              
                 MOVE SPACES                  TO WS-MONTH-DESC                  
                 MOVE F205-DTE-REQUEST(5:2)   TO WS-MONTH                       
                 PERFORM 4000-DECODE-MONTH    THRU 4000-EXIT                    
                 STRING F205-DTE-REQUEST(7:2) ' '  WS-MONTH-DESC ' '            
                        F205-DTE-REQUEST(1:4) DELIMITED BY SIZE                 
                       INTO L25W-DATE                                           
                 END-STRING                                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO WS-MONTH-DESC.                       
           MOVE F800-DTE-BALLOT(5:2)    TO WS-MONTH.                            
           PERFORM 4000-DECODE-MONTH    THRU 4000-EXIT.                         
           STRING WS-MONTH-DESC ' ' F800-DTE-BALLOT(1:4)                        
                  ' ' F800-NUM-ALLO-CAT DELIMITED BY SIZE                       
                  INTO L25W-EXERCISE                                            
           END-STRING.                                                          
                                                                                
           MOVE F800-NUM-REGN          TO L25W-REGN-NO.                         
                                                                                
           MOVE SPACES                 TO WS-COMP-DATE                          
                                          WS-APPMT-TIME.                        
      *--  APPMT DATE/TIME                                                      
                                                                                
           IF F800-NUM-MGPS = 'P' OR 'C'                                        
              IF K200-DTE-BKAPPMT-DATE-MGPS NOT = SPACES AND                    
                                                  LOW-VALUES                    
                 MOVE K200-DTE-BKAPPMT-DATE-MGPS                                
                                            TO WS-COMP-DATE                     
                 MOVE K200-TME-BKAPPMT-MGPS TO WS-APPMT-TIME                    
              ELSE                                                              
                 MOVE F800-DTE-BK-APPT-MGPS TO WS-COMP-DATE                     
                 MOVE F800-TME-BK-APPT-MGPS TO WS-APPMT-TIME                    
              END-IF                                                            
           ELSE                                                                 
              IF K200-DTE-BKAPPMT-DATE NOT = SPACES AND LOW-VALUES              
                 MOVE K200-DTE-BKAPPMT-DATE TO WS-COMP-DATE                     
                 MOVE K200-TME-BKAPPMT      TO WS-APPMT-TIME                    
              ELSE                                                              
                 MOVE F800-DTE-BK-APPT      TO WS-COMP-DATE                     
                 MOVE F800-TME-BK-APPT      TO WS-APPMT-TIME                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-COMP-CCYY           TO  WS-EDITED-CCYY.                      
           MOVE WS-COMP-MM             TO  WS-EDITED-MM.                        
           MOVE WS-COMP-DD             TO  WS-EDITED-DD.                        
           MOVE WS-EDITED-DATE         TO  L25W-APPMT-DATE                      
                                           L25W-APPMT-DATE-S.                   
                                                                                
           IF WS-APPMT-TIME NOT = SPACES                                        
              IF WS-APPMT-HR > 11                                               
                 MOVE 'PM'  TO L25W-AM-PM                                       
                               L25W-AM-PM-S                                     
              ELSE                                                              
                 MOVE 'AM'  TO L25W-AM-PM                                       
                               L25W-AM-PM-S                                     
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES   TO L25W-AM-PM                                       
                               L25W-AM-PM-S                                     
           END-IF.                                                              
                                                                                
           IF WS-APPMT-TIME NOT = SPACES                                        
              IF WS-APPMT-HR > 12                                               
                 COMPUTE WS-APPMT-HR = WS-APPMT-HR - 12                         
                 MOVE WS-APPMT-HR TO   L25W-APPMT-HR                            
                                       L25W-APPMT-HR-S                          
              ELSE                                                              
                 MOVE WS-APPMT-HR TO   L25W-APPMT-HR                            
                                       L25W-APPMT-HR-S                          
              END-IF                                                            
            ELSE                                                                
               MOVE    SPACES     TO   L25W-APPMT-HR-R                          
                                       L25W-APPMT-HR-R-S                        
            END-IF.                                                             
                                                                                
            MOVE  WS-APPMT-MIN         TO L25W-APPMT-MIN                        
                                          L25W-APPMT-MIN-S.                     
                                                                                
            PERFORM 6000-READ-BP13K820 THRU 6000-EXIT.                          
                                                                                
            IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES                 
               CONTINUE                                                         
            ELSE                                                                
               IF K820-NUM-HP-PGR IS NOT NUMERIC                                
                  CONTINUE                                                      
               ELSE                                                             
                  IF F800-NUM-ALLO-CAT = 'ROF'                                  
                     IF F800-NUM-ELDERLY = 'F' OR 'S'                           
                        CONTINUE                                                
                     ELSE                                                       
                        GO TO 3000-EXIT                                         
                     END-IF                                                     
                  ELSE                                                          
                     IF (F800-NUM-FLAT-TYPE = '2F' AND                          
                         F800-DTE-BALLOT >= '201608' AND                        
                        (F800-NUM-ELDERLY = 'F' OR 'S'))                        
                        CONTINUE                                                
                     ELSE                                                       
                        GO TO 3000-EXIT                                         
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            MOVE K820-NME-OCCP         TO L25W-APPLN-NAME.                      
                                                                                
            PERFORM 7000-GET-ADDR      THRU 7000-EXIT.                          
                                                                                
            IF F800-NUM-ALLO-CAT = 'SER'                                        
               PERFORM 8100-PRINT-SERS    THRU 8100-EXIT                        
            ELSE                                                                
               PERFORM 8000-PRINT-LETTER  THRU 8000-EXIT                        
            END-IF.                                                             
                                                                                
           MOVE  BP13F800-MASTER TO P13F800A-MASTER.                            
           WRITE P13F800A-MASTER.                                               
           ADD 1 TO   WS-WRITE-F800A.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-COMPUTE-AGE.                                                        
      *-------------------------------------------------------------            
                                                                                
            MOVE F800-DTE-REGN             TO WS-F800-DTE-REGN.                 
            MOVE K820-DTE-BIRTH            TO WS-K820-DTE-BIRTH.                
                                                                                
            COMPUTE WS-AGE = WS-F800-DTE-REGN-YR   -                            
                             WS-K820-DTE-BIRTH-YR                               
                                                                                
      *---TO CHECK MONTH AND DAY OF BIRTH FOR AGE = 30 CASES -------*           
                                                                                
            IF WS-AGE  = 30                                                     
               IF WS-K820-DTE-BIRTH-MM   > WS-F800-DTE-REGN-MM                  
                     SUBTRACT 1 FROM WS-AGE                                     
               ELSE                                                             
                  IF WS-K820-DTE-BIRTH-MM   = WS-F800-DTE-REGN-MM               
                     IF WS-K820-DTE-BIRTH-DD   >                                
                                         WS-F800-DTE-REGN-DD                    
                        SUBTRACT 1 FROM WS-AGE                                  
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
                                                                                
               IF WS-F800-DTE-REGN-MM > WS-K820-DTE-BIRTH-MM                    
                  ADD 1 TO  WS-AGE                                              
               ELSE                                                             
                  IF WS-F800-DTE-REGN-MM  = WS-K820-DTE-BIRTH-MM                
                     IF WS-F800-DTE-REGN-DD > WS-K820-DTE-BIRTH-DD              
                        ADD 1 TO WS-AGE                                         
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
                                                                                
            END-IF.                                                             
                                                                                
      *---TO CHECK MONTH AND DAY OF BIRTH FOR AGE = 55 CASES -------*           
                                                                                
           IF WS-AGE = 55                                                       
              IF WS-K820-DTE-BIRTH-MM > WS-F800-DTE-REGN-MM                     
                 SUBTRACT 1                 FROM WS-AGE                         
              ELSE                                                              
                 IF WS-K820-DTE-BIRTH-MM = WS-F800-DTE-REGN-MM                  
                    IF WS-K820-DTE-BIRTH-DD > WS-F800-DTE-REGN-DD               
                       SUBTRACT 1           FROM WS-AGE                         
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------*              
       4000-DECODE-MONTH.                                                       
      *----------------------------------------------------------*              
           EVALUATE WS-MONTH                                                    
               WHEN '01'                                                        
                  MOVE 'JAN'        TO WS-MONTH-DESC                            
               WHEN '02'                                                        
                  MOVE 'FEB'        TO WS-MONTH-DESC                            
               WHEN '03'                                                        
                  MOVE 'MAR'        TO WS-MONTH-DESC                            
               WHEN '04'                                                        
                  MOVE 'APR'        TO WS-MONTH-DESC                            
               WHEN '05'                                                        
                  MOVE 'MAY'        TO WS-MONTH-DESC                            
               WHEN '06'                                                        
                  MOVE 'JUN'        TO WS-MONTH-DESC                            
               WHEN '07'                                                        
                  MOVE 'JUL'        TO WS-MONTH-DESC                            
               WHEN '08'                                                        
                  MOVE 'AUG'        TO WS-MONTH-DESC                            
               WHEN '09'                                                        
                  MOVE 'SEP'        TO WS-MONTH-DESC                            
               WHEN '10'                                                        
                  MOVE 'OCT'        TO WS-MONTH-DESC                            
               WHEN '11'                                                        
                  MOVE 'NOV'        TO WS-MONTH-DESC                            
               WHEN '12'                                                        
                  MOVE 'DEC'        TO WS-MONTH-DESC                            
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES         TO K820-KEY-FLD.                                
            MOVE F800-NUM-REGN  TO K820-NUM-REGN.                               
            MOVE F800-NUM-NRIC1 TO K820-NUM-NRIC.                               
                                                                                
            READ BP13K820.                                                      
                                                                                
            EVALUATE BP13K820-STATUS                                            
              WHEN  00                                                          
                    CONTINUE                                                    
              WHEN  23                                                          
                    MOVE SPACES      TO BP13K820-REC                            
                    DISPLAY 'REC NOTFND IN BP13K820 : ' BP13K820-STATUS         
                       ', K820-KEY-FLD : ' K820-KEY-FLD                         
              WHEN  OTHER                                                       
                    DISPLAY 'READ BP13K820 ERROR ' F800-NUM-REGN                
                    DISPLAY 'STATUS ' BP13K820-STATUS                           
                    MOVE BP13K820-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       7000-GET-ADDR.                                                           
      *-------------------------------------------------------------            
           MOVE SPACES                TO L25W-BLK-NO                            
                                         L25W-NUM-FLAT                          
                                         L25W-CODE                              
                                         WS-STREET-NAME                         
                                         WS-BLK-NO.                             
                                                                                
           MOVE F800-NUM-REGN TO K830-NUM-REGN.                                 
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K830-STATUS                                             
              WHEN  00                                                          
              WHEN  02                                                          
                    IF (K830-NUM-MAIL-ADDR  = SPACES OR LOW-VALUES) OR          
                       ((K830-NUM-MAIL-LVL-CHAR = '#') AND                      
                       (K830-NUM-MAIL-LVL-NUM = SPACES OR LOW-VALUES))          
                       IF K830-NUM-ADDR-TYPE  = 'H'                             
                          MOVE K830-NUM-BLK       TO WS-BLK-NO                  
                          MOVE K830-NME-STREET    TO WS-STREET-NAME             
                          MOVE K830-NUM-LVL-CHAR  TO L25W-CHAR                  
                          MOVE K830-NUM-LVL-NUM   TO L25W-LEVEL                 
                          MOVE K830-NUM-LVL-DASH  TO L25W-DASH                  
                          MOVE K830-NUM-HSEMAIN   TO L25W-HSE-MAIN              
                          MOVE K830-NUM-HSESUB    TO L25W-HSE-SUB               
                          PERFORM 7100-GET-STREET-NAME  THRU 7100-EXIT          
                          STRING 'SINGAPORE ' K830-NUM-POSTAL                   
                                  DELIMITED BY SIZE INTO L25W-CODE              
                       ELSE                                                     
                          MOVE K830-NUM-BLK         TO WS-BLK-NO                
                          MOVE K830-NUM-FLAT        TO L25W-NUM-FLAT            
                          MOVE K830-NME-STREET-BLDG TO WS-STREET-NAME           
                          PERFORM 7100-GET-STREET-NAME  THRU 7100-EXIT          
                          IF K830-NUM-POSTAL = SPACES OR LOW-VALUES OR          
                                               ZEROES                           
                             MOVE SPACES TO L25W-CODE                           
                          ELSE                                                  
                             STRING 'SINGAPORE ' K830-NUM-POSTAL                
                                     DELIMITED BY SIZE INTO L25W-CODE           
                          END-IF                                                
                       END-IF                                                   
                    ELSE                                                        
                       IF K830-NUM-MAIL-ADDR-TYPE =  'H'                        
                          MOVE K830-NUM-MAIL-BLK      TO WS-BLK-NO              
                          MOVE K830-NME-MAIL-STREET                             
                                     TO WS-STREET-NAME                          
                          MOVE K830-NUM-MAIL-LVL-CHAR TO L25W-CHAR              
                          MOVE K830-NUM-MAIL-LVL-NUM  TO L25W-LEVEL             
                          MOVE K830-NUM-MAIL-LVL-DASH TO L25W-DASH              
                          MOVE K830-NUM-MAIL-HSEMAIN  TO L25W-HSE-MAIN          
                          MOVE K830-NUM-MAIL-HSESUB   TO L25W-HSE-SUB           
                          PERFORM 7100-GET-STREET-NAME   THRU 7100-EXIT         
                          STRING 'SINGAPORE '   K830-NUM-MAIL-POSTAL            
                                  DELIMITED BY SIZE INTO L25W-CODE              
                       ELSE                                                     
                          MOVE K830-NUM-MAIL-BLK      TO WS-BLK-NO              
                          MOVE K830-NME-MAIL-STR-BLDG                           
                                            TO WS-STREET-NAME                   
                          MOVE K830-NUM-MAIL-FLAT TO L25W-NUM-FLAT              
                          PERFORM 7100-GET-STREET-NAME  THRU 7100-EXIT          
                          IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES        
                                            OR ZEROES                           
                             MOVE SPACES TO L25W-CODE                           
                          ELSE                                                  
                             STRING 'SINGAPORE '   K830-NUM-MAIL-POSTAL         
                                   DELIMITED BY SIZE INTO L25W-CODE             
                          END-IF                                                
                       END-IF                                                   
                   END-IF                                                       
              WHEN 23                                                           
                   MOVE SPACES  TO L25W-NUM-FLAT                                
                                   L25W-BLK-NO                                  
                                   L25W-CODE                                    
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K830 = ' BP13K830-STATUS          
                   DISPLAY '      K830-NUM-REGN    = ' K830-NUM-REGN            
                   EXIT PROGRAM                                                 
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7100-GET-STREET-NAME.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES       TO WS-STRING-TO-PARSE.                             
           STRING WS-BLK-NO ' ' WS-STREET-NAME                                  
                  DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                     
           END-STRING.                                                          
           MOVE LENGTH OF WS-STRING-TO-PARSE                                    
                             TO WS-STRING-LENGTH                                
           MOVE 'Y'          TO WS-INSERT-SPACE-FLAG                            
           PERFORM 7200-PARSE-STRING    THRU 7200-EXIT                          
           MOVE WS-PARSED-STRING   TO  L25W-BLK-NO.                             
                                                                                
       7100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------           
       7200-PARSE-STRING.                                                       
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 7300-GET-CHAR          THRU 7300-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
       7200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------          
       7300-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       7300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------*              
       8000-PRINT-LETTER.                                                       
      *----------------------------------------------------------*              
           MOVE  SPACES       TO   BP13L25W-REC.                                
           WRITE BP13L25W-REC FROM DJDE-LINE-SMS    AFTER PAGE.                 
                                                                                
           MOVE  SPACES       TO   BP13L25W-REC.                                
           WRITE BP13L25W-REC AFTER PAGE.                                       
                                                                                
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-1 AFTER 6.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-2 AFTER 3.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-3 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-4 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-5 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-6 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-6A AFTER 8.                     
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-7 AFTER 1.                      
                                                                                
           IF K820-NUM-HP-PGR IS NOT NUMERIC                                    
              MOVE SPACES  TO K820-NUM-HP-PGR                                   
           END-IF.                                                              
                                                                                
           IF F800-NUM-ALLO-CAT = 'ROF'                                         
              IF F800-NUM-ELDERLY = 'F' OR 'S'                                  
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC FROM DJDE-LINE-SMS2 AFTER PAGE              
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC AFTER 5                                     
              ELSE                                                              
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC FROM DJDE-LINE-BLANKF AFTER PAGE            
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC AFTER 5                                     
              END-IF                                                            
           ELSE                                                                 
              IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES               
                 IF F800-NUM-FLAT-TYPE = '2F'                                   
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC FROM DJDE-LINE-SMS2 AFTER PAGE           
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC AFTER 5                                  
                 ELSE                                                           
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC FROM DJDE-LINE-BLANKF AFTER PAGE         
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC AFTER 5                                  
                 END-IF                                                         
              ELSE                                                              
                 IF (F800-NUM-FLAT-TYPE = '2F' AND                              
                    (F800-NUM-ELDERLY = 'F' OR 'S'))                            
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC FROM DJDE-LINE-SMS2 AFTER PAGE           
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC AFTER 5                                  
                 ELSE                                                           
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC FROM DJDE-LINE-BLANKF AFTER PAGE         
                    MOVE  SPACES         TO    BP13L25W-REC                     
                    WRITE BP13L25W-REC AFTER 5                                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------*              
       8100-PRINT-SERS.                                                         
      *----------------------------------------------------------*              
           MOVE  SPACES       TO   BP13L25W-REC.                                
           WRITE BP13L25W-REC FROM DJDE-LINE-SMS-SERS AFTER PAGE.               
                                                                                
           MOVE  SPACES       TO   BP13L25W-REC.                                
           WRITE BP13L25W-REC AFTER PAGE.                                       
                                                                                
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-1 AFTER 6.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-2 AFTER 3.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-3 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-4 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-5 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-6 AFTER 1.                      
           WRITE BP13L25W-REC FROM L25W-PRT-HDG-7A AFTER 9.                     
                                                                                
           IF K820-NUM-HP-PGR IS NOT NUMERIC                                    
              MOVE SPACES  TO K820-NUM-HP-PGR                                   
           END-IF.                                                              
                                                                                
           IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES                  
              IF F800-NUM-FLAT-TYPE = '2F'                                      
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC FROM DJDE-LINE-SMS2-SERS AFTER PAGE         
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC AFTER 5                                     
              ELSE                                                              
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC FROM DJDE-LINE-BLANKF AFTER PAGE            
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC AFTER 5                                     
              END-IF                                                            
           ELSE                                                                 
              IF (F800-NUM-FLAT-TYPE = '2F' AND                                 
                 (F800-NUM-ELDERLY = 'F' OR 'S'))                               
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC FROM DJDE-LINE-SMS2-SERS AFTER PAGE         
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC AFTER 5                                     
              ELSE                                                              
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC FROM DJDE-LINE-BLANKF AFTER PAGE            
                 MOVE  SPACES         TO    BP13L25W-REC                        
                 WRITE BP13L25W-REC AFTER 5                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------*              
       8200-READ-BP13K200.                                                      
      *----------------------------------------------------------*              
           MOVE F800-NUM-REGN            TO K200-NUM-REGN.                      
                                                                                
           READ BP13K200 KEY IS K200-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K200-STATUS                                             
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K200-REC                        
                    DISPLAY 'REGN NO. NOT FOUND IN BP13K200. REGN = '           
                            F800-NUM-REGN                                       
               WHEN OTHER                                                       
                    DISPLAY 'READ ERROR BP13K200, STATUS '                      
                            BP13K200-STATUS ' ' F800-NUM-REGN                   
                    MOVE BP13K200-STATUS  TO RETURN-CODE                        
           END-EVALUATE.                                                        
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '--- BP13C25W CONTROL TOTAL ---- '                           
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY ' BP13F800 RECORDS READ : ' WS-READ-F800.                    
           DISPLAY ' P13F800A RECORDS WRITE: ' WS-WRITE-F800A.                  
                                                                                
           CLOSE BP13F800                                                       
                 BP13F205                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K200                                                       
                 BP13L25W                                                       
                 P13F800A.                                                      
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR, BP13K820   ' BP13K820-STATUS              
              MOVE BP13K820-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR, BP13K830   ' BP13K830-STATUS              
              MOVE BP13K830-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13K200-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR, BP13K200   ' BP13K200-STATUS              
              MOVE BP13K200-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
