       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB9A.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  26/07/2010.                                               
      ******************************************************************        
      **               SYSTEM OF COMMITMENT (BP13)                    **        
      ******************************************************************        
      ** PROGRAM OBJECTIVE : BACKUP SURVEY FEE VOUCHER FILE           **        
      **                     CREATE TRANSACTION FILE                  **        
      **--------------------------------------------------------------**        
      ** FILE USED - INPUT  SY02F001 - SYSTEM DATE                    **        
      **                    BP13FD85 - SURVEY FEE VOUCHER FILE        **        
      **                    AG07F640 - VOUCHER TRANSACTION FILE       **        
      **                    BP13KA10 - PROJECT FILE                   **        
      **             I-O    BP13KD85 - SURVEY FEE VOUCHER FILE        **        
      **             OUTPUT P13FD85A - BACKUP FILE                    **        
      **                    BP13F125 - SOC DAILY TRANSACTION FILE     **        
      **                    BP13LB9A - VOUCHER REPORT                 **        
      ******************************************************************        
      **CHG REQ   BY     DATE      DETAIL                             **        
      **--------  -----  --------  -----------------------------------**        
      **BP133905  JB8    26072010  NEW PROGRAM.                       **        
      **BP133905  JB8    13102010  TO OUTPUT VOUCHER REPORT.          **        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001       ASSIGN TO SY02F001.                            
           SELECT BP13FD85       ASSIGN TO BP13FD85.                            
           SELECT AG07F640       ASSIGN TO AG07F640.                            
           SELECT BP13KD85       ASSIGN TO BP13KD85                             
                          ACCESS MODE   IS DYNAMIC                              
                          ORGANIZATION  IS INDEXED                              
                          RECORD KEY    IS KD85-KEY-FLD                         
                          FILE STATUS   IS WS-KD85-STATUS.                      
           SELECT BP13KA10       ASSIGN TO BP13KA10                             
                          ACCESS MODE   IS DYNAMIC                              
                          ORGANIZATION  IS INDEXED                              
                          RECORD KEY    IS KA10-KEY-FLD                         
                          FILE STATUS   IS WS-KA10-STATUS.                      
           SELECT BP13F125       ASSIGN TO BP13F125.                            
           SELECT P13FD85A       ASSIGN TO P13FD85A.                            
           SELECT BP13LB9A       ASSIGN TO BP13LB9A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD85                                                            
            RECORD CONTAINS 80   CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD85.                                                           
                                                                                
       FD   AG07F640                                                            
            RECORD CONTAINS 100  CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AG07F640.                                                           
                                                                                
       FD   BP13KD85                                                            
            RECORD CONTAINS 80   CHARACTERS.                                    
       COPY BP13KD85.                                                           
                                                                                
       FD   BP13KA10                                                            
            RECORD CONTAINS 800  CHARACTERS.                                    
       COPY BP13KA10.                                                           
                                                                                
       FD   P13FD85A                                                            
            RECORD CONTAINS 80   CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13FD85A-REC PIC X(80).                                             
                                                                                
       FD   BP13F125                                                            
            RECORD CONTAINS 150  CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F125.                                                           
                                                                                
       FD   BP13LB9A                                                            
            RECORD CONTAINS 132  CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13LB9A-REC   PIC X(132).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  CNT-FD85-READ                PIC 9(07)  VALUE ZEROS.             
           05  CNT-F640-READ                PIC 9(07)  VALUE ZEROS.             
           05  CNT-KD85-READ                PIC 9(07)  VALUE ZEROS.             
           05  CNT-KD85-DELETE              PIC 9(07)  VALUE ZEROS.             
           05  CNT-FD85A-WRITE              PIC 9(07)  VALUE ZEROS.             
           05  CNT-F125-WRITE               PIC 9(07)  VALUE ZEROS.             
           05  WS-KD85-STATUS               PIC 9(02)  VALUE ZEROS.             
           05  WS-KA10-STATUS               PIC 9(02)  VALUE ZEROS.             
           05  WS-SYS-DATE-TIME.                                                
               10  WS-SYS-DATE              PIC X(08)  VALUE SPACES.            
               10  WS-SYS-TIME              PIC X(08)  VALUE SPACES.            
               10  FILLER                   PIC X(05)  VALUE SPACES.            
           05  CNT-VR                       PIC 9(02)  VALUE ZEROS.             
           05  CNT-VR-REGN                  PIC 9(02)  VALUE ZEROS.             
           05  CNT-REGN                     PIC 9(02)  VALUE ZEROS.             
           05  CNT-PAGE                     PIC 9(02)  VALUE ZEROS.             
           05  CNT-PAGE-REC                 PIC 9(02)  VALUE 60.                
           05  WS-NUM-VR                    PIC X(11)  VALUE SPACES.            
                                                                                
      *-----------------------------------------------------------*             
      * LAYOUT FOR NOT FOUND REPORT                               *             
      *-----------------------------------------------------------*             
       01 LB9A-HEADER1.                                                         
          05 FILLER                      PIC X(08) VALUE 'BP13LB9A'.            
          05 FILLER                      PIC X(06) VALUE SPACES.                
          05 FILLER                      PIC X(04) VALUE 'HDB3'.                
          05 FILLER                      PIC X(38) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE                        
                                         'SYSTEM OF COMMITMENT'.                
          05 FILLER                      PIC X(26) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'DATE : '.             
          05 LB9A-DATE                   PIC X(10).                             
          05 FILLER                      PIC X(02) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'PAGE : '.             
          05 LB9A-PAGE                   PIC Z(04).                             
                                                                                
       01 LB9A-HEADER2.                                                         
          05 FILLER                      PIC X(55) VALUE SPACES.                
          05 FILLER                      PIC X(22) VALUE                        
             'VOUCHERS OF SURVEY FEE'.                                          
          05 FILLER                      PIC X(55) VALUE SPACES.                
                                                                                
       01 LB9A-VR-HEADER1.                                                      
          05 FILLER                      PIC X(18) VALUE                        
             'PROJECT CODE    : '.                                              
          05 LB9A-PRJCT                  PIC X(03) VALUE SPACES.                
                                                                                
       01 LB9A-VR-HEADER2.                                                      
          05 FILLER                      PIC X(12) VALUE                        
             'VOUCHER NO ('.                                                    
          05 LB9A-VR-SNO                 PIC Z9    VALUE ZEROES.                
          05 FILLER                      PIC X(04) VALUE                        
             ') : '.                                                            
          05 LB9A-VR-NO                  PIC X(14) VALUE SPACES.                
                                                                                
       01 LB9A-VR-DTL-HEADER.                                                   
          05 FILLER                      PIC X(13) VALUE SPACES.                
          05 FILLER                      PIC X(42) VALUE                        
             'SNO     REGN        SURVEY FEE     ADDRESS'.                      
                                                                                
       01 LB9A-VR-DTL.                                                          
          05 FILLER                      PIC X(14) VALUE SPACES.                
          05 LB9A-VR-DTE-SNO             PIC Z9    VALUE ZEROES.                
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 LB9A-VR-REGN                PIC X(08) VALUE SPACES.                
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 LB9A-VR-SURVEY-FEE          PIC ZZZZZ9.99 VALUE ZEROES.            
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 LB9A-VR-ADDR                PIC X(25) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 8000-OPEN-FILES          THRU 8000-EXIT.                     
           PERFORM 8001-READ-BP13FD85       THRU 8001-EXIT.                     
           PERFORM 8002-READ-AG07F640       THRU 8002-EXIT.                     
                                                                                
           PERFORM 1000-PROCESS-MATCH       THRU 1000-EXIT                      
             UNTIL FD85-NUM-VOUCHER = HIGH-VALUES                               
               AND F640-NUM-VR      = HIGH-VALUES.                              
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-PROCESS-MATCH.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF FD85-NUM-VOUCHER = F640-NUM-VR                                    
              PERFORM 2000-BACKUP-BP13KD85  THRU 2000-EXIT                      
                UNTIL FD85-NUM-VOUCHER NOT = F640-NUM-VR                        
              PERFORM 8002-READ-AG07F640    THRU 8002-EXIT                      
           ELSE                                                                 
              IF FD85-NUM-VOUCHER < F640-NUM-VR                                 
                 PERFORM 8001-READ-BP13FD85 THRU 8001-EXIT                      
              ELSE                                                              
                 PERFORM 8002-READ-AG07F640 THRU 8002-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-BACKUP-BP13KD85.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE FD85-KEY-FLD                  TO KD85-KEY-FLD.                  
           PERFORM 8003-READ-BP13KD85       THRU 8003-EXIT.                     
                                                                                
           WRITE P13FD85A-REC FROM BP13KD85-REC                                 
           ADD 1                              TO CNT-FD85A-WRITE.               
                                                                                
           PERFORM 2100-CREATE-BP13F125     THRU 2100-EXIT.                     
           PERFORM 2200-OUTPUT-REPORT       THRU 2200-EXIT.                     
                                                                                
           PERFORM 8004-DELETE-BP13KD85     THRU 8004-EXIT.                     
                                                                                
           PERFORM 8001-READ-BP13FD85       THRU 8001-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-CREATE-BP13F125.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                       TO BP13F125-CASHTRAN.              
                                                                                
           MOVE '72'                         TO F125-CDE-TRANS-TYPE.            
                                                                                
           MOVE KD85-AMT-SURVEY-FEE          TO F125-AMT-RECEIPT.               
           MOVE KD85-NUM-REGN                TO F125-NUM-REGN.                  
                                                                                
           MOVE 'DSV'                        TO F125-CDE-PAYMENT-TYPE.          
           MOVE SPACES                       TO F125-NUM-NRIC.                  
           MOVE SPACES                       TO F125-CDE-CPF-APPL.              
                                                                                
           MOVE KD85-NUM-PRJCT               TO KA10-NUM-PRJCT                  
           PERFORM 8005-READ-BP13KA10      THRU 8005-EXIT.                      
           MOVE KA10-NME-DVLPR-PARENT        TO F125-NME-NAME.                  
                                                                                
           STRING F640-NUM-BATCH                                                
                  F640-NUM-VR-SERIAL                                            
                  DELIMITED BY SIZE INTO F125-NUM-RECPT-JRNO.                   
           MOVE F640-NUM-VR-DDD              TO F125-NUM-VR-DDD.                
           MOVE F640-NUM-VR-YY               TO F125-NUM-VR-YY.                 
                                                                                
           MOVE F640-DTE-CHQ-READY           TO F125-DTE-TRANS.                 
           MOVE WS-SYS-DATE                  TO F125-DTE-POST.                  
           MOVE SPACES                       TO F125-CDE-APPLT-CPF.             
           MOVE ZERO                         TO F125-COUNT-HISTORY.             
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1                             TO CNT-F125-WRITE.                 
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-OUTPUT-REPORT.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF KD85-NUM-VOUCHER NOT = WS-NUM-VR                                  
              ADD 1                          TO CNT-VR                          
              MOVE ZEROES                    TO CNT-VR-REGN                     
              MOVE KD85-NUM-VOUCHER          TO WS-NUM-VR                       
              PERFORM 2220-OUTPUT-VR-HEAD  THRU 2220-EXIT                       
           END-IF.                                                              
                                                                                
           ADD 1                             TO CNT-REGN                        
           ADD 1                             TO CNT-VR-REGN                     
           PERFORM 2230-OUTPUT-REGN-DTL    THRU 2230-EXIT.                      
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2210-OUTPUT-HEADER.                                                      
      *-------------------------------------------------------------            
                                                                                
           ADD 1                             TO CNT-PAGE.                       
           MOVE CNT-PAGE                     TO LB9A-PAGE.                      
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE INTO LB9A-DATE.                             
                                                                                
           MOVE SPACES                       TO BP13LB9A-REC                    
           WRITE BP13LB9A-REC AFTER PAGE.                                       
           WRITE BP13LB9A-REC FROM LB9A-HEADER1 AFTER 3.                        
           WRITE BP13LB9A-REC FROM LB9A-HEADER2.                                
           MOVE SPACES                       TO BP13LB9A-REC                    
           WRITE BP13LB9A-REC.                                                  
                                                                                
           MOVE 6                            TO CNT-PAGE-REC.                   
                                                                                
       2210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2220-OUTPUT-VR-HEAD.                                                     
      *-------------------------------------------------------------            
                                                                                
           ADD 5                             TO CNT-PAGE-REC                    
                                                                                
           IF CNT-PAGE-REC > 60                                                 
              PERFORM 2210-OUTPUT-HEADER   THRU 2210-EXIT                       
              ADD 5                          TO CNT-PAGE-REC                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                       TO BP13LB9A-REC                    
           WRITE BP13LB9A-REC.                                                  
                                                                                
           MOVE KD85-NUM-PRJCT               TO LB9A-PRJCT                      
           WRITE BP13LB9A-REC              FROM LB9A-VR-HEADER1.                
                                                                                
           MOVE KD85-NUM-VOUCHER             TO LB9A-VR-NO                      
           MOVE CNT-VR                       TO LB9A-VR-SNO                     
           WRITE BP13LB9A-REC              FROM LB9A-VR-HEADER2.                
                                                                                
           MOVE SPACES                       TO BP13LB9A-REC                    
           WRITE BP13LB9A-REC.                                                  
           WRITE BP13LB9A-REC              FROM LB9A-VR-DTL-HEADER.             
                                                                                
       2220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2230-OUTPUT-REGN-DTL.                                                    
      *-------------------------------------------------------------            
                                                                                
           ADD 1                             TO CNT-PAGE-REC                    
                                                                                
           IF CNT-PAGE-REC > 60                                                 
              PERFORM 2210-OUTPUT-HEADER   THRU 2210-EXIT                       
              ADD 1                          TO CNT-PAGE-REC                    
           END-IF.                                                              
                                                                                
           MOVE CNT-VR-REGN                  TO LB9A-VR-DTE-SNO                 
           MOVE KD85-NUM-REGN                TO LB9A-VR-REGN                    
           MOVE KD85-AMT-SURVEY-FEE          TO LB9A-VR-SURVEY-FEE              
           IF KD85-NUM-BLOCK NOT = SPACES AND LOW-VALUES                        
              STRING 'BLK ' KD85-NUM-BLOCK                                      
                     ' # '  KD85-NUM-LEVEL                                      
                     ' - '  KD85-NUM-MAIN-UNIT KD85-NUM-SUB-UNIT                
                     DELIMITED BY SIZE INTO LB9A-VR-ADDR                        
           ELSE                                                                 
              MOVE SPACES                    TO LB9A-VR-ADDR                    
           END-IF.                                                              
                                                                                
           WRITE BP13LB9A-REC              FROM LB9A-VR-DTL.                    
                                                                                
       2230-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT SY02F001                                                  
                      BP13FD85                                                  
                      AG07F640                                                  
                      BP13KA10                                                  
                  I-O BP13KD85                                                  
               OUTPUT P13FD85A                                                  
                      BP13F125                                                  
                      BP13LB9A.                                                 
                                                                                
           IF WS-KD85-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD85 - OPEN ERROR (' WS-KD85-STATUS ')'              
              MOVE WS-KD85-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 - OPEN ERROR (' WS-KA10-STATUS ')'              
              MOVE WS-KA10-STATUS            TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE-TIME.                      
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-SYS-DATE.                           
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-READ-BP13FD85.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD85                                                        
             AT END                                                             
                MOVE HIGH-VALUES     TO FD85-NUM-VOUCHER                        
                GO                   TO 8001-EXIT.                              
                                                                                
           ADD  1                    TO CNT-FD85-READ.                          
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8002-READ-AG07F640.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ AG07F640                                                        
             AT END                                                             
                MOVE HIGH-VALUES     TO F640-NUM-VR                             
                GO                   TO 8002-EXIT.                              
                                                                                
           ADD  1                    TO CNT-F640-READ.                          
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8003-READ-BP13KD85.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD85.                                                       
                                                                                
           IF WS-KD85-STATUS = 00 OR 02                                         
              ADD 1                         TO CNT-KD85-READ                    
           ELSE                                                                 
              IF WS-KD85-STATUS = 23                                            
                 DISPLAY 'BP13KD85 - RECORD NOT FOUND, KEY '                    
                         KD85-KEY-FLD                                           
                 GO TO 9000-CLOSE-FILES                                         
              ELSE                                                              
                 DISPLAY 'BP13KD85 - READ ERROR (' WS-KD85-STATUS ')'           
                 MOVE WS-KD85-STATUS        TO RETURN-CODE                      
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8004-DELETE-BP13KD85.                                                    
      *-------------------------------------------------------------            
                                                                                
           DELETE BP13KD85.                                                     
                                                                                
           IF WS-KD85-STATUS = 00                                               
              ADD 1                         TO CNT-KD85-DELETE                  
           ELSE                                                                 
              DISPLAY 'BP13KD85 - DELETE ERROR (' WS-KD85-STATUS ')'            
              MOVE WS-KD85-STATUS           TO RETURN-CODE                      
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       8004-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8005-READ-BP13KA10.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KA10.                                                       
                                                                                
           IF WS-KA10-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KA10-STATUS = 23                                            
                 DISPLAY 'BP13KA10 - RECORD NOT FOUND, KEY '                    
                         KA10-KEY-FLD                                           
                 GO TO 9000-CLOSE-FILES                                         
              ELSE                                                              
                 DISPLAY 'BP13KA10 - READ ERROR (' WS-KA10-STATUS ')'           
                 MOVE WS-KA10-STATUS        TO RETURN-CODE                      
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CB9A'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'CNT-FD85-READ            : ' CNT-FD85-READ.                 
           DISPLAY 'CNT-F640-READ            : ' CNT-F640-READ.                 
           DISPLAY 'CNT-KD85-READ            : ' CNT-KD85-READ.                 
           DISPLAY 'CNT-KD85-DELETE          : ' CNT-KD85-DELETE.               
           DISPLAY 'CNT-FD85A-WRITE          : ' CNT-FD85A-WRITE.               
           DISPLAY 'CNT-F125-WRITE           : ' CNT-F125-WRITE.                
                                                                                
           CLOSE SY02F001                                                       
                 BP13FD85                                                       
                 AG07F640                                                       
                 BP13KD85                                                       
                 BP13KA10                                                       
                 P13FD85A                                                       
                 BP13F125                                                       
                 BP13LB9A.                                                      
                                                                                
           IF WS-KD85-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD85 - CLOSE ERROR (' WS-KD85-STATUS ')'             
              MOVE WS-KD85-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 - CLOSE ERROR (' WS-KA10-STATUS ')'             
              MOVE WS-KA10-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
