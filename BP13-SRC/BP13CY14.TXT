       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY14.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 02 MAY 2008                          *              
      *                                                          *              
      * 1. OBJECTIVE      : BTO APPLICATION BY NRIC MATCH WITH   *              
      *                   : SMS, DTE-SALES > DTE-REQUEST, COUNT  *              
      *                   : AS BOUGHT A FLAT.                    *              
      *                   : IF NOT FOUND, COUNT AS NEW REQUEST.  *              
      *                                                          *              
      * 2. INPUT          : BP13F595                             *              
      *                   : BP13K816                             *              
      *                   : AB03K030 - AB03.K030.SALEMAST        *              
      *                   : AB03K030 - AB03.K030.SALEMAST        *              
      *                                                          *              
      * 3. OUTPUT         : P13F595F                             *              
      *                   : P13F595N                             *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP133340 02/05/2008 JB8  NEW PROGRAM.(BP13C993)         *              
      *  BP133434 21/11/2008 JB8  RECTIFY NOTFND OUTPUT FILE     *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT AB03K080     ASSIGN        TO AB03K080                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K080-KEY                        
                               FILE STATUS   IS WS-K080-STATUS.                 
                                                                                
           SELECT AB03K030     ASSIGN        TO AB03K030                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K030-NUM-HDB-REF                
                               FILE STATUS   IS WS-K030-STATUS.                 
                                                                                
           SELECT BP13K816     ASSIGN        TO BP13K816                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K816-KEY-FLD                    
                               FILE STATUS   IS WS-K816-STATUS.                 
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  AB03K080                                                             
                               RECORD CONTAINS 50 CHARACTERS.                   
       COPY AB03K080.                                                           
                                                                                
       FD  AB03K030                                                             
                               RECORD CONTAINS 800 CHARACTERS.                  
       COPY AB03K030.                                                           
                                                                                
       FD  BP13K816                                                             
                               RECORD CONTAINS 400 CHARACTERS.                  
       COPY BP13K816.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 100 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC.                                                        
           05  F595F-NUM-REGN            PIC X(08).                             
           05  F595F-NUM-ALLO-CAT        PIC X(03).                             
           05  F595F-DTE-BALLOT          PIC X(06).                             
           05  F595F-CDE-HOUSEHOLD       PIC X(03).                             
           05  F595F-CDE-FLAT-TYPE       PIC X(02).                             
           05  F595F-NUM-NRIC1           PIC X(09).                             
           05  F595F-NUM-NRIC2           PIC X(09).                             
           05  F595F-NUM-SCH-ACC         PIC X(11).                             
           05  F595F-NUM-TYPE            PIC X(03).                             
           05  FILLER                    PIC X(06).                             
           05  F595F-CDE-NTFT            PIC X(05).                             
           05  FILLER                    PIC X(35).                             
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 100 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC.                                                        
           05  F595N-NUM-REGN            PIC X(08).                             
           05  F595N-NUM-ALLO-CAT        PIC X(03).                             
           05  F595N-DTE-BALLOT          PIC X(06).                             
           05  F595N-CDE-HOUSEHOLD       PIC X(03).                             
           05  F595N-CDE-FLAT-TYPE       PIC X(02).                             
           05  F595N-NUM-NRIC1           PIC X(09).                             
           05  F595N-NUM-NRIC2           PIC X(09).                             
           05  F595N-NUM-SCH-ACC         PIC X(11).                             
           05  F595N-NUM-TYPE            PIC X(03).                             
           05  F595N-NUM-NEW-TOWN        PIC X(03).                             
           05  F595N-NUM-REQUEST-STATUS  PIC X(01).                             
           05  FILLER                    PIC X(02).                             
           05  F595N-CDE-NTFT            PIC X(05).                             
           05  FILLER                    PIC X(35).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K080-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K030-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K816-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K080-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K080-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K080-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BP13K816                                                 
                       AB03K080                                                 
                       AB03K030                                                 
                OUTPUT P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 OPEN ERROR ' WS-K080-STATUS                     
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 OPEN ERROR ' WS-K030-STATUS                     
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K816-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K816 OPEN ERROR ' WS-K816-STATUS                     
              MOVE WS-K816-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO P13F595F-REC                        
                                                                                
                   PERFORM 3000-START-AB03K080 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              MOVE SPACES                       TO P13F595N-REC                 
              MOVE F595-NUM-REGN                TO F595N-NUM-REGN               
              MOVE F595-NUM-ALLO-CAT            TO F595N-NUM-ALLO-CAT           
              MOVE F595-DTE-BALLOT              TO F595N-DTE-BALLOT             
              MOVE F595-CDE-HOUSEHOLD           TO F595N-CDE-HOUSEHOLD          
              MOVE F595-CDE-FLAT-TYPE           TO F595N-CDE-FLAT-TYPE          
              MOVE F595-NUM-NRIC1               TO F595N-NUM-NRIC1              
              MOVE F595-NUM-NRIC2               TO F595N-NUM-NRIC2              
              MOVE F595-CDE-REQUEST-STATUS   TO F595N-NUM-REQUEST-STATUS        
              MOVE 'REQ'                        TO F595N-NUM-TYPE               
                                                                                
              PERFORM 7000-DECODE-NT          THRU 7000-EXIT                    
                                                                                
              WRITE P13F595N-REC                                                
                                                                                
              ADD 1                             TO WS-595N-WRITE-CNT            
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-AB03K080.                                                     
      *************************************************************             
           MOVE SPACES                   TO AB03K080-REC.                       
           INITIALIZE                       AB03K080-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K080-NUM-UIN                        
                                            WS-K080-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K080-EOF                         
                                            WS-K080-REC-PROCESS.                
                                                                                
           START AB03K080 KEY >= K080-KEY.                                      
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-AB03K080 THRU 3100-EXIT               
                      UNTIL K080-NUM-UIN NOT = WS-K080-NRIC-PREV                
                         OR WS-K080-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'NRIC NOT FOUND IN K080: ' K080-NUM-UIN             
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 START ERROR ' WS-K080-STATUS              
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-AB03K080.                                                  
      *************************************************************             
           READ AB03K080 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K080-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K080-NUM-UIN = WS-K080-NRIC-PREV                         
                       PERFORM 3500-READ-AB03K030 THRU 3500-EXIT                
                                                                                
                       MOVE 'Y'          TO WS-K080-REC-PROCESS                 
                    ELSE                                                        
                       IF WS-K080-REC-PROCESS NOT = 'Y'                         
                          CONTINUE                                              
      *                   DISPLAY 'NRIC NOT FOUND IN K080: '                    
      *                           WS-K080-NRIC-PREV                             
                       END-IF                                                   
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'NRIC NOT FOUND IN K080: ' K080-NUM-UIN             
               WHEN OTHER                                                       
                    DISPLAY 'AB03K080 READ NEXT ERROR ' WS-K080-STATUS          
                    DISPLAY 'K080 KEY: ' K080-KEY                               
                    MOVE WS-K080-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3500-READ-AB03K030.                                                      
      *************************************************************             
           MOVE SPACES                   TO AB03K030-REC.                       
           INITIALIZE                       AB03K030-REC.                       
                                                                                
           MOVE K080-NUM-HDB-REF         TO K030-NUM-HDB-REF.                   
                                                                                
           READ AB03K030.                                                       
                                                                                
           EVALUATE WS-K030-STATUS                                              
              WHEN 00                                                           
                                                                                
                 IF K030-CDE-ACCT-STAT = 'C'    AND                             
                    K030-DTE-EXE-SALE-AGRMT(1:6) >= F595-DTE-BALLOT             
                    MOVE SPACES                 TO P13F595F-REC                 
                    MOVE F595-NUM-REGN          TO F595F-NUM-REGN               
                    MOVE F595-CDE-HOUSEHOLD     TO F595F-CDE-HOUSEHOLD          
                    MOVE F595-CDE-FLAT-TYPE     TO F595F-CDE-FLAT-TYPE          
                    MOVE F595-NUM-NRIC1         TO F595F-NUM-NRIC1              
                    MOVE F595-NUM-NRIC2         TO F595F-NUM-NRIC2              
                    MOVE 'SMS'                  TO F595F-NUM-TYPE               
                    WRITE P13F595F-REC                                          
                    ADD 1                       TO WS-595F-WRITE-CNT            
                 ELSE                                                           
                    MOVE SPACES                 TO P13F595N-REC                 
                    MOVE F595-NUM-REGN          TO F595N-NUM-REGN               
                    MOVE F595-CDE-HOUSEHOLD     TO F595N-CDE-HOUSEHOLD          
                    MOVE F595-CDE-FLAT-TYPE     TO F595N-CDE-FLAT-TYPE          
                    MOVE F595-NUM-NRIC1         TO F595N-NUM-NRIC1              
                    MOVE F595-NUM-NRIC2         TO F595N-NUM-NRIC2              
                    MOVE F595-NUM-ALLO-CAT      TO F595N-NUM-ALLO-CAT           
                    MOVE F595-DTE-BALLOT        TO F595N-DTE-BALLOT             
                    MOVE F595-CDE-REQUEST-STATUS TO                             
                                               F595N-NUM-REQUEST-STATUS         
                                                                                
                    PERFORM 7000-DECODE-NT    THRU 7000-EXIT                    
                                                                                
                    MOVE 'REQ'                  TO F595N-NUM-TYPE               
                    WRITE P13F595N-REC                                          
                    ADD 1                       TO WS-595N-WRITE-CNT            
                 END-IF                                                         
                                                                                
                 MOVE 'Y'                       TO WS-NRIC-FOUND                
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
      *          DISPLAY 'REF NOT FOUND IN AB03K030: '                          
      *          K030-NUM-HDB-REF                                               
              WHEN OTHER                                                        
                 DISPLAY 'AB03K030 READ ERROR ' WS-K030-STATUS                  
                 DISPLAY 'K030 KEY: ' K030-NUM-HDB-REF                          
                 MOVE WS-K030-STATUS  TO RETURN-CODE                            
                                                                                
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ************************************************************              
       7000-DECODE-NT.                                                          
      ************************************************************              
                                                                                
           MOVE SPACES                          TO BP13K816-REC.                
           INITIALIZE                              BP13K816-REC.                
                                                                                
           MOVE F595-CDE-NT1                    TO K816-NUM-ZONE.               
                                                                                
           READ BP13K816.                                                       
                                                                                
           IF WS-K816-STATUS = 00                                               
              IF K816-NUM-FLAT-TYPE(1:1) = '3' OR '4' OR '5'                    
                 MOVE SPACE           TO K816-NUM-FLAT-TYPE(2:1)                
              END-IF                                                            
              STRING K816-CDE-NT K816-NUM-FLAT-TYPE                             
                 DELIMITED BY SIZE INTO F595N-CDE-NTFT                          
           ELSE                                                                 
              IF WS-K816-STATUS = 23                                            
                 MOVE F595-CDE-NT1              TO F595N-CDE-NTFT               
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K816 : '                          
                         WS-K816-STATUS                                         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CY14 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS FOUND               : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS NOT FOUND           : ' WS-595N-WRITE-CNT.             
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 AB03K080                                                       
                 AB03K030                                                       
                 BP13K816                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K080-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K080 CLOSE ERROR ' WS-K080-STATUS                    
              MOVE WS-K080-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 CLOSE ERROR ' WS-K030-STATUS                    
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K816-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K816 CLOSE ERROR ' WS-K816-STATUS                    
              MOVE WS-K816-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
