      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C607.                                                 
       AUTHOR.        ANNALYN C BANTA.                                          
       DATE-WRITTEN.  10/09/94.                                                 
                                                                                
      * ======================================================= *               
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                   *               
      *    SYSTEM ID   : BP13                                   *               
      *    OBJECTIVE   : TO PRINT THE LIST OF UNAUTHORIZED      *               
      *                  ACCESSES.                              *               
      *---------------------------------------------------------*               
      * CHG-NO   BY   DATE      DETAILS                         *               
      * -------- ---  --------  --------------------------------*               
      * BP130522 SGK  22/07/98  TO CHGE SIZE OF AB02E990 FROM   *               
      *                         1000 TO 1500 BYTES.             *               
      * BP130418 CLT  28/08/98  Y2K CHANGES.                    *               
      * BP130929 ESC  30/12/00  USE AB02900C INSTEAD OF AB02E990*               
      * BP132587 CLT  27/09/04  USE BP13F990 INSTEAD OF AB02F990*               
      * BP132795 CG7  07/12/05  TO CHGE SIZE OF AB02E990 FROM   *               
      *                         1500 TO 2000 BYTES.             *               
      * ======================================================= *               
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BP13K330 ASSIGN TO BP13K330                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K330-KEY-FLD                               
                  FILE STATUS     IS K330-STATUS.                               
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS K410-STATUS.                               
                                                                                
           SELECT AB02E990 ASSIGN TO AB02E990.                                  
                                                                                
           SELECT BP13F990 ASSIGN TO BP13F990.                                  
                                                                                
           SELECT BP13L607 ASSIGN TO BP13L607.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K330                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K330.                                                           
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K410.                                                           
                                                                                
       FD  AB02E990                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       COPY AB02E990.                                                           
                                                                                
       FD  BP13F990                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  BP13F990-REC                   PIC X(2000).                          
                                                                                
       FD  BP13L607                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS OMITTED.                                             
       01  BP13L607-REC                   PIC X(132).                           
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       COPY AB02900C.                                                           
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K800-STATUS                 PIC 99    VALUE ZEROS.                
           05 K820-STATUS                 PIC 99    VALUE ZEROS.                
           05 K330-STATUS                 PIC 99    VALUE ZEROS.                
           05 K410-STATUS                 PIC 99    VALUE ZEROS.                
           05 K010-STATUS                 PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-E990-READ                PIC 9(5)  VALUE ZEROS.                
           05 WS-F990-WRITE               PIC 9(5)  VALUE ZEROS.                
           05 WS-SERNO                    PIC 9(5)  VALUE ZEROS.                
           05 WS-PAGE-CTR                 PIC 9(5)  VALUE ZEROS.                
           05 WS-LINE-CTR                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-E990                    PIC 9     VALUE 0.                    
           88 EOF-E990                    VALUE 1.                              
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-CCYY                 PIC 9(4).                             
           05 WS-SYS-MM                   PIC 9(2).                             
           05 WS-SYS-DD                   PIC 9(2).                             
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 L607-HDG1.                                                        
              10 FILLER                   PIC X(08) VALUE                       
                'BP13L607'.                                                     
              10 FILLER                   PIC X(37) VALUE SPACES.               
              10 FILLER                   PIC X(42) VALUE                       
                'SOC INTERFACED WITH CPF TRANSACTION SYSTEM'.                   
              10 FILLER                   PIC X(28) VALUE SPACES.               
              10 FILLER                   PIC X(07) VALUE 'DATE : '.            
              10 L607-SYS-DATE            PIC X(10) VALUE 'DD/MM/CCYY'.         
                                                                                
           05 L607-HDG2.                                                        
              10 FILLER                   PIC X(23) VALUE                       
                'TO SEO(MR KOH KIM LENG)'.                                      
              10 FILLER                   PIC X(19) VALUE SPACES.               
              10 FILLER                   PIC X(48) VALUE                       
                'EXCEPTIONAL LISTING ON UNMATCHED PERSONS IN SOC'.              
              10 FILLER                   PIC X(25) VALUE SPACES.               
              10 FILLER                   PIC X(07) VALUE 'PAGE : '.            
              10 L607-PAGE                PIC ZZZZ9.                            
              10 FILLER                   PIC X(03) VALUE SPACES.               
                                                                                
           05 L607-HDG3.                                                        
              10 FILLER                   PIC X(14) VALUE                       
                '    SALES UNIT'.                                               
              10 FILLER                   PIC X(38) VALUE SPACES.               
              10 FILLER                   PIC X(28) VALUE                       
                'SALES UNIT (AGREEMENT TEAM)'.                                  
              10 FILLER                   PIC X(52) VALUE SPACES.               
                                                                                
           05 L607-HDG4.                                                        
              10 FILLER                   PIC X(5)  VALUE ' S/N'.               
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(8)  VALUE 'REGN NO.'.           
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(34) VALUE 'NAME'.               
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(8)  VALUE 'CPF NO.'.            
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(8)  VALUE 'STAFF ID'.           
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(14) VALUE                       
                                                   'DATE OF ACCESS'.            
           05 L607-DASH.                                                        
              10 FILLER                   PIC X(5)  VALUE '-----'.              
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(8)  VALUE '--------'.           
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(34) VALUE ALL '-'.              
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(8)  VALUE '--------'.           
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(8)  VALUE '--------'.           
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 FILLER                   PIC X(14) VALUE ALL '-'.              
                                                                                
           05 L607-DTL.                                                         
              10 L607-SERNO               PIC 9(5).                             
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 L607-REGN                PIC X(8).                             
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 L607-NAME                PIC X(34).                            
              10 FILLER                   PIC X(11) VALUE SPACES.               
              10 L607-CPF                 PIC X(9).                             
              10 FILLER                   PIC X(10) VALUE SPACES.               
              10 L607-STAFID              PIC X(8).                             
              10 FILLER                   PIC X(14) VALUE SPACES.               
              10 L607-DTE-ACCESS          PIC X(08).                            
              10 FILLER                   PIC X(03) VALUE SPACES.               
           05 L607-DTL-R REDEFINES L607-DTL   PIC X(132).                       
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
           PERFORM 100-INITIALIZATION     THRU 100-EXIT.                        
           PERFORM 200-READ-E990          THRU 200-EXIT                         
           PERFORM 300-PROCESS-E990       THRU 300-EXIT                         
             UNTIL EOF-E990.                                                    
           PERFORM 999-CLOSE-FILES        THRU 999-EXIT.                        
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
           OPEN INPUT  BP13K800                                                 
                       BP13K820                                                 
                       BP13K330                                                 
                       BP13K410                                                 
                       AB02E990                                                 
                OUTPUT BP13F990                                                 
                       BP13L607.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              MOVE K800-STATUS           TO RETURN-CODE                         
              DISPLAY 'ERROR OPENING BP13K800 FILE'                             
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              MOVE K820-STATUS           TO RETURN-CODE                         
              DISPLAY 'ERROR OPENING BP13K820 FILE'                             
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF K330-STATUS NOT = 00 AND 97                                       
              MOVE K330-STATUS           TO RETURN-CODE                         
              DISPLAY 'ERROR OPENING BP13K330 FILE'                             
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              MOVE K410-STATUS           TO RETURN-CODE                         
              DISPLAY 'ERROR OPENING BP13K410 FILE'                             
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF K010-STATUS NOT = 00 AND 97                                       
              MOVE K010-STATUS           TO RETURN-CODE                         
              DISPLAY 'ERROR OPENING BP13K010 FILE'                             
              GO TO 999-CLOSE-FILES.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-E990.                                                           
      ******************************************************************        
           READ AB02E990 AT END                                                 
                MOVE 1   TO WS-EOF-E990                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1               TO WS-E990-READ.                                
           MOVE E990-REC        TO WS-B900-AREA.                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-E990.                                                        
      ******************************************************************        
           PERFORM 310-READ-K800      THRU 310-EXIT.                            
                                                                                
           IF K800-NUM-NRIC1 = B900-NUM-UIN-LSE1 AND                            
              K800-NUM-NRIC2 = B900-NUM-UIN-LSE2 AND                            
              K800-NUM-NRIC3 = B900-NUM-UIN-LSE3 AND                            
              K800-NUM-NRIC4 = B900-NUM-UIN-LSE4                                
              NEXT SENTENCE                                                     
           ELSE                                                                 
           PERFORM 320-READ-K330      THRU 320-EXIT                             
           IF K330-NRIC1     = B900-NUM-UIN-LSE1 AND                            
              K330-NRIC2     = B900-NUM-UIN-LSE2 AND                            
              K330-NRIC3     = B900-NUM-UIN-LSE3 AND                            
              K330-NRIC4     = B900-NUM-UIN-LSE4                                
              NEXT SENTENCE                                                     
           ELSE                                                                 
           PERFORM 330-READ-K410      THRU 330-EXIT                             
           IF K410-NRIC1     = B900-NUM-UIN-LSE1 AND                            
              K410-NRIC2     = B900-NUM-UIN-LSE2 AND                            
              K410-NRIC3     = B900-NUM-UIN-LSE3 AND                            
              K410-NRIC4     = B900-NUM-UIN-LSE4                                
              NEXT SENTENCE                                                     
           ELSE                                                                 
           MOVE B900-NUM-UIN-LSE1       TO K820-NUM-NRIC                        
           PERFORM 340-READ-K820           THRU 340-EXIT                        
           IF K820-STATUS NOT = 00                                              
              PERFORM 400-PROCESS-UNAUTH   THRU 400-EXIT                        
           ELSE                                                                 
           MOVE B900-NUM-UIN-LSE2       TO K820-NUM-NRIC                        
           PERFORM 340-READ-K820           THRU 340-EXIT                        
           IF K820-STATUS NOT = 00                                              
              PERFORM 400-PROCESS-UNAUTH   THRU 400-EXIT                        
           ELSE                                                                 
           MOVE B900-NUM-UIN-LSE3       TO K820-NUM-NRIC                        
           PERFORM 340-READ-K820           THRU 340-EXIT                        
           IF K820-STATUS NOT = 00                                              
              PERFORM 400-PROCESS-UNAUTH   THRU 400-EXIT                        
           ELSE                                                                 
           MOVE B900-NUM-UIN-LSE4       TO K820-NUM-NRIC                        
           PERFORM 340-READ-K820           THRU 340-EXIT                        
           IF K820-STATUS NOT = 00                                              
              PERFORM 400-PROCESS-UNAUTH   THRU 400-EXIT.                       
                                                                                
           PERFORM 200-READ-E990           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       310-READ-K800.                                                           
      ******************************************************************        
           MOVE B900-NUM-ORGN           TO K800-NUM-REGN.                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-STATUS                                                 
              WHEN 00  CONTINUE                                                 
              WHEN 23                                                           
                       DISPLAY 'RECORD NOT FOUND IN BP13K800, '                 
                       K800-NUM-REGN                                            
              WHEN OTHER                                                        
                       MOVE K800-STATUS TO RETURN-CODE                          
                       DISPLAY 'ERROR READING BP13K800, '                       
                       K800-NUM-REGN                                            
                       GO TO 999-CLOSE-FILES                                    
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       320-READ-K330.                                                           
      ******************************************************************        
           MOVE SPACES                  TO K330-KEY-FLD.                        
           MOVE B900-NUM-ORGN           TO K330-CURR-REG.                       
           MOVE 1                       TO K330-SEQ-NO.                         
                                                                                
           READ BP13K330.                                                       
                                                                                
           EVALUATE K330-STATUS                                                 
              WHEN 00  CONTINUE                                                 
              WHEN 23                                                           
                       DISPLAY 'RECORD NOT FOUND IN BP13K330, '                 
                       K330-KEY-FLD                                             
              WHEN OTHER                                                        
                       MOVE K330-STATUS TO RETURN-CODE                          
                       DISPLAY 'ERROR READING BP13K330, '                       
                       K330-KEY-FLD                                             
                       GO TO 999-CLOSE-FILES                                    
           END-EVALUATE.                                                        
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       330-READ-K410.                                                           
      ******************************************************************        
           MOVE B900-NUM-ORGN           TO K410-NUM-REGN.                       
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE K410-STATUS                                                 
              WHEN 00  CONTINUE                                                 
              WHEN 23                                                           
                       DISPLAY 'RECORD NOT FOUND IN BP13K410, '                 
                       K410-KEY-FLD                                             
              WHEN OTHER                                                        
                       MOVE K410-STATUS TO RETURN-CODE                          
                       DISPLAY 'ERROR READING BP13K410, '                       
                       K410-KEY-FLD                                             
                       GO TO 999-CLOSE-FILES                                    
           END-EVALUATE.                                                        
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       340-READ-K820.                                                           
      ******************************************************************        
           MOVE B900-NUM-ORGN           TO K820-NUM-REGN.                       
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE K820-STATUS                                                 
              WHEN 00  CONTINUE                                                 
              WHEN 23                                                           
                       DISPLAY 'RECORD NOT FOUND IN BP13K820, '                 
                       K820-KEY-FLD                                             
                       GO TO 340-EXIT                                           
              WHEN OTHER                                                        
                       MOVE K820-STATUS TO RETURN-CODE                          
                       DISPLAY 'ERROR READING BP13K820, '                       
                       K820-KEY-FLD                                             
                       GO TO 999-CLOSE-FILES                                    
           END-EVALUATE.                                                        
                                                                                
       340-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-PROCESS-UNAUTH.                                                      
      ******************************************************************        
           WRITE BP13F990-REC      FROM AB02E990-REC.                           
           ADD   1                   TO WS-F990-WRITE.                          
           PERFORM 410-PRINT-RTN   THRU 410-EXIT.                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       410-PRINT-RTN.                                                           
      ******************************************************************        
           IF WS-SERNO = ZEROS OR WS-LINE-CTR > 55                              
              PERFORM 420-PRINT-HDG    THRU 420-EXIT.                           
                                                                                
           ADD 1                         TO WS-SERNO.                           
           MOVE WS-SERNO                 TO L607-SERNO.                         
           MOVE B900-NUM-ORGN            TO L607-REGN.                          
           MOVE B900-NME-LSE1            TO L607-NAME.                          
           MOVE B900-NUM-CPF1            TO L607-CPF.                           
           MOVE E990-NUM-USERID          TO L607-STAFID.                        
           MOVE E990-DTE-UPDATE          TO L607-DTE-ACCESS.                    
           WRITE BP13L607-REC FROM L607-DTL AFTER 2.                            
           ADD  2                        TO WS-LINE-CTR.                        
                                                                                
           IF B900-NUM-CPF2 NOT = SPACES AND LOW-VALUES                         
           MOVE SPACES                   TO L607-DTL-R                          
           MOVE B900-NME-LSE2            TO L607-NAME                           
           MOVE B900-NUM-CPF2            TO L607-CPF                            
           WRITE BP13L607-REC FROM L607-DTL AFTER 1                             
           ADD  1                        TO WS-LINE-CTR.                        
                                                                                
           IF B900-NUM-CPF3 NOT = SPACES AND LOW-VALUES                         
           MOVE SPACES                   TO L607-DTL-R                          
           MOVE B900-NME-LSE3            TO L607-NAME                           
           MOVE B900-NUM-CPF3            TO L607-CPF                            
           WRITE BP13L607-REC FROM L607-DTL AFTER 1                             
           ADD  1                        TO WS-LINE-CTR.                        
                                                                                
           IF B900-NUM-CPF4 NOT = SPACES AND LOW-VALUES                         
           MOVE SPACES                   TO L607-DTL-R                          
           MOVE B900-NME-LSE4            TO L607-NAME                           
           MOVE B900-NUM-CPF4            TO L607-CPF                            
           WRITE BP13L607-REC FROM L607-DTL AFTER 1                             
           ADD  1                        TO WS-LINE-CTR.                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       420-PRINT-HDG.                                                           
      ******************************************************************        
           MOVE FUNCTION CURRENT-DATE TO  WS-SYS-DATE.                          
           STRING WS-SYS-DD   DELIMITED BY SIZE                                 
              '/' WS-SYS-MM   DELIMITED BY SIZE                                 
              '/' WS-SYS-CCYY DELIMITED BY SIZE                                 
             INTO L607-SYS-DATE.                                                
                                                                                
           ADD    1                 TO WS-PAGE-CTR.                             
           MOVE   WS-PAGE-CTR       TO L607-PAGE.                               
                                                                                
           WRITE BP13L607-REC     FROM L607-HDG1 AFTER PAGE.                    
           WRITE BP13L607-REC     FROM L607-HDG2 AFTER 1.                       
           WRITE BP13L607-REC     FROM L607-HDG3 AFTER 1.                       
           WRITE BP13L607-REC     FROM L607-HDG4 AFTER 3.                       
           WRITE BP13L607-REC     FROM L607-DASH AFTER 1.                       
                                                                                
           MOVE  7                  TO WS-LINE-CTR.                             
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BP13K330                                                       
                 BP13K410                                                       
                 AB02E990                                                       
                 BP13F990                                                       
                 BP13L607.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C607 ================*'.              
           DISPLAY '    NO. OF E990 RECORDS READ    = ' WS-E990-READ.           
           DISPLAY '    NO. OF F990 RECORDS WRITTEN = ' WS-F990-WRITE.          
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
