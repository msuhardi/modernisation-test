      *===*===*===*===*===*====*                                                
       IDENTIFICATION DIVISION.                                                 
      *===*===*===*===*===*====*                                                
       PROGRAM-ID.    BP13C288.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  20/08/02.                                                 
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *    TO PRINT SUBMISSION REPORT FOR ALLOCATION EXERCISE.     *            
      *                                                            *            
      *  INPUT FILES : BP13F800                                    *            
      *                BP13K800                                    *            
      *                                                            *            
      *  OUTPUT/S    : BP13L288                                    *            
      * ---------------------------------------------------------- *            
      * BP132201 AS1  210802  NEW PROGRAM                          *            
      * BP135838 ESA1 010715  TO CATER FOR MODL-DESIGN 92          *            
      * ========================================================== *            
                                                                                
      *===*===*===*===*====*                                                    
       ENVIRONMENT DIVISION.                                                    
      *===*===*===*===*====*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *=====================*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *=====================*                                                   
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT    BP13F800     ASSIGN     TO    BP13F800.                    
                                                                                
           SELECT    BP13K800     ASSIGN     TO    BP13K800                     
                     ORGANIZATION IS INDEXED                                    
                     ACCESS MODE  IS RANDOM                                     
                     RECORD KEY   IS K800-NUM-REGN                              
                     FILE STATUS  IS WS-K800-STATUS.                            
                                                                                
           SELECT    BP13L288     ASSIGN     TO    BP13L288.                    
                                                                                
                                                                                
      *===*===*==*==*                                                           
       DATA DIVISION.                                                           
      *===*===*==*==*                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE F.                                                    
       01  WS-F800A-REC.                                                        
           05 FILLER                        PIC X(3).                           
           05 WS-F800A-NUM-REGN             PIC X(8).                           
           05 FILLER                        PIC X(519).                         
           05 WS-F800A-NUM-HHTY-QUEUE-ALPHA PIC X(01).                          
           05 FILLER                        PIC X(412).                         
           05 WS-F800A-NUM-MAIN             PIC X(04).                          
           05 WS-F800A-NUM-SUB              PIC X(03).                          
           05 WS-F800A-NUM-LEVEL            PIC X(02).                          
           05 WS-F800A-BLK                  PIC X(05).                          
           05 WS-F800A-STREET-NAME          PIC X(20).                          
           05 WS-F800A-CDE-TYP-ACTLUSE      PIC X(02).                          
           05 WS-F800A-CDE-MODL-DESG        PIC X(02).                          
           05 WS-F800A-F205-NUM-NT-ZONE     PIC X(03).                          
           05 WS-F800A-F205-NUM-NT-ZONE-SW  PIC X(01).                          
           05 WS-F800A-K110-ALLOC-SCH       PIC X(03).                          
           05 FILLER                        PIC X(988).                         
           05 WS-F800A-REC2                 PIC X(24).                          
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13L288                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 99  CHARACTERS                                       
           LABEL  RECORDS  ARE OMITTED                                          
           RECORDING MODE F.                                                    
       01  PRINT-REC-A                    PIC X(99).                            
                                                                                
      *========================*                                                
       WORKING-STORAGE SECTION.                                                 
      *========================*                                                
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K800-STATUS             PIC 9(02) VALUE ZEROS.                
                                                                                
       01  WS-CNT.                                                              
           05  WS-BP13F800-READ           PIC 9(04) VALUE ZEROS.                
           05  WS-K800-READ               PIC 9(04) VALUE ZEROS.                
           05  WS-L288-WRITE              PIC 9(04) VALUE ZEROS.                
                                                                                
       01  WS-TEMP.                                                             
           05  WS-F800-EOF                PIC X(01).                            
               88 EOF-F800                VALUE 'Y'.                            
           05  WS-LINE-CNT                PIC 9(02) VALUE ZEROS.                
           05  WS-NUMBER                  PIC 9(05) VALUE ZEROS.                
           05  WS-PAGE-CNT                PIC 9(04) VALUE ZEROES.               
           05  WS-SQL-CODE                PIC 9(04) VALUE ZEROS.                
           05  WS-F800-PRINTED            PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-F800-KEY.                                                         
           05 WS-F800-NUM-NEW-TOWN        PIC X(03) VALUE SPACES.               
           05 WS-F800-NUM-FLAT-TYPE       PIC X(01) VALUE SPACES.               
                                                                                
                                                                                
      *============================================================*            
      *                H E A D E R   L A Y O U T                   *            
      *============================================================*            
       01  L288-HEADER-1.                                                       
           05 FILLER                      PIC X(6)  VALUE 'PAGE :'.             
           05 L288-PAGE                   PIC ZZZ9.                             
                                                                                
       01  L288-HEADER-2.                                                       
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 FILLER                      PIC X(54) VALUE                       
             'SELECTION/SUBMISSION DETAIL LIST           ANNEXURE  A'.          
           05 FILLER                      PIC X(06) VALUE SPACES.               
                                                                                
       01  L288-HEADER-3.                                                       
           05 FILLER                      PIC X(41) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE 'FLAT'.               
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE 'APT'.                
                                                                                
       01  L288-HEADER-4.                                                       
           05 FILLER                      PIC X(33) VALUE                       
             'NO    REGN NO'.                                                   
           05 FILLER                      PIC X(08) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE 'TYPE'.               
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 FILLER                      PIC X(06) VALUE 'BLOCK'.              
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 FILLER                      PIC X(07) VALUE                       
             'FLAT NO'.                                                         
           05 FILLER                      PIC X(05) VALUE SPACES.               
           05 FILLER                      PIC X(11) VALUE                       
             'STREET NAME'.                                                     
                                                                                
       01  L288-HEADER-5.                                                       
           05 FILLER                      PIC X(99) VALUE ALL '-'.              
                                                                                
       01  L288-FOOTER-1.                                                       
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 FILLER                      PIC X(12) VALUE                       
             'CHECKED  BY:'.                                                    
                                                                                
       01  L288-FOOTER-2.                                                       
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 FILLER                      PIC X(12) VALUE                       
             'VERIFIED BY:'.                                                    
                                                                                
      *============================================================*            
      *                 D E T A I L    L A Y O U T                 *            
      *============================================================*            
       01  L288-DETAIL-1.                                                       
           05 L288-NT-REGN-FORMAT.                                              
              10 L288-SER-NO              PIC ZZZZ9.                            
              10 FILLER                   PIC X(01).                            
              10 L288-NT-REGN-NO          PIC X(08).                            
              10 FILLER                   PIC X(20).                            
           05 FILLER                      PIC X(01) VALUE SPACE.                
           05 L288-LEFT-PAR               PIC X(01).                            
           05 L288-ALLOC                  PIC X(03).                            
           05 L288-RIGHT-PAR              PIC X(01).                            
           05 FILLER                      PIC X(02) VALUE SPACE.                
           05 L288-RM-TY                  PIC X(02).                            
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 L288-BLK                    PIC X(05).                            
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 L288-NUM-LEVEL              PIC X(02).                            
           05 L288-DASH                   PIC X(01).                            
           05 L288-NUM-MAIN               PIC X(04).                            
           05 L288-NUM-SUB                PIC X(03).                            
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 L288-STREET-NAME            PIC X(20).                            
                                                                                
      *===*===*====*=====*                                                      
       PROCEDURE DIVISION.                                                      
      *===*===*====*=====*                                                      
                                                                                
      *================*                                                        
       000-MAIN-ROUTINE.                                                        
      *================*                                                        
                                                                                
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F800      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-F800   THRU 300-EXIT                             
             UNTIL EOF-F800.                                                    
           PERFORM 800-WRITE-FOOTER   THRU 800-EXIT.                            
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==================*                                                      
       100-INITIALIZATION.                                                      
      *==================*                                                      
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K800                                                 
                OUTPUT BP13L288.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K800  OPEN ERROR ' WS-K800-STATUS               
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE 'N'                TO WS-F800-EOF.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *=============*                                                           
       200-READ-F800.                                                           
      *=============*                                                           
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'        TO WS-F800-EOF                                  
                MOVE HIGH-VALUE TO WS-F800-KEY                                  
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================*                                                        
       300-PROCESS-F800.                                                        
      *================*                                                        
                                                                                
           PERFORM    400-MOVE-TO-REPORT       THRU   400-EXIT.                 
           PERFORM    600-PRINT-REPORT         THRU   600-EXIT.                 
                                                                                
           PERFORM    200-READ-F800            THRU   200-EXIT.                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==================*                                                      
       400-MOVE-TO-REPORT.                                                      
      *==================*                                                      
                                                                                
           MOVE SPACES                 TO L288-DETAIL-1.                        
           INITIALIZE L288-DETAIL-1.                                            
                                                                                
           MOVE WS-F800A-NUM-MAIN      TO L288-NUM-MAIN.                        
           MOVE WS-F800A-NUM-SUB       TO L288-NUM-SUB.                         
           MOVE '-'                    TO L288-DASH.                            
           MOVE WS-F800A-NUM-LEVEL     TO L288-NUM-LEVEL.                       
           MOVE WS-F800A-BLK           TO L288-BLK.                             
           MOVE WS-F800A-STREET-NAME   TO L288-STREET-NAME.                     
                                                                                
           MOVE SPACES                 TO L288-RM-TY.                           
                                                                                
           EVALUATE WS-F800A-CDE-TYP-ACTLUSE                                    
           WHEN '01'                                                            
                EVALUATE WS-F800A-CDE-MODL-DESG                                 
                WHEN '80'                                                       
                WHEN '89'                                                       
                WHEN '92'                                                       
                     MOVE '1A'    TO L288-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '1 '    TO L288-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '02'                                                            
                EVALUATE WS-F800A-CDE-MODL-DESG                                 
                WHEN '80'                                                       
                WHEN '89'                                                       
                WHEN '92'                                                       
                     MOVE '2A'    TO L288-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '2 '    TO L288-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '03'                                                            
                MOVE '3 '         TO L288-RM-TY                                 
           WHEN '04'                                                            
                EVALUATE WS-F800A-CDE-MODL-DESG                                 
                WHEN '06'                                                       
                     MOVE '4S'    TO L288-RM-TY                                 
                WHEN '34'                                                       
                WHEN '70'                                                       
                WHEN '71'                                                       
                WHEN '72'                                                       
                WHEN '73'                                                       
                WHEN '81'                                                       
                WHEN '82'                                                       
                     MOVE '4D'    TO L288-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '4 '    TO L288-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '05'                                                            
                EVALUATE WS-F800A-CDE-MODL-DESG                                 
                WHEN '34'                                                       
                WHEN '70'                                                       
                WHEN '71'                                                       
                WHEN '72'                                                       
                WHEN '73'                                                       
                WHEN '81'                                                       
                WHEN '82'                                                       
                     MOVE '5D'    TO L288-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE '5 '    TO L288-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '06'                                                            
                EVALUATE WS-F800A-CDE-MODL-DESG                                 
                WHEN '34'                                                       
                WHEN '70'                                                       
                WHEN '71'                                                       
                WHEN '72'                                                       
                WHEN '73'                                                       
                WHEN '81'                                                       
                WHEN '82'                                                       
                     MOVE 'ED'    TO L288-RM-TY                                 
                WHEN OTHER                                                      
                     MOVE 'E '    TO L288-RM-TY                                 
                END-EVALUATE                                                    
           WHEN '07'                                                            
                MOVE 'H '         TO L288-RM-TY                                 
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================*                                                        
       500-READ-BP13K800.                                                       
      *================*                                                        
                                                                                
           INITIALIZE K800-NUM-REGN.                                            
           MOVE WS-F800A-NUM-REGN      TO K800-NUM-REGN.                        
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K800-READ                                           
                CONTINUE                                                        
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN K800 ' WS-K800-STATUS              
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING K800 FILE  ' WS-K800-STATUS              
                MOVE WS-K800-STATUS  TO RETURN-CODE                             
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================*                                                        
       600-PRINT-REPORT.                                                        
      *================*                                                        
                                                                                
            IF (WS-LINE-CNT = 0)                                                
               PERFORM 650-PRINT-HEADING  THRU 650-EXIT                         
               MOVE 5                       TO WS-LINE-CNT                      
            END-IF.                                                             
                                                                                
            PERFORM 700-PRINT-DETAIL      THRU 700-EXIT.                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *=================*                                                       
       650-PRINT-HEADING.                                                       
      *=================*                                                       
                                                                                
           ADD   1                       TO WS-PAGE-CNT.                        
           MOVE WS-PAGE-CNT              TO L288-PAGE.                          
                                                                                
           WRITE PRINT-REC-A FROM L288-HEADER-1   AFTER PAGE.                   
           WRITE PRINT-REC-A FROM L288-HEADER-2   AFTER 1.                      
           WRITE PRINT-REC-A FROM L288-HEADER-3   AFTER 2.                      
           WRITE PRINT-REC-A FROM L288-HEADER-4   AFTER 1.                      
           WRITE PRINT-REC-A FROM L288-HEADER-5   AFTER 1.                      
           MOVE SPACES         TO PRINT-REC-A.                                  
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *================*                                                        
       700-PRINT-DETAIL.                                                        
      *================*                                                        
                                                                                
           IF WS-F800A-K110-ALLOC-SCH NOT = 'PA '                               
              MOVE '('                        TO L288-LEFT-PAR                  
              MOVE ')'                        TO L288-RIGHT-PAR                 
              MOVE WS-F800A-K110-ALLOC-SCH    TO L288-ALLOC                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                        TO L288-NT-REGN-FORMAT.           
           MOVE WS-F800A-NUM-REGN             TO L288-NT-REGN-NO.               
           ADD  1                             TO WS-NUMBER.                     
           MOVE WS-NUMBER                     TO L288-SER-NO.                   
                                                                                
           WRITE PRINT-REC-A                FROM L288-DETAIL-1 AFTER 1.         
                                                                                
           MOVE SPACES                        TO PRINT-REC-A .                  
           ADD 1                              TO WS-LINE-CNT.                   
           ADD 1                              TO WS-L288-WRITE.                 
           MOVE 'Y'                           TO WS-F800-PRINTED.               
                                                                                
           IF WS-LINE-CNT > 44                                                  
              MOVE 0 TO WS-LINE-CNT                                             
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===============*                                                         
       800-WRITE-FOOTER.                                                        
      *===============*                                                         
                                                                                
           IF WS-F800-PRINTED = 'Y'                                             
              WRITE PRINT-REC-A FROM L288-FOOTER-1 AFTER 2                      
              WRITE PRINT-REC-A FROM L288-FOOTER-2 AFTER 2                      
              MOVE  ZEROS TO WS-PAGE-CNT                                        
              MOVE  ZEROS TO WS-NUMBER                                          
              MOVE  'N'   TO WS-F800-PRINTED                                    
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===============*                                                         
       999-CLOSE-FILES.                                                         
      *===============*                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BP13K800                                                       
                 BP13L288.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K800 CLOSE ERROR ' WS-K800-STATUS               
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY ' BP13C288 CONTROL TOTAL'.                                   
           DISPLAY '----------------------------------------'.                  
           DISPLAY ' NO. OF F800 RECORDS READ = ' WS-BP13F800-READ.             
           DISPLAY ' NO. OF K800 RECORDS READ = ' WS-K800-READ.                 
           DISPLAY ' NO. OF RECORDS IN REPORT = ' WS-L288-WRITE.                
           DISPLAY SPACES.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
