      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C23Z.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  04 FEBRUARY 2015.                                         
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  SORT QUEUE FOR 100% AND 200%                *          
      *                                                              *          
      *    INPUT FILES  : BP13F205                                   *          
      *                   BP13F200                                   *          
      *    OUTPUT       : P13F20A1                                   *          
      *                   P13F20A2                                   *          
      *                   P13F20B1                                   *          
      *                   P13F20B2                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP135555 ESA1 04022015 NEW PGM(MODELLED AFTER BP23C23S)       *          
      *BP136217 ESA1 27042016 TO REMOVE SORTING OF BROCHURE FOR      *          
      *                       BALLOT-QTR >= '201602'                 *          
      *BP136836 ESA1 22082017 TO CATER FOR 'ROF' SELECTION TYPE      *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13F200 ASSIGN       TO BP13F200.                            
           SELECT P13F20A1 ASSIGN       TO P13F20A1.                            
           SELECT P13F20A2 ASSIGN       TO P13F20A2.                            
           SELECT P13F20B1 ASSIGN       TO P13F20B1.                            
           SELECT P13F20B2 ASSIGN       TO P13F20B2.                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F200.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD   P13F20A1                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F20A1-REC         PIC X(300).                                    
                                                                                
       FD   P13F20A2                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F20A2-REC         PIC X(300).                                    
                                                                                
       FD   P13F20B1                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F20B1-REC         PIC X(300).                                    
                                                                                
       FD   P13F20B2                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F20B2-REC         PIC X(300).                                    
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F200-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-K813-FND                PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-FIRST-100               PIC X     VALUE SPACE.                
           05  WS-CNT-F20A1-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F20A2-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F20B1-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F20B2-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F200-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F205-READ           PIC 9(8)  VALUE 0.                    
           05  WS-K813-STATUS             PIC 9(02) VALUE 0.                    
           05  WS-K800-STATUS             PIC 9(02) VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1100-READ-F205         THRU 1100-EXIT.                       
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                   UNTIL WS-F205-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F200                                                 
                       BP13K800                                                 
                       BP13K813                                                 
                OUTPUT P13F20A1                                                 
                       P13F20A2                                                 
                       P13F20B1                                                 
                       P13F20B2.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS ' WS-K800-STATUS             
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 OPEN ERROR, STATUS ' WS-K813-STATUS             
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-READ-F205.                                                          
      *--------------------------------------------------------------*          
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                        MOVE 'Y' TO WS-F200-EOF                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1                 TO WS-CNT-F205-READ.                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F200.                                                          
      *--------------------------------------------------------------*          
           READ BP13F200                                                        
                AT END  MOVE 'Y' TO WS-F200-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F200-READ.                                           
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       1300-CLOSE-OPEN-F200.                                                    
      *--------------------------------------------------------------*          
           CLOSE      BP13F200.                                                 
           OPEN INPUT BP13F200.                                                 
                                                                                
           MOVE 'N'   TO WS-F200-EOF.                                           
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           PERFORM 4000-PROCESS           THRU 4000-EXIT                        
                   UNTIL WS-F200-EOF = 'Y'.                                     
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       4000-PROCESS.                                                            
      *----------------------------------------------------------------         
           PERFORM 1200-READ-F200    THRU 1200-EXIT.                            
                                                                                
           IF WS-F200-EOF = 'Y'                                                 
              PERFORM 1100-READ-F205        THRU 1100-EXIT                      
                                                                                
              IF WS-F205-EOF = 'Y'                                              
                 GO TO 4000-EXIT                                                
              END-IF                                                            
                                                                                
              PERFORM 1300-CLOSE-OPEN-F200  THRU 1300-EXIT                      
              GO TO 4000-EXIT                                                   
           ELSE                                                                 
              IF ((F205-NUM-NT-ZONE = F200-NUM-NT-ZONE) AND                     
                  (F205-NUM-FLAT-TYPE = F200-NUM-FLAT-TYPE))                    
                  IF F205-NUM-SELECTION = 'NPL'                                 
                    PERFORM 5000-WRITE-P13F20A1   THRU 5000-EXIT                
                  ELSE                                                          
                    PERFORM 4100-READ-BP13K800    THRU 4100-EXIT                
                    IF F205-NUM-SELECTION = 'SBF' OR 'ROF'                      
                       PERFORM 4500-VALIDATE-FT-QUEUE THRU 4500-EXIT            
                       IF WS-FIRST-100 = 'Y'                                    
                          IF K800-DTE-BALLOT >= '201602'                        
                             PERFORM 5000-WRITE-P13F20A1 THRU 5000-EXIT         
                          ELSE                                                  
                           IF K800-NUM-SALE-BROCHURE-SCPY = 'H'                 
                             PERFORM 5000-WRITE-P13F20A1 THRU 5000-EXIT         
                           ELSE                                                 
                             PERFORM 5100-WRITE-P13F20A2 THRU 5100-EXIT         
                           END-IF                                               
                          END-IF                                                
                       ELSE                                                     
                          IF WS-FIRST-100 = 'N'                                 
                             IF K800-DTE-BALLOT >= '201602'                     
                              PERFORM 5100-WRITE-P13F20A2 THRU 5100-EXIT        
                             ELSE                                               
                               IF K800-NUM-SALE-BROCHURE-SCPY = 'H'             
                                  PERFORM 6000-WRITE-P13F20B1                   
                                                 THRU 6000-EXIT                 
                               ELSE                                             
                                  PERFORM 6100-WRITE-P13F20B2                   
                                                 THRU 6100-EXIT                 
                               END-IF                                           
                             END-IF                                             
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       4100-READ-BP13K800.                                                      
      *--------------------------------------------------------------*          
           MOVE SPACES               TO K800-NUM-REGN.                          
           MOVE F200-NUM-REGN        TO K800-NUM-REGN.                          
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE SPACES      TO BP13K800-MASTER                             
             WHEN OTHER                                                         
                DISPLAY 'READ BP13K800 ERROR ' F200-NUM-REGN                    
                DISPLAY 'STATUS ' WS-K800-STATUS                                
                MOVE WS-K800-STATUS TO RETURN-CODE                              
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4500-VALIDATE-FT-QUEUE.                                                  
      *-------------------------------------------------------------            
           MOVE SPACES   TO  WS-FIRST-100.                                      
                                                                                
           PERFORM 7000-READ-BP13K813    THRU 7000-EXIT.                        
                                                                                
           IF WS-K813-FND = 'Y'                                                 
              IF F200-NUM-HHTY-QUEUE-SERIAL <= K813-NUM-QUEUE-MID               
                 MOVE 'Y'    TO  WS-FIRST-100                                   
              ELSE                                                              
                 MOVE 'N'    TO  WS-FIRST-100                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-P13F20A1.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F20A1-REC.                               
            WRITE P13F20A1-REC FROM BP13F200-REC.                               
            ADD  1  TO WS-CNT-F20A1-WRITE.                                      
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       5100-WRITE-P13F20A2.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F20A2-REC.                               
            WRITE P13F20A2-REC FROM BP13F200-REC.                               
            ADD  1  TO WS-CNT-F20A2-WRITE.                                      
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       6000-WRITE-P13F20B1.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F20B1-REC.                               
            WRITE P13F20B1-REC FROM BP13F200-REC.                               
            ADD  1  TO WS-CNT-F20B1-WRITE.                                      
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       6100-WRITE-P13F20B2.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F20B2-REC.                               
            WRITE P13F20B2-REC FROM BP13F200-REC.                               
            ADD  1  TO WS-CNT-F20B2-WRITE.                                      
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       7000-READ-BP13K813.                                                      
      *--------------------------------------------------------------*          
           MOVE 'N'                 TO WS-K813-FND.                             
           MOVE F205-NUM-NT-ZONE    TO K813-NUM-ZONE.                           
           MOVE F205-NUM-FLAT-TYPE  TO K813-NUM-FLAT-TYPE.                      
           MOVE F205-DTE-BALLOT     TO K813-DTE-BALLOT.                         
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y'            TO WS-K813-FND                              
           WHEN 23                                                              
                MOVE 'N'            TO WS-K813-FND                              
                DISPLAY 'RECORD NOT FOUND IN BP13K813 : '                       
                         K813-KEY-FLD                                           
           WHEN OTHER                                                           
                DISPLAY 'READ BP13K813 ERROR ' K813-KEY-FLD                     
                DISPLAY 'STATUS ' WS-K813-STATUS                                
                MOVE WS-K813-STATUS TO RETURN-CODE                              
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F205                                                       
                 BP13F200                                                       
                 BP13K800                                                       
                 BP13K813                                                       
                 P13F20A1                                                       
                 P13F20A2                                                       
                 P13F20B1                                                       
                 P13F20B2.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K800, STATUS ' WS-K800-STATUS            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K813, STATUS ' WS-K813-STATUS            
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C23Z CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F200) = ' WS-CNT-F200-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(100%)(H)  = ' WS-CNT-F20A1-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(100%)(S)  = ' WS-CNT-F20A1-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(200%)(H)  = ' WS-CNT-F20B1-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(200%)(S)  = ' WS-CNT-F20B2-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
