       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD09.                                                 
      *AUTHOR.        EDEN ROSE DINGAL                                          
      *DATE-WRITTEN.  22/01/07.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  OUTPUT MCPS  CASES                          *            
      *  CONDITION  :  NUM-MCPS NOT = SPACES AND                   *            
      *               (CDE-RESIDENT-HA1 NOT = 'H' AND              *            
      *               (CDE-RESIDENT-HA1 NOT = SPACES AND           *            
      *                CDE-RESIDENT-HA1 NOT = 'P' AND              *            
      *                CDE-RESIDENT-HA1 NOT = 'M')                 *            
      *                                                            *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F740                                 *            
      *  OUTPUT FILE:  1. P13F740A                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133244 ED1 20080122 NEW PROGRAM                          *            
      * BP133425 IL5 20081205 CHECK F740-CDE-RESIDENT-HA1 ^=SPACES *            
      * BP134400 IL5 20111101 CHECK F740-NUM-ALLOC-SCH = PA        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT P13F740A ASSIGN TO P13F740A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F740                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       FD  P13F740A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01 P13F740A-REC  PIC X(500).                                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F740-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-F740-WRITE       PIC 9(5)    VALUE ZEROS.                     
           05  WS-BYPASS-CTR       PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01  WS-CUR-DTE               PIC X(8)  VALUE SPACES.                     
       01  WS-CUR-DTE-R REDEFINES WS-CUR-DTE.                                   
           05  WS-CUR-DTE-N         PIC 9(8).                                   
       01  WS-DIFFERENCE            PIC 9(8)  VALUE 0.                          
       01  WS-CURR-DTE-J            PIC 9(8)  VALUE 0.                          
       01  WS-TOM-DTE-J             PIC 9(8)  VALUE 0.                          
       01  WS-EOF                   PIC X       VALUE 'N'.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F740        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                OUTPUT P13F740A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DTE.                            
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F740.                                                          
      *-------------------------------------------------------------            
            READ BP13F740 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F740-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
            IF (F740-NUM-MCPS NOT = SPACES AND                                  
                F740-CDE-ALLOC-SCH = 'PA ' AND                                  
                F740-CDE-RESIDENT-HA1 NOT = SPACES AND                          
                F740-CDE-RESIDENT-HA1 NOT = 'H' AND                             
                F740-CDE-RESIDENT-HA1 NOT = 'P' AND                             
                F740-CDE-RESIDENT-HA1 NOT = 'M')                                
                WRITE P13F740A-REC FROM BP13F740-REC                            
                ADD 1 TO NO-F740-WRITE                                          
            END-IF.                                                             
                                                                                
            PERFORM 2000-READ-F740         THRU 2000-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13F740                                                       
                 P13F740A.                                                      
                                                                                
           DISPLAY '-----------------------------------------'.                 
           DISPLAY '          PROGRAM BP13CD09 '.                               
           DISPLAY '        RUN DATE : ' WS-CUR-DTE.                            
           DISPLAY 'NO. OF F740 REC READ   > ' NO-F740-READ.                    
           DISPLAY 'NO. OF MCPS CASES      > ' NO-F740-WRITE.                   
           DISPLAY '-----------------------------------------'.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
