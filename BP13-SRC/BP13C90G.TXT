      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C90G.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  SEPTEMBER 07, 2015.                                       
      *===============================================================*         
      *   OBJECTIVE:  TO MATCH BP13F454 & AB02F875 BY REGN NO         *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F454                                  *         
      *                  2. AB02F875                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. P18F875A                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP135901 07/09/2015 PCL4 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F454  ASSIGN        TO BP13F454.                         
           SELECT  AB02F875  ASSIGN        TO AB02F875.                         
           SELECT  P13F875A  ASSIGN        TO P13F875A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F454                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F454.                                                           
                                                                                
       FD   AB02F875                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 1500 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AB02F875.                                                           
                                                                                
       FD   P13F875A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 1500 CHARACTERS                                     
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   P13F875A-REC                PIC X(1500).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F454-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-F875-EOF             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F454-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F875-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K454-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K454-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-NFND1            PIC 9(08) VALUE ZEROES.                  
           05  WS-CNT-NFND2            PIC 9(08) VALUE ZEROES.                  
                                                                                
       01  WS-F454-KEY                 PIC X(08) VALUE SPACES.                  
       01  WS-F875-KEY                 PIC X(08) VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE            THRU 1000-EXIT.                 
           PERFORM 2000-READ-BP13F454           THRU 2000-EXIT.                 
           PERFORM 3000-READ-AB02F875           THRU 3000-EXIT.                 
                                                                                
           PERFORM 4000-PROCESS-RECORDS         THRU 4000-EXIT                  
             UNTIL WS-F454-KEY = HIGH-VALUES                                    
               AND WS-F875-KEY = HIGH-VALUES.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  AB02F875                                                 
                       BP13F454                                                 
              OUTPUT   P13F875A.                                                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F454.                                                      
      ******************************************************************        
                                                                                
           READ BP13F454           AT END                                       
                MOVE 'Y'           TO WS-F454-EOF                               
                MOVE HIGH-VALUES   TO WS-F454-KEY                               
                GO                 TO 2000-EXIT.                                
                                                                                
                                                                                
           MOVE F454-NUM-REGN      TO WS-F454-KEY                               
           ADD  1                  TO WS-F454-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-AB02F875.                                                      
      ******************************************************************        
                                                                                
           READ AB02F875           AT END                                       
                MOVE 'Y'           TO WS-F875-EOF                               
                MOVE HIGH-VALUES   TO WS-F875-KEY                               
                GO                 TO 3000-EXIT.                                
                                                                                
           MOVE F875-NUM-REGN      TO WS-F875-KEY.                              
                                                                                
           ADD  1                  TO WS-F875-READ.                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF WS-F454-KEY = WS-F875-KEY                                         
              PERFORM 4100-PROCESS-BP13F875     THRU 4100-EXIT                  
                UNTIL WS-F454-KEY NOT = WS-F875-KEY                             
                   OR WS-F875-EOF         = 'Y'                                 
                                                                                
              PERFORM 2000-READ-BP13F454        THRU 2000-EXIT                  
           ELSE                                                                 
              IF WS-F454-KEY  < WS-F875-KEY                                     
                 ADD 1                            TO WS-CNT-NFND1               
                 PERFORM 2000-READ-BP13F454     THRU 2000-EXIT                  
              ELSE                                                              
                 ADD 1                            TO WS-CNT-NFND2               
                 PERFORM 3000-READ-AB02F875     THRU 3000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4100-PROCESS-BP13F875.                                                   
      ******************************************************************        
                                                                                
           PERFORM 5000-WRITE-P13F875A             THRU 5000-EXIT               
           PERFORM 3000-READ-AB02F875              THRU 3000-EXIT.              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-P13F875A.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                             TO P13F875A-REC.             
           INITIALIZE                                 P13F875A-REC.             
                                                                                
                                                                                
           WRITE P13F875A-REC                    FROM AB02F875-REC.             
           ADD 1                                   TO WS-CNT-WRITE.             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C90G'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1)  NO OF BP13F875 RECORDS READ............. : '           
                    WS-F875-READ.                                               
           DISPLAY '(2)  NO OF BP13F454 RECORDS READ............. : '           
                    WS-F454-READ.                                               
           DISPLAY '(3)  NO OF P13F875A RECORDS WRITTEN.......... : '           
                    WS-CNT-WRITE.                                               
                                                                                
           CLOSE    AB02F875                                                    
                    BP13F454                                                    
                    P13F875A.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
