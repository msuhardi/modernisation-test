       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C040.                                                 
      *AUTHOR.        BALAKRISHNAN S.                                           
      *DATE-WRITTEN.  10/08/1999.                                               
                                                                                
      *================================================================*        
      *                  SYSTEM OF COMMITEMENT(BP13)                   *        
      *================================================================*        
      * OBJECTIVES : UPDATE ELIG-SCH = PAU IF RECORD FOUND IN RESALE   *        
      *              HISTORY STATUS = CL, CN, CP, CM, 2L, 2N, 2P.      *        
      *================================================================*        
      * FILE DESCRIPTION :-                                            *        
      *                                                                *        
      * DD NAME   I/O/E ACCESSMODE  RECLEN KEY          REMARKS        *        
      *----------------------------------------------------------------*        
      * BP13F558  I     SEQUENTIAL   600                               *        
      * BP13F520  O     SEQUENTIAL   300                               *        
      *================================================================*        
      * MODIFICATIONS :                                                *        
      * CHGE REF #       DATE       AUTHOR      REMARKS                *        
      *----------------------------------------------------------------*        
      * BP130718         10/08/1999 SB          NEW PROGRAM            *        
      * BP130897         31/07/2000 AKT         F558 EXPAND TO X(600)  *        
      *================================================================*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558     BLOCK CONTAINS 0 RECORDS                               
                         RECORD CONTAINS 600 CHARACTERS                         
                         LABEL RECORDS ARE STANDARD                             
                         RECORDING MODE IS F.                                   
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F520     BLOCK CONTAINS 0 RECORDS                               
                         RECORD CONTAINS 500 CHARACTERS                         
                         LABEL RECORDS ARE STANDARD                             
                         RECORDING MODE IS F.                                   
       COPY BP13F520.                                                           
                                                                                
      ****************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                PIC 9(04).                            
           05  WS-CUR-MM                  PIC 9(02).                            
           05  WS-CUR-DD                  PIC 9(02).                            
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD                  PIC 9(02).                            
           05  FILLER                     PIC X(01) VALUE '/'.                  
           05  WS-EDT-MM                  PIC 9(02).                            
           05  FILLER                     PIC X(01) VALUE '/'.                  
           05  WS-EDT-CCYY                PIC 9(04).                            
                                                                                
       01  WS-COUNTERS.                                                         
           05  BP13F558-READ              PIC 9(08) VALUE ZEROES.               
           05  BP13F520-READ              PIC 9(08) VALUE ZEROES.               
           05  BP13F520-UPDT              PIC 9(08) VALUE ZEROES.               
                                                                                
       01  BP13F558-EOF-FLAG              PIC X(01).                            
           88  BP13F558-EOF               VALUE 'Y'.                            
                                                                                
       01  BP13F520-EOF-FLAG              PIC X(01).                            
           88  BP13F520-EOF               VALUE 'Y'.                            
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-CNT-MATCH               PIC 9(08) VALUE ZEROES.               
           05  WS-EDT-VAR                 PIC ZZ,ZZZ,ZZ9.                       
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      *==============================================================*          
       0000-MAIN.                                                               
      *==============================================================*          
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F558     THRU 2000-EXIT.                       
           PERFORM 2500-READ-BP13F520     THRU 2500-EXIT.                       
           PERFORM 3000-PROCESS-REC       THRU 3000-EXIT                        
                       UNTIL BP13F558-EOF OR BP13F520-EOF.                      
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       1000-OPEN-FILES.                                                         
      *==============================================================*          
           OPEN INPUT  BP13F558                                                 
                I-O    BP13F520.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
           MOVE WS-CUR-CCYY TO WS-EDT-CCYY.                                     
           MOVE WS-CUR-MM   TO WS-EDT-MM.                                       
           MOVE WS-CUR-DD   TO WS-EDT-DD.                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       2000-READ-BP13F558.                                                      
      *==============================================================*          
           READ BP13F558 AT END                                                 
                MOVE 'Y' TO BP13F558-EOF-FLAG                                   
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO BP13F558-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       2500-READ-BP13F520.                                                      
      *==============================================================*          
           READ BP13F520 AT END                                                 
                MOVE 'Y' TO BP13F520-EOF-FLAG                                   
                GO TO 2500-EXIT.                                                
                                                                                
           ADD 1 TO BP13F520-READ.                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       3000-PROCESS-REC.                                                        
      *==============================================================*          
      *--------------------------------------------------------------*          
      *  MATCH BY REGISTRATION NUMBER.                               *          
      *--------------------------------------------------------------*          
           IF F558-NUM-REGN = F520-NUM-REGN                                     
              ADD 1 TO WS-CNT-MATCH                                             
              PERFORM 3500-UPDATE-BP13F520  THRU 3500-EXIT                      
              PERFORM 2000-READ-BP13F558    THRU 2000-EXIT                      
              PERFORM 2500-READ-BP13F520    THRU 2500-EXIT                      
           ELSE                                                                 
              IF F558-NUM-REGN < F520-NUM-REGN                                  
                 PERFORM 2000-READ-BP13F558 THRU 2000-EXIT                      
              ELSE                                                              
                 PERFORM 2500-READ-BP13F520 THRU 2500-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       3500-UPDATE-BP13F520.                                                    
      *==============================================================*          
      *--------------------------------------------------------------*          
      *  UPDATE ELIG-SCH IN BP13F520.                                *          
      *--------------------------------------------------------------*          
           IF F558-CDE-FILE-STATUS-RSL NOT = 'CL' AND 'CN' AND 'CP' AND         
                         'CM' AND '2L' AND '2N' AND '2P'                        
              GO TO 3500-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE 'PAU' TO F520-CDE-ELIG-SCH.                                     
                                                                                
           REWRITE BP13F520-REC.                                                
           ADD 1 TO BP13F520-UPDT.                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       9000-CLOSE-FILES.                                                        
      *==============================================================*          
           DISPLAY '================================================='          
           DISPLAY ' PROGRAM-ID : BP13C040         DATE : ' WS-EDT-DATE.        
           DISPLAY '================================================='          
           DISPLAY ' '.                                                         
           MOVE BP13F558-READ   TO WS-EDT-VAR.                                  
           DISPLAY 'NO OF RECORDS READ    FROM BP13F558 : ' WS-EDT-VAR.         
           MOVE BP13F520-READ   TO WS-EDT-VAR.                                  
           DISPLAY 'NO OF RECORDS READ    FROM BP13F520 : ' WS-EDT-VAR.         
           MOVE WS-CNT-MATCH    TO WS-EDT-VAR.                                  
           DISPLAY 'NO OF RECORDS MATCHED               : ' WS-EDT-VAR.         
           MOVE BP13F520-UPDT   TO WS-EDT-VAR.                                  
           DISPLAY 'NO OF RECORDS UPDATED INTO BP13F520 : ' WS-EDT-VAR.         
           DISPLAY ' '.                                                         
           DISPLAY '================================================='          
                                                                                
           CLOSE BP13F520                                                       
                 BP13F558.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
