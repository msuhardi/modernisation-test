       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB92.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  26/03/2010.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO UPDATE CODE CITIZENSHIP HOUSEHOLD  *                
      *               1) ALL SC IN THE FAMILY ==> SCH          *                
      *               2) SCP FOUND WITH NO SC ==> SCP          *                
      *               3) 1 SC BUT > 1 SCP     ==> SCP          *                
      *               4) OTHER UPDATE AS BLANK                 *                
      *                                                        *                
      *    INPUT FILES : BP13F740                              *                
      *                  BP13K825                              *                
      *    OUTPUT FILES: BP13K740                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * ------    --     ----      -----------                 *                
      * BP13XXXX  PCL3  20100212   NEW PROGRAM                 *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN       TO BP13F740.                            
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS K825-STATUS.                         
                                                                                
           SELECT  BP13K740  ASSIGN        TO BP13K740                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K740-KEY-FLD                      
                             FILE STATUS   IS K740-STATUS.                      
           SELECT BP13F500 ASSIGN       TO BP13F500.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  BP13K825                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K740                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K740.                                                           
                                                                                
       FD  BP13F740                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13F500                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F500.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURR-DATE                   PIC X(8)  VALUE SPACES.               
                                                                                
       01  K825-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K740-STATUS                    PIC 9(2)  VALUE ZERO.                 
                                                                                
       01  WS-F740-READ                   PIC 9(5)  VALUE ZERO.                 
       01  WS-K825-READ                   PIC 9(5)  VALUE ZERO.                 
       01  WS-K825-NFND                   PIC 9(5)  VALUE ZERO.                 
       01  WS-CNT-UPD                     PIC 9(5)  VALUE ZERO.                 
       01  WS-HH-CDE                      PIC X(3)  VALUE SPACES.               
       01  WS-HH-SCP                      PIC X(3)  VALUE SPACES.               
       01  WS-HH-SCH                      PIC X(3)  VALUE SPACES.               
       01  CNT-HH-SCH                     PIC 9(2)  VALUE ZEROS.                
       01  CNT-HH-SCP                     PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-F740                    PIC X     VALUE SPACE.                
           88 EOF-F740                    VALUE 'Y'.                            
                                                                                
       01  WS-EOF-K825                    PIC X     VALUE SPACE.                
           88 EOF-K825                    VALUE 'Y'.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F740     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-BP13F740  THRU 3000-EXIT                        
             UNTIL EOF-F740.                                                    
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K825                                                 
                I-O    BP13K740                                                 
               OUTPUT  BP13F500.                                                
                                                                                
           IF K825-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K825-STATUS ' K825-STATUS                    
              MOVE K825-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K740-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K740-STATUS ' K740-STATUS                    
              MOVE K740-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F740.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-EOF-F740                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-BP13F740.                                                   
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                       TO WS-HH-SCH.                      
           MOVE SPACES                       TO WS-HH-SCP.                      
           MOVE ZEROS                        TO CNT-HH-SCH.                     
           MOVE ZEROS                        TO CNT-HH-SCP.                     
           PERFORM 3200-BROWSE-BP13K825    THRU 3200-EXIT.                      
           PERFORM 5000-UPDATE-CTZSHP-HH   THRU 5000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-BROWSE-BP13K825.                                                    
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                  TO BP13K825-REC.                        
           INITIALIZE                      BP13K825-REC.                        
                                                                                
           MOVE 'N'                     TO WS-EOF-K825.                         
                                                                                
           MOVE F740-NUM-REGN           TO K825-NUM-REGN.                       
                                                                                
           START BP13K825 KEY >= K825-NUM-REGN.                                 
                                                                                
           EVALUATE K825-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3210-READNEXT-BP13K825 THRU 3210-EXIT               
                    PERFORM 3220-PROCESS-BP13K825  THRU 3220-EXIT               
                      UNTIL EOF-K825                                            
               WHEN 20                                                          
               WHEN 23                                                          
                    ADD 1                            TO WS-K825-NFND            
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K825 : ' K825-STATUS                 
                          ' REGN : ' F740-NUM-REGN                              
                  PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3210-READNEXT-BP13K825.                                                  
      *---------------------------------------------------------------*         
                                                                                
           READ BP13K825 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K825.                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3220-PROCESS-BP13K825.                                                   
      *---------------------------------------------------------------*         
                                                                                
           IF F740-NUM-REGN = K825-NUM-REGN                                     
              ADD 1                               TO WS-K825-READ               
              IF K825-NUM-RELATIONSHIP NOT = '00'                               
                 PERFORM 4000-CHECK-CTZSHP-CDE     THRU 4000-EXIT               
              END-IF                                                            
              PERFORM 3210-READNEXT-BP13K825    THRU 3210-EXIT                  
           ELSE                                                                 
             MOVE 'Y'                             TO WS-EOF-K825                
           END-IF.                                                              
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-CHECK-CTZSHP-CDE.                                                   
      *---------------------------------------------------------------*         
                                                                                
           IF K825-NUM-CITIZENSHIP  = '10' OR 'SC'                              
              ADD 1                TO CNT-HH-SCH                                
              MOVE 'SCH'           TO WS-HH-SCH                                 
           ELSE                                                                 
              IF K825-NUM-CITIZENSHIP NOT = '10' AND 'SC'                       
                 ADD 1                TO CNT-HH-SCP                             
                 MOVE 'SCP'           TO WS-HH-SCP                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-UPDATE-CTZSHP-HH.                                                   
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                            TO BP13K740-REC.              
           INITIALIZE                                BP13K740-REC.              
                                                                                
           MOVE F740-KEY-FLD                      TO K740-KEY-FLD.              
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE K740-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 6000-REWRITE-BP13K740   THRU 6000-EXIT              
                                                                                
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K740 : ' K740-STATUS             
                            ' KEY = ' K740-NUM-REGN                             
                    PERFORM  9000-CLOSE-FILES       THRU 9000-EXIT              
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-REWRITE-BP13K740.                                                   
      *---------------------------------------------------------------*         
                                                                                
           IF WS-HH-SCP = SPACES AND WS-HH-SCH NOT = SPACES                     
               MOVE 'SCH'        TO K740-CDE-CITIZENSHIP-HOUSEHOLD              
           ELSE                                                                 
              IF WS-HH-SCP NOT = SPACES AND WS-HH-SCH = SPACES                  
                 MOVE 'SCP'      TO K740-CDE-CITIZENSHIP-HOUSEHOLD              
              ELSE                                                              
                 MOVE SPACES     TO K740-CDE-CITIZENSHIP-HOUSEHOLD              
              END-IF                                                            
           END-IF.                                                              
      **                                                                        
      **   IF  WS-HH-SCH NOT = SPACES                                           
      **       MOVE 'SCH'       TO K740-CDE-CITIZENSHIP-HOUSEHOLD               
      **   ELSE                                                                 
      **       MOVE 'SCP'       TO K740-CDE-CITIZENSHIP-HOUSEHOLD               
      **   END-IF.                                                              
                                                                                
           WRITE BP13F500-REC FROM BP13K740-REC.                                
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE K740-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1                             TO WS-CNT-UPD             
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR REWRITING BP13K740 : ' K740-STATUS           
                            ' KEY = ' K740-NUM-REGN                             
                    PERFORM  9000-CLOSE-FILES       THRU 9000-EXIT              
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
                                                                                
           CLOSE BP13F740                                                       
                 BP13K825                                                       
                 BP13K740                                                       
                 BP13F500.                                                      
                                                                                
           IF K825-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K825-STATUS ' K825-STATUS                   
              MOVE K825-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K740-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K740-STATUS ' K740-STATUS                   
              MOVE K740-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CB92 ================*'.              
           DISPLAY '   NO. OF F740 RECORDS READ      = ' WS-F740-READ.          
           DISPLAY '   NO. OF K825 RECORDS READ      = ' WS-K825-READ.          
           DISPLAY '   NO. OF K825 RECORDS NOT FOUND = ' WS-K825-NFND.          
           DISPLAY '   NO. OF K740 RECORDS UPDATED   = ' WS-CNT-UPD.            
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
