       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C711.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  03/08/91.                                                 
      *DATE-COMPILED.                                                           
      *============================================================*            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      *============================================================*            
      *  OBJECTIVE :                                               *            
      *    1.  TO UPDATE THE RANDOM NUMBER FOR THE JB GROUP WITH   *            
      *        THE SMALLEST RANDOM NUMBER WITHIN THE LINK.         *            
      *    2.  TO UPDATE THE JC WITHIN THE GROUP TO                *            
      *        A) TOGETHER IN TRANSITIONAL OR                      *            
      *        B) TOGETHER IN NHH > 6 MTHS OR                      *            
      *        C) TOGETHER IN HIGHEST COHORT                       *            
      *                                                            *            
      *   INPUT FILES:     1.  BP13F520                            *            
      *                    2.  BP13K520                            *            
      *                                                            *            
      *   OUTPUT FILES :   1.  BP13F510                            *            
      *------------------------------------------------------------*            
      * REQ-NO  BY   ON     DESCRIPTION                            *            
      * ---------------------------------                          *            
      * C9204F6 LSB 920410 - JB GROUP UNDER BIGGER COHORT WITHIN   *            
      *                      THE JB-LINK                           *            
      * C920887 LSB 921021 - UPG > 18 MTHS, TREATED AS TARGET GP   *            
      * C930154 YPH 930120 - JB GROUP BASED ON BIGGER NHH-BAND     *            
      *                      OR BIGGER COHORT WITHIN JB-LINK       *            
      *BP130340 SSS 971114 - CHG K740/F740 TO K740/F740            *            
      *BP130652 NOR 990219 - EXPAND F510 F740 & K740 RECL 300 - 500*            
      *BP132317 ROM 030310 - TO CHANGE F740 TO F520 AND K740 TO    *            
      *                      K520; KEY-FLD IS REGISTRATION NUMBER. *            
      *============================================================*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13K520 ASSIGN TO BP13K520                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K520-KEY-NEW                                    
                FILE STATUS IS BP13K520-STATUS.                                 
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13F510 ASSIGN TO BP13F510.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13K520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K520.                                                           
                                                                                
       FD   BP13F520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F510                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F510.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-ACCESS-STATUS.                                               
           05  BP13K520-STATUS         PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-JB-CNT               PIC 99      VALUE ZEROS.                 
           05  WS-CNT-F520-READ        PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-F510-WRITE       PIC 9(5)    VALUE ZEROS.                 
           05  WS-SMALL-RANDOM         PIC X(28)   VALUE SPACES.                
           05  WS-SMALL-REGN           PIC X(28)   VALUE SPACES.                
           05  WS-F520-NUM-REGN        PIC X(8)    VALUE SPACES.                
           05  WS-TRANS-MTH            PIC 9(2)    VALUE ZEROS.                 
           05  WS-DTE-BALLOT-MM        PIC 9(6)    VALUE ZEROS.                 
           05  WS-DTE-9201-MM          PIC 9(6)    VALUE ZEROS.                 
                                                                                
       01  WS-F520-DTE-BALLOT.                                                  
           05  WS-DTE-BAL-YY           PIC 9(4).                                
           05  WS-DTE-BAL-MM           PIC 9(2).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-RANDOM-NO.                                                    
               10  WS-RANDOM-27CHAR    PIC X(27)   VALUE SPACES.                
               10  WS-RANDOM-01CHAR    PIC X(1)    VALUE SPACES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F520-STATUS          PIC X       VALUE 'N'.                   
               88  WS-F520-EOF                     VALUE 'Y'.                   
           05  WS-LINK-STATUS          PIC X       VALUE ' '.                   
               88  WS-LINK-COMPLETE                VALUE 'C'.                   
               88  WS-LINK-ERROR                   VALUE 'E'.                   
               88  WS-INVALID-CNT                  VALUE 'I'.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES        THRU 100-EXIT.                         
           PERFORM 200-PROCESS-BP13F520  THRU 200-EXIT                          
              UNTIL (WS-F520-EOF).                                              
           PERFORM 900-CLOSE-ROUTINE     THRU 900-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13K520                                                 
                       BP13F520                                                 
                OUTPUT BP13F510.                                                
                                                                                
           IF BP13K520-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K520-STATUS : '                       
                                         BP13K520-STATUS                        
              MOVE BP13K520-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-PROCESS-BP13F520.                                                    
      *-------------------------------------------------------------            
      *   READ SEQUENTIALLY FROM  BP13F520 EXTRACT FILE,           *            
      *        IF JB CODE = 1,2,3 OR 5                             *            
      *           BROWSE THRU THE JOINT BALLOT LINK TO SELECT      *            
      *           THE SMALLEST RANDOM NO AND UPDATE THE KEY-FLD    *            
      *           WITH THE SMALLEST RANDOM NUMBER.                 *            
      *------------------------------------------------------------*            
           READ BP13F520                                                        
                AT END                                                          
                   MOVE 'Y' TO  WS-F520-STATUS                                  
                   GO TO 200-EXIT.                                              
           ADD 1 TO WS-CNT-F520-READ.                                           
                                                                                
           MOVE F520-NUM-RANDOM    TO WS-SMALL-RANDOM.                          
           MOVE F520-NUM-REGN      TO WS-SMALL-REGN                             
                                      WS-F520-NUM-REGN.                         
                                                                                
           IF F520-CDE-JB = 1 OR 2 OR 3 OR 5                                    
                 PERFORM 400-PROCESS-JB-APPLTN                                  
                    THRU 400-EXIT.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PROCESS-JB-APPLTN.                                                   
      *-------------------------------------------------------------            
           MOVE F520-NUM-REGN    TO  K520-NUM-REGN.                             
           MOVE     0            TO  WS-JB-CNT.                                 
           MOVE  SPACES          TO  WS-LINK-STATUS.                            
                                                                                
      *  BROWSING THRU THE JB LINK FOR THE SMALLEST RANDOM NUMBER.              
                                                                                
           PERFORM 450-READ-BP13K520                                            
              THRU 450-EXIT                                                     
             UNTIL (WS-JB-CNT > F520-NUM-JB-CNT) OR                             
                   (WS-LINK-ERROR) OR (WS-LINK-COMPLETE) OR                     
                   (WS-INVALID-CNT).                                            
                                                                                
      *  CHECK FOR ERROR IN JB LINK                                             
                                                                                
           IF WS-JB-CNT > 10                                                    
              DISPLAY '*** MORE THAN 10 PAIR IN AN JB FOR REGN NO :'            
                       F520-NUM-REGN                                            
              MOVE '99'            TO RETURN-CODE                               
              GO TO 400-EXIT.                                                   
                                                                                
           IF WS-LINK-ERROR                                                     
              DISPLAY '*** JOINT BALLOT APPLICANT LINK ERROR '                  
                      ' AT REGN NUM = ' F520-NUM-REGN                           
              MOVE '99'            TO RETURN-CODE                               
              GO TO 400-EXIT.                                                   
                                                                                
           IF WS-INVALID-CNT                                                    
              MOVE '99'            TO RETURN-CODE                               
              GO TO 400-EXIT.                                                   
                                                                                
                                                                                
      *  SETUP RECORD AND ASSIGN REGN NO. FOR BP13F510                          
                                                                                
           MOVE SPACES           TO  BP13F510-REC.                              
           INITIALIZE                BP13F510-REC.                              
                                                                                
           MOVE BP13F520-REC     TO  BP13F510-REC.                              
           ADD        1          TO  WS-CNT-F510-WRITE.                         
           MOVE WS-SMALL-REGN    TO  F510-NUM-SEQUENCE                          
                                     F510-NUM-JB-PARENT-REGN.                   
           MOVE WS-SMALL-RANDOM  TO  WS-RANDOM-NO.                              
           IF WS-SMALL-RANDOM NOT = F520-NUM-RANDOM                             
               MOVE '2'          TO  WS-RANDOM-01CHAR                           
           ELSE                                                                 
               MOVE '1'          TO  WS-RANDOM-01CHAR.                          
           MOVE WS-RANDOM-NO     TO  F510-NUM-RANDOM.                           
                                                                                
           MOVE F520-DTE-BALLOT TO WS-F520-DTE-BALLOT.                          
           COMPUTE WS-DTE-BALLOT-MM                                             
                   = (WS-DTE-BAL-YY * 12) + WS-DTE-BAL-MM.                      
           COMPUTE WS-DTE-9201-MM                                               
                   = (1992 * 12) + 01.                                          
           COMPUTE WS-TRANS-MTH                                                 
                   = (WS-DTE-BALLOT-MM - WS-DTE-9201-MM) + 12.                  
                                                                                
           WRITE BP13F510-REC.                                                  
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-READ-BP13K520.                                                       
      *-------------------------------------------------------------            
           READ BP13K520 KEY IS K520-KEY-NEW.                                   
                                                                                
           IF BP13K520-STATUS = 23                                              
              DISPLAY '*** NOT FOUND IN BP13K520 KEY = '                        
                      K520-KEY-FLD                                              
              MOVE 'E' TO  WS-LINK-STATUS                                       
              MOVE BP13K520-STATUS TO RETURN-CODE                               
              GO TO 450-EXIT                                                    
           ELSE                                                                 
           IF BP13K520-STATUS NOT = 0                                           
              DISPLAY '*** ERROR READING BP13K520 KEY = '                       
                      K520-KEY-FLD                                              
              DISPLAY '***       RETURN CODE = '  BP13K520-STATUS               
              MOVE BP13K520-STATUS TO RETURN-CODE                               
              GO TO 450-EXIT.                                                   
                                                                                
           ADD  1  TO  WS-JB-CNT.                                               
                                                                                
      *  SELECT THE SMALLER RANDOM NUMBER FOR THE JB GROUP                      
                                                                                
           IF K520-KEY-FLD  <  WS-SMALL-RANDOM                                  
              MOVE K520-KEY-FLD       TO  WS-SMALL-RANDOM                       
              MOVE K520-NUM-REGN      TO  WS-SMALL-REGN.                        
                                                                                
      *  IF BROKEN LINK EXISTS .....                                            
                                                                                
           IF K520-NUM-JB-REGN = SPACES OR LOW-VALUES OR '00000000'             
              MOVE 'E' TO  WS-LINK-STATUS                                       
              GO TO 450-EXIT.                                                   
                                                                                
      *  IF JB COUNT DOES NOT TALLY WITH THE ACTUAL LINK .....                  
                                                                                
           IF ( (K520-NUM-JB-REGN = WS-F520-NUM-REGN) AND                       
                (WS-JB-CNT NOT = F520-NUM-JB-CNT) )                             
              DISPLAY '*** F520-JB-CNT > ACTUAL NO. EXIST : REGN-NUM = '        
                      F520-NUM-REGN                                             
              MOVE 'I' TO WS-LINK-STATUS                                        
              GO TO 450-EXIT.                                                   
                                                                                
           IF ( (K520-NUM-JB-REGN NOT = WS-F520-NUM-REGN) AND                   
                (WS-JB-CNT = F520-NUM-JB-CNT) )                                 
              DISPLAY '*** F520-JB-CNT < ACTUAL NO. EXIST : REGN-NUM = '        
                      F520-NUM-REGN                                             
              MOVE 'I' TO WS-LINK-STATUS                                        
              GO TO 450-EXIT.                                                   
                                                                                
      *  IF THE LINK IS COMPLETED, EXIT.                                        
                                                                                
           IF K520-NUM-JB-REGN = WS-F520-NUM-REGN                               
              MOVE 'C' TO WS-LINK-STATUS                                        
              GO TO 450-EXIT.                                                   
                                                                                
      *  OTHERWISE, READ NEXT MEMBER.                                           
                                                                                
           MOVE K520-NUM-JB-REGN TO  K520-NUM-REGN.                             
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13K520                                                       
                 BP13F520                                                       
                 BP13F510.                                                      
                                                                                
           IF BP13K520-STATUS NOT = 00 AND 97                                   
              DISPLAY '***  CLOSING ERROR, BP13K520-STATUS '                    
              BP13K520-STATUS.                                                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*---------------- BP13C711 ----------------* '.             
           DISPLAY ' '.                                                         
           DISPLAY                                                              
           'TOTAL NO. OF REC READ FROM EXTRACT FILE (BP13F520)   : '            
                   WS-CNT-F520-READ.                                            
           DISPLAY                                                              
           'TOTAL NO. OF REC WRITTEN TO SEQUENCE FILE (BP13F510) : '            
                   WS-CNT-F510-WRITE.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
