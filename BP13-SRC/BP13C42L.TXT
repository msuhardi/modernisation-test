       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42L.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  31/08/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    :  TO GENERATE REPORT ON CASES UNDER MGPS     *            
      *                 / MGLS                                     *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F800                               *            
      *                 2.) BM06K110                               *            
      *                 3.) BM06K100                               *            
      *                 4.) BP13K813                               *            
      * OUTPUT FILES :  1.) P13L42LA                               *            
      *                 2.) P13L42LB                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136330 KSJ3 31/08/2016 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BM06K100  ASSIGN       TO BM06K100                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BP13K813  ASSIGN       TO BP13K813                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K813-KEY-FLD                        
                            FILE STATUS  IS WS-K813-STATUS.                     
                                                                                
           SELECT P13L42LA  ASSIGN       TO P13L42LA.                           
                                                                                
           SELECT P13L42LB  ASSIGN       TO P13L42LB.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F800.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
           COPY BM06K110.                                                       
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
           COPY BM06K100.                                                       
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
           COPY BP13K813.                                                       
                                                                                
       FD  P13L42LA                                                             
           RECORDING MODE  IS  V.                                               
       01  P13L42LA-REC                  PIC X(255).                            
                                                                                
       FD  P13L42LB                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  P13L42LB-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K100-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K813-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F800-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F800-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L42LA-WRITE        PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L42LB-WRITE        PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L42LA-LINE         PIC 9(2)   VALUE 60.                   
           05  WS-CNT-L42LB-LINE         PIC 9(2)   VALUE 60.                   
           05  WS-CNT-L42LA-PAGE         PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-L42LB-PAGE         PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-L42LA-SNO          PIC 9(5)   VALUE ZEROES.               
           05  WS-CNT-L42LB-SNO          PIC 9(5)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F800-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K100-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-PIDB-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K813-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DESC               PIC X(60)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L42LA-PR-HEAD-01.                                                    
           05  FILLER                    PIC X(14)   VALUE 'BP13L42LA'.         
           05  FILLER                    PIC X(8)    VALUE 'HDB3    '.          
           05  FILLER                    PIC X(25)   VALUE SPACES.              
           05  FILLER                    PIC X(39)   VALUE                      
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(13)   VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'DATE: '.            
           05  L42LA-DTE-CURRENT         PIC X(10)   VALUE SPACES.              
           05  FILLER                    PIC X(3)    VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'PAGE: '.            
           05  L42LA-PAGE                PIC ZZZ9.                              
                                                                                
       01  L42LA-PR-HEAD-02.                                                    
           05  FILLER                    PIC X(45)   VALUE SPACES.              
           05  FILLER                    PIC X(41)   VALUE                      
              'MONTHLY REPORT ON CASES UNDER MGPS / MGLS'.                      
                                                                                
       01  L42LA-PR-HEAD-03.                                                    
           05  FILLER                    PIC X(132)  VALUE                      
              '    S/NO   REGN      PAIRED    ROOM     ALLOC   ELIG    L        
      -       'OAN  ESTATE  NEIGHBORHOOD  CONTRACT  PCD         1ST KEY         
      -       '    KEY ISSUED'.                                                 
                                                                                
       01  L42LA-PR-HEAD-04.                                                    
           05  FILLER                    PIC X(132)  VALUE                      
              '                     REGN      TYPE     SCHEME  SCHEME  T        
      -       'AG                                               ISSUE DA        
      -       'TE  DATE'.                                                       
                                                                                
       01  L42LA-PR-HEAD-05.                                                    
           05  FILLER                    PIC X(132)  VALUE                      
              '                               ADDRESS                           
      -       '                                     PROJECT NAME'.              
                                                                                
       01  L42LA-PR-DTL-1.                                                      
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  L42LA-SNO                 PIC ZZZZ9.                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LA-NUM-REGN            PIC X(8).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LA-NUM-PAIR-REGN       PIC X(8).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LA-NUM-FLAT-TYPE       PIC X(7).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LA-NUM-ALLO-SCH        PIC X(3).                              
           05  FILLER                    PIC X(5)    VALUE SPACES.              
           05  L42LA-NUM-ELIG-SCH        PIC X(3).                              
           05  FILLER                    PIC X(5)    VALUE SPACES.              
           05  L42LA-NUM-LOAN-TAG        PIC X(1).                              
           05  FILLER                    PIC X(5)    VALUE SPACES.              
           05  L42LA-NUM-ESTATE          PIC X(3).                              
           05  FILLER                    PIC X(5)    VALUE SPACES.              
           05  L42LA-NUM-NEIGHBORHOOD    PIC X(4).                              
           05  FILLER                    PIC X(10)   VALUE SPACES.              
           05  L42LA-NUM-CONTRACT        PIC X(4).                              
           05  FILLER                    PIC X(6)    VALUE SPACES.              
           05  L42LA-DTE-PACD            PIC X(10).                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LA-DTE-1ST-KEY-ISS     PIC X(10).                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LA-DTE-KEY-ISSUED      PIC X(10).                             
                                                                                
       01  L42LA-PR-DTL-2.                                                      
           05  FILLER                    PIC X(31)   VALUE SPACES.              
           05  L42LA-NUM-ADDRESS         PIC X(60).                             
           05  FILLER                    PIC X(3)    VALUE SPACES.              
           05  L42LA-NME-PROJECT         PIC X(20).                             
                                                                                
       01  L42LA-PR-TOTAL-LINE.                                                 
           05  FILLER                    PIC X(45)   VALUE SPACES.              
           05  FILLER                    PIC X(31)                              
           VALUE 'TOTAL NO. OF RECORDS PRINTED = '.                             
           05  L42LA-TOTAL-REC           PIC ZZZZ9.                             
                                                                                
       01  L42LA-PR-ASTERICK-LINE.                                              
           05  FILLER                    PIC X(56)   VALUE SPACES.              
           05  FILLER                    PIC X(15)   VALUE ALL '*'.             
           05  FILLER                    PIC X(61)   VALUE SPACES.              
                                                                                
       01  L42LA-PR-DASH-LINE.                                                  
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  FILLER                    PIC X(124)  VALUE ALL '-'.             
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT FOR EXCEPTIONS                                         
      *-------------------------------------------------------------            
       01  L42LB-PR-HEAD-01.                                                    
           05  FILLER                    PIC X(14)   VALUE 'BP13L42LB'.         
           05  FILLER                    PIC X(8)    VALUE 'HDB3    '.          
           05  FILLER                    PIC X(25)   VALUE SPACES.              
           05  FILLER                    PIC X(39)   VALUE                      
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(13)   VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'DATE: '.            
           05  L42LB-DTE-CURRENT         PIC X(10)   VALUE SPACES.              
           05  FILLER                    PIC X(3)    VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'PAGE: '.            
           05  L42LB-PAGE                PIC ZZZ9.                              
                                                                                
       01  L42LB-PR-HEAD-02.                                                    
           05  FILLER                    PIC X(44)   VALUE SPACES.              
           05  FILLER                    PIC X(43)   VALUE                      
              'EXCEPTION REPORT ON CASES UNDER MGPS / MGLS'.                    
                                                                                
       01  L42LB-PR-HEAD-03.                                                    
           05  FILLER                    PIC X(132)  VALUE                      
              '    S/NO   REGN      PAIRED REGN  ERROR DESCRIPTION'.            
                                                                                
       01  L42LB-PR-DTL.                                                        
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  L42LB-SNO                 PIC ZZZZ9.                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LB-NUM-REGN            PIC X(8).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L42LB-NUM-PAIR-REGN       PIC X(8).                              
           05  FILLER                    PIC X(5)    VALUE SPACES.              
           05  L42LB-NUM-DESC            PIC X(60).                             
                                                                                
       01  L42LB-PR-DASH-LINE.                                                  
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  FILLER                    PIC X(90)   VALUE ALL '-'.             
                                                                                
      *-------------------------------------------------------------            
      *    LINK VARIABLES FOR BP13C913                                          
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH       PIC X(4).                              
               10  WS-LINK-NUM-ACC       PIC X(5).                              
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K813                                                 
                OUTPUT P13L42LA                                                 
                       P13L42LB.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K100 ERROR ' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K813 ERROR ' WS-K813-STATUS                  
              MOVE     WS-K813-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 4010-PRINT-REPORT-HDR   THRU 4010-EXIT.                      
           PERFORM 4110-PRINT-ERROR-HDR    THRU 4110-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F800                                                        
              AT END MOVE 'Y'              TO   WS-F800-EOF.                    
                                                                                
           IF WS-F800-EOF NOT = 'Y'                                             
              ADD  1                       TO   WS-CNT-F800-READ                
              MOVE SPACES                  TO   WS-NUM-DESC                     
                                                L42LA-PR-DTL-1                  
                                                L42LA-PR-DTL-2                  
                                                L42LB-PR-DTL                    
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF (F800-NUM-REGN     = SPACES OR LOW-VALUES OR ZEROES) OR           
              (F800-NUM-JT-BALLOT-REGN                                          
                                 = SPACES OR LOW-VALUES OR ZEROES) OR           
              (F800-NUM-SCH-ACC  = SPACES OR LOW-VALUES OR ZEROES) OR           
              (F800-NUM-MGPS NOT = 'P' AND 'C')                                 
              ADD 1                        TO   WS-CNT-F800-SKIP                
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET FLAT INV RECORD                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   K110-REC.                       
           MOVE F800-NUM-SCH-ACC           TO   K110-SCH-ACC-NO.                
           PERFORM 3100-READ-BM06K110      THRU 3100-EXIT.                      
                                                                                
           IF WS-K110-FND NOT = 'Y'                                             
              MOVE 'FLAT INVENTORY RECORD NOT FOUND'                            
                                           TO   WS-NUM-DESC                     
              PERFORM 4100-PRINT-ERROR     THRU 4100-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET BLOCK RECORD                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   K100-REC.                       
           MOVE K110-AIX1                  TO   K100-KEY-FLD.                   
           PERFORM 3200-READ-BM06K100      THRU 3200-EXIT.                      
                                                                                
           IF WS-K100-FND NOT = 'Y'                                             
              MOVE 'BLOCK RECORD NOT FOUND'                                     
                                           TO   WS-NUM-DESC                     
              PERFORM 4100-PRINT-ERROR     THRU 4100-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET PIDB RECORD                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13COMM8-REC.                  
           MOVE F800-NUM-SCH-ACC           TO   WS-LINK-REC.                    
           PERFORM 3300-CALL-BP13C913      THRU 3300-EXIT.                      
                                                                                
           IF WS-PIDB-FND NOT = 'Y'                                             
              MOVE SPACES                  TO   COMM8-NUM-BLK                   
                                                COMM8-NUM-LEVEL                 
                                                COMM8-NUM-UNIT-MAIN             
                                                COMM8-NME-STREET                
                                                COMM8-NUM-POSTAL-CODE           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET PROJECT NAME                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K813-REC.                   
           MOVE F800-NUM-BTO-ZONE          TO   K813-NUM-ZONE.                  
           MOVE F800-DTE-BALLOT            TO   K813-DTE-BALLOT.                
                                                                                
           IF F800-NUM-BTO-ZONE = '034'                                         
              MOVE '4 '                    TO   K813-NUM-FLAT-TYPE              
           ELSE                                                                 
           IF F800-NUM-BTO-ZONE = '036'                                         
              MOVE '3 '                    TO   K813-NUM-FLAT-TYPE              
           ELSE                                                                 
           IF F800-NUM-BTO-ZONE = '0C3'                                         
              MOVE '4 '                    TO   K813-NUM-FLAT-TYPE              
           ELSE                                                                 
           IF F800-NUM-BTO-ZONE = '0C4'                                         
              MOVE '5 '                    TO   K813-NUM-FLAT-TYPE              
           ELSE                                                                 
              IF F800-NUM-BTO-FLAT-TYPE = '1A' OR '2A'                          
                 MOVE F800-NUM-BTO-FLAT-TYPE                                    
                                           TO   K813-NUM-FLAT-TYPE              
              ELSE                                                              
                 MOVE F800-NUM-BTO-FLAT-TYPE(1:1)                               
                                           TO   K813-NUM-FLAT-TYPE              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3400-READ-BP13K813      THRU 3400-EXIT                       
                                                                                
           IF WS-K813-FND NOT = 'Y'                                             
              MOVE SPACES                  TO   K813-NME-PROJECT-LTR            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    PRINT RECORD IN REPORT                                               
      *-------------------------------------------------------------            
           PERFORM 4000-PRINT-REPORT       THRU 4000-EXIT.                      
                                                                                
       3000-CONTINUE-READ-NEXT.                                                 
                                                                                
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BM06K110.                                                      
      *=============================================================            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K110-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K110-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BM06K110 ERROR '    WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-READ-BM06K100.                                                      
      *=============================================================            
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K100-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K100-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BM06K100 ERROR '    WS-K100-STATUS                  
              MOVE     WS-K100-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-CALL-BP13C913.                                                      
      *=============================================================            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN 100                                                             
              MOVE 'N'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'LINK ERROR ON BP13C913 '                                 
              DISPLAY 'SQL CODE:   '            COMM8-CDE-SYSERR                
              DISPLAY 'ERROR CODE: '            COMM8-CDE-ERROR                 
              MOVE     COMM8-CDE-SYSERR    TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-READ-BP13K813.                                                      
      *=============================================================            
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K813-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K813-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K813 ERROR '    WS-K813-STATUS                  
              MOVE     WS-K813-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-PRINT-REPORT.                                                       
      *=============================================================            
                                                                                
      *    IF WS-CNT-L42LA-LINE > 54                                            
      *       PERFORM 4010-PRINT-REPORT-HDR     THRU 4010-EXIT                  
      *    END-IF.                                                              
      *    ADD  2                          TO   WS-CNT-L42LA-LINE               
                                                                                
           ADD  1                          TO   WS-CNT-L42LA-SNO                
                                                                                
      *-------------------------------------------------------------            
      *    PRINT 1ST LINE                                                       
      *-------------------------------------------------------------            
           MOVE WS-CNT-L42LA-SNO           TO   L42LA-SNO.                      
           MOVE F800-NUM-REGN              TO   L42LA-NUM-REGN                  
           MOVE F800-NUM-JT-BALLOT-REGN    TO   L42LA-NUM-PAIR-REGN             
                                                                                
           IF (F800-NUM-FLAT-TYPE  = '1A' OR '2A') OR                           
              (F800-NUM-IDP-SCHEME = 'SA ')                                     
              MOVE 'SA'                    TO   L42LA-NUM-FLAT-TYPE             
           ELSE                                                                 
              IF F800-NUM-FLAT-TYPE = '2F'                                      
                 STRING F800-NUM-FLAT-TYPE ' ('                                 
                        F800-NUM-LEASE-TENURE ')'                               
                        DELIMITED BY SIZE  INTO L42LA-NUM-FLAT-TYPE             
              ELSE                                                              
                 MOVE F800-NUM-FLAT-TYPE   TO   L42LA-NUM-FLAT-TYPE             
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-ALLO-SCHEME       TO   L42LA-NUM-ALLO-SCH              
           MOVE F800-NUM-ELIG-SCHEME       TO   L42LA-NUM-ELIG-SCH              
           MOVE F800-NUM-LOAN-TAG          TO   L42LA-NUM-LOAN-TAG              
           MOVE K100-ESTATE                TO   L42LA-NUM-ESTATE                
           MOVE K100-NEIGHBOURHOOD         TO   L42LA-NUM-NEIGHBORHOOD          
           MOVE K100-CONTRACT-NO           TO   L42LA-NUM-CONTRACT              
                                                                                
           IF K100-DTE-PACD NOT = SPACES AND LOW-VALUES AND ZEROES              
              STRING K100-DTE-PACD-DD       '/'                                 
                     K100-DTE-PACD-QTR(5:2) '/'                                 
                     K100-DTE-PACD-QTR(1:4)                                     
                     DELIMITED BY SIZE     INTO L42LA-DTE-PACD                  
           END-IF.                                                              
                                                                                
           IF K100-DTE-1ST-KEY-ISSUE NOT = SPACES AND LOW-VALUES AND            
                                           ZEROES                               
              STRING K100-DTE-1ST-KEY-ISSUE(7:2) '/'                            
                     K100-DTE-1ST-KEY-ISSUE(5:2) '/'                            
                     K100-DTE-1ST-KEY-ISSUE(1:4)                                
                     DELIMITED BY SIZE     INTO L42LA-DTE-1ST-KEY-ISS           
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES AND               
                                        ZEROES                                  
              STRING K110-DTE-KEY-ISSUED(7:2) '/'                               
                     K110-DTE-KEY-ISSUED(5:2) '/'                               
                     K110-DTE-KEY-ISSUED(1:4)                                   
                     DELIMITED BY SIZE     INTO L42LA-DTE-KEY-ISSUED            
           END-IF.                                                              
                                                                                
           WRITE P13L42LA-REC              FROM L42LA-PR-DTL-1.                 
                                                                                
      *-------------------------------------------------------------            
      *    PRINT 2ND LINE                                                       
      *-------------------------------------------------------------            
           IF COMM8-NUM-BLK NOT = SPACES AND LOW-VALUES                         
              STRING 'BLK ' COMM8-NUM-BLK                                       
                     ' #'   COMM8-NUM-LEVEL                                     
                     ' -'   COMM8-NUM-UNIT-MAIN ' '   DELIMITED BY SIZE         
                     ' '    COMM8-NME-STREET          DELIMITED BY '   '        
                     ' S('  COMM8-NUM-POSTAL-CODE ')' DELIMITED BY SIZE         
                                           INTO L42LA-NUM-ADDRESS               
           END-IF.                                                              
                                                                                
           MOVE K813-NME-PROJECT-LTR       TO   L42LA-NME-PROJECT               
                                                                                
           WRITE P13L42LA-REC              FROM L42LA-PR-DTL-2.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-PRINT-REPORT-HDR.                                                   
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-L42LA-PAGE.              
           MOVE WS-CNT-L42LA-PAGE          TO   L42LA-PAGE.                     
           MOVE WS-DTE-CURR-10             TO   L42LA-DTE-CURRENT.              
                                                                                
           WRITE P13L42LA-REC FROM L42LA-PR-HEAD-01.                            
           WRITE P13L42LA-REC FROM L42LA-PR-HEAD-02.                            
                                                                                
           MOVE SPACES                     TO   P13L42LA-REC.                   
           WRITE P13L42LA-REC.                                                  
                                                                                
           WRITE P13L42LA-REC FROM L42LA-PR-DASH-LINE.                          
           WRITE P13L42LA-REC FROM L42LA-PR-HEAD-03.                            
           WRITE P13L42LA-REC FROM L42LA-PR-HEAD-04.                            
                                                                                
           MOVE SPACES                     TO   P13L42LA-REC.                   
           WRITE P13L42LA-REC.                                                  
                                                                                
           WRITE P13L42LA-REC FROM L42LA-PR-HEAD-05.                            
           WRITE P13L42LA-REC FROM L42LA-PR-DASH-LINE.                          
                                                                                
           MOVE SPACES                     TO   P13L42LA-REC.                   
           WRITE P13L42LA-REC.                                                  
                                                                                
           MOVE 10                         TO   WS-CNT-L42LA-LINE.              
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4020-PRINT-REPORT-TOTAL.                                                 
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   P13L42LA-REC.                   
           WRITE P13L42LA-REC.                                                  
                                                                                
           WRITE P13L42LA-REC              FROM L42LA-PR-ASTERICK-LINE.         
                                                                                
           MOVE SPACES                     TO   P13L42LA-REC.                   
           WRITE P13L42LA-REC.                                                  
                                                                                
           MOVE WS-CNT-L42LA-SNO           TO   L42LA-TOTAL-REC.                
           WRITE P13L42LA-REC              FROM L42LA-PR-TOTAL-LINE.            
                                                                                
       4020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-L42LB-LINE > 54                                            
              PERFORM 4110-PRINT-ERROR-HDR THRU 4110-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-L42LB-SNO                
                                                WS-CNT-L42LB-LINE               
                                                WS-CNT-F800-SKIP.               
                                                                                
           MOVE WS-CNT-L42LB-SNO           TO   L42LB-SNO.                      
           MOVE F800-NUM-REGN              TO   L42LB-NUM-REGN.                 
           MOVE F800-NUM-JT-BALLOT-REGN    TO   L42LB-NUM-PAIR-REGN.            
           MOVE WS-NUM-DESC                TO   L42LB-NUM-DESC.                 
                                                                                
           WRITE P13L42LB-REC              FROM L42LB-PR-DTL.                   
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4110-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-L42LB-PAGE.              
           MOVE WS-CNT-L42LB-PAGE          TO   L42LB-PAGE.                     
           MOVE WS-DTE-CURR-10             TO   L42LB-DTE-CURRENT.              
                                                                                
           WRITE P13L42LB-REC FROM L42LB-PR-HEAD-01 AFTER PAGE.                 
           WRITE P13L42LB-REC FROM L42LB-PR-HEAD-02.                            
           WRITE P13L42LB-REC FROM L42LB-PR-DASH-LINE AFTER 2.                  
           WRITE P13L42LB-REC FROM L42LB-PR-HEAD-03.                            
           WRITE P13L42LB-REC FROM L42LB-PR-DASH-LINE.                          
                                                                                
           MOVE SPACES                     TO   P13L42LB-REC.                   
           WRITE P13L42LB-REC.                                                  
                                                                                
           MOVE 7                          TO   WS-CNT-L42LB-LINE.              
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4120-PRINT-ERROR-TOTAL.                                                  
      *=============================================================            
                                                                                
           IF WS-CNT-L42LB-LINE > 50                                            
              PERFORM 4110-PRINT-ERROR-HDR THRU 4110-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO   P13L42LB-REC.                   
           WRITE P13L42LB-REC.                                                  
                                                                                
           WRITE P13L42LB-REC              FROM L42LA-PR-ASTERICK-LINE.         
                                                                                
           MOVE SPACES                     TO   P13L42LB-REC.                   
           WRITE P13L42LB-REC.                                                  
                                                                                
           MOVE WS-CNT-L42LB-SNO           TO   L42LA-TOTAL-REC.                
           WRITE P13L42LB-REC              FROM L42LA-PR-TOTAL-LINE.            
                                                                                
       4120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           PERFORM 4020-PRINT-REPORT-TOTAL THRU 4020-EXIT.                      
           PERFORM 4120-PRINT-ERROR-TOTAL  THRU 4120-EXIT.                      
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13K813                                                       
                 P13L42LA                                                       
                 P13L42LB.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K100 ERROR ' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K813 ERROR ' WS-K813-STATUS                  
              MOVE     WS-K813-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C3A5 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13K800 READ      : ' WS-CNT-F800-READ.          
           DISPLAY ' 2. NO OF BP13K800 SKIPPED   : ' WS-CNT-F800-SKIP.          
           DISPLAY ' 3. NO OF P13L42LA WRITTEN   : ' WS-CNT-L42LA-SNO.          
           DISPLAY ' 3. NO OF P13L42LB WRITTEN   : ' WS-CNT-L42LB-SNO.          
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
