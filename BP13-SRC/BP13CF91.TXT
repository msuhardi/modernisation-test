       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF91.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  24/06/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  GENERATE FILES WITH COMPLETE & INCOMPLETE  *            
      *                 APPLT/OCCUP PER REGN                       *            
      *                                                            *            
      * INPUT FILES  :  P13LF87C - LIST OF REGN (COMPLETE APP/OCC) *            
      *                 BE01F515 - OBTAIN FROM MHA VIA IDNET       *            
      * OUTPUT FILES :  F515COMP - COMPLETE APPLT/OCCUP PER REGN   *            
      *                 F515INCO - INCOMPLETE APPLT/OCCUP PER REGN *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134922  SMR2  24062013  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13LF87C  ASSIGN       TO P13LF87C.                           
           SELECT BE01F515  ASSIGN       TO BE01F515.                           
           SELECT F515COMP  ASSIGN       TO F515COMP.                           
           SELECT F515INCO  ASSIGN       TO F515INCO.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   P13LF87C                                                            
            RECORD CONTAINS  20 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   P13LF87C-REC.                                                       
            05 LF87-NUM-REGN       PIC X(08).                                   
            05 FILLER              PIC X(12).                                   
                                                                                
       FD   BE01F515                                                            
            RECORD CONTAINS 580 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BE01F515.                                                           
                                                                                
       FD   F515COMP                                                            
            RECORD CONTAINS 580 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   F515COMP-REC           PIC X(580).                                  
                                                                                
       FD   F515INCO                                                            
            RECORD CONTAINS 580 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   F515INCO-REC           PIC X(580).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-LF87-READ        PIC 9(06)   VALUE ZEROES.                    
           05  WS-F515-READ        PIC 9(06)   VALUE ZEROES.                    
           05  WS-COMP-WRITE       PIC 9(06)   VALUE ZEROES.                    
           05  WS-INCO-WRITE       PIC 9(06)   VALUE ZEROES.                    
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-LF87-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F515-REGN        PIC X(8)    VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-MAIN-ROUTINE        THRU 2000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  P13LF87C                                                 
                       BE01F515                                                 
                OUTPUT F515COMP                                                 
                       F515INCO.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 3000-READ-P13LF87C       THRU 3000-EXIT.                     
           PERFORM 4000-READ-BE01F515       THRU 4000-EXIT.                     
           PERFORM 5000-PROCESS-FILES       THRU 5000-EXIT                      
             UNTIL WS-LF87-REGN = HIGH-VALUES                                   
               AND WS-F515-REGN = HIGH-VALUES.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-P13LF87C.                                                      
      *-------------------------------------------------------------            
           READ P13LF87C AT END                                                 
                MOVE HIGH-VALUES            TO WS-LF87-REGN                     
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-LF87-READ.                    
           MOVE LF87-NUM-REGN               TO WS-LF87-REGN.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BE01F515.                                                      
      *-------------------------------------------------------------            
           READ BE01F515 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F515-REGN                     
                GO TO 4000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F515-READ.                    
           MOVE BE01F515-REF-NO(1:8)        TO WS-F515-REGN.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           IF WS-LF87-REGN = HIGH-VALUES AND                                    
              WS-F515-REGN NOT = HIGH-VALUES                                    
              MOVE SPACES                   TO F515INCO-REC                     
              WRITE F515INCO-REC            FROM BE01F515-REC                   
              ADD 1                         TO WS-INCO-WRITE                    
              PERFORM 4000-READ-BE01F515    THRU 4000-EXIT                      
              GO TO 5000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-LF87-REGN = WS-F515-REGN                                       
              MOVE SPACES                   TO F515COMP-REC                     
              WRITE F515COMP-REC            FROM BE01F515-REC                   
              ADD 1                         TO WS-COMP-WRITE                    
              PERFORM 4000-READ-BE01F515    THRU 4000-EXIT                      
           ELSE                                                                 
              IF WS-LF87-REGN < WS-F515-REGN                                    
                 PERFORM 3000-READ-P13LF87C THRU 3000-EXIT                      
              ELSE                                                              
                 MOVE SPACES                TO F515INCO-REC                     
                 WRITE F515INCO-REC         FROM BE01F515-REC                   
                 ADD 1                      TO WS-INCO-WRITE                    
                 PERFORM 4000-READ-BE01F515 THRU 4000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '*============== BP13CF91 ==============*'.                  
           DISPLAY '  NO. OF LF87 REGN READ       = ' WS-LF87-READ.             
           DISPLAY '  NO. OF F515 REGN READ       = ' WS-F515-READ.             
           DISPLAY '  NO. OF COMP CASES WRITTEN   = ' WS-COMP-WRITE.            
           DISPLAY '  NO. OF INCOMP CASES WRITTEN = ' WS-INCO-WRITE.            
           DISPLAY '*======================================*'.                  
                                                                                
           CLOSE P13LF87C                                                       
                 BE01F515                                                       
                 F515COMP                                                       
                 F515INCO.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
