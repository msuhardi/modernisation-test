       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB70                                                  
      *AUTHOR.        ANC1.                                                     
      *DATE-WRITTEN.  12/08/2002.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH BP13F800 WITH BP13F595 BY REGN-NO TO GET  *        
      *             BOOK STATUS (SCH-ACC-NO, ALLOC-TAG, BK-APPMT).     *        
      *                                                                *        
      * INPUT  FILES : BP13F595                                        *        
      *                BP13F800                                        *        
      *                BP13F205                                        *        
      * OUTPUT FILES : P13F595A                                        *        
      *                                                                *        
      * NOTE : BP13K813 WAS INCLUDED TO CATER FOR BOOK STATUS = 'HL'   *        
      *                                                                *        
      * CHG-NO   BY    ON          DESCRIPTION                         *        
      *----------------------------------------------------------------*        
      * BP133792 ANC1  12/08/2002  NEW PROGRAM                         *        
      * BP133792 PCL3  18/12/2009  TO READ BP13F205 AND GET F205-DTE-END        
      * BP135039 IMC1  16/12/2013  REMOVE CONDITION FOR DTE-BK-APPT             
      * BP138080 SMP3  06/11/2019  CATER FOR BOOK STATUS = 'HL'                 
      * BP138080 SMP3  22/01/2020  CATER FOR BOOK STATUS = 'HS'                 
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   BP13F595      ASSIGN TO BP13F595.                           
           SELECT   BP13F205      ASSIGN TO BP13F205.                           
           SELECT   BP13F800      ASSIGN TO BP13F800.                           
                                                                                
           SELECT   BP13K813      ASSIGN TO BP13K813                            
                             ACCESS MODE IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                              RECORD KEY IS K813-KEY-FLD                        
                             FILE STATUS IS WS-K813-STATUS.                     
                                                                                
           SELECT   P13F595A      ASSIGN TO P13F595A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F595                RECORD CONTAINS 500 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F595.                                                           
                                                                                
       FD   BP13F800                RECORD CONTAINS 2000 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K813                RECORD CONTAINS 1000 CHARACTERS.            
       COPY BP13K813.                                                           
                                                                                
       FD   P13F595A                RECORD CONTAINS 500 CHARACTERS              
                                    LABEL RECORDS ARE OMITTED                   
                                    RECORDING MODE IS F.                        
       01   P13F595A-REC            PIC X(500).                                 
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F595-READ                 PIC 9(7)  VALUE ZEROS.              
           05  WS-F800-READ                 PIC 9(7)  VALUE ZEROS.              
           05  WS-F595A-WRITTEN             PIC 9(7)  VALUE ZEROS.              
           05  WS-F59A-WRITTEN              PIC 9(7)  VALUE ZEROS.              
           05  WS-UNMATCH-F800              PIC 9(7)  VALUE ZEROS.              
           05  WS-UNMATCH-F595              PIC 9(7)  VALUE ZEROS.              
           05  WS-UNMATCH-REGN              PIC 9(7)  VALUE ZEROS.              
           05  WS-UNMATCH-NRIC              PIC 9(7)  VALUE ZEROS.              
           05  WS-MATCH-REGN                PIC 9(7)  VALUE ZEROS.              
           05  WS-MATCH-NRIC                PIC 9(7)  VALUE ZEROS.              
           05  WS-NOTFS                     PIC 9(7)  VALUE ZEROS.              
           05  WS-OMIT                      PIC 9(7)  VALUE ZEROS.              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K813-STATUS              PIC 9(02) VALUE ZEROES.              
                                                                                
       01  WS-NEI-CONT.                                                         
           05  WS-NEI                      PIC X(04) VALUE SPACES.              
           05  WS-CONT                     PIC X(04) VALUE SPACES.              
                                                                                
       01  WS-F800-KEY.                                                         
           05  WS-F800-REGN                PIC X(08) VALUE SPACES.              
           05  WS-F800-NRIC                PIC X(09) VALUE SPACES.              
                                                                                
       01  WS-F595-KEY.                                                         
           05  WS-F595-REGN                PIC X(08) VALUE SPACES.              
           05  WS-F595-NRIC                PIC X(09) VALUE SPACES.              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                 PIC X(08) VALUE SPACES.              
           05  WS-SYS-TIME                 PIC X(08) VALUE SPACES.              
           05  FILLER                      PIC X(05) VALUE SPACES.              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       0000-MAIN.                                                               
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F595       THRU 2000-EXIT.                     
           PERFORM 2010-READ-BP13F800       THRU 2010-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F595-NUM-REGN = HIGH-VALUES AND                              
                   F800-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
           OPEN INPUT BP13F595                                                  
                      BP13F205                                                  
                      BP13F800                                                  
                      BP13K813                                                  
               OUTPUT P13F595A.                                                 
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K813 : ' WS-K813-STATUS              
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F595.                                                      
                                                                                
           READ BP13F595                                                        
             AT END MOVE HIGH-VALUES TO F595-NUM-REGN                           
             GO TO 2000-EXIT.                                                   
           MOVE F595-NUM-REGN TO WS-F595-REGN.                                  
                                                                                
           ADD  1                    TO WS-F595-READ.                           
                                                                                
           PERFORM 3600-READ-BP13K813 THRU 3600-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2010-READ-BP13F800.                                                      
                                                                                
           READ BP13F800                                                        
             AT END MOVE HIGH-VALUES TO  F800-NUM-REGN                          
             GO TO 2010-EXIT.                                                   
           MOVE F800-NUM-REGN TO WS-F800-REGN.                                  
           MOVE F800-NUM-NRIC1 TO WS-F800-NRIC.                                 
                                                                                
           ADD  1                    TO  WS-F800-READ.                          
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-READ-BP13F205.                                                      
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9000-CLOSE-FILES.                         
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-RECORD.                                                     
                                                                                
           IF F800-NUM-REGN = F595-NUM-REGN                                     
              ADD 1                            TO WS-MATCH-REGN                 
              PERFORM 3500-MATCH-REGN        THRU 3500-EXIT                     
              PERFORM 2010-READ-BP13F800     THRU 2010-EXIT                     
              PERFORM 2000-READ-BP13F595     THRU  2000-EXIT                    
           ELSE                                                                 
             IF F800-NUM-REGN > F595-NUM-REGN                                   
                                                                                
                 MOVE  SPACES             TO F595-DTE-UPDATE                    
                 MOVE  SPACES             TO F595-TME-SYSTEM                    
                 MOVE  SPACES             TO F595-NUM-REGN-REF                  
                 PERFORM 4500-WRITE-F595A    THRU 4500-EXIT                     
                 ADD   1                     TO WS-UNMATCH-F595                 
                 PERFORM 2000-READ-BP13F595  THRU 2000-EXIT                     
              ELSE                                                              
                 ADD   1                     TO WS-UNMATCH-F800                 
                 PERFORM 2010-READ-BP13F800  THRU 2010-EXIT                     
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-MATCH-REGN.                                                         
      *                                                                         
      *    IF F800-DTE-BK-APPT = '99999999'                                     
      *       ADD 1                    TO WS-OMIT                               
      *       GO TO 3500-EXIT.                                                  
      *                                                                         
           IF F800-DTE-BK-APPT = SPACES                                         
              MOVE  SPACES             TO F595-DTE-UPDATE                       
           ELSE                                                                 
              MOVE  F800-DTE-BK-APPT   TO F595-DTE-UPDATE                       
              MOVE  F800-DTE-BK-APPT   TO F595-DTE-BK-APPT                      
           END-IF.                                                              
                                                                                
           MOVE SPACES                 TO F595-TME-SYSTEM.                      
           MOVE F800-NUM-ALLOC-TAG     TO F595-TME-SYSTEM.                      
           MOVE F800-NUM-SCH-ACC       TO F595-NUM-REGN-REF.                    
                                                                                
           PERFORM 4500-WRITE-F595A  THRU 4500-EXIT.                            
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3600-READ-BP13K813.                                                      
                                                                                
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
                                                                                
           MOVE F595-CDE-NT1                TO K813-NUM-ZONE.                   
           IF F595-CDE-FLAT-TYPE(2:1)  = 'P'                                    
              EVALUATE F595-CDE-FLAT-TYPE(1:1)                                  
              WHEN 'S'                                                          
                   MOVE '1A'                TO F595-CDE-FLAT-TYPE               
              WHEN '3'                                                          
                   MOVE '3 '                TO F595-CDE-FLAT-TYPE               
              WHEN '4'                                                          
                   MOVE '4 '                TO F595-CDE-FLAT-TYPE               
              WHEN '5'                                                          
                   MOVE '5 '                TO F595-CDE-FLAT-TYPE               
              END-EVALUATE                                                      
           END-IF.                                                              
           MOVE F595-CDE-FLAT-TYPE          TO K813-NUM-FLAT-TYPE.              
           MOVE F595-DTE-BALLOT             TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
           EVALUATE WS-K813-STATUS                                              
           WHEN 00                                                              
                CONTINUE                                                        
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K813 : ' K813-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                MOVE WS-K813-STATUS         TO RETURN-CODE                      
                DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS               
                        ' KEY : ' K813-KEY-FLD                                  
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4500-WRITE-F595A.                                                        
           MOVE SPACES             TO P13F595A-REC.                             
                                                                                
      **   IF F595-DTE-BALLOT = '200710'                                        
      **     IF F595-CDE-NT1 = '44 ' OR                                         
      **        F595-CDE-NT1 = '45 ' OR                                         
      **        F595-CDE-NT1 = '46 '                                            
      **           MOVE 'BE ' TO F595-NUM-ALLO-CAT                              
      **           MOVE 'BM ' TO F595-CDE-NT1                                   
      **     END-IF                                                             
      **   END-IF.                                                              
                                                                                
           IF F595-CDE-NT1 = 'SK ' AND F595-DTE-BALLOT = '200702'               
              MOVE 'BTO' TO F595-NUM-ALLO-CAT                                   
           END-IF.                                                              
                                                                                
           IF F595-CDE-NT1 = 'JW ' AND F595-DTE-BALLOT = '200707'               
              MOVE 'BTO' TO F595-NUM-ALLO-CAT                                   
           END-IF.                                                              
                                                                                
           IF F595-CDE-NT1 = 'JW ' AND F595-DTE-BALLOT = '200801'               
              MOVE 'BTO' TO F595-NUM-ALLO-CAT                                   
           END-IF.                                                              
                                                                                
      *** NOTE : THIS IS TO DETERMINE 'HL' BOOK STATUS REQUESTED BY             
      ***        THE USER USING CR# - BP138080                                  
                                                                                
           IF (F595-NUM-ALLO-CAT = 'BTO') AND                                   
              (F595-NUM-BOOK-STATUS = SPACES) AND                               
              (F595-CDE-REQUEST-STATUS = 'W' OR 'R' OR 'P' OR 'K') AND          
              (F595-DTE-BALLOT >= '201905') AND                                 
              (K813-NUM-MATURE-EST-TAG NOT = 'Y')                               
              MOVE 'HL'         TO F595-NUM-BOOK-STATUS                         
           END-IF.                                                              
                                                                                
           IF (F595-NUM-ALLO-CAT = 'BTO') AND                                   
              (F595-NUM-BOOK-STATUS = SPACES) AND                               
              (F595-CDE-REQUEST-STATUS = 'S') AND                       D       
              (F595-DTE-BALLOT >= '201905')                                     
                 MOVE 'HS'         TO F595-NUM-BOOK-STATUS                      
           END-IF.                                                              
                                                                                
           MOVE BP13F595-REC    TO P13F595A-REC.                                
                                                                                
           WRITE P13F595A-REC.                                                  
                                                                                
           ADD   1                 TO  WS-F59A-WRITTEN.                         
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CB70'.                                
           DISPLAY 'CURRENT DATE   : ' WS-CURRENT-DATE.                         
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F595 READ     : ' WS-F595-READ.                  
           DISPLAY 'NO. OF BP13F800 READ     : ' WS-F800-READ.                  
           DISPLAY 'NO. OF F595 UNMATCHED    : ' WS-UNMATCH-F595.               
           DISPLAY 'NO. OF F800 UNMATCHED    : ' WS-UNMATCH-F800.               
           DISPLAY 'NO. OF F800/F595 MATCHED : ' WS-MATCH-REGN.                 
           DISPLAY 'NOT FS                   : ' WS-NOTFS.                      
           DISPLAY 'NO. OF P13F595A WRITTEN  : ' WS-F595A-WRITTEN.              
           DISPLAY 'NO. OF P13F595A WRITTEN  : ' WS-F59A-WRITTEN.               
           DISPLAY 'NO. OF OMIT DUE CUTOFF   : ' WS-OMIT.                       
                                                                                
           CLOSE BP13F595                                                       
                 BP13F800                                                       
                 BP13F205                                                       
                 BP13K813                                                       
                 P13F595A.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING - BP13K813 : ' WS-K813-STATUS              
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
