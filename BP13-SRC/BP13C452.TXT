       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C452.                                                 
      *AUTHOR.        LJL.                                                      
      *DATE-WRITTEN.  18/03/91.                                                 
                                                                                
      * ========================================================== *            
      *  SYSTEM OF COMMITMENT (SOC)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO DELETE APPOINTMENTS IN APPTABLE AFTER    *            
      *                IT EXPIRES, IE CURRENT DATE > APPT-DATE.    *            
      *  I/O FILE                                                  *            
      *  --------                                                  *            
      *         I-O :  BB12.K452.WORKPLAN                          *            
      *             :  BP13.K454.APPTABLE                          *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP130418 MPA  19940604  YEAR 2000 CHANGES                  *            
      * BP130839 JMS  20000520  REVAMP OF APPT MODULE              *            
      * BP134212 ESD1 20110530  CATER TO PROCESS THE NEW BP13K452  *            
      * BP136430 KSJ3 20161119  EXPAND WORKPLAN TO 1000            *            
      * ========================================================== *            
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *---------------------*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------*                                                   
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K452                                                      
              ASSIGN       TO BP13K452                                          
              ACCESS MODE  IS DYNAMIC                                           
              ORGANIZATION IS INDEXED                                           
              RECORD KEY   IS K452-KEY-FLD                                      
              FILE STATUS  IS K452-STATUS.                                      
                                                                                
           SELECT BP13K454                                                      
              ASSIGN       TO BP13K454                                          
              ACCESS MODE  IS DYNAMIC                                           
              ORGANIZATION IS INDEXED                                           
              RECORD KEY   IS K454-KEY-FLD                                      
              FILE STATUS  IS K454-STATUS.                                      
                                                                                
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K454                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13K454.                                                           
                                                                                
       FD  BP13K452                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 1000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13K452.                                                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  K454-EOF                       PIC X     VALUE SPACES.               
       01  K454-STATUS                    PIC XX    VALUE ZEROS.                
       01  K452-STATUS                    PIC XX    VALUE ZEROS.                
           88  K452-FOUND                           VALUE ZEROS.                
           88  K452-NOTFND                          VALUE '23'.                 
                                                                                
       01  WS-CUR-CCYYMMDD.                                                     
           05  WS-CUR-DTE-CC               PIC X(2).                            
           05  WS-CUR-DTE-YY               PIC X(2).                            
           05  WS-CUR-DTE-MM               PIC X(2).                            
           05  WS-CUR-DTE-DD               PIC X(2).                            
                                                                                
      *---------------------------------------------------------*               
      *    DEFINE APPOINTMENT TABEL FROM BP13K452               *               
      *---------------------------------------------------------*               
       01  WS-COUNTERS.                                                         
           05  WS-CNT                     PIC 9(2) VALUE ZEROS.                 
           05  WS-K454-READ               PIC 9(5) VALUE ZEROS.                 
           05  WS-K452-READ               PIC 9(5) VALUE ZEROS.                 
           05  WS-K452-DELETE             PIC 9(5) VALUE ZEROS.                 
           05  WS-K454-DELETE             PIC 9(5) VALUE ZEROS.                 
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
           PERFORM  0100-INITIALIZATION         THRU 0100-EXIT.                 
           PERFORM  0150-OPEN-FILES             THRU 0150-EXIT.                 
           PERFORM  0200-READ-APPT-FILE         THRU 0200-EXIT.                 
           PERFORM  0300-MAIN-PROCESS           THRU 0300-EXIT                  
                    UNTIL K454-EOF = 'Y'.                                       
           PERFORM  0500-DISPLAY-TOTAL          THRU 0500-EXIT.                 
           PERFORM  0900-CLOSE-FILES            THRU 0900-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0100-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-CCYYMMDD.                       
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0150-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN I-O    BP13K454                                                 
                       BP13K452.                                                
                                                                                
           IF K452-STATUS NOT = ZEROS AND '97'                                  
              DISPLAY 'ERROR IN OPEN BP13K452 : ' K452-STATUS                   
              MOVE K452-STATUS TO RETURN-CODE                                   
              PERFORM  0900-CLOSE-FILES THRU  0900-EXIT.                        
                                                                                
           IF K454-STATUS NOT = ZEROS AND '97'                                  
              DISPLAY 'ERROR IN OPEN BP13K454 : ' K454-STATUS                   
              MOVE K454-STATUS TO RETURN-CODE                                   
              PERFORM  0900-CLOSE-FILES THRU  0900-EXIT.                        
                                                                                
       0150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0200-READ-APPT-FILE.                                                     
      *-------------------------------------------------------------            
           READ BP13K454 NEXT AT END MOVE 'Y' TO K454-EOF                       
                  GO TO 0200-EXIT.                                              
           ADD 1 TO WS-K454-READ.                                               
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0300-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           IF K454-DTE-APPT < WS-CUR-CCYYMMDD                                   
              MOVE K454-DTE-APPT TO K452-KEY-FLD                                
              PERFORM 0400-DELETE-K452  THRU 0400-EXIT                          
           ELSE                                                                 
             NEXT SENTENCE.                                                     
                                                                                
           PERFORM  0200-READ-APPT-FILE THRU 0200-EXIT.                         
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **  DELETE THE WORKPLAN AS IT IS ALREADY OBSOLETE                         
      *-------------------------------------------------------------            
       0400-DELETE-K452.                                                        
      *-------------------------------------------------------------            
           READ BP13K452.                                                       
                                                                                
           IF K452-NOTFND                                                       
              CONTINUE                                                          
           ELSE                                                                 
           IF K452-FOUND                                                        
              ADD 1 TO    WS-K452-READ                                          
              DISPLAY 'K452 DELETED :' K452-KEY-FLD                             
              DELETE BP13K452                                                   
              ADD 1 TO    WS-K452-DELETE                                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K452   ' K452-STATUS                   
              MOVE K452-STATUS TO RETURN-CODE                                   
              PERFORM 0900-CLOSE-FILES THRU 0900-EXIT.                          
                                                                                
           DELETE BP13K454.                                                     
           ADD 1 TO          WS-K454-DELETE.                                    
                                                                                
       0400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0500-DISPLAY-TOTAL.                                                      
      *-------------------------------------------------------------            
           DISPLAY '********************************************'.              
           DISPLAY '     CONTROL REPORT FOR BP13C452            '.              
           DISPLAY '                                            '.              
           DISPLAY 'NO BP13K452(WPLAN) READ   : ' WS-K452-READ.                 
           DISPLAY 'NO BP13K454(APPT)  READ   : ' WS-K454-READ.                 
           DISPLAY 'NO OF BP13K452 REC DELETED: ' WS-K452-DELETE.               
           DISPLAY 'NO OF BP13K454 REC DELETED: ' WS-K454-DELETE.               
           DISPLAY '                                            '.              
           DISPLAY '********************************************'.              
                                                                                
       0500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0900-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           CLOSE BP13K452                                                       
                 BP13K454.                                                      
                                                                                
           IF K452-STATUS NOT = '00'                                            
              DISPLAY 'ERROR IN CLOSING BP13K452 : ' K452-STATUS                
              MOVE K452-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K454-STATUS NOT = '00'                                            
              DISPLAY 'ERROR IN CLOSING BP13K454 : ' K454-STATUS                
              MOVE K454-STATUS TO RETURN-CODE.                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       0900-EXIT.                                                               
           EXIT.                                                                
