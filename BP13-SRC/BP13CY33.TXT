       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY33.                                                 
      *AUTHOR.        BO JIANG.                                                 
      *DATE-WRITTEN.  14/08/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                             
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO DECODE NT-CODE                              *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133340  JB8   20080814  SIMILAR TO BP13CH02              *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800  ASSIGN     TO BP13F800.                             
                                                                                
           SELECT BP13K816  ASSIGN        TO BP13K816                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K816-KEY-FLD                       
                            FILE STATUS   IS BP13K816-STATUS.                   
                                                                                
           SELECT P13F800A  ASSIGN     TO P13F800A.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K816                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K816.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORDING MODE IS F.                                                
       01 P13F800A-REC                 PIC X(2000).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ              PIC 9(08) VALUE ZEROES.                  
          05 WS-F800-WRITE             PIC 9(08) VALUE ZEROES.                  
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13K816-STATUS           PIC 9(02) VALUE ZEROES.                  
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-NUM-NEW-TOWN           PIC X(3).                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F800-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K816                                                 
               OUTPUT  P13F800A.                                                
                                                                                
           IF BP13K816-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K816 : ' BP13K816-STATUS             
              MOVE     BP13K816-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
                MOVE HIGH-VALUES TO F800-NUM-REGN                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F800-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO  WS-NUM-NEW-TOWN.                                     
           IF F800-NUM-NEW-TOWN = SPACES                                        
              PERFORM 3100-DECODE-NT THRU 3100-EXIT                             
              MOVE WS-NUM-NEW-TOWN TO F800-NUM-NEW-TOWN                         
           END-IF                                                               
           MOVE SPACES TO F800-NUM-ROOM-TYPE                                    
           WRITE P13F800A-REC     FROM BP13F800-MASTER                          
           ADD 1      TO WS-F800-WRITE.                                         
                                                                                
           PERFORM 2000-READ-BP13F800 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-DECODE-NT.                                                          
           MOVE F800-NUM-ZONE    TO K816-NUM-ZONE.                              
                                                                                
           READ BP13K816.                                                       
           IF BP13K816-STATUS = 00                                              
              MOVE K816-CDE-NT   TO WS-NUM-NEW-TOWN                             
                                                                                
           ELSE                                                                 
              IF BP13K816-STATUS = 23                                           
                 DISPLAY 'READ ERROR, STATUS : ' BP13K816-STATUS                
                         ' KEY : ' K816-NUM-ZONE                                
              ELSE                                                              
                 MOVE BP13K816-STATUS TO RETURN-CODE                            
                 DISPLAY 'READ ERROR, STATUS : ' BP13K816-STATUS                
                         ' KEY : ' K816-NUM-ZONE                                
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CY33 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F800 : ' WS-F800-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO P13F800A : ' WS-F800-WRITE.           
                                                                                
           CLOSE BP13F800                                                       
                 BP13K816                                                       
                 P13F800A.                                                      
                                                                                
           IF BP13K816-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K816 : ' BP13K816-STATUS             
              MOVE BP13K816-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
