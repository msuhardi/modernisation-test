       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C3A7.                                                 
       AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  08/10/2020.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : PROGRAM TO OUTPUT OA OF 6 LOW RISK CASES         *        
      *               REQUESTED BY COS DUE TO CPF API DOWN            *         
      *                                                                         
      * OUTPUT DESCRIPTION:                                            *        
      *                                                                *        
      * BP13KC00 : SEQUENTIAL FILE TO LEAS CONTAINS REGISTRATION       *        
      *            NUMBER, HLE NUMBER AND PAC DATE.                    *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13FCPF  - CPF FILE WITH REGN, HLA, OA   *        
      *                  2.  BM06K110  - FLATINV FILE                  *        
      *                  3.  BP13K820  - OCCUPIER INFO                 *        
      *                  4.  BP13K800  - SOC APPLICATION MASTER        *        
      *                  5.  BP27K860  - HDB PRPRTY FILE               *        
      *                  6.  BP13K80A  - 2ND CA CASES FOR PROCESSING   *        
      *                  7.  BP27K800  - HLA APPLN FILE                *        
      *                  8.  BP13F800  - CA2ND LOWRISK                 *        
      *   OUTPUT FILE :  1.  BP13KC00  - WORKSHEET/CASHIER ORDER FILE  *        
      *                  3.  BP13FOUT  - CASE UPDATED IN KC00 IN F800  *        
      *                                  FORMAT                        *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *                                                                *        
      *   BP138476  FP6   08/10/20 NEW PROGRAM (ATER BP13C909)         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FCPF  ASSIGN        TO BP13FCPF.                          
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT BP13FOUT  ASSIGN        TO BP13FOUT.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD    KEY    IS K110-KEY-FLD                    
                            FILE STATUS      IS WS-K110-STATUS.                 
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-REGN                   
                            FILE STATUS      IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K80A  ASSIGN        TO BP13K80A                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS WS-K80A-REGN                    
                            FILE STATUS      IS WS-K80A-STATUS.                 
                                                                                
           SELECT BP13K640  ASSIGN        TO BP13K640                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K640-KEY-FLD                    
                            FILE STATUS      IS WS-K640-STATUS.                 
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K820-KEY-FLD                    
                            FILE STATUS      IS WS-K820-STATUS.                 
                                                                                
           SELECT BP27K860  ASSIGN        TO BP27K860                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K860-NUM-HLA                    
                            FILE STATUS      IS WS-K860-STATUS.                 
                                                                                
           SELECT BP13KC00  ASSIGN        TO BP13KC00                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS KC00-KEY-FLD                    
                            FILE STATUS      IS WS-KC00-STATUS.                 
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-HLA                    
                                                OF BP27K800-MASTER              
                            FILE STATUS      IS WS-BP27K800-STATUS.             
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FCPF                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 50   CHARACTERS.                                     
       01  BP13FCPF-REC.                                                        
           05  FCPF-NUM-REGN          PIC X(08).                                
           05  FCPF-NUM-HLA           PIC X(09).                                
           05  FCPF-AMT-CPF-OA        PIC 9(6)V99.                              
           05  FILLER                 PIC X(25).                                
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F800.                                                           
                                                                                
       FD  BP13FOUT                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       01  BP13FOUT-REC           PIC X(2000).                                  
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K80A                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       01  BP13K80A-REC.                                                        
           05 FILLER              PIC X(3).                                     
           05 WS-K80A-REGN        PIC X(8).                                     
           05 FILLER              PIC X(1989).                                  
                                                                                
       FD  BP13K640                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K640.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP27K860                                                             
           RECORD CONTAINS 10250 CHARACTERS.                                    
       COPY BP27K860.                                                           
                                                                                
       FD  BP13KC00                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13KC00.                                                           
                                                                                
       FD  BP27K800                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP27K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-COMPUTE-VARIABLES.                                                
           05 WS-CASH-PROCEED              PIC 9(7)     VALUE ZEROES.           
           05 WS-AMT-NO-SIGN               PIC 9(7)     VALUE ZEROES.           
           05 WS-TOT-FEES                  PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-SPOUSE-CPF            PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-PAY-CASH              PIC S9(7)V99  VALUE ZEROES.          
           05 WS-DOWNPAYMENT               PIC S9(7)V99  VALUE ZEROES.          
           05 WS-AMT-UPG-COST              PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-CPF-AMT               PIC S9(7)V99  VALUE ZEROES.          
           05 WS-APPLN-CPF-AMT             PIC S9(7)V99  VALUE ZEROES.          
           05 WS-SPOUSE-CPF-AMT            PIC S9(7)V99  VALUE ZEROES.          
           05 WS-LSE-ESCR-REGN-FEE         PIC S9(7)V99  VALUE ZEROES.          
           05 WS-MORT-ESCR-REGN-FEE        PIC S9(7)V99  VALUE ZEROES.          
           05 WS-SHORTFALL-TOT             PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-SURVEY-FEE            PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-STAMP-FEES            PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-AMT-TP                PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-PREM-UPGCOST          PIC S9(7)V99  VALUE ZEROES.          
           05 WS-EST-LOAN-DISB             PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-LESS-PAY              PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-SELLPR                PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOT-SUPL-CONVEY-FEE       PIC S9(7)V99  VALUE ZEROES.          
           05 WS-COMM-AMT-LEFT             PIC S9(7)V99  VALUE ZEROES.          
           05 WS-COMM-AMT-LEFT-A           PIC S9(7)V99  VALUE ZEROES.          
           05 WS-COMM-AMT-LEFT-B           PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOTAL-1                   PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOTAL-2                   PIC S9(7)V99  VALUE ZEROES.          
           05 WS-TOTAL-3                   PIC S9(7)V99  VALUE ZEROES.          
           05 WS-AMT-LEFT-SAVE             PIC S9(7)V99  VALUE ZEROES.          
           05 WS-NEW-TOT-FEES              PIC S9(7)V99  VALUE ZEROES.          
           05 WS-NEW-SELLPR-FEES           PIC S9(7)V99  VALUE ZEROES.          
           05 WS-COMM-MORT-FEE             PIC S9(7)V99  VALUE ZEROES.          
           05 WS-COMM-TOT-FEES             PIC S9(7)V99  VALUE ZEROES.          
           05 WS-COMM-SELLPR-FEES          PIC S9(7)V99  VALUE ZEROES.          
           05 WS-AMT-CD-SERCNTR            PIC S9(7)V99  VALUE ZEROES.          
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-MORT-TAG                      PIC X(01)   VALUE SPACE.            
       01  WS-FCPF-READ                     PIC 9(07)   VALUE 0.                
       01  WS-F800-READ                     PIC 9(07)   VALUE 0.                
       01  FCPF-MATCHED                     PIC 9(07)   VALUE 0.                
       01  FCPF-NOMATCH                     PIC 9(07)   VALUE 0.                
       01  F800-NOMATCH                     PIC 9(07)   VALUE 0.                
       01  WS-FOUT-WRITE                    PIC 9(07)   VALUE 0.                
       01  WS-KC00-WRITE                    PIC 9(07)   VALUE 0.                
       01  WS-KC00-REWRITE                  PIC 9(07)   VALUE 0.                
       01  WS-K80A-DELETE                   PIC 9(07)   VALUE 0.                
       01  WS-TIME                          PIC 9(08)   VALUE 0.                
       01  WS-WRITE-KC00                    PIC X   VALUE SPACE.                
       01  WS-REWRITE-KC00                  PIC X   VALUE SPACE.                
       01  WS-FOUND-K80A                    PIC X   VALUE SPACE.                
       01  WS-SKIP-TAG                      PIC X   VALUE SPACE.                
       01  WS-FIRST-ENTRY                   PIC X   VALUE 'Y'.                  
       01  WS-KC00-COUNT                    PIC 9(7)   VALUE ZERO.              
       01  WS-NON-2ND-CA-COUNT              PIC 9(7)   VALUE ZERO.              
       01  WS-2ND-CA-COUNT                  PIC 9(7)   VALUE ZERO.              
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K80A-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K860-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K820-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K640-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K110-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-KC00-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-BP27K800-STATUS          PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-NUM-HLA                     PIC X(9)  VALUE SPACES.               
                                                                                
                                                                                
       01 WS-LINK-C90E.                                                         
          05 WS-LINK-NUM-REGN             PIC X(08)    VALUE SPACES.            
          05 WS-LINK-AMT-LOAN             PIC 9(8)V99  VALUE ZEROES.            
          05 WS-LINK-SELLING-PRICE        PIC 9(8)V99  VALUE ZEROES.            
          05 WS-LINK-PREMIUM              PIC 9(7)V99  VALUE ZEROES.            
          05 WS-LINK-FLAT-DISCOUNT        PIC 9(6)V99  VALUE ZEROES.            
          05 WS-LINK-ALLO-SCHEME          PIC X(03)    VALUE SPACES.            
          05 WS-LINK-SCH-ACC              PIC X(11)    VALUE SPACES.            
          05 WS-LINK-FLAT-TYPE            PIC X(02)    VALUE SPACES.            
          05 WS-LINK-FLAT-STATUS          PIC X(02)    VALUE SPACES.            
          05 WS-LINK-ALLOCN-TYPE          PIC X(01)    VALUE SPACES.            
          05 WS-LINK-REPOSSESSED-CODE     PIC X(02)    VALUE SPACES.            
          05 WS-LINK-SCCC-ELGBTY          PIC X(01)    VALUE SPACES.            
          05 WS-LINK-CDE-FLAT-ACCEPT      PIC X(01)    VALUE SPACES.            
          05 WS-LINK-LEASE-REGN-FEE       PIC 9(3)V99  VALUE ZEROES.            
          05 WS-LINK-LEASE-ESCROW-FEE     PIC 9(3)V99  VALUE ZEROES.            
          05 WS-LINK-LEASE-STAMP-FEE      PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-PREMIUM-STAMP-FEE    PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-TOT-LSE-STAMP-FEE    PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-SURVEY-FEE           PIC 9(3)V99  VALUE ZEROES.            
          05 WS-LINK-SURVEY-FEE-GST       PIC 9(3)V99  VALUE ZEROES.            
          05 WS-LINK-CONVEY-FEE           PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-CONVEY-FEE-GST       PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-PREM-CNVY-FEE        PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-PREM-CNVY-FEE-GST    PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-TOT-CNVY-FEE         PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-TOT-CNVY-FEE-GST     PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-MORT-ST-FEE          PIC 9(5)V99  VALUE ZEROES.            
          05 WS-LINK-MORT-REGN-FEE        PIC 9(3)V99  VALUE ZEROES.            
          05 WS-LINK-MORT-ESCROW-FEE      PIC 9(3)V99  VALUE ZEROES.            
          05 WS-LINK-SALES-TYPE           PIC X(02)    VALUE SPACES.            
          05 FILLER                       PIC X(87)    VALUE SPACES.            
          05 WS-LINK-RETCODE              PIC X(03)    VALUE SPACES.            
          05 WS-LINK-ERRMSG               PIC X(40)    VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13FCPF   THRU 2000-EXIT.                         
           PERFORM 2500-READ-BP13F800   THRU 2500-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
              UNTIL F800-NUM-REGN = HIGH-VALUES AND                             
                    FCPF-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13FCPF                                                 
                       BP13F800                                                 
                       BM06K110                                                 
                       BP27K860                                                 
                       BP13K800                                                 
                       BP13K640                                                 
                       BP13K820                                                 
                       BP27K800                                                 
                I-O    BP13KC00                                                 
                       BP13K80A                                                 
                OUTPUT BP13FOUT.                                                
                                                                                
           INITIALIZE  BP13K800-MASTER BP13FCPF-REC K110-REC                    
                       BP27K860-REC BP13KC00-REC BP13K820-REC                   
                       BP13K640-TRANS-REC BP13K80A-REC                          
                       BP13FOUT-REC.                                            
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K80A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K80A ERROR ' WS-K80A-STATUS                  
              MOVE     WS-K80A-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K640-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K640 ERROR ' WS-K640-STATUS                  
              MOVE     WS-K640-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K860-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP27K860 ERROR ' WS-K860-STATUS                  
              MOVE     WS-K860-STATUS           TO RETURN-CODE                  
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC00 ERROR ' WS-KC00-STATUS                  
              MOVE     WS-KC00-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-BP27K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING BP27K800 ERROR ' WS-BP27K800-STATUS              
              MOVE     WS-BP27K800-STATUS  TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-BP13FCPF.                                                      
      *=============================================================*           
                                                                                
            READ BP13FCPF AT END                                                
                 MOVE HIGH-VALUES TO FCPF-NUM-REGN                              
                 GO TO 2000-EXIT                                                
            END-READ.                                                           
                                                                                
            ADD 1 TO WS-FCPF-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2500-READ-BP13F800.                                                      
      *=============================================================*           
                                                                                
            READ BP13F800 AT END                                                
                 MOVE HIGH-VALUES TO F800-NUM-REGN                              
                 GO TO 2500-EXIT                                                
            END-READ.                                                           
                                                                                
            ADD 1 TO WS-F800-READ.                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           IF FCPF-NUM-REGN = F800-NUM-REGN                                     
              ADD 1 TO FCPF-MATCHED                                             
              PERFORM 3050-PROCESS-MATCHED   THRU 3050-EXIT                     
                 UNTIL FCPF-NUM-REGN > F800-NUM-REGN                            
                 OR FCPF-NUM-REGN = HIGH-VALUES                                 
              PERFORM 2500-READ-BP13F800  THRU 2500-EXIT                        
           ELSE                                                                 
              IF FCPF-NUM-REGN < F800-NUM-REGN                                  
                 ADD 1 TO FCPF-NOMATCH                                          
                 PERFORM 2000-READ-BP13FCPF  THRU 2000-EXIT                     
              ELSE                                                              
                 ADD 1 TO F800-NOMATCH                                          
                 PERFORM 2500-READ-BP13F800  THRU 2500-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3050-PROCESS-MATCHED.                                                    
      *=============================================================*           
                                                                                
                                                                                
           MOVE     F800-NUM-REGN       TO   WS-K80A-REGN                       
           PERFORM  3500-READ-BP13K80A  THRU 3500-EXIT                          
                                                                                
           IF WS-FOUND-K80A = 'Y'                                               
               PERFORM 4000-RETRIEVE-BP13K800 THRU 4000-EXIT                    
               IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                  
                  PERFORM 4100-RETRIEVE-BP27K860 THRU 4100-EXIT                 
                  PERFORM 4200-RETRIEVE-BM06K110 THRU 4200-EXIT                 
                  PERFORM 4400-RETRIEVE-FEES     THRU 4400-EXIT                 
                  PERFORM 4500-RETRIEVE-OTHERS   THRU 4500-EXIT                 
                                                                                
                  PERFORM 5000-CREATE-BP13KC00   THRU 5000-EXIT                 
                  PERFORM 5300-DELETE-BP13K80A   THRU 5300-EXIT                 
                  WRITE BP13FOUT-REC         FROM BP13K800-MASTER               
                  ADD 1 TO WS-FOUT-WRITE                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FCPF  THRU 2000-EXIT.                          
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-READ-BP13K800.                                                      
      *================================================================*        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                CONTINUE                                                        
              WHEN 23                                                           
                MOVE  WS-K800-STATUS TO RETURN-CODE                             
                DISPLAY 'RECORD NOT FOUND BP13K800 : ' K800-NUM-REGN            
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              WHEN OTHER                                                        
                MOVE  WS-K800-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K800 FILE : ' WS-K800-STATUS         
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3200-READ-BP27K860.                                                      
      *================================================================*        
                                                                                
           READ BP27K860.                                                       
                                                                                
           IF WS-K860-STATUS = 00 OR 23                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  WS-K860-STATUS TO RETURN-CODE                               
              DISPLAY 'ERROR READING BP27K860 FILE : '                          
                       WS-K860-STATUS                                           
              DISPLAY 'K860-NUM-HLA  = ' K860-NUM-HLA OF BP27K860-REC           
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3300-READ-BP13K820.                                                      
      *================================================================*        
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                CONTINUE                                                        
              WHEN 23                                                           
                MOVE  WS-K820-STATUS TO RETURN-CODE                             
                DISPLAY 'RECORD NOT FOUND BP13K820 : ' K820-NUM-REGN            
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              WHEN OTHER                                                        
                MOVE  WS-K820-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K820 FILE : ' WS-K820-STATUS         
                DISPLAY 'K820-NUM-REGN = ' K820-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3400-READ-BM06K110.                                                      
      *================================================================*        
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                CONTINUE                                                        
              WHEN 23                                                           
                MOVE  WS-K110-STATUS TO RETURN-CODE                             
                DISPLAY 'SCH ACCT NOT FOUND IN FIS   : ' K110-KEY-FLD           
                DISPLAY 'K110-KEY-FIELD = ' K110-KEY-FLD                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              WHEN OTHER                                                        
                MOVE  WS-K110-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BM06K110 FILE : ' WS-K110-STATUS         
                DISPLAY 'K110-KEY-FIELD = ' K110-KEY-FLD                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3500-READ-BP13K80A.                                                      
      *================================================================*        
                                                                                
           DISPLAY 'READING BP13K80A FOR: ' WS-K80A-REGN                        
           READ BP13K80A.                                                       
                                                                                
           EVALUATE WS-K80A-STATUS                                              
              WHEN 00                                                           
                MOVE 'Y' TO WS-FOUND-K80A                                       
              WHEN 23                                                           
                MOVE 'N' TO WS-FOUND-K80A                                       
              WHEN OTHER                                                        
                MOVE  WS-K80A-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K80A FILE : ' WS-K80A-STATUS         
                DISPLAY 'K80A-NUM-REGN = ' WS-K80A-REGN                         
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       3600-READ-BP13K640.                                                      
      *================================================================*        
                                                                                
           READ BP13K640.                                                       
                                                                                
           EVALUATE WS-K640-STATUS                                              
              WHEN 00                                                           
                CONTINUE                                                        
              WHEN 23                                                           
                MOVE  WS-K640-STATUS TO RETURN-CODE                             
                DISPLAY 'RECORD NOT FOUND BP13K640 : ' K640-REGN-NO             
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              WHEN OTHER                                                        
                MOVE  WS-K640-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K640 FILE : ' WS-K640-STATUS         
                DISPLAY 'K640-REGN-NO  = ' K640-REGN-NO                         
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3700-READ-BP13KC00.                                                      
      *================================================================*        
                                                                                
           READ BP13KC00.                                                       
                                                                                
           EVALUATE WS-KC00-STATUS                                              
              WHEN 00                                                           
                MOVE 'N' TO  WS-WRITE-KC00                                      
                MOVE 'Y' TO  WS-REWRITE-KC00                                    
              WHEN 23                                                           
                MOVE 'N' TO  WS-REWRITE-KC00                                    
                MOVE 'Y' TO  WS-WRITE-KC00                                      
              WHEN OTHER                                                        
                MOVE  WS-KC00-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13KC00 FILE : ' WS-KC00-STATUS         
                DISPLAY 'KC00-NUM-REGN = ' KC00-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3800-READ-BP27K800.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES                   TO BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
                                                                                
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
             TO K800-NUM-HLA OF BP27K800-MASTER.                                
                                                                                
           READ BP27K800                                                        
                                                                                
           EVALUATE WS-BP27K800-STATUS                                          
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE  WS-BP27K800-STATUS TO RETURN-CODE                     
                    DISPLAY 'RECORD NOT FOUND BP27K800 : '                      
                             K800-NUM-HLA OF BP13K800-MASTER                    
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN OTHER                                                       
                    MOVE  WS-BP27K800-STATUS TO RETURN-CODE                     
                    DISPLAY 'FILE ERROR  BP27K800 : '                           
                             K800-NUM-HLA OF BP13K800-MASTER                    
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       3800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-RETRIEVE-BP13K800.                                                  
      *================================================================*        
                                                                                
           MOVE    F800-NUM-REGN       TO   K800-NUM-REGN K640-REGN-NO          
                                            KC00-NUM-REGN                       
           MOVE    WS-CUR-DATE         TO   KC00-DTE-UPDATE                     
           PERFORM 3100-READ-BP13K800  THRU 3100-EXIT                           
           PERFORM 3800-READ-BP27K800  THRU 3800-EXIT                           
           PERFORM 3600-READ-BP13K640  THRU 3600-EXIT                           
           PERFORM 3700-READ-BP13KC00  THRU 3700-EXIT                           
                                                                                
           IF K640-AMT-REPAY-PERIOD  IS NOT NUMERIC                             
              MOVE ZEROES TO K640-AMT-REPAY-PERIOD                              
           END-IF                                                               
           IF K640-AMT-ELIG-INSTAL   IS NOT NUMERIC                             
              MOVE ZEROES TO K640-AMT-ELIG-INSTAL                               
           END-IF                                                               
           IF K640-AMT-INTEREST-RATE IS NOT NUMERIC                             
              MOVE ZEROES TO K640-AMT-INTEREST-RATE                             
           END-IF                                                               
           IF K640-AMT-MAX-LOAN IS NOT NUMERIC                                  
              MOVE ZEROES TO K640-AMT-MAX-LOAN                                  
           END-IF                                                               
           IF K800-AMT-MAX-LOAN IS NOT NUMERIC                                  
              MOVE ZEROES TO K800-AMT-MAX-LOAN                                  
           END-IF                                                               
           IF K800-AMT-PREMIUM IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-PREMIUM                                   
           END-IF                                                               
           IF K800-AMT-CD-CASH IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CASH                                   
           END-IF                                                               
           IF K800-AMT-CD-CPF1 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF1                                   
           END-IF                                                               
           IF K800-AMT-CD-CPF2 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF2                                   
           END-IF                                                               
           IF K800-AMT-CD-CPF3 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF3                                   
           END-IF                                                               
           IF K800-AMT-CD-CPF4 IS NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-CD-CPF4                                   
           END-IF                                                               
           IF K800-AMT-CD-SERCNTR IS NOT NUMERIC                                
              MOVE ZEROES TO K800-AMT-CD-SERCNTR                                
           END-IF                                                               
           IF K800-AMT-CD-SERCNTR-NEW IS NOT NUMERIC                            
              MOVE ZEROES TO K800-AMT-CD-SERCNTR-NEW                            
           END-IF                                                               
           IF K800-AMT-CD-REFUND  IS NOT NUMERIC                                
              MOVE ZEROES TO K800-AMT-CD-REFUND                                 
           END-IF                                                               
                                                                                
           MOVE  K800-AMT-PREMIUM      TO   KC00-AMT-PREMIUM                    
                                                                                
           IF K800-AMT-CD-SERCNTR-NEW > 0                                       
              MOVE K800-AMT-CD-SERCNTR-NEW                                      
                                       TO   WS-AMT-CD-SERCNTR                   
           ELSE                                                                 
              MOVE K800-AMT-CD-SERCNTR TO   WS-AMT-CD-SERCNTR                   
           END-IF                                                               
                                                                                
           COMPUTE WS-DOWNPAYMENT  = K800-AMT-CD-CASH +                         
                                     K800-AMT-CD-CPF1 +                         
                                     K800-AMT-CD-CPF2 +                         
                                     K800-AMT-CD-CPF3 +                         
                                     K800-AMT-CD-CPF4 +                         
                                     WS-AMT-CD-SERCNTR -                        
                                     K800-AMT-CD-REFUND                         
                                                                                
           MOVE  WS-DOWNPAYMENT          TO   KC00-AMT-DP-MADE.                 
           MOVE  K800-AMT-MAX-LOAN       TO   KC00-OTHER-MAX-LOAN.              
           MOVE  K640-AMT-INTEREST-RATE  TO   KC00-OTHER-INTEREST-RATE.         
           MOVE  K640-AMT-ELIG-INSTAL    TO   KC00-OTHER-MTHLY-INSTALL.         
           MOVE  K640-AMT-REPAY-PERIOD   TO   KC00-OTHER-MAX-REPAY.             
                                                                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4100-RETRIEVE-BP27K860.                                                  
      *================================================================*        
                                                                                
           MOVE    K800-NUM-HLA OF BP13K800-MASTER                              
                                          TO   K860-NUM-HLA                     
           PERFORM 3200-READ-BP27K860     THRU 3200-EXIT                        
                                                                                
           IF K860-AMT-CASH-APPEAL-SOLD  IS NOT NUMERIC                         
              MOVE ZEROES                 TO  K860-AMT-CASH-APPEAL-SOLD         
           END-IF.                                                              
           IF K860-AMT-CASH-USED-SOLD    IS NOT NUMERIC                         
              MOVE ZEROES                 TO  K860-AMT-CASH-USED-SOLD           
           END-IF.                                                              
           IF K800-AMT-CASH-PROCEED      IS NOT NUMERIC                         
              MOVE ZEROES                 TO  K800-AMT-CASH-PROCEED             
           END-IF.                                                              
           IF K860-NUM-CASH-APPEAL-SOLD = 'Y'                                   
              MOVE K860-AMT-CASH-APPEAL-SOLD TO WS-CASH-PROCEED                 
           ELSE                                                                 
              MOVE K860-AMT-CASH-USED-SOLD   TO WS-CASH-PROCEED                 
           END-IF.                                                              
           MOVE WS-CASH-PROCEED          TO  KC00-OTHER-LEAS-PROCEEDS           
                                                                                
           MOVE K800-AMT-CASH-PROCEED    TO  KC00-OTHER-SOC-PROCEEDS            
                                                                                
           IF K800-AMT-CASH-PROCEED > ZEROES                                    
              IF WS-CASH-PROCEED > K800-AMT-CASH-PROCEED                        
                 MOVE K800-AMT-CASH-PROCEED   TO WS-CASH-PROCEED                
              END-IF                                                            
           END-IF                                                               
           IF WS-CASH-PROCEED       = ZEROES AND                                
              K800-AMT-CASH-PROCEED > ZEROES                                    
              MOVE K800-AMT-CASH-PROCEED   TO WS-CASH-PROCEED                   
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4200-RETRIEVE-BM06K110.                                                  
      *================================================================*        
                                                                                
           MOVE    K800-NUM-SCH-ACC       TO   K110-KEY-FLD                     
           PERFORM 3400-READ-BM06K110     THRU 3400-EXIT                        
                                                                                
           IF K800-NUM-UTS-STATUS = 'MU2' OR 'MU3'                              
              PERFORM 4210-COMP-UPG-COST  THRU 4210-EXIT                        
              MOVE WS-AMT-UPG-COST  TO  KC00-AMT-UPGRADE-COST                   
           ELSE                                                                 
              MOVE ZEROES           TO  KC00-AMT-UPGRADE-COST                   
           END-IF.                                                              
                                                                                
           IF K110-CURR-SELLING-PRICE IS NOT NUMERIC                            
              MOVE ZEROES           TO  K110-CURR-SELLING-PRICE                 
           END-IF.                                                              
           IF K110-AMT-SC-SPR-PREMIUM IS NOT NUMERIC                            
              MOVE ZEROES           TO  K110-AMT-SC-SPR-PREMIUM                 
           END-IF.                                                              
           IF K110-AMT-FLAT-DISCOUNT  IS NOT NUMERIC                            
              MOVE ZEROES           TO  K110-AMT-FLAT-DISCOUNT                  
           END-IF.                                                              
                                                                                
           COMPUTE WS-TOT-SELLPR = K110-CURR-SELLING-PRICE -                    
                                   K110-AMT-SC-SPR-PREMIUM                      
           MOVE WS-TOT-SELLPR            TO  KC00-AMT-PURCHASE-PRICE            
           MOVE K110-AMT-SC-SPR-PREMIUM  TO  KC00-AMT-SCSPR-HH-PREM.            
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *<><><><><><><><><><><>*                                                  
       4210-COMP-UPG-COST.                                                      
      *<><><><><><><><><><><>*                                                  
                                                                                
           IF K110-AMT-MUP-COST1 IS NOT NUMERIC                                 
              MOVE ZEROES           TO   K110-AMT-MUP-COST1                     
           END-IF.                                                              
           IF K110-AMT-MUP-COST2 IS NOT NUMERIC                                 
              MOVE ZEROES           TO   K110-AMT-MUP-COST2                     
           END-IF.                                                              
           IF K110-AMT-MUP-COST3 IS NOT NUMERIC                                 
              MOVE ZEROES           TO   K110-AMT-MUP-COST3                     
           END-IF.                                                              
                                                                                
           IF K800-NUM-UTS-STATUS = 'MU2'                                       
              IF K110-AMT-MUP-COST2 > 0                                         
                 COMPUTE WS-AMT-UPG-COST = K110-AMT-MUP-COST2 -                 
                                           K110-AMT-MUP-COST1                   
              END-IF                                                            
           ELSE                                                                 
              IF K110-AMT-MUP-COST3 > 0                                         
                 COMPUTE WS-AMT-UPG-COST = K110-AMT-MUP-COST3 -                 
                                           K110-AMT-MUP-COST1                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-AMT-UPG-COST < 0                                               
              MOVE ZEROES           TO   WS-AMT-UPG-COST                        
           END-IF.                                                              
                                                                                
       4210-EXIT.                                                               
           EXIT.                                                                
      *                                                                         
                                                                                
      *================================================================*        
       4400-RETRIEVE-FEES.                                                      
      *================================================================*        
                                                                                
           INITIALIZE                       WS-LINK-C90E.                       
                                                                                
           MOVE K800-NUM-REGN            TO WS-LINK-NUM-REGN                    
           MOVE K800-AMT-MAX-LOAN        TO WS-LINK-AMT-LOAN                    
           MOVE K110-CURR-SELLING-PRICE  TO WS-LINK-SELLING-PRICE               
           MOVE K800-AMT-PREMIUM         TO WS-LINK-PREMIUM                     
           MOVE K110-AMT-FLAT-DISCOUNT   TO WS-LINK-FLAT-DISCOUNT               
           MOVE K800-NUM-ALLO-SCHEME     TO WS-LINK-ALLO-SCHEME                 
           MOVE K800-NUM-SCH-ACC         TO WS-LINK-SCH-ACC                     
           MOVE K800-NUM-FLAT-TYPE       TO WS-LINK-FLAT-TYPE                   
           MOVE K110-FLAT-STATUS         TO WS-LINK-FLAT-STATUS                 
           MOVE K110-ALLOCN-TYPE         TO WS-LINK-ALLOCN-TYPE                 
           MOVE K110-REPOSSESSED-CODE    TO WS-LINK-REPOSSESSED-CODE            
           MOVE K800-NUM-SCCC-ELGBTY     TO WS-LINK-SCCC-ELGBTY                 
           MOVE SPACES                   TO WS-LINK-CDE-FLAT-ACCEPT             
           PERFORM   4600-GET-SALES-TYPE    THRU 4600-EXIT                      
                                                                                
           CALL 'BP13C90E' USING WS-LINK-C90E.                                  
                                                                                
           IF K800-LSF-PAID-TAG  = 'Y'                                          
              MOVE ZEROES               TO WS-LINK-PREMIUM-STAMP-FEE            
                                            WS-LINK-PREM-CNVY-FEE               
                                            WS-LINK-PREM-CNVY-FEE-GST           
           END-IF.                                                              
                                                                                
           MOVE WS-LINK-LEASE-REGN-FEE    TO KC00-AMT-LEASE-ESCROW-REGN         
           MOVE WS-LINK-MORT-REGN-FEE     TO KC00-AMT-MORT-ESCROW-REGN          
           MOVE WS-LINK-MORT-ST-FEE       TO KC00-AMT-MORT-STAMP-FEE            
           MOVE WS-LINK-SURVEY-FEE        TO KC00-AMT-SURVEY-FEE                
           MOVE WS-LINK-SURVEY-FEE-GST    TO KC00-AMT-SURVEY-FEE-GST            
           MOVE WS-LINK-PREMIUM-STAMP-FEE TO KC00-AMT-SUPL-STAMP-FEE            
           MOVE WS-LINK-PREM-CNVY-FEE     TO KC00-AMT-SUPL-CF                   
           MOVE WS-LINK-PREM-CNVY-FEE-GST TO KC00-AMT-SUPL-CF-GST               
                                                                                
           COMPUTE KC00-AMT-SUPL-CF-TOT = KC00-AMT-SUPL-CF +                    
                                          KC00-AMT-SUPL-CF-GST                  
                                                                                
           COMPUTE KC00-AMT-SURVEY-FEE-TOT = KC00-AMT-SURVEY-FEE +              
                                             KC00-AMT-SURVEY-FEE-GST            
                                                                                
           COMPUTE WS-LSE-ESCR-REGN-FEE   = WS-LINK-LEASE-ESCROW-FEE +          
                                            WS-LINK-LEASE-REGN-FEE.             
           COMPUTE WS-MORT-ESCR-REGN-FEE  = WS-LINK-MORT-ESCROW-FEE +           
                                            WS-LINK-MORT-REGN-FEE.              
           COMPUTE WS-TOT-SUPL-CONVEY-FEE = WS-LINK-PREM-CNVY-FEE +             
                                            WS-LINK-PREM-CNVY-FEE-GST.          
           COMPUTE WS-TOT-SURVEY-FEE      = WS-LINK-SURVEY-FEE +                
                                            WS-LINK-SURVEY-FEE-GST.             
                                                                                
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-RETRIEVE-OTHERS.                                                    
      *================================================================*        
                                                                                
           MOVE FCPF-AMT-CPF-OA           TO  WS-TOT-CPF-AMT                    
                                              KC00-AMT-COMBINE-CPF              
                                              KC00-APPLN-CPF1.                  
                                                                                
           MOVE ZEROES                    TO  KC00-APPLN-CPF2                   
                                              KC00-APPLN-CPF3                   
                                              KC00-APPLN-CPF4.                  
                                                                                
                                                                                
           COMPUTE WS-TOT-PREM-UPGCOST = K110-AMT-SC-SPR-PREMIUM +              
                                         K800-AMT-PREMIUM  +                    
                                         WS-AMT-UPG-COST                        
                                                                                
           COMPUTE WS-TOT-STAMP-FEES  = WS-LINK-PREMIUM-STAMP-FEE +             
                                        WS-LSE-ESCR-REGN-FEE      +             
                                        WS-MORT-ESCR-REGN-FEE     +             
                                        WS-LINK-MORT-ST-FEE                     
           COMPUTE WS-TOT-FEES        = WS-TOT-STAMP-FEES         +             
                                        WS-TOT-SUPL-CONVEY-FEE    +             
                                        WS-TOT-SURVEY-FEE                       
                                                                                
           COMPUTE WS-TOT-AMT-TP = K110-CURR-SELLING-PRICE +                    
                                   WS-TOT-PREM-UPGCOST +                        
                                   WS-TOT-FEES -                                
                                   K110-AMT-SC-SPR-PREMIUM                      
           COMPUTE WS-COMM-MORT-FEE = WS-LINK-MORT-ST-FEE   +                   
                                      WS-LINK-MORT-REGN-FEE                     
                                                                                
           COMPUTE WS-COMM-AMT-LEFT = WS-TOT-AMT-TP -                           
                                      WS-DOWNPAYMENT  -                         
                                      WS-TOT-CPF-AMT                            
           MOVE WS-COMM-AMT-LEFT TO WS-AMT-LEFT-SAVE                            
           MOVE WS-TOT-AMT-TP    TO WS-COMM-SELLPR-FEES                         
           MOVE WS-TOT-FEES      TO WS-COMM-TOT-FEES                            
           PERFORM 4510-COMPUTE-AMT   THRU 4510-EXIT                            
                                                                                
           COMPUTE WS-TOTAL-1 = WS-TOT-PREM-UPGCOST                             
           COMPUTE WS-TOTAL-2 = WS-TOT-FEES                                     
           COMPUTE WS-TOTAL-3 = WS-TOT-AMT-TP                                   
                                                                                
           MOVE WS-TOTAL-1        TO   KC00-AMT-ADD-TOTAL1                      
           MOVE WS-TOTAL-2        TO   KC00-AMT-ADD-TOTAL2                      
           MOVE WS-TOTAL-3        TO   KC00-AMT-ADD-TOTAL3.                     
           IF WS-MORT-TAG = 'Y'                                                 
              MOVE WS-NEW-TOT-FEES   TO   KC00-AMT-ADD-TOTAL2                   
              MOVE WS-NEW-SELLPR-FEES TO  KC00-AMT-ADD-TOTAL3                   
              MOVE  ZEROES           TO   KC00-AMT-MORT-ESCROW-REGN             
                                          KC00-AMT-MORT-STAMP-FEE               
           END-IF.                                                              
                                                                                
           IF KC00-AMT-LOAN-DISBURSED > 0                                       
              IF (KC00-AMT-MORT-ESCROW-REGN = 0) OR                             
                 (KC00-AMT-MORT-STAMP-FEE   = 0)                                
                 DISPLAY 'INVALID MORTGAGE FEES! '                              
                         'MORT-ESCROW : ' KC00-AMT-MORT-ESCROW-REGN             
                         'MORT-STAMP  : ' KC00-AMT-MORT-STAMP-FEE               
                 PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-CUR-DATE       TO   KC00-UPDATE-DTE                          
           MOVE WS-TIME           TO   KC00-UPDATE-TME                          
           MOVE 'BP13C3A7'        TO   KC00-USERID-UPDATE.                      
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=========================================================*               
       4510-COMPUTE-AMT.                                                        
      *=========================================================*               
                                                                                
           MOVE SPACES            TO WS-MORT-TAG.                               
                                                                                
           IF WS-COMM-AMT-LEFT  <= ZEROES                                       
              MOVE  ZEROES        TO WS-SHORTFALL-TOT                           
                                     WS-CASH-PROCEED                            
                                     KC00-AMT-SHORTFALL                         
                                     KC00-AMT-LOAN-DISBURSED                    
                                     KC00-AMT-CASH-PROCEEDS                     
              MOVE  'Y'            TO WS-MORT-TAG                               
           ELSE                                                                 
              COMPUTE WS-COMM-AMT-LEFT-A = WS-COMM-AMT-LEFT -                   
                                         WS-CASH-PROCEED                        
              IF WS-COMM-AMT-LEFT-A  <= ZEROES                                  
                 MOVE  ZEROES        TO WS-SHORTFALL-TOT                        
                                        KC00-AMT-LOAN-DISBURSED                 
                                        KC00-AMT-SHORTFALL                      
                                                                                
                 MOVE  WS-COMM-AMT-LEFT    TO  WS-AMT-NO-SIGN                   
                                             WS-CASH-PROCEED                    
                 MOVE  WS-AMT-NO-SIGN    TO  KC00-AMT-CASH-PROCEEDS             
                 MOVE  'Y'            TO WS-MORT-TAG                            
              ELSE                                                              
                 COMPUTE WS-COMM-AMT-LEFT-B = WS-COMM-AMT-LEFT-A -              
                                            K800-AMT-MAX-LOAN                   
                 IF WS-COMM-AMT-LEFT-B  <= ZEROES                               
                    MOVE  ZEROES   TO WS-SHORTFALL-TOT                          
                                      KC00-AMT-SHORTFALL                        
                    MOVE  WS-CASH-PROCEED   TO  KC00-AMT-CASH-PROCEEDS          
                                                                                
                    MOVE  WS-COMM-AMT-LEFT-A  TO  WS-AMT-NO-SIGN                
                    MOVE  WS-AMT-NO-SIGN    TO  KC00-AMT-LOAN-DISBURSED         
                 ELSE                                                           
                    MOVE WS-COMM-AMT-LEFT-B TO  KC00-AMT-SHORTFALL              
                                                WS-SHORTFALL-TOT                
                    MOVE K800-AMT-MAX-LOAN  TO  KC00-AMT-LOAN-DISBURSED         
                    MOVE  WS-CASH-PROCEED   TO  KC00-AMT-CASH-PROCEEDS          
              END-IF                                                            
           END-IF                                                               
           COMPUTE WS-TOT-PAY-CASH = WS-CASH-PROCEED + WS-SHORTFALL-TOT         
           MOVE WS-TOT-PAY-CASH  TO KC00-AMT-TOTAL-PAY-CASH.                    
           IF WS-MORT-TAG = 'Y'                                                 
              PERFORM 4520-2ND-COMPUTE THRU 4520-EXIT                           
           END-IF.                                                              
                                                                                
       4510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       4520-2ND-COMPUTE.                                                        
      *=========================================================*               
            MOVE    WS-AMT-LEFT-SAVE   TO   WS-COMM-AMT-LEFT                    
            COMPUTE WS-NEW-TOT-FEES     =   WS-COMM-TOT-FEES -                  
                                            WS-COMM-MORT-FEE                    
            COMPUTE WS-NEW-SELLPR-FEES  =   WS-COMM-SELLPR-FEES -               
                                            WS-COMM-MORT-FEE                    
            COMPUTE WS-COMM-AMT-LEFT    =   WS-COMM-AMT-LEFT -                  
                                            WS-COMM-MORT-FEE                    
                                                                                
           IF WS-COMM-AMT-LEFT  <= ZEROES                                       
              MOVE  ZEROES        TO WS-SHORTFALL-TOT                           
                                     WS-CASH-PROCEED                            
                                     KC00-AMT-SHORTFALL                         
                                     KC00-AMT-LOAN-DISBURSED                    
                                     KC00-AMT-CASH-PROCEEDS                     
           ELSE                                                                 
              COMPUTE WS-COMM-AMT-LEFT-A = WS-COMM-AMT-LEFT -                   
                                         WS-CASH-PROCEED                        
              IF WS-COMM-AMT-LEFT-A  <= ZEROES                                  
                 MOVE  ZEROES        TO WS-SHORTFALL-TOT                        
                                        KC00-AMT-LOAN-DISBURSED                 
                                        KC00-AMT-SHORTFALL                      
                                                                                
                 MOVE  WS-COMM-AMT-LEFT    TO  WS-AMT-NO-SIGN                   
                                             WS-CASH-PROCEED                    
                 MOVE  WS-AMT-NO-SIGN    TO  KC00-AMT-CASH-PROCEEDS             
              ELSE                                                              
                 COMPUTE WS-COMM-AMT-LEFT-B = WS-COMM-AMT-LEFT-A -              
                                            K800-AMT-MAX-LOAN                   
                 IF WS-COMM-AMT-LEFT-B  <= ZEROES                               
                    MOVE  ZEROES   TO WS-SHORTFALL-TOT                          
                                      KC00-AMT-SHORTFALL                        
                    MOVE  WS-CASH-PROCEED   TO  KC00-AMT-CASH-PROCEEDS          
                                                                                
                    MOVE  WS-COMM-AMT-LEFT-A  TO  WS-AMT-NO-SIGN                
                    MOVE  WS-AMT-NO-SIGN    TO  KC00-AMT-LOAN-DISBURSED         
                 ELSE                                                           
                    MOVE WS-COMM-AMT-LEFT-B TO  KC00-AMT-SHORTFALL              
                                                WS-SHORTFALL-TOT                
                    MOVE K800-AMT-MAX-LOAN  TO  KC00-AMT-LOAN-DISBURSED         
                    MOVE  WS-CASH-PROCEED   TO  KC00-AMT-CASH-PROCEEDS          
              END-IF                                                            
           END-IF                                                               
           COMPUTE WS-TOT-PAY-CASH = WS-CASH-PROCEED + WS-SHORTFALL-TOT         
           MOVE WS-TOT-PAY-CASH  TO KC00-AMT-TOTAL-PAY-CASH.                    
                                                                                
       4520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4600-GET-SALES-TYPE.                                                     
      *================================================================*        
                                                                                
           IF K800-NUM-ALLO-CAT = 'RT'                                          
              MOVE '02'  TO   WS-LINK-SALES-TYPE                                
           ELSE                                                                 
              PERFORM 4650-DETR-SALES-TYPE THRU 4650-EXIT                       
           END-IF.                                                              
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       4650-DETR-SALES-TYPE.                                                    
      *----------------------------------------------------------------*        
           EVALUATE WS-LINK-FLAT-STATUS ALSO WS-LINK-ALLOCN-TYPE                
              WHEN 'NW'  ALSO  'I'                                              
                 MOVE '10'            TO   WS-LINK-SALES-TYPE                   
                                                                                
              WHEN 'RP'  ALSO  'I'                                              
                 IF WS-LINK-REPOSSESSED-CODE = '40'                             
                    MOVE '3A'         TO   WS-LINK-SALES-TYPE                   
                 ELSE                                                           
                    MOVE '03'         TO   WS-LINK-SALES-TYPE                   
                 END-IF                                                         
                                                                                
              WHEN 'RP'  ALSO  'S'                                              
                 IF WS-LINK-REPOSSESSED-CODE = '40'                             
                    MOVE '3A'         TO   WS-LINK-SALES-TYPE                   
                 ELSE                                                           
                    MOVE '03'         TO   WS-LINK-SALES-TYPE                   
                 END-IF                                                         
                                                                                
              WHEN 'ST'  ALSO  'S'                                              
                 IF WS-LINK-REPOSSESSED-CODE = '00' OR '01' OR '02'             
                    MOVE '01'         TO   WS-LINK-SALES-TYPE                   
                 ELSE                                                           
                    IF WS-LINK-REPOSSESSED-CODE = '40'                          
                       MOVE '3A'      TO   WS-LINK-SALES-TYPE                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
              WHEN 'ST'  ALSO  'I'                                              
                 IF WS-LINK-REPOSSESSED-CODE = '00' OR '01'                     
                    MOVE '10'         TO   WS-LINK-SALES-TYPE                   
                 ELSE                                                           
                    IF WS-LINK-REPOSSESSED-CODE = '40'                          
                       MOVE '3A'      TO   WS-LINK-SALES-TYPE                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
              WHEN 'SD'  ALSO  'S'                                              
                 MOVE  '2B'           TO   WS-LINK-SALES-TYPE                   
                                                                                
              WHEN 'CV'  ALSO  'S'                                              
                 MOVE  '01'           TO   WS-LINK-SALES-TYPE                   
                                                                                
              WHEN 'CV'  ALSO  'I'                                              
                 MOVE  '10'           TO   WS-LINK-SALES-TYPE                   
                                                                                
              WHEN OTHER                                                        
                 MOVE  '  '           TO   WS-LINK-SALES-TYPE                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       5000-CREATE-BP13KC00.                                                    
      *================================================================*        
                                                                                
           IF WS-WRITE-KC00 = 'Y'                                               
              PERFORM 5100-WRITE-BP13KC00 THRU 5100-EXIT                        
              ADD 1 TO WS-KC00-WRITE                                            
           ELSE                                                                 
              IF WS-REWRITE-KC00 = 'Y'                                          
                 PERFORM 5200-REWRITE-BP13KC00 THRU 5200-EXIT                   
                 ADD 1 TO WS-KC00-REWRITE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       5100-WRITE-BP13KC00.                                                     
      *================================================================*        
                                                                                
           WRITE BP13KC00-REC                                                   
                                                                                
           IF WS-KC00-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  WS-KC00-STATUS TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13KC00 FILE : ' WS-KC00-STATUS           
              DISPLAY 'KC00-NUM-REGN = ' KC00-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5200-REWRITE-BP13KC00.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13KC00-REC                                                 
                                                                                
           IF WS-KC00-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  WS-KC00-STATUS TO RETURN-CODE                               
              DISPLAY 'ERROR REWRITING BP13KC00 FILE : ' WS-KC00-STATUS         
              DISPLAY 'KC00-NUM-REGN = ' KC00-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5300-DELETE-BP13K80A.                                                    
      *================================================================*        
                                                                                
           DELETE BP13K80A                                                      
                                                                                
           IF WS-K80A-STATUS = 00                                               
              ADD 1 TO WS-K80A-DELETE                                           
           ELSE                                                                 
              MOVE  WS-K80A-STATUS TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13K80A FILE : ' WS-K80A-STATUS           
              DISPLAY 'K80A-NUM-REGN = ' WS-K80A-REGN                           
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'TOTAL BP13FCPF READ        : ' WS-FCPF-READ                 
           DISPLAY 'TOTAL BP13F800 READ        : ' WS-F800-READ                 
           DISPLAY 'TOTAL BP13FCPF MATCHED     : ' FCPF-MATCHED                 
           DISPLAY 'TOTAL BP13FCPF NO MATCH    : ' FCPF-NOMATCH                 
           DISPLAY 'TOTAL BP13F800 NO MATCH    : ' F800-NOMATCH                 
           DISPLAY 'TOTAL BP13FOUT WRITTEN     : ' WS-FOUT-WRITE                
           DISPLAY 'TOTAL WRITE TO BP13KC00    : ' WS-KC00-WRITE.               
           DISPLAY 'TOTAL REWRITE TO BP13KC00  : ' WS-KC00-REWRITE.             
           DISPLAY 'TOTAL DELETE  TO BP13K80A  : ' WS-K80A-DELETE.              
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13K800                                                       
                 BP13K80A                                                       
                 BP13K820                                                       
                 BP27K860                                                       
                 BP13K640                                                       
                 BP13FCPF                                                       
                 BP13F800                                                       
                 BP13FOUT                                                       
                 BM06K110                                                       
                 BP13KC00                                                       
                 BP27K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
           IF WS-K80A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K80A ERROR ' WS-K80A-STATUS                  
              MOVE     WS-K80A-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K640-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K640 ERROR ' WS-K640-STATUS                  
              MOVE     WS-K640-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K860-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP27K860 ERROR ' WS-K860-STATUS                  
              MOVE     WS-K860-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-KC00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC00 ERROR ' WS-KC00-STATUS                  
              MOVE     WS-KC00-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-BP27K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'CLOSING BP27K800 ERROR ' WS-BP27K800-STATUS              
              MOVE     WS-BP27K800-STATUS  TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
