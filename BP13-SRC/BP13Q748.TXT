       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13Q748.                                                    
       AUTHOR.     SURESH.                                                      
       INSTALLATION. HDB.                                                       
       DATE-WRITTEN. 20-02-1999.                                                
      *-----------------------------------------------------------*             
      *        SOC  - BP13                                        *             
      *-----------------------------------------------------------*             
      * OBJECTIVES :  TO RETREIVE APPLICANTS RECS USING NRIC      *             
      *               (RETRIEVES ONLY APPLICANTS DETAILS AND NOT  *             
      *               OF FAMILY MEMBERS)                          *             
      *                                                           *             
      * INPUT FILES   : BP13F716                                  *             
      *                 AB03K030                                  *             
      *                 BP13K060                                  *             
      *                                                           *             
      * DATABASE      : RDB2                                      *             
      *                                                           *             
      * OUTPUT FILE   : P13F716A                                  *             
      *                                                           *             
      * REQ NO.   DATE       CHG BY  DESCRIPTION                  *             
      * -------   ----       ------  -----------                  *             
      * BP130630 10-02-1999   SRN    NEW PROGRAM                  *             
      * BP130825 03-05-2000   SRN    TO INCLUDE MARITAL-STATUS    *             
      * BP132421 15-10-2003   MJB    TO INCLUDE RENTAL ALLOC-SCH  *             
      *          28-10-2003   MJB    GET NUM_ALCTN_SCHEME AS      *             
      *                              NUM_HSNG_SCHEME FROM RNTL TBL*             
      *                                                           *             
      *-----------------------------------------------------------*             
       ENVIRONMENT DIVISION.                                                    
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT P13F716A   ASSIGN TO P13F716A.                                
                                                                                
           SELECT AB03K030   ASSIGN TO AB03K030                                 
                             ORGANIZATION INDEXED                               
                             ACCESS MODE  RANDOM                                
                             RECORD KEY   K030-NUM-HDB-REF                      
                             FILE STATUS  WS-K030-STATUS.                       
                                                                                
           SELECT BP13K060   ASSIGN TO BP13K060                                 
                             ORGANIZATION INDEXED                               
                             ACCESS MODE  RANDOM                                
                             RECORD KEY   K060-KEY-FLD                          
                             FILE STATUS  WS-K060-STATUS.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13F716.                                                             
       COPY BP13F716.                                                           
                                                                                
       FD P13F716A.                                                             
       01 P13F716A-REC                       PIC X(350).                        
                                                                                
       FD AB03K030.                                                             
       COPY AB03K030.                                                           
                                                                                
       FD BP13K060.                                                             
       COPY BP13K060.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
           EXEC SQL INCLUDE P13RSROL  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13IDNO   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13CSID   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13RSDNT  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13RNTL   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13PERSN  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE SQLCA     END-EXEC.                                 
                                                                                
           EXEC SQL                                                             
             DECLARE CURSOR1 CURSOR FOR                                         
               SELECT T1.NUM_IDNTY_NO,                                          
                      T1.NUM_IDNTY_NO_TYPE,                                     
                      T3.NUM_ROLE,                                              
                      T3.NUM_SCHEME,                                            
                      T3.NUM_ACNT,                                              
                      T3.NUM_TNCY,                                              
                      T3.DTE_ROLE_START,                                        
                      T4.NUM_RLTNSP,                                            
                      T5.NUM_SEX,                                               
                      T5.NUM_MRTL_STATUS                                        
               FROM                                                             
                 IDNTY_NO       T1,                                             
                 CSTMR_ID       T2,                                             
                 RSDNT_ROLE     T3,                                             
                 RSDNT          T4,                                             
                 PERSONS        T5                                              
               WHERE                                                            
                 T4.NUM_SCHEME        = T3.NUM_SCHEME                AND        
                 T4.NUM_ACNT          = T3.NUM_ACNT                  AND        
                 T4.NUM_TNCY          = T3.NUM_TNCY                  AND        
                 T4.NUM_CSTMR_SOURCE  = T3.NUM_CSTMR_SOURCE          AND        
                 T3.NUM_CSTMR_SOURCE  = T2.NUM_CSTMR_SOURCE          AND        
                 T3.DTE_ROLE_END      = '0001-01-01'                 AND        
                 T5.NUM_CSTMR         = T1.NUM_CSTMR                 AND        
                 T2.NUM_CSTMR         = T1.NUM_CSTMR                 AND        
                 T1.NUM_IDNTY_NO      = :DCLIDNTY-NO.NUM-IDNTY-NO    AND        
                 T1.NUM_IDNTY_NO_TYPE = :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE          
           END-EXEC                                                             
                                                                                
      *--------------------------------------------------------------*          
      * COMMAREA FOR CALLING AV02C013 FOR FINDING NRIC NUMBER TYPE   *          
      *--------------------------------------------------------------*          
       01  WS-COM-AREA.                                                         
           05  WS-COM-NRIC             PIC X(9)    VALUE SPACES.                
           05  WS-COM-RFLAG            PIC X(1)    VALUE SPACES.                
           05  WS-COM-NUMBER-TYPE      PIC X(3)    VALUE SPACES.                
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-F716-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-INVALID-UINFIN       PIC 9(8)    VALUE ZEROS.                 
           05  WS-RDB2-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RDB2-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K060-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K060-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K030-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K030-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RNTL-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RNTL-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-F716-WRITE           PIC 9(8)    VALUE ZEROS.                 
                                                                                
       01 WS-FILE-STATUS.                                                       
          05  WS-EOF-F716              PIC X       VALUE 'N'.                   
              88 EOF-F716                          VALUE 'Y'.                   
          05  WS-K060-STATUS           PIC 9(2)    VALUE ZEROS.                 
          05  WS-K030-STATUS           PIC 9(2)    VALUE ZEROS.                 
                                                                                
          05  WS-EOC                   PIC X(1)    VALUE 'N'.                   
              88 END-OF-CURSOR                     VALUE 'Y'.                   
                                                                                
       01  WS-SQLCODE                  PIC 9(4)    VALUE ZEROS.                 
                                                                                
       01  WS-SYS-DATE                 PIC X(8)    VALUE SPACES.                
       01  WS-PRT-DATE                 PIC X(10)   VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN.                                                               
                                                                                
            EXEC SQL WHENEVER SQLERROR                                          
                              GO TO 9900-ABORT-PROCESS                          
            END-EXEC.                                                           
                                                                                
            EXEC SQL WHENEVER SQLWARNING                                        
                              GO TO 9900-ABORT-PROCESS                          
            END-EXEC.                                                           
                                                                                
            PERFORM 0500-OPEN-FILES        THRU 0500-EXIT.                      
                                                                                
            PERFORM 1000-READ-BP13F716     THRU 1000-EXIT.                      
                                                                                
            PERFORM 2000-RETRIEVE-RDBINFO  THRU 2000-EXIT                       
                    UNTIL EOF-F716.                                             
                                                                                
            PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                      
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       0500-OPEN-FILES.                                                         
            OPEN INPUT  BP13F716                                                
                        BP13K060                                                
                        AB03K030                                                
                 OUTPUT P13F716A.                                               
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-SYS-DATE.                   
                                                                                
            IF WS-K060-STATUS NOT = 0 AND 97                                    
               DISPLAY 'ERROR OPENING BP13K060 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                         
               MOVE WS-K060-STATUS            TO RETURN-CODE                    
               GO TO 9000-CLOSE-FILES                                           
            END-IF.                                                             
                                                                                
            IF WS-K030-STATUS NOT = 0 AND 97                                    
               DISPLAY 'ERROR OPENING AB03K030 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K030-STATUS                         
               MOVE WS-K030-STATUS            TO RETURN-CODE                    
               GO TO 9000-CLOSE-FILES                                           
            END-IF.                                                             
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-READ-BP13F716.                                                      
            READ BP13F716 AT END MOVE 'Y'     TO WS-EOF-F716                    
                 GO TO 1000-EXIT.                                               
                                                                                
            ADD 1 TO WS-F716-READ.                                              
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-RETRIEVE-RDBINFO.                                                   
                                                                                
            MOVE SPACES                  TO WS-COM-AREA.                        
            MOVE F716-NUM-NRIC           TO   WS-COM-NRIC                       
                                                                                
            IF WS-COM-NRIC(1:1) = 'X'                                           
               MOVE 'XIN'                TO WS-COM-NUMBER-TYPE                  
               MOVE 'Y'                  TO WS-COM-RFLAG                        
            ELSE                                                                
               CALL 'AV02C013'           USING WS-COM-NRIC,                     
                                               WS-COM-RFLAG,                    
                                               WS-COM-NUMBER-TYPE               
            END-IF.                                                             
                                                                                
            IF WS-COM-RFLAG = 'Y'                                               
               PERFORM 3000-READ-RDB     THRU 3000-EXIT                         
            ELSE                                                                
               ADD 1                     TO WS-INVALID-UINFIN                   
            END-IF.                                                             
                                                                                
            PERFORM 1000-READ-BP13F716   THRU 1000-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-READ-RDB.                                                           
                                                                                
            MOVE 'N'                     TO WS-EOC.                             
            MOVE WS-COM-NUMBER-TYPE      TO NUM-IDNTY-NO-TYPE                   
                                                                                
            IF WS-COM-NUMBER-TYPE = 'XIN'                                       
               MOVE F716-NUM-NRIC(2:8)   TO                                     
                                       NUM-IDNTY-NO OF DCLIDNTY-NO              
            ELSE                                                                
               MOVE F716-NUM-NRIC        TO                                     
                                       NUM-IDNTY-NO OF DCLIDNTY-NO              
            END-IF.                                                             
                                                                                
            PERFORM 3100-OPEN-CURSOR     THRU 3100-EXIT                         
                                                                                
            PERFORM 3200-FETCH-CURSOR    THRU 3200-EXIT                         
                                                                                
            IF END-OF-CURSOR                                                    
               ADD 1    TO WS-RDB2-NFND                                         
               DISPLAY 'RECORD NOT FOUND IN RDBII'                              
               DISPLAY 'IDENTITY NO :- ' F716-NUM-NRIC                          
            END-IF.                                                             
                                                                                
            PERFORM 3200-FETCH-CURSOR      THRU 3200-EXIT                       
                    UNTIL END-OF-CURSOR.                                        
                                                                                
            PERFORM 3300-CLOSE-CURSOR      THRU 3300-EXIT.                      
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3100-OPEN-CURSOR.                                                        
            EXEC SQL                                                            
              OPEN CURSOR1                                                      
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING RDBII'                                
                   DISPLAY 'SQLCODE :- ' SQLCODE                                
                   MOVE SQLCODE          TO WS-SQLCODE                          
                   MOVE WS-SQLCODE       TO RETURN-CODE                         
                   GO TO 9000-CLOSE-FILES                                       
            END-EVALUATE.                                                       
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3200-FETCH-CURSOR.                                                       
            EXEC SQL                                                            
              FETCH CURSOR1                                                     
                INTO                                                            
                  :DCLIDNTY-NO.NUM-IDNTY-NO,                                    
                  :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE,                               
                  :DCLRSDNT-ROLE.NUM-ROLE,                                      
                  :DCLRSDNT-ROLE.NUM-SCHEME,                                    
                  :DCLRSDNT-ROLE.NUM-ACNT,                                      
                  :DCLRSDNT-ROLE.NUM-TNCY,                                      
                  :DCLRSDNT-ROLE.DTE-ROLE-START,                                
                  :DCLRSDNT.NUM-RLTNSP,                                         
                  :DCLPERSONS.NUM-SEX,                                          
                  :DCLPERSONS.NUM-MRTL-STATUS                                   
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                 ADD 1    TO WS-RDB2-READ                                       
                 PERFORM 4000-WRITE-P13F716A  THRU 4000-EXIT                    
              WHEN 100                                                          
                 MOVE 'Y'               TO WS-EOC                               
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING RDBII'                                  
                 DISPLAY 'SQLCODE :- ' SQLCODE                                  
                 MOVE SQLCODE          TO WS-SQLCODE                            
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3300-CLOSE-CURSOR.                                                       
            EXEC SQL                                                            
              CLOSE CURSOR1                                                     
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                CONTINUE                                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING RDBII'                                  
                 DISPLAY 'SQLCODE :- ' SQLCODE                                  
                 MOVE SQLCODE          TO WS-SQLCODE                            
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-WRITE-P13F716A.                                                     
                                                                                
            STRING NUM-SCHEME OF DCLRSDNT-ROLE,                                 
                   NUM-ACNT   OF DCLRSDNT-ROLE,                                 
                   NUM-TNCY   OF DCLRSDNT-ROLE DELIMITED BY SIZE                
                   INTO                                                         
                   F716-NUM-HDB-REF                                             
                                                                                
            IF F716-NUM-HDB-REF > '6999999999'                                  
               MOVE F716-NUM-HDB-REF              TO K030-NUM-HDB-REF           
               PERFORM 5000-READ-AB03K030         THRU 5000-EXIT                
            ELSE                                                                
               PERFORM 5500-READ-RNTL             THRU 5500-EXIT                
            END-IF.                                                             
                                                                                
                                                                                
            IF DTE-ROLE-START OF DCLRSDNT-ROLE = '0001-01-01'                   
               MOVE SPACES                        TO F716-DTE-MOVING-IN         
            ELSE                                                                
               STRING DTE-ROLE-START OF DCLRSDNT-ROLE(1:4)                      
                                     DELIMITED BY SIZE,                         
                      DTE-ROLE-START OF DCLRSDNT-ROLE(6:2)                      
                                     DELIMITED BY SIZE,                         
                      DTE-ROLE-START OF DCLRSDNT-ROLE(9:2)                      
                                     DELIMITED BY SIZE,                         
                      INTO                                                      
                      F716-DTE-MOVING-IN                                        
            END-IF.                                                             
                                                                                
            MOVE NUM-ROLE  OF DCLRSDNT-ROLE        TO F716-NUM-ROLE             
                                                                                
            MOVE SPACES                            TO BP13K060-REC              
            MOVE '50'                              TO K060-SERIAL-NO            
            MOVE NUM-ROLE  OF DCLRSDNT-ROLE        TO K060-CODE                 
            PERFORM 4500-READ-BP13K060             THRU 4500-EXIT               
            MOVE K060-DESC1(1:1)                   TO F716-CDE-OCCUPIER         
                                                                                
            MOVE SPACES                            TO BP13K060-REC              
            MOVE '49'                              TO K060-SERIAL-NO            
                                                                                
            IF NUM-RLTNSP = '01' OR '21'                                        
               STRING NUM-RLTNSP, NUM-SEX                                       
                      DELIMITED BY SIZE                                         
                      INTO                                                      
                      K060-CODE                                                 
            ELSE                                                                
               MOVE NUM-RLTNSP OF DCLRSDNT         TO K060-CODE                 
            END-IF.                                                             
                                                                                
            PERFORM 4500-READ-BP13K060             THRU 4500-EXIT               
            MOVE K060-DESC1(1:2)                   TO                           
                                                  F716-CDE-RELATIONSHIP         
                                                                                
            EVALUATE NUM-MRTL-STATUS                                            
              WHEN 'S'                                                          
                MOVE '1'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'M'                                                          
                MOVE '2'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'W'                                                          
                MOVE '3'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'D'                                                          
                MOVE '4'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'P'                                                          
                MOVE '5'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
            END-EVALUATE.                                                       
            WRITE P13F716A-REC  FROM BP13F716-REC.                              
            ADD 1   TO WS-F716-WRITE.                                           
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4500-READ-BP13K060.                                                      
            READ BP13K060.                                                      
                                                                                
            EVALUATE WS-K060-STATUS                                             
              WHEN 0                                                            
                ADD 1                    TO WS-K060-READ                        
              WHEN 23                                                           
                MOVE SPACES              TO K060-DESC1                          
                ADD 1                    TO WS-K060-NFND                        
                DISPLAY 'ERROR!! RECORD NOT FOUND IN BP13K060'                  
                DISPLAY 'K060-KEY-FLD :- ' K060-KEY-FLD                         
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP17K060'                                
                DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                        
                MOVE WS-K060-STATUS         TO RETURN-CODE                      
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5000-READ-AB03K030.                                                      
            READ AB03K030.                                                      
                                                                                
            EVALUATE WS-K030-STATUS                                             
              WHEN 0                                                            
                ADD 1                           TO WS-K030-READ                 
                                                                                
                IF K030-DTE-EXE-SALE-AGRMT = SPACES OR LOW-VALUES               
                   MOVE K030-DTE-SALE-EFF       TO F716-DTE-ACCEPTED            
                ELSE                                                            
                   MOVE K030-DTE-EXE-SALE-AGRMT TO F716-DTE-ACCEPTED            
                END-IF                                                          
                                                                                
              WHEN 23                                                           
                ADD 1                           TO WS-K030-NFND                 
                MOVE SPACES                     TO F716-DTE-ACCEPTED            
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING AB03K030'                                
                DISPLAY 'FILE-STATUS :- ' WS-K030-STATUS                        
                MOVE WS-K030-STATUS          TO RETURN-CODE                     
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5500-READ-RNTL.                                                          
            MOVE NUM-SCHEME OF DCLRSDNT-ROLE     TO                             
                                            NUM-SCHEME OF DCLRNTL               
            MOVE NUM-ACNT   OF DCLRSDNT-ROLE     TO                             
                                            NUM-ACNT OF DCLRNTL                 
            MOVE NUM-TNCY   OF DCLRSDNT-ROLE     TO                             
                                            NUM-TNCY OF DCLRNTL                 
                                                                                
            EXEC SQL                                                            
                 SELECT                                                         
                    DTE_CMCMNT,                                                 
                    NUM_HSNG_SCHEME                                             
                 INTO                                                           
                    :DTE-CMCMNT,                                                
                    :NUM-HSNG-SCHEME                                            
                 FROM                                                           
                    RNTL                                                        
                 WHERE                                                          
                    NUM_SCHEME = :DCLRNTL.NUM-SCHEME   AND                      
                    NUM_ACNT   = :DCLRNTL.NUM-ACNT     AND                      
                    NUM_TNCY   = :DCLRNTL.NUM-TNCY                              
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
               WHEN 0                                                           
                 IF DTE-CMCMNT = '0001-01-01'                                   
                    MOVE SPACES           TO F716-DTE-ACCEPTED                  
                 ELSE                                                           
                    STRING DTE-CMCMNT(1:4),                                     
                           DTE-CMCMNT(6:2),                                     
                           DTE-CMCMNT(9:2)                                      
                           DELIMITED BY SIZE                                    
                           INTO                                                 
                           F716-DTE-ACCEPTED                                    
                 END-IF                                                         
                                                                                
                 MOVE NUM-HSNG-SCHEME  OF DCLRNTL  TO                           
                                          F716-NUM-HSNG-SCH-RENT                
                 ADD 1                    TO WS-RNTL-READ                       
               WHEN 100                                                         
                 ADD 1                    TO WS-RNTL-NFND                       
                 MOVE SPACES              TO F716-DTE-ACCEPTED                  
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING RNTL '                                  
                 DISPLAY 'SQLCODE : ' SQLCODE                                   
                 MOVE SQLCODE             TO WS-SQLCODE                         
                 MOVE WS-SQLCODE          TO RETURN-CODE                        
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSE-FILES.                                                        
            STRING WS-SYS-DATE(7:2) DELIMITED BY SIZE,                          
                   '-'                                                          
                   WS-SYS-DATE(5:2) DELIMITED BY SIZE,                          
                   '-'                                                          
                   WS-SYS-DATE(1:4) DELIMITED BY SIZE                           
                   INTO                                                         
                   WS-PRT-DATE.                                                 
                                                                                
            DISPLAY '   '.                                                      
            DISPLAY 'SUMMARY REPORT   BP13Q748   RUNDATE ' WS-PRT-DATE          
            DISPLAY '______________________________________________'.           
            DISPLAY '   '.                                                      
            DISPLAY 'NO OF RECORDS READ FROM BP13F716         : '               
                                                      WS-F716-READ              
            DISPLAY 'NO OF RECORDS WITH INVALID UINFIN        : '               
                                                      WS-INVALID-UINFIN         
            DISPLAY 'NO OF RECORDS READ FROM RDB II           : '               
                                                      WS-RDB2-READ              
            DISPLAY 'NO OF RECORDS NOT FOUND IN RDB II        : '               
                                                      WS-RDB2-NFND              
            DISPLAY 'NO OF RECORDS READ FROM AB03K030 (SMS)   : '               
                                                      WS-K030-READ              
            DISPLAY 'NO OF RECORDS NOT FOUND IN AB03K030      : '               
                                                      WS-K030-NFND              
            DISPLAY "NO OF RECORDS READ FROM RENTAL D'BASE    : "               
                                                      WS-RNTL-READ              
            DISPLAY "NO OF RECORDS NOT FOUND IN RENTAL D'BASE : "               
                                                      WS-RNTL-NFND              
            DISPLAY 'NO OF RECORDS READ FROM BP13K060         : '               
                                                      WS-K060-READ              
            DISPLAY 'NO OF RECORDS NOT FOUND IN BP13K060      : '               
                                                      WS-K060-NFND              
            DISPLAY 'NO OF RECORDS WRITTEN TO P13F716A        : '               
                                                      WS-F716-WRITE             
                                                                                
            CLOSE BP13F716                                                      
                  BP13K060                                                      
                  AB03K030                                                      
                  P13F716A.                                                     
                                                                                
            IF WS-K060-STATUS NOT = 0                                           
               DISPLAY 'ERROR CLOSING BP13K060 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                         
               MOVE WS-K060-STATUS       TO RETURN-CODE                         
            END-IF.                                                             
                                                                                
            IF WS-K030-STATUS NOT = 0                                           
               DISPLAY 'ERROR CLOSING AB03K030 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K030-STATUS                         
               MOVE WS-K030-STATUS       TO RETURN-CODE                         
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9900-ABORT-PROCESS.                                                      
            CALL 'DBATIAR' USING SQLCA.                                         
            PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
