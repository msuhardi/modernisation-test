  *==============================================================*              
  *    SAS PROGRAM TO PRINT MONTHLY REPORT FOR TRANSACTION ON *                 
  *    FORFEITURE, ADJUSTMANT, REFUND AND REFUND *                              
  * BP130418 RHB 25/08/1998  TO CATER FOR Y2000.                                
  * BP131039 SN  20/07/2001  TO CATER FLT TYP '1A' OR '2A'                      
  * BP136224 KSJ 03/06/2016  TO CATER FOR LBS (VALID = 'L')                     
  *==============================================================*;             
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
     PROC FORMAT;                                                               
        PICTURE CREDIT LOW-<0='000,000,000.00DR'                                
                            (FILL=' ' PREFIX='$' MULT=100.00000000001)          
                        0-HIGH='000,000,000.00CR'                               
                            (FILL=' ' PREFIX='$' MULT=100.00000000001);         
                                                                                
  DATA BP13F127 ;                                                               
     INFILE BP13F127;                                                           
     INPUT  @01  TRANCDE  $CHAR2.      @05  ORIGNO   $CHAR8.                    
            @13  POSTDTE  YYMMDD8.     @25  NRIC     $CHAR9.                    
            @34  NAME     $CHAR32.     @66  DOCNO    $CHAR10.                   
            @66  DOCNO1   $CHAR14.     @81  AMTRE    ZD9.2                      
            @90  CRDR     $CHAR1.      @91  TRANDTE  YYMMDD8.                   
            @99  PAYTYPE  $CHAR3.      @102 SCHNO    $CHAR11.                   
            @114 VALID    $CHAR1.      @118 FT       $CHAR2.;                   
                                                                                
     FORMAT AMTRE CREDIT.;                                                      
                                                                                
     IF TRANCDE=13 OR TRANCDE=14 OR TRANCDE=15 OR TRANCDE=16;                   
                                                                                
     LENGTH TRANS $3;                                                           
     IF TRANCDE = 13 OR TRANCDE = 15 THEN                                       
        TRANS = 'REF';                                                          
     IF TRANCDE = 14 OR TRANCDE = 16 THEN                                       
        TRANS = 'ADJ';                                                          
     IF TRANCDE = 19 THEN                                                       
        TRANS = 'TRF';                                                          
                                                                                
                                                                                
     PROC SORT; BY PAYTYPE TRANS POSTDTE DOCNO;                                 
                                                                                
     DATA _NULLS_;                                                              
     SET BP13F127 END=FINAL;                                                    
     BY PAYTYPE TRANS POSTDTE DOCNO;                                            
                                                                                
     FILE BP13L113   HEADER=HH NOTITLES;                                        
     DOCAMT+AMTRE;                                                              
     SUBAMT+AMTRE;                                                              
     TOTAMT+AMTRE;                                                              
     GRDTOT+AMTRE;                                                              
     IF FIRST.PAYTYPE THEN PUT _PAGE_;                                          
                                                                                
        N+1;                                                                    
        PUT @01  POSTDTE  DDMMYY10.     @12  DOCNO1                             
            @28  ORIGNO                @47  NAME                                
            @72  NRIC                  @109 AMTRE CREDIT.;                      
                                                                                
        IF N=50 THEN DO;                                                        
           PUT _PAGE_;                                                          
           N=0;                                                                 
           END;                                                                 
                                                                                
        IF LAST.DOCNO THEN DO;                                                  
           LENGTH REMARK $25;                                                   
           PUT //@80 'SUB-TOTAL BY SCHEDULE NO'                                 
           @109 DOCAMT CREDIT.//;                                               
           DOCAMT=0;                                                            
           END;                                                                 
                                                                                
        IF LAST.TRANS THEN DO;                                                  
           LENGTH REMARK $25;                                                   
           IF TRANS = 'REF' THEN REMARK = 'REFUND';                             
           IF TRANS = 'ADJ' THEN REMARK = 'FORFEITURE/ADJUSTMENT';              
           IF TRANS = 'TRF' THEN REMARK = 'TRANSFER';                           
           PUT //@27 'SUB-TOTAL FOR '  @42 REMARK                               
                 @109 SUBAMT CREDIT.//;                                         
           SUBAMT=0;                                                            
           END;                                                                 
                                                                                
        IF LAST.PAYTYPE THEN DO;                                                
           PUT @27 'TOTAL FOR ' PAYTYPE @109 TOTAMT CREDIT.;                    
           TOTAMT=0;                                                            
           END;                                                                 
                                                                                
        IF FINAL THEN                                                           
           PUT //@27 'GRAND-TOTAL ' @109 GRDTOT CREDIT.;                        
        RETURN;                                                                 
  HH:                                                                           
     PAGE + 1;                                                                  
     CURRENT =DATE();                                                           
     IF FT = '1A' OR FT = '2A' OR VALID = 'L' THEN                              
     DO;                                                                        
        PUT @1   'BP13L113' @15 'HDBCAT 3'                                      
            @47  'S Y S T E M   O F   C O M M I T M E N T'                      
            @98  'DATE :' @105 CURRENT DDMMYY10.                                
            @119 'PAGE :' @126 PAGE 3.//;                                       
     PUT @32  'SOC - FORFEITURES/ADJUSTMENTS/REFUNDS/TRANSFERS SA CASES'        
         @92  'FOR ' POSTDTE MONYY7.//;                                         
     END;                                                                       
     ELSE                                                                       
     DO;                                                                        
        PUT @1   'BP13L113-A' @15 'HDBCAT 3'                                    
            @47  'S Y S T E M   O F   C O M M I T M E N T'                      
            @98  'DATE :' @105 CURRENT DDMMYY10.                                
            @119 'PAGE :' @126 PAGE 3.//;                                       
     PUT @32  'SOC - FORFEITURES/ADJUSTMENTS/REFUNDS/TRANSFERS FOR'             
         @84   POSTDTE MONYY7.//;                                               
     END;                                                                       
     PUT @1   'TRANSACTION TYPE : 'PAYTYPE//;                                   
     PUT @3   'DATE'                                                            
         @12  'SCHED/DOC NO'                                                    
         @28  'REGN-NO    '                                                     
         @52  'NAME'                                                            
         @72  ' CPF NO '                                                        
         @114 'AMOUNT';                                                         
     RETURN;                                                                    
