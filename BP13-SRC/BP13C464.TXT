       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C464                                                     
       AUTHOR. LJL.                                                             
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO CREATE TRANSACTION       *                
      *          FOR GENERATING GL TRANSACTIONS & HISTORICAL   *                
      *          FINANCIAL TRANSACTION FILE FOR BOOKING FEE    *                
      *          TRANSFER.                                     *                
      *     INPUT FILES:                                       *                
      *      1. SUCCESSFUL SALES ORDER FILE (BP13F410)         *                
      *                                                        *                
      *     OUTPUT FILES:                                      *                
      *      1. HISTORICAL FINANCIAL TRANSACTION               *                
      *         FILE (BP13F130)                                *                
      *      2. GL TRANSACTION FILE (BP13F120)                 *                
      *                                                        *                
      * CHG-NO   BY   ON      DESCRIPTION                      *                
      * -------  --- ------   ----------------                 *                
      * C950330  LJL 950313 - TO CATER FOR BK FEE TR TO CD FOR *                
      *                      AA CASES EXCEPT 2B.               *                
      * BP130037 LJL 951017 - TO CATER FOR THE AMALGAMATION OF *                
      *                      SALES TYPE 1,2,2B,3,3A AND 10.    *                
      * BP130302 RHB 980406 - TO CATER FOR REGN DEPOSIT.       *                
      * BP130418 MPA 980604 - TO CATER FOR Y2000.              *                
      * BP130418 RHB 980609 - FILE LENGTH,Y2K                  *                
      * BP133783PCL3091125 - ADD SY02F001 TO READ CURRENT DATE *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F400 ASSIGN TO BP13F400.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13F120 ASSIGN TO BP13F120.                                  
                                                                                
           SELECT BP13F130 ASSIGN TO BP13F130.                                  
                                                                                
           EJECT                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13F400                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F400.                                                           
                                                                                
       FD  BP13F120                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F120.                                                           
                                                                                
       FD  BP13F130                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F130.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SEQ                 PIC 9(3)   VALUE ZEROS.                       
                                                                                
       01  WS-CURR-CCYYMMDD.                                                    
           03  WS-CURR-CCYY       PIC X(4).                                     
           03  WS-CURR-MM         PIC X(2).                                     
           03  WS-CURR-DD         PIC X(2).                                     
                                                                                
       01  WS-TRANS-DATE.                                                       
           03  WS-TRANS-CCYY      PIC X(4).                                     
           03  WS-TRANS-MM        PIC X(2).                                     
           03  WS-TRANS-DD        PIC X(2).                                     
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CD               PIC S9(7)V9(2) VALUE +0.                     
           05  WS-BK-TRANSFER      PIC S9(5)V9(2) VALUE +0.                     
           05  WS-REGN-DEP-TRANSFER PIC S9(5)V9(2) VALUE +0.                    
           05  WS-NO-F400-READ     PIC  9(4)      VALUE ZEROS.                  
           05  WS-NO-F400-NIL      PIC  9(4)      VALUE ZEROS.                  
           05  WS-NO-F400-SA       PIC  9(4)      VALUE ZEROS.                  
           05  WS-NO-F120-WR       PIC  9(4)      VALUE ZEROS.                  
           05  WS-NO-F130-WR       PIC  9(4)      VALUE ZEROS.                  
                                                                                
       01  WS-STATUS.                                                           
           05  WS-F001-EOF         PIC X    VALUE 'N'.                          
               88 EOF-F001                  VALUE 'Y'.                          
           05  WS-EOF-F400         PIC X(3).                                    
               88  EOF-F400        VALUE 'YES'.                                 
                                                                                
       PROCEDURE DIVISION.                                                      
       0000-MAIN-LOGIC.                                                         
                                                                                
           PERFORM 1000-INIT-RTN  THRU 1000-EXIT.                               
                                                                                
           PERFORM 2000-READ-F400 THRU 2000-EXIT.                               
                                                                                
           PERFORM 3000-MAIN-PROCESS THRU 3000-EXIT                             
           UNTIL   EOF-F400.                                                    
                                                                                
           DISPLAY ' ********** BP13C464 ************'.                         
           DISPLAY 'TOTAL NO. OF RECORD READ FROM BP13F400          = '         
                    WS-NO-F400-READ.                                            
           DISPLAY 'NO. OF RECORD READ WITH F400-CDE-TRANS "SA"     = '         
                    WS-NO-F400-SA.                                              
           DISPLAY 'NO. OF RECORD READ WITH F400-CDE-TRANS NON "SA" = '         
                    WS-NO-F400-NIL.                                             
           DISPLAY 'NO. OF RECORDS CREATED IN BP13F120              = '         
                    WS-NO-F120-WR.                                              
           DISPLAY 'NO. OF RECORDS CREATED IN BP13F130              = '         
                    WS-NO-F130-WR.                                              
                                                                                
           PERFORM 9000-CLOSE-FILES                                             
           THRU    9000-EXIT.                                                   
                                                                                
       0000-FINISH.                                                             
           STOP RUN.                                                            
                                                                                
       1000-INIT-RTN.                                                           
           OPEN INPUT  BP13F400                                                 
                       SY02F001                                                 
                OUTPUT BP13F120                                                 
                       BP13F130.                                                
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
                                                                                
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              PERFORM 9000-CLOSE-FILES              THRU 9000-EXIT              
           ELSE                                                                 
              MOVE F001-DTE-CURRENT                 TO WS-CURR-CCYYMMDD         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO  WS-CURR-CCYYMMDD.                
           MOVE WS-CURR-DD                 TO  WS-TRANS-DD.                     
           MOVE WS-CURR-MM                 TO  WS-TRANS-MM.                     
           MOVE WS-CURR-CCYY               TO  WS-TRANS-CCYY.                   
                                                                                
           MOVE 'NO'   TO WS-EOF-F400.                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F400.                                                          
           READ BP13F400                                                        
               AT END MOVE 'YES' TO WS-EOF-F400                                 
                      MOVE HIGH-VALUE TO F400-NUM-REGN                          
                      GO TO  2000-EXIT.                                         
                                                                                
           ADD 1     TO WS-NO-F400-READ.                                        
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-PROCESS.                                                       
                                                                                
                                                                                
           IF F400-AMT-BK-TRANSFER NOT NUMERIC                                  
              MOVE ZEROS TO F400-AMT-BK-TRANSFER.                               
                                                                                
           IF F400-AMT-RDE-TRANSFER NOT NUMERIC                                 
              MOVE ZEROS TO F400-AMT-RDE-TRANSFER.                              
                                                                                
           IF F400-CDE-SALES-TYPE  = '10'                                       
              ADD 1  TO WS-NO-F400-NIL                                          
           ELSE                                                                 
      ************************************************                          
      * FOR AA CASES ONLY                            *                          
      ************************************************                          
           IF F400-NUM-BUYBACK = 'Y' OR 'N'                                     
              PERFORM 4000-PROCESS-REC THRU 4000-EXIT                           
              ADD 1  TO WS-NO-F400-SA.                                          
                                                                                
           MOVE +0 TO WS-BK-TRANSFER.                                           
           MOVE +0 TO WS-REGN-DEP-TRANSFER.                                     
                                                                                
           PERFORM 2000-READ-F400 THRU 2000-EXIT.                               
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-REC.                                                        
                                                                                
           IF F400-AMT-BK-TRANSFER NOT = 0                                      
              MOVE  SPACES               TO BP13F120-CASHTRAN                   
              MOVE  '19'                 TO F120-CDE-TRANS-TYPE                 
              MOVE  'BK'                 TO F120-CDE-PAYMENT-TYPE               
              COMPUTE  WS-BK-TRANSFER    =   F400-AMT-BK-TRANSFER * -1          
              MOVE  WS-BK-TRANSFER       TO F120-AMT-RECEIPT                    
              MOVE  'D'                  TO F120-CDE-CRDR                       
              MOVE  F400-NUM-REGN        TO F120-NUM-REGN                       
              MOVE  F400-NUM-SCH-ACC     TO F120-NUM-SCH-ACCT                   
              MOVE  WS-TRANS-DATE        TO F120-DTE-POST                       
              MOVE  F400-DTE-SO-EXECUTION TO F120-DTE-TRANS                     
              ADD   1                    TO WS-NO-F120-WR                       
              ADD  1                     TO WS-SEQ                              
              MOVE WS-SEQ                TO F120-COUNT-HISTORY                  
              MOVE F400-NRIC1            TO F120-NUM-NRIC                       
              WRITE BP13F120-CASHTRAN                                           
              MOVE  SPACES               TO BP13F130-REC                        
              MOVE  '19'                 TO F130-TRANS-TYPE                     
              MOVE  'BK'                 TO F130-CDE-PAYMENT-TYPE               
              MOVE  WS-TRANS-DATE        TO F130-DTE-POST                       
              MOVE  F400-DTE-SO-EXECUTION TO F130-DTE-TRANS                     
              MOVE  F400-AMT-BK-TRANSFER TO F130-AMT-RECEIPT                    
              ADD   1                    TO WS-NO-F130-WR                       
              MOVE  F400-NUM-REGN        TO F130-NUM-REGN                       
              MOVE ZEROS                 TO F130-COUNT-HISTORY                  
              WRITE BP13F130-REC.                                               
                                                                                
           IF F400-AMT-RDE-TRANSFER NOT = 0                                     
              MOVE  SPACES               TO BP13F120-CASHTRAN                   
              MOVE  '19'                 TO F120-CDE-TRANS-TYPE                 
              MOVE  'RDE'                TO F120-CDE-PAYMENT-TYPE               
              COMPUTE  WS-REGN-DEP-TRANSFER =                                   
                       F400-AMT-RDE-TRANSFER * -1                               
              MOVE  WS-REGN-DEP-TRANSFER       TO F120-AMT-RECEIPT              
              MOVE  'D'                  TO F120-CDE-CRDR                       
              MOVE  F400-NUM-REGN        TO F120-NUM-REGN                       
              MOVE  F400-NUM-SCH-ACC     TO F120-NUM-SCH-ACCT                   
              MOVE  WS-TRANS-DATE        TO F120-DTE-POST                       
              MOVE  F400-DTE-SO-EXECUTION TO F120-DTE-TRANS                     
              ADD   1                    TO WS-NO-F120-WR                       
              ADD  1                     TO WS-SEQ                              
              MOVE WS-SEQ                TO F120-COUNT-HISTORY                  
              MOVE F400-NRIC1            TO F120-NUM-NRIC                       
              WRITE BP13F120-CASHTRAN                                           
              MOVE  SPACES               TO BP13F130-REC                        
              MOVE  '19'                 TO F130-TRANS-TYPE                     
              MOVE  'RDE'                TO F130-CDE-PAYMENT-TYPE               
              MOVE  WS-TRANS-DATE        TO F130-DTE-POST                       
              MOVE  F400-DTE-SO-EXECUTION TO F130-DTE-TRANS                     
              MOVE  F400-AMT-RDE-TRANSFER TO F130-AMT-RECEIPT                   
              ADD   1                    TO WS-NO-F130-WR                       
              MOVE  F400-NUM-REGN        TO F130-NUM-REGN                       
              MOVE ZEROS                 TO F130-COUNT-HISTORY                  
              WRITE BP13F130-REC.                                               
                                                                                
                                                                                
           IF F400-AMT-BK-TRANSFER NOT = 0                                      
              MOVE  SPACES             TO BP13F120-CASHTRAN                     
              MOVE F400-NUM-REGN       TO F120-NUM-REGN                         
              MOVE F400-NUM-SCH-ACC    TO F120-NUM-SCH-ACCT                     
              MOVE  WS-TRANS-DATE      TO F120-DTE-POST                         
              MOVE  F400-DTE-SO-EXECUTION TO F120-DTE-TRANS                     
              MOVE '21'                TO F120-CDE-TRANS-TYPE                   
              MOVE 'CD'                TO F120-CDE-PAYMENT-TYPE                 
              COMPUTE WS-CD = F400-AMT-BK-TRANSFER                              
              MOVE WS-CD               TO F120-AMT-RECEIPT                      
              MOVE 'C'                 TO F120-CDE-CRDR                         
              ADD   1                  TO WS-NO-F120-WR                         
              ADD  1                   TO WS-SEQ                                
              MOVE WS-SEQ              TO F120-COUNT-HISTORY                    
              MOVE F400-NRIC1          TO F120-NUM-NRIC                         
              WRITE BP13F120-CASHTRAN                                           
              MOVE  SPACES               TO BP13F130-REC                        
              MOVE  '21'                 TO F130-TRANS-TYPE                     
              MOVE  'CD'                 TO F130-CDE-PAYMENT-TYPE               
              MOVE  WS-TRANS-DATE        TO F130-DTE-POST                       
              MOVE  F400-DTE-SO-EXECUTION TO F130-DTE-TRANS                     
              MOVE  WS-CD                TO F130-AMT-RECEIPT                    
              ADD   1                    TO WS-NO-F130-WR                       
              MOVE  F400-NUM-REGN        TO F130-NUM-REGN                       
              MOVE ZEROS                 TO F130-COUNT-HISTORY                  
              WRITE BP13F130-REC.                                               
                                                                                
           IF F400-AMT-RDE-TRANSFER NOT = 0                                     
              MOVE  SPACES             TO BP13F120-CASHTRAN                     
              MOVE F400-NUM-REGN       TO F120-NUM-REGN                         
              MOVE F400-NUM-SCH-ACC    TO F120-NUM-SCH-ACCT                     
              MOVE  WS-TRANS-DATE      TO F120-DTE-POST                         
              MOVE  F400-DTE-SO-EXECUTION TO F120-DTE-TRANS                     
              MOVE '21'                TO F120-CDE-TRANS-TYPE                   
              MOVE 'CD'                TO F120-CDE-PAYMENT-TYPE                 
              COMPUTE WS-CD = F400-AMT-RDE-TRANSFER                             
              MOVE WS-CD               TO F120-AMT-RECEIPT                      
              MOVE 'C'                 TO F120-CDE-CRDR                         
              ADD   1                  TO WS-NO-F120-WR                         
              ADD  1                   TO WS-SEQ                                
              MOVE WS-SEQ              TO F120-COUNT-HISTORY                    
              MOVE F400-NRIC1          TO F120-NUM-NRIC                         
              WRITE BP13F120-CASHTRAN                                           
              MOVE  SPACES               TO BP13F130-REC                        
              MOVE  '21'                 TO F130-TRANS-TYPE                     
              MOVE  'CD'                 TO F130-CDE-PAYMENT-TYPE               
              MOVE  WS-TRANS-DATE        TO F130-DTE-POST                       
              MOVE  F400-DTE-SO-EXECUTION TO F130-DTE-TRANS                     
              MOVE  WS-CD                TO F130-AMT-RECEIPT                    
              ADD   1                    TO WS-NO-F130-WR                       
              MOVE  F400-NUM-REGN        TO F130-NUM-REGN                       
              MOVE ZEROS                 TO F130-COUNT-HISTORY                  
              WRITE BP13F130-REC.                                               
                                                                                
                                                                                
              MOVE 0 TO WS-SEQ.                                                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
           CLOSE      BP13F400                                                  
                      SY02F001                                                  
                      BP13F120                                                  
                      BP13F130.                                                 
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
