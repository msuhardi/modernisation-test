       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23C.                                                 
      *AUTHOR. LMS.                                                             
      *DATE-WRITTEN.  24/04/2007.                                               
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   :                                       *                
      *    1.  TO PRINT REPORT ON APPLICATIONS WITH UTS TAG.   *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1    BP13F820                                       *                
      *    2    BP13K060                                       *                
      *    3.   BP13K800                                       *                
      *    4.   BP13K820                                       *                
      *                                                        *                
      *    OUPPUT FILES:                                       *                
      *    1    BP13L23C                                       *                
      *                                                        *                
      *========================================================*                
      * CHG-NO    BY  ON             DESCRIPTION               *                
      * -------- --- ---------- -----------------------------  *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-9121.                                               
       OBJECT-COMPUTER. IBM-9121.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820        ASSIGN       TO BP13F820.                     
                                                                                
           SELECT BP13K060        ASSIGN       TO BP13K060                      
                                  ACCESS MODE  IS RANDOM                        
                                  ORGANIZATION IS INDEXED                       
                                  RECORD KEY   IS K060-KEY-FLD                  
                                  FILE STATUS  IS WS-BP13K060-STATUS.           
                                                                                
           SELECT BP13K800        ASSIGN       TO BP13K800                      
                                  ORGANIZATION IS INDEXED                       
                                  ACCESS MODE  IS RANDOM                        
                                  RECORD KEY   IS K800-NUM-REGN                 
                                  FILE STATUS  IS WS-BP13K800-STATUS.           
                                                                                
           SELECT BP13K820        ASSIGN       TO BP13K820                      
                                  ACCESS MODE  IS SEQUENTIAL                    
                                  ORGANIZATION IS INDEXED                       
                                  RECORD KEY   IS K820-KEY-FLD                  
                                  FILE STATUS  IS WS-BP13K820-STATUS.           
                                                                                
           SELECT BP13L23C         ASSIGN      TO BP13L23C.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F820                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 400  CHARACTERS.                                     
       01  BP13F820-REC.                                                        
           05  F820-NUM-REGN     PIC X(8).                                      
           05  F820-NUM-NRIC     PIC X(9).                                      
           05  FILLER            PIC X(382).                                    
           05  F820-UTS          PIC X(1).                                      
                                                                                
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       01  BP13K820-REC.                                                        
           05 K820-KEY-FLD.                                                     
              10 K820-NUM-REGN                   PIC X(8).                      
              10 K820-NUM-NRIC                   PIC X(9).                      
           05 K820-CDE-NEW-OCCUP                 PIC X(1).                      
           05 K820-NUM-CPF                       PIC X(9).                      
           05 K820-NME-OCCP                      PIC X(66).                     
           05 K820-NUM-ETHNIC                    PIC X(1).                      
           05 K820-NUM-SEX                       PIC X(1).                      
           05 K820-NUM-MARITAL-STATUS            PIC X(1).                      
           05 K820-DTE-BIRTH                     PIC X(8).                      
           05 K820-NUM-CITIZENSHIP               PIC X(2).                      
           05 K820-NUM-RELATIONSHIP              PIC X(2).                      
           05 K820-AMT-INCOME                    PIC 9(5).                      
           05 K820-NUM-NATIONALITY               PIC X(2).                      
           05 K820-NUM-RESIDENT-NT               PIC X(1).                      
           05 K820-AMT-INCOME-TP                 PIC 9(5).                      
           05 K820-DTE-UPDATE                    PIC X(8).                      
           05 K820-NUM-5YR-OCCUPN                PIC X(1).                      
           05 K820-NUM-LESSEE-CHECK              PIC X(1).                      
           05 K820-NUM-LOAN-TAG                  PIC X(2).                      
           05 K820-NUM-DP-TAG                    PIC X(2).                      
           05 K820-NUM-CA-TAG                    PIC X(2).                      
           05 K820-NUM-SCH-ACCT                  PIC X(11).                     
           05 K820-DTE-EFFECTIVE                 PIC X(8).                      
           05 K820-NUM-PURCHASER-STATUS          PIC X(2).                      
           05 K820-NUM-FREEZE-OCCP               PIC X(1).                      
           05 K820-NUM-PPO                       PIC X(1).                      
           05 K820-NUM-SUBSIDISED-LOAN           PIC 9(2).                      
           05 K820-AMT-INSURANCE-PREM            PIC 9(6)V99.                   
           05 K820-AMT-OLD-INSTALMENT            PIC 9(5)V99.                   
           05 K820-AMT-INCOME-CA                 PIC 9(5).                      
           05 K820-AMT-CPF-BALANCE               PIC 9(6)V99.                   
           05 FILLER                             PIC X(03).                     
           05 K820-NUM-USERID                    PIC X(7).                      
           05 K820-NUM-FREEZE-OCCP-TAG           PIC X(2).                      
           05 K820-REJECTION-LOG.                                               
              10 K820-NUM-LOAN-TAG-PREV          PIC X(02).                     
              10 K820-NUM-PURCHASER-STATUS-PREV  PIC X(02).                     
              10 K820-NUM-FREEZE-OCCP-TAG-PREV   PIC X(02).                     
              10 K820-NUM-CA-TAG-PREV            PIC X(02).                     
           05 K820-NUM-NRIC-SPOUSE               PIC X(9).                      
           05 FILLER                             PIC X(7).                      
           05 K820-NUM-HP-PGR                    PIC X(8).                      
           05 K820-NUM-PHONE-OFF                 PIC X(8).                      
           05 K820-NUM-DEBAR-SHAP1               PIC X(1).                      
           05 K820-NUM-DEBAR-SHAP2               PIC X(1).                      
           05 K820-NUM-CSTMR-SOURCE              PIC X(9).                      
           05 K820-NME-SPOUSE                    PIC X(66).                     
           05 K820-NUM-EDUCATION                 PIC X(1).                      
           05 K820-NUM-PHONE                     PIC X(8).                      
           05 K820-NUM-TAG2                      PIC X(2).                      
           05 FILLER                             PIC X(69).                     
           05 K820-UTS                           PIC X(1).                      
                                                                                
       FD  BP13L23C                                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L23C-PRINT-REC              PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  WS-BP13K060-STATUS      PIC 9(02) VALUE ZEROES.                  
           05  WS-BP13K800-STATUS      PIC 9(02) VALUE ZEROES.                  
           05  WS-BP13K820-STATUS      PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05 WS-SYS-DATE              PIC X(08) VALUE SPACES.                  
           05 FILLER                   PIC X(13) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05 WS-DISP-DD               PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE '/'.                     
           05 WS-DISP-MM               PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE '/'.                     
           05 WS-DISP-CCYY             PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-NRIC-READ             PIC 9(07) VALUE ZEROES.                  
           05 WS-BP13K800-NOTFND       PIC 9(07) VALUE ZEROES.                  
           05 WS-BP13K820-NOTFND       PIC 9(07) VALUE ZEROES.                  
           05 WS-BP13K060-NOTFND       PIC 9(07) VALUE ZEROES.                  
           05 WS-BP13K800-FND          PIC 9(07) VALUE ZEROES.                  
           05 WS-BP13K820-FND          PIC 9(07) VALUE ZEROES.                  
           05 WS-BP13K060-FND          PIC 9(07) VALUE ZEROES.                  
           05 WS-LINE-CTR              PIC 9(07) VALUE 60.                      
           05 WS-APPLN-CTR             PIC 9(07) VALUE ZEROES.                  
           05 WS-PAGE-CTR              PIC 9(07) VALUE ZEROES.                  
           05 WS-SNO-CTR               PIC 9(07) VALUE ZEROES.                  
                                                                                
       01  WS-FLAGS.                                                            
           05 BP13F820-EOF             PIC X(01) VALUE 'N'.                     
           05 WS-REC-FIRST             PIC X(01) VALUE SPACES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-PRJCT             PIC X(03) VALUE SPACES.                  
                                                                                
      *--------------------------------*                                        
      *  BP13L23C REPORT LAYOUT        *                                        
      *--------------------------------*                                        
       01  L23C-HEADER-1.                                                       
           05 FILLER                   PIC X(08) VALUE 'BP13L23C'.              
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'HDB3'.                  
           05 FILLER                   PIC X(29) VALUE SPACES.                  
           05 FILLER                   PIC X(22) VALUE                          
             'SYSTEM OF COMMITMENT'.                                            
           05 FILLER                   PIC X(33) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'DATE : '.               
           05 L23C-DATE                PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'PAGE: '.                
           05 L23C-PAGE                PIC ZZZZ9.                               
                                                                                
       01  L23C-HEADER-2.                                                       
           05 FILLER                   PIC X(35) VALUE SPACES.                  
           05 FILLER                   PIC X(60) VALUE                          
           'REPORT ON APPLICATIONS WITH UTS RECORD'.                            
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(14) VALUE                          
           'USER : SOU'.                                                        
                                                                                
       01  L23C-HEADER-3.                                                       
           05 FILLER                   PIC X(36) VALUE                          
             'S/NO   REGN       NRIC NO       NAME'.                            
           05 FILLER                   PIC X(66) VALUE SPACES.                  
           05 FILLER                   PIC X(28) VALUE                          
             'RN  CTZ  M/S  STATUS  UTS'.                                       
                                                                                
       01  L23C-DETAIL.                                                         
           05 L23C-NO                     PIC ZZZ9.                             
           05 L23C-NO-X REDEFINES L23C-NO PIC X(04).                            
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 L23C-REGN-NO                PIC X(08) VALUE SPACES.               
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 L23C-NRIC-S                 PIC X(01) VALUE SPACES.               
           05 L23C-NRIC-DASH              PIC X(01) VALUE SPACES.               
           05 L23C-NRIC-NO                PIC X(09) VALUE SPACES.               
           05 FILLER                      PIC X(03) VALUE SPACES.               
           05 L23C-NAME                   PIC X(66) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE SPACES.               
           05 L23C-RELATIONSHIP           PIC X(02) VALUE SPACES.               
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 L23C-CTZ                    PIC X(02) VALUE SPACES.               
           05 FILLER                      PIC X(04) VALUE SPACES.               
           05 L23C-MARITAL-STAT           PIC X(02) VALUE SPACES.               
           05 FILLER                      PIC X(02) VALUE SPACES.               
           05 L23C-APPLN-STAT             PIC X(03) VALUE SPACES.               
           05 FILLER                      PIC X(05) VALUE SPACES.               
           05 L23C-UTS                    PIC X(01) VALUE SPACES.               
                                                                                
       01  L23C-DETAIL-LINE               PIC X(132) VALUE ALL '-'.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-CONTROL.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-OPEN-FILE      THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F820  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
             UNTIL BP13F820-EOF = 'Y'.                                          
           PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------*                                         
       1000-OPEN-FILE.                                                          
      *-------------------------------*                                         
                                                                                
            OPEN INPUT  BP13K800 BP13K820                                       
                        BP13K060 BP13F820                                       
                OUTPUT  BP13L23C.                                               
                                                                                
            IF WS-BP13K800-STATUS NOT = 0 AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR : ' WS-BP13K800-STATUS               
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
            END-IF.                                                             
                                                                                
            IF WS-BP13K820-STATUS NOT = 0 AND 97                                
              DISPLAY 'BP13K820 OPEN ERROR : ' WS-BP13K820-STATUS               
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
            END-IF.                                                             
                                                                                
            IF WS-BP13K060-STATUS NOT = 0 AND 97                                
               DISPLAY 'BP13K060 OPEN ERROR : ' WS-BP13K060-STATUS              
               PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                          
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE.                       
            MOVE WS-SYS-DATE(1:4)      TO WS-DISP-CCYY.                         
            MOVE WS-SYS-DATE(5:2)      TO WS-DISP-MM.                           
            MOVE WS-SYS-DATE(7:2)      TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------*                                         
       2000-READ-BP13F820.                                                      
      *-------------------------------*                                         
                                                                                
           READ BP13F820                                                        
             AT END DISPLAY 'NO RECORDS FOUND!'                                 
               MOVE 'Y'               TO BP13F820-EOF                           
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1                     TO WS-NRIC-READ.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORD.                                                     
      *--------------------------------*                                        
                                                                                
           PERFORM 6000-READ-BP13K800 THRU 6000-EXIT.                           
           PERFORM 2000-READ-BP13F820 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       4300-READ-BP13K060.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
                                                                                
           IF WS-BP13K060-STATUS = 00                                           
             ADD 1                      TO WS-BP13K060-FND                      
           ELSE                                                                 
             IF WS-BP13K060-STATUS = 23                                         
               ADD 1                    TO WS-BP13K060-NOTFND                   
               DISPLAY 'BP13K060 RECORD NOT FOUND :' K060-KEY-FLD               
             ELSE                                                               
               MOVE WS-BP13K060-STATUS  TO RETURN-CODE                          
               DISPLAY 'BP13K060 READ FILE ERROR : ' WS-BP13K060-STATUS         
               PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       5100-PRINT-DETAIL.                                                       
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55                                                  
              PERFORM 5200-PRINT-HEADER THRU 5200-EXIT                          
           END-IF.                                                              
                                                                                
           IF WS-REC-FIRST = 'Y'                                                
             ADD 1                     TO WS-SNO-CTR                            
             MOVE WS-SNO-CTR           TO L23C-NO                               
             MOVE 'N'                  TO WS-REC-FIRST                          
             MOVE SPACES               TO L23C-PRINT-REC                        
             WRITE L23C-PRINT-REC      FROM L23C-DETAIL AFTER 2                 
             ADD   2                   TO WS-LINE-CTR                           
           ELSE                                                                 
             MOVE SPACES               TO L23C-REGN-NO                          
                                          L23C-PRINT-REC                        
                                          L23C-NO-X                             
             WRITE L23C-PRINT-REC      FROM L23C-DETAIL AFTER 1                 
             ADD 1                     TO WS-LINE-CTR                           
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       5200-PRINT-HEADER.                                                       
      *--------------------------------*                                        
                                                                                
           ADD 1                       TO WS-PAGE-CTR.                          
           MOVE WS-DISP-DATE           TO L23C-DATE.                            
           MOVE WS-PAGE-CTR            TO L23C-PAGE.                            
                                                                                
           MOVE SPACES                 TO L23C-PRINT-REC.                       
           WRITE L23C-PRINT-REC        FROM L23C-HEADER-1 AFTER PAGE.           
           WRITE L23C-PRINT-REC        FROM L23C-HEADER-2 AFTER 1.              
           WRITE L23C-PRINT-REC        FROM L23C-HEADER-3 AFTER 2.              
           WRITE L23C-PRINT-REC        FROM L23C-DETAIL-LINE AFTER 1.           
           MOVE 6                      TO WS-LINE-CTR.                          
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       6000-READ-BP13K800.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE SPACES                 TO K800-NUM-REGN.                        
           MOVE F820-NUM-REGN          TO K800-NUM-REGN.                        
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-BP13K800-STATUS                                          
           WHEN 00                                                              
             PERFORM 6100-GET-RFS-DETAIL THRU 6100-EXIT                         
             ADD 1                       TO WS-BP13K800-FND                     
           WHEN 23                                                              
             ADD 1                       TO WS-BP13K800-NOTFND                  
             DISPLAY 'BP13K800 RECORD NOT FOUND : ' K800-NUM-REGN               
           WHEN OTHER                                                           
             MOVE WS-BP13K800-STATUS     TO RETURN-CODE                         
             DISPLAY 'BP13K800 READ FILE ERROR : ' WS-BP13K800-STATUS           
             PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       6100-GET-RFS-DETAIL.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE  ZEROES                TO WS-APPLN-CTR.                         
           MOVE 'Y'                    TO WS-REC-FIRST.                         
           MOVE SPACES                 TO K820-KEY-FLD.                         
           MOVE K800-NUM-REGN          TO K820-NUM-REGN.                        
                                                                                
           START BP13K820 KEY IS NOT < K820-KEY-FLD.                            
           PERFORM 6200-READNEXT-BP13K820  THRU 6200-EXIT                       
             UNTIL K800-NUM-REGN NOT = K820-NUM-REGN OR                         
                   K820-KEY-FLD = HIGH-VALUES.                                  
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       6200-READNEXT-BP13K820.                                                  
      *--------------------------------*                                        
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END MOVE HIGH-VALUES   TO K820-KEY-FLD                          
             GO TO 6200-EXIT.                                                   
                                                                                
           EVALUATE WS-BP13K820-STATUS                                          
           WHEN 00                                                              
             IF K820-NUM-REGN = K800-NUM-REGN                                   
               PERFORM 6300-PRINT-RFS  THRU 6300-EXIT                           
             END-IF                                                             
             ADD 1                     TO WS-BP13K820-FND                       
           WHEN 23                                                              
             ADD 1                     TO WS-BP13K820-NOTFND                    
             DISPLAY 'BP13K820 RECORD NOT FOUND : ' K820-KEY-FLD                
           WHEN  OTHER                                                          
             MOVE WS-BP13K820-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K820 READ FILE ERROR : ' WS-BP13K820-STATUS           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       6300-PRINT-RFS.                                                          
      *--------------------------------*                                        
                                                                                
           MOVE SPACES                 TO L23C-DETAIL.                          
           MOVE K820-NUM-REGN          TO L23C-REGN-NO.                         
           MOVE K820-NUM-NRIC(1:1)     TO L23C-NRIC-S.                          
           MOVE '-'                    TO L23C-NRIC-DASH.                       
           MOVE K820-NUM-NRIC(2:8)     TO L23C-NRIC-NO.                         
           MOVE K820-NME-OCCP          TO L23C-NAME.                            
           MOVE K820-NUM-RELATIONSHIP  TO L23C-RELATIONSHIP.                    
                                                                                
           IF K820-NUM-MARITAL-STATUS IS ALPHABETIC                             
             MOVE K820-NUM-MARITAL-STATUS TO L23C-MARITAL-STAT                  
           ELSE                                                                 
             MOVE K820-NUM-MARITAL-STATUS TO K060-CODE                          
             MOVE 44                      TO K060-SERIAL-NO                     
             PERFORM 4300-READ-BP13K060   THRU 4300-EXIT                        
             MOVE K060-DESC(1:1)          TO L23C-MARITAL-STAT                  
           END-IF.                                                              
                                                                                
           IF K820-NUM-CITIZENSHIP IS ALPHABETIC                                
             MOVE K820-NUM-CITIZENSHIP TO L23C-CTZ                              
           ELSE                                                                 
             MOVE K820-NUM-CITIZENSHIP  TO K060-CODE                            
             MOVE 41                    TO K060-SERIAL-NO                       
             PERFORM 4300-READ-BP13K060 THRU 4300-EXIT                          
             MOVE K060-DESC             TO L23C-CTZ                             
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
           WHEN K800-NUM-NRIC1 = K820-NUM-NRIC                                  
             MOVE 'HA1'               TO L23C-APPLN-STAT                        
           WHEN K800-NUM-NRIC2 = K820-NUM-NRIC                                  
             MOVE 'HA2'               TO L23C-APPLN-STAT                        
           WHEN K800-NUM-NRIC3 = K820-NUM-NRIC                                  
             MOVE 'HA3'               TO L23C-APPLN-STAT                        
           WHEN K800-NUM-NRIC4 = K820-NUM-NRIC                                  
             MOVE 'HA4'               TO L23C-APPLN-STAT                        
           WHEN OTHER                                                           
             MOVE 'OCC'               TO L23C-APPLN-STAT                        
           END-EVALUATE.                                                        
                                                                                
           MOVE K820-UTS TO L23C-UTS.                                           
                                                                                
           PERFORM 5100-PRINT-DETAIL  THRU 5100-EXIT.                           
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       9999-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID   : BP13C23C'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY 'TOTAL BP13F820 RECS READ  : ' WS-NRIC-READ.                 
           DISPLAY 'TOTAL BP13K800 RECS FOUND : ' WS-BP13K800-FND.              
           DISPLAY 'TOTAL BP13K820 RECS FOUND : ' WS-BP13K820-FND.              
           DISPLAY 'TOTAL BP13K800 RECS NOTFND: ' WS-BP13K800-NOTFND.           
           DISPLAY 'TOTAL BP13K820 RECS NOTFND: ' WS-BP13K820-NOTFND.           
                                                                                
           CLOSE BP13F820 BP13K060                                              
                 BP13K800 BP13K820                                              
                 BP13L23C.                                                      
                                                                                
           IF WS-BP13K060-STATUS NOT = 00                                       
             MOVE WS-BP13K060-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K060 CLOSE ERROR : ' WS-BP13K060-STATUS               
           END-IF.                                                              
                                                                                
           IF WS-BP13K800-STATUS NOT = 00                                       
             MOVE WS-BP13K800-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K800 CLOSE ERROR : ' WS-BP13K800-STATUS               
           END-IF.                                                              
                                                                                
           IF WS-BP13K820-STATUS NOT = 00                                       
             MOVE WS-BP13K820-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K820 CLOSE ERROR : ' WS-BP13K820-STATUS               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
