      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C754.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  : BASE ON THE SCH-ACC-NO IN RESIDENT FILE*                
      *                 PROGRAM WILL USE SCH-ACC-NO TO PBF TO  *                
      *                 GET EST-CODE, THEN USE EST-CODE TO GO  *                
      *                 TO BM06K510 ESTATE TABEL TO GET NT-CODE*                
      *                                                        *                
      * CHG-NO    BY   ON   -  DESCRIPTION                     *                
      * --------------------------------------------------     *                
      * BP130509 LSB 980416 - GET ED-GEORG  INSTEAD OF ED-CURR *                
      *                       GET NUM-CCC FOR IDENTIFY WARD    *                
      *                       GET ED-CURR IN BP13C774          *                
      * BP130630 NOR 990204 - CHGE F715 F716 EXPAND TO 350     *                
      * BP130883 CLT 041100 - READ PIDB INTERFACE TABLE        *                
      * BP135089 IMC 131002 - RETRIEVE NT FROM TABLE           *                
      **********************************************************                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT BM06K510 ASSIGN TO BM06K510                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K510-ESTCDE                            
                           FILE STATUS IS WS-BM06K510-STATUS.                   
                                                                                
           SELECT BP13OUT  ASSIGN TO BP13OUT.                                   
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD  BP13F716                                                             
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F716.                                                           
                                                                                
       FD  BP13OUT                                                              
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  BP13OUT-REC                     PIC X(350).                          
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60 CHARACTERS.                                       
       COPY BM06K510.                                                           
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *=====================*                                                   
      *   FILE STATUS &     *                                                   
      *   CONTROL COUNT     *                                                   
      *=====================*                                                   
                                                                                
       77  WS-BM06K510-STATUS               PIC 99        VALUE 0.              
       77  WS-F716-EOF                      PIC X         VALUE 'N'.            
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-CNT-F716-READ             PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-OUT-WRT               PIC 9(8)      VALUE ZERO.           
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CDE-EST                   PIC X(3).                           
           05  WS-NUM-BLDNG-GL              PIC X(5).                           
           05  WS-F716-SCH-ACC              PIC X(9).                           
                                                                                
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
           EXEC SQL INCLUDE P13VBBRY END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      *================*                                                        
       0000-MAIN-LOGIC.                                                         
      *================*                                                        
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F716   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-ROUTINE    THRU 3000-EXIT                          
                   UNTIL  WS-F716-EOF = 'Y'.                                    
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
           OPEN INPUT  BP13F716                                                 
                       BM06K510                                                 
                OUTPUT BP13OUT.                                                 
                                                                                
           IF WS-BM06K510-STATUS NOT = ZEROES AND 97                            
              MOVE WS-BM06K510-STATUS      TO RETURN-CODE                       
              DISPLAY 'OPEN BM06K510 FAIL STATUS = ' WS-BM06K510-STATUS         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       2000-READ-BP13F716.                                                      
      *===================*                                                     
           READ BP13F716 AT END                                                 
                        MOVE 'Y' TO WS-F716-EOF                                 
                        GO TO 2000-EXIT.                                        
                                                                                
           ADD 1 TO WS-CNT-F716-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===============================================================*         
      * RETRIEVE UNIT INFORMATION USING THE SCHEME AND ACCOUNT NUMBER *         
      * FROM THE BP13F716                                             *         
      *===============================================================*         
      *==================*                                                      
       3000-MAIN-ROUTINE.                                                       
      *==================*                                                      
           PERFORM 3100-GET-UNIT-INFO   THRU 3100-EXIT.                         
                                                                                
           IF SQLCODE = WS-SQL-FOUND                                            
              IF F716-CDE-NT-RESIDE = SPACES OR LOW-VALUES                      
                 PERFORM 3300-GET-NEWTOWN-NAME THRU 3300-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4000-BP13OUT-WRITE   THRU 4000-EXIT.                         
                                                                                
           PERFORM 2000-READ-BP13F716   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       3100-GET-UNIT-INFO.                                                      
      *===================*                                                     
      *===============================================================*         
      * RETRIEVE UNIT INFORMATION USING THE SCHEME ACCOUNT NUMBER     *         
      *===============================================================*         
           MOVE F716-NUM-SCH-ACCNT(1:9) TO WS-F716-SCH-ACC.                     
                                                                                
           EXEC SQL SELECT CDE_TYP_ACTLUSE,                                     
                           NUM_ESTATE_CIM,                                      
                           NUM_BLDNG_GL,                                        
                           NUM_TOWN                                             
                    INTO :DCLPIDB-INTERFACE.CDE-TYP-ACTLUSE,                    
                         :DCLPIDB-INTERFACE.NUM-ESTATE-CIM,                     
                         :DCLPIDB-INTERFACE.NUM-BLDNG-GL,                       
                         :DCLPIDB-INTERFACE.NUM-TOWN                            
                    FROM PIDB_INTERFACE                                         
                    WHERE NUM_SCHM_ACNT = :WS-F716-SCH-ACC                      
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = WS-SQL-NOT-FOUND                                        
              DISPLAY 'SCH ACC NOT FOUND IN UNIT TABLE : '                      
                                              F716-NUM-SCH-ACCNT                
              MOVE SPACES                  TO F716-CDE-FLAT-TYPE-RESIDE         
              MOVE SPACES                  TO WS-CDE-EST                        
              MOVE SPACES                  TO F716-CDE-NT-RESIDE                
              MOVE SPACES                  TO F716-NUM-ED-GEOG                  
              MOVE SPACES                  TO F716-NUM-CCC                      
              GO TO 3100-EXIT                                                   
           ELSE                                                                 
              IF SQLCODE NOT = WS-SQL-FOUND                                     
                 DISPLAY 'SQL CODE = ' SQLCODE                                  
                 PERFORM 9900-ABORT-PROCESS THRU 9900-EXIT.                     
                                                                                
           MOVE NUM-TOWN(1:3)              TO F716-CDE-NT-RESIDE.               
           MOVE CDE-TYP-ACTLUSE            TO F716-CDE-FLAT-TYPE-RESIDE.        
           MOVE NUM-ESTATE-CIM             TO WS-CDE-EST.                       
           MOVE NUM-BLDNG-GL OF DCLPIDB-INTERFACE TO WS-NUM-BLDNG-GL.           
                                                                                
      *===============================================================*         
      * RETRIEVE "ED" FROM BLDNG BNDRY TABLE USING BLDNG_GL           *         
      *===============================================================*         
           EXEC SQL SELECT NUM_BNDRY                                            
                    INTO :DCLBLDG-BNDRY.NUM-BNDRY                               
                    FROM BLDG_BNDRY                                             
                    WHERE NUM_BLDNG_GL   = :WS-NUM-BLDNG-GL                     
                    AND   NUM_TYPE_BNDRY = 'ED'                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = WS-SQL-NOT-FOUND                                        
              DISPLAY 'ED NOT FND IN BLDNG BNDRY : ' WS-NUM-BLDNG-GL            
              MOVE SPACES          TO F716-NUM-ED-GEOG                          
           ELSE                                                                 
              IF SQLCODE NOT = WS-SQL-FOUND                                     
                 DISPLAY 'SQL CODE = ' SQLCODE                                  
                 PERFORM 9900-ABORT-PROCESS THRU 9900-EXIT                      
           ELSE                                                                 
              MOVE NUM-BNDRY       TO F716-NUM-ED-GEOG                          
           END-IF.                                                              
                                                                                
      *===============================================================*         
      * RETRIEVE "CCC" FROM BLDNG BNDRY TABLE USING BLDNG_GL          *         
      *===============================================================*         
           EXEC SQL SELECT NUM_BNDRY                                            
                    INTO :DCLBLDG-BNDRY.NUM-BNDRY                               
                    FROM BLDG_BNDRY                                             
                    WHERE NUM_BLDNG_GL   = :WS-NUM-BLDNG-GL                     
                    AND   NUM_TYPE_BNDRY = 'CCC'                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = WS-SQL-NOT-FOUND                                        
              DISPLAY 'CCC NOT FND IN BLDNG BNDRY : ' WS-NUM-BLDNG-GL           
              MOVE SPACES          TO F716-NUM-CCC                              
           ELSE                                                                 
              IF SQLCODE NOT = WS-SQL-FOUND                                     
                 DISPLAY 'SQL CODE = ' SQLCODE                                  
                 PERFORM 9900-ABORT-PROCESS THRU 9900-EXIT                      
           ELSE                                                                 
              MOVE NUM-BNDRY       TO F716-NUM-CCC                              
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================*                        
      * RETRIEVE THE NEWTOWN NAME FROM THE BM06K510    *                        
      * USING THE ESTATE CODE FROM THE BUILDING TABLE. *                        
      *================================================*                        
                                                                                
      *======================*                                                  
       3300-GET-NEWTOWN-NAME.                                                   
      *======================*                                                  
           MOVE WS-CDE-EST   TO K510-ESTCDE.                                    
                                                                                
           READ BM06K510.                                                       
           IF WS-BM06K510-STATUS = 0                                            
              MOVE K510-ALPHA-NTCDE  TO F716-CDE-NT-RESIDE                      
           ELSE                                                                 
              IF WS-BM06K510-STATUS = 23                                        
                 MOVE SPACES         TO F716-CDE-NT-RESIDE                      
              ELSE                                                              
                 MOVE WS-BM06K510-STATUS      TO RETURN-CODE                    
                 DISPLAY 'ERROR READING BM06K510 FAIL STATUS = '                
                                     WS-BM06K510-STATUS                         
                 GO TO 9999-CLOSE-FILES.                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       4000-BP13OUT-WRITE.                                                      
      *===================*                                                     
           WRITE BP13OUT-REC FROM BP13F716-REC.                                 
           ADD 1 TO WS-CNT-OUT-WRT.                                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       9900-ABORT-PROCESS.                                                      
      *===================*                                                     
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE                 TO RETURN-CODE.                         
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       9900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=================*                                                       
       9999-CLOSE-FILES.                                                        
      *=================*                                                       
           DISPLAY ' '.                                                         
           DISPLAY ' **************** BP13C754 ******************** '.          
           DISPLAY '  GET ESTATE CODE FROM PBF, GET NT-CD FROM CIMS '.          
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL F716 REC READ     = ' WS-CNT-F716-READ.               
           DISPLAY 'TOTAL RECORDS WRITTEN   = ' WS-CNT-OUT-WRT.                 
           DISPLAY '    IN BP13OUT            '                                 
                                                                                
           CLOSE      BP13F716                                                  
                      BM06K510                                                  
                      BP13OUT.                                                  
                                                                                
           IF WS-BM06K510-STATUS NOT = ZEROES                                   
              DISPLAY ' FILE CLOSING ERROR , WS-BM06K510-STATUS :',             
                                  WS-BM06K510-STATUS                            
              MOVE WS-BM06K510-STATUS      TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
