      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C37H.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  MAY 16, 2017.                                             
      *===============================================================*         
      * OBJECTIVE  :  TO PRINT TEMP LOAN CASES (TM)                   *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *               2. BP13K480                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L37H                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136806 16/05/2017 PCL4 NEW PROGRAM                          *         
      * BP136806 25/05/2017 PCL4 TO ADD CDE-SALES-TYPE                *         
      * BP137836 16/05/2019 KV5  TO INCLUDED CONTRA CASES 'DT' OR 'TU'*         
      *                          & INCLUDED RECIPIENTS                *         
      * BP138757 15/05/2021 KV8  TO REMOVE LEE ADDED BERNICE          *         
      * BP139030 07/01/2022 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN          
      * BP139030 11/01/2022 ZAR7 REPLACE Bernice JH GAN with Jialing HE         
      * BP139030 11/02/2022 ZAR7 to add Shamsurianor SIBENGAT                   
      * BP139259 23/08/2022 ZAR7 REPLACE chia meng with Yusuf MOHAMMED          
      * BP139451 27/02/2023 KV8  ADDED TO ENG Siew Yan & SIM Xue Ying           
      * BP139276 05/04/2024 KAC1 REPLACE LIM KOK CHUN BY              *         
      *                                  KATHIJAH_BEE_ALI_MOHAMED     *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  BP13K480  ASSIGN        TO BP13K480                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K480-NUM-REGN                     
                             FILE STATUS   IS WS-K480-STATUS.                   
           SELECT  BP13L37H ASSIGN        TO BP13L37H.                          
           SELECT  MAILC37H ASSIGN        TO MAILC37H.                          
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K480                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K480.                                                           
                                                                                
       FD   BP13L37H                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L37H-REC               PIC X(132).                              
                                                                                
       FD   MAILC37H                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  100 CHARACTERS.                                    
       01   MAILC37H-REC              PIC X(100).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K480             PIC X(1)  VALUE 'N'.                     
           05  WS-K480-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L37H-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-F410-NUM-REGN         PIC X(8)  VALUE SPACES.                  
           05 WS-NUM-RELN              PIC X(20) VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-AMT-TEMP-LOAN         PIC S9(8)V99 VALUE ZEROES.               
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  L37H-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L37H'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L37H-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L37H-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L37H-HEADER02.                                                       
           05  FILLER              PIC X(30)    VALUE SPACES.                   
           05  FILLER              PIC X(48)     VALUE                          
               'REPORT ON TEMP LOAN / CONTRA CASES'.                            
                                                                                
       01  L37H-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'CODE    '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(11)    VALUE 'SCHEME ACCT'.            
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'SALES TYPE'.             
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(13)    VALUE 'KEY ISSUE-DTE'.          
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(20)  VALUE 'TEMP LOAN AMOUNT'.         
                                                                                
       01  L37H-DETAIL01.                                                       
           05  FILLER1             PIC X(01)        VALUE SPACES.               
           05  L37H-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER2             PIC X(03)        VALUE SPACES.               
           05  L37H-CODE           PIC X(02)        VALUE SPACES.               
           05  FILLER2             PIC X(05)        VALUE SPACES.               
           05  L37H-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER3             PIC X(01)        VALUE SPACES.               
           05  L37H-SCC-ACCT       PIC X(11)        VALUE SPACES.               
           05  FILLER4             PIC X(05)        VALUE SPACES.               
           05  L37H-SALES-TYPE     PIC X(02)        VALUE SPACES.               
           05  FILLER5             PIC X(07)        VALUE SPACES.               
           05  L37H-DTE-ISSUE      PIC X(10)        VALUE SPACES.               
           05  FILLER6             PIC X(05)        VALUE SPACES.               
           05  L37H-AMT-LOAN       PIC $$$$$$9.99   VALUE ZEROES.               
           05  FILLER7             PIC X(01)        VALUE SPACES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT3A            PIC X(50)                               
               VALUE 'TO:<Shamsurianor_SIBENGAT@hdb.gov.sg>'.                   
           05  WS-MAIL-RPT4             PIC X(50)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<THANGATHURAI_Y_DEVI@HDB.GOV.SG>'.                
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'RCPT TO:<Yusuf_MOHAMMED@hdb.gov.sg>'.                     
           05  WS-MAIL-RPT7             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT8             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT9             PIC X(50)                               
               VALUE 'TO:<HE_Jialing@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT9A            PIC X(50)                               
               VALUE 'TO:<Shamsurianor_SIBENGAT@hdb.gov.sg>'.                   
           05  WS-MAIL-RPT10            PIC X(50)                               
               VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT11            PIC X(50)                               
               VALUE 'TO:<THANGATHURAI_Y_DEVI@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT12            PIC X(50)                               
               VALUE 'TO:<Yusuf_MOHAMMED@hdb.gov.sg>'.                          
           05  WS-MAIL-RPT12A           PIC X(50)                               
               VALUE 'TO:<ENG_Siew_Yan@hdb.gov.sg>'.                            
           05  WS-MAIL-RPT12B           PIC X(50)                               
               VALUE 'TO:<SIM_Xue_Ying@hdb.gov.sg>'.                            
           05  WS-MAIL-RPT13            PIC X(100) VALUE                        
               'SUBJECT: Daily Number of Temp Loan / Contra cases'.             
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K480                                                 
              OUTPUT   BP13L37H                                                 
                       MAILC37H.                                                
                                                                                
           IF WS-K480-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K480 - ERROR OPENING : ' WS-K480-STATUS              
              MOVE WS-K480-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7100-GET-DATE           THRU 7100-EXIT.                      
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K480.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                            TO BP13K480-REC.              
           INITIALIZE                                BP13K480-REC.              
                                                                                
           MOVE K410-NUM-REGN                     TO K480-NUM-REGN.             
                                                                                
           READ BP13K480.                                                       
                                                                                
           EVALUATE WS-K480-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES                   TO BP13K480-REC               
                    INITIALIZE                       BP13K480-REC               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K480 READ ERROR ' WS-K480-STATUS               
                    DISPLAY 'K480 KEY: ' K480-NUM-REGN                          
                    MOVE WS-K480-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 2100-READ-BP13K480                 THRU 2100-EXIT.           
                                                                                
      *    IF (K410-DTE-KEY-ISSUE  >= WS-PRIO-DTE)    AND                       
      *       (K410-DTE-KEY-ISSUE  <= WS-SYSTEM-DATE)                           
              IF K480-NUM-CONTRA-TYPE = 'TM' OR 'DT' OR 'TU'                    
                 PERFORM 4000-PROCESS-RECORDS      THRU 4000-EXIT               
              END-IF.                                                           
      *    END-IF.                                                              
                                                                                
                                                                                
           PERFORM 2000-READ-BP13F410              THRU 2000-EXIT.              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                    TO L37H-DETAIL01                      
           INITIALIZE                        L37H-DETAIL01.                     
                                                                                
           IF K480-AMT-TEMP-LOAN-CPF1 NOT NUMERIC                               
              MOVE ZEROES                 TO K480-AMT-TEMP-LOAN-CPF1            
           END-IF.                                                              
                                                                                
           IF K480-AMT-TEMP-LOAN-CPF2 NOT NUMERIC                               
              MOVE ZEROES                 TO K480-AMT-TEMP-LOAN-CPF2            
           END-IF.                                                              
                                                                                
           IF K480-AMT-TEMP-LOAN-CPF3 NOT NUMERIC                               
              MOVE ZEROES                 TO K480-AMT-TEMP-LOAN-CPF3            
           END-IF.                                                              
                                                                                
           IF K480-AMT-TEMP-LOAN-CPF4 NOT NUMERIC                               
              MOVE ZEROES                 TO K480-AMT-TEMP-LOAN-CPF4            
           END-IF.                                                              
                                                                                
           IF K480-AMT-TEMP-LOAN-CASH NOT NUMERIC                               
              MOVE ZEROES                 TO K480-AMT-TEMP-LOAN-CASH            
           END-IF.                                                              
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L37H-SN.                           
           MOVE K410-NUM-REGN             TO L37H-REGN.                         
           MOVE K410-NUM-SCH-ACC          TO L37H-SCC-ACCT.                     
           MOVE K410-CDE-SALES-TYPE       TO L37H-SALES-TYPE.                   
           MOVE K480-NUM-CONTRA-TYPE      TO L37H-CODE.                         
                                                                                
           COMPUTE WS-AMT-TEMP-LOAN = K480-AMT-TEMP-LOAN-CPF1 +                 
                                      K480-AMT-TEMP-LOAN-CPF2 +                 
                                      K480-AMT-TEMP-LOAN-CPF3 +                 
                                      K480-AMT-TEMP-LOAN-CPF4 +                 
                                      K480-AMT-TEMP-LOAN-CASH.                  
                                                                                
           MOVE WS-AMT-TEMP-LOAN          TO L37H-AMT-LOAN.                     
                                                                                
           IF K410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES                    
                                              AND ZEROES                        
              STRING K410-DTE-KEY-ISSUE(7:2) '/'                                
                     K410-DTE-KEY-ISSUE(5:2) '/'                                
                     K410-DTE-KEY-ISSUE(1:4)                                    
              DELIMITED BY SIZE INTO L37H-DTE-ISSUE                             
           END-IF.                                                              
                                                                                
           PERFORM 6000-PRINT-REPORT            THRU 6000-EXIT.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC37H-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT3A.                              
            WRITE MAILC37H-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT7.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT8.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT9.                               
            WRITE MAILC37H-REC FROM WS-MAIL-RPT11.                              
            WRITE MAILC37H-REC FROM WS-MAIL-RPT12.                              
            WRITE MAILC37H-REC FROM WS-MAIL-RPT9A.                              
            WRITE MAILC37H-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILC37H-REC FROM WS-MAIL-RPT12A.                             
            WRITE MAILC37H-REC FROM WS-MAIL-RPT12B.                             
                                                                                
            STRING 'SUBJECT: Daily Number of Temp Loan / Contra cases ('        
                    WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY  ')'             
              DELIMITED BY SIZE INTO WS-MAIL-RPT13.                             
                                                                                
            WRITE MAILC37H-REC FROM WS-MAIL-RPT13.                              
                                                                                
                                                                                
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                    DELIMITED BY SIZE INTO WS-DATE-RPT.                         
                                                                                
            WRITE MAILC37H-REC FROM WS-DATE-RPT-DTL                             
                                                                                
            MOVE SPACES TO MAILC37H-REC.                                        
            WRITE MAILC37H-REC.                                                 
                                                                                
            WRITE MAILC37H-REC FROM L37H-HEADER01.                              
            WRITE MAILC37H-REC FROM L37H-HEADER02.                              
                                                                                
            MOVE SPACES TO MAILC37H-REC.                                        
            WRITE MAILC37H-REC.                                                 
                                                                                
            WRITE MAILC37H-REC FROM L37H-HEADER03.                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           WRITE BP13L37H-REC           FROM L37H-DETAIL01.                     
           WRITE MAILC37H-REC           FROM L37H-DETAIL01.                     
           ADD 1                          TO WS-CNT-L37H-WRITE.                 
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L37H-DATE-HDR.                             
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L37H-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L37H-PAGE-HDR.                
                                                                                
           WRITE BP13L37H-REC    FROM L37H-HEADER01 AFTER PAGE.                 
           WRITE BP13L37H-REC    FROM L37H-HEADER02.                            
           WRITE BP13L37H-REC    FROM L37H-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
           PERFORM 5000-HEADER-EMAIL           THRU 5000-EXIT.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-GET-DATE.                                                           
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-PRIOR-DTE.                
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           SUBTRACT 1 FROM  WS-NUM-DTE-APPT.                                    
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C37H'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ............. : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF BP13L37H RECORDS WRITTEN.......... : '            
                    WS-CNT-L37H-WRITE.                                          
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K480                                                    
                    BP13L37H                                                    
                    MAILC37H.                                                   
                                                                                
           IF WS-K480-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K480-STATUS              
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
