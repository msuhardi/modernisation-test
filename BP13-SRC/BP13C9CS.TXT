       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C9CS.                                                 
       AUTHOR.        LIZ PATIAG.                                               
       DATE-WRITTEN.  09/01/2018.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO OUTPUT BP13F800                    *                
      *                  OUTPUT WITH RSN CODE = 'FC'           *                
      *                                                        *                
      *    INPUT FILES : BP13FINP                              *                
      *                  BP13K857                              *                
      *                  P04K010                               *                
      *    OUTPUT FILES: BP13F800                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * ------    --     ----      -----------                 *                
      * BP137161  FNP1  09/01/18   NEW PROGRAM                 *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FINP ASSIGN       TO BP13FINP.                            
                                                                                
           SELECT BP13K857 ASSIGN       TO BP13K857                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K857-KEY-FLD                         
                           FILE STATUS  IS K857-STATUS.                         
                                                                                
           SELECT  P04K010 ASSIGN       TO P04K010                              
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K010-PRIME-KEY                       
                           FILE STATUS  IS K010-STATUS.                         
                                                                                
           SELECT BP13F800 ASSIGN       TO BP13F800.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K857                                                             
           RECORD CONTAINS 200  CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD  P04K010                                                              
           RECORD CONTAINS  300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD  BP13FINP                                                             
           RECORD CONTAINS 37   CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13FINP-REC.                                                        
           05  FINP-REGN           PIC X(08).                                   
           05  FILLER              PIC X(01).                                   
           05  FINP-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(01).                                   
           05  FINP-DTE-BALLOT     PIC X(06).                                   
           05  FILLER              PIC X(01).                                   
           05  FINP-SCH-ACC        PIC X(11).                                   
                                                                                
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURR-DATE                   PIC X(8)  VALUE SPACES.               
                                                                                
       01  K857-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K010-STATUS                    PIC 9(2)  VALUE ZERO.                 
                                                                                
       01  WS-FINP-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-K857-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-K010-READ                   PIC 9(6)  VALUE ZERO.                 
       01  WS-F800-WRITTEN                PIC 9(6)  VALUE ZERO.                 
       01  WS-CNT-FC                      PIC 9(6)  VALUE ZERO.                 
       01  WS-CNT-FW                      PIC 9(6)  VALUE ZERO.                 
       01  WS-CNT-FD                      PIC 9(6)  VALUE ZERO.                 
       01  WS-CNT-FE                      PIC 9(6)  VALUE ZERO.                 
       01  WS-CNT-BOOK                    PIC 9(6)  VALUE ZERO.                 
                                                                                
       01  WS-EOF-FINP                    PIC X     VALUE SPACE.                
           88 EOF-FINP                    VALUE 'Y'.                            
                                                                                
       01  WS-K010-EOF                    PIC X     VALUE SPACES.               
       01  WS-K010-FND                    PIC X     VALUE SPACES.               
       01  WS-SCH-FND                     PIC X     VALUE SPACES.               
       01  WS-DEBAR-FND                   PIC X     VALUE SPACES.               
       01  WS-FC-FND                      PIC X     VALUE SPACES.               
       01  WS-FW-FND                      PIC X     VALUE SPACES.               
       01  WS-REGN                        PIC X(08) VALUE SPACES.               
       01  WS-DTE-BALLOT                  PIC X(08) VALUE SPACES.               
       01  WS-UIN                         PIC X(09) VALUE SPACES.               
       01  WS-PREV-NRIC                   PIC X(09) VALUE SPACES.               
       01  WS-SCH-ACC                     PIC X(11) VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13FINP     THRU 2000-EXIT.                       
           MOVE FINP-NRIC     TO WS-PREV-NRIC.                                  
           MOVE FINP-REGN     TO WS-REGN.                                       
           PERFORM 3000-PROCESS-BP13FINP  THRU 3000-EXIT                        
             UNTIL EOF-FINP.                                                    
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
                                                                                
           OPEN INPUT  BP13FINP                                                 
                       BP13K857                                                 
                       P04K010                                                  
                OUTPUT BP13F800.                                                
                                                                                
           IF K857-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K857-STATUS ' K857-STATUS                    
              MOVE K857-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K010-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K010-STATUS ' K010-STATUS                    
              MOVE K010-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13FINP.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13FINP AT END                                                 
                MOVE 'Y' TO WS-EOF-FINP                                         
                MOVE HIGH-VALUES TO FINP-NRIC                                   
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-FINP-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-BP13FINP.                                                   
      *---------------------------------------------------------------*         
           MOVE  SPACES               TO WS-FC-FND                              
                                         WS-FW-FND.                             
           MOVE SPACES       TO WS-SCH-FND.                                     
                                                                                
           MOVE FINP-NRIC       TO WS-PREV-NRIC.                                
           MOVE FINP-REGN       TO WS-REGN.                                     
           MOVE FINP-DTE-BALLOT TO WS-DTE-BALLOT.                               
           MOVE FINP-SCH-ACC    TO WS-SCH-ACC.                                  
           PERFORM 3500-PROCESS-CHECK THRU 3500-EXIT                            
                UNTIL FINP-NRIC > WS-PREV-NRIC                                  
                OR EOF-FINP.                                                    
                                                                                
           IF WS-SCH-FND = SPACES                                               
              IF (WS-FC-FND = 'Y' AND WS-FW-FND = 'Y') OR                       
                  WS-DEBAR-FND NOT = 'Y'                                        
                  PERFORM 4000-WRITE-F800  THRU 4000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3500-PROCESS-CHECK.                                                      
      *---------------------------------------------------------------*         
                                                                                
           IF WS-SCH-ACC NOT = SPACES                                           
              ADD 1 TO WS-CNT-BOOK                                              
              MOVE 'Y' TO WS-SCH-FND                                            
           END-IF.                                                              
                                                                                
           IF WS-SCH-FND = SPACES                                               
              PERFORM 3550-START-P04K010 THRU 3550-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FINP     THRU 2000-EXIT.                       
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3550-START-P04K010.                                                      
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES          TO K010-PRIME-KEY.                              
           MOVE 'N'             TO WS-K010-EOF                                  
                                   WS-DEBAR-FND                                 
                                                                                
           MOVE WS-PREV-NRIC  TO K010-NUM-UINFIN.                               
                                                                                
           START P04K010  KEY >= K010-PRIME-KEY                                 
                                                                                
           EVALUATE K010-STATUS                                                 
           WHEN ZERO                                                            
                PERFORM 3600-READ-P04K010   THRU 3600-EXIT                      
                    UNTIL WS-K010-EOF = 'Y'                                     
           WHEN 23                                                              
                MOVE 'Y' TO WS-K010-EOF                                         
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR, K010-STATUS ' K010-STATUS                  
                DISPLAY 'K010-NUM-UINFIN = ' K010-NUM-UINFIN                    
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3600-READ-P04K010.                                                       
      *---------------------*                                                   
                                                                                
           READ P04K010  NEXT RECORD                                            
                                                                                
           EVALUATE K010-STATUS                                                 
           WHEN 00                                                              
                                                                                
              IF K010-NUM-UINFIN(1:9) = WS-PREV-NRIC                            
                 ADD 1 TO WS-K010-READ                                          
                 IF K010-REASON-CODE = 'FC'                                     
                    MOVE 'Y'                  TO WS-FC-FND                      
                    ADD 1 TO WS-CNT-FC                                          
                 END-IF                                                         
                 IF K010-REASON-CODE = 'FW'                                     
                    MOVE 'Y'                  TO WS-FW-FND                      
                    ADD 1 TO WS-CNT-FW                                          
                 END-IF                                                         
                 MOVE 'Y' TO WS-DEBAR-FND                                       
                 IF K010-REASON-CODE = 'FD'                                     
                    ADD 1 TO WS-CNT-FD                                          
                 END-IF                                                         
                 IF K010-REASON-CODE = 'FE'                                     
                    ADD 1 TO WS-CNT-FE                                          
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'        TO WS-K010-EOF                                 
                 MOVE 'N' TO WS-DEBAR-FND                                       
              END-IF                                                            
           WHEN 23                                                              
              MOVE 'Y'           TO WS-K010-EOF                                 
              DISPLAY 'RECORD NOT FOUND IN P04K010, KEY IS: '                   
                        FINP-NRIC                                               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING P04K010 , STATUS IS: '                     
                        K010-STATUS  ' -' FINP-NRIC                             
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-WRITE-F800.                                                         
      *********************                                                     
           MOVE SPACES           TO BP13F800-MASTER.                            
           INITIALIZE               BP13F800-MASTER.                            
                                                                                
                                                                                
           MOVE WS-REGN          TO F800-NUM-REGN.                              
           MOVE WS-DTE-BALLOT    TO F800-DTE-BALLOT.                            
           MOVE WS-PREV-NRIC     TO F800-NUM-NRIC1.                             
           MOVE WS-SCH-ACC       TO F800-NUM-SCH-ACC.                           
                                                                                
           WRITE BP13F800-MASTER.                                               
           ADD 1 TO WS-F800-WRITTEN.                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       4100-READ-BP13K857.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K857-REC.                    
           INITIALIZE                          BP13K857-REC.                    
                                                                                
           MOVE WS-REGN                        TO K857-NUM-REGN.                
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE K857-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO WS-K857-READ                                           
           WHEN 23                                                              
                MOVE SPACES       TO K857-NUM-EMAIL                             
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K857 : ' K857-STATUS                 
                MOVE K857-STATUS         TO RETURN-CODE                         
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
                                                                                
           CLOSE BP13FINP                                                       
                 BP13K857                                                       
                 P04K010                                                        
                 BP13F800.                                                      
                                                                                
           IF K857-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K857-STATUS ' K857-STATUS                   
              MOVE K857-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K010-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K010-STATUS ' K010-STATUS                   
              MOVE K010-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C9CS ================*'.              
           DISPLAY '   NO. OF FINP RECORDS READ      = ' WS-FINP-READ.          
           DISPLAY '   NO. OF K857 RECORDS READ      = ' WS-K857-READ.          
           DISPLAY '   NO. OF CASE FOUND BOOKED      = ' WS-CNT-BOOK.           
           DISPLAY '   NO. OF K010 READ              = ' WS-K010-READ.          
           DISPLAY '   NO. OF K010 FC FOUND          = ' WS-CNT-FC.             
           DISPLAY '   NO. OF K010 FW FOUND          = ' WS-CNT-FW.             
           DISPLAY '   NO. OF K010 FD FOUND -INVALID = ' WS-CNT-FD.             
           DISPLAY '   NO. OF K010 FE FOUND -INVALID = ' WS-CNT-FE.             
           DISPLAY '   NO. OF F800 RECORDS WRITTEN   = ' WS-F800-WRITTEN        
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
