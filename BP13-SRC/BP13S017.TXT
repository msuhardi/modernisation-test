  /*******************************************************************/         
  /*  SYSTEM OF COMMITMENT  (SOC)                                    */         
  /*=================================================================*/         
  /*-----------------------------------------------------------------*/         
  /* REPORT ON DEMAND BY FIRST TIMER AND SECOND TIMER UNDER BE       */         
  /*-----------------------------------------------------------------*/         
  /* CHGE     DATE       BY     DESCRIPTION                          */         
  /* -------- --------   ----   -----------                          */         
  /* BP132360 16062003   ANC1   NEW PGM                              */         
  /* BP132596 24092004 SD10 CHANGE BP13K760 TO BP13K012              */         
  /* BP134769 19032013 ZDD1 CHANGE PUNGGOL 21 TO PUNGGOL             */         
  /* BP135173 25042014 YCH2 CHGE DATA MERGE TO DATA MERGE1           */         
  /*******************************************************************/         
  PROC FORMAT;                                                                  
                                                                                
   VALUE $NTFMT  "AMK"="ANG MO KIO       "                                      
                 "BB "="BUKIT BATOK      "                                      
                 "BD "="BEDOK            "                                      
                 "BH "="BISHAN           "                                      
                 "BM "="BUKIT MERAH      "                                      
                 "BP "="BUKIT PANJANG    "                                      
                 "BT "="BUKIT TIMAH      "                                      
                 "CCK"="CHOA CHU KANG    "                                      
                 "CL "="CLEMENTI         "                                      
                 "CT "="CENTRAL          "                                      
                 "CTV"="CHESTERVALE      "                                      
                 "GL "="GEYLANG          "                                      
                 "HG "="HOUGANG          "                                      
                 "JE "="JURONG EAST      "                                      
                 "JK "="JALAN KAYU       "                                      
                 "JW "="JURONG WEST      "                                      
                 "KK "="KANGKAR          "                                      
                 "KWN"="KALLANG/WHAMPOA  "                                      
                 "LCK"="LIM CHU KANG     "                                      
                 "MD "="MANDAI           "                                      
                 "ME "="MAT EST (OTB)    "                                      
                 "MP "="MARINE PARADE    "                                      
                 "PG "="PUNGGOL          "                                      
                 "PGL"="PUNGGOL          "                                      
                 "PR "="PASIR RIS        "                                      
                 "PRC"="PASIR RIS/CHANGI "                                      
                 "QT "="QUEENSTOWN       "                                      
                 "RE "="GEYLANG          "                                      
                 "SB "="SEMBAWANG        "                                      
                 "SGN"="SERANGOON        "                                      
                 "SK "="SENGKANG         "                                      
                 "SP "="SIMPANG          "                                      
                 "STR"="SELETAR          "                                      
                 "TAP"="TAMPINES         "                                      
                 "TP "="TOA PAYOH        "                                      
                 "WIS"="WALK IN SELECTION"                                      
                 "WL "="WOODLANDS        "                                      
                 "YS "="YISHUN           "                                      
                 "ZN "="NON MAT (OTB)    "                                      
                 "00 "="OTHERS (FOR BTO) "                                      
                 ;                                                              
                                                                                
   VALUE $RMFMT  "1" = "1-RM"                                                   
                 "2" = "2-RM"                                                   
                 "3" = "3-RM"                                                   
                 "4" = "4-RM"                                                   
                 "5" = "5-RM"                                                   
                 "6" = "EXEC";                                                  
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
                                                                                
   DATA BP13K010;                                                               
   INFILE BP13K010 END=EOF;                                                     
   INPUT  @1     NT       $CHAR3.                                               
          @4     FT       $CHAR2.                                               
          @6     SUPPLY   5.                                                    
          ;                                                                     
                                                                                
   IF _N_ = 1 THEN                                                              
      DO;                                                                       
        CUR_DTE=PUT(TODAY(),DDMMYY10.);                                         
        CALL SYMPUT('DATE',CUR_DTE);                                            
      END;                                                                      
                                                                                
   IF NT ^= " ";                                                                
                                                                                
   IF FT="1" THEN FTCDE="1";                                                    
   IF FT="2" THEN FTCDE="2";                                                    
   IF FT="3" THEN FTCDE="3";                                                    
   IF FT="4" THEN FTCDE="4";                                                    
   IF FT="5" THEN FTCDE="5";                                                    
   IF FT="E" OR FT="6" THEN FTCDE="6";                                          
                                                                                
   DATA BP13K012;                                                               
     INFILE BP13K012 END=EOF;                                                   
     INPUT @1   NT       $CHAR3.                                                
           @4   FT       $CHAR2.                                                
           @270  MCPH     5.                                                    
           @275  MCPG     5.                                                    
           @280  NOMCH    5.                                                    
           @285  NOMCG    5.                                                    
           ;                                                                    
                                                                                
   IF NT ^= " ";                                                                
                                                                                
   PROC SORT DATA=BP13K010; BY NT FT;                                           
   PROC SORT DATA=BP13K012; BY NT FT;                                           
                                                                                
   DATA MERGE1;                                                                 
   MERGE BP13K010(IN=K010) BP13K012(IN=K012); BY NT FT;                         
   IF K010 AND K012;                                                            
                                                                                
                                                                                
   PROC SORT DATA=MERGE1; BY NT FT;                                             
   RUN;                                                                         
                                                                                
   DATA _NULL_;                                                                 
      SET MERGE1 END=EOF; BY NT FT;                                             
                                                                                
   RETAIN TSUPPLY 0  PTMCPH   0   TOT_FT   0   GTOTNHH  0                       
          TMCPH   0  PTMCPG   0   PCT_FT   0   GTOTUPG  0                       
          TMCPG   0  PTNMCPH  0   PCTMCPH  0   GTOTAL   0                       
          TNMCPH  0  PTNMCPG  0   PCTMCPG  0   PTOTNHH  0                       
          TNMCPG  0  PCT_NT   0   PCTNMCPH 0   PTOTUPG  0                       
          TOT_NT  0  PCTNMCPG 0   PTOTAL   0;                                   
                                                                                
   FILE BP13L017 N=PS LINESLEFT=L PS=60 LS=132 HEADER=HH NOTITLES;              
                                                                                
   IF FIRST.NT THEN                                                             
   DO;                                                                          
      NTCDE   = PUT(NT,$NTFMT.);                                                
      TOT_NT  = 0;                                                              
      PTMCPH  = 0;                                                              
      PTMCPG  = 0;                                                              
      PTNMCPH = 0;                                                              
      PTNMCPG = 0;                                                              
      PCT_NT  = 0;                                                              
   END;                                                                         
                                                                                
   NT_CTR + 1;                                                                  
                                                                                
   IF FIRST.FT THEN                                                             
   DO;                                                                          
      FTTYP   = PUT(FTCDE,$RMFMT.);                                             
      FT_CTR = 0;                                                               
      TOT_FT = 0;                                                               
      PCT_FT = 0;                                                               
      PCTMCPH  = 0;                                                             
      PCTMCPG  = 0;                                                             
      PCTNMCPH = 0;                                                             
      PCTNMCPG = 0;                                                             
   END;                                                                         
                                                                                
   FT_CTR + 1;                                                                  
                                                                                
   IF LAST.FT THEN                                                              
   DO;                                                                          
      TOT_FT = MCPH + MCPG + NOMCH + NOMCG;                                     
                                                                                
      IF FT_CTR > 0 AND TOT_FT ^= 0 THEN                                        
      DO;                                                                       
         PCTMCPH  = MCPH    / TOT_FT ;                                          
         PCTMCPG  = MCPG    / TOT_FT ;                                          
         PCTNMCPH = NOMCH   / TOT_FT ;                                          
         PCTNMCPG = NOMCG   / TOT_FT ;                                          
         PCT_FT   = PCTMCPH + PCTMCPG + PCTNMCPH + PCTNMCPG;                    
      END;                                                                      
                                                                                
      PUT @1   NTCDE     $CHAR20.                                               
          @25  FTTYP     $CHAR5.                                                
          @34  SUPPLY    4.                                                     
          @42  MCPH      5.                                                     
          @51  PCTMCPH   PERCENT6.                                              
          @59  NOMCH     5.                                                     
          @68  PCTNMCPH  PERCENT6.                                              
          @76  MCPG      5.                                                     
          @85  PCTMCPG   PERCENT6.                                              
          @93  NOMCG     5.                                                     
          @102 PCTNMCPG  PERCENT6.                                              
          @110 TOT_FT    5.                                                     
          @119 PCT_FT    PERCENT6.                                              
          ;                                                                     
                                                                                
   END;                                                                         
                                                                                
   IF LAST.NT THEN                                                              
   DO;                                                                          
      PUT @1   '--------------------'                                           
          @23  '----------'                                                     
          @34  '------'                                                         
          @42  '------'                                                         
          @51  '------'                                                         
          @59  '------'                                                         
          @68  '------'                                                         
          @76  '------'                                                         
          @85  '------'                                                         
          @93  '------'                                                         
          @102 '------'                                                         
          @110 '------'                                                         
          @119 '------'                                                         
          ;                                                                     
   END;                                                                         
                                                                                
      TSUPPLY = TSUPPLY + SUPPLY;                                               
      TMCPH   = TMCPH   + MCPH;                                                 
      TMCPG   = TMCPG   + MCPG;                                                 
      TNMCPH  = TNMCPH  + NOMCH;                                                
      TNMCPG  = TNMCPG  + NOMCG;                                                
      TOT_NT  = TMCPH + TMCPG + TNMCPH + TNMCPG;                                
                                                                                
      IF NT_CTR > 0 AND TOT_NT ^= 0 THEN                                        
      DO;                                                                       
         PTMCPH  = TMCPH  / TOT_NT;                                             
         PTMCPG  = TMCPG  / TOT_NT;                                             
         PTNMCPH = TNMCPH / TOT_NT;                                             
         PTNMCPG = TNMCPG / TOT_NT;                                             
         PCT_NT  = PTMCPH + PTMCPG + PTNMCPH + PTNMCPG;                         
      END;                                                                      
                                                                                
   IF EOF THEN                                                                  
   DO;                                                                          
      GTOTNHH = TMCPH + TNMCPH;                                                 
      GTOTUPG = TMCPG + TNMCPG;                                                 
      GTOTAL  = GTOTNHH + GTOTUPG;                                              
                                                                                
      IF GTOTAL ^= 0 THEN                                                       
      DO;                                                                       
         PTOTNHH = GTOTNHH / GTOTAL ;                                           
         PTOTUPG = GTOTUPG / GTOTAL ;                                           
         PTOTAL  = PTOTNHH + PTOTUPG;                                           
      END;                                                                      
                                                                                
      PUT @1   'TOTAL'                                                          
          @34  TSUPPLY   4.                                                     
          @42  TMCPH     5.                                                     
          @51  PTMCPH    PERCENT6.                                              
          @59  TNMCPH    5.                                                     
          @68  PTNMCPH   PERCENT6.                                              
          @76  TMCPG     5.                                                     
          @85  PTMCPH    PERCENT6.                                              
          @93  TNMCPG    5.                                                     
          @102 PTNMCPG   PERCENT6.                                              
          @110 TOT_NT    5.                                                     
          @119 PCT_NT    PERCENT6.                                              
          ;                                                                     
                                                                                
      PUT @42  '--------------------------------'                               
          @76  '--------------------------------'                               
          @110 '---------------'                                                
          ;                                                                     
                                                                                
      PUT @1   'GRAND TOTAL'                                                    
          @47  GTOTNHH   5.                                                     
          @64  PTOTNHH   PERCENT6.                                              
          @81  GTOTUPG   5.                                                     
          @98  PTOTUPG   PERCENT6.                                              
          @110 GTOTAL    5.                                                     
          @119 PTOTAL    PERCENT6.                                              
          ;                                                                     
   END;                                                                         
                                                                                
   RETURN;                                                                      
                                                                                
   HH:                                                                          
     FORMAT NTCDE  $NTFMT.                                                      
            FTTYP  $RMFMT.;                                                     
     PAGE+1;                                                                    
     CUR_DATE=TODAY();                                                          
                                                                                
     PUT @001 'BP13L017'                                                        
         @040 'S Y S T E M   O F   C O M M I T M E N T'                         
         @104 'DATE : ' CUR_DATE DDMMYY10.                                      
         @123 'PAGE : ' PAGE;                                                   
                                                                                
     PUT @021 'REPORT ON DEMAND BY FIRST-TIMER/SECOND-TIMER UNDER'              
         @072 'BALLOTING EXERCISE';                                             
                                                                                
     PUT //;                                                                    
                                                                                
     PUT @70   'DEMAND'                                                         
         ;                                                                      
                                                                                
     PUT @52   'FIRST-TIMER'                                                    
         @85   'SECOND-TIMER'                                                   
         ;                                                                      
                                                                                
     PUT @48   'MCPS'                                                           
         @62   'NON-MCPS'                                                       
         @82   'MCPS'                                                           
         @96   'NON-MCPS'                                                       
         @115  'TOTAL'                                                          
         ;                                                                      
                                                                                
     PUT @5    'ESTATE'                                                         
         @25   'FLAT'                                                           
         @34   'SUPPLY'                                                         
         @44   'NO'                                                             
         @53   '%'                                                              
         @61   'NO'                                                             
         @70   '%'                                                              
         @78   'NO'                                                             
         @87   '%'                                                              
         @95   'NO'                                                             
         @104  '%'                                                              
         @112  'NO'                                                             
         @121  '%'                                                              
         ;                                                                      
                                                                                
   PUT @1   '--------------------'                                              
       @23  '----------'                                                        
       @34  '------'                                                            
       @42  '------'                                                            
       @51  '------'                                                            
       @59  '------'                                                            
       @68  '------'                                                            
       @76  '------'                                                            
       @85  '------'                                                            
       @93  '------'                                                            
       @102 '------'                                                            
       @110 '------'                                                            
       @119 '------'                                                            
       ;                                                                        
                                                                                
   RETURN;                                                                      
                                                                                
