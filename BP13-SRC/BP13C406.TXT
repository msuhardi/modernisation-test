      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C406.                                                 
       AUTHOR.        S B LIAN.                                                 
      *DATE-WRITTEN.  04/09/84.                                                 
                                                                                
      * ====================================================== *                
      *    SYSTEM NAME : SYSTEM OF COMMIMTMENT (SOC)           *                
      *    SYSTEM ID : BP13                                    *                
      * ====================================================== *                
      *                                                        *                
      *    INPUT FILE:                                         *                
      *           1. AB02F860 - FILE GOT FROM BP13C470 PGM.    *                
      *           2. BP13K410 - SALES ORDER FILE (SOS + SOC)   *                
      *                                                        *                
      *     OUTPUT FILE :                                      *                
      *           1. BP13CPF   - TEMP ACKNOWLEDGEMENT FILE     *                
      *                                                        *                
      *  CHG NO.   BY    DATE   DESCRIPTION                    *                
      *  =======  ===   ======  ============                   *                
      *  C910943  TJF   911201  CHG SPC TO BUC                 *                
      *  SOC-PH8  RML   932707  REFER TO BP13F410/F460/F462    *                
      *  C950420  GJ    950412  CHANGE INPUT FILE TO F435.     *                
      *                         CHANGE F410 TO K410.           *                
      *                         CHANGE F460 TO FCPF.           *                
      *  C950420  LJL   950525  CHANGE WS-CPF >= 0.            *                
      *  BP130290 SGK   080198  F435 FILE SIZE CHANGED         *                
      *  BP130315 RHB   190698  TO CATER FOR RETIREMENT ACCT.  *                
      *  BP130522 SGK   310798  TO ADD CPF-PREFIX & CHANGE FILE*                
      *                         SIZE OF BP13CPF FROM 130 TO    *                
      *                         140 BYTES.                     *                
      *  BP130418 RHB   280898  TO CHANGE BP13K410 TO BP13K410 *                
      *  BP130719 CLT   101099  TO USE AMT FROM BP13F435       *                
      *                         INSTEAD OF BP13K410 AS AMT IN  *                
      *                         BP13F435 ARE LATEST.           *                
      * BP130929  MCC   131000  TO CHNGE BP13F435 TO AB02F860  *                
      *  BP136692 KSJ3  150217  EXPAND AB02F860 FR 600 TO 2000 *                
      * ====================================================== *                
                                                                                
      *----------------------*                                                  
       ENVIRONMENT DIVISION.                                                    
      *----------------------*                                                  
       CONFIGURATION SECTION.                                                   
      *----------------------*                                                  
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
       FILE-CONTROL.                                                            
      *----------------------*                                                  
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS RANDOM                                     
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS K410-STATUS.                               
                                                                                
           SELECT BP13FCPF ASSIGN TO BP13FCPF.                                  
           SELECT AB02F860 ASSIGN TO AB02F860.                                  
                                                                                
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13FCPF                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 140 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01  BP13FCPF-REC.                                                        
           05 CPF-NUM-BATCH              PIC 9(04).                             
           05 CPF-NUM-SCH-ACC.                                                  
              10 CPF-SCHEME              PIC 9(04).                             
              10 CPF-ACCOUNT             PIC 9(04).                             
              10 CPF-CHK-CDE             PIC 9(01).                             
              10 CPF-LESSEE-CDE          PIC 9(02).                             
           05 CPF-NAME                   PIC X(60).                             
           05 CPF-NUM-REGN               PIC X(08).                             
           05 CPF-CPF-NO                 PIC X(09).                             
           05 CPF-AMT-ICP-CPF            PIC S9(6)V99.                          
           05 CPF-AMT-FIRST-CPF          PIC S9(5)V99.                          
           05 CPF-AMT-MTHLY-CPF          PIC S9(5)V99.                          
           05 CPF-CDE-SALES-TYPE         PIC X(02).                             
           05 CPF-NUM-SERIAL             PIC X(09).                             
           05 CPF-CDE-ACTION             PIC X(01).                             
           05 CPF-AMT-RETIRE             PIC 9(6)V99 COMP-3.                    
           05 FILLER                     PIC X(09).                             
                                                                                
       FD   AB02F860                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY AB02F860.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-EOF-F860                   PIC X(01) VALUE 'N'.                   
       01  K410-STATUS                   PIC 9(02) VALUE ZEROES.                
       01  WS-F860-READ                  PIC 9(05)   VALUE ZEROS.               
       01  WS-K410-READ                  PIC 9(05)   VALUE ZEROS.               
       01  WS-KEY-ISSUED                 PIC 9(04)   VALUE ZEROS.               
       01  WS-FCPF-WRITE                 PIC 9(04)   VALUE ZEROS.               
       01  WS-TOT-CPF                    PIC 9(7)V99 VALUE ZEROS.               
                                                                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
      **********                                                                
       000-MAIN.                                                                
      **********                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-AB02F860 THRU 200-EXIT.                             
           PERFORM 250-ACTIVE-CASE   THRU 250-EXIT                              
                   UNTIL WS-EOF-F860 = 'Y'.                                     
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILES.                                                          
      ****************                                                          
           OPEN INPUT  AB02F860.                                                
           OPEN INPUT  BP13K410.                                                
           OPEN OUTPUT BP13FCPF.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       200-READ-AB02F860.                                                       
      ********************                                                      
            READ AB02F860 AT END                                                
                 MOVE 'Y' TO WS-EOF-F860                                        
                 GO TO 200-EXIT                                                 
                 END-READ.                                                      
                                                                                
            ADD 1 TO WS-F860-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **************************************************************            
      *   CHECK TO ENSURE THAT PAYMENT HAS ALREADY BEEN MADE       *            
      *   IF YES, THEN CHECK IF APPLICANT UTILISES CPF FOR HIS     *            
      *   DOWNPAYMENT AND REPAYMENTS.                              *            
      *   THOSE SALES ORDER RAISED MANUALLY (I.E. WHEN SYSTEM IS   *            
      *   DOWN), THE PROGRAM WILL NOT OUTPUT ANY RECORDS BECAUSE   *            
      *   HPI/9 FORMS FOR THESE CASES WILL HAVE BEEN MANUALLY      *            
      *   RAISED AND DESPATCH TO CPF BOARD.                        *            
      **************************************************************            
                                                                                
      *****************                                                         
       250-ACTIVE-CASE.                                                         
      *****************                                                         
           MOVE F860-NUM-ORIG-REGN   TO K410-KEY-FLD.                           
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE K410-STATUS                                                 
               WHEN 00                                                          
                  IF (K410-DTE-KEY-ISSUE  NOT = SPACES AND ZEROS) AND           
                     (K410-CDE-MANUAL-TAG NOT = 'M')                            
                     DISPLAY 'CPF-ACCT :' F860-NUM-CPF-ACCT                     
                                          K410-KEY-FLD                          
                     IF (F860-NUM-CPF-ACCT EQUAL K410-CPF1 OR K410-CPF2         
                                         OR K410-CPF3 OR K410-CPF4)             
                        ADD 1 TO WS-KEY-ISSUED                                  
                        PERFORM 300-APPLN-USING-CPF THRU 300-EXIT               
                     END-IF                                                     
                  END-IF                                                        
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND ' K410-KEY-FLD                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR IN READING K410 ' K410-STATUS                
                    DISPLAY 'REGN NO IS ' K410-KEY-FLD                          
                    PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           PERFORM 200-READ-AB02F860 THRU 200-EXIT.                             
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **************************************************************            
      *   CHECK IF CO-APPLICANT UTILISES CPF FOR HIS DOWNPAYMENT   *            
      *   AND INSTALMENT                                           *            
      **************************************************************            
      *********************                                                     
       300-APPLN-USING-CPF.                                                     
      *********************                                                     
           MOVE SPACES              TO BP13FCPF-REC.                            
           INITIALIZE                  BP13FCPF-REC.                            
                                                                                
           MOVE F860-NUM-HDB-REF    TO CPF-NUM-SCH-ACC.                         
           MOVE F860-NUM-ORIG-REGN  TO CPF-NUM-REGN.                            
           MOVE F860-NUM-SNO        TO CPF-NUM-SERIAL.                          
           MOVE F860-CDE-ACTION     TO CPF-CDE-ACTION.                          
           MOVE K410-CDE-SALES-TYPE TO CPF-CDE-SALES-TYPE.                      
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K410-CPF1                                 
              PERFORM 400-WRITE-FROM-CPF1 THRU 400-EXIT                         
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K410-CPF2                                 
              PERFORM 500-WRITE-FROM-CPF2 THRU 500-EXIT                         
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K410-CPF3                                 
              PERFORM 600-WRITE-FROM-CPF3 THRU 600-EXIT                         
           END-IF.                                                              
                                                                                
           IF F860-NUM-CPF-ACCT EQUAL K410-CPF4                                 
              PERFORM 700-WRITE-FROM-CPF4 THRU 700-EXIT                         
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       400-WRITE-FROM-CPF1.                                                     
      *********************                                                     
           COMPUTE WS-TOT-CPF =                                                 
                   K410-AMT-CD-CPF1    +  F860-AMT-LOAN-REPAY +                 
                   F860-AMT-INST-MTHLY +  F860-AMT-INST-FIRST.                  
                                                                                
           IF F860-AMT-MSS IS NOT NUMERIC                                       
              MOVE ZEROES TO F860-AMT-MSS.                                      
                                                                                
           IF WS-TOT-CPF >=  0                                                  
              MOVE K410-CPF1            TO CPF-CPF-NO                           
              MOVE K410-NAME1           TO CPF-NAME                             
              MOVE F860-AMT-LOAN-REPAY  TO CPF-AMT-ICP-CPF                      
              MOVE F860-AMT-INST-FIRST  TO CPF-AMT-FIRST-CPF                    
              MOVE F860-AMT-INST-MTHLY  TO CPF-AMT-MTHLY-CPF                    
              MOVE F860-AMT-MSS         TO CPF-AMT-RETIRE                       
              ADD  1                    TO WS-FCPF-WRITE                        
              WRITE  BP13FCPF-REC                                               
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       500-WRITE-FROM-CPF2.                                                     
      *********************                                                     
           COMPUTE WS-TOT-CPF =                                                 
                   K410-AMT-CD-CPF2    +  F860-AMT-LOAN-REPAY +                 
                   F860-AMT-INST-MTHLY +  F860-AMT-INST-FIRST.                  
                                                                                
           IF F860-AMT-MSS IS NOT NUMERIC                                       
              MOVE ZEROES TO F860-AMT-MSS.                                      
                                                                                
           IF WS-TOT-CPF >= 0                                                   
              MOVE K410-CPF2            TO CPF-CPF-NO                           
              MOVE K410-NAME2           TO CPF-NAME                             
              MOVE F860-AMT-LOAN-REPAY  TO CPF-AMT-ICP-CPF                      
              MOVE F860-AMT-INST-FIRST  TO CPF-AMT-FIRST-CPF                    
              MOVE F860-AMT-INST-MTHLY  TO CPF-AMT-MTHLY-CPF                    
              MOVE F860-AMT-MSS         TO CPF-AMT-RETIRE                       
              ADD  1                    TO WS-FCPF-WRITE                        
              WRITE  BP13FCPF-REC                                               
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       600-WRITE-FROM-CPF3.                                                     
      *********************                                                     
           COMPUTE WS-TOT-CPF =                                                 
                   K410-AMT-CD-CPF3    +  F860-AMT-LOAN-REPAY +                 
                   F860-AMT-INST-MTHLY +  F860-AMT-INST-FIRST.                  
                                                                                
           IF F860-AMT-MSS IS NOT NUMERIC                                       
              MOVE ZEROES TO F860-AMT-MSS.                                      
                                                                                
           IF WS-TOT-CPF >= 0                                                   
              MOVE K410-CPF3            TO CPF-CPF-NO                           
              MOVE K410-NAME3           TO CPF-NAME                             
              MOVE F860-AMT-LOAN-REPAY  TO CPF-AMT-ICP-CPF                      
              MOVE F860-AMT-INST-FIRST  TO CPF-AMT-FIRST-CPF                    
              MOVE F860-AMT-INST-MTHLY  TO CPF-AMT-MTHLY-CPF                    
              MOVE F860-AMT-MSS         TO CPF-AMT-RETIRE                       
              ADD  1                    TO WS-FCPF-WRITE                        
              WRITE  BP13FCPF-REC                                               
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       700-WRITE-FROM-CPF4.                                                     
      *********************                                                     
           COMPUTE WS-TOT-CPF =                                                 
                   K410-AMT-CD-CPF4    +  F860-AMT-LOAN-REPAY +                 
                   F860-AMT-INST-MTHLY +  F860-AMT-INST-FIRST.                  
                                                                                
           IF F860-AMT-MSS         IS NOT NUMERIC                               
              MOVE ZEROES TO F860-AMT-MSS.                                      
                                                                                
           IF WS-TOT-CPF >= 0                                                   
              MOVE K410-CPF4            TO CPF-CPF-NO                           
              MOVE K410-NAME4           TO CPF-NAME                             
              MOVE F860-AMT-LOAN-REPAY  TO CPF-AMT-ICP-CPF                      
              MOVE F860-AMT-INST-FIRST  TO CPF-AMT-FIRST-CPF                    
              MOVE F860-AMT-INST-MTHLY  TO CPF-AMT-MTHLY-CPF                    
              MOVE F860-AMT-MSS         TO CPF-AMT-RETIRE                       
              ADD  1                    TO WS-FCPF-WRITE                        
              WRITE  BP13FCPF-REC                                               
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    CONTROL LIST OF NUMBER OF RECORDS THAT HAVE BEEN      *              
      *    CREATED IN THE CPF WITHDRAWAL FILE.                   *              
      ************************************************************              
      *******************                                                       
       900-CLOSE-ROUTINE.                                                       
      *******************                                                       
           CLOSE BP13K410                                                       
                 BP13FCPF                                                       
                 AB02F860.                                                      
                                                                                
           IF K410-STATUS NOT EQUAL 00 AND 97                                   
              DISPLAY 'ERROR IN CLOSING K410 ' K410-STATUS                      
           END-IF.                                                              
                                                                                
           DISPLAY '    ******* BP13C406 ******* '.                             
           DISPLAY 'TOTAL RECORDS READ FROM F860 ' WS-F860-READ.                
           DISPLAY 'TOTAL RECORDS READ FROM K410 ' WS-KEY-ISSUED.               
           DISPLAY 'TOTAL RECORDS WRITTEN TO CPF ' WS-FCPF-WRITE.               
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
