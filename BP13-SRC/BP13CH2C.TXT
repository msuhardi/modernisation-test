       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH2C.                                                 
      *AUTHOR.        KATHRINA VALENZUELA.                                      
      *DATE-WRITTEN.  20/09/2019.                                               
      **************************************************************            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      **************************************************************            
      *  OBJECTIVE  :  TO DELETE SECOND CA LOWRISK CASES IN        *            
      *                BP13.K683.CPF.SECOND.CA                     *            
      *                                                            *            
      *  INPUT FILE :  BP13F683                                    *            
      *  I-O        :  BP13K683                                    *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP137713 KV6 20190920   NEW PROGRAM                        *            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F683        ASSIGN BP13F683.                              
                                                                                
           SELECT BP13K683        ASSIGN       TO BP13K683                      
                                  ACCESS MODE  IS RANDOM                        
                                  ORGANIZATION IS INDEXED                       
                                  RECORD KEY   IS K683-KEY-FLD                  
                                  FILE STATUS  IS K683-STATUS.                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F683               RECORD CONTAINS 2000 CHARACTERS               
                                  LABEL RECORDS ARE STANDARD                    
                                  RECORDING MODE IS F.                          
       COPY BP13F683.                                                           
                                                                                
       FD   BP13K683                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K683.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  K683-STATUS             PIC 9(2)  VALUE ZEROES.                  
           05  WS-F683-READ            PIC 9(5)  VALUE ZEROES.                  
           05  WS-K683-DELETED         PIC 9(5)  VALUE ZEROES.                  
           05  WS-K683-NOTFND          PIC 9(5)  VALUE ZEROES.                  
           05  WS-F683-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K683-FND             PIC X(1)  VALUE 'N'.                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F683       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-FILES       THRU 3000-EXIT                      
                   UNTIL WS-F683-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F683                                                 
                I-O    BP13K683.                                                
                                                                                
           IF K683-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K683 OPEN ERROR, STATUS : ' K683-STATUS              
              MOVE K683-STATUS              TO RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F683.                                                      
      *-------------------------------------------------------------            
           READ BP13F683                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F683-EOF                      
             NOT AT END                                                         
                ADD   1                     TO WS-F683-READ                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
                                                                                
           INITIALIZE BP13K683-REC.                                             
           MOVE  SPACES                     TO BP13K683-REC                     
                                               WS-K683-FND.                     
           MOVE  F683-KEY-FLD               TO K683-KEY-FLD.                    
                                                                                
           PERFORM 3100-READ-BP13K683       THRU 3100-EXIT.                     
                                                                                
           IF WS-K683-FND = 'Y'                                                 
              PERFORM 3200-DELETE-REC       THRU 3200-EXIT                      
           ELSE                                                                 
              ADD 1                         TO WS-K683-NOTFND                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F683       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K683.                                                      
      *-------------------------------------------------------------            
           READ BP13K683                                                        
                                                                                
           EVALUATE K683-STATUS                                                 
              WHEN 00                                                           
                 MOVE 'Y'                   TO WS-K683-FND                      
              WHEN 23                                                           
                 MOVE 'N'                   TO WS-K683-FND                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING OF BP13K683. STATUS : '                 
                          K683-STATUS                                           
                 MOVE K683-STATUS           TO RETURN-CODE                      
                 PERFORM  9000-CLOSE-FILES  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-DELETE-REC.                                                         
      *-------------------------------------------------------------            
           DELETE BP13K683                                                      
                                                                                
           IF K683-STATUS = 00                                                  
              ADD 1                         TO WS-K683-DELETED                  
              DISPLAY 'RECORD DELETED IN K683 : ' K683-NUM-REGN                 
           ELSE                                                                 
              DISPLAY 'ERROR DELETING  BP13K683. STATUS : '                     
                       K683-STATUS                                              
              MOVE K683-STATUS              TO RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------------ BP13CH2C -----------'.                         
           DISPLAY 'RECORDS READ      IN F683 = ' WS-F683-READ.                 
           DISPLAY 'RECORDS DELETED   IN K683 = ' WS-K683-DELETED.              
           DISPLAY 'RECORDS NOT FOUND IN K683 = ' WS-K683-NOTFND.               
           DISPLAY '---------------------------------'.                         
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13K683                                                       
                 BP13F683.                                                      
                                                                                
           IF K683-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K683 CLOSE ERROR, STATUS : ' K683-STATUS             
              MOVE K683-STATUS              TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
