      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38K.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  JUNE 10, 2014.                                            
      *===============================================================*         
      * OBJECTIVE  :  TO UPDATE P04F100 EFF-DATE                      *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *               2. BP13K820                                     *         
      *               3. P04K010                                      *         
      *                                                               *         
      * OUTPUT FILE:  1. P04F100                                      *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP135348 10/06/2014 PCL4 NEW PROGRAM.                         *         
      * BP135348 08/07/2014 PCL4 TO READ BP13KD05/BP13K895            *         
      * BP135348 17/07/2014 PCL4 TO CATER FOR P04F10A & P04F10B       *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
           SELECT  BP13K895  ASSIGN        TO BP13K895                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K895-KEY-FLD                      
                             FILE STATUS   IS WS-K895-STATUS.                   
           SELECT  BP13KD05  ASSIGN        TO BP13KD05                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD05-KEY-FLD                      
                             FILE STATUS   IS WS-KD05-STATUS.                   
           SELECT  P04K010   ASSIGN        TO P04K010                           
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K010-PRIME-KEY                    
                             FILE STATUS   IS WS-K010-STATUS.                   
           SELECT  P04F100   ASSIGN        TO P04F100.                          
           SELECT  P04F10A   ASSIGN        TO P04F10A.                          
           SELECT  P04F10B   ASSIGN        TO P04F10B.                          
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   BP13KD05                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD   P04F100                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY P04F100.                                                            
                                                                                
       FD   P04F10A                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P04F10A-REC  PIC X(300).                                              
                                                                                
       FD   P04F10B                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P04F10B-REC  PIC X(300).                                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K010             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K820             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K895             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-KD05             PIC X(1)  VALUE 'N'.                     
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K895-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-KD05-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K010-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-HA-INFO.                                                      
               10  HA-INFO OCCURS 4 TIMES.                                      
                   20  HA-NRIC               PIC  X(09).                        
                   20  FTS-DEB               PIC  X(02).                        
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-HA                   PIC 9(1)  VALUE ZEROES.                  
           05  WS-FTS-DEBAR            PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K820-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K895-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-P04K             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38K-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-L38X-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE ZEROES.                  
           05  WS-CNT-WRITEA           PIC 9(1)  VALUE ZEROES.                  
           05  WS-CNT-WRITEB           PIC 9(1)  VALUE ZEROES.                  
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-K410-NUM-REGN         PIC X(8)  VALUE SPACES.                  
           05 WS-NUM-NRIC              PIC X(9)  VALUE SPACES.                  
           05 WS-K820-FOUND            PIC X(1)  VALUE SPACES.                  
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K820                                                 
                       BP13K895                                                 
                       BP13KD05                                                 
                       P04K010                                                  
              OUTPUT   P04F100                                                  
                       P04F10A                                                  
                       P04F10B.                                                 
                                                                                
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD05 - ERROR OPENING : ' WS-KD05-STATUS              
              MOVE WS-KD05-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR OPENING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010 - ERROR OPENING : ' WS-K010-STATUS               
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 3100-STARTBR-BP13K820        THRU 3100-EXIT.                 
           PERFORM 3700-STARTBR-BP13KD05        THRU 3700-EXIT.                 
           PERFORM 2000-READ-BP13F410           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-STARTBR-BP13K820.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                              TO BP13K820-REC.            
           INITIALIZE                                  BP13K820-REC.            
                                                                                
           MOVE 'N'                                 TO WS-EOF-K820              
                                                       WS-K820-FOUND.           
                                                                                
           MOVE SPACES                              TO K820-KEY-FLD.            
           MOVE K410-NUM-REGN                       TO K820-NUM-REGN.           
                                                                                
           START BP13K820  KEY >= K820-KEY-FLD.                                 
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3200-READNEXT-BP13K820 THRU 3200-EXIT               
                    PERFORM 3300-PROCESS-BP13K820  THRU 3300-EXIT               
                            UNTIL WS-EOF-K820 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR BP13K820 : ' WS-K820-STATUS            
                            ' KEY : ' K820-KEY-FLD                              
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
            IF WS-K820-FOUND = 'N'                                              
               PERFORM 3400-STARTBR-BP13K895       THRU 3400-EXIT               
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3200-READNEXT-BP13K820.                                                  
      ******************************************************************        
                                                                                
           READ BP13K820 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K820.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-PROCESS-BP13K820.                                                   
      ******************************************************************        
                                                                                
           IF K820-NUM-REGN = K410-NUM-REGN                                     
              MOVE 'Y'                             TO WS-K820-FOUND             
              MOVE K820-NUM-NRIC                   TO WS-NUM-NRIC               
              PERFORM 4000-START-P04K010         THRU 4000-EXIT                 
              PERFORM 3200-READNEXT-BP13K820     THRU 3200-EXIT                 
           ELSE                                                                 
              MOVE 'Y'                             TO WS-EOF-K820               
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3400-STARTBR-BP13K895.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K895-REC.               
           INITIALIZE                               BP13K895-REC.               
                                                                                
           MOVE 'N'                              TO WS-EOF-K895.                
                                                                                
           MOVE SPACES                           TO K895-KEY-FLD.               
           MOVE K410-NUM-REGN                    TO K895-NUM-REGN-HIST.         
                                                                                
           START BP13K895  KEY >= K895-KEY-FLD.                                 
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3500-READNEXT-BP13K895 THRU 3500-EXIT               
                    PERFORM 3600-PROCESS-BP13K895  THRU 3600-EXIT               
                            UNTIL WS-EOF-K895 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR BP13K895 : ' WS-K895-STATUS            
                            ' KEY : ' K895-KEY-FLD                              
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3500-READNEXT-BP13K895.                                                  
      ******************************************************************        
                                                                                
           READ BP13K895 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K895.                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-PROCESS-BP13K895.                                                   
      ******************************************************************        
                                                                                
           IF K895-NUM-REGN-HIST = K410-NUM-REGN                                
              MOVE K895-NUM-NRIC-HIST              TO WS-NUM-NRIC               
              PERFORM 4000-START-P04K010         THRU 4000-EXIT                 
              PERFORM 3500-READNEXT-BP13K895     THRU 3500-EXIT                 
           ELSE                                                                 
              MOVE 'Y'                             TO WS-EOF-K895               
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3700-STARTBR-BP13KD05.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13KD05-REC.               
           INITIALIZE                               BP13KD05-REC.               
                                                                                
           MOVE 'N'                              TO WS-EOF-KD05.                
                                                                                
           MOVE SPACES                           TO KD05-KEY-FLD.               
           MOVE K410-NUM-REGN                    TO KD05-NUM-REGN.              
                                                                                
           START BP13KD05  KEY >= KD05-KEY-FLD.                                 
                                                                                
           EVALUATE WS-KD05-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3800-READNEXT-BP13KD05 THRU 3800-EXIT               
                    PERFORM 3900-PROCESS-BP13KD05  THRU 3900-EXIT               
                            UNTIL WS-EOF-KD05 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR BP13KD05 : ' WS-KD05-STATUS            
                            ' KEY : ' KD05-KEY-FLD                              
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       3700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3800-READNEXT-BP13KD05.                                                  
      ******************************************************************        
                                                                                
           READ BP13KD05 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-KD05.                        
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3900-PROCESS-BP13KD05.                                                   
      ******************************************************************        
                                                                                
           IF KD05-NUM-REGN = K410-NUM-REGN                                     
              IF KD05-NUM-HA-TAG = '5' OR '6'                                   
                 MOVE KD05-NUM-NRIC                TO WS-NUM-NRIC               
                 PERFORM 4000-START-P04K010      THRU 4000-EXIT                 
              END-IF                                                            
                                                                                
              PERFORM 3800-READNEXT-BP13KD05     THRU 3800-EXIT                 
           ELSE                                                                 
              MOVE 'Y'                             TO WS-EOF-KD05               
           END-IF.                                                              
                                                                                
       3900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4000-START-P04K010.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                              TO P04K010-REC.             
           INITIALIZE                                  P04K010-REC.             
                                                                                
           MOVE 'N'                                 TO WS-EOF-K010.             
                                                                                
           MOVE SPACES                              TO K010-PRIME-KEY.          
           MOVE WS-NUM-NRIC                         TO K010-NUM-UINFIN.         
                                                                                
           START P04K010  KEY = K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-P04K010  THRU 4100-EXIT               
                    PERFORM 4200-PROCESS-P04K010   THRU 4200-EXIT               
                            UNTIL WS-EOF-K010 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010 : ' WS-K010-STATUS             
                            ' NRIC : ' K010-NUM-UINFIN                          
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4100-READNEXT-P04K010.                                                   
      ******************************************************************        
                                                                                
           READ P04K010 NEXT RECORD                                             
                         AT END MOVE 'Y' TO WS-EOF-K010.                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-PROCESS-P04K010.                                                    
      ******************************************************************        
                                                                                
           IF (K010-NUM-UINFIN  = WS-NUM-NRIC)                                  
              IF (K410-NUM-SCH-ACC(1:9) = K010-EX-SCH-ACCTNO) AND               
                 (K010-REASON-CODE      = '71' OR 'GZ' OR 'SB' OR               
                                                  'MA' OR 'SC')                 
                                                                                
                 IF K010-REASON-CODE NOT = 'MA'                                 
                    PERFORM 5000-WRITE-P04F100   THRU 5000-EXIT                 
                 ELSE                                                           
                    PERFORM 5100-WRITE-P04F10A   THRU 5100-EXIT                 
                    PERFORM 5200-WRITE-P04F10B   THRU 5200-EXIT                 
                 END-IF                                                         
                                                                                
                                                                                
              END-IF                                                            
                                                                                
              PERFORM 4100-READNEXT-P04K010      THRU 4100-EXIT                 
           ELSE                                                                 
              MOVE 'Y'                             TO WS-EOF-K010               
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-WRITE-P04F100.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO P04F100-REC.                      
           INITIALIZE                         P04F100-REC.                      
                                                                                
                                                                                
           MOVE K010-PRIME-KEY             TO F100-PRIME-KEY.                   
           MOVE K010-LSE-TEN-CODE          TO F100-LSE-TEN-CODE.                
           MOVE K410-DTE-KEY-ISSUE         TO F100-EFF-DATE.                    
           MOVE K410-NUM-REGN              TO F100-NUM-REGNO.                   
           MOVE FUNCTION CURRENT-DATE(1:8) TO F100-LUPDATE.                     
                                                                                
           WRITE P04F100-REC.                                                   
           ADD 1                           TO WS-CNT-L38K-WRITE.                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-WRITE-P04F10A.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO P04F10A-REC.                      
           INITIALIZE                         P04F10A-REC.                      
                                                                                
           MOVE P04K010-REC                TO P04F10A-REC.                      
           WRITE P04F10A-REC.                                                   
                                                                                
           ADD 1                           TO WS-CNT-WRITEA.                    
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-WRITE-P04F10B.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO P04F10B-REC.                      
           INITIALIZE                         P04F10B-REC.                      
                                                                                
           MOVE 'MB'                       TO K010-REASON-CODE.                 
           MOVE K410-DTE-KEY-ISSUE         TO K010-EFF-DATE.                    
                                                                                
           MOVE P04K010-REC                TO P04F10B-REC.                      
           WRITE P04F10B-REC.                                                   
           ADD 1                           TO WS-CNT-WRITEB.                    
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38K'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ............. : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF P04F100 RECORDS WRITTEN.......... : '             
                    WS-CNT-L38K-WRITE.                                          
           DISPLAY '(3) NO OF P04F10A RECORDS WRITTEN.......... : '             
                    WS-CNT-WRITEA.                                              
           DISPLAY '(4) NO OF P04F10B RECORDS WRITTEN.......... : '             
                    WS-CNT-WRITEB.                                              
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K820                                                    
                    P04K010                                                     
                    P04F100                                                     
                    P04F10A                                                     
                    P04F10B.                                                    
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : ' WS-K820-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010  - ERROR CLOSING : ' WS-K010-STATUS              
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
