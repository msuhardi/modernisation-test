       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CG09.                                                 
       AUTHOR.        PRAKASH.                                                  
      *DATE-WRITTEN.  01-12-2020.                                               
      *DATE-COMPILED.                                                           
                                                                                
      *************************************************************             
      *     DBS BOOKING SYSTEM.                                   *             
      *===========================================================*             
      *  OBJECTIVES  : TO OMIT THE SAME NRIC AND GRANT TYPE AND                 
      *                DIFFERENT DATE APPROVAL FROM GRANT REPORT                
      *===========================================================*             
      * FILE DESCRIPTION :-                                       *             
      *                  REC                                      *             
      * DD NAME   I/O/E  LEN   KEY             REMARKS            *             
      * -------   -----  ---   ---             -------            *             
      * BP13GRNT  I      0100                  SEQUENTIAL         *             
      * BP13GRNR  I      0100  WS-K29A-KEYFLD  VSAM               *             
      * BP13F001  I      0100                  SEQUENTIAL         *             
      * BP13F002  I      0100                  SEQUENTIAL         *             
      *===========================================================*             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHGE REQ #   DATE       AUTHOR    REMARKS                 *             
      * ----------   --------   ------    -------                 *             
      * BP138332     01/12/20   PP11      INITIAL DEVELOPMENT     *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13GRNR ASSIGN          TO BP13GRNR                          
                            FILE STATUS    IS WS-GRNR-STATUS.                   
                                                                                
           SELECT BP13K210 ASSIGN          TO BP13K210                          
                            ORGANIZATION   IS INDEXED                           
                            ACCESS MODE    IS DYNAMIC                           
                            RECORD KEY     IS WS-K29A-KEYFLD                    
                            ALTERNATE KEY  IS WS-K29A-ALT-KEY                   
                            FILE STATUS    IS WS-K29A-STATUS.                   
                                                                                
           SELECT BP13F001  ASSIGN         TO  BP13F001                         
                            FILE STATUS    IS  WS-F001-STATUS.                  
                                                                                
           SELECT BP13F002  ASSIGN         TO  BP13F002                         
                            FILE STATUS    IS  WS-F002-STATUS.                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13GRNR                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13GRNR-REC.                                                        
           05 WS-NUM-NRICR                  PIC X(09).                          
           05 WS-NUM-GRANT-TYPER            PIC X(06).                          
           05 WS-AMT-GRANTR                 PIC 9(6).9(2).                      
           05 WS-DTE-APPROVALR              PIC X(10).                          
           05 FILLER                        PIC X(66).                          
                                                                                
       FD  BP13K210                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       01  BP13K210-REC.                                                        
           05 WS-K29A-KEYFLD.                                                   
              10 WS-K29A-REGNO                 PIC X(08).                       
              10 WS-K29A-ALT-KEY.                                               
                 15 WS-K29A-NRIC               PIC X(09).                       
                 15 WS-K29A-GRANT-TYPE         PIC X(06).                       
           05 WS-K29A-AMT-GRANT             PIC 9(5).9(2).                      
           05 WS-K29A-DTE-APPROVAL          PIC X(10).                          
           05 FILLER                        PIC X(59).                          
                                                                                
       FD  BP13F001                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
                                                                                
       01  WS-F001-REC-DTLS         PIC X(100).                                 
                                                                                
       FD  BP13F002                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
                                                                                
       01  WS-F002-REC-DTLS         PIC X(100).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
      * WORKING STORAGE                                                         
      *----------------------------------------------------------------*        
                                                                                
       01  WS-FIILE-STATUS.                                                     
           05  WS-F001-STATUS         PIC  9(02).                               
           05  WS-F002-STATUS         PIC  9(02).                               
           05  WS-RETURN-CODE         PIC  9(02).                               
           05  BP13GRNT-PREV-REC      PIC  X(100) VALUE SPACES.                 
           05  WS-GRNT-READ-COUNT     PIC  9(9) VALUE ZEROES.                   
           05  WS-GRNR-READ-COUNT     PIC  9(9) VALUE ZEROES.                   
           05  WS-NOT-WRT-CNT         PIC  9(9) VALUE ZEROES.                   
           05  WS-READ-K210-CNT       PIC  9(9) VALUE ZEROES.                   
           05  WS-WRT-GRNR-CNT        PIC  9(9) VALUE ZEROES.                   
           05  WS-WRT-F001-CNT        PIC  9(9) VALUE ZEROES.                   
           05  WS-WRT-F002-CNT        PIC  9(9) VALUE ZEROES.                   
           05  WS-K29A-STATUS         PIC  9(02) VALUE ZEROES.                  
           05  WS-K29A-EOF            PIC  X(01) VALUE 'N'.                     
           05  WS-GRNR-STATUS         PIC  9(02) VALUE ZEROES.                  
           05  WS-GRNR-EOF            PIC  X(01) VALUE 'N'.                     
           05  WS-APPRV-TAG           PIC  X(01) VALUE SPACES.                  
           05  WS-PREV-NRIC           PIC  X(09) VALUE SPACES.                  
           05  WS-PREV-GRANT-TYPE     PIC  X(06) VALUE SPACES.                  
           05  WS-TEMP-REC-DTLS       PIC X(100) VALUE SPACES.                  
                                                                                
       01  BP13GRNR-TEMP.                                                       
           05 WS-TMP-NRICR                  PIC X(09).                          
           05 WS-TMP-GRANT-TYPER            PIC X(06).                          
           05 WS-TMP-AMT-GRANTR             PIC 9(6).9(2).                      
           05 WS-TMP-DTE-APPROVALR          PIC X(10).                          
           05 FILLER                        PIC X(66).                          
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *0000       M  A  I  N        C  O  N  T  R  O  L            0000*        
      ******************************************************************        
       0000-MAIN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
            PERFORM 1000-START-UP      THRU  1000-EXIT.                         
                                                                                
            PERFORM 2000-MAIN-PROCESS  THRU  2000-EXIT.                         
                                                                                
            PERFORM 9000-CLOSE-FILES   THRU  9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *1000       S  T  A  R  T         U  P                       1000*        
      ******************************************************************        
      *  THIS SECTION OPEN FILES                                       *        
      ******************************************************************        
       1000-START-UP.                                                           
                                                                                
            OPEN     INPUT   BP13GRNR                                           
                             BP13K210                                           
                     OUTPUT  BP13F001                                           
                             BP13F002.                                          
                                                                                
                                                                                
            IF WS-GRNR-STATUS  = 00                                             
               CONTINUE                                                         
            ELSE                                                                
               DISPLAY 'FILE OPEN ERROR BP13GRNR'                               
               MOVE WS-GRNR-STATUS      TO  RETURN-CODE                         
               DISPLAY 'RETURN CODE: '      RETURN-CODE                         
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
            END-IF.                                                             
                                                                                
            IF WS-K29A-STATUS = 00                                              
               CONTINUE                                                         
            ELSE                                                                
               DISPLAY 'FILE OPEN ERROR BP13K210'                               
               MOVE WS-K29A-STATUS      TO  RETURN-CODE                         
               DISPLAY 'RETURN CODE: '      RETURN-CODE                         
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
      *2000       M  A  I  N      P  R  O  C  E  S  S              2000*        
      ******************************************************************        
      * THIS SECTION PROCESS THE RECORDS READ.                                  
      ******************************************************************        
                                                                                
       2000-MAIN-PROCESS.                                                       
            MOVE 'N'                     TO WS-GRNR-EOF                         
            PERFORM 2100-READ-BP13GRNR    THRU 2100-EXIT                        
                                          UNTIL WS-GRNR-EOF = 'Y'.              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2100-READ-BP13GRNR.                                                      
      *----------------------------------------------------------------*        
      * READ BP13GRNR FILE                                                      
      *----------------------------------------------------------------*        
            READ BP13GRNR                                                       
                 AT END  MOVE 'Y' TO WS-GRNR-EOF                                
                 NOT AT END                                                     
                     CONTINUE                                                   
            END-READ.                                                           
                                                                                
            EVALUATE WS-GRNR-STATUS                                             
             WHEN 00                                                            
               ADD  1                   TO  WS-GRNR-READ-COUNT                  
                                                                                
               IF WS-NUM-NRICR = WS-PREV-NRIC AND                               
                  WS-NUM-GRANT-TYPER =  WS-PREV-GRANT-TYPE                      
                  CONTINUE                                                      
               ELSE                                                             
                  PERFORM 2200-STARTBR-BP13K210 THRU 2200-EXIT                  
               END-IF                                                           
                                                                                
               MOVE WS-NUM-NRICR               TO WS-PREV-NRIC                  
               MOVE WS-NUM-GRANT-TYPER         TO WS-PREV-GRANT-TYPE            
             WHEN 10                                                            
               CONTINUE                                                         
             WHEN OTHER                                                         
               DISPLAY 'READ ERROR IN BP13GRNR'                                 
               MOVE WS-GRNR-STATUS TO WS-RETURN-CODE                            
               MOVE WS-RETURN-CODE      TO  RETURN-CODE                         
               DISPLAY 'RETURN CODE: '      RETURN-CODE                         
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
            END-EVALUATE.                                                       
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------*                                                  
       2200-STARTBR-BP13K210.                                                   
      *----------------------*                                                  
                                                                                
           MOVE SPACES                     TO BP13K210-REC.                     
           INITIALIZE                         BP13K210-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K29A-EOF.                      
           MOVE SPACES                     TO WS-APPRV-TAG.                     
                                                                                
           MOVE WS-NUM-NRICR               TO WS-K29A-NRIC.                     
           MOVE WS-NUM-GRANT-TYPER         TO WS-K29A-GRANT-TYPE.               
                                                                                
           START BP13K210 KEY >= WS-K29A-ALT-KEY.                               
                                                                                
           EVALUATE WS-K29A-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2300-READNEXT-BP13K210 THRU 2300-EXIT               
                      UNTIL WS-K29A-EOF = 'Y'                                   
                    IF WS-APPRV-TAG = 'N'                                       
                       WRITE WS-F002-REC-DTLS                                   
                       ADD 1   TO WS-WRT-F002-CNT                               
                    END-IF                                                      
               WHEN 10                                                          
                   MOVE 'Y'      TO WS-K29A-EOF                                 
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K210 : ' WS-K29A-STATUS              
                          '-KEY  : '  WS-K29A-KEYFLD                            
                  MOVE     WS-K29A-STATUS    TO    RETURN-CODE                  
                  PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       2300-READNEXT-BP13K210.                                                  
      ************************                                                  
                                                                                
                                                                                
           READ BP13K210 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-K29A-EOF.                        
                                                                                
           EVALUATE WS-K29A-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                 IF  WS-K29A-NRIC      =  WS-NUM-NRICR AND                      
                     WS-K29A-GRANT-TYPE =  WS-NUM-GRANT-TYPER                   
                                                                                
                     IF  WS-K29A-DTE-APPROVAL NOT = WS-DTE-APPROVALR            
                                                                                
                         IF WS-APPRV-TAG = 'N'                                  
                           WRITE WS-F001-REC-DTLS FROM WS-TEMP-REC-DTLS         
                           ADD 1   TO WS-WRT-F001-CNT                           
                         END-IF                                                 
                                                                                
                         PERFORM 2350-MOVE-GRANT-DTLS THRU 2350-EXIT            
                                                                                
                         MOVE BP13GRNR-TEMP  TO WS-F001-REC-DTLS                
                         WRITE WS-F001-REC-DTLS                                 
                         ADD 1   TO WS-WRT-F001-CNT                             
                         MOVE 'Y'  TO WS-APPRV-TAG                              
                     ELSE                                                       
                         IF WS-APPRV-TAG = 'Y'                                  
                           PERFORM 2350-MOVE-GRANT-DTLS THRU 2350-EXIT          
                                                                                
                           MOVE BP13GRNR-TEMP  TO WS-F001-REC-DTLS              
                           WRITE WS-F001-REC-DTLS                               
                           ADD 1   TO WS-WRT-F001-CNT                           
                         ELSE                                                   
                           MOVE 'N'  TO WS-APPRV-TAG                            
                                                                                
                           PERFORM 2350-MOVE-GRANT-DTLS THRU 2350-EXIT          
                                                                                
                           MOVE BP13GRNR-TEMP  TO WS-F002-REC-DTLS              
                                                  WS-TEMP-REC-DTLS              
                         END-IF                                                 
                                                                                
                     END-IF                                                     
                     ADD 1  TO WS-READ-K210-CNT                                 
                 ELSE                                                           
                   MOVE 'Y'      TO WS-K29A-EOF                                 
                 END-IF                                                         
                                                                                
               WHEN 10                                                          
                   MOVE 'Y'      TO WS-K29A-EOF                                 
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'READ  ERROR BP13K210 : ' WS-K29A-STATUS              
                          '-KEY  : '  WS-K29A-KEYFLD                            
                  MOVE     WS-K29A-STATUS    TO    RETURN-CODE                  
                  PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2350-MOVE-GRANT-DTLS.                                                    
      *----------------------------------------------------------------*        
           MOVE WS-K29A-NRIC           TO  WS-TMP-NRICR.                        
           MOVE WS-K29A-GRANT-TYPE     TO  WS-TMP-GRANT-TYPER.                  
           MOVE WS-K29A-AMT-GRANT      TO  WS-TMP-AMT-GRANTR.                   
           MOVE WS-K29A-DTE-APPROVAL   TO  WS-TMP-DTE-APPROVALR.                
                                                                                
       2350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
      * THIS SECTION CLOSES WS-KD05-FND FILES                          *        
      *----------------------------------------------------------------*        
       9000-CLOSE-FILES.                                                        
                                                                                
           DISPLAY 'BP13GRNR FILE READ COUNT    : ' WS-GRNR-READ-COUNT.         
           DISPLAY 'BP13K210 FILE READ COUNT    : ' WS-READ-K210-CNT.           
           DISPLAY 'BP13F001 WRITE  COUNT       : ' WS-WRT-F001-CNT.            
           DISPLAY 'BP13F002 WRITE  COUNT       : ' WS-WRT-F002-CNT.            
                                                                                
            CLOSE   BP13GRNR                                                    
                    BP13K210                                                    
                    BP13F001                                                    
                    BP13F002.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
      *                   END OF SOURCE CODE                     *              
      *----------------------------------------------------------*              
