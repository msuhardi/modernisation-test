       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY12.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 02 MAY 2008                          *              
      *                                                          *              
      * 1. OBJECTIVE      : BASED ON BTO APPLICATION, PROGRAM TO *              
      *                   : MATCH BY NRIC BP13K820               *              
      *                   : IF FOUND, CHECK STATUS IN B1P3K800   *              
      *                     1) WITH SCHACC, COUNT AS BOOKED FLAT *              
      *                     2) WITH STATUS=C, COUNT AS CACELLED  *              
      *                     IF FOUND IN-ELIGIBLE CODE.           *              
      *                     3) OTHERS, PASS TO SMS/RSL MATCHING  *              
      *                                                          *              
      * 2. INPUT          : BP13F595 -                           *              
      *                   : BP13K816                             *              
      *                   : BP13K820 - BP13.K820.OCCUP           *              
      *                   : BP13K800 - BP13.K800.APPLN           *              
      *                                                          *              
      * 3. OUTPUT         : P13F595F                             *              
      *                   : P13F595N                             *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP133340 02/05/2008 JB8  NEW PROGRAM. (BP13C995)        *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT BP13K820     ASSIGN        TO BP13K820                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K820-KEY-FLD                    
                               ALTERNATE RECORD KEY IS K820-NUM-NRIC            
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K820-STATUS.                 
                                                                                
           SELECT BP13K800     ASSIGN        TO BP13K800                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K800-NUM-REGN                   
                               FILE STATUS   IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K816     ASSIGN        TO BP13K816                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K816-KEY-FLD                    
                               FILE STATUS   IS WS-K816-STATUS.                 
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
                                                                                
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  BP13K820                                                             
                               RECORD CONTAINS 400 CHARACTERS.                  
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K800                                                             
                               RECORD CONTAINS 2000 CHARACTERS.                 
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K816                                                             
                               RECORD CONTAINS 400 CHARACTERS.                  
       COPY BP13K816.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 100 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC.                                                        
           05  F595F-NUM-REGN            PIC X(08).                             
           05  F595F-NUM-ALLO-CAT        PIC X(03).                             
           05  F595F-DTE-BALLOT          PIC X(06).                             
           05  F595F-CDE-HOUSEHOLD       PIC X(03).                             
           05  F595F-CDE-FLAT-TYPE       PIC X(02).                             
           05  F595F-NUM-NRIC1           PIC X(09).                             
           05  F595F-NUM-NRIC2           PIC X(09).                             
           05  F595F-NUM-SCH-ACC         PIC X(11).                             
           05  F595F-NUM-TYPE            PIC X(03).                             
           05  F595F-NUM-NEW-TOWN        PIC X(03).                             
           05  F595F-NUM-REQUEST-STATUS  PIC X(01).                             
           05  F595F-NUM-REQUEST-FT      PIC X(02).                             
           05  F595F-CDE-NTFT            PIC X(05).                             
           05  FILLER                    PIC X(35).                             
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC                  PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K820-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K800-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K816-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K820-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
           05  WS-CANCEL-NOTELIG         PIC 9(6)  VALUE ZERO.                  
           05  WS-BOOK                   PIC 9(6)  VALUE ZERO.                  
           05  WS-OTHER                  PIC 9(6)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K820-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K820-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BP13K820                                                 
                       BP13K800                                                 
                       BP13K816                                                 
                OUTPUT P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 OPEN ERROR ' WS-K820-STATUS                     
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR ' WS-K800-STATUS                     
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K816-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K816 OPEN ERROR ' WS-K816-STATUS                     
              MOVE WS-K816-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO P13F595F-REC                        
                                            P13F595N-REC                        
                                                                                
                   PERFORM 3000-START-BP13K820 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              MOVE BP13F595-REC          TO P13F595N-REC                        
                                                                                
              WRITE P13F595N-REC                                                
                                                                                
              ADD 1                      TO WS-595N-WRITE-CNT                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-BP13K820.                                                     
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K820-NUM-NRIC                       
                                            WS-K820-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K820-EOF                         
                                            WS-K820-REC-PROCESS.                
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC.                                 
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BP13K820 THRU 3100-EXIT               
                      UNTIL K820-NUM-NRIC NOT = WS-K820-NRIC-PREV               
                         OR WS-K820-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'NRIC NOT FOUND IN K820: ' K820-NUM-NRIC            
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 START ERROR ' WS-K820-STATUS              
                    DISPLAY 'K820 KEY: ' K820-KEY-FLD                           
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-BP13K820.                                                  
      *************************************************************             
      *    DISPLAY '3100=' K820-NUM-REGN                                        
      *            ' '     K820-NUM-NRIC                                        
      *            ' FD='  WS-NRIC-FOUND                                        
      *            ' PRE= ' WS-K820-NRIC-PREV.                                  
           READ BP13K820 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K820-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K820-NUM-NRIC = WS-K820-NRIC-PREV                        
                       PERFORM 3500-READ-BP13K800 THRU 3500-EXIT                
                                                                                
                       MOVE 'Y'          TO WS-K820-REC-PROCESS                 
                    ELSE                                                        
                       IF WS-K820-REC-PROCESS NOT = 'Y'                         
                          CONTINUE                                              
      *                   DISPLAY 'NRIC NOT FOUND IN K820: '                    
      *                           WS-K820-NRIC-PREV                             
                       END-IF                                                   
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'NRIC NOT FOUND IN K820: ' K820-NUM-NRIC            
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ NEXT ERROR ' WS-K820-STATUS          
                    DISPLAY 'K820 KEY: ' K820-KEY-FLD                           
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3500-READ-BP13K800.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE K820-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    PERFORM 4000-PROCESS-FOUND-CASES THRU 4000-EXIT             
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'REGN NOT FOUND IN BP13K800: ' K800-NUM-REGN        
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       4000-PROCESS-FOUND-CASES.                                                
      *************************************************************             
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              PERFORM 4100-PROCESS-BOOKING         THRU 4100-EXIT               
           ELSE                                                                 
              IF K800-NUM-STATUS = 'C'                                          
                 PERFORM 4200-PROCESS-CANCEL       THRU 4200-EXIT               
              ELSE                                                              
                 IF K800-DTE-BK-APPT > WS-CURRENT-DATE1                         
                    PERFORM 4300-PROCESS-PENDING   THRU 4300-EXIT               
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***************************************************************           
       4100-PROCESS-BOOKING.                                                    
      ***************************************************************           
                                                                                
           MOVE K800-NUM-REGN                TO F595F-NUM-REGN                  
           MOVE K800-NUM-ALLO-CAT            TO F595F-NUM-ALLO-CAT              
           MOVE K800-DTE-BALLOT              TO F595F-DTE-BALLOT                
           MOVE K800-NUM-HOUSEHOLD           TO F595F-CDE-HOUSEHOLD             
           MOVE K800-NUM-FLAT-TYPE           TO F595F-NUM-REQUEST-FT            
           MOVE F595-CDE-FLAT-TYPE           TO F595F-CDE-FLAT-TYPE             
           MOVE K800-NUM-NRIC1               TO F595F-NUM-NRIC1                 
           MOVE K800-NUM-NRIC2               TO F595F-NUM-NRIC2                 
           MOVE K800-NUM-SCH-ACC             TO F595F-NUM-SCH-ACC               
           MOVE K800-NUM-NEW-TOWN            TO F595F-NUM-NEW-TOWN              
           MOVE F595-CDE-REQUEST-STATUS TO  F595F-NUM-REQUEST-STATUS            
           MOVE 'SOC'                        TO F595F-NUM-TYPE.                 
                                                                                
           WRITE P13F595F-REC.                                                  
           ADD 1             TO WS-595F-WRITE-CNT.                              
           ADD 1             TO WS-BOOK.                                        
           MOVE 'Y'          TO WS-NRIC-FOUND.                                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       4200-PROCESS-CANCEL.                                                     
      **************************************************************            
                                                                                
           IF K800-NUM-CANCEL-REASON = 'C4' OR 'C6' OR                          
                                       'C7' OR 'D1' OR                          
                                       'C2' OR                                  
                                       'A1' OR                                  
                                       '27' OR '83'                             
              ADD 1 TO WS-CANCEL-NOTELIG                                        
           ELSE                                                                 
              MOVE F595-NUM-REGN          TO F595F-NUM-REGN                     
              MOVE SPACES                 TO F595F-NUM-ALLO-CAT                 
              MOVE SPACES                 TO F595F-DTE-BALLOT                   
              MOVE F595-CDE-HOUSEHOLD     TO F595F-CDE-HOUSEHOLD                
              MOVE F595-CDE-FLAT-TYPE     TO F595F-CDE-FLAT-TYPE                
              MOVE F595-NUM-NRIC1         TO F595F-NUM-NRIC1                    
              MOVE F595-NUM-NRIC2         TO F595F-NUM-NRIC2                    
              MOVE SPACES                 TO F595F-NUM-SCH-ACC                  
              MOVE SPACES                 TO F595F-NUM-NEW-TOWN                 
              MOVE SPACES             TO F595F-NUM-REQUEST-STATUS               
              MOVE 'CAN'                  TO F595F-NUM-TYPE                     
                                                                                
              WRITE P13F595F-REC                                                
                                                                                
              ADD 1             TO WS-595F-WRITE-CNT                            
              MOVE 'Y'          TO WS-NRIC-FOUND                                
              ADD 1 TO WS-OTHER                                                 
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       4300-PROCESS-PENDING.                                                    
      ************************************************************              
                                                                                
           MOVE K800-NUM-REGN          TO F595F-NUM-REGN                        
           MOVE K800-NUM-ALLO-CAT      TO F595F-NUM-ALLO-CAT                    
           MOVE K800-DTE-BALLOT        TO F595F-DTE-BALLOT                      
           MOVE K800-NUM-HOUSEHOLD     TO F595F-CDE-HOUSEHOLD                   
           MOVE F595-CDE-FLAT-TYPE     TO F595F-CDE-FLAT-TYPE                   
           MOVE K800-NUM-FLAT-TYPE     TO F595F-NUM-REQUEST-FT                  
           MOVE K800-NUM-NRIC1         TO F595F-NUM-NRIC1                       
           MOVE K800-NUM-NRIC2         TO F595F-NUM-NRIC2                       
           MOVE K800-NUM-SCH-ACC       TO F595F-NUM-SCH-ACC                     
           IF K800-NUM-NEW-TOWN = SPACES OR LOW-VALUES                          
              PERFORM 7000-DECODE-NT THRU 7000-EXIT                             
           ELSE                                                                 
              MOVE K800-NUM-NEW-TOWN   TO F595F-NUM-NEW-TOWN                    
                                          F595F-CDE-NTFT                        
           END-IF                                                               
           MOVE F595-CDE-REQUEST-STATUS                                         
                                   TO F595F-NUM-REQUEST-STATUS                  
           MOVE 'PED'                  TO F595F-NUM-TYPE                        
                                                                                
           WRITE P13F595F-REC.                                                  
           ADD 1                   TO WS-595F-WRITE-CNT.                        
           MOVE 'Y'                TO WS-NRIC-FOUND.                            
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       7000-DECODE-NT.                                                          
      ************************************************************              
                                                                                
           MOVE SPACES                          TO BP13K816-REC.                
           INITIALIZE                              BP13K816-REC.                
                                                                                
           MOVE K800-NUM-ZONE                   TO K816-NUM-ZONE.               
                                                                                
           READ BP13K816.                                                       
                                                                                
           IF WS-K816-STATUS = 00                                               
              MOVE K816-CDE-NT                  TO F595F-NUM-NEW-TOWN           
              STRING K816-CDE-NT K816-NUM-FLAT-TYPE                             
                 DELIMITED BY SIZE INTO F595F-CDE-NTFT                          
           ELSE                                                                 
              IF WS-K816-STATUS = 23                                            
                 MOVE K800-NUM-ZONE             TO F595F-CDE-NTFT               
                                                   F595F-NUM-NEW-TOWN           
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K816 : '                          
                         WS-K816-STATUS                                         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CY12 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS WRITTEN TO P13F595F : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F595N : ' WS-595N-WRITE-CNT.             
           DISPLAY 'RECS BOOKED FLAT           ' WS-BOOK.                       
           DISPLAY 'RECS CANCEL NOT ELIGIBLE : ' WS-CANCEL-NOTELIG.             
           DISPLAY 'RECS OTHERS              : ' WS-OTHER.                      
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP13K816                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 CLOSE ERROR ' WS-K820-STATUS                    
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 CLOSE ERROR ' WS-K800-STATUS                    
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K816-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K816 CLOSE ERROR ' WS-K816-STATUS                    
              MOVE WS-K816-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
