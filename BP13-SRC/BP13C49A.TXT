      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C49A.                                                 
       AUTHOR.        KV5.                                                      
       DATE-WRITTEN.  23/04/2019.                                               
                                                                                
      *************************************************************             
      * SYSTEM NAME : SYSTEM OF COMMITTMENT - BP13                *             
      *===========================================================*             
      * OBJECTIVE   : DELETING THE RECORDS IN BP13K491 IF THE     *             
      *               REGISTRAION FOUND IN BP13K410 OR NOT FOUND  *             
      *               THE REGISTRAION IN BP13K800 OR              *             
      *               K800-NUM-SCH-ACCT = SPACES OR LOW-VALUES    *             
      *                                                           *             
      * FILES USED  : BP13F491 - INPUT FILE.                      *             
      *               BP13K410 - INPUT FILE.                      *             
      *               BP13K800 - INPUT FILE.                      *             
      *===========================================================*             
      * CHGEREQ  DATE       BY   AMMENDMENTS                      *             
      * -------- ---------- ---- -----------                      *             
      * BP137741 23/04/2019 KV5  NEW PROGRAM                      *             
      *************************************************************             
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F491 ASSIGN           TO BP13F491.                        
           SELECT BP13K410 ASSIGN           TO BP13K410                         
                           ORGANIZATION     IS INDEXED                          
                           ACCESS MODE      IS RANDOM                           
                           RECORD KEY       IS K410-KEY-FLD                     
                           FILE STATUS      IS WS-K410-STATUS.                  
                                                                                
           SELECT BP13K800 ASSIGN           TO BP13K800                         
                           ACCESS MODE      IS DYNAMIC                          
                           ORGANIZATION     IS INDEXED                          
                           RECORD KEY       IS K800-NUM-REGN                    
                           FILE STATUS      IS WS-K800-STATUS.                  
                                                                                
           SELECT BP13K491 ASSIGN           TO BP13K491                         
                           ORGANIZATION     IS INDEXED                          
                           ACCESS MODE      IS RANDOM                           
                           RECORD KEY       IS K491-KEY-FLD                     
                           FILE STATUS      IS WS-K491-STATUS.                  
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F491                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F491.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K491                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K491.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-K410-STATUS                 PIC 9(02) VALUE ZEROS.              
          05 WS-K800-STATUS                 PIC 9(02) VALUE ZEROS.              
          05 WS-K491-STATUS                 PIC 9(02) VALUE ZEROS.              
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F491-READ                   PIC 9(08) VALUE ZEROS.              
          05 WS-K410-FOUND                  PIC 9(08) VALUE ZEROS.              
          05 WS-K800-NOTFD                  PIC 9(08) VALUE ZEROS.              
          05 WS-K800-SCH-SPACE              PIC 9(08) VALUE ZEROS.              
          05 WS-K491-DELETE                 PIC 9(08) VALUE ZEROS.              
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-F491-EOF                    PIC X(01) VALUE 'N'.                
                                                                                
       01 WS-DTE-TME.                                                           
          05 WS-DTE                         PIC X(08) VALUE SPACES.             
          05 WS-TME                         PIC X(08) VALUE SPACES.             
          05 FILLER                         PIC X(05) VALUE SPACES.             
                                                                                
       01 WS-DISPLAY-DATE.                                                      
          05 WS-DD                          PIC X(02) VALUE SPACES.             
          05 FILLER                         PIC X(01) VALUE '/'.                
          05 WS-MM                          PIC X(02) VALUE SPACES.             
          05 FILLER                         PIC X(01) VALUE '/'.                
          05 WS-CCYY                        PIC X(04) VALUE SPACES.             
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *-----------------*                                                       
       0000-MAIN-ROUTINE.                                                       
      *-----------------*                                                       
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F491       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
             UNTIL WS-F491-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       1000-OPEN-FILES.                                                         
      *---------------*                                                         
           OPEN INPUT BP13F491                                                  
                      BP13K410                                                  
                      BP13K800                                                  
                I-O   BP13K491.                                                 
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 OPENING ERROR, STATUS : ' WS-K410-STATUS        
              MOVE WS-K410-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPENING ERROR, STATUS : ' WS-K800-STATUS        
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           IF WS-K491-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K491 OPENING ERROR, STATUS : ' WS-K491-STATUS        
              MOVE WS-K491-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DTE-TME.                      
           MOVE WS-DTE(1:4)                 TO WS-CCYY.                         
           MOVE WS-DTE(5:2)                 TO WS-MM.                           
           MOVE WS-DTE(7:2)                 TO WS-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       2000-READ-BP13F491.                                                      
      *------------------*                                                      
           READ BP13F491                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F491-EOF                      
             NOT AT END                                                         
                ADD 1                       TO WS-F491-READ                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3000-PROCESS-RECORDS.                                                    
      *--------------------*                                                    
      **BP13K410                                                                
           MOVE SPACES                      TO K410-KEY-FLD                     
                                               K491-NUM-REGN                    
           MOVE F491-KEY-FLD                TO K410-KEY-FLD                     
                                               K491-NUM-REGN                    
                                                                                
           READ BP13K410.                                                       
           EVALUATE WS-K410-STATUS                                              
              WHEN 00                                                           
                   ADD 1                    TO WS-K410-FOUND                    
                   PERFORM 4000-K491-DELETE THRU 4000-EXIT                      
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY ' ERROR READING BP13K410 REC '                       
                      ' STATUS ' WS-K410-STATUS                                 
                   DISPLAY ' KEY IS ' K410-KEY-FLD                              
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
      **BP13K800                                                                
           MOVE SPACES                      TO K800-NUM-REGN                    
                                               K491-NUM-REGN                    
           MOVE F491-KEY-FLD                TO K800-NUM-REGN                    
                                               K491-NUM-REGN                    
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                   IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                   
                   ADD 1                 TO WS-K800-SCH-SPACE                   
                   PERFORM 4000-K491-DELETE THRU 4000-EXIT                      
                   END-IF                                                       
              WHEN 23                                                           
                   ADD 1                    TO WS-K800-NOTFD                    
                   PERFORM 4000-K491-DELETE THRU 4000-EXIT                      
              WHEN OTHER                                                        
                   DISPLAY ' ERROR READING BP13K800 REC '                       
                           ' STATUS ' WS-K800-STATUS                            
                   DISPLAY ' KEY IS ' K800-NUM-REGN                             
                   PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F491   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       4000-K491-DELETE.                                                        
      *----------------*                                                        
                                                                                
           DELETE BP13K491.                                                     
                                                                                
           EVALUATE WS-K491-STATUS                                              
               WHEN 00                                                          
                    ADD 1                   TO WS-K491-DELETE                   
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'DELETE ERROR BP13K491 : ' WS-K491-STATUS           
                    MOVE WS-K491-STATUS  TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       9000-CLOSE-FILES.                                                        
      *----------------*                                                        
           CLOSE BP13F491                                                       
                 BP13K410                                                       
                 BP13K800                                                       
                 BP13K491.                                                      
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 CLOSING ERROR, STATUS : ' WS-K410-STATUS        
              MOVE WS-K410-STATUS           TO RETURN-CODE                      
           END-IF                                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 CLOSING ERROR, STATUS : ' WS-K800-STATUS        
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
           END-IF                                                               
                                                                                
           IF WS-K491-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 CLOSING ERROR, STATUS : ' WS-K491-STATUS        
              MOVE WS-K491-STATUS           TO RETURN-CODE                      
           END-IF                                                               
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'PROGRAM NAME   : BP13C49A'.                                 
           DISPLAY 'DATE OF RUN    : ' WS-DISPLAY-DATE.                         
           DISPLAY ' '.                                                         
           DISPLAY 'RECORDS READ FROM BP13F491    : ' WS-F491-READ.             
           DISPLAY 'RECORDS FOUND IN BP13K410     : ' WS-K410-FOUND.            
           DISPLAY 'RECORDS NOT FND IN BP13K800   : ' WS-K800-NOTFD.            
           DISPLAY 'RECORDS SCHEME SPACES IN K800 : ' WS-K800-SCH-SPACE.        
           DISPLAY 'RECODRS DELETED FROM BP13K491 : ' WS-K491-DELETE            
           DISPLAY ' '.                                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
