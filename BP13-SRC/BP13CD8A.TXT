       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CD8A.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 24/05/2019.                                                
      *****************************************************************         
      *               SOC - SYSTEM OF COMMITMENT (BP13)               *         
      *===============================================================*         
      * OBJECTIVES   : TO GENERATE REPORT FOR 2ND-TIMER APPLICANTS    *         
      *                LIVING IN PUBLIC RENTAL FLAT BUYING 2-ROOM OR  *         
      *                3-ROOM FLATS IN NON-MATURE ESTATES             *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F730 I     SEQUENTIAL 500                                 *         
      * BP13F716 I     SEQUENTIAL 350                                 *         
      * BP13K813 I     RANDOM     1000 K813-KEY-FLD                   *         
      * BP13K825 I     RANDOM     200  K825-KEY-FLD                   *         
      * BP13K800 I/O   RANDOM     2000 K800-NUM-REGN                  *         
      * BP13LD8A O     SEQUENTIAL 133                                 *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHGE REQ  DD/MM/YY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP137841  24/05/19  AM25  NEW PROGRAM                         *         
      * BP137841  20/06/19  AM25  ADDT'L CHECK WHEN WRITING/UPDATING  *         
      * BP137841  05/09/19  AM25  NEW COL QUEUE & BYPASS IF IT'S BLANK*         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN      TO BP13F730.                             
           SELECT BP13F716 ASSIGN      TO BP13F716.                             
                                                                                
           SELECT BP13K813 ASSIGN      TO BP13K813                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K813-KEY-FLD                                     
              FILE STATUS   IS WS-K813-STATUS.                                  
                                                                                
           SELECT BP13K825 ASSIGN      TO BP13K825                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K825-KEY-FLD                                     
              FILE STATUS   IS WS-K825-STATUS.                                  
                                                                                
           SELECT BP13K800 ASSIGN      TO BP13K800                              
              ORGANIZATION IS INDEXED                                           
              ACCESS MODE  IS RANDOM                                            
              RECORD KEY   IS K800-NUM-REGN                                     
              FILE STATUS  IS WS-K800-STATUS.                                   
                                                                                
           SELECT BP13LD8A ASSIGN      TO BP13LD8A.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F730.                                                           
                                                                                
       FD  BP13F716                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F716.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13LD8A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 133 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13LD8A-REC                PIC X(133).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K813-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K825-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K800-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-NUM-NRIC1            PIC X(9)        VALUE SPACES.            
           05  WS-NUM-NRIC2            PIC X(9)        VALUE SPACES.            
           05  WS-NUM-NRIC3            PIC X(9)        VALUE SPACES.            
           05  WS-NUM-NRIC4            PIC X(9)        VALUE SPACES.            
           05  WS-NUM-HDB-REF-RDB      PIC X(11)       VALUE SPACES.            
           05  WS-NUM-RDB-FT           PIC X(2)        VALUE SPACES.            
           05  WS-NUM-NEW-TOWN-RDB     PIC X(3)        VALUE SPACES.            
           05  WS-DTE-MOVEIN           PIC X(8)        VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-F716-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K813-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K825-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K800-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K800-UPDT            PIC 9(6)        VALUE ZEROES.            
           05  WS-LD8A-WRTE            PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F730-EOF             PIC X           VALUE 'N'.               
           05  WS-F716-EOF             PIC X           VALUE 'N'.               
           05  WS-LD8A-OK              PIC X           VALUE 'N'.               
                                                                                
      *------------------------------------------------------------*            
      *                  REPORT LAYOUT - BP13LD8A                  *            
      *------------------------------------------------------------*            
       01  LD8A-HEADER.                                                         
           05  FILLER                  PIC X(8)        VALUE 'REGN'.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(9)        VALUE 'NRIC1'.           
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(9)        VALUE 'NRIC2'.           
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(9)        VALUE 'NRIC3'.           
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(9)        VALUE 'NRIC4'.           
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'CAT'.             
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'NT'.              
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE 'FT'.              
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'HH '.             
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'BHH'.             
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE 'HHINC'.           
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'ALO'.             
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE 'ELIG'.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(11)       VALUE 'RDB SCH'.         
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE 'FT'.              
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'NT'.              
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(8)        VALUE 'MOVE IN'.         
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE 'QUEUE'.           
           05  FILLER                  PIC X(17)       VALUE SPACES.            
                                                                                
       01  LD8A-DETAIL.                                                         
           05  LD8A-NUM-REGN           PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-NRIC1          PIC X(9)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-NRIC2          PIC X(9)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-NRIC3          PIC X(9)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-NRIC4          PIC X(9)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-CATEGORY       PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-NEW-TOWN       PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-FLAT-TYPE      PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-HOUSEHOLD      PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-BALLOT-HH      PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-HH-INCOME      PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-ALLO-SCH       PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-ELIG-SCH       PIC X(4)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-HDB-REF-RDB    PIC X(11)       VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-RDB-FT         PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-NEW-TOWN-RDB   PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-DTE-MOVEIN         PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LD8A-NUM-NT-FT-QUEUE    PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(17)       VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F730   THRU 2000-EXIT.                         
           PERFORM 2500-READ-BP13F716   THRU 2500-EXIT.                         
           PERFORM 3000-MAIN-PROCESSING THRU 3000-EXIT                          
              UNTIL WS-F730-EOF = 'Y' AND WS-F716-EOF = 'Y'.                    
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F716                                                 
                       BP13K813                                                 
                       BP13K825                                                 
                I-O    BP13K800                                                 
                OUTPUT BP13LD8A.                                                
                                                                                
           IF (WS-K813-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K813 FAIL STATUS=' WS-K813-STATUS               
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K825-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K825 FAIL STATUS=' WS-K825-STATUS               
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K800-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K800 FAIL STATUS=' WS-K800-STATUS               
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F730.                                                      
      *****************************************************************         
                                                                                
           READ BP13F730                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F730-NUM-REGN                         
                 MOVE 'Y'              TO WS-F730-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F730-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2500-READ-BP13F716.                                                      
      *****************************************************************         
                                                                                
           READ BP13F716                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F716-NUM-REGN                         
                 MOVE 'Y'              TO WS-F716-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F716-READ                          
           END-READ.                                                            
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           EVALUATE TRUE                                                        
              WHEN F730-NUM-REGN = F716-NUM-REGN                                
                 PERFORM 3500-READ-BP13K813 THRU 3500-EXIT                      
                                                                                
                 IF (F730-NUM-ALLO-CAT = 'ROF' OR                               
                     K813-NUM-MATURE-EST-TAG = 'N') AND                         
                    (F730-NUM-NT-FT-QUEUE NOT = SPACES AND LOW-VALUES)          
                    PERFORM 4500-PROCESS-F716 THRU 4500-EXIT                    
                    PERFORM 4600-CHECK-APPLT  THRU 4600-EXIT                    
                 ELSE                                                           
                    PERFORM 2000-READ-BP13F730 THRU 2000-EXIT                   
                    PERFORM 2500-READ-BP13F716 THRU 2500-EXIT                   
                       UNTIL F730-NUM-REGN NOT = F716-NUM-REGN                  
                 END-IF                                                         
              WHEN F730-NUM-REGN < F716-NUM-REGN                                
                 PERFORM 2000-READ-BP13F730 THRU 2000-EXIT                      
              WHEN F730-NUM-REGN > F716-NUM-REGN                                
                 PERFORM 2500-READ-BP13F716 THRU 2500-EXIT                      
                    UNTIL F730-NUM-REGN <= F716-NUM-REGN                        
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3500-READ-BP13K813.                                                      
      *****************************************************************         
                                                                                
           MOVE F730-CDE-NT1           TO K813-NUM-ZONE.                        
           MOVE F730-CDE-FLAT-TYPE     TO K813-NUM-FLAT-TYPE.                   
           MOVE F730-DTE-BALLOT        TO K813-DTE-BALLOT.                      
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K813-READ                          
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K813: '                          
                    'REGN=' F730-NUM-REGN ',KEY=' K813-KEY-FLD                  
                                                                                
                 MOVE SPACES           TO K813-NUM-MATURE-EST-TAG               
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K813 FAIL STATUS=' WS-K813-STATUS            
                 MOVE WS-K813-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4500-PROCESS-F716.                                                       
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO WS-NUM-NRIC1                          
                                          WS-NUM-NRIC2                          
                                          WS-NUM-NRIC3                          
                                          WS-NUM-NRIC4.                         
                                                                                
           MOVE F716-NUM-HDB-REF       TO WS-NUM-HDB-REF-RDB.                   
           MOVE F716-CDE-FLAT-TYPE-RESIDE                                       
                                       TO WS-NUM-RDB-FT.                        
           MOVE F716-CDE-NT-RESIDE     TO WS-NUM-NEW-TOWN-RDB.                  
           MOVE F716-DTE-MOVING-IN     TO WS-DTE-MOVEIN.                        
                                                                                
           PERFORM UNTIL F730-NUM-REGN NOT = F716-NUM-REGN                      
              EVALUATE TRUE                                                     
                 WHEN F716-NUM-NRIC = F730-NUM-NRIC1                            
                    MOVE F716-NUM-NRIC TO WS-NUM-NRIC1                          
                 WHEN F716-NUM-NRIC = F730-NUM-NRIC2                            
                    MOVE F716-NUM-NRIC TO WS-NUM-NRIC2                          
                 WHEN F716-NUM-NRIC = F730-NUM-NRIC3                            
                    MOVE F716-NUM-NRIC TO WS-NUM-NRIC3                          
                 WHEN F716-NUM-NRIC = F730-NUM-NRIC4                            
                    MOVE F716-NUM-NRIC TO WS-NUM-NRIC4                          
                 WHEN OTHER                                                     
                    CONTINUE                                                    
              END-EVALUATE                                                      
                                                                                
              PERFORM 2500-READ-BP13F716 THRU 2500-EXIT                         
           END-PERFORM.                                                         
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4600-CHECK-APPLT.                                                        
      *****************************************************************         
                                                                                
           IF  WS-NUM-NRIC1 = SPACES AND WS-NUM-NRIC2 = SPACES                  
           AND WS-NUM-NRIC3 = SPACES AND WS-NUM-NRIC4 = SPACES                  
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 4610-CHECK-HA1 THRU 4610-EXIT                             
              PERFORM 4620-CHECK-HA2 THRU 4620-EXIT                             
              PERFORM 4630-CHECK-HA3 THRU 4630-EXIT                             
              PERFORM 4640-CHECK-HA4 THRU 4640-EXIT                             
                                                                                
              IF WS-LD8A-OK = 'Y'                                               
                 PERFORM 5500-WRITE-BP13LD8A THRU 5500-EXIT                     
                 PERFORM 6000-READ-BP13K800  THRU 6000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730 THRU 2000-EXIT.                           
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4610-CHECK-HA1.                                                          
      *****************************************************************         
                                                                                
           MOVE 'N'                    TO WS-LD8A-OK.                           
                                                                                
           IF WS-NUM-NRIC1 = SPACES OR LOW-VALUES                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE F730-NUM-REGN       TO K825-NUM-REGN                         
              MOVE WS-NUM-NRIC1        TO K825-NUM-NRIC                         
              PERFORM 4700-READ-BP13K825 THRU 4700-EXIT                         
              PERFORM 4800-CHECK-K825    THRU 4800-EXIT                         
           END-IF.                                                              
                                                                                
       4610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4620-CHECK-HA2.                                                          
      *****************************************************************         
                                                                                
           IF WS-LD8A-OK = 'N'                                                  
              IF WS-NUM-NRIC2 = SPACES OR LOW-VALUES                            
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE F730-NUM-REGN    TO K825-NUM-REGN                         
                 MOVE WS-NUM-NRIC2     TO K825-NUM-NRIC                         
                 PERFORM 4700-READ-BP13K825 THRU 4700-EXIT                      
                 PERFORM 4800-CHECK-K825    THRU 4800-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4620-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4630-CHECK-HA3.                                                          
      *****************************************************************         
                                                                                
           IF WS-LD8A-OK = 'N'                                                  
              IF WS-NUM-NRIC3 = SPACES OR LOW-VALUES                            
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE F730-NUM-REGN    TO K825-NUM-REGN                         
                 MOVE WS-NUM-NRIC3     TO K825-NUM-NRIC                         
                 PERFORM 4700-READ-BP13K825 THRU 4700-EXIT                      
                 PERFORM 4800-CHECK-K825    THRU 4800-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4630-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4640-CHECK-HA4.                                                          
      *****************************************************************         
                                                                                
           IF WS-LD8A-OK = 'N'                                                  
              IF WS-NUM-NRIC4 = SPACES OR LOW-VALUES                            
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE F730-NUM-REGN    TO K825-NUM-REGN                         
                 MOVE WS-NUM-NRIC4     TO K825-NUM-NRIC                         
                 PERFORM 4700-READ-BP13K825 THRU 4700-EXIT                      
                 PERFORM 4800-CHECK-K825    THRU 4800-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4640-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4700-READ-BP13K825.                                                      
      *****************************************************************         
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K825-READ                          
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K825: ' K825-KEY-FLD             
                                                                                
                 MOVE SPACES           TO K825-NUM-MARITAL-STATUS               
                                          K825-NUM-CITIZENSHIP                  
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K825 FAIL STATUS=' WS-K825-STATUS            
                 MOVE WS-K825-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4800-CHECK-K825.                                                         
      *****************************************************************         
                                                                                
           EVALUATE K825-NUM-MARITAL-STATUS                                     
              WHEN '1'                                                          
                 IF F730-CDE-ELIG-SCH = 'PAS'                                   
                    MOVE 'Y'           TO WS-LD8A-OK                            
                 END-IF                                                         
              WHEN '2'                                                          
                 IF K825-NUM-CITIZENSHIP = '10'                                 
                    MOVE 'Y'           TO WS-LD8A-OK                            
                 ELSE                                                           
                    PERFORM 4900-CHECK-HA-SPOUSE THRU 4900-EXIT                 
                 END-IF                                                         
              WHEN '3'                                                          
              WHEN '4'                                                          
              WHEN '5'                                                          
                 IF K825-NUM-CITIZENSHIP = '10'                                 
                    MOVE 'Y'           TO WS-LD8A-OK                            
                 END-IF                                                         
              WHEN OTHER                                                        
                 CONTINUE                                                       
           END-EVALUATE.                                                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4900-CHECK-HA-SPOUSE.                                                    
      *****************************************************************         
                                                                                
           IF K825-NUM-NRIC-SPOUSE = SPACES OR LOW-VALUES                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE F730-NUM-REGN       TO K825-NUM-REGN                         
              MOVE K825-NUM-NRIC-SPOUSE                                         
                                       TO K825-NUM-NRIC                         
              PERFORM 4700-READ-BP13K825 THRU 4700-EXIT                         
                                                                                
              IF K825-NUM-CITIZENSHIP = '10' OR '20'                            
                 MOVE 'Y'              TO WS-LD8A-OK                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5500-WRITE-BP13LD8A.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO LD8A-DETAIL.                          
           INITIALIZE LD8A-DETAIL.                                              
                                                                                
           MOVE F730-NUM-REGN          TO LD8A-NUM-REGN.                        
           MOVE WS-NUM-NRIC1           TO LD8A-NUM-NRIC1.                       
           MOVE WS-NUM-NRIC2           TO LD8A-NUM-NRIC2.                       
           MOVE WS-NUM-NRIC3           TO LD8A-NUM-NRIC3.                       
           MOVE WS-NUM-NRIC4           TO LD8A-NUM-NRIC4.                       
           MOVE F730-CDE-CATEGORY      TO LD8A-NUM-CATEGORY.                    
           MOVE K813-CDE-NT            TO LD8A-NUM-NEW-TOWN.                    
           MOVE F730-CDE-FLAT-TYPE     TO LD8A-NUM-FLAT-TYPE.                   
           MOVE F730-CDE-HOUSEHOLD     TO LD8A-NUM-HOUSEHOLD.                   
           MOVE F730-CDE-BALLOT-HOUSEHOLD                                       
                                       TO LD8A-NUM-BALLOT-HH.                   
           IF F730-AMT-HSE-INCOME IS NUMERIC                                    
              MOVE F730-AMT-HSE-INCOME TO LD8A-NUM-HH-INCOME                    
           ELSE                                                                 
              MOVE ZEROES              TO LD8A-NUM-HH-INCOME                    
           END-IF.                                                              
           MOVE F730-CDE-ALLOC-SCH     TO LD8A-NUM-ALLO-SCH.                    
           MOVE F730-CDE-ELIG-SCH      TO LD8A-NUM-ELIG-SCH.                    
           MOVE WS-NUM-HDB-REF-RDB     TO LD8A-NUM-HDB-REF-RDB.                 
           MOVE WS-NUM-RDB-FT          TO LD8A-NUM-RDB-FT.                      
           MOVE WS-NUM-NEW-TOWN-RDB    TO LD8A-NUM-NEW-TOWN-RDB.                
           MOVE WS-DTE-MOVEIN          TO LD8A-DTE-MOVEIN.                      
           MOVE F730-NUM-NT-FT-QUEUE   TO LD8A-NUM-NT-FT-QUEUE.                 
                                                                                
           IF WS-LD8A-WRTE = ZEROES                                             
              WRITE BP13LD8A-REC FROM LD8A-HEADER                               
           END-IF.                                                              
                                                                                
           WRITE BP13LD8A-REC FROM LD8A-DETAIL.                                 
           ADD 1                       TO WS-LD8A-WRTE.                         
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6000-READ-BP13K800.                                                      
      *****************************************************************         
                                                                                
           MOVE F730-NUM-REGN          TO K800-NUM-REGN.                        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K800-READ                          
                                                                                
                 PERFORM 6100-UPDATE-BP13K800 THRU 6100-EXIT                    
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K800: ' F730-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K800 FAIL STATUS=' WS-K800-STATUS            
                 MOVE WS-K800-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6100-UPDATE-BP13K800.                                                    
      *****************************************************************         
                                                                                
           MOVE 'T'                    TO K800-NUM-STEPUP.                      
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS = ZEROES                                           
              ADD 1                    TO WS-K800-UPDT                          
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K800 FAIL STATUS=' WS-K800-STATUS            
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F730                                                       
                 BP13F716                                                       
                 BP13K813                                                       
                 BP13K825                                                       
                 BP13K800                                                       
                 BP13LD8A.                                                      
                                                                                
           IF WS-K813-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K813 FAIL STATUS=' WS-K813-STATUS              
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K825 FAIL STATUS=' WS-K825-STATUS              
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K800 FAIL STATUS=' WS-K800-STATUS              
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*******************************'.                           
           DISPLAY '* PROGRAM BP13CD8A            *'.                           
           DISPLAY '* CONTROL COUNTS              *'.                           
           DISPLAY '*-----------------------------*'.                           
           DISPLAY '* REC READ F730      : ' WS-F730-READ ' *'.                 
           DISPLAY '* REC READ F716      : ' WS-F716-READ ' *'.                 
           DISPLAY '* REC READ K813      : ' WS-K813-READ ' *'.                 
           DISPLAY '* REC READ K825      : ' WS-K825-READ ' *'.                 
           DISPLAY '* REC READ K800      : ' WS-K800-READ ' *'.                 
           DISPLAY '* REC UPDT K800      : ' WS-K800-UPDT ' *'.                 
           DISPLAY '* REC WRTE LD8A      : ' WS-LD8A-WRTE ' *'.                 
           DISPLAY '*******************************'.                           
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
