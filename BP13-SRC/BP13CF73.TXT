       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF73.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  22/01/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : LIST OF CASES WITH PPS ELIGIBLE, PPS NOT    *            
      *                ELIGIBLE AND PPS PENDING                    *            
      *                                                            *            
      * INPUT  FILE  : BP13F730 - CASES PER REGN                   *            
      *                BP13K825 - BP13.K825.REQOCCP                *            
      *                BP13K060 - BP13.K060.CODETAB                *            
      * OUTPUT FILE  : P13LF73A - PPS ELIGIBLE                     *            
      *                P13LF73B - PPS NOT ELIGIBLE                 *            
      *                P13LF73C - PPS PENDING                      *            
      *                P13LF73D - PPS PREGNANT                     *            
      *                P13LF73E - GEN3 ELIGIBLE                    *            
      *                P13LF73F - GEN3 PENDING                     *            
      *                P13LF73G - FPPS                             *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134626  SMR2  22012013  NEW PROGRAM                      *            
      * BP134906  SMR2  28052013  CHANGE NUM-YOUNGEST-SC-CHILD TO  *            
      *                           NUM-YOUNG-CHILD-AGE-SC           *            
      * BP134996  SMR2  08072013  COMPUTE AGE-SC & ADD NEW REPORT  *            
      *                           FOR PPS PREGNANT                 *            
      * BP135059  SMR2  10092013  NEW REPORTS FOR NUM-GEN3         *            
      * BP135190  SMR2  04122013  EXPAND BP13K813 TO 1000          *            
      * BP137132  KR13  30072018  ADDED PAGE BREAK FOR REPORTS     *            
      * BP137749  AM25  27052019  CATER FOR CHILD AGE INCR TO 18   *            
      * BP139673  AM25  15092023  CATER FOR HFE SUBMISSION DATE    *            
      * BP139780  AM25  30112023  CATER FOR PPS TAG 'F'            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS WS-K825-STATUS.                      
                                                                                
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS WS-K060-STATUS.                      
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
           SELECT P13LF73A ASSIGN       TO P13LF73A.                            
           SELECT P13LF73B ASSIGN       TO P13LF73B.                            
           SELECT P13LF73C ASSIGN       TO P13LF73C.                            
           SELECT P13LF73D ASSIGN       TO P13LF73D.                            
           SELECT P13LF73E ASSIGN       TO P13LF73E.                            
           SELECT P13LF73F ASSIGN       TO P13LF73F.                            
           SELECT P13LF73G ASSIGN       TO P13LF73G.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS   25 CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   P13LF73A                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  131 CHARACTERS.                                    
       01   P13LF73A-REC                   PIC X(131).                          
                                                                                
       FD   P13LF73B                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  131 CHARACTERS.                                    
       01   P13LF73B-REC                   PIC X(131).                          
                                                                                
       FD   P13LF73C                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  131 CHARACTERS.                                    
       01   P13LF73C-REC                   PIC X(131).                          
                                                                                
       FD   P13LF73D                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  131 CHARACTERS.                                    
       01   P13LF73D-REC                   PIC X(131).                          
                                                                                
       FD   P13LF73E                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  131 CHARACTERS.                                    
       01   P13LF73E-REC                   PIC X(131).                          
                                                                                
       FD   P13LF73F                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  131 CHARACTERS.                                    
       01   P13LF73F-REC                   PIC X(131).                          
                                                                                
       FD   P13LF73G                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  131 CHARACTERS.                                    
       01   P13LF73G-REC                   PIC X(131).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K825-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K060-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K813-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-YES-SNO                  PIC 9(05)   VALUE ZEROES.            
           05  WS-NO-SNO                   PIC 9(05)   VALUE ZEROES.            
           05  WS-NO-OTHERS                PIC 9(05)   VALUE ZEROES.            
           05  WS-FPPS-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-PEND-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-PREG-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-GEN3-Y-SNO               PIC 9(05)   VALUE ZEROES.            
           05  WS-GEN3-P-SNO               PIC 9(05)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-OCCP-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-YES-HDR                  PIC X(01)   VALUE 'N'.               
           05  WS-NO-HDR                   PIC X(01)   VALUE 'N'.               
           05  WS-FPPS-HDR                 PIC X(01)   VALUE 'N'.               
           05  WS-PEND-HDR                 PIC X(01)   VALUE 'N'.               
           05  WS-PREG-HDR                 PIC X(01)   VALUE 'N'.               
           05  WS-GEN3-Y-HDR               PIC X(01)   VALUE 'N'.               
           05  WS-GEN3-P-HDR               PIC X(01)   VALUE 'N'.               
           05  WS-BYPASS-REPORT            PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
           05  WS-DTE-REQUEST              PIC 9(08)   VALUE ZEROES.            
           05  WS-DTE-CHILD-BIRTH          PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
           05  WS-NUM-AGE-CHILD            PIC 9(02)   VALUE ZEROES.            
           05  WS-NUM-AGE                  PIC 9(08)   VALUE ZEROES.            
           05  WS-CHILD-AGE REDEFINES WS-NUM-AGE.                               
               10  WS-AGE-CY               PIC 9(04).                           
               10  WS-AGE-MM               PIC 9(02).                           
               10  WS-AGE-DD               PIC 9(02).                           
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LF73-HEADER1.                                                      
             10 LF73-RPTNAME      PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 LF73-DATE         PIC X(12) VALUE SPACES.                       
          05 LF73-HEADER2.                                                      
             10 FILLER            PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(26) VALUE                               
                'LIST OF PPS ELIGIBLE CASES'.                                   
          05 LF73-HEADER3.                                                      
             10 FILLER            PIC X(45) VALUE SPACES.                       
             10 FILLER            PIC X(30) VALUE                               
                'LIST OF PPS NOT ELIGIBLE CASES'.                               
          05 LF73-HEADER4.                                                      
             10 FILLER            PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(25) VALUE                               
                'LIST OF PPS PENDING CASES'.                                    
          05 LF73-HEADER5.                                                      
             10 FILLER            PIC X(46) VALUE                               
                '   S/NO      REG NO               RELATIONSHIP'.               
             10 FILLER            PIC X(47) VALUE                               
                '             MARITAL STATUS     BIRTHDATE      '.              
             10 FILLER            PIC X(11) VALUE 'CITIZENSHIP'.                
          05 LF73-HEADER6.                                                      
             10 FILLER            PIC X(43) VALUE SPACES.                       
             10 FILLER            PIC X(35) VALUE                               
                'LIST OF PPS PREGNANT ELIGIBLE CASES'.                          
          05 LF73-HEADER7.                                                      
             10 FILLER            PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(27) VALUE                               
                'LIST OF GEN3 ELIGIBLE CASES'.                                  
          05 LF73-HEADER8.                                                      
             10 FILLER            PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(26) VALUE                               
                'LIST OF GEN3 PENDING CASES'.                                   
          05 LF73-HEADER9.                                                      
             10 FILLER            PIC X(52) VALUE                               
                '   S/NO    REG NO     NRIC              RELATIONSHIP'.         
             10 FILLER            PIC X(51) VALUE                               
                '           MARITAL STATUS   CITIZENSHIP            '.          
             10 FILLER            PIC X(18) VALUE 'FLAT TYPE   ESTATE'.         
          05 LF73-HEADER10.                                                     
             10 FILLER            PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(18) VALUE                               
                'LIST OF FPPS CASES'.                                           
          05 LF73-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
          05 LF73-YES-REC.                                                      
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-YES-SNO      PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-YES-REGN     PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-YES-DET      PIC X(87) VALUE SPACES.                       
          05 LF73-NO-REC.                                                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-NO-SNO       PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-NO-REGN      PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-NO-DET       PIC X(87) VALUE SPACES.                       
          05 LF73-PEND-REC.                                                     
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-PEND-SNO     PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-PEND-REGN    PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-PEND-DET     PIC X(87) VALUE SPACES.                       
          05 LF73-PREG-REC.                                                     
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-PREG-SNO     PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-PREG-REGN    PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-PREG-DET     PIC X(87) VALUE SPACES.                       
          05 LF73-DET-REC.                                                      
             10 LF73-DET-APPLN    PIC X(03) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-DET-REL      PIC X(20) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-DET-MS       PIC X(14) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-DET-BIRTH    PIC X(10) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-DET-CITZ     PIC X(20) VALUE SPACES.                       
          05 LF73-GEN3-Y-REC.                                                   
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-Y-SNO   PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-Y-REGN  PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-Y-DET   PIC X(78) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-Y-FT    PIC X(07) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-GEN3-Y-NT    PIC X(03) VALUE SPACES.                       
          05 LF73-GEN3-P-REC.                                                   
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-P-SNO   PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-P-REGN  PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-P-DET   PIC X(78) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-P-FT    PIC X(07) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-GEN3-P-NT    PIC X(03) VALUE SPACES.                       
          05 LF73-GEN3-DET-REC.                                                 
             10 LF73-GEN3-D-NRIC  PIC X(09) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-D-APPLN PIC X(03) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-D-REL   PIC X(20) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-D-MS    PIC X(14) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-GEN3-D-CITZ  PIC X(20) VALUE SPACES.                       
          05 LF73-FPPS-REC.                                                     
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-FPPS-SNO     PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-FPPS-REGN    PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-FPPS-DET     PIC X(87) VALUE SPACES.                       
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                       BP13K060                                                 
                       BP13K813                                                 
                OUTPUT P13LF73A                                                 
                       P13LF73B                                                 
                       P13LF73C                                                 
                       P13LF73D                                                 
                       P13LF73E                                                 
                       P13LF73F                                                 
                       P13LF73G.                                                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K060 FILE ERROR ' WS-K060-STATUS             
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K813 FILE ERROR ' WS-K813-STATUS             
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
             INTO LF73-DATE.                                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
           PERFORM 3000-PROCESS-RECS       THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           MOVE 'N'                        TO WS-K825-FND                       
                                              WS-BYPASS-REPORT.                 
           INITIALIZE                         WS-NUM-AGE-CHILD.                 
                                                                                
           IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                     
              EVALUATE F730-NUM-PPS-ELIG-TAG                                    
              WHEN 'Y'                                                          
                 ADD 1                     TO WS-YES-SNO                        
                                                                                
              WHEN 'F'                                                          
                 ADD 1                     TO WS-FPPS-SNO                       
                                                                                
              WHEN 'N'                                                          
                 PERFORM 5300-STARTBR-K825 THRU 5300-EXIT                       
                 IF WS-NUM-AGE-CHILD <= 18 AND                                  
                    WS-NUM-AGE-CHILD NOT = ZEROES                               
                    ADD 1                  TO WS-NO-SNO                         
                 ELSE                                                           
                    ADD 1                  TO WS-NO-OTHERS                      
                    MOVE 'Y'               TO WS-BYPASS-REPORT                  
                 END-IF                                                         
                                                                                
              WHEN 'P'                                                          
                 ADD 1                     TO WS-PEND-SNO                       
                                                                                
              WHEN 'E'                                                          
                 ADD 1                     TO WS-PREG-SNO                       
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           EVALUATE F730-NUM-GEN3                                               
           WHEN 'Y'                                                             
              ADD 1                        TO WS-GEN3-Y-SNO                     
                                                                                
           WHEN 'P'                                                             
              ADD 1                        TO WS-GEN3-P-SNO                     
           END-EVALUATE.                                                        
                                                                                
           MOVE 'HA1'                      TO LF73-DET-APPLN                    
                                              LF73-GEN3-D-APPLN.                
           MOVE F730-NUM-NRIC1             TO WS-NRIC.                          
           PERFORM 4000-READ-BP13K825      THRU 4000-EXIT.                      
           IF WS-K825-FND = 'Y'                                                 
              PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                       
           END-IF.                                                              
           PERFORM 4300-CHECK-OTHER-HA     THRU 4300-EXIT.                      
           PERFORM 5000-STARTBR-K825       THRU 5000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K825.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
           MOVE WS-NRIC                    TO K825-NUM-NRIC                     
                                              LF73-GEN3-D-NRIC.                 
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K825-FND                       
              STRING K825-DTE-BIRTH(7:2), '/',                                  
                     K825-DTE-BIRTH(5:2), '/',                                  
                     K825-DTE-BIRTH(1:4) DELIMITED BY SIZE                      
                INTO LF73-DET-BIRTH                                             
              PERFORM 4100-MOVE-DECODED    THRU 4100-EXIT                       
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K825-FND                       
              DISPLAY 'RECORD NOT FOUND IN BP13K825 : ' K825-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K825'                               
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4100-MOVE-DECODED.                                                       
      ********************                                                      
                                                                                
           IF K825-NUM-RELATIONSHIP NOT = SPACES AND LOW-VALUES                 
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '35'                    TO K060-SERIAL-NO                    
              MOVE K825-NUM-RELATIONSHIP   TO K060-CODE                         
              PERFORM 4200-READ-BP13K060   THRU 4200-EXIT                       
              MOVE K060-DESC               TO LF73-DET-REL                      
                                              LF73-GEN3-D-REL                   
           END-IF.                                                              
                                                                                
           IF K825-NUM-MARITAL-STATUS NOT = SPACES AND LOW-VALUES               
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '21'                    TO K060-SERIAL-NO                    
              MOVE K825-NUM-MARITAL-STATUS TO K060-CODE                         
              PERFORM 4200-READ-BP13K060   THRU 4200-EXIT                       
              MOVE K060-DESC               TO LF73-DET-MS                       
                                              LF73-GEN3-D-MS                    
           END-IF.                                                              
                                                                                
           IF K825-NUM-CITIZENSHIP NOT = SPACES AND LOW-VALUES                  
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '25'                    TO K060-SERIAL-NO                    
              MOVE K825-NUM-CITIZENSHIP    TO K060-CODE                         
              PERFORM 4200-READ-BP13K060   THRU 4200-EXIT                       
              MOVE K060-DESC               TO LF73-DET-CITZ                     
                                              LF73-GEN3-D-CITZ                  
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4200-READ-BP13K060.                                                      
      *********************                                                     
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
           WHEN '00'                                                            
              CONTINUE                                                          
                                                                                
           WHEN '23'                                                            
              DISPLAY 'BP13K060 RECORD NOT FOUND : '                            
                       K060-SERIAL-NO ' ' K060-CODE                             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K060 : ' WS-K060-STATUS                
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4300-CHECK-OTHER-HA.                                                     
      **********************                                                    
                                                                                
           IF F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE 'HA2'                TO LF73-DET-APPLN                    
                                              LF73-GEN3-D-APPLN                 
                 MOVE F730-NUM-NRIC2       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4300-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC3(1:1) NOT = '#'                                  
                 MOVE 'HA3'                TO LF73-DET-APPLN                    
                                              LF73-GEN3-D-APPLN                 
                 MOVE F730-NUM-NRIC3       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4300-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC4(1:1) NOT = '#'                                  
                 MOVE 'HA4'                TO LF73-DET-APPLN                    
                                              LF73-GEN3-D-APPLN                 
                 MOVE F730-NUM-NRIC4       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5000-STARTBR-K825.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K825-EOF                       
                                              WS-OCCP-FND.                      
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 5100-READNEXT-K825   THRU 5100-EXIT                       
              PERFORM 5200-PROCESS-K825    THRU 5200-EXIT                       
                UNTIL WS-K825-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K825 : ' WS-K825-STATUS                  
                      ' REGN : ' K825-NUM-REGN                                  
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5100-READNEXT-K825.                                                      
      *********************                                                     
                                                                                
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K825 : ' K825-KEY-FLD            
              MOVE 'Y'                     TO WS-K825-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K825'                               
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5200-PROCESS-K825.                                                       
      ********************                                                      
                                                                                
           IF F730-NUM-REGN = K825-NUM-REGN                                     
              IF K825-NUM-NRIC NOT = F730-NUM-NRIC1 AND                         
                                     F730-NUM-NRIC2 AND                         
                                     F730-NUM-NRIC3 AND                         
                                     F730-NUM-NRIC4                             
                 IF WS-OCCP-FND = 'N'                                           
                    MOVE 'OCC'             TO LF73-DET-APPLN                    
                                              LF73-GEN3-D-APPLN                 
                    MOVE 'Y'               TO WS-OCCP-FND                       
                 ELSE                                                           
                    MOVE SPACES            TO LF73-DET-APPLN                    
                                              LF73-GEN3-D-APPLN                 
                 END-IF                                                         
                 STRING K825-DTE-BIRTH(7:2), '/',                               
                        K825-DTE-BIRTH(5:2), '/',                               
                        K825-DTE-BIRTH(1:4) DELIMITED BY SIZE                   
                   INTO LF73-DET-BIRTH                                          
                 MOVE K825-NUM-NRIC        TO LF73-GEN3-D-NRIC                  
                 PERFORM 4100-MOVE-DECODED THRU 4100-EXIT                       
                 PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K825-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 5100-READNEXT-K825      THRU 5100-EXIT.                      
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5300-STARTBR-K825.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K825-EOF.                      
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 5100-READNEXT-K825   THRU 5100-EXIT                       
              PERFORM 5400-PROCESS-AGE     THRU 5400-EXIT                       
                UNTIL WS-K825-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K825 : ' WS-K825-STATUS                  
                      ' REGN : ' K825-NUM-REGN                                  
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       5400-PROCESS-AGE.                                                        
      *******************                                                       
                                                                                
           IF F730-NUM-REGN = K825-NUM-REGN                                     
              IF K825-NUM-RELATIONSHIP = '00' OR '09'                           
                 CONTINUE                                                       
              ELSE                                                              
                 PERFORM 6000-COMPUTE-AGE  THRU 6000-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K825-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 5100-READNEXT-K825      THRU 5100-EXIT.                      
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       6000-COMPUTE-AGE.                                                        
      *******************                                                       
                                                                                
           IF K825-NUM-MARITAL-STATUS NOT = '1'                                 
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 6100-READ-BP13K813      THRU 6100-EXIT.                      
                                                                                
           IF F730-DTE-REQUEST NOT NUMERIC                                      
              MOVE ZEROES                  TO WS-DTE-REQUEST                    
           ELSE                                                                 
              MOVE F730-DTE-REQUEST        TO WS-DTE-REQUEST                    
           END-IF.                                                              
                                                                                
           IF K825-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES                  TO WS-DTE-CHILD-BIRTH                
           ELSE                                                                 
              MOVE K825-DTE-BIRTH          TO WS-DTE-CHILD-BIRTH                
           END-IF.                                                              
                                                                                
           IF WS-DTE-REQUEST > WS-DTE-CHILD-BIRTH                               
              COMPUTE WS-NUM-AGE = WS-DTE-REQUEST -                             
                                   WS-DTE-CHILD-BIRTH                           
           ELSE                                                                 
      *** FOR CHILD THAT WAS BORN AFTER LAUNCH DATE ***                         
              MOVE 1                       TO WS-AGE-CY                         
           END-IF.                                                              
                                                                                
      *** FOR CHILD LESS THAN 1 YEAR OLD ***                                    
           IF (WS-AGE-CY = ZEROES) AND                                          
              (WS-AGE-MM > ZEROES OR WS-AGE-DD > ZEROES)                        
              COMPUTE WS-AGE-CY = WS-AGE-CY + 1                                 
           END-IF.                                                              
                                                                                
      *** FOR SINGLE SINGAPORE CITIZEN CHILD ***                                
           IF K825-NUM-MARITAL-STATUS = '1' AND                                 
              K825-NUM-CITIZENSHIP = '10'                                       
              IF WS-NUM-AGE-CHILD = ZEROES                                      
                 MOVE WS-AGE-CY            TO WS-NUM-AGE-CHILD                  
              ELSE                                                              
                 IF WS-AGE-CY < WS-NUM-AGE-CHILD                                
                    MOVE WS-AGE-CY         TO WS-NUM-AGE-CHILD                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       6100-READ-BP13K813.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K813-REC.                     
           INITIALIZE                         BP13K813-REC.                     
                                                                                
           MOVE F730-CDE-NT1               TO K813-NUM-ZONE.                    
           MOVE F730-CDE-FLAT-TYPE         TO K813-NUM-FLAT-TYPE.               
           MOVE F730-DTE-BALLOT            TO K813-DTE-BALLOT.                  
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K813 : ' K813-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K813 : ' WS-K813-STATUS             
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LF73.                                                     
      **********************                                                    
                                                                                
           IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                     
              EVALUATE F730-NUM-PPS-ELIG-TAG                                    
              WHEN 'Y'                                                          
                 PERFORM 8100-WRITE-P13LF73A  THRU 8100-EXIT                    
                                                                                
              WHEN 'F'                                                          
                 PERFORM 8700-WRITE-P13LF73G  THRU 8700-EXIT                    
                                                                                
              WHEN 'N'                                                          
                 IF WS-BYPASS-REPORT = 'N'                                      
                    PERFORM 8200-WRITE-P13LF73B  THRU 8200-EXIT                 
                 END-IF                                                         
                                                                                
              WHEN 'P'                                                          
                 PERFORM 8300-WRITE-P13LF73C  THRU 8300-EXIT                    
                                                                                
              WHEN 'E'                                                          
                 PERFORM 8400-WRITE-P13LF73D  THRU 8400-EXIT                    
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           EVALUATE F730-NUM-GEN3                                               
           WHEN 'Y'                                                             
              PERFORM 8500-WRITE-P13LF73E  THRU 8500-EXIT                       
                                                                                
           WHEN 'P'                                                             
              PERFORM 8600-WRITE-P13LF73F  THRU 8600-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8100-WRITE-P13LF73A.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73A-REC.                     
                                                                                
           IF WS-YES-HDR = 'N'                                                  
              MOVE 'Y'                     TO WS-YES-HDR                        
              MOVE 'P13LF73A'              TO LF73-RPTNAME                      
              WRITE P13LF73A-REC           AFTER PAGE                           
              WRITE P13LF73A-REC           FROM LF73-HEADER1                    
              WRITE P13LF73A-REC           FROM LF73-HEADER2                    
              WRITE P13LF73A-REC           FROM LF73-BLANK                      
              WRITE P13LF73A-REC           FROM LF73-BLANK                      
              WRITE P13LF73A-REC           FROM LF73-HEADER5                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-YES-REC.                     
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-YES-SNO              TO LF73-YES-SNO                      
              MOVE F730-NUM-REGN           TO LF73-YES-REGN                     
              WRITE P13LF73A-REC           FROM LF73-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF73-YES-SNO                      
                                              LF73-YES-REGN                     
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-YES-DET.                     
           WRITE P13LF73A-REC              FROM LF73-YES-REC.                   
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8200-WRITE-P13LF73B.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73B-REC.                     
                                                                                
           IF WS-NO-HDR = 'N'                                                   
              MOVE 'Y'                     TO WS-NO-HDR                         
              MOVE 'P13LF73B'              TO LF73-RPTNAME                      
              WRITE P13LF73B-REC           AFTER PAGE                           
              WRITE P13LF73B-REC           FROM LF73-HEADER1                    
              WRITE P13LF73B-REC           FROM LF73-HEADER3                    
              WRITE P13LF73B-REC           FROM LF73-BLANK                      
              WRITE P13LF73B-REC           FROM LF73-BLANK                      
              WRITE P13LF73B-REC           FROM LF73-HEADER5                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-NO-REC.                      
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-NO-SNO               TO LF73-NO-SNO                       
              MOVE F730-NUM-REGN           TO LF73-NO-REGN                      
              WRITE P13LF73B-REC           FROM LF73-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF73-NO-SNO                       
                                              LF73-NO-REGN                      
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-NO-DET.                      
           WRITE P13LF73B-REC              FROM LF73-NO-REC.                    
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8300-WRITE-P13LF73C.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73C-REC.                     
                                                                                
           IF WS-PEND-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-PEND-HDR                       
              MOVE 'P13LF73C'              TO LF73-RPTNAME                      
              WRITE P13LF73C-REC           AFTER PAGE                           
              WRITE P13LF73C-REC           FROM LF73-HEADER1                    
              WRITE P13LF73C-REC           FROM LF73-HEADER4                    
              WRITE P13LF73C-REC           FROM LF73-BLANK                      
              WRITE P13LF73C-REC           FROM LF73-BLANK                      
              WRITE P13LF73C-REC           FROM LF73-HEADER5                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-PEND-REC.                    
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-PEND-SNO             TO LF73-PEND-SNO                     
              MOVE F730-NUM-REGN           TO LF73-PEND-REGN                    
              WRITE P13LF73C-REC           FROM LF73-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF73-PEND-SNO                     
                                              LF73-PEND-REGN                    
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-PEND-DET.                    
           WRITE P13LF73C-REC              FROM LF73-PEND-REC.                  
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8400-WRITE-P13LF73D.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73D-REC.                     
                                                                                
           IF WS-PREG-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-PREG-HDR                       
              MOVE 'P13LF73D'              TO LF73-RPTNAME                      
              WRITE P13LF73D-REC           AFTER PAGE                           
              WRITE P13LF73D-REC           FROM LF73-HEADER1                    
              WRITE P13LF73D-REC           FROM LF73-HEADER6                    
              WRITE P13LF73D-REC           FROM LF73-BLANK                      
              WRITE P13LF73D-REC           FROM LF73-BLANK                      
              WRITE P13LF73D-REC           FROM LF73-HEADER5                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-PREG-REC.                    
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-PREG-SNO             TO LF73-PREG-SNO                     
              MOVE F730-NUM-REGN           TO LF73-PREG-REGN                    
              WRITE P13LF73D-REC           FROM LF73-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF73-PREG-SNO                     
                                              LF73-PREG-REGN                    
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-PREG-DET.                    
           WRITE P13LF73D-REC              FROM LF73-PREG-REC.                  
                                                                                
       8400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8500-WRITE-P13LF73E.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73E-REC.                     
                                                                                
           IF WS-GEN3-Y-HDR = 'N'                                               
              MOVE 'Y'                     TO WS-GEN3-Y-HDR                     
              MOVE 'P13LF73E'              TO LF73-RPTNAME                      
              WRITE P13LF73E-REC           AFTER PAGE                           
              WRITE P13LF73E-REC           FROM LF73-HEADER1                    
              WRITE P13LF73E-REC           FROM LF73-HEADER7                    
              WRITE P13LF73E-REC           FROM LF73-BLANK                      
              WRITE P13LF73E-REC           FROM LF73-BLANK                      
              WRITE P13LF73E-REC           FROM LF73-HEADER9                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-GEN3-Y-REC.                  
           IF LF73-GEN3-D-APPLN = 'HA1'                                         
              MOVE WS-GEN3-Y-SNO           TO LF73-GEN3-Y-SNO                   
              MOVE F730-NUM-REGN           TO LF73-GEN3-Y-REGN                  
              STRING F730-CDE-FLAT-TYPE '-ROOM'                                 
              DELIMITED BY SPACE INTO LF73-GEN3-Y-FT                            
              MOVE F730-CDE-NT1            TO LF73-GEN3-Y-NT                    
              WRITE P13LF73E-REC           FROM LF73-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF73-GEN3-Y-SNO                   
                                              LF73-GEN3-Y-REGN                  
                                              LF73-GEN3-Y-FT                    
                                              LF73-GEN3-Y-NT                    
           END-IF.                                                              
           MOVE LF73-GEN3-DET-REC          TO LF73-GEN3-Y-DET.                  
           WRITE P13LF73E-REC              FROM LF73-GEN3-Y-REC.                
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8600-WRITE-P13LF73F.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73F-REC.                     
                                                                                
           IF WS-GEN3-P-HDR = 'N'                                               
              MOVE 'Y'                     TO WS-GEN3-P-HDR                     
              MOVE 'P13LF73F'              TO LF73-RPTNAME                      
              WRITE P13LF73F-REC           AFTER PAGE                           
              WRITE P13LF73F-REC           FROM LF73-HEADER1                    
              WRITE P13LF73F-REC           FROM LF73-HEADER8                    
              WRITE P13LF73F-REC           FROM LF73-BLANK                      
              WRITE P13LF73F-REC           FROM LF73-BLANK                      
              WRITE P13LF73F-REC           FROM LF73-HEADER9                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-GEN3-P-REC.                  
           IF LF73-GEN3-D-APPLN = 'HA1'                                         
              MOVE WS-GEN3-P-SNO           TO LF73-GEN3-P-SNO                   
              MOVE F730-NUM-REGN           TO LF73-GEN3-P-REGN                  
              STRING F730-CDE-FLAT-TYPE '-ROOM'                                 
              DELIMITED BY SPACE INTO LF73-GEN3-P-FT                            
              MOVE F730-CDE-NT1            TO LF73-GEN3-P-NT                    
              WRITE P13LF73F-REC           FROM LF73-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF73-GEN3-P-SNO                   
                                              LF73-GEN3-P-REGN                  
                                              LF73-GEN3-P-FT                    
                                              LF73-GEN3-P-NT                    
           END-IF.                                                              
           MOVE LF73-GEN3-DET-REC          TO LF73-GEN3-P-DET.                  
           WRITE P13LF73F-REC              FROM LF73-GEN3-P-REC.                
                                                                                
       8600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8700-WRITE-P13LF73G.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73G-REC.                     
                                                                                
           IF WS-FPPS-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-FPPS-HDR                       
              MOVE 'P13LF73G'              TO LF73-RPTNAME                      
              WRITE P13LF73G-REC           AFTER PAGE                           
              WRITE P13LF73G-REC           FROM LF73-HEADER1                    
              WRITE P13LF73G-REC           FROM LF73-HEADER10                   
              WRITE P13LF73G-REC           FROM LF73-BLANK                      
              WRITE P13LF73G-REC           FROM LF73-BLANK                      
              WRITE P13LF73G-REC           FROM LF73-HEADER5                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-FPPS-REC.                    
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-FPPS-SNO             TO LF73-FPPS-SNO                     
              MOVE F730-NUM-REGN           TO LF73-FPPS-REGN                    
              WRITE P13LF73G-REC           FROM LF73-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF73-FPPS-SNO                     
                                              LF73-FPPS-REGN                    
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-FPPS-DET.                    
           WRITE P13LF73G-REC              FROM LF73-FPPS-REC.                  
                                                                                
       8700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CF73 ================*'.              
           DISPLAY '  NO OF F730 RECORDS READ     = ' WS-F730-READ.             
           DISPLAY '  NO OF ELIGIBLE CASES        = ' WS-YES-SNO.               
           DISPLAY '  NO OF NOT ELIGIBLE (SC,<19) = ' WS-NO-SNO.                
           DISPLAY '  NO OF NOT ELIGIBLE (OTHERS) = ' WS-NO-OTHERS.             
           DISPLAY '  NO OF FPPS CASES            = ' WS-FPPS-SNO.              
           DISPLAY '  NO OF PENDING CASES         = ' WS-PEND-SNO.              
           DISPLAY '  NO OF PREGNANT ELIG CASES   = ' WS-PREG-SNO.              
           DISPLAY '  NO OF GEN3 ELIGIBLE CASES   = ' WS-GEN3-Y-SNO.            
           DISPLAY '  NO OF GEN3 PENDING CASES    = ' WS-GEN3-P-SNO.            
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 BP13K060                                                       
                 BP13K813                                                       
                 P13LF73A                                                       
                 P13LF73B                                                       
                 P13LF73C                                                       
                 P13LF73D                                                       
                 P13LF73E                                                       
                 P13LF73F                                                       
                 P13LF73G.                                                      
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K060 FILE ERROR ' WS-K060-STATUS             
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K813 FILE ERROR ' WS-K813-STATUS             
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
