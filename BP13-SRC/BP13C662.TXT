      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C662.                                                 
       AUTHOR.        SGK.                                                      
      *DATE-WRITTEN.  02/11/98.                                                 
                                                                                
      * ============================================================== *        
      *                  SYSTEM OF COMMITMENT  (BP13)                  *        
      * ============================================================== *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS A PROGRAM TO PRINT LIST OF        *        
      *                      CASES OPTED FOR SUBSEQ. INSTALMENT BY     *        
      *                      GIRO.                                     *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F410  -  TRANSACTION FILE             *        
      *                                                                *        
      *   LISTINGS    :  1.  GIRO FORM PRINTED CASES LIST.             *        
      *                      (BP13L662)                                *        
      *                                                                *        
      *  CHGE-NO   BY    DATE   DESCRIPTION                            *        
      *  -------   ---  ------  -----------                            *        
      *  BP130582  SGK  280898  NEW PGM.                               *        
      *  BP130582  JMS  231298  TO PRINT ACKNOWLEDGMENT.               *        
      *  BP130582  JMS  020199  TO INCLUCE HEADER IN ACKNOWLEDGMENT.   *        
      *  BP132131  MCC  150502  TO EXPAND NME-BANK-ACC-1ST TO 50 BYTES *        
      *  BP135696  ZDD1 210415  REMOVE NME-BANK-ACC-1ST                *        
      *  BP139118  KAC1 230622  CATER FOR GIRO COLUMN                  *        
      ******************************************************************        
                                                                                
      *-------------------------*                                               
       ENVIRONMENT DIVISION.                                                    
      *-------------------------*                                               
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-------------------------*                                               
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------*                                               
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410  ASSIGN       TO BP13F410.                           
           SELECT BP13L662  ASSIGN       TO BP13L662.                           
           SELECT P13L662A  ASSIGN       TO P13L662A.                           
                                                                                
                                                                                
      *-------------------------*                                               
       DATA DIVISION.                                                           
      *-------------------------*                                               
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
       FD  BP13L662                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L662-REC                     PIC X(132).                         
                                                                                
       FD  P13L662A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  P13L662A-REC                     PIC X(132).                         
                                                                                
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *-------------------------*                                               
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-F410-EOF                PIC X     VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F410                PIC 9(3)  VALUE ZEROS.                
           05  WS-PAGE-NO                 PIC 9(4)  VALUE ZEROS.                
           05  WS-CNT                     PIC 9(2)  VALUE ZEROS.                
           05  WS-SEQ-NO                  PIC 9(4)  VALUE ZEROS.                
           05  WS-CNT-PROC                PIC 9(3)  VALUE ZEROS.                
           05  WS-PRT-LINE                PIC 9(2)  VALUE ZEROS.                
           05  WS-CUR-DATE.                                                     
               09 WS-CUR-CCYY             PIC 9(4).                             
               09 WS-CUR-MM               PIC 9(2).                             
               09 WS-CUR-DD               PIC 9(2).                             
                                                                                
       01  L662-HEAD-1.                                                         
           05  FILLER                     PIC X(08)  VALUE 'BP13L662'.          
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(19)  VALUE                      
                                         'S O C   S Y S T E M'.                 
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  L662-PRT-DATE              PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(06)  VALUE 'PAGE: '.            
           05  L662-NUM-PAGE              PIC ZZZ9.                             
                                                                                
       01  L662-HEAD-2.                                                         
           05  FILLER                     PIC X(26)  VALUE SPACES.              
           05  FILLER                     PIC X(72)  VALUE                      
               'LIST OF CASES OPTED TO USE GIRO FOR SUBSEQUENT HOUSING I        
      -     'NSTALMENT'.                                                        
       01  L662-HEAD-3.                                                         
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE 'S/N'.               
           05  FILLER                     PIC X(06)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE 'REGN NO.'.          
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(11)  VALUE 'SCH A/C'.           
      **   05  FILLER                     PIC X(07)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE 'GIRO'.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  FILLER                     PIC X(17)  VALUE                      
                'SALES ORDER DATE'.                                             
                                                                                
       01  L662-DETAIL.                                                         
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L662-SNO                   PIC ZZZ9.                             
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  L662-REGN-NO               PIC X(08)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L662-SCH-ACC               PIC X(11)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L662-CDE-INSTAL-GIRO       PIC X(01)  VALUE SPACES.              
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  L662-SALE-ORD-DTE          PIC X(10)  VALUE SPACES.              
                                                                                
       01  L662A-HEAD-1.                                                        
           05  FILLER                     PIC X(08)  VALUE 'P13L662A'.          
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(19)  VALUE                      
                                         'S O C   S Y S T E M'.                 
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  L662A-PRT-DATE             PIC X(10)  VALUE SPACES.              
                                                                                
       01  L662A-LINE1.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                'TO :    ACCOUNTANT'.                                           
                                                                                
       01  L662A-LINE2.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                '        HAD FINANCE'.                                          
                                                                                
       01  L662A-LINE3.                                                         
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(20)  VALUE                      
                ' PLEASE ACKNOWLEDGE' .                                         
           05  L662A-NO-APLN              PIC Z,ZZ9.                            
           05  FILLER                     PIC X(25)  VALUE                      
                ' GIRO APPLICATION FORMS'.                                      
                                                                                
       01  L662A-LINE4.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(35)  VALUE                      
                ' ________________________________'.                            
                                                                                
       01  L662A-LINE5.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(35)  VALUE                      
                ' FOR SALES UNIT (AGREEMENT TEAM)'.                             
                                                                                
       01  L662A-LINE6.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                'TO :    EEO (SALES)'.                                          
                                                                                
       01  L662A-LINE7.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(30)  VALUE                      
                '        SALES UNIT'.                                           
                                                                                
       01  L662A-LINE8.                                                         
           05  FILLER                     PIC X(35)  VALUE SPACES.              
           05  FILLER                     PIC X(31)  VALUE                      
                'WE ACKNOWLEDGE THE RECEIPT OF'.                                
           05  L662A-APLN-NO              PIC Z,ZZ9 .                           
           05  FILLER                     PIC X(28)  VALUE                      
                ' GIRO APPLICATION FORMS'.                                      
                                                                                
       01  L662A-LINE9.                                                         
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(28)  VALUE                      
                '__________________________'.                                   
                                                                                
       01  L662A-LINE10.                                                        
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(28)  VALUE                      
                'HAD FINANCE '.                                                 
                                                                                
       01  L662A-LINE11.                                                        
           05  FILLER                     PIC X(15)  VALUE SPACES.              
           05  FILLER                     PIC X(10)  VALUE                      
                'DATE  : '.                                                     
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       100-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 200-OPEN-FILES              THRU    200-EXIT.                
           MOVE    60                          TO WS-PRT-LINE.                  
           PERFORM 300-READ-BP13F410           THRU    300-EXIT.                
           PERFORM 400-PRINT-REPORT            THROUGH 400-EXIT                 
                       UNTIL WS-F410-EOF = 'Y'.                                 
           PERFORM 500-PRINT-LETTER            THROUGH 500-EXIT                 
           PERFORM 1000-CLOSE-ROUTINE          THRU    1000-EXIT.               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT   BP13F410                                                
                OUTPUT  BP13L662                                                
                        P13L662A.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-READ-BP13F410.                                                       
      ******************************************************************        
           READ BP13F410                                                        
                AT END MOVE 'Y' TO WS-F410-EOF                                  
                       GO TO 300-EXIT.                                          
                                                                                
           ADD  1  TO  WS-CNT-F410.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-PRINT-REPORT.                                                        
      ******************************************************************        
           IF F410-CDE-SUBS-INSTAL-GIRO NOT = 'Y' AND 'E'                       
              GO TO 400-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF WS-PRT-LINE > 55                                                  
              ADD   1                          TO  WS-PAGE-NO                   
              MOVE  WS-PAGE-NO                 TO  L662-NUM-PAGE                
                                                                                
              MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE                  
              STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                    
              DELIMITED BY SIZE  INTO L662-PRT-DATE                             
              MOVE L662-PRT-DATE   TO L662A-PRT-DATE                            
                                                                                
              WRITE BP13L662-REC FROM L662-HEAD-1 AFTER ADVANCING PAGE          
              WRITE BP13L662-REC FROM L662-HEAD-2 AFTER 1                       
              MOVE  ALL '-'      TO   BP13L662-REC                              
              WRITE BP13L662-REC AFTER ADVANCING 2                              
              WRITE BP13L662-REC FROM L662-HEAD-3 AFTER 1                       
              MOVE  ALL '-'      TO   BP13L662-REC                              
              WRITE BP13L662-REC AFTER ADVANCING 2                              
              MOVE  5            TO   WS-PRT-LINE                               
           END-IF.                                                              
                                                                                
           MOVE  SPACES                 TO  BP13L662-REC.                       
           ADD      1                   TO  WS-SEQ-NO                           
                                            WS-CNT-PROC.                        
           MOVE  WS-SEQ-NO              TO  L662-SNO.                           
           MOVE  F410-NUM-REGN          TO  L662-REGN-NO.                       
           MOVE  F410-NUM-SCH-ACC       TO  L662-SCH-ACC.                       
           MOVE  F410-CDE-SUBS-INSTAL-GIRO  TO L662-CDE-INSTAL-GIRO.            
           MOVE  F410-DTE-SO-EXECUTION  TO  L662-SALE-ORD-DTE.                  
                                                                                
           WRITE BP13L662-REC  FROM L662-DETAIL AFTER 2.                        
           ADD   2             TO  WS-PRT-LINE.                                 
                                                                                
           PERFORM    300-READ-BP13F410      THROUGH  300-EXIT.                 
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-PRINT-LETTER.                                                        
      ******************************************************************        
           IF WS-SEQ-NO = ZEROES OR LOW-VALUES OR SPACES                        
              GO TO 500-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE                     
           STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                       
           DELIMITED BY SIZE  INTO    L662A-PRT-DATE.                           
                                                                                
           MOVE   WS-SEQ-NO      TO   L662A-APLN-NO                             
                                      L662A-NO-APLN.                            
           MOVE   SPACES         TO   P13L662A-REC.                             
                                                                                
           WRITE    P13L662A-REC FROM L662A-HEAD-1 AFTER ADVANCING PAGE         
           MOVE     ALL '-'      TO   P13L662A-REC                              
           WRITE    P13L662A-REC AFTER ADVANCING 2                              
           WRITE    P13L662A-REC FROM L662A-LINE1 AFTER 8.                      
           WRITE    P13L662A-REC FROM L662A-LINE2.                              
                                                                                
           WRITE    P13L662A-REC FROM L662-HEAD-2 AFTER 3.                      
                                                                                
           WRITE    P13L662A-REC FROM L662A-LINE3 AFTER 3.                      
                                                                                
           WRITE    P13L662A-REC FROM L662A-LINE4 AFTER 4.                      
           WRITE    P13L662A-REC FROM L662A-LINE5.                              
                                                                                
           MOVE     ALL '='      TO   P13L662A-REC.                             
           WRITE    P13L662A-REC AFTER 4.                                       
                                                                                
           WRITE    P13L662A-REC FROM L662A-LINE6 AFTER 4.                      
           WRITE    P13L662A-REC FROM L662A-LINE7.                              
                                                                                
           WRITE    P13L662A-REC FROM L662-HEAD-2 AFTER 4.                      
                                                                                
           WRITE    P13L662A-REC FROM L662A-LINE8 AFTER 2.                      
                                                                                
           WRITE    P13L662A-REC FROM L662A-LINE9 AFTER 4.                      
           WRITE    P13L662A-REC FROM L662A-LINE10.                             
                                                                                
           WRITE    P13L662A-REC FROM L662A-LINE11 AFTER 2.                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            IF WS-CNT-PROC > ZERO                                               
               MOVE SPACES      TO    BP13L662-REC                              
               STRING '                            '                            
                      'TOTAL NO. OF CASES LISTED : '                            
                       WS-CNT-PROC DELIMITED BY SIZE                            
                       INTO   BP13L662-REC                                      
               WRITE BP13L662-REC AFTER 2                                       
            END-IF.                                                             
            DISPLAY ' ***  BP13C662 CONTROL TOTALS  ***'.                       
            DISPLAY ' '.                                                        
            DISPLAY ' NO READ FROM F410 : ' WS-CNT-F410.                        
            DISPLAY ' NO CASES PRINTED  : ' WS-CNT-PROC.                        
                                                                                
            CLOSE BP13F410                                                      
                  BP13L662                                                      
                  P13L662A.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
