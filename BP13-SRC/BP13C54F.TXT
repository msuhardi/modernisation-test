       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         BP13C54F.                                            
      *AUTHOR.             ZARITA SARMIENTO.                                    
      *DATE-WRITTEN.       11 JUN 2009.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  PROGRAM TO PROCESS APPLICANT THAT DEBAR     *            
      *                CODE IS MA                                  *            
      *                                                            *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F410                                 *            
      *                2. BP13K853                                 *            
      *                3. BP13K410                                 *            
      *     I-O        1. BP04K010                                 *            
      *                2. BP13K665                                 *            
      *  OUTPUT FILE:  1. P13F010A                                 *            
      *                2. P13F010B                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133534  ZDD1 11062009 NEW PROGRAM                        *            
      * BP135599  PCL4 11122014 TO UPDATE K010-INTERFACE-SYS       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BP13F410 ASSIGN TO BP13F410                                  
                            FILE STATUS  IS WS-F410-STAT.                       
                                                                                
            SELECT BP13K853 ASSIGN TO BP13K853                                  
                            FILE STATUS  IS WS-K853-STAT                        
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K853-KEY-FLD.                       
                                                                                
            SELECT BP04K010 ASSIGN TO BP04K010                                  
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K010-PRIME-KEY                      
                            FILE STATUS  IS WS-K010-STAT.                       
                                                                                
            SELECT BP04K020 ASSIGN TO BP04K020                                  
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K020-DEBAR-CODE                     
                            FILE STATUS  IS WS-K020-STAT.                       
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K410-KEY-FLD                        
                            FILE STATUS  IS WS-K410-STAT.                       
                                                                                
           SELECT BP13K665 ASSIGN TO BP13K665                                   
                            FILE STATUS  IS WS-K665-STAT                        
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS WS-K665-KEY-FLD.                    
                                                                                
           SELECT P13F010A ASSIGN TO P13F010A                                   
                            FILE STATUS  IS WS-F010A-STAT.                      
                                                                                
           SELECT P13F010B ASSIGN TO P13F010B                                   
                            FILE STATUS  IS WS-F010B-STAT.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP04K010                                                            
                RECORD CONTAINS  300 CHARACTERS                                 
                LABEL RECORDS ARE STANDARD.                                     
       COPY P04K010.                                                            
                                                                                
       FD   BP04K020                                                            
                RECORD CONTAINS  800 CHARACTERS                                 
                LABEL RECORDS ARE STANDARD.                                     
       COPY P04K020.                                                            
                                                                                
       FD  BP13K410                                                             
                RECORD CONTAINS 2000 CHARACTERS.                                
       COPY BP13K410.                                                           
                                                                                
       FD   BP13F410                                                            
                RECORD CONTAINS  2000 CHARACTERS                                
                LABEL RECORDS ARE STANDARD                                      
                RECORDING MODE IS F.                                            
                                                                                
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K853                                                            
                RECORD CONTAINS  250  CHARACTERS.                               
                                                                                
       COPY BP13K853.                                                           
                                                                                
       FD  BP13K665                                                             
                RECORD CONTAINS 50 CHARACTERS.                                  
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD   P13F010A                                                            
                RECORD CONTAINS  300  CHARACTERS                                
                LABEL RECORDS ARE STANDARD                                      
                RECORDING MODE IS F.                                            
                                                                                
       01   P13F010A-REC   PIC X(300).                                          
                                                                                
       FD   P13F010B                                                            
                RECORD CONTAINS  300  CHARACTERS                                
                LABEL RECORDS ARE STANDARD                                      
                RECORDING MODE IS F.                                            
                                                                                
       01   P13F010B-REC   PIC X(300).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-VARIABLES.                                                         
          10 WS-F410-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K010-READ-CTR-MA      PIC 9(8) VALUE ZEROES.                    
          10 WS-K010-NOTFND-CTR-MA    PIC 9(8) VALUE ZEROES.                    
          10 WS-K853-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K853-NOTFND-CTR       PIC 9(8) VALUE ZEROES.                    
          10 WS-F010-WRITE-CTR-MA     PIC 9(8) VALUE ZEROES.                    
          10 WS-F010-WRITE-CTR-MB     PIC 9(8) VALUE ZEROES.                    
          10 WS-MB-EXIST              PIC 9(8) VALUE ZEROES.                    
          10 WS-MB-NOT-EXIST          PIC 9(8) VALUE ZEROES.                    
          10 WS-K853-REWRITE-CTR      PIC 9(8) VALUE ZEROES.                    
          10 WS-K665-WRITE-CTR        PIC 9(8) VALUE ZEROES.                    
          10 WS-K665-READ-CTR         PIC 9(8) VALUE ZEROES.                    
          10 WS-K410-READ             PIC 9(6) VALUE ZEROS.                     
                                                                                
          10 WS-F410-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K010-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K020-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K853-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-K665-STAT             PIC X(2) VALUE SPACES.                    
          10 WS-F010A-STAT            PIC X(2) VALUE SPACES.                    
          10 WS-F010B-STAT            PIC X(2) VALUE SPACES.                    
          10 WS-K410-STAT             PIC X(2) VALUE ZEROS.                     
                                                                                
          10 WS-EOF-F410              PIC X(1)  VALUE 'N'.                      
          10 WS-EOF-K853              PIC X(1)  VALUE 'N'.                      
          10 WS-DEBARMENT-TAG         PIC X(1)  VALUE SPACE.                    
          10 WS-CURR-DATE             PIC X(8)  VALUE SPACES.                   
          10 WS-DEBAR-TAG             PIC X(1)  VALUE SPACE.                    
                                                                                
       COPY BP13K665.                                                           
       COPY P04F010.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F410  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
                   UNTIL WS-EOF-F410 = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP04K010                                                 
                       BP04K020                                                 
                       BP13K410                                                 
                I-O    BP13K853                                                 
                       BP13K665                                                 
                OUTPUT P13F010A                                                 
                       P13F010B.                                                
                                                                                
           IF WS-K010-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP04K010 STATUS - '                     
                              WS-K010-STAT                                      
              MOVE WS-K010-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K020-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP04K020 STATUS - '                     
                              WS-K020-STAT                                      
              MOVE WS-K020-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K853-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K853 STATUS - '                     
                              WS-K853-STAT                                      
              MOVE WS-K853-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K410-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K410 STATUS - '                     
                              WS-K410-STAT                                      
              MOVE WS-K410-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K665-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN OPENING BP13K665 STATUS - '                     
                              WS-K665-STAT                                      
              MOVE WS-K665-STAT              TO RETURN-CODE                     
              PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT                     
           END-IF.                                                              
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-READ-BP13F410.                                                      
      ****************************                                              
                                                                                
           READ BP13F410 AT END                                                 
                MOVE 'Y' TO WS-EOF-F410                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F410-READ-CTR.                                           
           MOVE 'N' TO WS-DEBAR-TAG.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-PROCESS-RECORD.                                                     
      ****************************                                              
                                                                                
           MOVE SPACES            TO  K853-KEY-FLD                              
                                      K410-KEY-FLD                              
           MOVE F410-NUM-REGN     TO  K853-NUM-REGN                             
                                      K410-KEY-FLD                              
           ACCEPT WS-CURR-DATE    FROM   DATE YYYYMMDD.                         
                                                                                
           PERFORM 4000-START-BP13K853  THRU 4000-EXIT.                         
           PERFORM 2000-READ-BP13F410   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-START-BP13K853.                                                     
      ****************************                                              
                                                                                
           MOVE 'N' TO WS-EOF-K853.                                             
                                                                                
           START BP13K853 KEY IS >= K853-KEY-FLD.                               
           EVALUATE WS-K853-STAT                                                
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 4001-READNEXT-K853   THRU 4001-EXIT                    
                   UNTIL F410-NUM-REGN NOT = K853-NUM-REGN OR                   
                          WS-EOF-K853 = 'Y'                                     
              WHEN 10                                                           
              WHEN 23                                                           
                   ADD 1 TO WS-K853-NOTFND-CTR                                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K853..' WS-K853-STAT              
                   MOVE WS-K853-STAT TO RETURN-CODE                             
                   PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4001-READNEXT-K853.                                                      
      ****************************                                              
                                                                                
           READ BP13K853 NEXT RECORD                                            
                                                                                
           EVALUATE WS-K853-STAT                                                
               WHEN 00                                                          
               WHEN 22                                                          
                  IF F410-NUM-REGN = K853-NUM-REGN                              
                   MOVE SPACES                 TO P13F010A-REC                  
                                                  P13F010B-REC                  
                   INITIALIZE                     P13F010A-REC                  
                                                  P13F010B-REC                  
                                                  K010-PRIME-KEY                
                                                  P04K010-REC                   
                                                  P04F010-REC                   
                                                                                
                   ADD 1                       TO WS-K853-READ-CTR              
                                                                                
                   MOVE K853-NUM-NRIC          TO K010-NUM-UINFIN               
                   MOVE K853-DEB-REF-NO        TO K010-DEB-REF-NO               
                   MOVE 'MA'                   TO K010-REASON-CODE              
                   MOVE F410-NUM-SCH-ACC(1:9)  TO K010-EX-SCH-ACCTNO            
                   MOVE 'BP13'                 TO K010-INTERFACE-SYS            
                                                                                
                   PERFORM 5000-READ-BP04K010  THRU 5000-EXIT                   
                                                                                
                   MOVE SPACES                 TO P04K010-REC                   
                   MOVE K853-NUM-NRIC          TO K010-NUM-UINFIN               
                   MOVE K853-DEB-REF-NO        TO K010-DEB-REF-NO               
                   MOVE F410-NUM-SCH-ACC(1:9)  TO K010-EX-SCH-ACCTNO            
                   MOVE 'MB'                   TO K010-REASON-CODE              
                   MOVE 'BP13'                 TO K010-INTERFACE-SYS            
                                                                                
                   PERFORM 5000-READ-BP04K010  THRU 5000-EXIT                   
                  END-IF                                                        
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y' TO WS-EOF-K853                                     
                    ADD 1 TO WS-K853-NOTFND-CTR                                 
               WHEN OTHER                                                       
                   DISPLAY 'ERROR PROCESSING BP13K853..' WS-K853-STAT           
                   MOVE WS-K853-STAT TO RETURN-CODE                             
                   PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       5000-READ-BP04K010.                                                      
      ****************************                                              
                                                                                
           READ  BP04K010                                                       
                                                                                
           EVALUATE WS-K010-STAT                                                
               WHEN 00                                                          
               WHEN 02                                                          
                  IF F410-LESSEE-CODE = K010-LSE-TEN-CODE                       
                    IF K010-REASON-CODE = 'MA'                                  
                     ADD 1                    TO WS-K010-READ-CTR-MA            
                     PERFORM 6000-WRITE-F010  THRU 6000-EXIT                    
                    ELSE                                                        
                      IF K010-REASON-CODE = 'MB'                                
                         ADD 1                TO WS-MB-EXIST                    
                      END-IF                                                    
                    END-IF                                                      
                  ELSE                                                          
                    IF K010-REASON-CODE = 'MB'                                  
                      ADD 1                    TO WS-MB-NOT-EXIST               
                      PERFORM 6000-WRITE-F010  THRU 6000-EXIT                   
                    ELSE                                                        
                      IF K010-REASON-CODE = 'MA'                                
                        ADD 1                 TO WS-K010-NOTFND-CTR-MA          
                        DISPLAY 'F410-LESSEE-CODE & K010-LSE-TEN-CODE '         
                          'NOT MATCHED ' K010-PRIME-KEY                         
                        DISPLAY 'K010 STATUS CODE..' WS-K010-STAT               
                      END-IF                                                    
                    END-IF                                                      
                  END-IF                                                        
               WHEN 23                                                          
                    IF K010-REASON-CODE = 'MA'                                  
                     ADD 1                    TO WS-K010-NOTFND-CTR-MA          
                     DISPLAY 'RECORD NOT FOUND IN BP04K010'                     
                         K010-PRIME-KEY                                         
                     DISPLAY 'K010 STATUS CODE..' WS-K010-STAT                  
                    ELSE                                                        
                      IF K010-REASON-CODE = 'MB'                                
                       ADD 1                    TO WS-MB-NOT-EXIST              
                       PERFORM 6000-WRITE-F010  THRU 6000-EXIT                  
                      END-IF                                                    
                    END-IF                                                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP04K010..' WS-K010-STAT             
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       6000-WRITE-F010.                                                         
      ****************************                                              
                                                                                
           IF K010-REASON-CODE = 'MA'                                           
             WRITE P13F010A-REC   FROM P04K010-REC                              
             ADD 1                TO WS-F010-WRITE-CTR-MA                       
           ELSE                                                                 
             IF K010-REASON-CODE = 'MB'                                         
               IF WS-DEBAR-TAG = 'N'                                            
                  PERFORM 7000-GET-DEBARMENT-NO THRU 7000-EXIT                  
                  MOVE 'Y' TO WS-DEBAR-TAG                                      
               END-IF                                                           
                                                                                
               MOVE WS-CURR-DATE            TO K853-DTE-UPDATE                  
               MOVE 'P13C54F'               TO K853-NUM-USERID                  
               MOVE K665-NUM-LAST-DEBARMENT TO K853-DEB-REF-NO                  
                                                                                
               REWRITE BP13K853-REC                                             
                    INVALID KEY                                                 
                    DISPLAY 'REWRITING ERROR, K853-STAT   '                     
                    WS-K853-STAT                                                
                    MOVE WS-K853-STAT         TO RETURN-CODE                    
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
               END-REWRITE                                                      
               ADD 1                           TO WS-K853-REWRITE-CTR           
                                                                                
               MOVE K665-NUM-LAST-DEBARMENT    TO F010-DEB-REF-NO               
               MOVE K853-NUM-NRIC              TO F010-NUM-UINFIN               
                                                                                
               MOVE 'MB'                       TO F010-REASON-CODE              
                                                  K020-DEBAR-CODE               
                                                                                
               PERFORM 6200-READ-K020          THRU 6200-EXIT                   
               MOVE K020-DEBAR-PRD1            TO F010-PERIOD-DEBAR             
                                                                                
               MOVE F410-NUM-SCH-ACC(1:9)      TO F010-EX-SCH-ACCTNO            
               MOVE F410-LESSEE-CODE           TO F010-LSE-TEN-CODE             
               MOVE K853-NME-OCCP              TO F010-NAME                     
               MOVE '1'                        TO F010-PROP-TYPE                
               MOVE ZEROS                      TO F010-AMT-OWING                
                                                                                
               PERFORM 6100-READ-K410          THRU 6100-EXIT                   
                                                                                
               IF K410-DTE-KEY-ISSUE        = SPACES OR  LOW-VALUES             
                 MOVE SPACES                   TO F010-EFF-DATE                 
               ELSE                                                             
                 MOVE K410-DTE-KEY-ISSUE       TO F010-EFF-DATE                 
               END-IF                                                           
                                                                                
               MOVE WS-CURR-DATE               TO F010-INCLUSION-DATE           
                                                  F010-LUPDATE                  
                                                                                
               WRITE P13F010B-REC              FROM P04F010-REC                 
               ADD 1                           TO WS-F010-WRITE-CTR-MB          
                                                                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       6100-READ-K410.                                                          
      ****************************                                              
                                                                                
           READ  BP13K410 KEY IS K410-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K410-STAT                                                
             WHEN  00                                                           
                    ADD   1    TO WS-K410-READ                                  
             WHEN  23                                                           
                    DISPLAY 'BP13K410 RECORD NOT FOUND  = '                     
                                 WS-K410-STAT                                   
                    DISPLAY '    K410-KEY-FLD  = '   K410-KEY-FLD               
             WHEN  OTHER                                                        
                    DISPLAY 'ERROR READING BP13K410 = '                         
                                 WS-K410-STAT                                   
                    DISPLAY '      K410-KEY-FLD     = '                         
                                                    K410-KEY-FLD                
                    MOVE WS-K410-STAT TO RETURN-CODE                            
                    PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT            
           END-EVALUATE.                                                        
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       6200-READ-K020.                                                          
      ****************************                                              
                                                                                
           READ BP04K020.                                                       
                                                                                
           EVALUATE WS-K020-STAT                                                
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'CODE DEBAR NOT EXISTING.' K020-DEBAR-CODE           
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP04K010..' WS-K020-STAT              
                   MOVE WS-K020-STAT TO RETURN-CODE                             
                   PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7000-GET-DEBARMENT-NO.                                                   
      ****************************                                              
                                                                                
           MOVE  LOW-VALUES       TO WS-K665-KEY-FLD.                           
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K665-STATUS ' WS-K665-STAT             
                 MOVE WS-K665-STAT         TO RETURN-CODE                       
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                       
                                                                                
           PERFORM 7010-READ-BP13K665 THRU 7010-EXIT                            
                   UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                         
                                                                                
           IF K665-KEY-FLD = SPACES OR LOW-VALUES                               
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           ADD   1                  TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO.                
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13C54F'          TO  K665-NUM-USERID.                        
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              WRITE WS-BP13K665-REC FROM BP13K665-REC.                          
                                                                                
           EVALUATE WS-K665-STAT                                                
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
              ADD   1                 TO  WS-K665-WRITE-CTR                     
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STAT   '                   
                      WS-K665-STAT                                              
                      MOVE WS-K665-STAT         TO RETURN-CODE                  
                      PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                   
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-WRITE-CTR                     
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STAT                
              MOVE WS-K665-STAT       TO   RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-EVALUATE.                                                        
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       7010-READ-BP13K665.                                                      
      ****************************                                              
                                                                                
           READ BP13K665 NEXT                                                   
                AT END MOVE HIGH-VALUES TO WS-K665-KEY-FLD                      
                GO TO 7010-EXIT.                                                
                                                                                
           ADD 1 TO WS-K665-READ-CTR.                                           
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC                              
           END-IF.                                                              
                                                                                
       7010-EXIT.                                                               
           EXIT.                                                                
      ****************************                                              
       9999-CLOSE-FILES.                                                        
      ****************************                                              
           DISPLAY ' '.                                                         
           DISPLAY '---- BP13C54F CONTROL LIST ----'.                           
           DISPLAY ' TOTAL F410 TOTAL RECORD : ' WS-F410-READ-CTR               
           DISPLAY ' TOTAL K853 FOUND(NRIC)  : ' WS-K853-READ-CTR.              
           DISPLAY ' TOTAL K853 NOT FOUND    : ' WS-K853-NOTFND-CTR.            
           DISPLAY ' TOTAL K010 FOUND(MA)    : ' WS-K010-READ-CTR-MA.           
           DISPLAY ' TOTAL K010 NOT FOUND(MA): ' WS-K010-NOTFND-CTR-MA.         
           DISPLAY ' TOTAL F010 WRITE MA     : ' WS-F010-WRITE-CTR-MA.          
           DISPLAY ' TOTAL K010 FOUND(MB)    : ' WS-MB-EXIST.                   
           DISPLAY ' TOTAL K010 NOT FOUND(MB): ' WS-MB-NOT-EXIST.               
           DISPLAY ' TOTAL F010 WRITE MB     : ' WS-F010-WRITE-CTR-MB.          
                                                                                
           CLOSE  BP13F410                                                      
                  BP04K010                                                      
                  BP04K020                                                      
                  BP13K853                                                      
                  BP13K665                                                      
                  BP13K410                                                      
                  P13F010A                                                      
                  P13F010B.                                                     
                                                                                
                                                                                
           IF WS-K010-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP04K010 STATUS - '                     
                              WS-K010-STAT                                      
              MOVE WS-K010-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K020-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP04K020 STATUS - '                     
                              WS-K020-STAT                                      
              MOVE WS-K020-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K853-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K853 STATUS - '                     
                              WS-K853-STAT                                      
              MOVE WS-K853-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K665-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K665 STATUS - '                     
                              WS-K665-STAT                                      
              MOVE WS-K665-STAT              TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-K410-STAT  NOT = '00' AND '97'                                 
              DISPLAY 'ERROR IN CLOSING BP13K410 STATUS - '                     
                              WS-K410-STAT                                      
              MOVE WS-K410-STAT              TO RETURN-CODE                     
           END-IF.                                                              
           STOP RUN.                                                            
       9999-EXIT.                                                               
            EXIT.                                                               
