      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C461.                                                 
       AUTHOR.        LJL.                                                      
       DATE-WRITTEN.  19/3/92.                                                  
                                                                                
      ************************************************************              
      *                                                          *              
      *    SYSTEM NAME :  SYSTEM OF COMMITMENT                   *              
      *                                                          *              
      *    SYSTEM ID   :  BP13                                   *              
      *                                                          *              
      *    OBJECTIVE   :                                         *              
      *      1.  THIS IS A PROGRAM TO UPDATE SOC MASTER          *              
      *          FROM SALES ORDER FILE ON SA, AO, DELIVERY       *              
      *          POSSESSION DATE AND STAMP FEES (CF & LSF)       *              
      *          AND ZEROLISED BK FEE IN RFF FOR TRANSFER CASE.  *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *      1.  SORTED SALES ORDER  (BP13F410)                  *              
      *      2.  SOC MASTER FILE     (BP13K800)                  *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *      1.  SOC MASTER FILE     (BP13K800)                  *              
      *                                                          *              
      *                                                          *              
      *  CHG-NO   BY    ON         DESCRIPTION                   *              
      *  ------   ---   --------   ----------------------------  *              
      * C950330   LJL   13/03/95   TO CATER FOR THE BK FEE TR    *              
      *                            TO CD FOR AA CASES EXCEPT     *              
      *                            TYPE 2B.                      *              
      * BP130020  SSS   17/10/95   RENAME BP13-L461 TO BP13L461. *              
      *                            RENAME BP13L461  TO P13L461A. *              
      * BP130037  LJL   17/10/95   TO CATER FOR THE AMALGAMATION *              
      *                            OF SALES TYPE 1,2,2B,3,3A AND *              
      *                            10.    .                      *              
      * BP130302  LJL   20/08/97   TO CATER FOR TR FOR REGN DEP  *              
      *                            TO CD.                        *              
      * BP130418  MPA   03/06/98   TO CATER FOR Y2000            *              
      * BP130418  RHB   06/09/98   Y2K                           *              
      ************************************************************              
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-FILE-STATUS.                 
                                                                                
           SELECT BP13F410 ASSIGN       TO BP13F410                             
                           ACCESS MODE  IS SEQUENTIAL.                          
                                                                                
           SELECT BP13L461 ASSIGN       TO BP13L461.                            
                                                                                
           SELECT P13L461A ASSIGN       TO P13L461A.                            
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
       FD   BP13L461                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 133 CHARACTERS                                      
            DATA RECORD     IS  BP13L461-REC.                                   
       01   BP13L461-REC                 PIC X(133).                            
                                                                                
       FD   P13L461A                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 133 CHARACTERS                                      
            DATA RECORD     IS  P13L461A-REC.                                   
       01   P13L461A-REC                 PIC X(133).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-K800-FILE-STATUS      PIC 99       VALUE ZEROES.              
           05  WS-EOF-F410              PIC X(3)     VALUE 'NO '.               
               88  EOF-OF-F410                       VALUE 'YES'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K800-OUTPUT           PIC 9(7)     VALUE ZEROES.              
           05  WS-PAGE                  PIC 9(3)     VALUE ZEROES.              
           05  WS-PAGE1                 PIC 9(3)     VALUE ZEROES.              
           05  WS-LINE                  PIC 9(2)     VALUE 60.                  
           05  WS-LINE1                 PIC 9(2)     VALUE 60.                  
                                                                                
       01  WS-STORE-FIELDS.                                                     
           05  WS-BK                    PIC S9(4)V99 VALUE +0.                  
           05  WS-CD                    PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CASH               PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CASH-RDE           PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CPF1               PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CPF2               PIC S9(7)V99 VALUE +0.                  
           05  WS-HIST                  PIC 9(3)     VALUE 0.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-YY               PIC 9(4).                               
           05  WS-CURR-MM               PIC 9(2).                               
           05  WS-CURR-DD               PIC 9(2).                               
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD             PIC X(2)     VALUE SPACES.              
           05  FILLER                   PIC X        VALUE '/'.                 
           05  WS-EDITED-MM             PIC X(2)     VALUE SPACES.              
           05  FILLER                   PIC X        VALUE '/'.                 
           05  WS-EDITED-YY             PIC X(4)     VALUE SPACES.              
                                                                                
       01  WS-SO-EXECUTION-DATE.                                                
           05  WS-SO-EXECUTION-YY       PIC X(4)     VALUE SPACES.              
           05  WS-SO-EXECUTION-MM       PIC X(2)     VALUE SPACES.              
           05  WS-SO-EXECUTION-DD       PIC X(2)     VALUE SPACES.              
                                                                                
                                                                                
       01  PRINT-ERR1.                                                          
           05  FILLER                   PIC XX     VALUE SPACES.                
           05  FILLER                   PIC X(08)  VALUE 'BP13L461'.            
           05  FILLER                   PIC X(24)  VALUE SPACES.                
           05  FILLER                   PIC X(58)  VALUE                        
              'ERROR LIST FOR UPDATING SOC MASTER'.                             
           05  FILLER                   PIC X(7)   VALUE 'PAGE : '.             
           05  PRINT-PAGE               PIC ZZZ9.                               
           05  FILLER                   PIC X(6)   VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'DATE : '.             
           05  PRINT-DATE1              PIC X(10)  VALUE SPACES.                
                                                                                
       01  PRINT-ERR2.                                                          
           05  FILLER                   PIC X(22)  VALUE SPACES.                
           05  FILLER                   PIC X(12)  VALUE 'REGN NO'.             
           05  FILLER                   PIC X(5)   VALUE SPACES.                
           05  FILLER                   PIC X(6)   VALUE 'SEQ NO'.              
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(10)  VALUE 'REMARKS'.             
                                                                                
       01  PRINT-ERR3.                                                          
           05  FILLER                   PIC X(22)  VALUE SPACES.                
           05  PRT-REGNO                PIC X(8)   VALUE SPACES.                
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PRT-SEQNO                PIC X(3)   VALUE SPACES.                
           05  FILLER                   PIC X(13)  VALUE SPACES.                
           05  PRT-REMARKS              PIC X(60)  VALUE SPACES.                
                                                                                
       01  PRINT-HDG1.                                                          
           05  FILLER                   PIC X(08)  VALUE 'P13L461A'.            
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  FILLER                   PIC X(60)  VALUE                        
               'CONTROL LIST FOR UPDATING SOC MASTER'.                          
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'DATE : '.             
           05  PRINT-DATE               PIC X(10)  VALUE SPACES.                
                                                                                
       01  PRINT-LIST1.                                                         
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE 'REGN NO'.              
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(12) VALUE 'SCHEME ACCT'.          
           05  FILLER                   PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'CDE-BO'.               
           05  FILLER                   PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE 'BK FEE TRF'.           
           05  FILLER                   PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(12) VALUE 'REGN DEP TRF'.         
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(12) VALUE 'DTE-RAISE-SO'.         
                                                                                
       01  PRINT-LIST2.                                                         
           05  FILLER                   PIC X(2)   VALUE SPACES.                
           05  PRT-NUM-REGN             PIC X(8)   VALUE SPACES.                
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PRT-SCH-ACCT             PIC X(11)  VALUE SPACES.                
           05  FILLER                   PIC X(6)   VALUE SPACES.                
           05  PRT-CDE-BO               PIC X(3)   VALUE SPACES.                
           05  FILLER                   PIC X(13)  VALUE SPACES.                
           05  PRT-BK-FEE               PIC $$$$$$.99 VALUE ZEROS.              
           05  FILLER                   PIC X(05)  VALUE SPACES.                
           05  PRT-REGN-DEP             PIC $$$$$$.99 VALUE ZEROS.              
           05  FILLER                   PIC X(6)   VALUE SPACES.                
           05  PRT-DTE-RAISE-SO.                                                
               10  PRT-DTE-RAISE-YY     PIC X(4)   VALUE SPACES.                
               10  FILLER               PIC X(1)   VALUE '/'.                   
               10  PRT-DTE-RAISE-MM     PIC X(2)   VALUE SPACES.                
               10  FILLER               PIC X(1)   VALUE '/'.                   
               10  PRT-DTE-RAISE-DD     PIC X(2)   VALUE SPACES.                
                                                                                
       01  PRINT-REC.                                                           
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PRINT-DESC               PIC X(30)  VALUE SPACES.                
           05  PRINT-TOTAL              PIC ZZZZZZZ9.                           
                                                                                
       01  WS-KIV-OUTPUT                PIC 999    VALUE ZEROS.                 
       01  WS-F410-INPUT                PIC 999    VALUE ZEROS.                 
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
                                                                                
      *==========================================*                              
      * PROGRAM-LOGIC.                           *                              
      *------------------------------------------*                              
      *    10-INITIALIZATION.                    *                              
      *    20-MAIN-PROCESS.                      *                              
      *    30-FINALIZATION.                      *                              
      *==========================================*                              
                                                                                
      *********************                                                     
       10-INITIALIZATION.                                                       
      *********************                                                     
                                                                                
           OPEN INPUT  BP13F410                                                 
                I-O    BP13K800                                                 
                OUTPUT BP13L461                                                 
                       P13L461A.                                                
                                                                                
           IF WS-K800-FILE-STATUS NOT = 00 AND 97                               
              DISPLAY 'BP13K800 - OPEN ERROR ' WS-K800-FILE-STATUS              
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE                           
              PERFORM 30-FINALIZATION.                                          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-CURRENT-DATE.                
           MOVE    WS-CURR-YY        TO    WS-EDITED-YY.                        
           MOVE    WS-CURR-MM        TO    WS-EDITED-MM.                        
           MOVE    WS-CURR-DD        TO    WS-EDITED-DD.                        
           MOVE    WS-EDITED-DATE    TO    PRINT-DATE.                          
           MOVE    WS-EDITED-DATE    TO    PRINT-DATE1.                         
                                                                                
                                                                                
      *******************                                                       
       20-MAIN-PROCESS.                                                         
      *******************                                                       
                                                                                
           PERFORM 100-READ-BP13F410 THRU  100-EXIT.                            
           PERFORM 200-UPDATE-SOC    THRU  200-EXIT                             
                       UNTIL  EOF-OF-F410.                                      
           PERFORM 300-PRINT-CONTROL THRU  300-EXIT.                            
                                                                                
                                                                                
      *******************                                                       
       30-FINALIZATION.                                                         
      *******************                                                       
                                                                                
           CLOSE   BP13F410                                                     
                   BP13K800                                                     
                   BP13L461                                                     
                   P13L461A.                                                    
                                                                                
           IF WS-K800-FILE-STATUS NOT = 00                                      
              DISPLAY 'BP13K800 - CLOSE ERROR ' WS-K800-FILE-STATUS             
              MOVE WS-K800-FILE-STATUS  TO RETURN-CODE.                         
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      *********************                                                     
       100-READ-BP13F410.                                                       
      *********************                                                     
                                                                                
           READ BP13F410 AT END                                                 
                         MOVE 'YES'  TO WS-EOF-F410                             
                         GO TO 100-EXIT.                                        
           ADD 1 TO WS-F410-INPUT.                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *=================================================================        
      * 200-UPDATE-SOC.               |                                *        
      *----------------------------------------------------------------*        
      *   NO NEED TO CHECK BK-FEE     | CHECK AMT-BK-PAID              *        
      *   AS STD BK FEE = ZEROES      |                                *        
      *   2110-UPD-AMOUNT-SOC-INFO.   | MOVE UPDATED AMOUNT-SOC-INFO   *        
      *                               |      TO BP13K800 RECORD        *        
      *   2400-WRITE-TO-FILE.         | REWRITE BP13K800 RECORD        *        
      *   2500-PRINT-ERROR.           | PRINT ERROR  DETAIL LINE       *        
      *   2500-PRINT-REPORT.          | PRINT REPORT DETAIL LINE       *        
      *================================================================*        
                                                                                
      ******************                                                        
       200-UPDATE-SOC.                                                          
      ******************                                                        
           IF   F410-CDE-SALES-TYPE =  '10'                                     
           PERFORM 100-READ-BP13F410 THRU 100-EXIT                              
           GO TO 200-EXIT.                                                      
                                                                                
           MOVE F410-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-FILE-STATUS  = 00                                         
              ADD  1                 TO   WS-HIST                               
              PERFORM 2110-UPD-AMOUNT-SOC-INFO THRU 2110-EXIT                   
              PERFORM 2400-WRITE-TO-FILE THRU 2400-EXIT                         
           ELSE                                                                 
              MOVE F410-NUM-REGN               TO PRT-REGNO                     
              MOVE SPACES                      TO PRT-SEQNO                     
              MOVE 'RECORD NOT IN SOC MASTER ' TO PRT-REMARKS                   
              PERFORM 2500-PRINT-ERROR  THRU 2500-EXIT                          
              ADD   1 TO   WS-KIV-OUTPUT.                                       
                                                                                
           PERFORM 100-READ-BP13F410 THRU 100-EXIT.                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       300-PRINT-CONTROL.                                                       
      *********************                                                     
                                                                                
           WRITE BP13L461-REC FROM PRINT-HDG1 AFTER ADVANCING PAGE.             
           MOVE SPACES TO BP13L461-REC.                                         
           WRITE BP13L461-REC AFTER ADVANCING 1.                                
                                                                                
           MOVE 'TOTAL RECORDS PROCESSED   '       TO PRINT-DESC.               
           WRITE BP13L461-REC FROM PRINT-REC AFTER ADVANCING 1.                 
                                                                                
           MOVE WS-F410-INPUT   TO PRINT-TOTAL.                                 
           MOVE '      BP13F410 READ          : '  TO PRINT-DESC.               
           WRITE BP13L461-REC FROM PRINT-REC AFTER ADVANCING 2.                 
                                                                                
           MOVE WS-K800-OUTPUT TO PRINT-TOTAL.                                  
           MOVE '      UPDATE TO BP13K800     : '  TO PRINT-DESC.               
           WRITE BP13L461-REC FROM PRINT-REC AFTER ADVANCING 2.                 
                                                                                
           MOVE WS-KIV-OUTPUT  TO PRINT-TOTAL.                                  
           MOVE '      WRITE  TO BP13KIV      : '  TO PRINT-DESC.               
           WRITE BP13L461-REC FROM PRINT-REC AFTER ADVANCING 2.                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ***************************                                               
       2110-UPD-AMOUNT-SOC-INFO.                                                
      ***************************                                               
                                                                                
      *-------------------------------------------------*                       
      *  SINCE APPLICANT ALREADY COLLECTED KEYS         *                       
      *  DTE-SALE-AGMT SHOULD BE SO-DATE.               *                       
      *-------------------------------------------------*                       
           IF F410-DTE-SO-EXECUTION IS NOT NUMERIC                              
              MOVE ZEROES                TO   K800-DTE-SALE-AGMT                
                                              K800-DTE-RAISE-AO                 
           ELSE                                                                 
             MOVE F410-DTE-SO-EXECUTION  TO   K800-DTE-RAISE-AO                 
             MOVE F410-DTE-SO-EXECUTION  TO   K800-DTE-SALE-AGMT.               
                                                                                
      *-------------------------------------------------*                       
      *  SINCE APPLICANT ALREADY COLLECTED KEYS         *                       
      *  DELIVERY POSSESSION DATE SHOULD BE SO-DATE.    *                       
      *-------------------------------------------------*                       
           IF F410-DTE-DELIVERY-POSSN  NOT NUMERIC                              
              MOVE ZEROES                  TO K800-DTE-DELIVERY-POSSN           
           ELSE                                                                 
              MOVE F410-DTE-DELIVERY-POSSN TO K800-DTE-DELIVERY-POSSN.          
                                                                                
           IF F410-CDE-ASPF = 'Y'                                               
              MOVE F410-CDE-ASPF            TO   K800-NUM-ASPF.                 
                                                                                
                                                                                
           IF F410-AMT-LEASE-STAMP NOT NUMERIC                                  
              MOVE ZEROES                   TO  K800-AMT-LEASE-FEE              
           ELSE                                                                 
              MOVE F410-AMT-LEASE-STAMP     TO  K800-AMT-LEASE-FEE.             
                                                                                
           IF F410-AMT-CONVEY      NOT NUMERIC                                  
              MOVE ZEROES                   TO  F410-AMT-CONVEY                 
           ELSE                                                                 
              MOVE F410-AMT-CONVEY          TO  K800-AMT-CONVEY-FEE             
              MOVE F410-AMT-CONVEY-GST      TO  K800-AMT-CONVEY-FEE-GST.        
                                                                                
                                                                                
              DISPLAY ' REGN '   F410-NUM-REGN.                                 
              DISPLAY 'BK FEE TR '    F410-AMT-BK-TRANSFER.                     
           IF F410-AMT-BK-TRANSFER NOT NUMERIC                                  
              MOVE ZEROES                   TO  F410-AMT-BK-TRANSFER            
              MOVE ZEROES                   TO  K800-AMT-BK-TR                  
           ELSE                                                                 
              MOVE F410-AMT-BK-TRANSFER     TO   K800-AMT-BK-TR.                
                                                                                
           IF F410-AMT-RDE-TRANSFER NOT NUMERIC                                 
              MOVE ZEROES                   TO  F410-AMT-RDE-TRANSFER.          
                                                                                
                                                                                
      *-------------------------------------------------*                       
      *  ZEROLISE BOOKING FEE FOR BOOKING FEE USED AS   *                       
      *  BOOKING FEE IS TRANSFER TO CD TO BE USED AS ICP*                       
      *-------------------------------------------------*                       
           IF F410-AMT-BK-TRANSFER = ZEROES                                     
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF F410-AMT-BK-TRANSFER > K800-AMT-BK-PAID                           
              MOVE F410-NUM-REGN              TO PRT-REGNO                      
              MOVE SPACES                     TO PRT-SEQNO                      
              MOVE 'BK FEE TRANSFER > PAID '  TO PRT-REMARKS                    
              PERFORM 2500-PRINT-ERROR THRU 2500-EXIT                           
              ADD   1 TO   WS-KIV-OUTPUT                                        
           ELSE                                                                 
              ADD  F410-AMT-BK-TRANSFER TO   K800-AMT-SOC-CASH                  
              COMPUTE WS-BK =  K800-AMT-BK-PAID  - F410-AMT-BK-TRANSFER         
              MOVE WS-BK                TO   K800-AMT-BK-PAID                   
              MOVE ZEROES               TO   WS-BK                              
              PERFORM 2600-PRINT-REPORT THRU 2600-EXIT.                         
                                                                                
      *-------------------------------------------------*                       
      *  ZEROLISE REGN-DEP    FOR REGN DEP    USED AS   *                       
      *  REGN DEP    IS TRANSFER TO CD TO BE USED AS ICP*                       
      *-------------------------------------------------*                       
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CASH NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CASH.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF1 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF1.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF2 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF2.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF3 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF3.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF4 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF4.                         
                                                                                
           MOVE ZEROES          TO   K800-AMT-CD-CPF1                           
                                     K800-AMT-CD-CPF2                           
                                     K800-AMT-CD-CPF3                           
                                     K800-AMT-CD-CPF4.                          
                                                                                
           IF F410-AMT-RDE-TRANSFER = ZEROES                                    
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF F410-AMT-RDE-TRANSFER > (K800-AMT-REGN-DEPOSIT-CASH +             
                                       K800-AMT-REGN-DEPOSIT-CPF1 +             
                                       K800-AMT-REGN-DEPOSIT-CPF2 +             
                                       K800-AMT-REGN-DEPOSIT-CPF3 +             
                                       K800-AMT-REGN-DEPOSIT-CPF4)              
              MOVE F410-NUM-REGN              TO PRT-REGNO                      
              MOVE SPACES                     TO PRT-SEQNO                      
              MOVE 'RDE FEE TRANSFER > PAID '  TO PRT-REMARKS                   
              PERFORM 2500-PRINT-ERROR THRU 2500-EXIT                           
              ADD   1 TO   WS-KIV-OUTPUT                                        
           ELSE                                                                 
           IF F410-AMT-RDE-TRANSFER NOT = 0 AND 5000                            
              MOVE F410-NUM-REGN              TO PRT-REGNO                      
              MOVE SPACES                     TO PRT-SEQNO                      
              MOVE 'RDE FEE TRANSFER NOT = 5000'  TO PRT-REMARKS                
              PERFORM 2500-PRINT-ERROR THRU 2500-EXIT                           
              ADD   1 TO   WS-KIV-OUTPUT                                        
           ELSE                                                                 
              ADD  K800-AMT-REGN-DEPOSIT-CASH TO K800-AMT-SOC-CASH              
                                                 WS-CD-CASH-RDE                 
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CASH                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF1 TO K800-AMT-CPF1-PAID             
                                                 K800-AMT-CD-CPF1               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF1                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF2 TO K800-AMT-CPF2-PAID             
                                                 K800-AMT-CD-CPF2               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF2                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF3 TO K800-AMT-CPF3-PAID             
                                                 K800-AMT-CD-CPF3               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF3                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF4 TO K800-AMT-CPF4-PAID             
                                                 K800-AMT-CD-CPF4               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF4.                         
                                                                                
           COMPUTE WS-CD-CASH = F410-AMT-BK-TRANSFER +                          
                                WS-CD-CASH-RDE.                                 
                                                                                
                                                                                
           MOVE WS-CD-CASH      TO   K800-AMT-CD-CASH.                          
                                                                                
           COMPUTE WS-CD = K800-AMT-CD-CASH +                                   
                           K800-AMT-CD-CPF1 +                                   
                           K800-AMT-CD-CPF2 +                                   
                           K800-AMT-CD-CPF3 +                                   
                           K800-AMT-CD-CPF4.                                    
                                                                                
           MOVE WS-CD           TO   K800-AMT-CD.                               
                                                                                
      *------------------------------*                                          
      *  INITIALIZE WORK AREA        *                                          
      *------------------------------*                                          
           MOVE ZEROES          TO   WS-CD-CASH                                 
                                     WS-CD-CASH-RDE.                            
           MOVE ZEROES          TO   WS-CD-CPF1.                                
           MOVE ZEROES          TO   WS-CD-CPF2.                                
                                                                                
       2110-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       2400-WRITE-TO-FILE.                                                      
      **********************                                                    
                                                                                
           MOVE WS-CURRENT-DATE TO K800-DTE-UPDATE.                             
           REWRITE BP13K800-MASTER.                                             
           ADD  1  TO  WS-K800-OUTPUT.                                          
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       2500-PRINT-ERROR.                                                        
      ********************                                                      
                                                                                
           IF WS-LINE > 55                                                      
              ADD   1             TO    WS-PAGE                                 
              MOVE  WS-PAGE       TO    PRINT-PAGE                              
              WRITE BP13L461-REC  FROM  PRINT-ERR1 AFTER ADVANCING PAGE         
              WRITE BP13L461-REC  FROM  PRINT-ERR2 AFTER ADVANCING 2            
              MOVE  0             TO    WS-LINE                                 
              MOVE  SPACES        TO    BP13L461-REC                            
              WRITE BP13L461-REC  AFTER ADVANCING 1.                            
           WRITE BP13L461-REC     FROM  PRINT-ERR3 AFTER ADVANCING 1.           
           ADD 1 TO  WS-LINE.                                                   
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       2600-PRINT-REPORT.                                                       
      *********************                                                     
                                                                                
           MOVE F410-NUM-REGN        TO PRT-NUM-REGN.                           
           MOVE F410-NUM-SCH-ACC     TO PRT-SCH-ACCT.                           
           MOVE F410-CDE-BO          TO PRT-CDE-BO.                             
           MOVE F410-AMT-BK-TRANSFER TO PRT-BK-FEE.                             
           MOVE F410-AMT-RDE-TRANSFER TO  PRT-REGN-DEP.                         
           MOVE F410-DTE-SO-EXECUTION TO WS-SO-EXECUTION-DATE.                  
           MOVE WS-SO-EXECUTION-YY   TO PRT-DTE-RAISE-YY                        
           MOVE WS-SO-EXECUTION-MM   TO PRT-DTE-RAISE-MM.                       
           MOVE WS-SO-EXECUTION-DD   TO PRT-DTE-RAISE-DD.                       
           IF WS-LINE1  > 55                                                    
              ADD   1             TO    WS-PAGE1                                
              MOVE  WS-PAGE1      TO    PRINT-PAGE                              
              WRITE P13L461A-REC  FROM  PRINT-LIST1 AFTER ADVANCING PAGE        
              MOVE  0             TO    WS-LINE1                                
              MOVE  SPACES        TO    P13L461A-REC                            
              WRITE P13L461A-REC  AFTER ADVANCING 1.                            
           WRITE P13L461A-REC     FROM  PRINT-LIST2 AFTER ADVANCING 1.          
           ADD 1 TO  WS-LINE1.                                                  
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
