       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C121.                                                 
      *AUTHOR.        CHEAH KH.                                                 
      *DATE-WRITTEN.  12/05/89.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
      *****************************************************************         
      *                                                               *         
      *   SYSTEM NAME : SYSTEM OF COMMITMENT                         *          
      *                                                               *         
      *   OBJECTIVE   : THIS PROGRAM IS TO CARRY BALANCE CLOSE TO     *         
      *                 BALANCE OPEN.                                 *         
      *                                                               *         
      *   INPUT FILES :                                               *         
      *                                                               *         
      *                                                               *         
      *   2.  BP13K160 -- BALANCE FILE                                *         
      *           ORGANISATION  : SEQ                                 *         
      *           ACCESS MODE   : SEQUANTIAL                          *         
      *           RECORD LENGTH : 100                                 *         
      *           RECORD KEY    : K160-KEY-FLD                        *         
      *                                                               *         
      *   OUTPUT FILES:                                               *         
      *                                                               *         
      *   1.  BP13K160 -- BALANCE FILE                                *         
      *           ORGANISATION  : KSDS                                *         
      *           ACCESS MODE   : RANDOM                              *         
      *           RECORD LENGTH : 100                                 *         
      *                                                               *         
      *   FUNCTION:                                                   *         
      *   1.  MOVE CLOSING AMOUNT TO OPENING AMOUNT.                  *         
      *   2.  MOVE ZEROES TO TRANS AMOUNT.                            *         
      *                                                               *         
      *   AMENDMENTS:                                                 *         
      *   REF      DATE      BY    DESCRIPTIONS                       *         
      *   -------  --------  ----  ------------                       *         
      *   SOC-PH8  23/07/93  LSB   COBOL II                           *         
      *   BP130418 25/08/98  RHB   TO CATER FOR Y2000.                *         
      *   BP136098 09/05/16  FNP1  CHECK IF FIELDS ARE NUMERIC BEFORE *         
      *                            USING IN COMPUTATION               *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K160 ASSIGN TO BP13K160                                   
                ACCESS MODE IS SEQUENTIAL                                       
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K160-KEY-FLD                                      
                FILE STATUS IS BP13K160-STATUS.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13K160                                                              
            BLOCK CONTAINS   0  RECORDS                                         
            RECORD CONTAINS  100  CHARACTERS                                    
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K160.                                                   00150000
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BP13K160-STATUS         PIC 99      VALUE ZEROS.                     
       01  CNT-K160-READ           PIC 9(5)    VALUE ZEROS.                     
       01  CNT-K160-REWRITE        PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01  WS-SWITCHES.                                                         
           05  K160-EOF          PIC X       VALUE 'N'.                         
                                                                                
       01  WS-DTE-OPEN-BF.                                                      
           05  WS-DTE-OPEN-BF-CCYY PIC 9(4).                                    
           05  WS-DTE-OPEN-BF-CYN  REDEFINES WS-DTE-OPEN-BF-CCYY.               
               10  WS-DTE-OPEN-BF-CC   PIC 99.                                  
               10  WS-DTE-OPEN-BF-YY   PIC 99.                                  
           05  WS-DTE-OPEN-BF-MM   PIC 99.                                      
           05  WS-DTE-OPEN-BF-DD   PIC 99.                                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      **********************************************************                
      *                                                        *                
      *              MAINLINE PROCEDURE                        *                
      *                                                        *                
      **********************************************************                
                                                                                
       MAIN-PROCESS.                                                            
           PERFORM 100-OPEN-FILES          THRU 100-EXIT.                       
           PERFORM 200-READ-K160           THRU 200-EXIT.                       
           PERFORM 300-PROCESS-RECORD      THRU 300-EXIT                        
                   UNTIL K160-EOF = 'Y'.                                        
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *                  OPENING OF MASTER FILES               *                
      *                                                        *                
      **********************************************************                
                                                                                
       100-OPEN-FILES.                                                          
                                                                                
           OPEN   I-O    BP13K160.                                              
                                                                                
           IF BP13K160-STATUS NOT = '00' AND NOT = '97'                         
               DISPLAY ' OPENING ERROR,BP13K160-STATUS' BP13K160-STATUS         
               MOVE BP13K160-STATUS TO RETURN-CODE                              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *                READ OF K160 BALANCE FILE              *                 
      *                                                        *                
      **********************************************************                
                                                                                
       200-READ-K160.                                                           
           READ BP13K160  AT END                                                
                MOVE 'Y' TO K160-EOF                                            
                GO TO 200-EXIT.                                                 
           ADD 1 TO CNT-K160-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *               SELECTING RECORDS                        *                
      *                                                        *                
      **********************************************************                
                                                                                
       300-PROCESS-RECORD.                                                      
                                                                                
           MOVE ZEROES                      TO   K160-AMT-TRANS.                
           IF K160-AMT-CLOSE NOT NUMERIC                                        
              MOVE ZEROES TO K160-AMT-CLOSE                                     
           END-IF.                                                              
           MOVE K160-AMT-CLOSE              TO   K160-AMT-OPEN.                 
           PERFORM 420-UPDATE-OPEN-DATE     THRU 420-EXIT.                      
           PERFORM 430-REWRITE-BP13K160     THRU 430-EXIT.                      
           PERFORM 200-READ-K160            THRU 200-EXIT.                      
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-UPDATE-OPEN-DATE.                                                    
                                                                                
           IF K160-DTE-OPEN-BF NOT = SPACES AND LOW-VALUES                      
              MOVE K160-DTE-OPEN-BF TO   WS-DTE-OPEN-BF                         
           END-IF.                                                              
           IF WS-DTE-OPEN-BF NOT NUMERIC                                        
              MOVE ZEROES  TO WS-DTE-OPEN-BF                                    
           END-IF.                                                              
           ADD  1                TO   WS-DTE-OPEN-BF-MM.                        
                                                                                
           IF WS-DTE-OPEN-BF-MM = 13                                            
              MOVE 01     TO   WS-DTE-OPEN-BF-MM                                
              ADD  1      TO   WS-DTE-OPEN-BF-CCYY.                             
                                                                                
           MOVE WS-DTE-OPEN-BF       TO   K160-DTE-OPEN-BF.                     
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *             WRITTING TO BALANCE FILE                   *                
      *                                                        *                
      **********************************************************                
                                                                                
       430-REWRITE-BP13K160.                                                    
                                                                                
           REWRITE BP13K160-BAL.                                                
           ADD   1   TO  CNT-K160-REWRITE.                                      
                                                                                
           IF BP13K160-STATUS NOT = ZEROS                                       
               DISPLAY 'WRITE BP13K160-STATUS IS ' BP13K160-STATUS              
               MOVE BP13K160-STATUS TO RETURN-CODE                              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                       
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *                CLOSING ROUTINES                        *                
      *                                                        *                
      **********************************************************                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
           DISPLAY 'NO OF BALANCE RECS READ      : ' CNT-K160-READ.             
           DISPLAY 'NO OF BALANCE RECS REWRITTEN : ' CNT-K160-REWRITE.          
                                                                                
           CLOSE BP13K160.                                                      
                                                                                
           IF BP13K160-STATUS NOT = 0                                           
              DISPLAY ' CLOSING ERROR, BP13K160-STATUS '                        
                       BP13K160-STATUS                                          
              MOVE BP13K160-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
