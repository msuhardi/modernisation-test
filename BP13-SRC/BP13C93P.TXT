      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C93P.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  21 JUN 2017.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE BP13KW3C BASED ON COMPLETED      *            
      *                 BOOKING TAG FROM BP13K813.                  *           
      *                 M = MGPS SELECTION IS OVER                  *           
      *                 B = BOOKING EXERCISE IS OVER                *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FW3C                                              *           
      *   2.  BP13K813                                              *           
      *                                                             *           
      *   I-O                                                       *           
      *   1.  BP13KW3C                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136856 21/06/2017 FNP1 NEW PROGRAM                        *           
      * BP137001 04/10/2017 FNP1 UPDATE BOOKING START/END DATE      *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3C ASSIGN TO BP13FW3C.                                  
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS BP13K813-STATUS.                     
                                                                                
           SELECT BP13KW3C ASSIGN       TO BP13KW3C                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KW3C-KEY-FLD                         
                           FILE STATUS  IS BP13KW3C-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW3C.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13KW3C                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13KW3C.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-FW3C-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-KW3C-UPDATED        PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3C-EOF            PIC X     VALUE SPACES.                       
       01  BP13K813-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KW3C-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-NUM-FT              PIC X(02) VALUE SPACES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-FW3C    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-FW3C-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3C                                                 
                       BP13K813                                                 
                I-O    BP13KW3C.                                                
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR BP13K813. STATUS : ' BP13K813-STATUS          
              MOVE BP13K813-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF BP13KW3C-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR BP13KW3C. STATUS : ' BP13KW3C-STATUS          
              MOVE BP13KW3C-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-FW3C.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FW3C AT END                                                 
                MOVE HIGH-VALUES TO BP13FW3C-REC                                
                MOVE 'Y' TO WS-FW3C-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-FW3C-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES          TO WS-NUM-FT.                                   
                                                                                
           IF FW3C-NUM-FLAT-TYPE(1:2) = 'EX'                                    
              MOVE 'E '       TO WS-NUM-FT                                      
           ELSE                                                                 
              IF FW3C-NUM-FLAT-TYPE(2:1) = '-'                                  
                 IF FW3C-NUM-FLAT-TYPE(1:1) = '2'                               
                    MOVE '2F'                 TO WS-NUM-FT                      
                 ELSE                                                           
                    MOVE FW3C-NUM-FLAT-TYPE(1:1) TO WS-NUM-FT                   
                 END-IF                                                         
              ELSE                                                              
                 MOVE FW3C-NUM-FLAT-TYPE(1:2) TO WS-NUM-FT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF FW3C-NUM-SELECTION = 'BTO' AND                                    
              FW3C-NUM-BTO-ZONE NOT = SPACES                                    
              PERFORM 4000-READ-BP13K813   THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FW3C          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-READ-BP13K813.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                        TO  BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           MOVE FW3C-DTE-BALLOT               TO  K813-DTE-BALLOT.              
           MOVE WS-NUM-FT                     TO  K813-NUM-FLAT-TYPE.           
           MOVE FW3C-NUM-BTO-ZONE             TO  K813-NUM-ZONE.                
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE BP13K813-STATUS                                             
           WHEN 00                                                              
                IF K813-NUM-BOOKING-COMPLT = 'M' OR 'B'                         
                   PERFORM 5000-READ-BP13KW3C THRU 5000-EXIT                    
                END-IF                                                          
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13K813, STATUS  '                         
                        BP13K813-STATUS                                         
                        ',  KEY    ' K813-KEY-FLD                               
                MOVE BP13K813-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-READ-BP13KW3C.                                                      
      *-----------------*                                                       
           MOVE SPACES                 TO KW3C-KEY-FLD.                         
           MOVE FW3C-KEY-FLD           TO KW3C-KEY-FLD.                         
                                                                                
           READ BP13KW3C.                                                       
                                                                                
           EVALUATE BP13KW3C-STATUS                                             
           WHEN 00                                                              
              PERFORM 5500-UPDATE-BP13KW3C  THRU 5500-EXIT                      
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13KW3C, KEY IS'                    
                      KW3C-KEY-FLD                                              
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13KW3C, STATUS  '                         
                        BP13KW3C-STATUS                                         
                        ',  KEY    ' KW3C-KEY-FLD                               
                MOVE BP13KW3C-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5500-UPDATE-BP13KW3C.                                                    
      *-----------------*                                                       
                                                                                
           IF K813-DTE-BOOKING-START NOT = KW3C-DTE-BOOKING-START               
              MOVE K813-DTE-BOOKING-START TO KW3C-DTE-BOOKING-START             
           END-IF.                                                              
                                                                                
           IF K813-DTE-BOOKING-END NOT = KW3C-DTE-BOOKING-END                   
              MOVE K813-DTE-BOOKING-END   TO KW3C-DTE-BOOKING-END               
           END-IF.                                                              
                                                                                
           IF KW3C-CDE-COMPLETE-TAG = SPACES                                    
              MOVE K813-NUM-BOOKING-COMPLT TO KW3C-CDE-COMPLETE-TAG             
           ELSE                                                                 
              IF KW3C-CDE-COMPLETE-TAG = 'M'                                    
                 IF K813-NUM-BOOKING-COMPLT = 'B'                               
                    MOVE K813-NUM-BOOKING-COMPLT TO                             
                                              KW3C-CDE-COMPLETE-TAG             
                 END-IF                                                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           REWRITE BP13KW3C-REC.                                                
                                                                                
           EVALUATE BP13KW3C-STATUS                                             
           WHEN 00                                                              
                ADD 1 TO WS-KW3C-UPDATED                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR REWRITING BP13KW3C, STATUS  '                    
                        BP13KW3C-STATUS                                         
                        ',  KEY    ' KW3C-KEY-FLD                               
                MOVE BP13KW3C-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           DISPLAY '******  BP13C93P  *************'.                           
           DISPLAY 'RECS READ FROM  BP13FW3C    : ' WS-FW3C-READ.               
           DISPLAY '                              '.                            
           DISPLAY 'RECS UPDATED IN BP13KW3C    : ' WS-KW3C-UPDATED.            
                                                                                
           CLOSE BP13FW3C                                                       
                 BP13K813                                                       
                 BP13KW3C.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR BP13K813. STATUS : ' BP13K813-STATUS         
              MOVE BP13K813-STATUS       TO   RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13KW3C-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR BP13KW3C. STATUS : ' BP13KW3C-STATUS         
              MOVE BP13KW3C-STATUS       TO   RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
