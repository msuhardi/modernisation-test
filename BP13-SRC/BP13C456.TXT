       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C456.                                                 
      *AUTHOR.        RAYATHATHA ASHOK.                                         
      *DATE-WRITTEN.  04/11/92.                                                 
                                                                                
      ***************************************************************           
      *  SYSTEM OF COMMITMENT (SOC)                                 *           
      *=============================================================*           
      *  OBJECTIVE  :  TO DELETE APPOINTMENTS FILE FOR CASE THAT    *           
      *                ARE CANCELLED.                               *           
      *=============================================================*           
      * CHGE     DATE     BY   DESCRIPTION                          *           
      * -------  -------- ---  -----------------                    *           
      * I920718  11/01/93 LJL  TO CONVERT TO COBOL II               *           
      * C930144  19/03/93 LMS  TO CHANGE F901 TO F310               *           
      * SOC-PH8  28/07/93 RML  REFER TO BP13K800                    *           
      * BP130418 04/06/98 SGK  TO CATER FOR Y2K.                    *           
      * BP130839 20/05/00 JMS  CHANGE THE LENGTH OF BP13K452 TO 500 *           
      * BP134212 31/05/11 ESD1 CATER TO PROCESS THE NEW BP13K452 FILE           
      * BP136430 07/12/16 PCL4 RECOMPILE, BP13K452 LENGTH UPDATED   *           
      ***************************************************************           
                                                                                
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *--------------------*                                                    
       INPUT-OUTPUT SECTION.                                                    
      *--------------------*                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                     ACCESS MODE  IS DYNAMIC                                    
                     ORGANIZATION IS INDEXED                                    
                     RECORD KEY   IS K800-NUM-REGN                              
                     FILE STATUS  IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K452 ASSIGN TO BP13K452                                   
                     ACCESS MODE  IS DYNAMIC                                    
                     ORGANIZATION IS INDEXED                                    
                     RECORD KEY   IS K452-KEY-FLD                               
                     FILE STATUS  IS WS-K452-STATUS.                            
                                                                                
           SELECT BP13K454 ASSIGN TO P13K454                                    
                     ACCESS MODE  IS DYNAMIC                                    
                     ORGANIZATION IS INDEXED                                    
                     RECORD KEY   IS K454-KEY-FLD                               
                     ALTERNATE RECORD KEY   IS K454-ALT1-KEY                    
                     FILE STATUS  IS WS-K454-STATUS.                            
                                                                                
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13K454.                                                            
       COPY BP13K454.                                                           
                                                                                
       FD  BP13K452.                                                            
       COPY BP13K452.                                                           
                                                                                
       FD  BP13K800.                                                            
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F310.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-F310-EOF                   PIC X     VALUE 'N'.                   
           88  F310-EOF                            VALUE 'Y'.                   
                                                                                
       01  WS-K452-STATUS                PIC 99    VALUE ZEROS.                 
       01  WS-K454-STATUS                PIC 99    VALUE ZEROS.                 
       01  WS-K800-STATUS                PIC 99    VALUE ZEROS.                 
       01  WS-COMPLETE-TAG               PIC X     VALUE SPACES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT                    PIC 9(2)  VALUE ZEROS.                 
           05  WS-F310-READ              PIC 9(5)  VALUE ZEROS.                 
           05  WS-K454-READ              PIC 9(5)  VALUE ZEROS.                 
           05  WS-K800-READ              PIC 9(5)  VALUE ZEROS.                 
           05  WS-K452-READ              PIC 9(5)  VALUE ZEROS.                 
           05  WS-K800-REWRITE           PIC 9(5)  VALUE ZEROS.                 
           05  WS-K452-REWRITE           PIC 9(5)  VALUE ZEROS.                 
           05  WS-K454-DELETE            PIC 9(5)  VALUE ZEROS.                 
                                                                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
      *------------------*                                                      
       0000-MAIN-LOGIC.                                                         
      *------------------*                                                      
           PERFORM  1000-OPEN-FILES      THRU 1000-EXIT.                        
           PERFORM  2000-READ-BP13F310   THRU 2000-EXIT.                        
           PERFORM  3000-MAIN-PROCESS    THRU 3000-EXIT                         
                    UNTIL F310-EOF.                                             
           PERFORM  9999-CLOSE-FILES     THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       1000-OPEN-FILES.                                                         
      *------------------*                                                      
           OPEN INPUT BP13F310                                                  
                  I-O BP13K452                                                  
                      BP13K454                                                  
                      BP13K800.                                                 
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR IN OPENING BP13K452 : ' WS-K452-STATUS             
              MOVE WS-K452-STATUS        TO RETURN-CODE                         
              PERFORM  9999-CLOSE-FILES  THRU 9999-EXIT.                        
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR IN OPENING BP13K454 : ' WS-K454-STATUS             
              MOVE WS-K454-STATUS        TO RETURN-CODE                         
              PERFORM  9999-CLOSE-FILES  THRU 9999-EXIT.                        
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR IN OPENING BP13K800 : ' WS-K800-STATUS             
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
              PERFORM  9999-CLOSE-FILES  THRU 9999-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13F310.                                                      
      *-------------------*                                                     
           READ BP13F310                 AT END                                 
           MOVE 'Y'                      TO WS-F310-EOF                         
           GO TO 2000-EXIT.                                                     
                                                                                
           ADD  1                        TO WS-F310-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       3000-MAIN-PROCESS.                                                       
      *-------------------*                                                     
           MOVE F310-REGN-NO  TO K800-NUM-REGN.                                 
           READ BP13K800 KEY  IS K800-NUM-REGN.                                 
                                                                                
           IF WS-K800-STATUS = 00                                               
              ADD  1          TO WS-K800-READ                                   
              PERFORM 4000-PROCESS-BP13K800   THRU 4000-EXIT                    
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 DISPLAY 'REC NOT FOUND IN BP13K800 KEY IS '                    
                                           K800-NUM-REGN                        
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800, STATUS - '                    
                         WS-K800-STATUS ' KEY IS ' K800-NUM-REGN                
                 MOVE WS-K800-STATUS     TO RETURN-CODE.                        
                                                                                
           PERFORM  2000-READ-BP13F310   THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       4000-PROCESS-BP13K800.                                                   
      *----------------------*                                                  
           IF  K800-NUM-STATUS = 'C'                                            
               PERFORM 5000-PROCESS-BP13K454 THRU 5000-EXIT                     
           ELSE                                                                 
               GO TO 4000-EXIT.                                                 
                                                                                
           MOVE SPACES                   TO K800-DTE-AO-APPT.                   
           MOVE SPACES                   TO K800-TME-AO-APPT.                   
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'ERROR REWRITING BP13K800, STATUS - '                     
                      WS-K800-STATUS ' KEY IS ' K800-NUM-REGN                   
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           ELSE                                                                 
              ADD  1                     TO WS-K800-REWRITE.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       5000-PROCESS-BP13K454.                                                   
      *---------------------*                                                   
           MOVE 'SA'                     TO K454-CDE-APPT-TYPE.                 
           MOVE K800-NUM-REGN            TO K454-NUM-REGN.                      
                                                                                
           READ BP13K454 KEY IS K454-ALT1-KEY.                                  
           IF WS-K454-STATUS = 00                                               
              ADD  1                     TO WS-K454-READ                        
              PERFORM 6000-PROCESS-BP13K452 THRU 6000-EXIT                      
              PERFORM 8000-DELETE-BP13K454  THRU 8000-EXIT                      
           ELSE                                                                 
              IF WS-K454-STATUS = 23                                            
                 DISPLAY 'REC NOT FOUND IN BP13K454 KEY IS '                    
                                           K454-ALT1-KEY                        
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K454, STATUS - '                    
                         WS-K454-STATUS ' KEY IS ' K454-ALT1-KEY                
                 MOVE WS-K454-STATUS     TO RETURN-CODE.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       6000-PROCESS-BP13K452.                                                   
      *----------------------*                                                  
           MOVE K454-DTE-APPT            TO K452-KEY-FLD.                       
                                                                                
           READ BP13K452.                                                       
           IF WS-K452-STATUS = 00                                               
              ADD  1                     TO WS-K452-READ                        
              MOVE 0                     TO WS-CNT                              
              PERFORM 7000-UPDATE-TIMESLOT    THRU 7000-EXIT                    
                UNTIL WS-COMPLETE-TAG = 'Y'                                     
           ELSE                                                                 
              IF WS-K452-STATUS = 23                                            
                 DISPLAY 'REC NOT FOUND IN BP13K452 KEY IS '                    
                                           K452-KEY-FLD                         
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K452, STATUS - '                    
                         WS-K452-STATUS ' KEY IS ' K452-KEY-FLD                 
                 MOVE WS-K452-STATUS     TO RETURN-CODE.                        
                                                                                
           REWRITE BP13K452-REC.                                                
           IF WS-K452-STATUS NOT = 00                                           
              DISPLAY 'ERROR REWRITING BP13K452, STATUS - '                     
                      WS-K452-STATUS ' KEY IS ' K452-KEY-FLD                    
              MOVE WS-K452-STATUS        TO RETURN-CODE                         
           ELSE                                                                 
              ADD 1                      TO WS-K452-REWRITE.                    
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       7000-UPDATE-TIMESLOT.                                                    
      *--------------------*                                                    
           ADD 1 TO WS-CNT.                                                     
           IF K452-TME-SLOT (WS-CNT) = K454-TME-APPT                            
              MOVE 'Y' TO WS-COMPLETE-TAG                                       
              SUBTRACT 1  FROM K452-NUM-TOT-SA (WS-CNT)                         
           END-IF.                                                              
                                                                                
           IF (K452-PM-TME-SLOT (WS-CNT) = K454-TME-APPT) AND                   
              (WS-COMPLETE-TAG NOT = 'Y')                                       
              MOVE 'Y' TO WS-COMPLETE-TAG                                       
              SUBTRACT 1  FROM K452-PM-TOT-SA(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF (K452-OT-TME-SLOT (WS-CNT) = K454-TME-APPT) AND                   
              (WS-COMPLETE-TAG NOT = 'Y')                                       
              MOVE 'Y' TO WS-COMPLETE-TAG                                       
              SUBTRACT 1  FROM K452-OT-TOT-SA(WS-CNT)                           
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       8000-DELETE-BP13K454.                                                    
      *--------------------*                                                    
           DELETE BP13K454.                                                     
           IF WS-K454-STATUS NOT = 00                                           
              DISPLAY 'ERROR DELETING BP13K454, STATUS - '                      
                      WS-K454-STATUS ' KEY IS ' K454-ALT1-KEY                   
              MOVE WS-K454-STATUS        TO RETURN-CODE                         
           ELSE                                                                 
              ADD 1                      TO WS-K454-DELETE.                     
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       9999-CLOSE-FILES.                                                        
      *--------------------*                                                    
           DISPLAY '********************************************'.              
           DISPLAY '     CONTROL REPORT FOR BP13C456            '.              
           DISPLAY '********************************************'.              
           DISPLAY '                                            '.              
           DISPLAY 'NO. READ FROM BP13F310    : ' WS-F310-READ.                 
           DISPLAY 'NO. READ FROM BP13K800    : ' WS-K800-READ.                 
           DISPLAY 'NO. READ FROM BP13K452    : ' WS-K452-READ.                 
           DISPLAY 'NO. READ FROM BP13K454    : ' WS-K454-READ.                 
           DISPLAY 'NO. REWRITTEN IN BP13K452 : ' WS-K452-REWRITE.              
           DISPLAY 'NO. REWRITTEN IN BP13K800 : ' WS-K800-REWRITE.              
           DISPLAY 'NO. DELETED FROM BP13K454 : ' WS-K454-DELETE.               
           DISPLAY '                                            '.              
           DISPLAY '********************************************'.              
           CLOSE BP13K452                                                       
                 BP13F310                                                       
                 BP13K800                                                       
                 BP13K454.                                                      
                                                                                
           IF WS-K452-STATUS NOT = 00                                           
              DISPLAY 'ERROR IN CLOSING BP13K452 : ' WS-K452-STATUS             
              MOVE WS-K452-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K454-STATUS NOT = 00                                           
              DISPLAY 'ERROR IN CLOSING BP13K454 : ' WS-K454-STATUS             
              MOVE WS-K454-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'ERROR IN CLOSING BP13K800 : ' WS-K800-STATUS             
              MOVE WS-K800-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
