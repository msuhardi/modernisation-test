       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23G.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  25/01/2008.                                               
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   :                                       *                
      *    1.  TO PRINT REPORT WITH MULTIPLE APPLICATIONS      *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1    BP13F200                                       *                
      *    2.   BP13K800                                       *                
      *    3.   BP13K820                                       *                
      *    4.   BM06K110                                       *                
      *                                                        *                
      *    OUPPUT FILES:                                       *                
      *    1    BP13L23G                                       *                
      *                                                        *                
      *========================================================*                
      * CHG-NO    BY  ON             DESCRIPTION               *                
      * -------- ---- ---------  ----------------------------  *                
      * BP133229 RB12 28/01/2008 NEW PROGRAM                   *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-9121.                                               
       OBJECT-COMPUTER. IBM-9121.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200        ASSIGN        TO BP13F200.                    
                                                                                
           SELECT BM06K110        ASSIGN        TO BM06K110                     
                                  ORGANIZATION  IS INDEXED                      
                                  ACCESS MODE   IS RANDOM                       
                                  RECORD KEY    IS K110-KEY-FLD                 
                                  FILE STATUS   IS WS-BM06K110-STATUS.          
                                                                                
           SELECT BP13K800        ASSIGN        TO BP13K800                     
                                  ORGANIZATION  IS INDEXED                      
                                  ACCESS MODE   IS RANDOM                       
                                  RECORD KEY    IS K800-NUM-REGN                
                                  FILE STATUS   IS WS-BP13K800-STATUS.          
                                                                                
           SELECT BP13K820        ASSIGN        TO BP13K820                     
                                  ACCESS MODE   IS DYNAMIC                      
                                  ORGANIZATION  IS INDEXED                      
                                  RECORD KEY    IS K820-KEY-FLD                 
                                  ALTERNATE KEY IS K820-NUM-NRIC                
                                  FILE STATUS   IS WS-BP13K820-STATUS.          
                                                                                
           SELECT BP13L23G         ASSIGN       TO BP13L23G.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F200                                                            
            RECORDING MODE IS F                                                 
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F200.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13L23G                                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L23G-PRINT-REC              PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  WS-BM06K110-STATUS      PIC 9(02) VALUE ZEROES.                  
           05  WS-BP13K800-STATUS      PIC 9(02) VALUE ZEROES.                  
           05  WS-BP13K820-STATUS      PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05 WS-SYS-DATE              PIC X(08) VALUE SPACES.                  
           05 FILLER                   PIC X(13) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05 WS-DISP-DD               PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE '/'.                     
           05 WS-DISP-MM               PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE '/'.                     
           05 WS-DISP-CCYY             PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-NUM-NRIC-TBL.                                                     
           05  WS-NUM-NRIC              PIC X(09) OCCURS 12 TIMES.              
                                                                                
       01  WS-K820-TBL.                                                         
           05  WS-K820-KEY OCCURS 20 TIMES.                                     
               10  WS-K820-REGN        PIC X(08).                               
               10  WS-K820-NRIC        PIC X(09).                               
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F200-READ             PIC 9(07) VALUE ZEROES.                  
           05 WS-K110-FOUND            PIC 9(07) VALUE ZEROES.                  
           05 WS-K110-NOT-FOUND        PIC 9(07) VALUE ZEROES.                  
           05 WS-LINE-CTR              PIC 9(07) VALUE 60.                      
           05 WS-APPLN-CTR             PIC 9(07) VALUE ZEROES.                  
           05 WS-PAGE-CTR              PIC 9(07) VALUE ZEROES.                  
           05 WS-SNO-CTR               PIC 9(04) VALUE ZEROES.                  
           05 WS-SNO-CTR2              PIC 9(02) VALUE ZEROES.                  
           05 WS-CTR                   PIC 9(02) VALUE ZEROES.                  
           05 WS-CTR2                  PIC 9(02) VALUE ZEROES.                  
           05 WS-CTR3                  PIC 9(02) VALUE ZEROES.                  
           05 WS-REGN-CTR              PIC 9(02) VALUE ZEROES.                  
           05 WS-QUEUE                 PIC X(07) VALUE SPACES.                  
                                                                                
       01  WS-FLAGS.                                                            
           05 F200-EOF                 PIC X(01) VALUE 'N'.                     
           05 WS-K820-FND              PIC X(01) VALUE 'N'.                     
           05 WS-REC-FIRST             PIC X(01) VALUE SPACES.                  
           05 WS-SNO-FIRST             PIC X(01) VALUE SPACES.                  
           05 WS-MAIN-REGN             PIC X(01) VALUE SPACES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-PRJCT             PIC X(03) VALUE SPACES.                  
           05 WS-NUM-SCH-ACC           PIC X(11) VALUE SPACES.                  
           05 WS-PRINT-IND             PIC 9(02) VALUE ZEROES.                  
                                                                                
      *--------------------------------*                                        
      *  BP13L23G REPORT LAYOUT        *                                        
      *--------------------------------*                                        
       01  L23G-HEADER-1.                                                       
           05 FILLER                   PIC X(08) VALUE 'BP13L23G'.              
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'HDB3'.                  
           05 FILLER                   PIC X(30) VALUE SPACES.                  
           05 FILLER                   PIC X(30) VALUE                          
             '    SYSTEM OF COMMITMENT    '.                                    
           05 FILLER                   PIC X(24) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'DATE : '.               
           05 L23G-DATE                PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'PAGE: '.                
           05 L23G-PAGE                PIC ZZZZ9.                               
                                                                                
       01  L23G-HEADER-2.                                                       
           05 FILLER                   PIC X(28) VALUE SPACES.                  
           05 FILLER                   PIC X(55) VALUE                          
           'REPORT ON CASES WITH NRICS HAVING MULTIPLE APPLICATIONS'.           
           05 FILLER                   PIC X(03) VALUE ' - '.                   
           05 L23G-ZONE                PIC X(03) VALUE SPACES.                  
                                                                                
       01  L23G-HEADER-3.                                                       
           05 FILLER                   PIC X(04) VALUE SPACES.                  
           05 FILLER                   PIC X(36) VALUE                          
             'S/NO   REGN       NRIC NO     NAME'.                              
           05 FILLER                   PIC X(37) VALUE SPACES.                  
           05 FILLER                   PIC X(41) VALUE                          
             '              ACCEPTANCE DATE   SOCA DATE'.                       
                                                                                
       01  L23G-NO-RECORDS-LINE.                                                
           05 FILLER                   PIC X(04) VALUE SPACES.                  
           05 L23G-NO-RECORDS-TXT      PIC X(26) VALUE                          
              'NO RECORDS WITH DUPLICATES'.                                     
                                                                                
       01  L23G-MAIN-LINE-1.                                                    
           05 FILLER                   PIC X(04) VALUE SPACES.                  
           05 L23G-NO                  PIC ZZZ9.                                
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 L23G-REGN-NO             PIC X(08) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 L23G-NRIC-NO             PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 L23G-NAME                PIC X(40) VALUE SPACES.                  
           05 FILLER                   PIC X(45) VALUE SPACES.                  
                                                                                
       01  L23G-MAIN-LINE-2.                                                    
           05 FILLER                   PIC X(11) VALUE SPACES.                  
           05 L23G-NUM-QUEUE           PIC X(07) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE SPACES.                  
           05 L23G-NRIC-NO-2           PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 L23G-NAME-2              PIC X(40) VALUE SPACES.                  
                                                                                
       01  L23G-DUP-LINE-1.                                                     
           05 FILLER                   PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(10) VALUE                          
           'DUPLICATE '.                                                        
           05 L23G-DUP-NO              PIC Z9.                                  
                                                                                
       01  L23G-DUP-LINE-2.                                                     
           05 FILLER                   PIC X(11) VALUE SPACES.                  
           05 L23G-DUP-REGN-NO         PIC X(08) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE '('.                     
           05 L23G-DUP-ALLO-CAT        PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE ')'.                     
                                                                                
       01  L23G-DUP-LINE-3.                                                     
           05 FILLER                   PIC X(20) VALUE SPACES.                  
           05 L23G-DUP-NRIC-IND        PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L23G-DUP-NRIC            PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 L23G-DUP-NAME            PIC X(40) VALUE SPACES.                  
                                                                                
       01  L23G-DUP-LINE-4.                                                     
           05 FILLER                   PIC X(11) VALUE SPACES.                  
           05 L23G-SCH-ACC-TEXT        PIC X(13) VALUE SPACES.                  
           05 L23G-DUP-SCH-ACC         PIC X(11) VALUE SPACES.                  
           05 L23G-DASH                PIC X(03) VALUE SPACES.                  
           05 L23G-ADDRESS             PIC X(98) VALUE SPACES.                  
                                                                                
       01  L23G-DUP-LINE-5.                                                     
           05 FILLER                   PIC X(91) VALUE SPACES.                  
           05 L23G-DTE-ACCEPT          PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(08) VALUE SPACES.                  
           05 L23G-DTE-SALE-AGMT       PIC X(10) VALUE SPACES.                  
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH          PIC X(04).                               
           05 WS-LINK-NUM-ACC          PIC X(04).                               
           05 WS-LINK-NUM-CHK          PIC X(01).                               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-CONTROL.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-OPEN-FILE      THRU 1000-EXIT.                          
                                                                                
           PERFORM 2000-READ-BP13F200  THRU 2000-EXIT.                          
                                                                                
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
             UNTIL F200-EOF = 'Y'.                                              
                                                                                
           IF WS-SNO-CTR = 0                                                    
              MOVE 8                   TO   WS-PRINT-IND                        
              PERFORM 7000-PRINT-LINE  THRU 7000-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------*                                         
       1000-OPEN-FILE.                                                          
      *-------------------------------*                                         
                                                                                
            OPEN INPUT  BM06K110                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13F200                                                
                OUTPUT  BP13L23G.                                               
                                                                                
            IF WS-BM06K110-STATUS NOT = 0 AND 97                                
              DISPLAY 'BM06K110 OPEN ERROR : ' WS-BM06K110-STATUS               
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
            END-IF.                                                             
                                                                                
            IF WS-BP13K800-STATUS NOT = 0 AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR : ' WS-BP13K800-STATUS               
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
            END-IF.                                                             
                                                                                
            IF WS-BP13K820-STATUS NOT = 0 AND 97                                
              DISPLAY 'BP13K820 OPEN ERROR : ' WS-BP13K820-STATUS               
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE TO   WS-SYSTEM-DATE.                     
            MOVE WS-SYS-DATE(1:4)      TO   WS-DISP-CCYY.                       
            MOVE WS-SYS-DATE(5:2)      TO   WS-DISP-MM.                         
            MOVE WS-SYS-DATE(7:2)      TO   WS-DISP-DD.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------*                                         
       2000-READ-BP13F200.                                                      
      *-------------------------------*                                         
                                                                                
           READ BP13F200                                                        
             AT END                                                             
               MOVE 'Y'               TO F200-EOF                               
             GO TO 2000-EXIT.                                                   
                                                                                
           MOVE F200-NUM-NT-ZONE      TO L23G-ZONE.                             
                                                                                
           ADD  1                     TO WS-F200-READ.                          
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORD.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE 'Y'                   TO   WS-MAIN-REGN.                        
           MOVE ZEROES                TO   WS-CTR WS-SNO-CTR2.                  
           MOVE SPACES                TO   WS-NUM-SCH-ACC.                      
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
             UNTIL WS-CTR > 12                                                  
             MOVE SPACES              TO   WS-NUM-NRIC(WS-CTR)                  
           END-PERFORM.                                                         
                                                                                
           PERFORM 4000-READ-BP13K800 THRU 4000-EXIT.                           
           PERFORM 2000-READ-BP13F200 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       4000-READ-BP13K800.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE F200-NUM-REGN                  TO   K800-NUM-REGN.              
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-BP13K800-STATUS                                          
           WHEN 00                                                              
             IF K800-NUM-STATUS NOT = 'C'                                       
                STRING '(' K800-NUM-NT-FT-QUEUE ')'                             
                    DELIMITED BY SIZE          INTO WS-QUEUE                    
                PERFORM 4025-GET-DUPLICATES    THRU 4025-EXIT                   
             END-IF                                                             
           WHEN 23                                                              
             DISPLAY 'BP13K800 RECORD NOT FOUND : ' K800-NUM-REGN               
           WHEN OTHER                                                           
             MOVE WS-BP13K800-STATUS           TO   RETURN-CODE                 
             DISPLAY 'BP13K800 READ FILE ERROR : ' WS-BP13K800-STATUS           
             PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4025-GET-DUPLICATES.                                                     
      *--------------------------------*                                        
           MOVE SPACES                    TO   K820-KEY-FLD.                    
           MOVE K800-NUM-REGN             TO   K820-NUM-REGN.                   
           MOVE ZEROES                    TO   WS-CTR.                          
                                                                                
           START BP13K820 KEY IS GREATER OR EQUAL K820-KEY-FLD.                 
                                                                                
           PERFORM 4050-READNEXT-BP13K820 THRU 4050-EXIT                        
             UNTIL K820-NUM-REGN NOT = K800-NUM-REGN OR                         
                   K820-KEY-FLD = HIGH-VALUES.                                  
                                                                                
           MOVE K800-NUM-SCH-ACC          TO   WS-NUM-SCH-ACC.                  
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
             UNTIL WS-CTR > 12                                                  
              IF WS-NUM-NRIC(WS-CTR) NOT = SPACES AND LOW-VALUES                
                 MOVE ZEROES              TO   WS-REGN-CTR                      
                 PERFORM VARYING WS-CTR2 FROM 1 BY 1                            
                   UNTIL WS-CTR2 > 20                                           
                   MOVE SPACES            TO   WS-K820-KEY(WS-CTR2)             
                 END-PERFORM                                                    
                 PERFORM 4100-CHECK-MULT  THRU 4100-EXIT                        
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-REGN-CTR > 0                                                   
              PERFORM VARYING WS-CTR2 FROM 1 BY 1                               
                UNTIL WS-CTR2 > WS-REGN-CTR                                     
                PERFORM 4500-READ-BP13K800 THRU 4500-EXIT                       
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       4025-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4050-READNEXT-BP13K820.                                                  
      *--------------------------------*                                        
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END MOVE HIGH-VALUES   TO   K820-KEY-FLD                        
             GO TO 4050-EXIT.                                                   
                                                                                
           EVALUATE WS-BP13K820-STATUS                                          
           WHEN 00                                                              
           WHEN 02                                                              
             IF K820-NUM-REGN = K800-NUM-REGN                                   
                ADD 1 TO WS-CTR                                                 
                MOVE K820-NUM-NRIC     TO   WS-NUM-NRIC(WS-CTR)                 
             ELSE                                                               
                MOVE HIGH-VALUES       TO   K820-KEY-FLD                        
             END-IF                                                             
           WHEN 23                                                              
             DISPLAY 'BP13K820 RECORD NOT FOUND : ' K820-KEY-FLD                
           WHEN  OTHER                                                          
             MOVE WS-BP13K820-STATUS   TO   RETURN-CODE                         
             DISPLAY 'BP13K820 READ FILE ERROR : ' WS-BP13K820-STATUS           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4100-CHECK-MULT.                                                         
      *--------------------------------*                                        
                                                                                
           MOVE 'Y'                        TO   WS-REC-FIRST.                   
           MOVE 'Y'                        TO   WS-SNO-FIRST.                   
           MOVE ZEROES                     TO   WS-APPLN-CTR.                   
                                                                                
           MOVE SPACES                     TO   K820-KEY-FLD.                   
           MOVE WS-NUM-NRIC(WS-CTR)        TO   K820-NUM-NRIC.                  
                                                                                
           START BP13K820 KEY IS GREATER OR EQUAL K820-NUM-NRIC.                
                                                                                
           PERFORM 4200-READNEXT-BP13K820  THRU 4200-EXIT                       
             UNTIL K820-NUM-NRIC NOT = WS-NUM-NRIC(WS-CTR) OR                   
                   K820-KEY-FLD = HIGH-VALUES.                                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4200-READNEXT-BP13K820.                                                  
      *--------------------------------*                                        
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END MOVE HIGH-VALUES   TO   K820-KEY-FLD                        
             GO TO 4200-EXIT.                                                   
                                                                                
           EVALUATE WS-BP13K820-STATUS                                          
           WHEN 00                                                              
           WHEN 02                                                              
             IF K820-NUM-NRIC = WS-NUM-NRIC(WS-CTR)                             
                IF K820-NUM-REGN NOT = F200-NUM-REGN                            
                   PERFORM 4250-CHECK-REGNS THRU 4250-EXIT                      
                END-IF                                                          
             ELSE                                                               
                MOVE HIGH-VALUES       TO   K820-KEY-FLD                        
             END-IF                                                             
           WHEN 23                                                              
             DISPLAY 'BP13K820 RECORD NOT FOUND : ' K820-KEY-FLD                
           WHEN  OTHER                                                          
             MOVE WS-BP13K820-STATUS   TO   RETURN-CODE                         
             DISPLAY 'BP13K820 READ FILE ERROR : ' WS-BP13K820-STATUS           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4250-CHECK-REGNS.                                                        
      *--------------------------------*                                        
                                                                                
           PERFORM VARYING WS-CTR3 FROM 1 BY 1                                  
             UNTIL WS-CTR3 > WS-REGN-CTR                                        
               IF WS-K820-REGN(WS-CTR3) = K820-NUM-REGN                         
                  GO TO 4250-EXIT                                               
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           ADD 1 TO WS-REGN-CTR.                                                
           MOVE K820-KEY-FLD   TO   WS-K820-KEY(WS-REGN-CTR).                   
                                                                                
       4250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4500-READ-BP13K800.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE WS-K820-REGN(WS-CTR2) TO   K800-NUM-REGN.                       
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-BP13K800-STATUS                                          
           WHEN 00                                                              
             IF (K800-NUM-SCH-ACC NOT EQUAL SPACES AND LOW-VALUES) AND          
                (K800-NUM-STATUS NOT = 'C')                                     
                PERFORM 5000-PRINT    THRU 5000-EXIT                            
             END-IF                                                             
           WHEN 23                                                              
             DISPLAY 'BP13K800 RECORD NOT FOUND : ' K800-NUM-REGN               
           WHEN OTHER                                                           
             MOVE WS-BP13K800-STATUS  TO   RETURN-CODE                          
             DISPLAY 'BP13K800 READ FILE ERROR : ' WS-BP13K800-STATUS           
             PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                            
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-PRINT.                                                              
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55                                                  
              PERFORM 5300-PRINT-HEADER      THRU 5300-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-MAIN-REGN = 'Y'                                                
              MOVE 'N'                       TO   WS-MAIN-REGN                  
              PERFORM 5100-PRINT-MAIN        THRU 5100-EXIT                     
              MOVE SPACES                    TO   K820-KEY-FLD                  
              MOVE F200-NUM-REGN             TO   K820-NUM-REGN                 
              START BP13K820 KEY IS GREATER OR EQUAL K820-KEY-FLD               
              PERFORM 6600-READNEXT-BP13K820 THRU 6600-EXIT                     
                UNTIL K820-NUM-REGN NOT = F200-NUM-REGN OR                      
                      K820-KEY-FLD = HIGH-VALUES                                
           END-IF.                                                              
                                                                                
           PERFORM 5200-PRINT-DUP            THRU 5200-EXIT.                    
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       5100-PRINT-MAIN.                                                         
      *--------------------------------*                                        
           MOVE SPACES                  TO   L23G-MAIN-LINE-1.                  
                                                                                
           ADD 1                        TO   WS-SNO-CTR.                        
                                                                                
           MOVE WS-SNO-CTR              TO   L23G-NO.                           
                                                                                
           MOVE F200-NUM-REGN           TO   L23G-REGN-NO.                      
                                                                                
           MOVE WS-NUM-NRIC(1)          TO   L23G-NRIC-NO                       
                                            K820-NUM-NRIC.                      
                                                                                
           MOVE F200-NUM-REGN           TO   K820-NUM-REGN.                     
                                                                                
           PERFORM 6000-READ-BP13K820   THRU 6000-EXIT.                         
                                                                                
           IF WS-K820-FND = 'Y'                                                 
              MOVE K820-NME-OCCP(1:40)  TO  L23G-NAME                           
           END-IF.                                                              
                                                                                
           MOVE 1 TO WS-PRINT-IND.                                              
                                                                                
           PERFORM 7000-PRINT-LINE      THRU 7000-EXIT.                         
                                                                                
           IF WS-NUM-NRIC(2) EQUAL SPACES OR LOW-VALUES                         
              GO TO 5100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-NRIC(2)          TO   K820-NUM-NRIC.                     
                                                                                
           PERFORM 6000-READ-BP13K820   THRU 6000-EXIT.                         
                                                                                
           IF WS-K820-FND NOT = 'Y'                                             
              MOVE SPACES               TO   K820-NME-OCCP                      
           END-IF.                                                              
                                                                                
           PERFORM 6700-PRINT-MAIN-LINE THRU 6700-EXIT.                         
                                                                                
           MOVE SPACES                  TO   WS-QUEUE.                          
                                                                                
           IF WS-NUM-NRIC(3) EQUAL SPACES OR LOW-VALUES                         
              GO TO 5100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-NRIC(3)          TO   K820-NUM-NRIC.                     
                                                                                
           PERFORM 6000-READ-BP13K820   THRU 6000-EXIT.                         
                                                                                
           IF WS-K820-FND NOT = 'Y'                                             
              MOVE SPACES               TO   K820-NME-OCCP                      
           END-IF.                                                              
                                                                                
           PERFORM 6700-PRINT-MAIN-LINE THRU 6700-EXIT.                         
                                                                                
           IF WS-NUM-NRIC(4) EQUAL SPACES OR LOW-VALUES                         
              GO TO 5100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-NRIC(4)          TO   K820-NUM-NRIC.                     
                                                                                
           PERFORM 6000-READ-BP13K820   THRU 6000-EXIT.                         
                                                                                
           IF WS-K820-FND NOT = 'Y'                                             
              MOVE SPACES               TO   K820-NME-OCCP                      
           END-IF.                                                              
                                                                                
           PERFORM 6700-PRINT-MAIN-LINE THRU 6700-EXIT.                         
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5200-PRINT-DUP.                                                          
      *--------------------------------*                                        
                                                                                
           ADD 1 TO WS-SNO-CTR2.                                                
                                                                                
           MOVE WS-SNO-CTR2               TO   L23G-DUP-NO.                     
                                                                                
           MOVE SPACES                    TO   K820-KEY-FLD.                    
                                                                                
           MOVE WS-K820-REGN(WS-CTR2)     TO   L23G-DUP-REGN-NO                 
                                               K820-NUM-REGN.                   
                                                                                
           MOVE K800-NUM-ALLO-CAT         TO   L23G-DUP-ALLO-CAT.               
                                                                                
           MOVE 3                         TO   WS-PRINT-IND.                    
                                                                                
           PERFORM 7000-PRINT-LINE        THRU 7000-EXIT.                       
                                                                                
           MOVE 4                         TO   WS-PRINT-IND.                    
                                                                                
           PERFORM 7000-PRINT-LINE        THRU 7000-EXIT.                       
                                                                                
           START BP13K820 KEY IS GREATER OR EQUAL K820-KEY-FLD.                 
                                                                                
           PERFORM 6300-READNEXT-BP13K820 THRU 6300-EXIT                        
             UNTIL K820-NUM-REGN NOT = WS-K820-REGN(WS-CTR2) OR                 
                   K820-KEY-FLD = HIGH-VALUES.                                  
                                                                                
           MOVE SPACES                    TO   L23G-DUP-LINE-5.                 
                                                                                
           PERFORM 6200-READ-BM06K110   THRU 6200-EXIT.                         
                                                                                
           IF K800-DTE-SALE-AGMT NOT = SPACES AND LOW-VALUES                    
              STRING K800-DTE-SALE-AGMT(7:2) '/'                                
                     K800-DTE-SALE-AGMT(5:2) '/'                                
                     K800-DTE-SALE-AGMT(1:4)                                    
                 DELIMITED BY SIZE      INTO L23G-DTE-SALE-AGMT                 
           END-IF.                                                              
                                                                                
           MOVE 7                         TO   WS-PRINT-IND.                    
                                                                                
           PERFORM 7000-PRINT-LINE        THRU 7000-EXIT.                       
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5300-PRINT-HEADER.                                                       
      *--------------------------------*                                        
                                                                                
           ADD 1                TO   WS-PAGE-CTR.                               
           MOVE WS-DISP-DATE    TO   L23G-DATE.                                 
           MOVE WS-PAGE-CTR     TO   L23G-PAGE.                                 
                                                                                
           MOVE SPACES          TO   L23G-PRINT-REC.                            
           WRITE L23G-PRINT-REC FROM L23G-HEADER-1 AFTER PAGE.                  
           WRITE L23G-PRINT-REC FROM L23G-HEADER-2 AFTER 1.                     
           WRITE L23G-PRINT-REC FROM L23G-HEADER-3 AFTER 2.                     
           MOVE 6               TO   WS-LINE-CTR.                               
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6000-READ-BP13K820.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE 'N' TO WS-K820-FND.                                             
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           EVALUATE WS-BP13K820-STATUS                                          
           WHEN 00                                                              
             MOVE 'Y'                  TO WS-K820-FND                           
           WHEN 23                                                              
             DISPLAY 'BP13K820 RECORD NOT FOUND : ' K820-KEY-FLD                
           WHEN OTHER                                                           
             MOVE WS-BP13K820-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K820 READ FILE ERROR : ' WS-BP13K820-STATUS           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6100-MOVE-DUP-IND.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM VARYING WS-CTR3 FROM 1 BY 1                                  
             UNTIL WS-CTR3 > 12                                                 
             IF L23G-DUP-NRIC = WS-NUM-NRIC(WS-CTR3)                            
                MOVE '*' TO L23G-DUP-NRIC-IND                                   
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6200-READ-BM06K110.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE K800-NUM-SCH-ACC TO K110-KEY-FLD.                               
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD.                                   
                                                                                
           EVALUATE WS-BM06K110-STATUS                                          
           WHEN 00                                                              
             ADD 1                     TO WS-K110-FOUND                         
             IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES                 
                STRING K110-DTE-ACCEPTANCE(7:2) '/'                             
                       K110-DTE-ACCEPTANCE(5:2) '/'                             
                       K110-DTE-ACCEPTANCE (1:4)                                
                    DELIMITED BY SIZE   INTO L23G-DTE-ACCEPT                    
             ELSE                                                               
                MOVE SPACES               TO L23G-DTE-ACCEPT                    
             END-IF                                                             
           WHEN 23                                                              
             ADD 1                     TO WS-K110-NOT-FOUND                     
             DISPLAY 'BM06K110 RECORD NOT FOUND : ' K110-KEY-FLD                
           WHEN OTHER                                                           
             MOVE WS-BM06K110-STATUS   TO RETURN-CODE                           
             DISPLAY 'BM06K110 READ FILE ERROR : ' WS-BM06K110-STATUS           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6300-READNEXT-BP13K820.                                                  
      *--------------------------------*                                        
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END MOVE HIGH-VALUES   TO K820-KEY-FLD                          
             GO TO 6300-EXIT.                                                   
                                                                                
           EVALUATE WS-BP13K820-STATUS                                          
           WHEN 00                                                              
           WHEN 02                                                              
             IF K820-NUM-REGN = WS-K820-REGN(WS-CTR2)                           
                PERFORM 6400-PRINT-DUP-LINE THRU 6400-EXIT                      
             ELSE                                                               
                IF WS-SNO-CTR2 > 0                                              
                   IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES              
                      MOVE SPACES TO L23G-DUP-LINE-4                            
                      MOVE 'SCH ACC NO: '   TO L23G-SCH-ACC-TEXT                
                      MOVE K800-NUM-SCH-ACC TO L23G-DUP-SCH-ACC                 
                      MOVE ' - '            TO L23G-DASH                        
                      PERFORM 6500-GET-FLAT-ADDR THRU 6500-EXIT                 
                      MOVE 6 TO WS-PRINT-IND                                    
                      PERFORM 7000-PRINT-LINE    THRU 7000-EXIT                 
                   END-IF                                                       
                END-IF                                                          
                MOVE HIGH-VALUES   TO K820-KEY-FLD                              
             END-IF                                                             
           WHEN 23                                                              
             DISPLAY 'BP13K820 RECORD NOT FOUND : ' K820-KEY-FLD                
           WHEN  OTHER                                                          
             MOVE WS-BP13K820-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K820 READ FILE ERROR : ' WS-BP13K820-STATUS           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6400-PRINT-DUP-LINE.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55                                                  
              PERFORM 5300-PRINT-HEADER THRU 5300-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO L23G-DUP-LINE-3.                     
                                                                                
           MOVE K820-NUM-NRIC           TO L23G-DUP-NRIC.                       
                                                                                
           MOVE K820-NME-OCCP(1:40)     TO L23G-DUP-NAME.                       
                                                                                
           PERFORM 6100-MOVE-DUP-IND  THRU 6100-EXIT.                           
                                                                                
           MOVE 5 TO WS-PRINT-IND.                                              
                                                                                
           PERFORM 7000-PRINT-LINE    THRU 7000-EXIT.                           
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6500-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9)   TO WS-LINK-REC.                         
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  ' '                               
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE             
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO L23G-ADDRESS                                        
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' L23G-DUP-REGN-NO          
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' L23G-DUP-REGN-NO                            
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------*                                        
       6600-READNEXT-BP13K820.                                                  
      *--------------------------------*                                        
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END MOVE HIGH-VALUES   TO K820-KEY-FLD                          
             GO TO 6600-EXIT.                                                   
                                                                                
           EVALUATE WS-BP13K820-STATUS                                          
           WHEN 00                                                              
           WHEN 02                                                              
             IF K820-NUM-REGN = F200-NUM-REGN                                   
                IF (K820-NUM-NRIC NOT = WS-NUM-NRIC(1)) AND                     
                   (K820-NUM-NRIC NOT = WS-NUM-NRIC(2)) AND                     
                   (K820-NUM-NRIC NOT = WS-NUM-NRIC(3)) AND                     
                   (K820-NUM-NRIC NOT = WS-NUM-NRIC(4))                         
                   PERFORM 6700-PRINT-MAIN-LINE THRU 6700-EXIT                  
                END-IF                                                          
             ELSE                                                               
                MOVE HIGH-VALUES   TO K820-KEY-FLD                              
             END-IF                                                             
           WHEN 23                                                              
             DISPLAY 'BP13K820 RECORD NOT FOUND : ' K820-KEY-FLD                
           WHEN  OTHER                                                          
             MOVE WS-BP13K820-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K820 READ FILE ERROR : ' WS-BP13K820-STATUS           
             PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6700-PRINT-MAIN-LINE.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE SPACES                  TO L23G-MAIN-LINE-2.                    
                                                                                
           MOVE K820-NUM-NRIC           TO L23G-NRIC-NO-2.                      
                                                                                
           MOVE K820-NME-OCCP(1:40)     TO L23G-NAME-2.                         
                                                                                
           MOVE WS-QUEUE                TO L23G-NUM-QUEUE.                      
                                                                                
           MOVE 2 TO WS-PRINT-IND.                                              
                                                                                
           PERFORM 7000-PRINT-LINE    THRU 7000-EXIT.                           
                                                                                
       6700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       7000-PRINT-LINE.                                                         
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55                                                  
              PERFORM 5300-PRINT-HEADER THRU 5300-EXIT                          
           END-IF.                                                              
                                                                                
           EVALUATE WS-PRINT-IND                                                
              WHEN 1                                                            
                 WRITE L23G-PRINT-REC FROM L23G-MAIN-LINE-1 AFTER 2             
                 ADD 2 TO WS-LINE-CTR                                           
              WHEN 2                                                            
                 WRITE L23G-PRINT-REC FROM L23G-MAIN-LINE-2                     
                 ADD 1 TO WS-LINE-CTR                                           
              WHEN 3                                                            
                 WRITE L23G-PRINT-REC FROM L23G-DUP-LINE-1 AFTER 2              
                 ADD 2 TO WS-LINE-CTR                                           
              WHEN 4                                                            
                 WRITE L23G-PRINT-REC FROM L23G-DUP-LINE-2 AFTER 2              
                 ADD 2 TO WS-LINE-CTR                                           
              WHEN 5                                                            
                 WRITE L23G-PRINT-REC FROM L23G-DUP-LINE-3                      
                 ADD 1 TO WS-LINE-CTR                                           
              WHEN 6                                                            
                 WRITE L23G-PRINT-REC FROM L23G-DUP-LINE-4 AFTER 2              
                 ADD 2 TO WS-LINE-CTR                                           
              WHEN 7                                                            
                 WRITE L23G-PRINT-REC FROM L23G-DUP-LINE-5                      
                 ADD 1 TO WS-LINE-CTR                                           
              WHEN 8                                                            
                 WRITE L23G-PRINT-REC FROM L23G-NO-RECORDS-LINE AFTER 2         
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9999-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID   : BP13C23G'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY 'TOTAL BP13F200 RECS READ     : ' WS-F200-READ.              
           DISPLAY 'CASES WITH MULTIPLE APPLNS   : ' WS-SNO-CTR.                
           DISPLAY 'TOTAL BM06K110 RECS FOUND    : ' WS-K110-FOUND.             
           DISPLAY 'TOTAL BM06K110 RECS NOT FOUND: ' WS-K110-NOT-FOUND.         
                                                                                
           CLOSE BP13F200                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13L23G.                                                      
                                                                                
           IF WS-BM06K110-STATUS NOT = 00                                       
             MOVE WS-BM06K110-STATUS   TO RETURN-CODE                           
             DISPLAY 'BM06K110 CLOSE ERROR : ' WS-BM06K110-STATUS               
           END-IF.                                                              
                                                                                
           IF WS-BP13K800-STATUS NOT = 00                                       
             MOVE WS-BP13K800-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K800 CLOSE ERROR : ' WS-BP13K800-STATUS               
           END-IF.                                                              
                                                                                
           IF WS-BP13K820-STATUS NOT = 00                                       
             MOVE WS-BP13K820-STATUS   TO RETURN-CODE                           
             DISPLAY 'BP13K820 CLOSE ERROR : ' WS-BP13K820-STATUS               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
