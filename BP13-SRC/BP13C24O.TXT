      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C24O.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  12 DECEMBER 2017.                                         
      *===============================================================*         
      * OBJECTIVE  :  TO CREATE BP13F23C FORMAT                       *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F203                                     *         
      *               2. BP13K857                                     *         
      *               3. BP13K800                                     *         
      *               4. BP13K813                                     *         
      *                                                               *         
      * OUTPUT FILE:                                                            
      *               5. BP13F23C                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137145 12/12/2017 ESA1 NEW PROGRAM.                         *         
      * BP137208 14/03/2018 FNP1 ADD 'A3' TYPE-APPMT FOR SERS         *         
      * BP137354 17/08/2018 ESA1 TO UPDATE THE CHECKING OF APPT-DTE   *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F203  ASSIGN        TO BP13F203.                         
                                                                                
           SELECT  BP13K857  ASSIGN        TO BP13K857                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-KEY-FLD                      
                             FILE STATUS   IS WS-K857-STATUS.                   
                                                                                
           SELECT BP13K800   ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
           SELECT  BP13F23C  ASSIGN        TO BP13F23C.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F203                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F203.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F23C                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F23C.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F203             PIC X(1)  VALUE 'N'.                     
           05  WS-K857-FOUND           PIC X(1)  VALUE 'N'.                     
           05  WS-K857-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K813-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F203-READ            PIC 9(07)  VALUE ZEROES.                 
           05  WS-K857-FND             PIC 9(07)  VALUE ZEROES.                 
           05  WS-K857-NFND            PIC 9(07)  VALUE ZEROES.                 
           05  WS-CNT-F23C             PIC 9(07)  VALUE ZEROES.                 
           05  WS-BLNK-EMAIL           PIC 9(07)  VALUE ZEROES.                 
           05  WS-APPT-BF-CURR         PIC 9(07)  VALUE ZEROES.                 
           05  WS-K857-EMAIL           PIC 9(07)  VALUE ZEROES.                 
           05  WS-K800-FND             PIC 9(07)  VALUE ZEROES.                 
           05  WS-K800-NFND            PIC 9(07)  VALUE ZEROES.                 
           05  WS-K813-FND             PIC 9(07)  VALUE ZEROES.                 
           05  WS-K813-NFND            PIC 9(07)  VALUE ZEROES.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-DATE                 PIC X(08) VALUE SPACES.                  
           05  WS-CURRENT-TIME         PIC 9(08) VALUE ZEROES.                  
           05  WS-NUM-QUEUE            PIC X(05) VALUE SPACES.                  
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F203      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F203 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F203                                                 
                       BP13K857                                                 
                       BP13K813                                                 
                       BP13K800                                                 
               OUTPUT  BP13F23C.                                                
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR OPENING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 - ERROR OPENING : ' WS-K813-STATUS              
              MOVE WS-K813-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F203.                                                      
      ******************************************************************        
                                                                                
           READ BP13F203           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F203                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                    TO WS-F203-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4000-READ-BP13K857  THRU 4000-EXIT                           
                                                                                
           IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES                        
              PERFORM 4100-READ-BP13K800     THRU 4100-EXIT                     
              PERFORM 5000-READ-BP13K813     THRU 5000-EXIT                     
              IF F203-DTE-APPMT >= WS-DATE                                      
                 PERFORM 6000-WRITE-BP13F23C    THRU 6000-EXIT                  
              ELSE                                                              
                 ADD 1 TO WS-APPT-BF-CURR                                       
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-BLNK-EMAIL                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F203        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4000-READ-BP13K857.                                                      
      ******************************************************************        
           MOVE SPACES         TO BP13K857-REC.                                 
           INITIALIZE             BP13K857-REC.                                 
                                                                                
           MOVE F203-NUM-REGN  TO K857-NUM-REGN.                                
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
                    ADD 1          TO WS-K857-FND                               
               WHEN 23                                                          
                    MOVE SPACES    TO K857-NUM-EMAIL                            
                    ADD 1          TO WS-K857-NFND                              
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K857 : ' WS-K857-STATUS          
                            ' REGN = ' K857-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4100-READ-BP13K800.                                                      
      *********************                                                     
                                                                                
           INITIALIZE BP13K800-MASTER.                                          
           MOVE F203-NUM-REGN  TO K800-NUM-REGN                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = ZEROS OR 02                                      
              ADD 1 TO WS-K800-FND                                              
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 ADD 1 TO WS-K800-NFND                                          
                 DISPLAY 'READ BP13K800 NOT FD = ' F203-NUM-REGN                
              ELSE                                                              
                 DISPLAY 'READ ERROR, BP13K800 STATUS = '                       
                 WS-K800-STATUS                                                 
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-MGPS = 'P' OR 'C'                                        
              MOVE K800-DTE-BK-APPT-MGPS   TO F203-DTE-APPMT                    
           ELSE                                                                 
              MOVE K800-DTE-BK-APPT        TO F203-DTE-APPMT                    
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-READ-BP13K813.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES               TO BP13K813-REC.                           
           INITIALIZE                   BP13K813-REC.                           
                                                                                
           IF K800-NUM-ALLO-CAT = 'BTO'                                         
              MOVE K800-NUM-BTO-ZONE TO K813-NUM-ZONE                           
           ELSE                                                                 
              MOVE K800-NUM-NEW-TOWN TO K813-NUM-ZONE                           
           END-IF.                                                              
           MOVE K800-NUM-FLAT-TYPE   TO K813-NUM-FLAT-TYPE.                     
           MOVE K800-DTE-BALLOT      TO K813-DTE-BALLOT.                        
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
            WHEN 00                                                             
              ADD 1            TO WS-K813-FND                                   
              IF K813-NUM-QUEUE-START NOT NUMERIC                               
                 MOVE ZEROES   TO K813-NUM-QUEUE-START                          
              END-IF                                                            
                                                                                
              IF K813-NUM-QUEUE-MID NOT NUMERIC                                 
                 MOVE ZEROES   TO K813-NUM-QUEUE-MID                            
              END-IF                                                            
                                                                                
              IF K813-NUM-QUEUE-END NOT NUMERIC                                 
                 MOVE ZEROES   TO K813-NUM-QUEUE-END                            
              END-IF                                                            
            WHEN 23                                                             
              ADD 1                         TO WS-K813-NFND                     
              DISPLAY 'BP13K813 NOT FND : ' K813-KEY-FLD                        
              ' REGN : ' F203-NUM-REGN                                          
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS              
                       ' ZONE = ' K813-NUM-ZONE                                 
                PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-BP13F23C.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO BP13F23C-REC.                     
           INITIALIZE                         BP13F23C-REC.                     
                                                                                
           MOVE K800-NUM-REGN              TO F23C-NUM-REGN.                    
           IF K800-NUM-MGPS = 'P' OR 'C'                                        
              MOVE K800-NUM-QUEUE-MGPS     TO F23C-NUM-QUEUE                    
              MOVE K800-DTE-BK-APPT-MGPS   TO F23C-DTE-APPMT                    
              MOVE K800-TME-BK-APPT-MGPS   TO F23C-TME-APPMT                    
           ELSE                                                                 
              MOVE K800-NUM-NT-FT-QUEUE    TO F23C-NUM-QUEUE                    
              MOVE K800-DTE-BK-APPT        TO F23C-DTE-APPMT                    
              MOVE K800-TME-BK-APPT        TO F23C-TME-APPMT                    
           END-IF.                                                              
                                                                                
           MOVE F23C-NUM-QUEUE             TO WS-NUM-QUEUE.                     
           IF WS-NUM-QUEUE(1:1) NOT NUMERIC                                     
              MOVE ZEROES         TO WS-NUM-QUEUE(1:1)                          
           END-IF.                                                              
           IF WS-NUM-QUEUE(2:1) NOT NUMERIC                                     
              MOVE ZEROES         TO WS-NUM-QUEUE(2:1)                          
           END-IF.                                                              
           IF K800-NUM-ALLO-CAT = 'SER'                                         
              MOVE 'A3'           TO F23C-TYPE-APPMT                            
              MOVE 'S'            TO F23C-NUM-STATUS                            
           ELSE                                                                 
              IF WS-NUM-QUEUE >  K813-NUM-QUEUE-MID                             
                 MOVE 'A2'           TO F23C-TYPE-APPMT                         
                 MOVE 'T'            TO F23C-NUM-STATUS                         
              ELSE                                                              
                 MOVE 'A1'           TO F23C-TYPE-APPMT                         
                 MOVE 'S'            TO F23C-NUM-STATUS                         
              END-IF                                                            
           END-IF.                                                              
           MOVE K800-NUM-ALLO-CAT          TO F23C-NUM-ALLO-MODE.               
           MOVE K800-NUM-NEW-TOWN          TO F23C-NUM-NEW-TOWN.                
           MOVE K800-NUM-FLAT-TYPE         TO F23C-NUM-FLAT-TYPE.               
           MOVE K800-DTE-BALLOT            TO F23C-DTE-BALLOT.                  
           MOVE K813-NUM-UNIT-OFFER        TO F23C-NUM-UNIT-OFFER.              
           MOVE K857-NUM-EMAIL             TO F23C-NUM-EMAIL.                   
           MOVE WS-DATE                    TO F23C-DTE-SEND-EMAIL.              
           ACCEPT WS-CURRENT-TIME   FROM TIME.                                  
           MOVE WS-CURRENT-TIME            TO F23C-TME-SEND-EMAIL.              
                                                                                
           WRITE BP13F23C-REC.                                                  
           ADD 1  TO WS-CNT-F23C.                                               
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C24O'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY ' (1) NO OF BP13F203 RECORDS READ              : '           
                    WS-F203-READ.                                               
           DISPLAY ' (2) NO OF BP13K857 RECORDS FOUND             : '           
                    WS-K857-FND.                                                
           DISPLAY ' (3) NO OF BP13K857 RECORDS NOT FOUND         : '           
                    WS-K857-NFND.                                               
           DISPLAY ' (4) NO OF BP13K857 RECORDS W/ BLANK EMAIL    : '           
                    WS-BLNK-EMAIL.                                              
           DISPLAY ' (5) NO OF REC WRITTEN ON F23C                : '           
                    WS-CNT-F23C.                                                
           DISPLAY ' (6) NO OF REC FOUND BP13K800                 : '           
                    WS-K800-FND.                                                
           DISPLAY ' (7) NO OF REC NOT FOUND BP13K800             : '           
                    WS-K800-NFND.                                               
           DISPLAY ' (8) NO OF REC FOUND BP13K813                 : '           
                    WS-K813-FND.                                                
           DISPLAY ' (9) NO OF REC NOT FOUND BP13K813             : '           
                    WS-K813-NFND.                                               
           DISPLAY ' (9) NO OF REC WITH APPMT < CURRENT DATE      : '           
                    WS-APPT-BF-CURR.                                            
           DISPLAY ' '.                                                         
                                                                                
           CLOSE    BP13F203                                                    
                    BP13K857                                                    
                    BP13K813                                                    
                    BP13K800                                                    
                    BP13F23C.                                                   
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR CLOSING : ' WS-K857-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 - ERROR CLOSING : ' WS-K813-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
