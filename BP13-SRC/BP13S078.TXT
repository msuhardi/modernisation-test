  /***************************************************************/             
  /*  SYSTEM OF COMMITMENT  (SOC)                                */             
  /*=============================================================*/             
  /* REPORT FOR BTO APPLICATION (REPEAT/INVITED)                 */             
  /*-------------------------------------------------------------*/             
  /* CHG REF  DATE     BY   DESCRIPTION                          */             
  /* -------- -------- ---- -----------                          */             
  /* BP133835 20100129 PCL3 NEW PROGRAM                          */             
  /* BP134304 20110803 PCL3 TO UPDATE THE POSITION OF INV-CBK    */             
  /* BP134930 20130531 IMC1 TO UPDATE THE POSITION OF INCOME FLD */             
  /* BP135340 20140514 IMC1 REPLACE BP13K816 WITH BP13K813       */             
  ****************************************************************/             
                                                                                
  %GLOBAL  DATE;                                                                
  OPTIONS  NODATE CAPS;                                                         
                                                                                
     PROC FORMAT;                                                               
                                                                                
          VALUE $RMFMT    "1 ","1A"="1R"                                        
                          "2 ","2A"="2R"                                        
                          "SA"="SA"                                             
                          "3 "="3R"                                             
                          "4 ","4D","4S"="4R"                                   
                          "5 ","5D"="5R"                                        
                          "6 ","E ","ED"="EF";                                  
                                                                                
          VALUE $APPLNFMT "1"="NO OF APPLNS RECEIVED (FT)"                      
                          "2"="NO OF APPLNS RECEIVED (G) ";                     
                                                                                
          VALUE $SUCCFMT  "1"="PREV SUCC   "                                    
                          "2"="PREV UNSUCC "                                    
                          "3"="PREV ALT(NE)";                                   
                                                                                
  DATA BP13F595;                                                                
  INFILE BP13F595 END=EOF;                                                      
  INPUT  @187 NT                         $CHAR3.                                
         @211 FT                         $CHAR1.                                
         @334 DATE_BALLOT                $CHAR6.                                
         @123 REGN                       $CHAR8.                                
         @41  NRIC                       $CHAR9.                                
         @462 INV_BK                     $CHAR1.                                
         @463 NOT_BK                     $CHAR1.                                
         @464 INV_DBK                    $CHAR1.                                
         @465 NOT_DBK                    $CHAR1.                                
         @474 INV_CBK                    $CHAR1.                                
         @467 NOT_CBK                    $CHAR1.                                
         @468 REP_TYPE                   $CHAR2.                                
         @470 INV_H                      $CHAR1.                                
         @471 NIV_H                      $CHAR1.                                
         @472 INV_G                      $CHAR1.                                
         @473 NIV_G                      $CHAR1.                                
         @406 INCOME                     5.                                     
         @493 ALLOC_TAG                  $CHAR2.;                               
                                                                                
   IF _N_ = 1 THEN                                                              
      DO;                                                                       
        CUR_DTE=PUT(TODAY(),DDMMYY10.);                                         
        CALL SYMPUT('DATE',CUR_DTE);                                            
      END;                                                                      
                                                                                
                                                                                
   /**********************************************/                             
   /* READING BP13K813 TO GET NO. OF FLAT SUPPLY */                             
   /**********************************************/                             
                                                                                
                                                                                
   /* READING BP13K813 TO DECODE NT-CODE */                                     
                                                                                
   K813_KEY    = NT||FT||DATE_BALLOT;                                           
                                                                                
   INFILE BP13K813 VSAM KEY = K813_KEY;                                         
    INPUT @;                                                                    
       RC = _IORC_;                                                             
       IF RC = 0 THEN                                                           
          DO;                                                                   
              INPUT @250 UNIT_OFFER         4.                                  
                    @168 PROJ_NAME    $CHAR30.;                                 
                    PNAME          = PROJ_NAME;                                 
                    FLAT_SUPPLY    = UNIT_OFFER;                                
          END;                                                                  
                                                                                
   IF INV_BK         = 'Y'   THEN                                               
      DO;                                                                       
         INV='INVITED    ';                                                     
         BK ='BOOKED     ';                                                     
         SUC="1";                                                               
      END;                                                                      
                                                                                
   IF NOT_BK         = 'Y'   THEN                                               
      DO;                                                                       
         INV='INVITED    ';                                                     
         BK ='BOOKED     ';                                                     
         SUC="2";                                                               
      END;                                                                      
                                                                                
   IF INV_DBK        = 'Y'   THEN                                               
      DO;                                                                       
         INV='INVITED    ';                                                     
         BK ='NOT BOOKED ';                                                     
                                                                                
         IF ALLOC_TAG NOT = 'NE' THEN SUC="1";                                  
         ELSE SUC="3";                                                          
                                                                                
      END;                                                                      
                                                                                
   IF NOT_DBK        = 'Y'   THEN                                               
      DO;                                                                       
         INV='INVITED    ';                                                     
         BK ='NOT BOOKED ';                                                     
                                                                                
         IF ALLOC_TAG NOT = 'NE' THEN SUC="2";                                  
         ELSE SUC="3";                                                          
                                                                                
      END;                                                                      
                                                                                
   IF INV_CBK        = 'Y'   THEN                                               
      DO;                                                                       
         INV='NOT INVITED';                                                     
         BK ='           ';                                                     
         SUC="1";                                                               
      END;                                                                      
                                                                                
   IF NOT_CBK        = 'Y'   THEN                                               
      DO;                                                                       
         INV='NOT INVITED';                                                     
         BK ='           ';                                                     
         SUC="2";                                                               
      END;                                                                      
                                                                                
   IF INV_H          = 'Y'   THEN                                               
      DO;                                                                       
         APPLN="1";                                                             
         BAL_HH='INVITED    ';                                                  
      END;                                                                      
                                                                                
   IF NIV_H          = 'Y'   THEN                                               
      DO;                                                                       
         APPLN="1";                                                             
         BAL_HH='NOT INVITED';                                                  
      END;                                                                      
                                                                                
   IF INV_G          = 'Y'   THEN                                               
      DO;                                                                       
         APPLN="2";                                                             
         BAL_HH='INVITED    ';                                                  
      END;                                                                      
                                                                                
   IF NIV_G          = 'Y'   THEN                                               
      DO;                                                                       
         APPLN="2";                                                             
         BAL_HH='NOT INVITED';                                                  
      END;                                                                      
                                                                                
   RUN;                                                                         
                                                                                
   /******************************************************/                     
   /* PRINT DETAIL FOR REPORT1                           */                     
   /******************************************************/                     
    DATA DETAIL1;                                                               
      SET BP13F595;                                                             
          IF INV_BK     = 'Y' OR                                                
             NOT_BK     = 'Y' OR                                                
             INV_DBK    = 'Y' OR                                                
             NOT_DBK    = 'Y' OR                                                
             INV_CBK    = 'Y' OR                                                
             NOT_CBK    = 'Y';                                                  
                                                                                
            IF INV_BK   = 'Y' THEN DTL1_BK    = 'Y';                            
            IF NOT_BK   = 'Y' THEN DTL1_NBK   = 'Y';                            
            IF INV_DBK  = 'Y' THEN DTL1_DBK   = 'Y';                            
            IF NOT_DBK  = 'Y' THEN DTL1_NDBK  = 'Y';                            
            IF INV_CBK  = 'Y' THEN DTL1_CBK   = 'Y';                            
            IF NOT_CBK  = 'Y' THEN DTL1_NCBK  = 'Y';                            
    RUN;                                                                        
                                                                                
    OPTIONS PAGENO=1 LINESIZE=200 PAGESIZE=32000;                               
    PROC PRINTTO FILE=P13F078A NEW;                                             
    PROC PRINT DATA=DETAIL1 SPLIT='*' N='TOTAL' OBS='OBS*-----';                
     VAR REGN                                                                   
         NRIC                                                                   
         INCOME                                                                 
         DATE_BALLOT                                                            
         PNAME                                                                  
         NT                                                                     
         FT                                                                     
         FLAT_SUPPLY                                                            
         REP_TYPE                                                               
         DTL1_BK                                                                
         DTL1_NBK                                                               
         DTL1_DBK                                                               
         DTL1_NDBK                                                              
         ALLOC_TAG                                                              
         DTL1_CBK                                                               
         DTL1_NCBK;                                                             
                                                                                
     LABEL REGN        ='REGN*--------'                                         
           NRIC        ='NRIC*---------'                                        
           INCOME      ='INCOME*------'                                         
           DATE_BALLOT ='DATE*BALLOT*------'                                    
           PNAME       ='PROJECT*NAME*-------'                                  
           NT          ='NEW*TOWN*----'                                         
           FT          ='FLAT*TYPE*----'                                        
           FLAT_SUPPLY ='FLAT*SUPPLY*------'                                    
           REP_TYPE    ='COUNT*-----'                                           
           DTL1_BK     ='INV-BK-PREV*SUCCESSFUL*-------------'                  
           DTL1_NBK    ='INV-BK-PREV*UNSUCCESSFUL*-----------'                  
           DTL1_DBK    ='INV-NOT-BK-PREV*SUCCESSFUL*---------------'            
           DTL1_NDBK   ='INV-NOT-BK-PREV*UNSUCCESSFUL*---------------'          
           ALLOC_TAG   ='INV-NOT-BK-PREV*ALLO-TAG*---------------'              
           DTL1_CBK    ='NOT-INV-PREV*SUCCESSFUL*------------'                  
           DTL1_NCBK   ='NOT-INV-PREV*UNSUCCESSFUL*------------';               
                                                                                
     TITLE "REPORT ON BTO APPLICATION (REPEAT/INVITED)";                        
    RUN;                                                                        
                                                                                
   /**************************************************************/             
   /* PRINT TABLE A1 - REPORT ON BTO APPLICATION (REPEAT/INVITED)*/             
   /* (P13L078A)                                                 */             
   /**************************************************************/             
                                                                                
   OPTIONS MISSING=0 LINESIZE=132 PAGESIZE=60 PAGENO=1;                         
   PROC PRINTTO FILE=P13L078A NEW;                                              
   PROC TABULATE DATA=DETAIL1 MISSING FORMAT=7.;                                
   CLASS DATE_BALLOT                                                            
         PNAME                                                                  
         FT                                                                     
         REP_TYPE                                                               
         BK                                                                     
         INV                                                                    
         SUC                                                                    
         FLAT_SUPPLY;                                                           
                                                                                
   FORMAT FLAT_TYPE         $RMFMT.;                                            
   FORMAT SUC               $SUCCFMT.;                                          
                                                                                
   KEYLABEL N   = " "                                                           
            SUM = " "                                                           
            ALL = "TOTAL";                                                      
                                                                                
   LABEL DATE_BALLOT      = 'DATE BALLOT';                                      
   LABEL FT               = 'FLAT TYPE';                                        
   LABEL FLAT_SUPPLY      = 'FLAT SUPPLY';                                      
   LABEL REP_TYPE         = 'COUNT';                                            
                                                                                
   TABLE (DATE_BALLOT*PNAME*FT*FLAT_SUPPLY*(                                    
          REP_TYPE ALL="SUB TOTAL") ALL="TOTAL"),                               
          INV=' '*BK=' '*SUC=' '                                                
         /RTS = 58  MISSTEXT = "0"                                              
          BOX="        ";                                                       
                                                                                
   TITLE1 "P13L078A      HDB3                              SYSTEM OF COMMITMENT 
                      DATE : &DATE  PAGE  :  ";                                 
   TITLE2 "REPORT ON BTO APPLICATION (REPEAT/INVITED)";                         
                                                                                
   /******************************************************/                     
   /* PRINT DETAIL FOR REPORT2                           */                     
   /******************************************************/                     
    DATA DETAIL2;                                                               
      SET BP13F595;                                                             
          IF INV_H      = 'Y' OR                                                
             NIV_H      = 'Y' OR                                                
             INV_G      = 'Y' OR                                                
             NIV_G      = 'Y';                                                  
                                                                                
            IF INV_H    = 'Y' THEN DTL2_INV_H = 'Y';                            
            IF NIV_H    = 'Y' THEN DTL2_NIV_H = 'Y';                            
            IF INV_G    = 'Y' THEN DTL2_INV_G = 'Y';                            
            IF NIV_G    = 'Y' THEN DTL2_NIV_G = 'Y';                            
    RUN;                                                                        
                                                                                
    OPTIONS PAGENO=1 LINESIZE=200 PAGESIZE=32000;                               
    PROC PRINTTO FILE=P13F078B NEW;                                             
    PROC PRINT DATA=DETAIL2 SPLIT='*' N='TOTAL' OBS='OBS*-----';                
     VAR REGN                                                                   
         NRIC                                                                   
         DATE_BALLOT                                                            
         PNAME                                                                  
         NT                                                                     
         FT                                                                     
         FLAT_SUPPLY                                                            
         REP_TYPE                                                               
         DTL2_INV_H                                                             
         DTL2_NIV_H                                                             
         DTL2_INV_G                                                             
         DTL2_NIV_G;                                                            
                                                                                
     LABEL REGN        ='REGN*--------'                                         
           NRIC        ='NRIC*---------'                                        
           DATE_BALLOT ='DATE*BALLOT*------'                                    
           PNAME       ='PROJECT*NAME*-------'                                  
           NT          ='NEW*TOWN*----'                                         
           FT          ='FLAT*TYPE*----'                                        
           FLAT_SUPPLY ='FLAT*SUPPLY*------'                                    
           REP_TYPE    ='COUNT*-----'                                           
           DTL2_INV_H  ='APPLN-FT*INVITED*-----------'                          
           DTL2_NIV_H  ='APPLN-FT*NOT INVITED*-----------'                      
           DTL2_INV_G  ='APPLN-G*INVITED*-----------'                           
           DTL2_NIV_G  ='APPLN-G*NOT INVITED*-----------';                      
                                                                                
     TITLE "REPORT ON BTO APPLICATION (REPEAT/INVITED)";                        
    RUN;                                                                        
                                                                                
   /**************************************************************/             
   /* PRINT TABLE A2 - REPORT ON BTO APPLICATION (REPEAT/INVITED)*/             
   /* (P13L078B)                                                 */             
   /**************************************************************/             
                                                                                
   OPTIONS MISSING=0 LINESIZE=132 PAGESIZE=60 PAGENO=1;                         
   PROC PRINTTO FILE=P13L078B NEW;                                              
   PROC TABULATE DATA=BP13F595 MISSING FORMAT=13.;                              
   CLASS DATE_BALLOT                                                            
         PNAME                                                                  
         FT                                                                     
         REP_TYPE                                                               
         APPLN                                                                  
         BAL_HH                                                                 
         FLAT_SUPPLY;                                                           
                                                                                
   FORMAT FLAT_TYPE         $RMFMT.                                             
          APPLN             $APPLNFMT.;                                         
                                                                                
   KEYLABEL N   = " "                                                           
            SUM = " "                                                           
            ALL = "TOTAL";                                                      
                                                                                
   LABEL DATE_BALLOT      = 'DATE BALLOT';                                      
   LABEL FT               = 'FLAT TYPE';                                        
   LABEL FLAT_SUPPLY      = 'FLAT SUPPLY';                                      
   LABEL REP_TYPE         = 'COUNT';                                            
                                                                                
   TABLE (DATE_BALLOT*PNAME*FT*FLAT_SUPPLY*(                                    
          REP_TYPE ALL="SUB TOTAL") ALL="TOTAL"),                               
          APPLN=' '*BAL_HH=' '                                                  
         /RTS = 60  MISSTEXT = "0"                                              
          BOX="        ";                                                       
                                                                                
   TITLE1 "P13L078B      HDB3                              SYSTEM OF COMMITMENT 
                      DATE : &DATE  PAGE  :  ";                                 
   TITLE2 "REPORT ON BTO APPLICATION (REPEAT/INVITED)";                         
