       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C298.                                                 
       AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  NOV2003.                                                  
      *****************************************************************         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *****************************************************************         
      * OBJECTIVES:  TO CALL SUBROUTINES BP13C29A, BP13C29B, BP13C29C,*         
      *              BP13C29D TO PRINT ADJUSTED FLATLIST              *         
      *---------------------------------------------------------------*         
      * CHECK F205-NUM-FORM-TYPE:                                     *         
      *    - 1 (CALLS BP13C29A-FLATLIST FOR 3R  - BP1340)             *         
      *    - 2 (CALLS BP13C29B-FLATLIST FOR BTO - BP134V)             *         
      *    - 3 (CALLS BP13C29C-FLATLIST FOR WIS - BP134W)             *         
      *    - 4 (CALLS BP13C29D-FLATLIST FOR SPECIAL PINNACLE @ DUXTON *         
      *         - BP134X)                                             *         
      *    - 6 (CALLS BP13C29C-FLATLIST FOR CASES WITH                *         
      *         F999-NUM-POD-DUE = 'N' AND SPACES)                    *         
      *    - 8 (CALLS BP13C29C-FLATLIST FOR WIS - USING FORM BP13UA)  *         
      *    - D (CALLS BP13C29G-FLATLIST FOR BTO - TREELODGE@PUNGGOL)  *         
      *                                                               *         
      * CHG REQ#  DATE        BY    DESCRIPTION                       *         
      * --------  ----------  ----  --------------------------------- *         
      * BP132432  08/11/2003  MTD   NEW PROGRAM.                      *         
      * BP132560  20/07/2004  MTD   ADD FORM TYPE = '4' (FLATLIST FOR *         
      *                             PINNACLE @ DUXTON)                *         
      * BP132630  10/01/2005  SSD   ADD FORM TYPE = '6'               *         
      * BP132688  03/06/2005  GAN1  ADD FORM TYPE = '8'               *         
      * BP133056  23/07/2007  LMS1  ADD FORM TYPE = 'D'               *         
      * BP133772  10/12/2009  ESA1  CATER FOR FORM A,C AND D ONLY     *         
      *                             ADD NEW PGM BP13C29H FOR FORM A   *         
      *                             ADD NEW PGM BP13C29I FOR FORM C   *         
      * BP133851  22/03/2010  ESA1  CATER FOR PAIR UNIT               *         
      *                             ADD NEW PGM BP13C29J FOR PAIR UNIT*         
      * BP135004  05/08/2013  ESA1  CATER FOR NEW FORM BP13UT         *         
      * BP135799  11/06/2015  ESA1  TO PASS FLAT-TYPE IN COMMAREA     *         
      * BP136066  17/09/2015  ESA1  ADD BP13C29L FOR ELDERLY FLATLIST *         
      * BP136671  23/03/2017  ESA1  ADD BP13C29M FOR FSH FLATLIST     *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205       ASSIGN TO BP13F205.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-WORK-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE             PIC X(08) VALUE SPACES.              
               10  FILLER                  PIC X(13) VALUE SPACES.              
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD              PIC X(02) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE '/'.                 
               10  WS-DISP-MM              PIC X(02) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE '/'.                 
               10  WS-DISP-CCYY            PIC X(04) VALUE SPACES.              
           05  WS-END-OF-F205-SW           PIC X(01)  VALUE SPACE.              
               88  C-END-OF-F205           VALUE 'Y'.                           
           05  WS-READ-CTR                 PIC 9(08) VALUE ZEROES.              
           05  WS-SUBROUTINE               PIC X(08) VALUE SPACES.              
                                                                                
       01  WS-C298-PARAMETERS.                                                  
           05  WS-C298-FORM-TYPE       PIC X(01) VALUE SPACES.                  
           05  WS-C298-FLAT-TYPE       PIC X(02) VALUE SPACES.                  
           05  WS-C298-NUM-GRO         PIC X(02) VALUE SPACES.                  
           05  WS-C298-RETURN-CODE     PIC 9(04) VALUE ZEROES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F205.                                               
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F205.                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F205  AT END                                                
                MOVE 'Y'   TO  WS-END-OF-F205-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1  TO WS-READ-CTR.                                              
                                                                                
           MOVE F205-NUM-FORM-TYPE   TO WS-C298-FORM-TYPE.                      
           MOVE F205-NUM-FLAT-TYPE   TO WS-C298-FLAT-TYPE.                      
           MOVE F205-NUM-GRO         TO WS-C298-NUM-GRO.                        
           MOVE ZEROES               TO WS-C298-RETURN-CODE.                    
           EVALUATE WS-C298-FORM-TYPE                                           
              WHEN 'A'                                                          
                   MOVE 'BP13C29H'   TO WS-SUBROUTINE                           
                   CALL 'BP13C29H'   USING  WS-C298-PARAMETERS                  
              WHEN 'C'                                                          
                   IF F205-NUM-FLAT-TYPE = '4P' OR '5P'                         
                      MOVE 'BP13C29J'   TO WS-SUBROUTINE                        
                      CALL 'BP13C29J'   USING  WS-C298-PARAMETERS               
                   ELSE                                                         
                      IF F205-CDE-SINGLES = 'Y' OR 'B'                          
                         MOVE 'BP13C29K'   TO WS-SUBROUTINE                     
                         CALL 'BP13C29K'   USING  WS-C298-PARAMETERS            
                      ELSE                                                      
                         MOVE 'BP13C29I'   TO WS-SUBROUTINE                     
                         CALL 'BP13C29I'   USING  WS-C298-PARAMETERS            
                      END-IF                                                    
                   END-IF                                                       
              WHEN 'D'                                                          
                   MOVE 'BP13C29G'   TO WS-SUBROUTINE                           
                   CALL 'BP13C29G'   USING  WS-C298-PARAMETERS                  
              WHEN 'F'                                                          
                   MOVE 'BP13C29L'   TO WS-SUBROUTINE                           
                   CALL 'BP13C29L'   USING  WS-C298-PARAMETERS                  
              WHEN 'S'                                                          
                   MOVE 'BP13C29M'   TO WS-SUBROUTINE                           
                   CALL 'BP13C29M'   USING  WS-C298-PARAMETERS                  
           END-EVALUATE.                                                        
                                                                                
           IF WS-C298-RETURN-CODE NOT = ZEROES                                  
              DISPLAY 'ERROR IN CALLING SUBROUTINE: ' WS-SUBROUTINE             
              MOVE WS-C298-RETURN-CODE       TO RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C298'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F205 RECORDS READ    : ' WS-READ-CTR.                   
                                                                                
           CLOSE BP13F205.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
