       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C748.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :TO COMPUTE NUMBER OF RESIDENT AND NON-  *                
      *                 RESIDENT APPLICATION FROM BALLOT FILE  *                
      *                 BP13F520 BY NT/FT/ALLOC-SCH.           *                
      *                                                        *                
      * CHG REQ BY   DATE  REMARKS                             *                
      * ------- --- ------ ----------------------------------- *                
      * C930335 LSB 930317 - EXPAND BP13K010, K720 FOR RDC     *                
      * I920718 LJL 930401 - CONVERT TO COBOLII.               *                
      * C9309B4 LSB 930927 - RESIDENT -HA3/HA4                 *                
      * C9309B4 LSB 930927 - RESIDENT -HA3/HA4                 *                
      * C950137 GD  950116 - ADD TCP SCHEME IN PROCESSING      *                
      *BP130652 NOR 990220 - EXPAND F520 RECL FROM 300 TO 500  *                
      *BP132162 ANC 020711 - CHECK IF RESIDENT-HA NOT BLANK    *                
      *BP132162 ANC 020711 - CHECK IF RESIDENT-HA NOT BLANK    *                
      *BP132463 SK  031209 - CATER FOR HA1,HA2,HA3 & HA4       *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520    ASSIGN TO BP13F520.                               
                                                                                
           SELECT BP13F720    ASSIGN TO BP13F720.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 500 CHARACTERS                    
                              RECORDING MODE IS F                               
                              LABEL RECORDS ARE STANDARD.                       
           COPY BP13F520.                                                       
                                                                                
       FD  BP13F720           LABEL RECORDS ARE STANDARD                        
                              RECORD CONTAINS 300 CHARACTERS                    
                              RECORDING MODE IS F                               
                              BLOCK CONTAINS 0 RECORDS.                         
           COPY BP13F720.                                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-CNT-F520-READ          PIC 9(8) VALUE 0.                      
           05  WS-CNT-F720-WRITE         PIC 9(8) VALUE 0.                      
           05  WS-CTR                    PIC 9    VALUE 0.                      
           05  WS-PREV-NT                PIC X(3) VALUE SPACES.                 
           05  WS-PREV-FT                PIC X(2) VALUE SPACES.                 
           05  WS-F520-EOF               PIC X    VALUE 'N'.                    
               88 F520-EOF                        VALUE 'Y'.                    
                                                                                
       01  WS-ALLOCSCH-TYPE              PIC X(3).                              
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-TABLE.                                                            
           05  WS-SCHEME-TABLE OCCURS 7 TIMES.                                  
               10  WS-CNT-RESIDENT       PIC 9(5).                              
               10  WS-CNT-NON-RESIDENT   PIC 9(5).                              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CC                PIC XX   VALUE SPACES.                 
           05  WS-DTE-CURRENT.                                                  
               10  WS-CURR-YY            PIC XX   VALUE SPACES.                 
               10  WS-CURR-MM            PIC XX   VALUE SPACES.                 
               10  WS-CURR-DD            PIC XX   VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-CONTROL-RTN.                                                        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-INIT-TABLE       THRU 2000-EXIT                         
           VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 7.                         
                                                                                
           PERFORM 3000-READ-BP13F520    THRU 3000-EXIT.                        
                                                                                
           MOVE F520-CDE-NT1             TO WS-PREV-NT.                         
           MOVE F520-CDE-FLAT-TYPE       TO WS-PREV-FT.                         
                                                                                
           PERFORM 4000-MAIN-ROUTINE     THRU 4000-EXIT                         
             UNTIL F520-EOF.                                                    
                                                                                
           PERFORM 9999-CLOSE-FILES      THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  BP13F520                                                 
               OUTPUT  BP13F720.                                                
                                                                                
           ACCEPT WS-DTE-CURRENT         FROM DATE.                             
                                                                                
           IF WS-CURR-YY < '90'                                                 
              MOVE  '20'                 TO WS-CURR-CC                          
           ELSE                                                                 
              MOVE  '19'                 TO WS-CURR-CC.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-INIT-TABLE.                                                         
                                                                                
           MOVE ZEROS               TO WS-CNT-RESIDENT (WS-CTR)                 
                                       WS-CNT-NON-RESIDENT (WS-CTR).            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13F520.                                                      
                                                                                
           READ BP13F520                 AT END                                 
           MOVE 'Y'                      TO WS-F520-EOF                         
           GO TO 3000-EXIT.                                                     
                                                                                
           ADD 1                         TO WS-CNT-F520-READ.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MAIN-ROUTINE.                                                       
                                                                                
           IF F520-CDE-NT1 NOT = WS-PREV-NT OR                                  
              F520-CDE-FLAT-TYPE NOT = WS-PREV-FT                               
              PERFORM 5000-WRITE-BP13F720     THRU 5000-EXIT.                   
                                                                                
      * CLASSIFYING  ALLOCATION SCHEME :                                        
                                                                                
           SET WS-ALLOC-PTR TO 1.                                               
                                                                                
           SEARCH WS-ALLOC-SCH                                                  
              AT END MOVE 'XXX' TO WS-ALLOCSCH-TYPE                             
              WHEN F520-CDE-ALLOC-SCH =                                         
                                WS-ALLOC-SCH-DESP(WS-ALLOC-PTR)                 
              MOVE WS-MAIN-SCH-CD(WS-ALLOC-PTR) TO                              
                                      WS-ALLOCSCH-TYPE.                         
                                                                                
           IF WS-ALLOCSCH-TYPE = 'XXX'                                          
              DISPLAY '***  INVALID ALLOC SCHEME FOR BP13F520 REC '             
                            F520-NUM-REF                                        
              DISPLAY '***  INVALID APPLN ALLOC SCHEME IS : '                   
                            F520-CDE-ALLOC-SCH                                  
              GO TO 4000-READ-F520.                                             
                                                                                
           IF WS-ALLOCSCH-TYPE = 'MTF'                                          
               MOVE 1            TO WS-CTR                                      
           ELSE                                                                 
           IF WS-ALLOCSCH-TYPE = 'CCC'                                          
               MOVE 2            TO WS-CTR                                      
           ELSE                                                                 
           IF WS-ALLOCSCH-TYPE = 'RH '                                          
               MOVE 3            TO WS-CTR                                      
           ELSE                                                                 
           IF WS-ALLOCSCH-TYPE = 'REN'                                          
               MOVE 4            TO WS-CTR                                      
           ELSE                                                                 
           IF WS-ALLOCSCH-TYPE = 'PA '                                          
               MOVE 5            TO WS-CTR                                      
           ELSE                                                                 
           IF WS-ALLOCSCH-TYPE = 'RDC'                                          
               MOVE 6            TO WS-CTR                                      
           ELSE                                                                 
           IF WS-ALLOCSCH-TYPE = 'TCP'                                          
               MOVE 7            TO WS-CTR                                      
           ELSE                                                                 
               GO TO 4000-READ-F520.                                            
                                                                                
           IF (F520-CDE-RESIDENT-HA1 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')   OR            
              (F520-CDE-RESIDENT-HA2 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')   OR            
              (F520-CDE-RESIDENT-HA3 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')   OR            
              (F520-CDE-RESIDENT-HA4 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')                 
              ADD 1                 TO WS-CNT-RESIDENT (WS-CTR)                 
           ELSE                                                                 
              ADD 1                 TO WS-CNT-NON-RESIDENT (WS-CTR).            
                                                                                
       4000-READ-F520.                                                          
                                                                                
           MOVE F520-CDE-NT1             TO WS-PREV-NT.                         
           MOVE F520-CDE-FLAT-TYPE       TO WS-PREV-FT.                         
                                                                                
           PERFORM 3000-READ-BP13F520    THRU 3000-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-WRITE-BP13F720.                                                     
                                                                                
           MOVE SPACES                   TO BP13F720-RESIDENT.                  
           MOVE ZEROS                    TO F720-NUM-FLAT-RES-MTF.              
           MOVE ZEROS                    TO F720-NUM-FLAT-NONRES-MTF.           
           MOVE ZEROS                    TO F720-NUM-REG-RES-MTF.               
           MOVE ZEROS                    TO F720-NUM-REG-NONRES-MTF.            
           MOVE ZEROS                    TO F720-NUM-FLAT-RES-CCC.              
           MOVE ZEROS                    TO F720-NUM-FLAT-NONRES-CCC.           
           MOVE ZEROS                    TO F720-NUM-REG-RES-CCC.               
           MOVE ZEROS                    TO F720-NUM-REG-NONRES-CCC.            
           MOVE ZEROS                    TO F720-NUM-FLAT-RES-RH.               
           MOVE ZEROS                    TO F720-NUM-FLAT-NONRES-RH.            
           MOVE ZEROS                    TO F720-NUM-REG-RES-RH.                
           MOVE ZEROS                    TO F720-NUM-REG-NONRES-RH.             
           MOVE ZEROS                    TO F720-NUM-FLAT-RES-REN.              
           MOVE ZEROS                    TO F720-NUM-FLAT-NONRES-REN.           
           MOVE ZEROS                    TO F720-NUM-REG-RES-REN.               
           MOVE ZEROS                    TO F720-NUM-REG-NONRES-REN.            
           MOVE ZEROS                    TO F720-NUM-FLAT-RES-RDC.              
           MOVE ZEROS                    TO F720-NUM-FLAT-NONRES-RDC.           
           MOVE ZEROS                    TO F720-NUM-REG-RES-RDC.               
           MOVE ZEROS                    TO F720-NUM-REG-NONRES-RDC.            
           MOVE ZEROS                    TO F720-NUM-FLAT-RES-PA.               
           MOVE ZEROS                    TO F720-NUM-FLAT-NONRES-PA.            
           MOVE ZEROS                    TO F720-NUM-REG-RES-PA.                
           MOVE ZEROS                    TO F720-NUM-REG-NONRES-PA.             
           MOVE ZEROS                    TO F720-NUM-FLAT-RES-TCP.              
           MOVE ZEROS                    TO F720-NUM-FLAT-NONRES-TCP.           
           MOVE ZEROS                    TO F720-NUM-REG-RES-TCP.               
           MOVE ZEROS                    TO F720-NUM-REG-NONRES-TCP.            
                                                                                
           MOVE WS-PREV-NT               TO F720-CDE-NT.                        
           MOVE WS-PREV-FT               TO F720-CDE-FLAT-TY.                   
           MOVE WS-CNT-RESIDENT     (1)  TO F720-NUM-APP-RES-MTF.               
           MOVE WS-CNT-NON-RESIDENT (1)  TO F720-NUM-APP-NONRES-MTF.            
           MOVE WS-CNT-RESIDENT     (2)  TO F720-NUM-APP-RES-CCC.               
           MOVE WS-CNT-NON-RESIDENT (2)  TO F720-NUM-APP-NONRES-CCC.            
           MOVE WS-CNT-RESIDENT     (3)  TO F720-NUM-APP-RES-RH.                
           MOVE WS-CNT-NON-RESIDENT (3)  TO F720-NUM-APP-NONRES-RH.             
           MOVE WS-CNT-RESIDENT     (4)  TO F720-NUM-APP-RES-REN.               
           MOVE WS-CNT-NON-RESIDENT (4)  TO F720-NUM-APP-NONRES-REN.            
           MOVE WS-CNT-RESIDENT     (5)  TO F720-NUM-APP-RES-PA.                
           MOVE WS-CNT-NON-RESIDENT (5)  TO F720-NUM-APP-NONRES-PA.             
           MOVE WS-CNT-RESIDENT     (6)  TO F720-NUM-APP-RES-RDC.               
           MOVE WS-CNT-NON-RESIDENT (6)  TO F720-NUM-APP-NONRES-RDC.            
           MOVE WS-CNT-RESIDENT     (7)  TO F720-NUM-APP-RES-TCP.               
           MOVE WS-CNT-NON-RESIDENT (7)  TO F720-NUM-APP-NONRES-TCP.            
           MOVE WS-CURRENT-DATE          TO F720-DTE-UPDATE.                    
           WRITE BP13F720-RESIDENT.                                             
           ADD 1                         TO WS-CNT-F720-WRITE.                  
                                                                                
           PERFORM 2000-INIT-TABLE       THRU 2000-EXIT                         
           VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 7.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
                                                                                
           PERFORM 5000-WRITE-BP13F720   THRU 5000-EXIT.                        
           CLOSE BP13F720                                                       
                 BP13F520.                                                      
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>>> BP13C748 <<<<<<<<<<<<<<<<<<<<<<<'.        
           DISPLAY 'COMPUTE NO. OF RESIDENT & NON RESIDENT APPLICATION'.        
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF RECORDS READ FROM BP13F520    : '                     
                                                      WS-CNT-F520-READ.         
           DISPLAY '                        BALLOT FILE.'.                      
           DISPLAY 'NO OF RECORDS WRITTEN IN BP13F720   : '                     
                                                      WS-CNT-F720-WRITE.        
           DISPLAY '  '.                                                        
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
