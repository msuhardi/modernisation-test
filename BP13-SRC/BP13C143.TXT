      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C143.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   26/06/2003.                                              
      *--------------------------------------------------------*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES  : TO DELETE OIC FROM BP13K022           *                
      *                                                        *                
      *    INPUT FILES  : BP13F022                             *                
      *                   BP13K800                             *                
      *                   BP13K400                             *                
      *    I-O            BP13K022                             *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE      DETAILS                         *                
      *-------- ---- --------  --------------------------------*                
      *BP132334 SK   20030627  NEW PGM                         *                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F022   ASSIGN TO BP13F022.                                
                                                                                
           SELECT BP13K022   ASSIGN TO BP13K022                                 
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE KEY IS K022-NUM-REGN                     
                             FILE STATUS   IS WS-K022-STATUS.                   
                                                                                
           SELECT BP13K800   ASSIGN TO BP13K800                                 
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT BP13K400   ASSIGN TO BP13K400                                 
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K400-NUM-REGN                     
                             FILE STATUS   IS WS-K400-STATUS.                   
                                                                                
           SELECT BP13L143  ASSIGN  TO BP13L143.                                
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F022                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F022.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K400                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13K400.                                                           
                                                                                
       FD  BP13L143                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  132 CHARACTERS.                                     
                                                                                
       01  BP13L143-REC         PIC X(132).                                     
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F022              PIC 9(8)    VALUE 0.                    
           05 WS-K022-STATUS            PIC 9(2)    VALUE 00.                   
           05 WS-K800-STATUS            PIC 9(2)    VALUE 00.                   
           05 WS-K400-STATUS            PIC 9(2)    VALUE 00.                   
           05 WS-F022-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-FOUND                  PIC X(1)    VALUE 'N'.                  
           05 WS-K022-FOUND             PIC X(1)    VALUE 'N'.                  
                                                                                
           05 WS-NOTFND                 PIC 9(8)    VALUE 0.                    
           05 WS-FOUND-K800             PIC 9(8)    VALUE 0.                    
           05 WS-FOUND-K400             PIC 9(8)    VALUE 0.                    
           05 WS-K022-DELETE            PIC 9(8)    VALUE 0.                    
                                                                                
           05 WS-CNT-LINE-ON-PAGE       PIC 9(8)    VALUE 66.                   
           05 WS-CNT-PRINT-L143         PIC 9(8)    VALUE 1.                    
           05 WS-CNT-WRITE-L143         PIC 9(8)    VALUE 0.                    
           05 WS-NO-OF-PAGE             PIC 9(8)    VALUE 0.                    
                                                                                
           05 WS-REASON                 PIC X(30)   VALUE SPACES.               
           05 WS-DATE-KEY               PIC 9(8)    VALUE 0.                    
           05 WS-DATE-CAN               PIC 9(8)    VALUE 0.                    
           05 WS-DATE1                  PIC 9(8)    VALUE 0.                    
           05 WS-DATE2                  PIC 9(8)    VALUE 0.                    
           05 WS-DAYS                   PIC 9(8)    VALUE 0.                    
           05 WS-SYS-DATE               PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
                                                                                
      *--------------------------------------------------------*                
      *         BP13L143  -  R E P O R T    L A Y O U T        *                
      *--------------------------------------------------------*                
                                                                                
       01  L143-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L143'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L143-RUN-DATE            PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L143-PAGE-NO             PIC ZZZ9.                               
                                                                                
       01  L143-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(55) VALUE SPACES.                 
           05  FILLER                   PIC X(73) VALUE                         
                'REPORT ON DELETION OF OIC'.                                    
                                                                                
       01  L143-PR-HEAD-03.                                                     
           05  FILLER          PIC X(5)  VALUE ' S-NO'.                         
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(5)  VALUE 'OIC'.                           
           05  FILLER          PIC X(5)  VALUE SPACES.                          
           05  FILLER          PIC X(17) VALUE 'TP DATE'.                       
           05  FILLER          PIC X(6)  VALUE SPACES.                          
           05  FILLER          PIC X(17) VALUE 'CANCEL DATE'.                   
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(10) VALUE 'REGN NO'.                       
           05  FILLER          PIC X(1)  VALUE SPACES.                          
           05  FILLER          PIC X(8)  VALUE 'BNK LOAN'.                      
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(6)  VALUE 'AL CAT'.                        
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(30) VALUE 'REASON'.                        
                                                                                
       01  L143-PR-HEAD-04.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L143-PR-DETAILS-1.                                                   
           05  L143-NUMBER           PIC ZZZZ9.                                 
           05  FILLER                PIC X(2)  VALUE SPACES.                    
           05  L143-OIC              PIC X(5)  VALUE SPACES.                    
           05  FILLER                PIC X(5)  VALUE SPACES.                    
           05  L143-TP-DATE          PIC X(17) VALUE SPACES.                    
           05  FILLER                PIC X(6)  VALUE SPACES.                    
           05  L143-CN-DATE          PIC X(17) VALUE SPACES.                    
           05  FILLER                PIC X(3)  VALUE SPACES.                    
           05  L143-REG-NO           PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(3)  VALUE SPACES.                    
           05  L143-BANK-LOAN        PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(6)  VALUE SPACES.                    
           05  L143-AL-CAT           PIC X(6)  VALUE SPACES.                    
           05  FILLER                PIC X(1)  VALUE SPACES.                    
           05  L143-REASON           PIC X(30) VALUE SPACES.                    
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F022     THRU 2000-EXIT.                           
                                                                                
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F022-EOF = 'Y'.                                     
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F022                                                 
                       BP13K800                                                 
                       BP13K400                                                 
                I-O    BP13K022                                                 
                OUTPUT BP13L143.                                                
                                                                                
           EVALUATE WS-K022-STATUS                                              
               WHEN 00                                                          
               WHEN 97                                                          
                  CONTINUE                                                      
               WHEN OTHER                                                       
                  MOVE WS-K022-STATUS        TO RETURN-CODE                     
                  DISPLAY 'OPEN BP13K022 FAIL. STATUS=' WS-K022-STATUS          
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K400-STATUS                                              
               WHEN 00                                                          
               WHEN 97                                                          
                  CONTINUE                                                      
               WHEN OTHER                                                       
                  MOVE WS-K400-STATUS        TO RETURN-CODE                     
                  DISPLAY 'OPEN BP13K400 FAIL. STATUS=' WS-K400-STATUS          
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
               WHEN 97                                                          
                  CONTINUE                                                      
               WHEN OTHER                                                       
                  MOVE WS-K800-STATUS        TO RETURN-CODE                     
                  DISPLAY 'OPEN BP13K800 FAIL. STATUS=' WS-K800-STATUS          
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
           MOVE WS-SYSTEM-DATE          TO WS-SYS-DATE.                         
                                                                                
           COMPUTE WS-DATE2 =                                                   
                   FUNCTION INTEGER-OF-DATE(WS-SYS-DATE).                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F022.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F022                                                        
                AT END MOVE 'Y' TO WS-F022-EOF                                  
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F022.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-PROCESS.                                                            
      *--------------------------------------------------------*                
                                                                                
           MOVE 'N' TO WS-FOUND.                                                
           PERFORM 3500-READ-BP13K400 THRU 3500-EXIT.                           
                                                                                
           IF WS-FOUND = 'Y'                                                    
              PERFORM 3200-GET-TPDTE  THRU 3200-EXIT                            
           END-IF.                                                              
                                                                                
           IF WS-FOUND = 'N'                                                    
              PERFORM 4000-READ-BP13K800 THRU 4000-EXIT                         
                                                                                
              IF WS-FOUND = 'Y'                                                 
                 PERFORM 3300-GET-CNLDTE THRU 3300-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F022      THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3200-GET-TPDTE.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE ZEROS TO WS-DATE1 WS-DAYS.                                      
                                                                                
           IF K400-DTE-SO-EXECUTION IS NOT NUMERIC                              
              GO TO 3200-EXIT                                                   
           ELSE                                                                 
              MOVE K400-DTE-SO-EXECUTION TO WS-DATE-KEY                         
           END-IF.                                                              
                                                                                
           COMPUTE WS-DATE1 =                                                   
                   FUNCTION INTEGER-OF-DATE(WS-DATE-KEY).                       
                                                                                
           COMPUTE WS-DAYS = WS-DATE2 - WS-DATE1.                               
                                                                                
           IF WS-DAYS > 7                                                       
              MOVE 'ONE WEEK AFTER TP DATE'  TO WS-REASON                       
              IF K400-CDE-NT NOT = SPACES AND LOW-VALUES                        
                 MOVE K400-CDE-NT TO L143-BANK-LOAN                             
              ELSE                                                              
                 MOVE SPACES      TO L143-BANK-LOAN                             
              END-IF                                                            
                                                                                
              STRING WS-DATE-KEY (7:2) '/'                                      
                     WS-DATE-KEY (5:2) '/'                                      
                     WS-DATE-KEY (1:4)                                          
                     DELIMITED BY SIZE                                          
               INTO L143-TP-DATE                                                
                                                                                
              PERFORM 3400-PROCESS1      THRU 3400-EXIT                         
                                                                                
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3300-GET-CNLDTE.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE ZEROS TO WS-DATE1 WS-DAYS.                                      
                                                                                
           IF K800-DTE-CANCEL IS NOT NUMERIC                                    
              GO TO 3300-EXIT                                                   
           ELSE                                                                 
           IF K800-DTE-CANCEL IS NUMERIC AND K800-NUM-STATUS = 'C'              
              MOVE K800-DTE-CANCEL TO WS-DATE-CAN                               
           ELSE                                                                 
              GO TO 3300-EXIT                                                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-DATE1 =                                                   
                   FUNCTION INTEGER-OF-DATE(WS-DATE-CAN).                       
                                                                                
           COMPUTE WS-DAYS = WS-DATE2 - WS-DATE1.                               
                                                                                
           IF WS-DAYS > 90                                                      
              MOVE '3 MONTHS AFTER CANCELLATION'  TO WS-REASON                  
                                                                                
              IF K800-NUM-NEW-TOWN NOT = SPACES AND LOW-VALUES                  
                 MOVE K800-NUM-NEW-TOWN TO L143-BANK-LOAN                       
              ELSE                                                              
                 MOVE SPACES            TO L143-BANK-LOAN                       
              END-IF                                                            
                                                                                
              STRING WS-DATE-CAN (7:2) '/'                                      
                     WS-DATE-CAN (5:2) '/'                                      
                     WS-DATE-CAN (1:4)                                          
                     DELIMITED BY SIZE                                          
               INTO L143-CN-DATE                                                
                                                                                
              PERFORM 3400-PROCESS1       THRU 3400-EXIT                        
                                                                                
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3400-PROCESS1.                                                           
      *--------------------------------------------------------*                
                                                                                
           PERFORM 4500-READ-K022 THRU 4500-EXIT.                               
                                                                                
           IF WS-K022-FOUND = 'Y'                                               
              PERFORM 5800-MOVE-PARA      THRU 5800-EXIT                        
              PERFORM 6000-WRITE-BP13L143 THRU 6000-EXIT                        
              PERFORM 5000-DELETE-K022    THRU 5000-EXIT                        
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3500-READ-BP13K400.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE F022-NUM-REGN  TO K400-NUM-REGN.                                
           MOVE SPACES         TO WS-REASON.                                    
                                                                                
           READ BP13K400.                                                       
                                                                                
           EVALUATE WS-K400-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 ADD 1 TO WS-FOUND-K400                                         
                 MOVE 'Y' TO WS-FOUND                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 MOVE 'N' TO WS-FOUND                                           
              WHEN OTHER                                                        
                 MOVE WS-K400-STATUS        TO RETURN-CODE                      
                 DISPLAY 'READ BP13K400 FAIL. STATUS=' WS-K400-STATUS           
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-READ-BP13K800.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE F022-NUM-REGN  TO K800-NUM-REGN.                                
           MOVE SPACES         TO WS-REASON.                                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 ADD 1 TO WS-FOUND-K800                                         
                 MOVE 'Y' TO WS-FOUND                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 MOVE 'N' TO WS-FOUND                                           
              WHEN OTHER                                                        
                 MOVE WS-K800-STATUS        TO RETURN-CODE                      
                 DISPLAY 'READ BP13K800 FAIL. STATUS=' WS-K800-STATUS           
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4500-READ-K022.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE F022-KEY-FLD   TO K022-KEY-FLD.                                 
           MOVE 'Y'            TO WS-K022-FOUND.                                
                                                                                
           START BP13K022 KEY >= K022-NUM-REGN.                                 
                                                                                
           IF WS-K022-STATUS = 00 OR 02                                         
              READ BP13K022 NEXT RECORD                                         
              IF WS-K022-STATUS = 00 OR 02                                      
                 IF K022-NUM-REGN = F022-NUM-REGN                               
                    MOVE 'Y' TO WS-K022-FOUND                                   
                 ELSE                                                           
                    MOVE 'N' TO WS-K022-FOUND                                   
                 END-IF                                                         
              ELSE                                                              
                 IF WS-K022-STATUS = 23                                         
                    MOVE 'N' TO WS-K022-FOUND                                   
                 ELSE                                                           
                    DISPLAY 'ERROR IN READ OF BP13K022. STATUS IS '             
                            WS-K022-STATUS                                      
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
           IF WS-K022-STATUS = 23                                               
              MOVE 'N' TO WS-K022-FOUND                                         
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR WITH START READ OF BP13K022. STATUS IS '           
                       WS-K022-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5000-DELETE-K022.                                                        
      *--------------------------------------------------------*                
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 ADD 1 TO WS-K022-DELETE                                        
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 DISPLAY 'DELETE BP13K022 FAIL. STATUS=' WS-K022-STATUS         
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5800-MOVE-PARA.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE K022-NUM-OIC      TO  L143-OIC.                                 
           MOVE K022-NUM-REGN     TO  L143-REG-NO.                              
           MOVE K022-CDE-ALLO-CAT TO  L143-AL-CAT.                              
           MOVE K022-CDE-BANKLOAN TO  L143-BANK-LOAN.                           
           MOVE WS-REASON         TO  L143-REASON.                              
                                                                                
       5800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-WRITE-BP13L143.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 50                                          
              PERFORM 6100-PRINT-HEADING THRU 6100-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L143  TO    L143-NUMBER.                           
                                                                                
           WRITE BP13L143-REC      FROM  L143-PR-DETAILS-1 AFTER 1.             
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE.                   
           ADD   1                 TO    WS-CNT-PRINT-L143.                     
           ADD   1                 TO    WS-CNT-WRITE-L143.                     
                                                                                
           MOVE  SPACES            TO    L143-PR-DETAILS-1.                     
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6100-PRINT-HEADING.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L143-PAGE-NO.                           
           MOVE WS-EDITED-DATE   TO     L143-RUN-DATE.                          
                                                                                
           MOVE SPACES           TO     BP13L143-REC.                           
                                                                                
           WRITE BP13L143-REC    AFTER  PAGE.                                   
           WRITE BP13L143-REC    FROM   L143-PR-HEAD-01 AFTER 1.                
           WRITE BP13L143-REC    FROM   L143-PR-HEAD-02 AFTER 1.                
           WRITE BP13L143-REC    FROM   L143-PR-HEAD-03 AFTER 2.                
           WRITE BP13L143-REC    FROM   L143-PR-HEAD-04 AFTER 1.                
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '**********  BP13C143  *************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE   : ' WS-EDITED-DATE.                                  
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ    :'                                    
           DISPLAY '   BP13F022           : '  WS-READ-F022.                    
           DISPLAY 'NO OF RECORDS DELETE  :'   WS-K022-DELETE.                  
           DISPLAY 'FOUND  - BP13K400     : '  WS-FOUND-K400.                   
           DISPLAY 'FOUND  - BP13K800     : '  WS-FOUND-K800.                   
           DISPLAY SPACES.                                                      
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE BP13F022                                                       
                 BP13K022                                                       
                 BP13K400                                                       
                 BP13K800                                                       
                 BP13L143.                                                      
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 DISPLAY 'CLOSE BP13K022 FAIL. STATUS=' WS-K022-STATUS          
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K400-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K400-STATUS        TO RETURN-CODE                      
                 DISPLAY 'CLOSE BP13K400 FAIL. STATUS=' WS-K400-STATUS          
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K800-STATUS        TO RETURN-CODE                      
                 DISPLAY 'CLOSE BP13K800 FAIL. STATUS=' WS-K800-STATUS          
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
