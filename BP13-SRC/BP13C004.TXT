       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C004.                                                 
      *AUTHOR.        RACHEL G. MENDOZA.                                        
      *DATE-WRITTEN.  05/11/1994.                                               
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS IS PART OF THE SYSTEM PROCESSING       *                
      *            THE COLL TRANS FILE. IT MAKES USE OF        *                
      *            THE COLL TRANS FILE WITH RECEIPT CODE       *                
      *            '0885H'. THE EDITED RECORDS ARE APPENDED    *                
      *            TO BP13K130 WHILE THE ERROR RECORDS ARE     *                
      *            WRITTEN TO KIVTRAN FILE(BP13F140)           *                
      *            THIS PROGRAM IS SAME AS BP13C553            *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *     1. AC28F526 - COLL TRANSACTION FILE                *                
      *                                                        *                
      *                                                        *                
      *     INPUT-OUTPUT                                       *                
      *     2. BP13F140 - KIV FILE                             *                
      *     3. BP13K130 - CDHIST FILE                          *                
      *                                                        *                
      *     LISTINGS:                                          *                
      *     1. BP13K800 - SOC APPLICATION MASTER               *                
      *     2. P13L004A - REPORT LIST                          *                
      *     3. P13L004B - ERROR  LIST                          *                
      *                                                        *                
      * CHGE    BY  DATE   DESCRIPTION                         *                
      * ------- --- ------ -----------                         *                
      *BP130549 SGK 110898 TO READ AC28F526 INSTEAD OF BP13F100*                
      *BP130418 CLT 200898 Y2K COMPLIANCE CHANGES.             *                
      *BP130855 LSB 240500 IF AMT NOT TALLY, WRITE TO BP13K130 *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AC28F526 ASSIGN TO AC28F526.                                  
                                                                                
           SELECT BP13F140 ASSIGN TO BP13F140.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K800-NUM-REGN                              
                FILE STATUS       IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13K130 ASSIGN TO BP13K130                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K130-KEY-FLD                               
                FILE STATUS       IS BP13K130-STATUS.                           
                                                                                
           SELECT P13L004A ASSIGN TO P13L004A.                                  
                                                                                
           SELECT P13L004B ASSIGN TO P13L004B.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AC28F526                                                             
           BLOCK     CONTAINS 0   RECORDS                                       
           RECORD    CONTAINS 130 CHARACTERS                                    
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY AC28F526.                                                           
                                                                                
       FD  BP13F140                                                             
           BLOCK     CONTAINS 0   RECORDS                                       
           RECORD    CONTAINS 150 CHARACTERS                                    
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F140.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD   CONTAINS 2000 CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K130                                                             
           RECORD    CONTAINS 150 CHARACTERS.                                   
       COPY BP13K130.                                                           
                                                                                
       FD  P13L004A                                                             
           BLOCK     CONTAINS 0   RECORDS                                       
           RECORD    CONTAINS 132 CHARACTERS                                    
           LABEL     RECORDS  ARE OMITTED                                       
           RECORDING MODE     IS  F.                                            
       01  PRINT-RECA             PIC X(132).                                   
                                                                                
       FD  P13L004B                                                             
           BLOCK     CONTAINS 0   RECORDS                                       
           RECORD    CONTAINS 132 CHARACTERS                                    
           LABEL     RECORDS  ARE OMITTED                                       
           RECORDING MODE     IS  F.                                            
       01  PRINT-RECB             PIC X(132).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05 BP13K800-STATUS      PIC 99      VALUE ZEROS.                     
           05 BP13K130-STATUS      PIC 99      VALUE ZEROS.                     
           05 WS-CNT-F526-READ     PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-K800-READ     PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-K800-UPDATE   PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-K130-WRITE    PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-F140-WRITE    PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-L004-ERROR    PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-L004-REPORT   PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-PAGE-ERROR    PIC 9(5)    VALUE ZEROS.                     
           05 WS-CNT-PAGE-REPORT   PIC 9(5)    VALUE ZEROS.                     
           05 WS-EOF-F526          PIC X       VALUE 'N'.                       
           05 WS-CNT-LINE-ERROR    PIC 99      VALUE 66.                        
           05 WS-CNT-LINE-REPORT   PIC 99      VALUE 66.                        
           05 WS-MAX-LINES         PIC 99      VALUE 57.                        
           05 WS-REGN-FEE-COL      PIC S9(4)V99 VALUE 0.                        
           05 WS-REGN-FEE-TOT      PIC S9(4)V99 VALUE 0.                        
       01  WS-DATES.                                                            
           05 WS-CUR-DATE.                                                      
               10 WS-YYYY          PIC 9(4).                                    
               10 WS-MM            PIC 99.                                      
               10 WS-DD            PIC 99.                                      
           05 WS-TRANS-DATE.                                                    
               10 WS-TRANS-YYYY    PIC 9(4).                                    
               10 WS-TRANS-MM      PIC 9(2).                                    
               10 WS-TRANS-DD      PIC 9(2).                                    
           05 WS-EDITED-DATE.                                                   
               10 WS-DD            PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS-MM            PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS-YYYY          PIC 9(4).                                    
           05 WS-PRINT-DATE.                                                    
               10 WS-PRINT-DD      PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS-PRINT-MM      PIC 99.                                      
               10 FILLER           PIC X      VALUE '/'.                        
               10 WS-PRINT-CCYY    PIC 9(4).                                    
                                                                                
       01  L004-PR-HEAD-01.                                                     
           05  L004-OUT            PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L004-DATE           PIC X(10).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L004-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L004-PR-HEAD-02-A.                                                   
           05  FILLER              PIC X(43)     VALUE SPACES.                  
           05  FILLER              PIC X(46)     VALUE                          
               'UPDATE REGISTRATION FEE FROM COLLECTION SYSTEM'.                
                                                                                
       01  L004-PR-HEAD-02-B.                                                   
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE                          
               'ERROR ON REGISTRATION FEE FROM COLLECTION SYSTEM'.              
                                                                                
       01  L004-PR-HEAD-03.                                                     
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
               'RECEIPT CODE = 0885H'.                                          
                                                                                
       01  L004-PR-HEAD-04.                                                     
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(13)     VALUE 'S/NO'.                  
           05  FILLER              PIC X(19)     VALUE 'DATE TRANS'.            
           05  FILLER              PIC X(16)     VALUE 'REGN-NO'.               
           05  FILLER              PIC X(19)     VALUE 'RECEIPT-NO'.            
           05  FILLER              PIC X(20)     VALUE 'RECEIPT-AMT'.           
           05  L004-REM-HEAD       PIC X(07)     VALUE 'REMARKS'.               
                                                                                
                                                                                
       01  L004-PR-DETAILS.                                                     
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  L004-SNO            PIC ZZZZ9 .                                  
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  L004-DTE-TRANS      PIC X(10).                                   
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  L004-REG-NO         PIC X(8).                                    
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  L004-RECEIPT-NO     PIC X(12).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L004-RECEIPT-AMT    PIC $$$$$$9.99DB.                            
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L004-REMARKS        PIC X(20)     VALUE SPACES.                  
           05  L004-REGNFEE        PIC X(4)      VALUE SPACES.                  
                                                                                
       01  L004-CONTROL-LINE.                                                   
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  L004-CNTL-ITEM      PIC X(35)     VALUE SPACES.                  
           05  L004-CNTL-CNT       PIC ZZZZ9.                                   
           05  FILLER              PIC X(58)     VALUE SPACES.                  
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       000-MAIN.                                                                
      ******************************************************************        
                                                                                
           PERFORM 100-OPEN-FILE                                                
              THRU 100-EXIT.                                                    
           PERFORM 200-READ-AC28F526                                            
              THRU 200-EXIT.                                                    
           PERFORM 300-PROCESS-F526                                             
              THRU 300-EXIT                                                     
             UNTIL WS-EOF-F526 = 'Y'.                                           
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILE.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  AC28F526                                                 
                I-O    BP13K130                                                 
                       BP13K800                                                 
                OUTPUT BP13F140                                                 
                       P13L004A                                                 
                       P13L004B.                                                
                                                                                
           IF BP13K800-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY 'OPENING ERROR,BP13K800-STATUS: ' BP13K800-STATUS         
              MOVE BP13K800-STATUS   TO    RETURN-CODE                          
              GO   TO   900-EXIT.                                               
                                                                                
           IF BP13K130-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY 'OPENING ERROR,BP13K130-STATUS: ' BP13K130-STATUS         
              MOVE BP13K130-STATUS   TO    RETURN-CODE                          
              GO   TO   900-EXIT.                                               
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                  
           MOVE   CORR WS-CUR-DATE   TO    WS-EDITED-DATE.                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-AC28F526.                                                       
      ******************************************************************        
                                                                                
           READ AC28F526                                                        
             AT END MOVE 'Y' TO WS-EOF-F526                                     
             GO TO 200-EXIT.                                                    
                                                                                
           ADD  1  TO WS-CNT-F526-READ.                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F526.                                                        
      ******************************************************************        
                                                                                
           IF F526-NUM-RCPTCD = '0885H'                                         
              IF F526-NUM-REF = SPACES OR LOW-VALUES                            
                 MOVE 'F526-NUM-REF  BLANK' TO L004-REMARKS                     
                 PERFORM 400-PRINT-ERROR                                        
                    THRU 400-EXIT                                               
                 PERFORM 500-WRITE-BP13F140                                     
                    THRU 500-EXIT                                               
              ELSE                                                              
                 PERFORM 600-READ-BP13K800                                      
                    THRU 600-EXIT.                                              
                                                                                
           PERFORM 200-READ-AC28F526                                            
              THRU 200-EXIT.                                                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       400-PRINT-ERROR.                                                         
      **********************************************************                
                                                                                
           IF WS-CNT-LINE-ERROR  > WS-MAX-LINES                                 
              PERFORM 410-ERROR-HEADING                                         
                 THRU 410-EXIT.                                                 
                                                                                
           ADD   1                  TO    WS-CNT-L004-ERROR.                    
           MOVE  WS-CNT-L004-ERROR  TO    L004-SNO.                             
           MOVE  F526-DTE-TXN       TO    WS-TRANS-DATE.                        
           MOVE  WS-TRANS-YYYY      TO    WS-PRINT-CCYY.                        
           MOVE  WS-TRANS-MM        TO    WS-PRINT-MM.                          
           MOVE  WS-TRANS-DD        TO    WS-PRINT-DD.                          
           MOVE  WS-PRINT-DATE      TO    L004-DTE-TRANS.                       
           MOVE  F526-NUM-REF       TO    L004-REG-NO.                          
           MOVE  F526-NUM-RCPT      TO    L004-RECEIPT-NO.                      
           MOVE  F526-AMT-TXN       TO    L004-RECEIPT-AMT.                     
           WRITE PRINT-RECB         FROM  L004-PR-DETAILS  AFTER 1.             
           MOVE  SPACES             TO    PRINT-RECB.                           
           ADD   1                  TO    WS-CNT-LINE-ERROR.                    
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       410-ERROR-HEADING.                                                       
      **********************************************************                
                                                                                
            ADD   1                 TO   WS-CNT-PAGE-ERROR.                     
            MOVE  WS-CNT-PAGE-ERROR TO   L004-PAGENO.                           
            MOVE  WS-EDITED-DATE    TO   L004-DATE.                             
            MOVE  'P13L004B'        TO   L004-OUT.                              
            WRITE PRINT-RECB        FROM L004-PR-HEAD-01   AFTER PAGE.          
            WRITE PRINT-RECB        FROM L004-PR-HEAD-02-B AFTER 1.             
            WRITE PRINT-RECB        FROM L004-PR-HEAD-03   AFTER 2.             
            WRITE PRINT-RECB        FROM L004-PR-HEAD-04   AFTER 2.             
            MOVE  SPACES            TO   PRINT-RECB.                            
            WRITE PRINT-RECB                               AFTER 1.             
            MOVE  8                 TO   WS-CNT-LINE-ERROR.                     
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       500-WRITE-BP13F140.                                                      
      **********************************************************                
                                                                                
           MOVE SPACES             TO  BP13F140-KIVTRAN.                        
           MOVE '11'               TO  F140-CDE-TRANS-TYPE.                     
           MOVE F526-NUM-REF       TO  F140-NUM-REGN.                           
           MOVE F526-DTE-TXN       TO  F140-DTE-POST                            
                                       F140-DTE-TRANS.                          
           MOVE SPACES             TO  F140-NUM-NRIC.                           
           MOVE F526-NUM-RCPT      TO  F140-NUM-RECPT-JRNO.                     
           MOVE F526-AMT-TXN       TO  F140-AMT-RECEIPT.                        
           MOVE F526-NUM-CRDR      TO  F140-CDE-CRDR.                           
           MOVE 'REG'              TO  F140-CDE-PAYMENT-TYPE.                   
           MOVE F526-NUM-HDBREF    TO  F140-NUM-SCH-ACCT.                       
                                                                                
           WRITE BP13F140-KIVTRAN.                                              
                                                                                
           ADD   1                 TO  WS-CNT-F140-WRITE.                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       600-READ-BP13K800.                                                       
      **********************************************************                
                                                                                
           MOVE F526-NUM-REF  TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF (BP13K800-STATUS     = 00                   ) AND                 
              (K800-NUM-ZONE   NOT = SPACES AND LOW-VALUES)                     
                ADD 1 TO WS-CNT-K800-READ                                       
                COMPUTE WS-REGN-FEE-COL = F526-AMT-TXN                          
                IF K800-AMT-REGN-REQ IS NOT NUMERIC                             
                   MOVE  ZERO        TO   K800-AMT-REGN-REQ                     
                END-IF                                                          
                IF K800-AMT-REGN-GST IS NOT NUMERIC                             
                   MOVE  ZERO        TO   K800-AMT-REGN-GST                     
                END-IF                                                          
                COMPUTE WS-REGN-FEE-TOT = K800-AMT-REGN-REQ +                   
                                          K800-AMT-REGN-GST                     
                                                                                
                IF (K800-AMT-REGN-PAID = ZEROS          ) AND                   
                   (WS-REGN-FEE-COL    = WS-REGN-FEE-TOT)                       
                    MOVE WS-REGN-FEE-TOT TO K800-AMT-REGN-PAID                  
                    PERFORM 650-UPDATE-BP13K800                                 
                       THRU 650-EXIT                                            
                    PERFORM 700-WRITE-BP13K130                                  
                       THRU 700-EXIT                                            
                    PERFORM 800-PRINT-REPORT                                    
                       THRU 800-EXIT                                            
                                                                                
                ELSE                                                            
                IF (K800-AMT-REGN-PAID = WS-REGN-FEE-COL) AND                   
                   (WS-REGN-FEE-COL    = WS-REGN-FEE-TOT)                       
                                                                                
                   ADD  WS-REGN-FEE-TOT TO K800-AMT-REGN-PAID                   
                   PERFORM 650-UPDATE-BP13K800                                  
                       THRU 650-EXIT                                            
                   PERFORM 720-WRITE-REPAYMENT                                  
                      THRU 720-EXIT                                             
                   MOVE 'REPAYMENT OF REGN  '  TO L004-REMARKS                  
                   PERFORM 400-PRINT-ERROR                                      
                      THRU 400-EXIT                                             
                   MOVE SPACES                 TO L004-REGNFEE                  
                                                                                
                ELSE                                                            
                   MOVE 'REGN FEE NOT TALLY $' TO L004-REMARKS                  
                   MOVE WS-REGN-FEE-TOT (1:4)  TO L004-REGNFEE                  
                   PERFORM 400-PRINT-ERROR                                      
                      THRU 400-EXIT                                             
                   PERFORM 700-WRITE-BP13K130                                   
                      THRU 700-EXIT                                             
                   PERFORM 500-WRITE-BP13F140                                   
                      THRU 500-EXIT                                             
                   MOVE SPACES                 TO L004-REGNFEE                  
                END-IF                                                          
                END-IF                                                          
                                                                                
           ELSE                                                                 
           IF (BP13K800-STATUS     = 00                   ) AND                 
              (K800-NUM-ZONE       = SPACES  OR LOW-VALUES)                     
              MOVE 'REGN NOT REQUIRED   ' TO L004-REMARKS                       
              PERFORM 400-PRINT-ERROR                                           
                 THRU 400-EXIT                                                  
              PERFORM 500-WRITE-BP13F140                                        
                 THRU 500-EXIT                                                  
                                                                                
           ELSE                                                                 
           IF BP13K800-STATUS = 23                                              
              MOVE 'REGN NOT FOUND      ' TO L004-REMARKS                       
              PERFORM 400-PRINT-ERROR                                           
                 THRU 400-EXIT                                                  
              PERFORM 500-WRITE-BP13F140                                        
                 THRU 500-EXIT                                                  
           END-IF                                                               
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       650-UPDATE-BP13K800.                                                     
      **********************************************************                
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF BP13K800-STATUS NOT = 0                                           
               DISPLAY ' REWRITE ERROR AT BP13K800 '                            
               DISPLAY ' BP13K800-STATUS IS ' BP13K800-STATUS                   
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE                                        
                  THRU 900-EXIT.                                                
                                                                                
           ADD 1 TO WS-CNT-K800-UPDATE.                                         
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       700-WRITE-BP13K130.                                                      
      **********************************************************                
                                                                                
           MOVE SPACES                 TO  BP13K130-CDHIST.                     
                                                                                
           MOVE '11'                   TO  K130-TRANS-TYPE.                     
           MOVE K800-NUM-REGN          TO  K130-NUM-ORIG-REGN.                  
           MOVE F526-DTE-TXN           TO  K130-DTE-POST                        
                                           K130-DTE-TRANS.                      
           MOVE K800-NUM-NRIC1         TO  K130-NUM-NRIC.                       
           MOVE F526-NUM-RCPT          TO  K130-NUM-RECPT-JRNO.                 
           MOVE F526-AMT-TXN           TO  K130-AMT-CASH.                       
           MOVE F526-NUM-CRDR          TO  K130-AMT-TYP.                        
           MOVE 'REG'                  TO  K130-PAYMENT-TYPE.                   
           MOVE F526-NUM-HDBREF        TO  K130-NUM-SCH-ACCT.                   
           MOVE ZEROS                  TO  K130-COUNT-HISTORY.                  
                                                                                
           WRITE BP13K130-CDHIST.                                               
           IF BP13K130-STATUS > 0 AND NOT = 22 AND NOT = 02                     
               DISPLAY ' WRITE ERROR AT BP13K130 '                              
               DISPLAY ' BP13K130-STATUS IS ' BP13K130-STATUS                   
               DISPLAY ' ORIGINAL REGN NO IS : ' F526-NUM-REF                   
               MOVE BP13K130-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE                                        
                  THRU 900-EXIT                                                 
           ELSE                                                                 
               IF BP13K130-STATUS  = 22                                         
                  PERFORM 710-WRITE-HISTORY                                     
                     THRU 710-EXIT                                              
                    UNTIL BP13K130-STATUS = 0.                                  
                                                                                
           ADD 1 TO WS-CNT-K130-WRITE.                                          
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       720-WRITE-REPAYMENT.                                                     
      **********************************************************                
                                                                                
           MOVE SPACES                 TO  BP13K130-CDHIST.                     
                                                                                
           MOVE '11'                   TO  K130-TRANS-TYPE.                     
           MOVE K800-NUM-REGN          TO  K130-NUM-ORIG-REGN.                  
           MOVE F526-DTE-TXN           TO  K130-DTE-POST                        
                                           K130-DTE-TRANS.                      
           MOVE K800-NUM-NRIC1         TO  K130-NUM-NRIC.                       
           MOVE F526-NUM-RCPT          TO  K130-NUM-RECPT-JRNO.                 
           MOVE F526-AMT-TXN           TO  K130-AMT-CASH.                       
           MOVE F526-NUM-CRDR          TO  K130-AMT-TYP.                        
           MOVE 'RPY'                  TO  K130-PAYMENT-TYPE.                   
           MOVE F526-NUM-HDBREF        TO  K130-NUM-SCH-ACCT.                   
           MOVE ZEROS                  TO  K130-COUNT-HISTORY.                  
                                                                                
           WRITE BP13K130-CDHIST.                                               
           IF BP13K130-STATUS > 0 AND NOT = 22 AND NOT = 02                     
               DISPLAY ' WRITE ERROR AT BP13K130 '                              
               DISPLAY ' BP13K130-STATUS IS ' BP13K130-STATUS                   
               DISPLAY ' ORIGINAL REGN NO IS : ' F526-NUM-REF                   
               MOVE BP13K130-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE                                        
                  THRU 900-EXIT                                                 
           ELSE                                                                 
               IF BP13K130-STATUS  = 22                                         
                  PERFORM 710-WRITE-HISTORY                                     
                     THRU 710-EXIT                                              
                    UNTIL BP13K130-STATUS = 0.                                  
                                                                                
           ADD 1 TO WS-CNT-K130-WRITE.                                          
                                                                                
       720-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       710-WRITE-HISTORY.                                                       
      **********************************************************                
                                                                                
           ADD 1     TO  K130-COUNT-HISTORY                                     
           WRITE BP13K130-CDHIST                                                
           IF BP13K130-STATUS NOT = 0                                           
              GO TO 710-EXIT.                                                   
                                                                                
       710-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       800-PRINT-REPORT.                                                        
      **********************************************************                
                                                                                
           IF WS-CNT-LINE-REPORT > WS-MAX-LINES                                 
              PERFORM 810-REPORT-HEADING                                        
                 THRU 810-EXIT.                                                 
                                                                                
           ADD   1                  TO    WS-CNT-L004-REPORT.                   
           MOVE  WS-CNT-L004-REPORT TO    L004-SNO.                             
           MOVE  F526-DTE-TXN       TO    WS-TRANS-DATE.                        
           MOVE  WS-TRANS-YYYY      TO    WS-PRINT-CCYY.                        
           MOVE  WS-TRANS-MM        TO    WS-PRINT-MM.                          
           MOVE  WS-TRANS-DD        TO    WS-PRINT-DD.                          
           MOVE  WS-PRINT-DATE      TO    L004-DTE-TRANS.                       
           MOVE  F526-NUM-REF       TO    L004-REG-NO.                          
           MOVE  F526-NUM-RCPT      TO    L004-RECEIPT-NO.                      
           MOVE  F526-AMT-TXN       TO    L004-RECEIPT-AMT.                     
           MOVE  SPACES             TO    L004-REMARKS.                         
           WRITE PRINT-RECA         FROM  L004-PR-DETAILS  AFTER 1.             
           MOVE  SPACES             TO    PRINT-RECA.                           
           ADD   1                  TO    WS-CNT-LINE-REPORT.                   
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       810-REPORT-HEADING.                                                      
      **********************************************************                
                                                                                
            ADD   1                  TO   WS-CNT-PAGE-REPORT.                   
            MOVE  WS-CNT-PAGE-REPORT TO   L004-PAGENO.                          
            MOVE  WS-EDITED-DATE     TO   L004-DATE.                            
            MOVE  'P13L004A'         TO   L004-OUT.                             
            MOVE  SPACES             TO   L004-REM-HEAD.                        
            WRITE PRINT-RECA         FROM L004-PR-HEAD-01   AFTER PAGE.         
            WRITE PRINT-RECA         FROM L004-PR-HEAD-02-A AFTER 1.            
            WRITE PRINT-RECA         FROM L004-PR-HEAD-03   AFTER 2.            
            WRITE PRINT-RECA         FROM L004-PR-HEAD-04   AFTER 2.            
            MOVE  SPACES             TO   PRINT-RECA.                           
            WRITE PRINT-RECA                               AFTER 1.             
            MOVE  8                  TO   WS-CNT-LINE-REPORT.                   
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       900-CLOSE-ROUTINE.                                                       
      **********************************************************                
                                                                                
           MOVE SPACES             TO    PRINT-RECB.                            
           WRITE PRINT-RECB        AFTER PAGE.                                  
           MOVE 'NO OF COLL RECS READ     (F526): ' TO L004-CNTL-ITEM.          
           MOVE  WS-CNT-F526-READ  TO    L004-CNTL-CNT.                         
           WRITE PRINT-RECB        FROM  L004-CONTROL-LINE AFTER 3.             
                                                                                
           MOVE 'NO OF SOC MASTER READ     (K800): ' TO L004-CNTL-ITEM.         
           MOVE  WS-CNT-K800-READ  TO    L004-CNTL-CNT.                         
           WRITE PRINT-RECB        FROM  L004-CONTROL-LINE.                     
                                                                                
           MOVE 'NO OF SOC MASTER REWRITTEN(K800): ' TO L004-CNTL-ITEM.         
           MOVE  WS-CNT-K800-UPDATE TO    L004-CNTL-CNT.                        
           WRITE PRINT-RECB        FROM  L004-CONTROL-LINE.                     
                                                                                
           MOVE 'NO OF KIV RECS WRITTEN    (F140): ' TO L004-CNTL-ITEM.         
           MOVE  WS-CNT-F140-WRITE TO    L004-CNTL-CNT.                         
           WRITE PRINT-RECB        FROM  L004-CONTROL-LINE .                    
                                                                                
           MOVE 'NO OF CDHIST RECS WRITTEN (K130): ' TO L004-CNTL-ITEM.         
           MOVE  WS-CNT-K130-WRITE TO    L004-CNTL-CNT.                         
           WRITE PRINT-RECB        FROM  L004-CONTROL-LINE .                    
                                                                                
           DISPLAY '******  BP13C004 *************'.                            
           DISPLAY 'NO OF F526 READ      (F526) : ' WS-CNT-F526-READ.           
           DISPLAY 'NO OF RFF RECS READ  (K800) : ' WS-CNT-K800-READ.           
           DISPLAY 'NO OF RFF RECS UPDATE(K800) : ' WS-CNT-K800-UPDATE.         
           DISPLAY 'NO OF F140 WRITE     (F140) : ' WS-CNT-F140-WRITE.          
           DISPLAY 'NO OF K130 WRITE     (K130) : ' WS-CNT-K130-WRITE.          
                                                                                
           CLOSE AC28F526                                                       
                 BP13F140                                                       
                 BP13K800                                                       
                 BP13K130                                                       
                 P13L004A                                                       
                 P13L004B.                                                      
                                                                                
                                                                                
           IF BP13K800-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS:' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE .                               
                                                                                
           IF BP13K130-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BP13K130-STATUS:' BP13K130-STATUS          
            MOVE BP13K130-STATUS TO RETURN-CODE .                               
                                                                                
                                                                                
       900-EXIT.                                                                
           STOP RUN.                                                            
                                                                                
