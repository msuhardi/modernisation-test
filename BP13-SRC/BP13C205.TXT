       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C205.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  FEB2007.                                                  
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *  OBJECTIVE   : TO UPDATE SERS SELECTION END DATE TO UNSELECTED*         
      *                UNITS.                                         *         
      *                                                               *         
      *  INPUT FILES : BP13F207                                       *         
      *                BM06K110                                       *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * CHG REQ# DATE       BY    DESCRIPTION                         *         
      * -------- ---------- ----  ------------------------------------*         
      * BP132967 16/02/2007 LMS   NEW PROGRAM                         *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F207  ASSIGN       TO BP13F207.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS DYNAMIC                             
                           RECORD KEY    IS K110-KEY-FLD                        
                           ALTERNATE KEY IS K110-AIX1                           
                           FILE STATUS   IS WS-K110-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F207                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F207.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-WORK-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10 WS-SYS-DATE          PIC X(08) VALUE SPACES.                  
               10 FILLER               PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DATE-DD     PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-DATE-MM     PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-DATE-YY     PIC X(04) VALUE SPACES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F207-READ            PIC 9(04) VALUE ZEROES.                  
           05  WS-K110-READ            PIC 9(04) VALUE ZEROES.                  
           05  WS-K110-UPDATE          PIC 9(04) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F207-EOF-SW          PIC X(01) VALUE SPACES.                  
               88  C-F207-EOF          VALUE 'Y'.                               
           05  WS-K110-EOF-SW          PIC X(01) VALUE SPACES.                  
               88  C-K110-EOF          VALUE 'Y'.                               
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                  
            PERFORM 2000-PROCESS-RECORDS      THRU  2000-EXIT                   
              UNTIL C-F207-EOF.                                                 
            PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F207                                                
                 I-O    BM06K110.                                               
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110-OPEN ERROR, STATUS: ' WS-K110-STATUS            
              MOVE     WS-K110-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES            THRU 9000-EXIT.              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           READ BP13F207                                                        
                AT END MOVE 'Y' TO WS-F207-EOF-SW                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1      TO WS-F207-READ.                                         
           MOVE SPACES TO WS-K110-EOF-SW.                                       
                                                                                
           PERFORM 2100-START-BM06K110       THRU 2100-EXIT.                    
           PERFORM 2200-READNXT-BM06K110     THRU 2200-EXIT                     
             UNTIL C-K110-EOF OR                                                
                   C-F207-EOF.                                                  
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2100-START-BM06K110.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                   TO K110-AIX1.                          
           MOVE F207-ESTATE-CDE          TO K110-ESTATE.                        
           MOVE F207-NEIGHBOURHOOD       TO K110-NEIGHBOURHOOD.                 
           MOVE F207-CONTRACT-NO         TO K110-CONTRACT-NO.                   
           MOVE F207-BLK-NO              TO K110-BLK-NO.                        
                                                                                
           START BM06K110 KEY NOT LESS THAN K110-AIX1.                          
           IF WS-K110-STATUS = ZEROES                                           
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'START-READ ERROR FOR BM06K110, STATUS: '                 
                       WS-K110-STATUS                                           
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2200-READNXT-BM06K110.                                                   
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110 NEXT RECORD AT END                                     
                MOVE 'Y'      TO WS-K110-EOF-SW                                 
                GO TO 2200-EXIT.                                                
                                                                                
           IF WS-K110-STATUS = 00 OR 02                                         
              PERFORM 3000-GET-K110-RECORD        THRU 3000-EXIT                
           ELSE                                                                 
              DISPLAY 'READNEXT ERROR FOR BM06K110, STATUS: '                   
                       WS-K110-STATUS                                           
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-GET-K110-RECORD.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF F207-ESTATE-CDE    = K110-ESTATE        AND                       
              F207-NEIGHBOURHOOD = K110-NEIGHBOURHOOD AND                       
              F207-CONTRACT-NO   = K110-CONTRACT-NO   AND                       
              F207-BLK-NO        = K110-BLK-NO                                  
              ADD 1 TO WS-K110-READ                                             
              IF K110-DTE-ACCEPTANCE = '00000000' OR SPACES OR                  
                                        LOW-VALUES                              
                 MOVE F207-DTE-END-SERS TO K110-DTE-SERS-UNSOLD                 
                 REWRITE K110-REC                                               
                 ADD 1 TO WS-K110-UPDATE                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '------- BP13C205 -------------'.                            
           DISPLAY ' NO OF F207 READ    : ' WS-F207-READ.                       
           DISPLAY ' NO OF K110 READ    : ' WS-K110-READ.                       
           DISPLAY ' NO OF K110 UPDATED : ' WS-K110-UPDATE.                     
                                                                                
           CLOSE BP13F207                                                       
                 BM06K110.                                                      
                                                                                
           IF WS-K110-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BM06K110, STAT ' WS-K110-STATUS            
              MOVE     WS-K110-STATUS   TO RETURN-CODE.                         
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
