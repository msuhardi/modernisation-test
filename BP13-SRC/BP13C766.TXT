       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C766.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  : TO TRANSFER THE BALANCE UNITS OF CCC   *                
      *                 SCHEME TO PA SCHEME.                   *                
      *                                                        *                
      *  CHG-NO  BY   ON   DESCRIPTION                         *                
      *  -----------------------------                         *                
      * C930194 RAZ 930118 TO TRANSFER THE BALANCE UNITS OF    *                
      *                    CCC SCHEME TO PA SCHEME.            *                
      * I920718 LJL 930212 TO CHANGE FROM COBOL74 TO COBOL85.  *                
      * C930335 LSB 930317 EXPAND BP13K010 FOR RDC             *                
      *BP130350 LSB 980829 TRANSFER BAL RH/REN/RDC TO PA       *                
      *BP132455 CKK 030121 TRANSFER BAL JB TO PA TOO           *                
      *BP132555 CKK 040707 TRANSFER BAL TCP TO PA TOO          *                
      *BP132626 CKK 041202 TRANSFER TCP AND REN UNUSED QUOTA   *                
      *BP132927 CKK 061110 TRANSFER MTF QUOTA AS PRIO FOR DBSS *                
      *BP133050 CKK 070423 TRANSFER CCC QUOTA EVEN TOTAL = 0   *                
      *BP134861 EAA1 220413 EXPAND BP13K010 TO 600             *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010    ASSIGN TO BP13K010                                
                              ORGANIZATION IS INDEXED                           
                              ACCESS MODE  IS SEQUENTIAL                        
                              RECORD KEY   IS K010-KEY-FLD                      
                              FILE STATUS  IS WS-K010-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
                              LABEL RECORD IS STANDARD                          
                              RECORD CONTAINS 600 CHARACTERS                    
                              BLOCK CONTAINS 0 RECORDS.                         
           COPY BP13K010.                                                       
                                                                                
        WORKING-STORAGE SECTION.                                                
        01  WS-VARIABLES.                                                       
            05  WS-CNT-K010-READ         PIC 9(8) VALUE 0.                      
            05  WS-CNT-K010-REWRITE      PIC 9(8) VALUE 0.                      
            05  WS-K010-EOF              PIC X    VALUE 'N'.                    
                88 K010-EOF                       VALUE 'Y'.                    
            05  WS-K010-STATUS           PIC 99   VALUE 0.                      
            05  WS-NUM-BAL-MTF           PIC 9(5) VALUE 0.                      
            05  WS-NUM-BAL-CCC           PIC 9(5) VALUE 0.                      
            05  WS-NUM-BAL-RH            PIC 9(5) VALUE 0.                      
            05  WS-NUM-BAL-REN           PIC 9(5) VALUE 0.                      
            05  WS-NUM-BAL-RDC           PIC 9(5) VALUE 0.                      
            05  WS-NUM-BAL-JB            PIC 9(5) VALUE 0.                      
            05  WS-NUM-BAL-TCP           PIC 9(5) VALUE 0.                      
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-CONTROL-RTN.                                                        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 1500-START-BP13K010-KEY THRU 1500-EXIT.              01790000
           PERFORM 3000-MAIN-ROUTINE     THRU 3000-EXIT                         
             UNTIL K010-EOF.                                                    
                                                                                
           PERFORM 9999-CLOSE-FILES      THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN I-O    BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                        01780000
       1500-START-BP13K010-KEY.                                         01790000
            MOVE SPACES TO K010-KEY-FLD                                 01800000
            START BP13K010 KEY > K010-KEY-FLD                           01810000
                                                                                
           IF WS-K010-STATUS  = 00                                              
              PERFORM 2000-READ-BP13K010    THRU 2000-EXIT                      
           ELSE                                                                 
              DISPLAY 'START READ BP13K010, STATUS ' WS-K010-STATUS             
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                        01860000
                                                                                
       2000-READ-BP13K010.                                                      
                                                                                
           READ BP13K010 NEXT            AT END                                 
           MOVE 'Y'                      TO WS-K010-EOF                         
           GO TO 2000-EXIT.                                                     
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'ERROR READING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           ADD 1                         TO WS-CNT-K010-READ.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
                                                                                
              PERFORM 4000-ADD-ROUTINE   THRU 4000-EXIT.                        
                                                                                
              REWRITE BP13K010-TENDER.                                          
                                                                                
              IF WS-K010-STATUS = 00                                            
                 ADD 1                   TO WS-CNT-K010-REWRITE                 
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K010, STATUS '                    
                                            WS-K010-STATUS                      
                 DISPLAY '                          KEY IS '                    
                                               K010-KEY-FLD                     
                 MOVE WS-K010-STATUS        TO RETURN-CODE                      
                 GO TO 9999-CLOSE-FILES.                                        
                                                                                
           PERFORM 2000-READ-BP13K010    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-ADD-ROUTINE.                                                        
                                                                                
      ** TRANSFER CCC TO PA                                                     
                                                                                
           COMPUTE WS-NUM-BAL-CCC = K010-NUM-TOT-CCC - K010-REG-CCC1 -          
                                    K010-REG-CCC2    - K010-REG-CCC3.           
                                                                                
                                                                                
           IF WS-NUM-BAL-CCC >= 0                                               
              ADD WS-NUM-BAL-CCC            TO K010-NUM-TOT-PA                  
              SUBTRACT WS-NUM-BAL-CCC       FROM K010-NUM-TOT-CCC               
                                                                                
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                     
                  (K010-NUM-MAX-CCC1 -  K010-REG-CCC1)                          
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                     
                  (K010-NUM-MAX-CCC2 -  K010-REG-CCC2)                          
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                     
                  (K010-NUM-MAX-CCC3 -  K010-REG-CCC3).                         
                                                                                
      ** TRANSFER MTF TO PA                                                     
                                                                                
           COMPUTE WS-NUM-BAL-MTF = K010-NUM-TOT-MTF - K010-REG-MTF1 -          
                                    K010-REG-MTF2    - K010-REG-MTF3.           
                                                                                
                                                                                
           IF WS-NUM-BAL-MTF > 0                                                
              ADD WS-NUM-BAL-MTF            TO K010-NUM-TOT-PA                  
              SUBTRACT WS-NUM-BAL-MTF       FROM K010-NUM-TOT-MTF               
                                                                                
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                     
                  (K010-NUM-MAX-MTF1 -  K010-REG-MTF1)                          
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                     
                  (K010-NUM-MAX-MTF2 -  K010-REG-MTF2)                          
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                     
                  (K010-NUM-MAX-MTF3 -  K010-REG-MTF3).                         
                                                                                
      ** TRANSFER REN TO PA                                                     
                                                                                
           COMPUTE WS-NUM-BAL-REN = K010-NUM-TOT-REN - K010-REG-REN1 -          
                                    K010-REG-REN2    - K010-REG-REN3.           
                                                                                
           IF WS-NUM-BAL-REN > 0                                                
              ADD WS-NUM-BAL-REN            TO K010-NUM-TOT-PA                  
              SUBTRACT WS-NUM-BAL-REN       FROM K010-NUM-TOT-REN               
           END-IF.                                                              
                                                                                
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                     
                  (K010-NUM-MAX-REN1 -  K010-REG-REN1).                         
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                     
                  (K010-NUM-MAX-REN2 -  K010-REG-REN2).                         
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                     
                  (K010-NUM-MAX-REN3 -  K010-REG-REN3).                         
                                                                                
      ** TRANSFER RH  TO PA                                                     
                                                                                
           COMPUTE WS-NUM-BAL-RH  = K010-NUM-TOT-RH  - K010-REG-RH1 -           
                                    K010-REG-RH2    - K010-REG-RH3.             
                                                                                
           IF WS-NUM-BAL-RH  > 0                                                
              ADD WS-NUM-BAL-RH             TO K010-NUM-TOT-PA                  
              SUBTRACT WS-NUM-BAL-RH        FROM K010-NUM-TOT-RH                
                                                                                
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                     
                  (K010-NUM-MAX-RH1 -   K010-REG-RH1)                           
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                     
                  (K010-NUM-MAX-RH2 -   K010-REG-RH2)                           
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                     
                  (K010-NUM-MAX-RH3 -   K010-REG-RH3).                          
                                                                                
      ** TRANSFER RDC TO PA                                                     
                                                                                
           COMPUTE WS-NUM-BAL-RDC = K010-NUM-TOT-RDC - K010-REG-RDC1 -          
                                    K010-REG-RDC2    - K010-REG-RDC3.           
                                                                                
           IF WS-NUM-BAL-RDC > 0                                                
              ADD WS-NUM-BAL-RDC            TO K010-NUM-TOT-PA                  
              SUBTRACT WS-NUM-BAL-RDC       FROM K010-NUM-TOT-RDC               
                                                                                
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                     
                  (K010-NUM-MAX-RDC1 -  K010-REG-RDC1)                          
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                     
                  (K010-NUM-MAX-RDC2 -  K010-REG-RDC2)                          
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                     
                  (K010-NUM-MAX-RDC3 -  K010-REG-RDC3).                         
                                                                                
      ** TRANSFER JB TO PA                                                      
                                                                                
           COMPUTE WS-NUM-BAL-JB = K010-NUM-TOT-JB - K010-REG-JB1 -             
                                    K010-REG-JB2    - K010-REG-JB3.             
                                                                                
           IF WS-NUM-BAL-JB > 0                                                 
              ADD WS-NUM-BAL-JB            TO K010-NUM-TOT-PA                   
              SUBTRACT WS-NUM-BAL-JB       FROM K010-NUM-TOT-JB                 
                                                                                
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                     
                  (K010-NUM-MAX-JB1 -  K010-REG-JB1)                            
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                     
                  (K010-NUM-MAX-JB2 -  K010-REG-JB2)                            
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                     
                  (K010-NUM-MAX-JB3 -  K010-REG-JB3).                           
                                                                                
      ** TRANSFER TCP TO PA                                                     
                                                                                
           COMPUTE WS-NUM-BAL-TCP = K010-NUM-TOT-TCP - K010-REG-TCP1 -          
                                    K010-REG-TCP2    - K010-REG-TCP3.           
                                                                                
           IF WS-NUM-BAL-TCP > 0                                                
              ADD WS-NUM-BAL-TCP           TO K010-NUM-TOT-PA                   
              SUBTRACT WS-NUM-BAL-TCP      FROM K010-NUM-TOT-TCP                
           END-IF.                                                              
                                                                                
              COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                     
                  (K010-NUM-MAX-TCP1 - K010-REG-TCP1).                          
              COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                     
                  (K010-NUM-MAX-TCP2 - K010-REG-TCP2).                          
              COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                     
                  (K010-NUM-MAX-TCP3 - K010-REG-TCP3).                          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>> BP13C766 <<<<<<<<<<<<<<<<<<'.              
           DISPLAY '- ADD MAX-CAT QUOTA FROM CCC        TO PA  -   '.           
           DISPLAY '  '.                                                        
           DISPLAY 'K010 RECORDS READ    : '  WS-CNT-K010-READ.                 
           DISPLAY 'K010 RECORDS REWRITE : '  WS-CNT-K010-REWRITE.              
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
               DISPLAY 'ERROR CLOSING BP13K010, STATUS ' WS-K010-STATUS         
               MOVE WS-K010-STATUS       TO RETURN-CODE.                        
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
