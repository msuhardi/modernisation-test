      *---------------------------------------------------------------*         
       IDENTIFICATION DIVISION.                                                 
      *---------------------------------------------------------------*         
       PROGRAM-ID.    BP13C018.                                                 
       AUTHOR.        SK.                                                       
      *DATE-WRITTEN.  22/01/03.                                                 
                                                                                
      *---------------------------------------------------------------*         
      *                     SYSTEM OF COMMITMENT                     *          
      *===============================================================*         
      * OBJECTIVE :- 1) DELETE FROM BP13K648                          *         
      *===============================================================*         
      * FILE DESCRIPTIONS  :-                                         *         
      *                ACCESS      REC                                *         
      * DD NAME  I/O/E  MODE       LEN  KEY       REMARKS             *         
      * -------  ----- ------      ---  ---       -------             *         
      * BP13F410 I     SEQUENTIAL  2000                               *         
      * BP13K648 I-O   VSAM        500                                *         
      *---------------------------------------------------------------*         
                                                                                
      *---------------------------------------------------------------*         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------------------------------------------------*         
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K648 ASSIGN TO BP13K648                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K648-KEY-FLD                         
                           FILE STATUS  IS WS-K648-STATUS.                      
                                                                                
      *---------------------------------------------------------------*         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
                                                                                
       FD  BP13F410                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
                                                                                
       COPY BP13F410.                                                           
                                                                                
       FD  BP13K648                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
                                                                                
       COPY BP13K648.                                                           
                                                                                
      *---------------------------------------------------------------*         
       WORKING-STORAGE SECTION.                                                 
      *---------------------------------------------------------------*         
                                                                                
       01  WS-COUNT-VARIABLES.                                                  
           05  WS-F410-CNT-READ       PIC 9(07) VALUE ZEROES.                   
           05  WS-K648-CNT-DELETE     PIC 9(07) VALUE ZEROES.                   
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-K648-STATUS         PIC 9(02) VALUE ZEROES.                   
           05  WS-BP13F410-EOF        PIC X(01) VALUE 'N'.                      
                                                                                
      *---------------------------------------------------------------*         
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
                                                                                
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU  1000-EXIT.                    
           PERFORM 2000-BP13F410-READ       THRU  2000-EXIT.                    
           PERFORM 3000-MAIN-ROUTINE        THRU  3000-EXIT                     
                   UNTIL WS-BP13F410-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-FILES         THRU  9000-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
                                                                                
           OPEN  INPUT   BP13F410                                               
                 I-O     BP13K648.                                              
                                                                                
           EVALUATE WS-K648-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K648-STATUS TO RETURN-CODE                           
                   DISPLAY 'OPEN BP13K648 FAIL. STATUS=' WS-K648-STATUS         
                   PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-BP13F410-READ.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13F410 AT END                                                 
                MOVE 'Y '  TO  WS-BP13F410-EOF.                                 
                                                                                
           IF WS-BP13F410-EOF  =  'Y'                                           
              IF  WS-F410-CNT-READ = ZEROES                                     
                  DISPLAY '*** BP13F410  FILE IS EMPTY ***'                     
                  PERFORM 9000-CLOSE-FILES THRU  9000-EXIT                      
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-F410-CNT-READ                                         
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
                                                                                
           MOVE F410-KEY-FLD     TO K648-KEY-FLD.                               
                                                                                
           READ BP13K648.                                                       
                                                                                
           EVALUATE WS-K648-STATUS                                              
              WHEN 00                                                           
                   DELETE BP13K648                                              
                   EVALUATE WS-K648-STATUS                                      
                      WHEN 00                                                   
                           ADD 1 TO WS-K648-CNT-DELETE                          
                           CONTINUE                                             
                      WHEN OTHER                                                
                           MOVE WS-K648-STATUS  TO RETURN-CODE                  
                           DISPLAY 'ERROR DELETE BP13K648, STATUS = '           
                                                 WS-K648-STATUS                 
                           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT             
                   END-EVALUATE                                                 
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K648-STATUS   TO RETURN-CODE                         
                   DISPLAY 'ERROR READING BP13K648, STATUS = '                  
                                          WS-K648-STATUS                        
                   PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-BP13F410-READ        THRU  2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
                                                                                
           DISPLAY '  ***************************************'.                 
           DISPLAY '            SUMMARY OF BP13C018          '.                 
           DISPLAY '  ***************************************'.                 
           DISPLAY '  TOTAL RECORDS READ FROM BP13F410    : '                   
                      WS-F410-CNT-READ.                                         
           DISPLAY '  TOTAL RECORDS DELETE FROM BP13K648  : '                   
                      WS-K648-CNT-DELETE                                        
           DISPLAY '  ***************************************'.                 
                                                                                
           CLOSE BP13F410                                                       
                 BP13K648.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------- E N D   O F   P R O G R A M --------------*        
