      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB0H.                                                 
      *AUTHOR.        MARGE LAM KO.                                             
      *DATE-WRITTEN.  01 AUG 2017.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM CREATES REPORT BASED ON AG07F630           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  AG07F630 -- VOUCHER TRANSACTION FILE                  *           
      *   2.  BP13K730 -- BP13T.K730.MATURERQ                       *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13LB0H -- REPORT FILE                               *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136964 01/08/2017 MRN1 NEW PROGRAM                        *           
      * BP137246 19/03/2018 MRN1 GET GRANT TYPE FROM DB2            *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F630   ASSIGN       TO AG07F630.                          
                                                                                
           SELECT BP13K730   ASSIGN       TO BP13K730                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K730-NUM-REGN                      
                             FILE STATUS  IS WS-K730-STATUS.                    
                                                                                
           SELECT BP13LB0H   ASSIGN       TO BP13LB0H.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   AG07F630                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  130 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AG07F630.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP13LB0H                                                            
            RECORD CONTAINS   131 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(131).                                   
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-PREV-REGN           PIC X(8)  VALUE SPACES.                       
       01  WS-PREV-DTE-TRANS      PIC X(8)  VALUE SPACES.                       
       01  WS-PREV-NUM-VR         PIC X(11) VALUE SPACES.                       
       01  WS-GRANT-AMT           PIC S9(7)V99 VALUE ZEROES.                    
       01  WS-TOTAL               PIC S9(7)V99 VALUE ZEROES.                    
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-F630-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-PRT-CASES          PIC 9(05) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
       01  WS-F630-EOF            PIC X     VALUE SPACE.                        
       01  WS-K730-STATUS         PIC 9(02) VALUE ZEROES.                       
       01  WS-K730-FND            PIC X     VALUE SPACE.                        
       01  WS-GRANT-ASMNT-FND     PIC X     VALUE SPACE.                        
       01  WS-END-CURSOR1         PIC X(01) VALUE 'N'.                          
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-TME-SYSTEM          PIC X(08).                                    
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-DATE        PIC 9(08).                                    
           05  FILLER             PIC X(13) VALUE SPACES.                       
       01  WS-SQLCODE             PIC ---9.                                     
       01  WS-RETURN-CODE         PIC 9(02) VALUE ZEROES.                       
       01  WS-DTE-CCYYMMDD.                                                     
           05 WS-DTE-CCYY                   PIC X(04) VALUE SPACES.             
           05 WS-DTE-MM                     PIC X(02) VALUE SPACES.             
           05 WS-DTE-DD                     PIC X(02) VALUE SPACES.             
       01  WS-TIME-HHMMSS.                                                      
           05 WS-TIME-HH                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MM                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-SS                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MS                    PIC X(02) VALUE SPACES.             
                                                                                
       01  WS-SQL-STATUS.                                                       
           05 WS-SQL-OK                PIC S9(03) VALUE +000.                   
           05 WS-SQL-NFND              PIC S9(03) VALUE +100.                   
           05 WS-SQL-DUPREC            PIC S9(03) VALUE -803.                   
                                                                                
       01  LB0H-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LB0H'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LB0H-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LB0H-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LB0H-PR-HEAD-02.                                                     
           05  FILLER             PIC X(40) VALUE SPACES.                       
           05  FILLER             PIC X(47) VALUE                               
               'VOUCHER DISTRIBUTION REPORT'.                                   
           05  FILLER             PIC X(33) VALUE SPACES.                       
                                                                                
       01  LB0H-PR-HEAD-03.                                                     
           05  FILLER             PIC X(45)                                     
                VALUE 'PYMT RLS DTE   VOUCHER NO    REGN NUM    GRAN'.          
           05  FILLER             PIC X(45)                                     
                VALUE 'T AMT  GRANT TYPE  HH TYPE  HH CTZ           '.          
                                                                                
       01  LB0H-PR-HEAD-04.                                                     
           05  FILLER             PIC X(45)                                     
                VALUE '------------   ------------  --------    ----'.          
           05  FILLER             PIC X(45)                                     
                VALUE '-----  ----------  -------  ------           '.          
                                                                                
       01  LB0H-PR-DETAILS.                                                     
           05  LB0H-DTE-TRANS     PIC X(10) VALUE SPACES.                       
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  LB0H-VOUCHER-NO    PIC X(13).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB0H-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB0H-AMT           PIC $$$$$$$9.99DB.                            
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  LB0H-GRANT-TYPE    PIC X(04).                                    
           05  FILLER             PIC X(08) VALUE SPACES.                       
           05  LB0H-HH-TYPE       PIC X(01).                                    
           05  FILLER             PIC X(06) VALUE SPACES.                       
           05  LB0H-HH-CTZ        PIC X(03).                                    
                                                                                
       01  LB0H-PR-TOTAL.                                                       
           05  FILLER             PIC X(29) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'TOTAL:  '.                   
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB0H-TOTAL         PIC $$$$$$$9.99DB.                            
                                                                                
       01  LB0H-PR-TOTAL-LINE.                                                  
           05  FILLER             PIC X(38) VALUE SPACES.                       
           05  FILLER             PIC X(12) VALUE '------------'.               
                                                                                
       01  LB0H-PR-BLANK.                                                       
           05  FILLER             PIC X(131) VALUE SPACES.                      
                                                                                
      *------------------------------------------------------------*            
      *  AV02C001 PARAMETERS                                       *            
      *------------------------------------------------------------*            
       COPY AV02COMM.                                                           
                                                                                
      *--------------------------------------------------------------*          
      *                          SQLCA                               *          
      *--------------------------------------------------------------*          
           EXEC SQL INCLUDE SQLCA          END-EXEC.                            
           EXEC SQL INCLUDE P13VGASM       END-EXEC.                            
                                                                                
      *===============================================================          
      *     CURSOR TO EXTRACT GRANT_ASMNT                                       
      *===============================================================          
           EXEC SQL                                                             
              DECLARE CURSOR1 CURSOR FOR                                        
               SELECT NUM_RGSTRN,                                               
                      NUM_GRANT_TYPE,                                           
                      NUM_ELGBL_GRANT,                                          
                      NUM_GRANT_AMOUNT_ELGBL,                                   
                      NUM_GRANT_STATUS,                                         
                      DTE_APRV,                                                 
                      DTE_CANCEL,                                               
                      DTE_REJECT,                                               
                      DTE_SUBMIT,                                               
                      DTE_WTHDRW,                                               
                      NUM_RJCTN_REASON,                                         
                      NUM_WTDRWL_REASON,                                        
                      TXT_CNCLTN_REASON,                                        
                      DTE_GRANT_RQST,                                           
                      NUM_GRANT_DOC_RCV,                                        
                      DTE_ACK,                                                  
                      DTE_GRANT_FORM_ISSUE,                                     
                      DTE_GRANT_OPT_OUT_FORM_ISSUE,                             
                      NUM_GRANT_OPT_OUT_REASON,                                 
                      DTE_GRANT_OPT_OUT_REASON,                                 
                      NUM_ALCTN_SCHEME,                                         
                      NUM_HSHLD_CTZNSP,                                         
                      NUM_WRKNG_CHILD_ASMNT,                                    
                      QTY_MONTH_WORK,                                           
                      NUM_APPEAL_ATCHMT,                                        
                      NUM_PARENT_ADRS_BLDNG,                                    
                      NUM_PARENT_ADRS_LEVEL,                                    
                      NUM_PARENT_ADRS_UNIT,                                     
                      TXT_PARENT_ADRS1,                                         
                      TXT_PARENT_ADRS2,                                         
                      NUM_PARENT_ADRS_POSTAL,                                   
                      DTE_PYMNT,                                                
                      DTE_CPF_ACK_GRANT_RCV,                                    
                      NUM_GRANT_CREDIT,                                         
                      TME_APNTMT,                                               
                      DTE_APNTMT,                                               
                      TME_CREATE_ASMNT,                                         
                      NUM_UPDATE_USERID,                                        
                      TME_UPDATE,                                               
                      DTE_UPDATE,                                               
                      NUM_SOURCE_SYSTEM,                                        
                      NUM_ACNT,                                                 
                      NUM_SCHEME,                                               
                      NUM_TNCY,                                                 
                      NUM_HALF_GRANT                                            
                FROM  GRANT_ASMNT                                               
                WHERE                                                           
                    NUM_RGSTRN = :DCLGRANT-ASMNT.NUM-RGSTRN                     
           END-EXEC.                                                            
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F630    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F630-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  AG07F630                                                 
                       BP13K730                                                 
                OUTPUT BP13LB0H.                                                
                                                                                
           IF WS-K730-STATUS NOT = 0 AND 97                                     
              DISPLAY 'ERROR OPENING - BP13K730 : ' WS-K730-STATUS              
              MOVE WS-K730-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO LB0H-DATE.                                 
                                                                                
           ACCEPT WS-DTE-CCYYMMDD   FROM   DATE YYYYMMDD.                       
           ACCEPT WS-TIME-HHMMSS    FROM   TIME.                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F630.                                                          
      *---------------*                                                         
                                                                                
           READ AG07F630 AT END                                                 
                PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                        
                MOVE WS-TOTAL    TO LB0H-TOTAL                                  
                WRITE PRINT-REC  FROM LB0H-PR-TOTAL-LINE                        
                WRITE PRINT-REC  FROM LB0H-PR-TOTAL                             
                WRITE PRINT-REC  FROM LB0H-PR-TOTAL-LINE                        
                WRITE PRINT-REC  FROM LB0H-PR-BLANK                             
                MOVE HIGH-VALUES TO AG07F630-REC                                
                MOVE 'Y' TO WS-F630-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F630-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF (F630-NUM-REGN = WS-PREV-REGN) OR (WS-PREV-REGN = SPACES)         
              COMPUTE WS-GRANT-AMT = WS-GRANT-AMT + F630-AMT-TRANS              
           ELSE                                                                 
              PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                          
              MOVE F630-AMT-TRANS TO WS-GRANT-AMT                               
           END-IF.                                                              
                                                                                
           IF (F630-DTE-TRANS = WS-PREV-DTE-TRANS) OR                           
              (WS-PREV-REGN = SPACES)                                           
              COMPUTE WS-TOTAL = WS-TOTAL + F630-AMT-TRANS                      
           ELSE                                                                 
              MOVE WS-TOTAL    TO LB0H-TOTAL                                    
              WRITE PRINT-REC  FROM LB0H-PR-TOTAL-LINE                          
              WRITE PRINT-REC  FROM LB0H-PR-TOTAL                               
              WRITE PRINT-REC  FROM LB0H-PR-TOTAL-LINE                          
              WRITE PRINT-REC  FROM LB0H-PR-BLANK                               
              MOVE F630-AMT-TRANS TO WS-TOTAL                                   
           END-IF.                                                              
                                                                                
           MOVE F630-NUM-REGN        TO WS-PREV-REGN.                           
           MOVE F630-DTE-TRANS       TO WS-PREV-DTE-TRANS.                      
           MOVE F630-NUM-VR          TO WS-PREV-NUM-VR.                         
                                                                                
           PERFORM 2000-READ-F630    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13K730.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13K730.                                                       
           IF WS-K730-STATUS = 0                                                
              MOVE 'Y' TO WS-K730-FND                                           
           ELSE                                                                 
              IF WS-K730-STATUS = 23                                            
                 MOVE 'N' TO WS-K730-FND                                        
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K730 : ' WS-K730-STATUS           
                 MOVE WS-K730-STATUS      TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-PRINT-REPORT.                                                       
      *------------------*                                                      
                                                                                
           MOVE SPACES               TO LB0H-PR-DETAILS.                        
                                                                                
           IF CNT-LINE-ON-PAGE > 46                                             
              MOVE 1 TO CNT-LINE-ON-PAGE                                        
              PERFORM 4100-REPORT-HEADING THRU 4100-EXIT                        
           ELSE                                                                 
              ADD 1 TO CNT-LINE-ON-PAGE                                         
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-PRT-CASES.                                              
                                                                                
           MOVE WS-PREV-REGN          TO LB0H-REG-NO.                           
           STRING WS-PREV-NUM-VR(1:3) '-'                                       
                  WS-PREV-NUM-VR(4:2) '-'                                       
                  WS-PREV-NUM-VR(6:6)                                           
              DELIMITED BY SIZE INTO LB0H-VOUCHER-NO.                           
           STRING WS-PREV-DTE-TRANS(7:2) '/'                                    
                  WS-PREV-DTE-TRANS(5:2) '/'                                    
                  WS-PREV-DTE-TRANS(1:4)                                        
              DELIMITED BY SIZE INTO LB0H-DTE-TRANS.                            
                                                                                
           MOVE WS-GRANT-AMT          TO LB0H-AMT.                              
                                                                                
           MOVE SPACES                TO BP13K730-REC.                          
           MOVE WS-PREV-REGN          TO K730-KEY-FLD.                          
           PERFORM 3200-READ-BP13K730 THRU 3200-EXIT.                           
           IF WS-K730-FND = 'Y'                                                 
              MOVE K730-CDE-HOUSEHOLD TO LB0H-HH-TYPE                           
           END-IF.                                                              
                                                                                
           MOVE WS-PREV-REGN      TO NUM-RGSTRN OF DCLGRANT-ASMNT.              
           PERFORM 4200-READ-GRANT-ASMNT THRU 4200-EXIT.                        
           IF WS-GRANT-ASMNT-FND = 'Y'                                          
              MOVE NUM-HSHLD-CTZNSP OF DCLGRANT-ASMNT TO LB0H-HH-CTZ            
              MOVE NUM-GRANT-TYPE   OF DCLGRANT-ASMNT TO LB0H-GRANT-TYPE        
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC          FROM LB0H-PR-DETAILS.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4100-REPORT-HEADING.                                                     
      *--------------------*                                                    
                                                                                
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LB0H-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
      *     WRITE PRINT-REC FROM LB0H-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LB0H-PR-HEAD-02.                               
            WRITE PRINT-REC FROM LB0H-PR-BLANK.                                 
            WRITE PRINT-REC FROM LB0H-PR-HEAD-03 AFTER 3.                       
            WRITE PRINT-REC FROM LB0H-PR-HEAD-04.                               
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4200-READ-GRANT-ASMNT.                                                   
      *--------------------*                                                    
           MOVE 'N' TO WS-GRANT-ASMNT-FND.                                      
                                                                                
           PERFORM 4300-OPEN-CURSOR1  THRU 4300-EXIT.                           
           MOVE 'N'                   TO WS-END-CURSOR1.                        
           PERFORM 4400-FETCH-CURSOR1 THRU 4400-EXIT                            
                 UNTIL WS-END-CURSOR1 = 'Y' OR WS-GRANT-ASMNT-FND = 'Y'         
           PERFORM 4500-CLOSE-CURSOR1 THRU 4500-EXIT.                           
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       4300-OPEN-CURSOR1.                                                       
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                OPEN CURSOR1                                                    
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       4400-FETCH-CURSOR1.                                                      
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                FETCH CURSOR1                                                   
                INTO  :DCLGRANT-ASMNT.NUM-RGSTRN,                               
                      :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                           
                      :DCLGRANT-ASMNT.NUM-ELGBL-GRANT,                          
                      :DCLGRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,                   
                      :DCLGRANT-ASMNT.NUM-GRANT-STATUS,                         
                      :DCLGRANT-ASMNT.DTE-APRV,                                 
                      :DCLGRANT-ASMNT.DTE-CANCEL,                               
                      :DCLGRANT-ASMNT.DTE-REJECT,                               
                      :DCLGRANT-ASMNT.DTE-SUBMIT,                               
                      :DCLGRANT-ASMNT.DTE-WTHDRW,                               
                      :DCLGRANT-ASMNT.NUM-RJCTN-REASON,                         
                      :DCLGRANT-ASMNT.NUM-WTDRWL-REASON,                        
                      :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,                        
                      :DCLGRANT-ASMNT.DTE-GRANT-RQST,                           
                      :DCLGRANT-ASMNT.NUM-GRANT-DOC-RCV,                        
                      :DCLGRANT-ASMNT.DTE-ACK,                                  
                      :DCLGRANT-ASMNT.DTE-GRANT-FORM-ISSUE,                     
                      :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,             
                      :DCLGRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,                 
                      :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,                 
                      :DCLGRANT-ASMNT.NUM-ALCTN-SCHEME,                         
                      :DCLGRANT-ASMNT.NUM-HSHLD-CTZNSP,                         
                      :DCLGRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,                    
                      :DCLGRANT-ASMNT.QTY-MONTH-WORK,                           
                      :DCLGRANT-ASMNT.NUM-APPEAL-ATCHMT,                        
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,                    
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,                    
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-UNIT,                     
                      :DCLGRANT-ASMNT.TXT-PARENT-ADRS1,                         
                      :DCLGRANT-ASMNT.TXT-PARENT-ADRS2,                         
                      :DCLGRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,                   
                      :DCLGRANT-ASMNT.DTE-PYMNT,                                
                      :DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,                    
                      :DCLGRANT-ASMNT.NUM-GRANT-CREDIT,                         
                      :DCLGRANT-ASMNT.TME-APNTMT,                               
                      :DCLGRANT-ASMNT.DTE-APNTMT,                               
                      :DCLGRANT-ASMNT.TME-CREATE-ASMNT,                         
                      :DCLGRANT-ASMNT.NUM-UPDATE-USERID,                        
                      :DCLGRANT-ASMNT.TME-UPDATE,                               
                      :DCLGRANT-ASMNT.DTE-UPDATE,                               
                      :DCLGRANT-ASMNT.NUM-SOURCE-SYSTEM,                        
                      :DCLGRANT-ASMNT.NUM-ACNT,                                 
                      :DCLGRANT-ASMNT.NUM-SCHEME,                               
                      :DCLGRANT-ASMNT.NUM-TNCY,                                 
                      :DCLGRANT-ASMNT.NUM-HALF-GRANT                            
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                MOVE 'Y' TO WS-GRANT-ASMNT-FND                                  
           WHEN 100                                                             
                MOVE 'Y'  TO WS-END-CURSOR1                                     
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       4500-CLOSE-CURSOR1.                                                      
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                CLOSE CURSOR1                                                   
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB0H *************'.                            
           DISPLAY 'NO OF RECS READ FROM  AG07F630  : ' CNT-F630-READ.          
           DISPLAY 'NO OF RECS WRITTEN TO BP13LB0H  : ' CNT-PRT-CASES.          
                                                                                
           CLOSE AG07F630                                                       
                 BP13K730                                                       
                 BP13LB0H.                                                      
                                                                                
           IF WS-K730-STATUS NOT = 0 AND 97                                     
              DISPLAY ' CLOSING ERROR, WS-K730-STATUS ' WS-K730-STATUS          
              MOVE WS-K730-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       9999-SQL-ERROR.                                                          
      *----------------------------------------------------------------         
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL ROLLBACK               END-EXEC.                            
           MOVE 99                         TO    WS-RETURN-CODE.                
           PERFORM 9000-CLOSE-FILES        THRU  9000-EXIT.                     
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
