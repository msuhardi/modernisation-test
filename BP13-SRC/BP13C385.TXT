      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C385.                                                 
       AUTHOR.        LOU J L.                                                  
       DATE-WRITTEN.  22/01/96.                                                 
                                                                                
      ******************************************************************        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS A PROGRAM TO CREATE THE INTERFACE *        
      *                      FILE SYSTEM FOR PRINTING PURPOSE.         *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F310  - TRANSACTION FILE              *        
                         2.  BP13K800  -  SOC APPLICATION MASTER       *        
      *                  5.  BM06K100  -  BLOCK FILE                   *        
      *                  6.  BM06K110  -  FLATINV FILE                 *        
      *   OUTPUT FILE :  1.  BP13F311  - TRANSACTION FILE FOR CLEAR    *        
      *                                  FILE SYSTEM                   *        
      *   LISTINGS    :  2.  ERROR LIST     -   BP13L31A               *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      * BP130068    LJL   220196   TO CREATE INTERFACE FILE FOR CLEAR  *        
      *                            FILE SYSTEM. (CREATED)              *        
      * BP130055    LJL   170596   TO CATER FOR THE NEW OCS PANELS     *        
      *                            (ADD NUM-DOOR).                     *        
      * BP130356    SGK   040897   TO INCLUDE GRANT IN SC/CC COMP.     *        
      * BP130418    SIA   980309   CHGE BM06K211 TO BM06K110,          *        
      *                            CHGE BM06K210 TO BM06K100           *        
      * BP130620    LSB   990109   TO PROVIDE THE EXTN OF SC/SS REBATES*        
      *                            & SC GRANT                          *        
      * BP130636    JMS   990305   TO CATER FOR TC RECOVERABLE AMOUNT  *        
      * BP130790    SCK   000202   TO ADD IN IN-SOURCE DOCUMENTATION   *        
      * BP130814    JMS   220200   TO GET NUM-DOOR FOR 2 & 3 ROOM      *        
      * BP130885    RASM  240600   TO CALL BP13C903.                   *        
      *                            REMOVE PASSING OF 2ND MTH'S SCCC.   *        
      * BP130883    RASM  070800   TO TAKE PIDB FIELDS FROM BP13F310.  *        
      * BP132207    MALC  230902   TO CATER FOR CHANGE OF GST          *        
      * BP13XXXX    PCL4  171012   TO CATER FOR CHANGE OF GST          *        
      * BP135127    ESA1  081113   TO REPLACE BP13K275 WITH BP13K277   *        
      * BP135177    ESA1  201213   TO CATER FOR OCS LAYOUT             *        
      * BP139211    KAC1  020123   TO IMPLEMENT 8% GST                 *        
      *                               REPLACED BP13C90C BY AV02C025    *        
      * BP139211    KAC1  030123   TO CALL WS-BP13C903 INSTEAD OF      *        
      *                            BP13C903.                                    
      * BP139485    KAC1  020124   TO IMPLEMENT 9% GST INSTEAD OF      *        
      *                               REPLACED BP13C90C BY AV02C025             
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      ************************                                                  
       INPUT-OUTPUT SECTION.                                                    
      ************************                                                  
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
           SELECT BP13F311  ASSIGN        TO BP13F311.                          
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
           SELECT BP13K277  ASSIGN        TO BP13K277                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K277-KEY-FLD                       
                            FILE STATUS   IS BP13K277-STATUS.                   
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS BM06K100-STATUS.                   
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
           SELECT BP13L31A  ASSIGN        TO BP13L31A.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F310.                                                           
                                                                                
       FD   BP13F311                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F311.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K277                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K277.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L31A                                                             
           RECORDING MODE   IS  F                                               
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  PRINT-REC-1                  PIC X(132).                             
                                                                                
                                                                                
      **************************                                                
       WORKING-STORAGE SECTION.                                                 
      **************************                                                
                                                                                
       01  BP13K800-STATUS              PIC 99       VALUE 0.                   
       01  BP13K277-STATUS              PIC 99       VALUE 0.                   
       01  BM06K100-STATUS              PIC 99       VALUE 0.                   
       01  BM06K110-STATUS              PIC 99       VALUE 0.                   
       01  CNT-F310-READ                PIC 9(3)     VALUE 0.                   
       01  CNT-K800-READ                PIC 9(3)     VALUE 0.                   
       01  CNT-K277-READ                PIC 9(3)     VALUE 0.                   
       01  CNT-K100-READ                PIC 9(3)     VALUE 0.                   
       01  CNT-K110-READ                PIC 9(3)     VALUE 0.                   
       01  CNT-L310A-WRITE              PIC 9(3)     VALUE 0.                   
       01  CNT-LINE-ON-PAGE             PIC 9(3)     VALUE 66.                  
       01  CNT-PRINT-L31A               PIC 9(3)     VALUE 1.                   
       01  NO-OF-PAGE                   PIC 9(3)     VALUE 0.                   
       01  WS-NAME-APPL                 PIC X(66)    VALUE SPACES.              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F310-EOF              PIC X(1)  VALUE 'N'.                    
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-CC-CURR               PIC 9(2)  VALUE 00.                     
           05  WS-YY-CURR               PIC 9(2)  VALUE 00.                     
           05  WS-MM-CURR               PIC 9(2)  VALUE 00.                     
           05  WS-DD-CURR               PIC 9(2)  VALUE 00.                     
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD             PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X     VALUE '/'.                    
           05  WS-EDITED-MM             PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X     VALUE '/'.                    
           05  WS-EDITED-CC             PIC X(2)  VALUE SPACES.                 
           05  WS-EDITED-YY             PIC X(2)  VALUE SPACES.                 
                                                                                
       01  WS-REMARKS                   PIC X(40).                              
                                                                                
       01  WS-BP13C903               PIC X(8)  VALUE 'BP13C903'.                
       01  WS-S903-DATE-EFF-CURR.                                               
           05  WS-S903-EFF-CURR-CCYY.                                           
               10  WS-S903-EFF-CURR-CC  PIC X(2)  VALUE SPACES.                 
               10  WS-S903-EFF-CURR-YY  PIC X(2)  VALUE SPACES.                 
           05  WS-S903-EFF-CURR-MM      PIC X(2)  VALUE SPACES.                 
           05  WS-S903-EFF-CURR-DD      PIC X(2)  VALUE SPACES.                 
                                                                                
      *-----------------------------------------------------------*             
      *    WS FOR ERROR LISTING                                   *             
      *-----------------------------------------------------------*             
       01  L31A-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L385'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  PRINT-DATE-1             PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  PRINT-PAGE               PIC ZZZ9.                               
                                                                                
       01  L31A-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(35) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE                         
                'REPORT ON ERROR LISTING FOR THE PRINTING OF SALES AGREE        
      -    'MENT LETTER'.                                                       
                                                                                
       01  L31A-PR-HEAD-03.                                                     
           05  FILLER                   PIC X(10) VALUE ' S-NO'.                
           05  FILLER                   PIC X(12) VALUE 'CURRENT-NO'.           
           05  FILLER                   PIC X(20) VALUE '   NRIC-NO'.           
           05  FILLER                   PIC X(15) VALUE SPACES.                 
           05  FILLER                   PIC X(18) VALUE                         
               'SCHEME-ACCT-NO    '.                                            
           05  FILLER                   PIC X(16) VALUE SPACES.                 
           05  FILLER                   PIC X(25) VALUE ' REMARKS'.             
           05  FILLER                   PIC X(16) VALUE SPACES.                 
                                                                                
       01  L31A-PR-DETAILS.                                                     
           05  L31A-NUMBER              PIC ZZZ9.                               
           05  FILLER                   PIC X(7)  VALUE SPACES.                 
           05  L31A-REGN-NO             PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  L31A-NRIC-NO             PIC X(9)  VALUE SPACES.                 
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(14) VALUE SPACES.                 
           05  L31A-SCH-ACCT            PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(15) VALUE SPACES.                 
           05  L31A-REMARKS             PIC X(40) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABELS PASSED  TO SUB-PROGRAM "BP13C903"         *             
      *-----------------------------------------------------------*             
       01  WS-LINK-S903.                                                        
           05  WS-S903-NUM-SCH-ACC         PIC X(9).                            
           05  WS-S903-SO-EFF-DATE.                                             
               10  WS-S903-SO-EFF-CC       PIC 9(2).                            
               10  WS-S903-SO-EFF-YYMMDD   PIC 9(6).                            
           05  WS-S903-DESIGN-TY           PIC X(3).                            
           05  WS-S903-ROOM-TY             PIC X(2).                            
           05  WS-S903-SC-CC1              PIC 9(6)V99.                         
           05  WS-S903-SC-CC2              PIC 9(6)V99.                         
           05  WS-S903-SC-CC3              PIC 9(6)V99.                         
           05  WS-S903-SC-TOTAL-AMT        PIC 9(7)V99.                         
           05  WS-S903-SC-REBATE1          PIC 9(4)V99.                         
           05  WS-S903-SC-REBATE2          PIC 9(4)V99.                         
           05  WS-S903-SC-REBATE3          PIC 9(4)V99.                         
           05  WS-S903-SC-GRANT1           PIC 9(4)V99.                         
           05  WS-S903-SC-GRANT2           PIC 9(4)V99.                         
           05  WS-S903-SC-GRANT3           PIC 9(4)V99.                         
           05  WS-S903-SC-REC-TC1          PIC 9(4)V99.                         
           05  WS-S903-SC-REC-TC2          PIC 9(4)V99.                         
           05  WS-S903-SC-REC-TC3          PIC 9(4)V99.                         
           05  WS-S903-SC-REB-SP1          PIC 9(4)V99.                         
           05  WS-S903-SC-REB-SP2          PIC 9(4)V99.                         
           05  WS-S903-SC-REB-SP3          PIC 9(4)V99.                         
           05  WS-S903-SC-CURR-EFF-DATE.                                        
               10  WS-S903-SC-CURR-EFF-CC  PIC X(2).                            
               10  WS-S903-SC-CURR-EFF-YY  PIC X(2).                            
               10  FILLER                  PIC X(1).                            
               10  WS-S903-SC-CURR-EFF-MM  PIC X(2).                            
               10  FILLER                  PIC X(1).                            
               10  WS-S903-SC-CURR-EFF-DD  PIC X(2).                            
           05  WS-S903-SC-NEXT-EFF-DATE.                                        
               10  WS-S903-SC-NEXT-EFF-CC  PIC X(2).                            
               10  WS-S903-SC-NEXT-EFF-YY  PIC X(2).                            
               10  FILLER                  PIC X(1).                            
               10  WS-S903-SC-NEXT-EFF-MM  PIC X(2).                            
               10  FILLER                  PIC X(1).                            
               10  WS-S903-SC-NEXT-EFF-DD  PIC X(2).                            
           05  WS-S903-RET-CODE            PIC X(2).                            
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABELS PASSED  TO SUB-PROGRAM "AV02C021"         *             
      *-----------------------------------------------------------*             
       01  C025-AREA.                                                           
           05  C025-DTE-EFF.                                                    
               10  C025-DTE-CCYY          PIC 9(4).                             
               10  C025-DTE-MM            PIC 9(2).                             
               10  C025-DTE-DD            PIC 9(2).                             
           05  FILLER                     PIC X(34).                            
           05  C025-NUM-GST-RATE          PIC 9(2)V99.                          
           05  C025-NUM-ERROR             PIC X(4).                             
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABELS PASSED  TO SUB-PROGRAM "AV02C021"         *             
      *-----------------------------------------------------------*             
       COPY P13GST01.                                                           
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ******************************************************************        
       100-MAIN-ROUTINE.                                                        
      ******************************************************************        
           PERFORM  200-OPEN-FILES        THRU    200-EXIT.                     
           PERFORM  250-MOVE-GST-RATE     THRU    250-EXIT.                     
           PERFORM  300-READ-BP13F310     THRU    300-EXIT                      
           PERFORM  310-READ-INPUT        THRU    310-EXIT                      
                        UNTIL WS-F310-EOF = 'Y'.                                
           PERFORM  1000-CLOSE-ROUTINE    THRU    1000-EXIT.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BP13K277                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                OUTPUT BP13F311                                                 
                       BP13L31A.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K800-STATUS ' BP13K800-STATUS        
               MOVE     BP13K800-STATUS TO RETURN-CODE                          
               PERFORM  1000-CLOSE-ROUTINE.                                     
           IF BP13K277-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K277-STATUS ' BP13K277-STATUS        
               MOVE     BP13K277-STATUS TO RETURN-CODE                          
               PERFORM  1000-CLOSE-ROUTINE.                                     
           IF BM06K100-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BM06K100-STATUS ' BM06K100-STATUS        
               MOVE     BM06K100-STATUS TO RETURN-CODE                          
               PERFORM  1000-CLOSE-ROUTINE.                                     
           IF BM06K110-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BM06K110-STATUS ' BM06K110-STATUS        
               MOVE     BM06K110-STATUS TO RETURN-CODE                          
               PERFORM  1000-CLOSE-ROUTINE.                                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       250-MOVE-GST-RATE.                                                       
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    CALL AV02C025 TO GET GST-RATE                          *             
      *-----------------------------------------------------------*             
           MOVE SPACES                     TO C025-AREA.                        
           INITIALIZE                         C025-AREA.                        
           MOVE FUNCTION CURRENT-DATE(1:8) TO C025-DTE-EFF.                     
           CALL 'AV02C025'           USING C025-AREA.                           
                                                                                
           MOVE C025-NUM-GST-RATE     TO WS-NUM-GST-RATE.                       
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-READ-BP13F310.                                                       
      ******************************************************************        
           READ BP13F310                                                        
                AT END MOVE 'Y'  TO  WS-F310-EOF                                
                       GO TO 300-EXIT.                                          
           ADD  1  TO CNT-F310-READ.                                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       310-READ-INPUT.                                                          
      ******************************************************************        
      *--------------------------------------------------------*                
      *    TO GET INPUT FOR PROCESSING                         *                
      *--------------------------------------------------------*                
            PERFORM 500-READ-K800      THRU  500-EXIT.                          
            PERFORM 300-READ-BP13F310  THRU  300-EXIT.                          
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-K800.                                                           
      ******************************************************************        
           MOVE F310-REGN-NO  TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00                                              
              IF K800-NUM-ALLOC-TAG = 'RI' OR 'RW' OR 'RS' OR 'RA'              
                 GO TO 500-EXIT                                                 
              ELSE                                                              
                 INITIALIZE    BP13F311-TRANS-REC                               
                 MOVE     F310-REGN-NO         TO F311-REGN-NO                  
                 MOVE     F310-CDE-TRANS       TO F311-CDE-TRANS                
                 MOVE     K800-NUM-SCH-ACC     TO   F311-SCH-ACC                
                 PERFORM  600-READ-K110        THRU 600-EXIT                    
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 DISPLAY 'K800 RECORD NOT FOUND, K800-NUM-REGN = '              
                          K800-NUM-REGN                                         
                 MOVE    'RECORD NOT FOUND IN SOC MASTER' TO WS-REMARKS         
                 PERFORM  950-WRITE-ERROR-LIST THRU 950-EXIT                    
              ELSE                                                              
                 DISPLAY 'ERROR READING, BP13K800 = ' BP13K800-STATUS           
                 MOVE     BP13K800-STATUS TO RETURN-CODE                        
                 PERFORM  1000-CLOSE-ROUTINE.                                   
                                                                                
           ADD 1 TO CNT-K800-READ.                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-READ-K110.                                                           
      ******************************************************************        
      *--------------------------------------------------------*                
      *    READ K110 FLAT INVENTORY TO GET SELLING PRICE       *                
      *--------------------------------------------------------*                
           MOVE K800-NUM-SCH-ACC    TO     K110-KEY-FLD                         
                                                                                
           READ BM06K110.                                                       
           IF BM06K110-STATUS = 00                                              
              DISPLAY ' DTE-KEY-AVAILABLE =   ' K110-DTE-KEY-AVAIL              
              IF K110-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROS             
                                      OR '00000000'                             
              CONTINUE                                                          
              ELSE                                                              
                 IF F310-CDE-TRANS = 'C0'                                       
                    CONTINUE                                                    
                 ELSE                                                           
                   ADD 1 TO CNT-K110-READ                                       
                   GO TO   600-EXIT                                             
                 END-IF                                                         
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                    DISPLAY 'BM0K110 RECORD NOT FOUND. SCH/ACC NUM = '          
                             K800-NUM-SCH-ACC                                   
                    MOVE 'RECORD NOT FOUND IN FIS' TO WS-REMARKS                
                    PERFORM 950-WRITE-ERROR-LIST THRU 950-EXIT                  
              ELSE                                                              
                    DISPLAY 'ERROR READING, BM06K110 = ' BM06K110-STATUS        
                    MOVE  BM06K110-STATUS TO RETURN-CODE                        
                    PERFORM 1000-CLOSE-ROUTINE                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS = 00                                              
              MOVE K800-NUM-ALLO-SCHEME TO F311-NUM-ALLOC-SCHEME                
              PERFORM 800-READ-BM06K100 THRU  800-EXIT                          
              PERFORM 850-COMPUTE-SCCC  THRU  850-EXIT                          
              PERFORM 900-PRINT-LETTER  THRU  900-EXIT                          
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 DISPLAY 'BM0K110 RECORD NOT FOUND. SCH/ACC NUM = '             
                          K800-NUM-SCH-ACC                                      
                 MOVE 'RECORD NOT FOUND IN FIS' TO WS-REMARKS                   
                 PERFORM 950-WRITE-ERROR-LIST THRU 950-EXIT                     
              ELSE                                                              
                 DISPLAY 'ERROR READING, BM06K110 = ' BM06K110-STATUS           
                 MOVE     BM06K110-STATUS TO RETURN-CODE                        
                 PERFORM  1000-CLOSE-ROUTINE.                                   
                                                                                
           ADD 1 TO CNT-K110-READ.                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-READ-BM06K100.                                                       
      ******************************************************************        
           MOVE K110-AIX1 TO K100-KEY-FLD.                                      
                                                                                
           READ BM06K100.                                                       
                                                                                
           IF BM06K100-STATUS = 0                                               
              ADD 1 TO CNT-K100-READ                                            
           ELSE                                                                 
              IF BM06K100-STATUS = 23                                           
                 DISPLAY 'REC NOT FOUND IN BM06K100 = ' K100-KEY-FLD            
                 MOVE    'RECORD NOT FOUND IN K100' TO  WS-REMARKS              
                 PERFORM  950-WRITE-ERROR-LIST THRU 950-EXIT                    
                 GO TO    800-EXIT                                              
              ELSE                                                              
                 DISPLAY 'READ ERROR, BM06K100-STATUS ' BM06K100-STATUS         
                 MOVE     BM06K100-STATUS TO RETURN-CODE                        
                 PERFORM  1000-CLOSE-ROUTINE.                                   
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DTE-CURRENT.                        
                                                                                
           IF K100-DTE-OCS-DUE IS NOT NUMERIC                                   
              MOVE  ZEROS  TO   F311-DTE-OCS-DUE                                
              GO TO 800-EXIT.                                                   
                                                                                
           MOVE K100-DTE-OCS-DUE     TO F311-DTE-OCS-DUE.                       
                                                                                
           IF K100-NUM-OCS-TYPE = SPACES OR LOW-VALUES                          
              MOVE SPACES            TO F311-CDE-OCS-TYPE                       
           ELSE                                                                 
              MOVE K100-NUM-OCS-TYPE TO F311-CDE-OCS-TYPE                       
           END-IF.                                                              
                                                                                
           IF K100-NUM-OCS-TYPE = 'D'                                           
              PERFORM 830-GET-NUM-DOORS THRU 830-EXIT.                          
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       830-GET-NUM-DOORS.                                                       
      ******************************************************************        
           MOVE SPACES            TO K277-KEY-FLD.                              
           MOVE K100-KEY-FLD      TO K277-KEY-FLD.                              
           MOVE K110-NUM-FEATURE  TO K277-NUM-FEATURE.                          
           MOVE K110-NUM-LAYOUT   TO K277-NUM-LAYOUT.                           
           READ BP13K277.                                                       
                                                                                
           IF BP13K277-STATUS = 00                                              
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K277-STATUS = 23                                           
                 DISPLAY 'K277 RECORD NOT FOUND, K800-NUM-REGN = '              
                          K100-KEY-FLD                                          
                 MOVE    'RECORD NOT FOUND IN OCS FILE  ' TO WS-REMARKS         
                 PERFORM  950-WRITE-ERROR-LIST THRU 950-EXIT                    
              ELSE                                                              
                 DISPLAY 'ERROR READING, BP13K277 = ' BP13K277-STATUS           
                 MOVE     BP13K277-STATUS TO RETURN-CODE                        
                 PERFORM  1000-CLOSE-ROUTINE.                                   
                                                                                
           EVALUATE F310-CDE-TYP-ACTUSE                                         
           WHEN '02'                                                            
             MOVE K277-NUM-DOOR-2RM    TO F311-NUM-DOOR                         
           WHEN '03'                                                            
             MOVE K277-NUM-DOOR-3RM    TO F311-NUM-DOOR                         
           WHEN '04'                                                            
             MOVE K277-NUM-DOOR-4RM    TO F311-NUM-DOOR                         
           WHEN '05'                                                            
             MOVE K277-NUM-DOOR-5RM    TO F311-NUM-DOOR                         
           WHEN '06'                                                            
             MOVE K277-NUM-DOOR-EXEC   TO F311-NUM-DOOR                         
           WHEN OTHER                                                           
             MOVE SPACES               TO F311-NUM-DOOR                         
           END-EVALUATE.                                                        
                                                                                
       830-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       850-COMPUTE-SCCC.                                                        
      ******************************************************************        
           MOVE FUNCTION CURRENT-DATE  TO  WS-DTE-CURRENT.                      
           MOVE   WS-DTE-CURRENT       TO   WS-S903-SO-EFF-DATE.                
           MOVE   ZEROES               TO   WS-S903-SC-CC1                      
                                            WS-S903-SC-CC2                      
                                            WS-S903-SC-CC3.                     
           MOVE   ZEROES               TO   WS-S903-SC-REC-TC1                  
                                            WS-S903-SC-REC-TC2                  
                                            WS-S903-SC-REC-TC3                  
           MOVE   ZEROES               TO   WS-S903-SC-REBATE1                  
                                            WS-S903-SC-REBATE2                  
                                            WS-S903-SC-REBATE3.                 
           MOVE   ZEROES               TO   WS-S903-SC-REB-SP1                  
                                            WS-S903-SC-REB-SP2                  
                                            WS-S903-SC-REB-SP3.                 
           MOVE   ZEROES               TO   WS-S903-SC-GRANT1                   
                                            WS-S903-SC-GRANT2                   
                                            WS-S903-SC-GRANT3.                  
           MOVE   K110-SCH-ACC-NO(1:9) TO   WS-S903-NUM-SCH-ACC.                
           MOVE   F310-CDE-TYP-ACTUSE  TO   WS-S903-ROOM-TY.                    
           MOVE   F310-CDE-MODL-DESG   TO   WS-S903-DESIGN-TY.                  
                                                                                
      *--------------------------------------------------------*                
      *    CALL "BP13C903" TO GET THE SC-CC1 VALUE             *                
      *--------------------------------------------------------*                
           CALL WS-BP13C903 USING WS-LINK-S903.                                 
                                                                                
           MOVE WS-S903-SC-CC1            TO  F311-AMT-SC-CC1.                  
           MOVE WS-S903-SC-REC-TC1        TO  F311-AMT-SC-REC-TC1.              
           MOVE WS-S903-SC-REBATE1        TO  F311-AMT-SC-REBATE1.              
           MOVE WS-S903-SC-REB-SP1        TO  F311-AMT-SC-REB-SP1.              
           MOVE WS-S903-SC-GRANT1         TO  F311-AMT-SC-GRANT1.               
                                                                                
      *--------------------------------------------------------*                
      *    COMPUTE THE GST FOR SC-CC1 AND REC-TC1.             *                
      *--------------------------------------------------------*                
           MOVE  WS-S903-SC-CC1           TO   WS-AMT-NETT.                     
           CALL 'AV02C021' USING WS-COMM-AREA-GST.                              
           MOVE WS-AMT-GST                TO   F311-AMT-SC-CC1-GST              
                                                                                
           MOVE  WS-S903-SC-REC-TC1       TO   WS-AMT-NETT.                     
           CALL 'AV02C021' USING WS-COMM-AREA-GST.                              
           MOVE WS-AMT-GST                TO   F311-AMT-SC-TC1-GST.             
                                                                                
           MOVE WS-S903-SC-CURR-EFF-CC   TO    WS-S903-EFF-CURR-CC.             
           MOVE WS-S903-SC-CURR-EFF-YY   TO    WS-S903-EFF-CURR-YY.             
           MOVE WS-S903-SC-CURR-EFF-MM   TO    WS-S903-EFF-CURR-MM.             
           MOVE WS-S903-SC-CURR-EFF-DD   TO    WS-S903-EFF-CURR-DD.             
           MOVE WS-S903-DATE-EFF-CURR    TO  F311-DTE-SC-CC-EFFECTIVE1.         
                                                                                
           MOVE K800-NUM-IDP-SCHEME      TO F311-NUM-IDP-SCHEME.                
           MOVE K800-NUM-RESIDENT-SCHEME TO F311-NUM-RESIDENT-SCHEME.           
           MOVE K800-NUM-HOUSEHOLD       TO F311-NUM-HOUSEHOLD.                 
           MOVE K800-NUM-ELIG-SCHEME     TO F311-NUM-ELIG-SCHEME.               
           MOVE K800-NUM-ZONE            TO F311-NUM-ZONE.                      
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-PRINT-LETTER.                                                        
      ******************************************************************        
           MOVE    K800-NUM-NEW-TOWN    TO    F311-CDE-NT.                      
           MOVE    K800-NUM-FLAT-TYPE   TO    F311-CDE-FLAT-TYPE.               
           MOVE    K800-AMT-SOC-DEBTOR  TO    F311-AMT-SOC-DEBTOR.              
           MOVE    K800-NUM-CA-TAG      TO    F311-NUM-CA-TAG.                  
           MOVE    K800-NUM-CAP         TO    F311-NUM-PPOS.                    
           MOVE    K800-NUM-SPR-TAG     TO    F311-NUM-SPR.                     
           MOVE    K800-DTE-EXPIRED-TP  TO    F311-DTE-EXP-TP.                  
           MOVE    SPACES               TO    F311-NUM-INTEREST-RATE.           
           MOVE    'BP13C385'           TO    F311-USERID.                      
           PERFORM  910-GET-FLAT-INFO   THRU  910-EXIT.                         
                                                                                
           WRITE BP13F311-TRANS-REC.                                            
           ADD 1 TO CNT-L310A-WRITE.                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       910-GET-FLAT-INFO.                                                       
      ******************************************************************        
           MOVE F310-NUM-BLK          TO F311-NUM-BLK.                          
           MOVE F310-NME-STREET       TO F311-NME-STREET.                       
           MOVE F310-NUM-LEVEL        TO F311-NUM-LEVEL.                        
           MOVE F310-NUM-MAIN         TO F311-NUM-MAIN.                         
           MOVE F310-NUM-POSTAL       TO F311-NUM-POSTAL.                       
           MOVE F310-CDE-CLS-DESG     TO F311-CDE-CLS-DESG.                     
           MOVE F310-CDE-TYP-DESG     TO F311-CDE-TYP-DESG.                     
           MOVE F310-CDE-MODL-DESG    TO F311-CDE-MODL-DESG.                    
           MOVE F310-CDE-CONFIG-DESG  TO F311-CDE-CONFIG-DESG.                  
           MOVE F310-CDE-CLS-ACTUSE   TO F311-CDE-CLS-ACTUSE.                   
           MOVE F310-CDE-TYP-ACTUSE   TO F311-CDE-TYP-ACTUSE.                   
                                                                                
       910-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       950-WRITE-ERROR-LIST.                                                    
      ******************************************************************        
      *--------------------------------------------------------*                
      *    PRINT ERROR LIST DETAIL                             *                
      *--------------------------------------------------------*                
           IF CNT-LINE-ON-PAGE > 46                                             
              PERFORM 960-PRINT-HEADING  THROUGH  960-EXIT                      
              MOVE  1  TO CNT-LINE-ON-PAGE.                                     
                                                                                
           MOVE  SPACES            TO    PRINT-REC-1.                           
           MOVE  CNT-PRINT-L31A    TO    L31A-NUMBER.                           
           MOVE  F310-REGN-NO      TO    L31A-REGN-NO.                          
           MOVE  F310-SCH-ACC      TO    L31A-SCH-ACCT.                         
           MOVE  WS-REMARKS        TO    L31A-REMARKS.                          
           WRITE PRINT-REC-1       FROM  L31A-PR-DETAILS AFTER 1.               
                                                                                
           ADD   1                 TO    CNT-LINE-ON-PAGE.                      
           ADD   1                 TO    CNT-PRINT-L31A.                        
           MOVE  SPACES            TO    L31A-PR-DETAILS.                       
                                                                                
       950-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       960-PRINT-HEADING.                                                       
      ******************************************************************        
      *--------------------------------------------------------*                
      *    PRINT CONTROL LIST HEADING LINE                     *                
      *--------------------------------------------------------*                
           ADD     1                  TO     NO-OF-PAGE.                        
           MOVE    NO-OF-PAGE         TO     PRINT-PAGE.                        
           MOVE FUNCTION CURRENT-DATE TO  WS-DTE-CURRENT.                       
           MOVE    WS-CC-CURR         TO     WS-EDITED-CC.                      
           MOVE    WS-YY-CURR         TO     WS-EDITED-YY.                      
           MOVE    WS-MM-CURR         TO     WS-EDITED-MM.                      
           MOVE    WS-DD-CURR         TO     WS-EDITED-DD.                      
           MOVE    WS-EDITED-DATE     TO     PRINT-DATE-1.                      
           MOVE    SPACES             TO     PRINT-REC-1.                       
           WRITE   PRINT-REC-1        AFTER  PAGE.                              
           WRITE   PRINT-REC-1        FROM   L31A-PR-HEAD-01 AFTER 4.           
           WRITE   PRINT-REC-1        FROM   L31A-PR-HEAD-02 AFTER 2.           
           WRITE   PRINT-REC-1        FROM   L31A-PR-HEAD-03 AFTER 4.           
                                                                                
       960-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K277                                                       
                 BM06K100                                                       
                 BM06K110                                                       
                 BP13F311                                                       
                 BP13L31A.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS.        
           IF BM06K100-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K100-STATUS ' BM06K100-STATUS.        
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS.        
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C385 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM DAILY TRANS   (BP13F310) = '        
                    CNT-F310-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM SOC MASTER    (BP13K800) = '        
                    CNT-K800-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM BLOCK SUMMARY (BM06K100) = '        
                    CNT-K100-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM FIS (BM06K110) = '                  
                    CNT-K110-READ.                                              
           DISPLAY 'NO OF LETTERS WRITTEN (BP13L310)(F311) = '                  
                    CNT-L310A-WRITE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
