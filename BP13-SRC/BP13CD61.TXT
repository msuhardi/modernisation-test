       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD61.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  23/03/2015.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES  : READ CDB SUBROUTINE (BE01) & WRITE RECORDS  *            
      *                IN EXCEPTION REPORT WHEN                    *            
      *            1.) APPLICANTS' DECLARED MARITAL STATUS IN      *            
      *                BP13K820 IS SINGLE/WIDOWED/SEPARATED, LIST  *            
      *                ALL MARRIAGE RECORDS                        *            
      *            2.) APPLICANTS' DECLARED MARITAL STATUS IN      *            
      *                BP13K820 IS DIVORCED, GET LATEST RECORD     *            
      *                                                            *            
      *  INPUT FILE  : BP13F82Z                                    *            
      *                BP13K800                                    *            
      *                BP13K820                                    *            
      *                BP13K734                                    *            
      *  OUTPUT FILE : BP13LD61                                    *            
      *                P13F82ZA                                    *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP135605 SMR2 23032015 NEW PROGRAM                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F82Z ASSIGN       TO BP13F82Z.                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-K820-STATUS.                      
                                                                                
           SELECT BP13K734 ASSIGN       TO BP13K734                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K734-KEY-FLD                         
                           FILE STATUS  IS WS-K734-STATUS.                      
                                                                                
           SELECT BP13LD61 ASSIGN       TO BP13LD61.                            
                                                                                
           SELECT P13F82ZA ASSIGN       TO P13F82ZA.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F82Z                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F82Z.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K734                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K734.                                                           
                                                                                
       FD   BP13LD61                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  132 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORDS    ARE OMITTED.                                       
       01   BP13LD61-REC                   PIC X(132).                          
                                                                                
       FD   P13F82ZA                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       01   P13F82ZA-REC                   PIC X(150).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       COPY BE01W812.                                                           
       COPY BE01W902.                                                           
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K820-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K734-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F82Z-EOF                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F82Z-READ                PIC 9(06)   VALUE ZEROES.            
           05  WS-F82Z-BLANK               PIC 9(06)   VALUE ZEROES.            
           05  WS-K800-NOTFND              PIC 9(06)   VALUE ZEROES.            
           05  WS-K800-NOTHA               PIC 9(06)   VALUE ZEROES.            
           05  WS-K820-NOTFND              PIC 9(06)   VALUE ZEROES.            
           05  WS-BE01-UNSUCC              PIC 9(06)   VALUE ZEROES.            
           05  WS-K734-NOTFND              PIC 9(06)   VALUE ZEROES.            
           05  WS-F82ZA-WRITE              PIC 9(06)   VALUE ZEROES.            
           05  WS-F82ZA-DELETE             PIC 9(06)   VALUE ZEROES.            
           05  WS-LD61-WRITE               PIC 9(06)   VALUE ZEROES.            
           05  WS-LD61-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-LD61-PAGE                PIC 9(05)   VALUE ZEROES.            
           05  WS-LD61-LINE                PIC 9(02)   VALUE 60.                
           05  WS-CTR                      PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                     PIC X(08)   VALUE SPACES.            
           05  WS-DATE-EDIT                PIC X(10)   VALUE SPACES.            
           05  WS-DTE-MRG-8                PIC X(08)   VALUE SPACES.            
           05  WS-DTE-MRG-10               PIC X(10)   VALUE SPACES.            
           05  WS-NUM-CURR                 PIC 9(08)   VALUE ZEROES.            
           05  WS-INT-CURR                 PIC 9(08)   VALUE ZEROES.            
           05  WS-NUM-UPD                  PIC 9(08)   VALUE ZEROES.            
           05  WS-INT-UPD                  PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-IDNTY-TYPE               PIC X(03)   VALUE SPACES.            
           05  WS-F82Z-KEY-FLD.                                                 
               10  WS-F82Z-ALLO-CAT        PIC X(03)   VALUE SPACES.            
               10  WS-F82Z-DTE-BAL         PIC X(06)   VALUE SPACES.            
               10  WS-F82Z-NT              PIC X(03)   VALUE SPACES.            
               10  WS-F82Z-FT              PIC X(02)   VALUE SPACES.            
           05  WS-F82Z-REGN                PIC X(08)   VALUE SPACES.            
           05  WS-PREV-KEY-FLD             PIC X(14)   VALUE SPACES.            
           05  WS-PREV-REGN                PIC X(08)   VALUE SPACES.            
                                                                                
      **************************************************************            
      *            BP13LD61 - R E P O R T   L A Y O U T            *            
      **************************************************************            
       01  LD61-HEADING-01.                                                     
           05  FILLER              PIC X(08)     VALUE 'BP13LD61'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LD61-RUN-DATE       PIC X(10).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD61-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  LD61-HEADING-02.                                                     
           05  FILLER              PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(32)     VALUE                          
               'MARRIAGE STATUS EXCEPTION REPORT'.                              
                                                                                
       01  LD61-HEADING-03.                                                     
           05  FILLER              PIC X(44)     VALUE                          
               'SALES MODE / BALLOT QTR / BTO ZONE / FLAT - '.                  
           05  LD61-ALLO-CAT       PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' / '.                   
           05  LD61-DTE-BAL        PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' / '.                   
           05  LD61-NT             PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' / '.                   
           05  LD61-FT             PIC X(02)     VALUE SPACES.                  
                                                                                
       01  LD61-HEADING-04.                                                     
           05  FILLER              PIC X(48)     VALUE                          
               'S/N    REGN           APPL NRIC        APPL NAME'.              
           05  FILLER              PIC X(43)     VALUE SPACES.                  
           05  FILLER              PIC X(17)     VALUE                          
               'M/S  DIVORCE DATE'.                                             
                                                                                
       01  LD61-HEADING-05.                                                     
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE                          
               'HSBND/WIFE       HSBND/WIFE'.                                   
           05  FILLER              PIC X(47)     VALUE SPACES.                  
           05  FILLER              PIC X(26)     VALUE                          
               'MARRIAGE     UPD   DTE UPD'.                                    
                                                                                
       01  LD61-HEADING-06.                                                     
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  FILLER              PIC X(21)     VALUE                          
               'IDNTY-NO         NAME'.                                         
           05  FILLER              PIC X(53)     VALUE SPACES.                  
           05  FILLER              PIC X(25)     VALUE                          
               'DATE         BY    IN CDB'.                                     
                                                                                
       01  LD61-HEADING-07.                                                     
           05  FILLER              PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  LD61-HEADING-08.                                                     
           05  FILLER              PIC X(132)    VALUE SPACES.                  
                                                                                
       01  LD61-DETAIL-01.                                                      
           05  LD61-S-NO           PIC ZZZZ9     VALUE ZEROES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD61-REGN           PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  LD61-APPL-NRIC      PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD61-APPL-NAME      PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD61-MS             PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD61-DTE-DIV        PIC X(10)     VALUE SPACES.                  
                                                                                
       01  LD61-DETAIL-02.                                                      
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  LD61-HSBND-NRIC     PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD61-HSBND-NAME     PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  LD61-DTE-MARR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD61-UPD-BY         PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD61-DTE-UPD        PIC X(10)     VALUE SPACES.                  
                                                                                
       01  LD61-DETAIL-03.                                                      
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  LD61-WIFE-NRIC      PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD61-WIFE-NAME      PIC X(50)     VALUE SPACES.                  
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *==================*                                                      
       0000-MAIN-ROUTINE.                                                       
      *==================*                                                      
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F82Z      THRU 2000-EXIT                       
             UNTIL WS-F82Z-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
           OPEN INPUT  BP13F82Z                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K734                                                 
                OUTPUT BP13LD61                                                 
                       P13F82ZA.                                                
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE WS-K800-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE WS-K820-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K734-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K734 FILE ERROR ' WS-K734-STATUS             
              MOVE WS-K734-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BE01W812-INPUT-INFO.              
           MOVE 'OPENS'                    TO BE01W812-IN-RTN-FXN.              
           CALL 'BE01C812' USING BE01W812-COMMAREA.                             
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-DATE.                          
           STRING WS-DATE(1:4) '-' WS-DATE(5:2) '-' WS-DATE(7:2)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO LD61-RUN-DATE.                         
                                                                                
           MOVE WS-DATE                    TO WS-NUM-CURR.                      
           COMPUTE WS-INT-CURR = FUNCTION INTEGER-OF-DATE(WS-NUM-CURR).         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2000-READ-BP13F82Z.                                                      
      *===================*                                                     
           READ BP13F82Z AT END                                                 
                MOVE 'Y'                    TO WS-F82Z-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE F82Z-NUM-ALLO-CAT           TO WS-F82Z-ALLO-CAT.                
           MOVE F82Z-DTE-BALLOT             TO WS-F82Z-DTE-BAL.                 
           MOVE F82Z-CDE-NEW-TOWN           TO WS-F82Z-NT.                      
           MOVE F82Z-CDE-FLAT-TYPE          TO WS-F82Z-FT.                      
                                                                                
           MOVE F82Z-NUM-REGN               TO WS-F82Z-REGN.                    
                                                                                
           ADD 1                            TO WS-F82Z-READ.                    
                                                                                
           IF F82Z-DTE-UPDATE NOT = SPACES AND LOW-VALUES                       
              MOVE F82Z-DTE-UPDATE          TO WS-NUM-UPD                       
              COMPUTE WS-INT-UPD = FUNCTION INTEGER-OF-DATE(WS-NUM-UPD)         
              ADD 4                         TO WS-INT-UPD                       
                                                                                
              IF WS-INT-UPD <= WS-INT-CURR                                      
                 PERFORM 3000-READ-BP13K800 THRU 3000-EXIT                      
                 ADD 1                      TO WS-F82ZA-DELETE                  
              ELSE                                                              
                 WRITE P13F82ZA-REC         FROM BP13F82Z-REC                   
                 ADD 1                      TO WS-F82ZA-WRITE                   
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'RECORD HAS BLANK DATE : ' F82Z-NUM-REGN                  
              WRITE P13F82ZA-REC            FROM BP13F82Z-REC                   
              ADD 1                         TO WS-F82ZA-WRITE                   
                                               WS-F82Z-BLANK                    
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       3000-READ-BP13K800.                                                      
      *===================*                                                     
           MOVE SPACES                      TO BP13K800-MASTER.                 
           INITIALIZE                          BP13K800-MASTER.                 
                                                                                
           MOVE F82Z-NUM-REGN               TO K800-NUM-REGN.                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              IF (K800-NUM-NRIC1 = F82Z-NUM-NRIC) OR                            
                 (K800-NUM-NRIC2 = F82Z-NUM-NRIC) OR                            
                 (K800-NUM-NRIC3 = F82Z-NUM-NRIC) OR                            
                 (K800-NUM-NRIC4 = F82Z-NUM-NRIC)                               
                 PERFORM 4000-READ-BP13K820 THRU 4000-EXIT                      
              ELSE                                                              
                 DISPLAY 'RECORD NOT HA : ' K800-NUM-REGN                       
                 ADD 1                      TO WS-K800-NOTHA                    
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K800 : ' K800-NUM-REGN           
              ADD 1                         TO WS-K800-NOTFND                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K800 : ' WS-K800-STATUS             
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       4000-READ-BP13K820.                                                      
      *===================*                                                     
           MOVE SPACES                      TO BP13K820-REC.                    
           INITIALIZE                          BP13K820-REC.                    
                                                                                
           MOVE F82Z-NUM-REGN               TO K820-NUM-REGN.                   
           MOVE F82Z-NUM-NRIC               TO K820-NUM-NRIC.                   
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 4100-LINK-BE01C812    THRU 4100-EXIT                      
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K820 : ' K820-KEY-FLD            
              ADD 1                         TO WS-K820-NOTFND                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K820 : ' WS-K820-STATUS             
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       4100-LINK-BE01C812.                                                      
      *===================*                                                     
           MOVE SPACES                 TO BE01W812-COMMAREA.                    
           INITIALIZE                     BE01W812-COMMAREA.                    
                                                                                
           MOVE 'ENQRY'                TO BE01W812-IN-RTN-FXN.                  
           MOVE 'BP13'                 TO BE01W812-IN-NUM-UPDATE-SOURCE.        
           IF F82Z-NUM-MARITAL-STATUS = '1' OR 'S' OR '3' OR 'W' OR             
                                        '5' OR 'P'                              
              MOVE 'ALL'               TO BE01W812-IN-ENQRY-TYPE                
                                          BE01W812-IN-ENQRY-SOURCE              
           ELSE                                                                 
              IF F82Z-NUM-MARITAL-STATUS = '4' OR 'D'                           
                 MOVE 'LAT'            TO BE01W812-IN-ENQRY-TYPE                
                 MOVE 'EXT'            TO BE01W812-IN-ENQRY-SOURCE              
              END-IF                                                            
           END-IF.                                                              
           IF K820-NUM-CSTMR-SOURCE = SPACES OR LOW-VALUES                      
              PERFORM 4200-LINK-BE01C902  THRU 4200-EXIT                        
              IF WS-IDNTY-TYPE NOT = 'UIN' AND 'FIN'                            
                 MOVE F82Z-NME-OCCP    TO BE01W812-IN-NME-PERSON                
              END-IF                                                            
              MOVE F82Z-NUM-NRIC       TO BE01W812-IN-IDNTY-NO                  
              MOVE WS-IDNTY-TYPE       TO BE01W812-IN-IDNTY-NO-TYPE             
           ELSE                                                                 
              MOVE K820-NUM-CSTMR-SOURCE  TO BE01W812-IN-CSTMR-SRC              
           END-IF.                                                              
                                                                                
           CALL 'BE01C812' USING BE01W812-COMMAREA.                             
                                                                                
           EVALUATE BE01W812-OUT-OUTPUT-TAG                                     
           WHEN 'F'                                                             
              IF F82Z-NUM-MARITAL-STATUS = '1' OR 'S' OR '3' OR 'W' OR          
                                           '5' OR 'P'                           
                 PERFORM 5000-WRITE-BP13LD61   THRU 5000-EXIT                   
              ELSE                                                              
                 IF F82Z-NUM-MARITAL-STATUS = '4' OR 'D'                        
                    PERFORM 6000-READ-BP13K734 THRU 6000-EXIT                   
                 END-IF                                                         
              END-IF                                                            
                                                                                
           WHEN 'N'                                                             
              DISPLAY 'CUSTOMER NOT FOUND / NO MRG INFO FOUND - '               
                       K820-KEY-FLD                                             
              ADD 1                         TO WS-BE01-UNSUCC                   
                                                                                
           WHEN 'I'                                                             
              DISPLAY 'INPUT REQUIRED - ' K820-KEY-FLD                          
              ADD 1                         TO WS-BE01-UNSUCC                   
                                                                                
           WHEN 'V'                                                             
              DISPLAY 'INPUT INVALID - ' K820-KEY-FLD                           
              ADD 1                         TO WS-BE01-UNSUCC                   
                                                                                
           WHEN 'M'                                                             
              DISPLAY 'MRG INFO FOUND & EXCEEDED MAX INFO TO RETURN - '         
                       K820-KEY-FLD                                             
              ADD 1                         TO WS-BE01-UNSUCC                   
                                                                                
           WHEN OTHER                                                           
              MOVE BE01W812-OUT-RTN-STATCDE TO RETURN-CODE                      
              DISPLAY BE01W812-OUT-RTN-PGMID ' ERROR : '                        
                      BE01W812-OUT-RTN-ACTION                                   
                      BE01W812-OUT-RTN-MSGNO ' / '                              
                      BE01W812-OUT-RTN-STATCDE                                  
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       4200-LINK-BE01C902.                                                      
      *===================*                                                     
           MOVE SPACES                     TO BE01W902-COMMAREA.                
           INITIALIZE                         BE01W902-COMMAREA.                
                                                                                
           MOVE F82Z-NUM-NRIC              TO BE01W902-IN-NUM-IDNTY-NO.         
                                                                                
           CALL 'BE01C902' USING BE01W902-COMMAREA.                             
                                                                                
           IF BE01W902-OUT-RTN-MSGNO = SPACES OR LOW-VALUES                     
              MOVE BE01W902-OUT-NUM-IDNTY-NO-TYPE                               
                                           TO WS-IDNTY-TYPE                     
           ELSE                                                                 
              IF BE01W902-OUT-RTN-MSGNO = '0127'                                
                 MOVE 'XIN'                TO WS-IDNTY-TYPE                     
              ELSE                                                              
                 DISPLAY 'ERROR IN BE01C902 NRIC : ' F82Z-NUM-NRIC              
                         ' / ' BE01W902-OUT-RTN-MSGNO                           
                 PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       5000-WRITE-BP13LD61.                                                     
      *====================*                                                    
           IF (WS-LD61-LINE > 55) OR                                            
              (WS-F82Z-KEY-FLD NOT = WS-PREV-KEY-FLD)                           
              PERFORM 5100-WRITE-HEAD-LD61 THRU 5100-EXIT                       
              MOVE WS-F82Z-KEY-FLD         TO WS-PREV-KEY-FLD                   
           END-IF.                                                              
                                                                                
           IF WS-F82Z-REGN NOT = WS-PREV-REGN                                   
              PERFORM 5200-WRITE-DET-LD61  THRU 5200-EXIT                       
              MOVE WS-F82Z-REGN            TO WS-PREV-REGN                      
           END-IF.                                                              
                                                                                
           MOVE 1                          TO WS-CTR.                           
           PERFORM 5300-WRITE-BE01         THRU 5300-EXIT                       
             UNTIL WS-CTR > BE01W812-OUT-MRG-REC-CNT                            
                OR WS-CTR > 10.                                                 
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=====================*                                                   
       5100-WRITE-HEAD-LD61.                                                    
      *=====================*                                                   
           ADD 1                       TO WS-LD61-PAGE.                         
           MOVE WS-LD61-PAGE           TO LD61-PAGE-NO.                         
                                                                                
           MOVE WS-F82Z-ALLO-CAT       TO LD61-ALLO-CAT.                        
           MOVE WS-F82Z-DTE-BAL        TO LD61-DTE-BAL.                         
           MOVE WS-F82Z-NT             TO LD61-NT.                              
           MOVE WS-F82Z-FT             TO LD61-FT.                              
                                                                                
           WRITE BP13LD61-REC          FROM LD61-HEADING-01 AFTER PAGE.         
           WRITE BP13LD61-REC          FROM LD61-HEADING-02.                    
           WRITE BP13LD61-REC          FROM LD61-HEADING-08.                    
           WRITE BP13LD61-REC          FROM LD61-HEADING-03.                    
           WRITE BP13LD61-REC          FROM LD61-HEADING-08.                    
           WRITE BP13LD61-REC          FROM LD61-HEADING-04.                    
           WRITE BP13LD61-REC          FROM LD61-HEADING-05.                    
           WRITE BP13LD61-REC          FROM LD61-HEADING-06.                    
           WRITE BP13LD61-REC          FROM LD61-HEADING-07.                    
                                                                                
           MOVE 9                      TO WS-LD61-LINE.                         
                                                                                
           IF WS-F82Z-KEY-FLD NOT = WS-PREV-KEY-FLD                             
              MOVE ZEROES              TO WS-LD61-SNO                           
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       5200-WRITE-DET-LD61.                                                     
      *====================*                                                    
           MOVE SPACES                     TO LD61-DETAIL-01.                   
                                                                                
           ADD 1                           TO WS-LD61-SNO                       
                                              WS-LD61-LINE                      
                                              WS-LD61-WRITE.                    
           MOVE WS-LD61-SNO                TO LD61-S-NO.                        
           MOVE F82Z-NUM-REGN              TO LD61-REGN.                        
           MOVE F82Z-NUM-NRIC              TO LD61-APPL-NRIC.                   
           MOVE F82Z-NME-OCCP              TO LD61-APPL-NAME.                   
           EVALUATE F82Z-NUM-MARITAL-STATUS                                     
           WHEN '1'                                                             
              MOVE 'S'                     TO LD61-MS                           
           WHEN '2'                                                             
              MOVE 'M'                     TO LD61-MS                           
           WHEN '3'                                                             
              MOVE 'W'                     TO LD61-MS                           
           WHEN '4'                                                             
              MOVE 'D'                     TO LD61-MS                           
           WHEN '5'                                                             
              MOVE 'P'                     TO LD61-MS                           
           END-EVALUATE.                                                        
           IF F82Z-NUM-MARITAL-STATUS = '4' OR 'D'                              
              STRING K734-DTE-DIVORCE(1:4) '/' K734-DTE-DIVORCE(5:2) '/'        
                     K734-DTE-DIVORCE(7:2)                                      
              DELIMITED BY SIZE INTO LD61-DTE-DIV                               
           ELSE                                                                 
              MOVE SPACES                  TO LD61-DTE-DIV                      
           END-IF.                                                              
           WRITE BP13LD61-REC              FROM LD61-DETAIL-01.                 
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       5300-WRITE-BE01.                                                         
      *================*                                                        
           MOVE SPACES                     TO LD61-DETAIL-02                    
                                              LD61-DETAIL-03.                   
                                                                                
           IF BE01W812-OUT-MRG-DTE-MRG(WS-CTR) NOT = SPACES AND                 
                           LOW-VALUES AND ALL 'X' AND ALL '9'                   
              IF WS-LD61-LINE > 55                                              
                 PERFORM 5100-WRITE-HEAD-LD61 THRU 5100-EXIT                    
              END-IF                                                            
                                                                                
              MOVE BE01W812-OUT-MRG-ID-NO-HSBND(WS-CTR)                         
                                           TO LD61-HSBND-NRIC                   
              MOVE BE01W812-OUT-MRG-NME-HSBND(WS-CTR)                           
                                           TO LD61-HSBND-NAME                   
              STRING BE01W812-OUT-MRG-DTE-MRG(WS-CTR)(1:4) '/'                  
                     BE01W812-OUT-MRG-DTE-MRG(WS-CTR)(6:2) '/'                  
                     BE01W812-OUT-MRG-DTE-MRG(WS-CTR)(9:2)                      
              DELIMITED BY SIZE INTO LD61-DTE-MARR                              
              MOVE BE01W812-OUT-MRG-NUM-UPDT-SRC(WS-CTR)                        
                                           TO LD61-UPD-BY                       
              STRING BE01W812-OUT-MRG-DTE-UPDT-SRC(WS-CTR)(1:4) '/'             
                     BE01W812-OUT-MRG-DTE-UPDT-SRC(WS-CTR)(6:2) '/'             
                     BE01W812-OUT-MRG-DTE-UPDT-SRC(WS-CTR)(9:2)                 
              DELIMITED BY SIZE INTO LD61-DTE-UPD                               
              WRITE BP13LD61-REC           FROM LD61-DETAIL-02                  
                                                                                
              MOVE BE01W812-OUT-MRG-ID-NO-WIFE(WS-CTR)                          
                                           TO LD61-WIFE-NRIC                    
              MOVE BE01W812-OUT-MRG-NME-WIFE(WS-CTR)                            
                                           TO LD61-WIFE-NAME                    
              WRITE BP13LD61-REC           FROM LD61-DETAIL-03                  
              WRITE BP13LD61-REC           FROM LD61-HEADING-08                 
              ADD 3                        TO WS-LD61-LINE                      
           END-IF.                                                              
                                                                                
           ADD 1                           TO WS-CTR.                           
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       6000-READ-BP13K734.                                                      
      *===================*                                                     
           MOVE SPACES                     TO BP13K734-REC.                     
           INITIALIZE                         BP13K734-REC.                     
                                                                                
           MOVE F82Z-NUM-REGN              TO K734-NUM-REGN.                    
           MOVE F82Z-NUM-NRIC              TO K734-NUM-NRIC.                    
                                                                                
           READ BP13K734.                                                       
                                                                                
           EVALUATE WS-K734-STATUS                                              
           WHEN ZEROES                                                          
              MOVE BE01W812-OUT-MRG-DTE-MRG(1) TO WS-DTE-MRG-10                 
              STRING WS-DTE-MRG-10(1:4) WS-DTE-MRG-10(6:2)                      
                     WS-DTE-MRG-10(9:2)                                         
              DELIMITED BY SIZE INTO WS-DTE-MRG-8                               
              IF WS-DTE-MRG-8 > K734-DTE-DIVORCE                                
                 PERFORM 5000-WRITE-BP13LD61   THRU 5000-EXIT                   
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K734 : ' K734-KEY-FLD            
              ADD 1                        TO WS-K734-NOTFND                    
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K734 : ' WS-K734-STATUS             
              MOVE WS-K734-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================*                                                       
       9000-CLOSE-FILES.                                                        
      *=================*                                                       
           DISPLAY '*=============================================*'.           
           DISPLAY '  BP13CD61 CONTROL TOTAL       : ' WS-DATE-EDIT.            
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '  NO OF F82Z RECORDS READ      : ' WS-F82Z-READ.            
           DISPLAY '  NO OF LD61 RECORDS WRITTEN   : ' WS-LD61-WRITE.           
           DISPLAY '  NO OF F82Z BLANK DATE        : ' WS-F82Z-BLANK.           
           DISPLAY '  NO OF K800 NOT FOUND         : ' WS-K800-NOTFND.          
           DISPLAY '  NO OF K800 NOT HA            : ' WS-K800-NOTHA.           
           DISPLAY '  NO OF K820 NOT FOUND         : ' WS-K820-NOTFND.          
           DISPLAY '  NO OF K734 NOT FOUND         : ' WS-K734-NOTFND.          
           DISPLAY '  NO OF BE01 UNSUCC ENQUIRY    : ' WS-BE01-UNSUCC.          
           DISPLAY '  NO OF F82ZA RECORDS RETAINED : ' WS-F82ZA-WRITE.          
           DISPLAY '  NO OF F82ZA RECORDS DELETED  : ' WS-F82ZA-DELETE.         
           DISPLAY '*=============================================*'.           
                                                                                
           CLOSE BP13F82Z                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K734                                                       
                 BP13LD61                                                       
                 P13F82ZA.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE WS-K800-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE WS-K820-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K734-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K734 FILE ERROR ' WS-K734-STATUS             
              MOVE WS-K734-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BE01W812-INPUT-INFO.              
           MOVE 'CLOSE'                    TO BE01W812-IN-RTN-FXN.              
           CALL 'BE01C812' USING BE01W812-COMMAREA.                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
