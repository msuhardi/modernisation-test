       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C236.                                                 
       AUTHOR.        GD.                                                       
      *DATE-WRITTEN.  15/04/95.                                                 
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO DELETE BP13F200 RECORDS BY ZONE/NT, FLAT-TYPE *                
      *       AND BALLOT-QUARTER.                              *                
      *                                                        *                
      *    INPUT  FILES : BP13F200                             *                
      *                   BP13F205                             *                
      *                                                        *                
      *    I/O   FILE   : NIL                                  *                
      *                                                        *                
      *    OUTPUT FILES : P13F200O - F200 RECORDS RETAINED     *                
      *                                                        *                
      *    LISTING      : NIL                                  *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * ------   --  ------   -------------------------------- *                
      * BP132095 MTD 22/04/02 EXPAND LENGTH OF BP13F200 AND    *                
      *                       AND P13F200O (FROM 100 TO 300)   *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT P13F200O ASSIGN TO P13F200O.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F205                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS  80 CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   P13F200O                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 300 CHARACTERS.                                     
       01   P13F200O-REC    PIC X(300).                                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F200-CONTROL-FIELDS.                                          
               10  WS-F200-NUM-NT-ZONE        PIC X(3).                         
               10  WS-F200-NUM-FLAT-TYPE      PIC X(2).                         
               10  WS-F200-DTE-BALLOT         PIC X(6).                         
           05  WS-F205-CONTROL-FIELDS.                                          
               10  WS-F205-NUM-NT-ZONE        PIC X(3).                         
               10  WS-F205-NUM-FLAT-TYPE      PIC X(2).                         
               10  WS-F205-DTE-BALLOT         PIC X(6).                         
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F200-READ-CTR               PIC 9(7)  VALUE ZEROS.            
           05  WS-F205-READ-CTR               PIC 9(7)  VALUE ZEROS.            
           05  WS-F200-WRITE-CTR              PIC 9(7)  VALUE ZEROS.            
           05  WS-F200-DELETE-CTR             PIC 9(7)  VALUE ZEROS.            
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-YY                  PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(2).                         
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F200         THRU 2000-EXIT.                       
           PERFORM 3000-READ-F205         THRU 3000-EXIT.                       
                                                                                
           PERFORM 4000-PROCESS-RECORDS   THRU 4000-EXIT                        
              UNTIL WS-F200-CONTROL-FIELDS = HIGH-VALUES AND                    
                    WS-F205-CONTROL-FIELDS = HIGH-VALUES.                       
                                                                                
           GO TO   9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13F205                                                 
                OUTPUT P13F200O.                                                
                                                                                
           ACCEPT WS-SYSTEM-DATE      FROM DATE.                                
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-YY           TO WS-EDITED-YY.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       2000-READ-F200.                                                          
      *************************************************************             
                                                                                
           READ BP13F200                                                        
              AT END  MOVE HIGH-VALUES   TO WS-F200-CONTROL-FIELDS              
              GO TO   2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE F200-NUM-NT-ZONE         TO WS-F200-NUM-NT-ZONE.                
           MOVE F200-NUM-FLAT-TYPE       TO WS-F200-NUM-FLAT-TYPE.              
           MOVE F200-DTE-BALLOT          TO WS-F200-DTE-BALLOT.                 
                                                                                
           ADD   +1     TO WS-F200-READ-CTR.                                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-READ-F205.                                                          
      ****************************************************************          
                                                                                
           READ BP13F205                                                        
              AT END MOVE   HIGH-VALUES TO WS-F205-CONTROL-FIELDS               
                     GO TO  3000-EXIT                                           
           END-READ.                                                            
                                                                                
           MOVE F205-NUM-NT-ZONE         TO WS-F205-NUM-NT-ZONE.                
           MOVE F205-NUM-FLAT-TYPE       TO WS-F205-NUM-FLAT-TYPE.              
           MOVE F205-DTE-BALLOT          TO WS-F205-DTE-BALLOT.                 
                                                                                
           ADD   +1     TO   WS-F205-READ-CTR.                                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       4000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           IF (WS-F200-CONTROL-FIELDS = WS-F205-CONTROL-FIELDS)                 
              PERFORM                                                           
                 UNTIL (WS-F200-CONTROL-FIELDS > WS-F205-CONTROL-FIELDS)        
                 ADD     1              TO   WS-F200-DELETE-CTR                 
                 PERFORM 2000-READ-F200 THRU 2000-EXIT                          
              END-PERFORM                                                       
              PERFORM 3000-READ-F205 THRU 3000-EXIT                             
           ELSE                                                                 
              IF (WS-F200-CONTROL-FIELDS < WS-F205-CONTROL-FIELDS)              
                 PERFORM 5000-WRITE-F200 THRU 5000-EXIT                         
                 PERFORM 2000-READ-F200  THRU 2000-EXIT                         
              ELSE                                                              
                 PERFORM 3000-READ-F205  THRU 3000-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ****************************************************************          
       5000-WRITE-F200.                                                         
      ****************************************************************          
                                                                                
           WRITE P13F200O-REC FROM BP13F200-REC.                                
                                                                                
           ADD   +1           TO   WS-F200-WRITE-CTR.                           
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F200                                                       
                 BP13F205                                                       
                 P13F200O.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C236 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F200 RECORDS READ      = ' WS-F200-READ-CTR.            
           DISPLAY 'BP13F205 RECORDS READ      = ' WS-F205-READ-CTR.            
           DISPLAY 'BP13F200 RECORDS DELETED   = ' WS-F200-DELETE-CTR.          
           DISPLAY 'BP13F200 RECORDS RETAINED  = ' WS-F200-WRITE-CTR.           
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           STOP RUN.                                                            
                                                                                
