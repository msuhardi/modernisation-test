      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.   BP13CC84.                                                  
      *AUTHOR.       BALA AMUTHAN.                                              
      *DATE-WRITTEN. 28/05/2007.                                                
                                                                                
      ***********************************************************               
      *                                                         *               
      *  OBJECTIVE  : THIS PROGRAM WILL CHECK APPLICANT AND     *               
      *             : SPOUSE TO DEBAR FOR ENJOY HDB LOAN AT     *               
      *             : SALES & PURCHASE STAGE.                   *               
      *             : Z1 (APPLICANT) AND                        *               
      *             : Z2 (OCCUPIER SPOUSE)                      *               
      *                                                         *               
      *  INPUT FILE :- BP13FD00                                 *               
      *                BP13K800                                 *               
      *                BP13K820                                 *               
      *  I-O           BP13K665                                 *               
      *  OUTPUT FILE:- BP04K010                                 *               
      ***********************************************************               
      *                                                         *               
      * CHG NO   BY  DATE        REMARKS                        *               
      * ------   --- ----------  -----------------------------  *               
      * BP133070 BA2 28/05/2007  NEW PROGRAM.                   *               
      * BP133091 RB12 17/10/2007 EXPAND BP13FD00 FROM 1200 TO   *               
      *                          1600 BYTES                     *               
      * BP135599 PCL4 11/12/2014 TO UPDATE K010-INTERFACE-SYS   *               
      ***********************************************************               
                                                                                
      *************************                                                 
       ENVIRONMENT DIVISION.                                                    
      *************************                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *************************                                                 
       INPUT-OUTPUT SECTION.                                                    
      *************************                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD00 ASSIGN TO BP13FD00.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K820-KEY-FLD                              
                  FILE STATUS      IS WS-K820-STATUS.                           
                                                                                
           SELECT BP13K665 ASSIGN TO BP13K665                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS WS-K665-KEY-FLD                            
                  FILE STATUS     IS WS-K665-STATUS.                            
                                                                                
           SELECT BP04K010 ASSIGN TO BP04K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13LC84 ASSIGN TO BP13LC84.                                  
           SELECT P13LC84B ASSIGN TO P13LC84B.                                  
                                                                                
                                                                                
      *************************                                                 
       DATA DIVISION.                                                           
      *************************                                                 
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD00   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 1600 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13FD00.                                                           
                                                                                
       FD  BP13K665   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 50 CHARACTERS                             
                      LABEL RECORDS ARE STANDARD.                               
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD  BP13K800   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K800.                                                           
                                                                                
       FD BP13K820    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K820.                                                           
                                                                                
       FD BP04K010    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 300 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY P04K010.                                                            
                                                                                
       FD BP13LC84    RECORD CONTAINS 132 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 BP13LC84-REC                   PIC X(132).                            
                                                                                
       FD P13LC84B    RECORD CONTAINS 132 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 P13LC84B-REC                   PIC X(132).                            
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
      *----------------------*                                                  
      * LINKAGE FOR BP13C913 *                                                  
      *----------------------*                                                  
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-ACC          PIC X(5).                           
                                                                                
        COPY P13COMM8.                                                          
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-FD00-EOF               PIC X    VALUE 'N'.                    
               88 FD00-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
           05  WS-DEBARMENT-TAG          PIC X    VALUE SPACES.                 
           05  WS-DEBAR-TAG              PIC X    VALUE SPACES.                 
           05  WS-K800-FOUND             PIC X    VALUE SPACES.                 
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-FD00          PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K010-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-LINE-CTR               PIC 9(6) VALUE 60.                     
           05  WS-LINE-CTR1              PIC 9(6) VALUE 60.                     
           05  WS-PAGE-CTR               PIC 9(6) VALUE ZEROS.                  
           05  WS-PAGE-CTR1              PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO                 PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO1                PIC 9(6) VALUE ZEROS.                  
           05  WS-DIRECT-PURCHASE        PIC 9(6) VALUE ZEROS.                  
           05  WS-IDX                    PIC 9(1) VALUE ZEROS.                  
                                                                                
       01  WS-TEMPORARY-VARS.                                                   
           05  WS-PREV-REGN              PIC X(8) VALUE SPACES.                 
           05  WS-NUM-LOAN-TAG           PIC X(2) VALUE SPACES.                 
           05  WS-NRIC                   PIC X(9) VALUE SPACES.                 
                                                                                
       01  WS-SPOUSE-NRICS.                                                     
           05  WS-SP-NRIC1               PIC X(9) VALUE SPACES.                 
           05  WS-SP-NRIC2               PIC X(9) VALUE SPACES.                 
           05  WS-SP-NRIC3               PIC X(9) VALUE SPACES.                 
           05  WS-SP-NRIC4               PIC X(9) VALUE SPACES.                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K665-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K800-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K820-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K010-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY        PIC 9(02).                                   
              10 WS-CURR-MM        PIC 9(02).                                   
              10 WS-CURR-DD        PIC 9(02).                                   
                                                                                
       COPY BP13K665.                                                           
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  LC84-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13LC84'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC84-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC84-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  LC84-PR-HEAD-02.                                                     
           05  FILLER              PIC X(45)     VALUE SPACES .                 
           05  FILLER              PIC X(41)     VALUE                          
               'TAGGING OF SUBSIDISED HOUSING LOAN REPORT'.                     
           05  FILLER              PIC X(46)     VALUE SPACES.                  
                                                                                
       01  LC84-PR-HEAD-03.                                                     
           05  FILLER              PIC X(52)     VALUE SPACES .                 
           05  FILLER              PIC X(28)     VALUE                          
               'COU UNIT  '.                                                    
           05  FILLER              PIC X(52)     VALUE SPACES.                  
                                                                                
       01  LC84-PR-HEAD-04.                                                     
           05  FILLER              PIC X(31)     VALUE                          
               'SEQ.    REGN   SCH & A/C   NAME'.                               
           05  FILLER              PIC X(27)     VALUE SPACES .                 
           05  FILLER              PIC X(18)     VALUE                          
               'I/C NO.   ADDRESS'.                                             
           05  FILLER              PIC X(29)     VALUE SPACES.                  
           05  FILLER              PIC X(25)     VALUE                          
               'DEB-REF     CODE EFF-DATE'.                                     
                                                                                
       01  LC84-PR-DTL-01.                                                      
           05  LC84-SEQ-NO         PIC ZZZ9.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LC84-NUM-REGN       PIC X(08).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84-NUM-SCHEME     PIC X(11).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84-NME-OCCP       PIC X(30).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84-NUM-NRIC       PIC X(09).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84-ADDRESS        PIC X(36).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84-DEB-REF-NO     PIC X(13).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84-DEB-CODE       PIC X(02).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84-EFF-DATE       PIC X(10).                                   
                                                                                
       01  LC84-PR-LINE            PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  LC84B-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'P13LC84B'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC84B-DATE          PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC84B-PAGENO         PIC ZZZZ9 .                                 
                                                                                
       01  LC84B-PR-HEAD-02.                                                    
           05  FILLER              PIC X(52)     VALUE SPACES .                 
           05  FILLER              PIC X(28)     VALUE                          
               'OUTRIGHT PURCHASE CASES     '.                                  
           05  FILLER              PIC X(52)     VALUE SPACES.                  
                                                                                
       01  LC84B-PR-HEAD-03.                                                    
           05  FILLER              PIC X(52)     VALUE SPACES .                 
           05  FILLER              PIC X(28)     VALUE                          
               'COU UNIT  '.                                                    
           05  FILLER              PIC X(52)     VALUE SPACES.                  
                                                                                
       01  LC84B-PR-HEAD-04.                                                    
           05  FILLER              PIC X(31)     VALUE                          
               'SEQ.    REGN   1ST APPLN   NAME'.                               
                                                                                
       01  LC84B-PR-DTL-01.                                                     
           05  LC84B-SEQ-NO        PIC ZZZ9.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LC84B-NUM-REGN      PIC X(08).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84B-NUM-NRIC      PIC X(11).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LC84B-NME-OCCP      PIC X(30).                                   
                                                                                
                                                                                
      *************************                                                 
       PROCEDURE DIVISION.                                                      
      *************************                                                 
                                                                                
      *-----------------                                                        
       000-MAIN-LOGIC.                                                          
      *-----------------                                                        
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-FD00    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-K665 THRU 300-EXIT                              
                    UNTIL FD00-EOF.                                             
            PERFORM 800-PRINT-CONTROL-TOTAL THRU 800-EXIT.                      
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BP13FD00                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                I-O    BP13K665                                                 
                       BP04K010                                                 
                OUTPUT BP13LC84                                                 
                       P13LC84B.                                                
                                                                                
           IF WS-K665-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/'                                                
                  WS-CURR-MM '/'                                                
                  WS-CURR-CC                                                    
                  WS-CURR-YY DELIMITED BY SIZE                                  
                             INTO LC84-DATE.                                    
                                                                                
           MOVE LC84-DATE TO LC84B-DATE.                                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       200-READ-FD00.                                                           
      *-----------------                                                        
           READ BP13FD00 AT END                                                 
                MOVE 'Y' TO WS-FD00-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD   1     TO WS-READ-CNT-FD00.                                     
           MOVE SPACES TO WS-FOUND.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       300-PROCESS-K665.                                                        
      *-----------------                                                        
                                                                                
           IF FD00-AMT-LOAN-GRANTED NOT NUMERIC                                 
              MOVE 0 TO FD00-AMT-LOAN-GRANTED.                                  
                                                                                
           IF FD00-AMT-LOAN-GRANTED <= 0                                        
              ADD 1 TO WS-DIRECT-PURCHASE                                       
              PERFORM 750-PRINT-L617B THRU 750-EXIT                             
              PERFORM 200-READ-FD00   THRU 200-EXIT                             
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE SPACES TO WS-K800-FOUND.                                        
           MOVE 'N' TO WS-DEBAR-TAG.                                            
           MOVE FD00-NUM-REGN              TO K800-NUM-REGN.                    
                                                                                
           PERFORM 320-READ-BP13K800       THRU 320-EXIT.                       
                                                                                
           IF WS-K800-FOUND = 'Y' AND K800-NUM-STATUS NOT = 'C'                 
                                                                                
               MOVE SPACES                     TO K820-KEY-FLD                  
               MOVE K800-NUM-REGN              TO K820-NUM-REGN                 
                                                                                
               PERFORM 360-FIND-SPOUSE-IC   THRU 360-EXIT                       
                                                                                
               MOVE SPACES                     TO K820-KEY-FLD                  
               MOVE K800-NUM-REGN              TO K820-NUM-REGN                 
               START BP13K820 KEY IS >= K820-KEY-FLD                            
               EVALUATE WS-K820-STATUS                                          
                 WHEN 00                                                        
                    PERFORM  350-PROCESS-K820  THRU  350-EXIT                   
                        UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                 
                        OR    K820-KEY-FLD      = HIGH-VALUES                   
                 WHEN 10                                                        
                 WHEN 23                                                        
                    DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY = '              
                                                 K820-KEY-FLD                   
                 WHEN OTHER                                                     
                    DISPLAY 'ERROR STARTING BP13K820, STATUS '                  
                             WS-K820-STATUS ', KEY =  ' K820-KEY-FLD            
                    MOVE WS-K820-STATUS       TO RETURN-CODE                    
                    PERFORM 999-CLOSE-FILES THRU 999-EXIT                       
               END-EVALUATE.                                                    
                                                                                
           PERFORM 200-READ-FD00  THRU 200-EXIT.                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       320-READ-BP13K800.                                                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                MOVE 'Y' TO WS-K800-FOUND                                       
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN BP13K800 KEY IS '                  
                                        K800-NUM-REGN                           
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K800, STATUS ' WS-K800-STATUS        
                        ', REGN IS ' K800-NUM-REGN                              
                MOVE WS-K800-STATUS       TO RETURN-CODE                        
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       350-PROCESS-K820.                                                        
      *--------------------                                                     
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K820-STATUS                                              
             WHEN 00                                                            
             WHEN 02                                                            
                IF K820-NUM-REGN NOT = K800-NUM-REGN                            
                   GO TO 350-EXIT                                               
                ELSE                                                            
                   ADD 1 TO WS-K820-READ                                        
                END-IF                                                          
             WHEN 10                                                            
             WHEN 23                                                            
                MOVE HIGH-VALUES TO K820-KEY-FLD                                
                GO TO 350-EXIT                                                  
             WHEN OTHER                                                         
                DISPLAY 'ERROR READNEXT BP13K820, STATUS '                      
                         WS-K820-STATUS ', REGN IS ' K820-NUM-REGN              
                MOVE WS-K820-STATUS       TO RETURN-CODE                        
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
            PERFORM 380-DETERMIN-LOANTAG  THRU  380-EXIT.                       
                                                                                
            IF K820-NUM-REGN = K800-NUM-REGN AND                                
                      (WS-NUM-LOAN-TAG = 'Z1' OR 'Z2')                          
               IF WS-DEBAR-TAG = 'N'                                            
                  PERFORM 400-GET-DEBARMENT-NO                                  
                  MOVE 'Y' TO WS-DEBAR-TAG                                      
               END-IF                                                           
               PERFORM 500-PREPARE-DEBARMENT-REC    THRU 500-EXIT               
            END-IF.                                                             
                                                                                
       350-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      *---------------------                                                    
       360-FIND-SPOUSE-IC.                                                      
      *---------------------                                                    
           MOVE SPACES  TO WS-SPOUSE-NRICS.                                     
           MOVE ZEROES  TO WS-IDX.                                              
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES AND         
                                   'XXXXXXXXX'                                  
              MOVE K800-NUM-NRIC1  TO  WS-NRIC                                  
              PERFORM 370-READ-BP13K820   THRU  370-EXIT                        
           END-IF.                                                              
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES AND         
                                   'XXXXXXXXX'                                  
              MOVE K800-NUM-NRIC2  TO  WS-NRIC                                  
              PERFORM 370-READ-BP13K820   THRU  370-EXIT                        
           END-IF                                                               
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROES AND         
                                   'XXXXXXXXX'                                  
              MOVE K800-NUM-NRIC3  TO  WS-NRIC                                  
              PERFORM 370-READ-BP13K820   THRU  370-EXIT                        
           END-IF.                                                              
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROES AND         
                                   'XXXXXXXXX'                                  
              MOVE K800-NUM-NRIC4  TO  WS-NRIC                                  
              PERFORM 370-READ-BP13K820   THRU  370-EXIT                        
           END-IF.                                                              
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       370-READ-BP13K820.                                                       
      *-------------------*                                                     
           ADD +1       TO WS-IDX.                                              
           MOVE SPACES            TO BP13K820-REC.                              
           MOVE LOW-VALUES        TO K820-KEY-FLD.                              
           MOVE K800-NUM-REGN     TO K820-NUM-REGN.                             
           MOVE WS-NRIC           TO K820-NUM-NRIC.                             
                                                                                
           READ BP13K820                                                        
                INVALID KEY                                                     
                DISPLAY 'RECORD NOT FOUND IN BP13K820 KEY IS '                  
                          K800-NUM-REGN '-' WS-NRIC                             
                GO TO 370-EXIT.                                                 
                                                                                
           IF WS-IDX = 1                                                        
              MOVE K820-NUM-NRIC-SPOUSE TO WS-SP-NRIC1                          
           ELSE                                                                 
           IF WS-IDX = 2                                                        
              MOVE K820-NUM-NRIC-SPOUSE TO WS-SP-NRIC2                          
           ELSE                                                                 
           IF WS-IDX = 3                                                        
              MOVE K820-NUM-NRIC-SPOUSE TO WS-SP-NRIC3                          
           ELSE                                                                 
           IF WS-IDX = 4                                                        
              MOVE K820-NUM-NRIC-SPOUSE TO WS-SP-NRIC4                          
           END-IF.                                                              
                                                                                
       370-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------                                                    
       380-DETERMIN-LOANTAG.                                                    
      *---------------------                                                    
           MOVE SPACES   TO  WS-NUM-LOAN-TAG.                                   
                                                                                
           IF K820-NUM-NRIC = K800-NUM-NRIC1                                    
              MOVE 'Z1' TO WS-NUM-LOAN-TAG                                      
           ELSE                                                                 
           IF K820-NUM-NRIC = K800-NUM-NRIC2                                    
              MOVE 'Z1' TO WS-NUM-LOAN-TAG                                      
           ELSE                                                                 
           IF K820-NUM-NRIC = K800-NUM-NRIC3                                    
              MOVE 'Z1' TO WS-NUM-LOAN-TAG                                      
           ELSE                                                                 
           IF K820-NUM-NRIC = K800-NUM-NRIC4                                    
              MOVE 'Z1' TO WS-NUM-LOAN-TAG                                      
           ELSE                                                                 
           IF K820-NUM-RELATIONSHIP = '20' OR '09' OR '10'                      
              MOVE 'Z2' TO  WS-NUM-LOAN-TAG                                     
           ELSE                                                                 
           IF (K820-NUM-NRIC = WS-SP-NRIC1 OR                                   
               K820-NUM-NRIC = WS-SP-NRIC2 OR                                   
               K820-NUM-NRIC = WS-SP-NRIC3 OR                                   
               K820-NUM-NRIC = WS-SP-NRIC4)                                     
               MOVE 'Z2' TO WS-NUM-LOAN-TAG                                     
           END-IF.                                                              
                                                                                
       380-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       400-GET-DEBARMENT-NO.                                                    
      *--------------------                                                     
           MOVE  LOW-VALUES    TO WS-K665-KEY-FLD.                              
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
           EVALUATE WS-K665-STATUS                                              
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 10                                                            
             WHEN 23                                                            
                DISPLAY 'RECORD NOT FOUND IN BP13K665 KEY - '                   
                                                       WS-K665-KEY-FLD          
                GO TO 400-EXIT                                                  
             WHEN OTHER                                                         
                DISPLAY 'ERROR STARTING BP13K665, STATUS '                      
                         WS-K665-STATUS ', KEY IS ' WS-K665-KEY-FLD             
                MOVE WS-K665-STATUS       TO RETURN-CODE                        
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           PERFORM 410-READ-BP13K665 THRU 410-EXIT                              
                   UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                         
                                                                                
           IF K665-KEY-FLD = SPACES OR LOW-VALUES                               
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           ADD   1                  TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO.                
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13CC84'          TO  K665-NUM-USERID.                        
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              WRITE   WS-BP13K665-REC FROM BP13K665-REC.                        
                                                                                
           EVALUATE WS-K665-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
              ADD   1                 TO  WS-K665-WRITE                         
                                                                                
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STATUS '                   
                      WS-K665-STATUS                                            
                      MOVE WS-K665-STATUS       TO RETURN-CODE                  
                      PERFORM 999-CLOSE-FILES THRU 999-EXIT                     
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-WRITE                         
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS     TO   RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       410-READ-BP13K665.                                                       
      *--------------------                                                     
           READ BP13K665 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K665-STATUS                                              
             WHEN 00                                                            
             WHEN 02                                                            
                CONTINUE                                                        
             WHEN 10                                                            
             WHEN 23                                                            
             WHEN 46                                                            
                MOVE HIGH-VALUES TO WS-K665-KEY-FLD                             
                GO TO 410-EXIT                                                  
             WHEN OTHER                                                         
                DISPLAY 'ERROR READNEXT BP13K665, STATUS '                      
                         WS-K665-STATUS ', KEY IS ' WS-K665-KEY-FLD             
                MOVE WS-K665-STATUS       TO RETURN-CODE                        
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO WS-K665-READ.                                               
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC.                             
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       500-PREPARE-DEBARMENT-REC.                                               
      *--------------------                                                     
           MOVE SPACES TO P04K010-REC.                                          
                                                                                
           PERFORM 530-WRITE-BP04K010   THRU 530-EXIT.                          
           PERFORM 700-PRINT-L617       THRU 700-EXIT.                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       530-WRITE-BP04K010.                                                      
      *--------------------                                                     
           MOVE SPACES                 TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE K800-NUM-SCH-ACC       TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF (COMM8-CDE-SYSERR = 0) AND                                        
              (COMM8-NUM-MGMT   NOT = SPACES)                                   
              STRING 'BLK' COMM8-NUM-BLK                                        
                     '#'   COMM8-NUM-LEVEL                                      
                     '-'   COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB               
                     ' '   COMM8-NME-STREET(1:20)                               
                     '('   COMM8-NUM-POSTAL-CODE ')'                            
                     DELIMITED BY SIZE INTO LC84-ADDRESS                        
           ELSE                                                                 
              DISPLAY 'SQL CODE = ' COMM8-CDE-SYSERR                            
              MOVE SPACES TO LC84-ADDRESS.                                      
                                                                                
           MOVE SPACES                   TO P04K010-REC.                        
           INITIALIZE                       P04K010-REC.                        
                                                                                
           MOVE WS-NUM-LOAN-TAG          TO K010-REASON-CODE.                   
           MOVE K820-NUM-NRIC            TO K010-NUM-UINFIN.                    
           MOVE K820-NME-OCCP            TO K010-NAME.                          
           MOVE K665-NUM-LAST-DEBARMENT  TO K010-DEB-REF-NO.                    
           MOVE FD00-DTE-SNP-SIGNED      TO K010-EFF-DATE.                      
           MOVE K800-NUM-SCH-ACC(1:9)    TO K010-EX-SCH-ACCTNO.                 
           MOVE K800-NUM-SCH-ACC(10:2)   TO K010-LSE-TEN-CODE.                  
           MOVE '1'                      TO K010-PROP-TYPE.                     
           MOVE '999'                    TO K010-PERIOD-DEBAR.                  
                                                                                
           IF K010-AMT-OWING NOT NUMERIC                                        
              MOVE ZEROES                TO K010-AMT-OWING                      
           END-IF.                                                              
                                                                                
           MOVE COMM8-NUM-BLK            TO K010-BLOCK.                         
           MOVE COMM8-NME-STREET         TO K010-STREETNAME.                    
           MOVE COMM8-NUM-POSTAL-CODE    TO K010-POSTCODE.                      
           MOVE 'BP13'                   TO K010-INTERFACE-SYS.                 
                                                                                
           STRING    COMM8-NUM-LEVEL '-'                                        
                     COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                     
                     DELIMITED BY SIZE INTO K010-HOUSE-NO.                      
                                                                                
           MOVE WS-CURR-DATE             TO K010-INCLUSION-DATE                 
                                            K010-LUPDATE.                       
                                                                                
           WRITE P04K010-REC                                                    
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K010-WRITE                                          
           WHEN 22                                                              
           WHEN 02                                                              
                DISPLAY 'DUPLICATE RECORD BP04K010: ' K010-PRIME-KEY            
           WHEN OTHER                                                           
                MOVE WS-K010-STATUS       TO RETURN-CODE                        
                DISPLAY 'ERROR IN WRITING BP04K010: ' K010-PRIME-KEY            
                DISPLAY K800-NUM-REGN                                           
                DISPLAY WS-K010-STATUS                                          
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       530-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       700-PRINT-L617.                                                          
      *--------------------                                                     
           ADD  1                        TO WS-SEQ-NO.                          
           MOVE WS-SEQ-NO                TO LC84-SEQ-NO.                        
           MOVE FD00-NUM-REGN            TO LC84-NUM-REGN.                      
           MOVE K665-NUM-LAST-DEBARMENT  TO LC84-DEB-REF-NO.                    
           MOVE K010-REASON-CODE         TO LC84-DEB-CODE.                      
           MOVE K820-NUM-NRIC            TO LC84-NUM-NRIC.                      
           MOVE K820-NME-OCCP            TO LC84-NME-OCCP.                      
           MOVE K800-NUM-SCH-ACC         TO LC84-NUM-SCHEME.                    
                                                                                
           STRING K010-EFF-DATE(7:2) '-'                                        
                  K010-EFF-DATE(5:2) '-'                                        
                  K010-EFF-DATE(1:4) DELIMITED BY SIZE                          
                  INTO LC84-EFF-DATE.                                           
                                                                                
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 6              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   LC84-PAGENO                              
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-01 AFTER PAGE               
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-02                          
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-03                          
              WRITE BP13LC84-REC  FROM LC84-PR-LINE    AFTER 2                  
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-04                          
              WRITE BP13LC84-REC  FROM LC84-PR-LINE.                            
                                                                                
           WRITE BP13LC84-REC  FROM LC84-PR-DTL-01.                             
           ADD   1             TO   WS-LINE-CTR .                               
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       750-PRINT-L617B.                                                         
      *--------------------                                                     
           ADD  1                         TO WS-SEQ-NO1.                        
           MOVE WS-SEQ-NO1                TO LC84B-SEQ-NO.                      
           MOVE FD00-NUM-REGN             TO LC84B-NUM-REGN.                    
                                                                                
           IF WS-LINE-CTR1 > 55                                                 
              ADD  1              TO   WS-PAGE-CTR1                             
              MOVE 6              TO   WS-LINE-CTR1                             
              MOVE WS-PAGE-CTR1   TO   LC84B-PAGENO                             
              WRITE P13LC84B-REC  FROM LC84B-PR-HEAD-01 AFTER PAGE              
              WRITE P13LC84B-REC  FROM LC84B-PR-HEAD-02                         
              WRITE P13LC84B-REC  FROM LC84B-PR-HEAD-03                         
              WRITE P13LC84B-REC  FROM LC84-PR-LINE    AFTER 2                  
              WRITE P13LC84B-REC  FROM LC84B-PR-HEAD-04                         
              WRITE P13LC84B-REC  FROM LC84-PR-LINE.                            
                                                                                
           WRITE P13LC84B-REC  FROM LC84B-PR-DTL-01.                            
           ADD   1             TO   WS-LINE-CTR1 .                              
                                                                                
       750-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       800-PRINT-CONTROL-TOTAL.                                                 
      *----------------------------------------------------------------*        
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 6              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   LC84-PAGENO                              
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-01 AFTER PAGE               
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-02                          
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-03                          
              WRITE BP13LC84-REC  FROM LC84-PR-LINE    AFTER 2                  
              WRITE BP13LC84-REC  FROM LC84-PR-HEAD-04                          
              WRITE BP13LC84-REC  FROM LC84-PR-LINE.                            
                                                                                
           MOVE    '*-------BP13CC84 CONTROL TOTALS ------------*'              
                   TO LC84-PR-LINE.                                             
           WRITE BP13LC84-REC  FROM LC84-PR-LINE AFTER 2.                       
           STRING  '1. NO OF REC READ        BP13FD00   = '                     
                    WS-READ-CNT-FD00 DELIMITED BY SIZE                          
                    INTO LC84-PR-LINE.                                          
           WRITE BP13LC84-REC  FROM LC84-PR-LINE AFTER 2.                       
           STRING  '2. NO OF CASES TAGGED    BP04K010   = '                     
                    WS-K010-WRITE DELIMITED BY SIZE                             
                    INTO LC84-PR-LINE.                                          
           WRITE BP13LC84-REC  FROM LC84-PR-LINE AFTER 2.                       
           STRING  '3. NO OF OUTRIGHT PURCHASE          = '                     
                    WS-DIRECT-PURCHASE DELIMITED BY SIZE                        
                    INTO LC84-PR-LINE.                                          
           WRITE BP13LC84-REC  FROM LC84-PR-LINE AFTER 2.                       
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       999-CLOSE-FILES.                                                         
      *----------------------------------------------------------------*        
           CLOSE BP13FD00                                                       
                 BP13K665                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP04K010                                                       
                 BP13LC84                                                       
                 P13LC84B.                                                      
                                                                                
           IF WS-K665-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K665, STATUS ' WS-K665-STATUS          
              MOVE WS-K665-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP04K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY '*------------------ BP13CC84 -------------------'.          
           DISPLAY '                  ' WS-CURR-DATE                            
           DISPLAY 'NO OF REC READ        BP13FD00   = '                        
                    WS-READ-CNT-FD00.                                           
           DISPLAY 'NO OF REC READ        BP13K665   = '                        
                    WS-K665-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP13K665   = '                        
                    WS-K665-WRITE                                               
           DISPLAY 'NO OF REC READ        BP13K820   = '                        
                    WS-K820-READ                                                
           DISPLAY 'NO OF REC READ        BP13K800   = '                        
                    WS-K800-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP04K010   = '                        
                    WS-K010-WRITE                                               
           DISPLAY 'NO OF DIRECT PURCHASE P13LC84B   = '                        
                    WS-DIRECT-PURCHASE                                          
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
