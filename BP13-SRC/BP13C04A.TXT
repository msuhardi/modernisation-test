      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C04A.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   17/07/2004.                                              
      *--------------------------------------------------------*                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1. TO MATCH BP13F730 WITH BP13F830 TO GET RESIDENT*                
      *         ADDRESS OF BTO REQUEST & HH TPYE               *                
      *                                                        *                
      * INPUT  : BP13F730                                      *                
      *          BP13F830                                      *                
      *                                                        *                
      * OUTPUT : P13F830A                                      *                
      *                                                        *                
      * CHG-REG# BY   ON          DESCRIPTION                  *                
      * -------- ---- ----------  -----------------------------*                
      * BP132558 SK   17/07/2004  NEW PRG                      *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730   ASSIGN TO BP13F730.                                
           SELECT BP13F830   ASSIGN TO BP13F830.                                
           SELECT P13F830A   ASSIGN TO P13F830A.                                
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F830                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F830.                                                           
                                                                                
       FD   P13F830A                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F830A-REC.                                                       
            05 FILLER              PIC X(250).                                  
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F730              PIC 9(8)    VALUE 0.                    
           05 WS-READ-F830              PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-F730-FND               PIC 9(8)    VALUE 0.                    
           05 WS-F820-FND               PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F830A            PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F730     THRU 2000-EXIT.                           
           PERFORM 3000-READ-F830     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
                   UNTIL F730-NUM-REGN    = HIGH-VALUES                         
                     AND F830-NUM-REGN-BK = HIGH-VALUES.                        
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F830                                                 
           OUTPUT      P13F830A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F730.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F730                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F730-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F730                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F830.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F830                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F830-NUM-REGN-BK                         
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F830                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           IF F830-NUM-REGN-BK  = F730-NUM-REGN                                 
              ADD 1 TO WS-MATCH                                                 
              PERFORM 4900-WRITE-F830A THRU 4900-EXIT                           
              PERFORM 2000-READ-F730   THRU 2000-EXIT                           
              PERFORM 3000-READ-F830   THRU 3000-EXIT                           
           ELSE                                                                 
              IF F830-NUM-REGN-BK > F730-NUM-REGN                               
                 ADD 1 TO WS-F730-FND                                           
                 PERFORM 2000-READ-F730 THRU 2000-EXIT                          
              ELSE                                                              
                 ADD 1 TO WS-F820-FND                                           
                 PERFORM 3000-READ-F830   THRU 3000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4900-WRITE-F830A.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE  F730-CDE-HOUSEHOLD  TO BP13F830-REC(226:1).                    
           WRITE P13F830A-REC FROM BP13F830-REC.                                
           ADD 1 TO WS-WRITE-F830A.                                             
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '********** BP13C04A **************'.                        
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13F730) :' WS-READ-F730.             
           DISPLAY 'NO. OF RECORDS READ (BP13F830) :' WS-READ-F830.             
           DISPLAY 'NO. OF RECORDS WRITE(P13F830A) :' WS-WRITE-F830A.           
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F730                                                 
                       BP13F830                                                 
                       P13F830A.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
