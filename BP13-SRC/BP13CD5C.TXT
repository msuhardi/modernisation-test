       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD5C.                                                 
      *AUTHOR.        IRVIN CHING.                                              
      *DATE-WRITTEN.  14/01/2013.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO READ BP13F430 TO GET REPUDUATION CASES TO PASS  *        
      *             TO SMS                                             *        
      *                                                                *        
      * INPUT  FILES : SY02F001                                        *        
      *                BP13F430                                        *        
      *                BP13KD00                                        *        
      * OUTPUT FILES : AB03F021                                        *        
      *                                                                *        
      * CHG-NO   BY    ON    - DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP134768 IMC1 14/01/13 NEW PROGRAM                             *        
      * BP134768 IMC1 10/04/13 ADD CHECKING OF AB03K030                *        
      * BP135133 ZDD1 25/11/13 ADD CHECKING OF BM06K115                *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   SY02F001      ASSIGN TO SY02F001.                           
           SELECT   BP13F430      ASSIGN TO BP13F430.                           
                                                                                
           SELECT   BP13KD00      ASSIGN TO BP13KD00                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS KD00-NUM-REGN                       
                    ALTERNATE RECORD KEY IS KD00-ALT-KEY1                       
                             FILE STATUS IS KD00-STATUS.                        
                                                                                
           SELECT BM06K110 ASSIGN           TO BM06K110                         
                           ACCESS MODE      IS DYNAMIC                          
                           ORGANIZATION     IS INDEXED                          
                           RECORD    KEY    IS K110-KEY-FLD                     
                           FILE STATUS      IS K110-STATUS.                     
                                                                                
           SELECT BM06K115 ASSIGN           TO BM06K115                         
                           ACCESS MODE      IS DYNAMIC                          
                           ORGANIZATION     IS INDEXED                          
                           RECORD    KEY    IS K115-KEY-FLD                     
                           FILE STATUS      IS K115-STATUS.                     
                                                                                
           SELECT AB03K030   ASSIGN TO AB03K030                                 
                             ORGANIZATION    IS INDEXED                         
                             ACCESS MODE     IS RANDOM                          
                             RECORD KEY      IS K030-NUM-HDB-REF                
                             FILE STATUS     IS K030-STATUS.                    
                                                                                
           SELECT   AB03F021      ASSIGN TO AB03F021.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13F430                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13F430.                                                           
                                                                                
       FD  BP13KD00.                                                            
       COPY BP13KD00.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K115                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K115.                                                           
                                                                                
       FD   AB03K030.                                                           
       COPY AB03K030.                                                           
                                                                                
       FD   AB03F021                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY AB03F021.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F430-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K110-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K110-NOTFND              PIC 9(7)  VALUE ZEROS.              
           05  CNT-K115-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K115-NOTFND              PIC 9(7)  VALUE ZEROS.              
           05  CNT-F021-WRITE               PIC 9(7)  VALUE ZEROS.              
           05  CNT-CHILD                    PIC 9(1)  VALUE ZEROS.              
           05  CNT-HH-SIZE                  PIC 9(2)  VALUE ZEROS.              
           05  CNT-CANCEL                   PIC 9(7)  VALUE ZEROS.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  KD00-STATUS                  PIC 99    VALUE ZEROES.             
           05  K110-STATUS                  PIC 9(2)  VALUE ZERO.               
           05  K115-STATUS                  PIC 9(2)  VALUE ZERO.               
           05  K030-STATUS                  PIC 99    VALUE ZEROES.             
           05  WS-F430-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-KD00-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-EX-LESSEE                 PIC X(11) VALUE SPACES.             
           05  WS-NEW-LESSEE                PIC X(11) VALUE SPACES.             
           05  WS-EOF-K110                  PIC X(11) VALUE SPACE.              
               88 EOF-K110                  VALUE 'Y'.                          
           05  WS-EOF-K115                  PIC X(11) VALUE SPACE.              
               88 EOF-K115                  VALUE 'Y'.                          
           05  WS-K030-FND                  PIC X(11) VALUE SPACE.              
               88 K030-FND                  VALUE 'Y'.                          
           05  WS-KD00-FND                  PIC X(01) VALUE SPACE.              
               88  KD00-FND                 VALUE 'Y'.                          
           05  WS-HH-SIZE                   PIC 99    VALUE ZEROES.             
           05  WS-NUM-AGE                   PIC 9(08) VALUE ZEROES.             
           05  WS-AGE REDEFINES WS-NUM-AGE.                                     
               10  WS-AGE-CY                PIC 9(04).                          
               10  WS-AGE-MM                PIC 9(02).                          
               10  WS-AGE-DD                PIC 9(02).                          
           05  WS-F595-BALDATE              PIC X(8)  VALUE SPACES.             
       01  WS-CUR-DATE               PIC 9(08) VALUE ZEROES.                    
       01  WS-CUR-TIME               PIC 9(08) VALUE ZEROES.                    
       01  WS-CUR-TIME-X REDEFINES WS-CUR-TIME PIC X(08).                       
       01  WS-NEW-LES                PIC X(11) VALUE SPACES.                    
       01  WS-EX-LES                 PIC X(11) VALUE SPACES.                    
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-MAIN.                                                               
      *===============================================================*         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F430       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-F430-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       1000-OPEN-FILES.                                                         
      *===============================================================*         
                                                                                
           OPEN INPUT SY02F001                                                  
                      BM06K110                                                  
                      BM06K115                                                  
                      BP13F430                                                  
                      AB03K030                                                  
               OUTPUT AB03F021.                                                 
                                                                                
           READ SY02F001.                                                       
                                                                                
           IF K030-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING AB03K030 , STATUS = ' K030-STATUS          
              MOVE K030-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT             TO WS-CUR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2000-READ-BP13F430.                                                      
      *===============================================================*         
                                                                                
           READ BP13F430                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-F430-EOF                          
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F430-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3000-PROCESS-RECORD.                                                     
      *===============================================================*         
                                                                                
           PERFORM 4300-START-BM06K110               THRU 4300-EXIT.            
                                                                                
           PERFORM 2000-READ-BP13F430                THRU 2000-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4300-START-BM06K110.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                        TO WS-EOF-K110                       
                                              WS-K030-FND.                      
                                                                                
           INITIALIZE K110-REC                                                  
                      WS-NEW-LESSEE                                             
                      WS-EX-LESSEE                                              
                      WS-EX-LES                                                 
                      WS-NEW-LES.                                               
                                                                                
           MOVE F430-NUM-HDB-REF(1:9)      TO K110-KEY-FLD.                     
                                                                                
           START BM06K110 KEY >= K110-KEY-FLD.                                  
                                                                                
           EVALUATE K110-STATUS                                                 
             WHEN 00                                                            
             WHEN 02                                                            
                  PERFORM 4400-READNEXT-BM06K110   THRU 4400-EXIT               
                    UNTIL WS-EOF-K110 = 'Y'                                     
                                                                                
                  IF WS-K030-FND = 'N'                                          
                     PERFORM 8000-WRITE-AB03F021 THRU 8000-EXIT                 
                  END-IF                                                        
             WHEN 10                                                            
             WHEN 23                                                            
                  CONTINUE                                                      
             WHEN OTHER                                                         
                  DISPLAY '**BM06K110 - ERROR READING : '                       
                          K110-STATUS                                           
                  MOVE K110-STATUS                 TO RETURN-CODE               
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4350-START-BM06K115.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                        TO WS-EOF-K115                       
                                                                                
           INITIALIZE K115-REC                                                  
                                                                                
           MOVE F430-NUM-HDB-REF(1:9)      TO K115-KEY-FLD.                     
                                                                                
           START BM06K115 KEY >= K115-KEY-FLD.                                  
                                                                                
           EVALUATE K115-STATUS                                                 
             WHEN 00                                                            
             WHEN 02                                                            
                  PERFORM 4450-READNEXT-BM06K115   THRU 4450-EXIT               
                    UNTIL WS-EOF-K115 = 'Y'                                     
                                                                                
             WHEN 10                                                            
             WHEN 23                                                            
                  CONTINUE                                                      
             WHEN OTHER                                                         
                  DISPLAY '**BM06K115 - ERROR READING : '                       
                          K115-STATUS                                           
                  MOVE K115-STATUS                 TO RETURN-CODE               
           END-EVALUATE.                                                        
                                                                                
       4350-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4400-READNEXT-BM06K110.                                                  
      *--------------------------------------------------------------*          
                                                                                
           READ BM06K110 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'  TO  WS-EOF-K110                                    
           END-READ.                                                            
                                                                                
           EVALUATE K110-STATUS                                                 
            WHEN 00                                                             
            WHEN 02                                                             
                 DISPLAY K110-SCH-ACC-NO                                        
                 IF K110-SCH-ACC-NO(1:9) = F430-NUM-HDB-REF(1:9)                
                    IF WS-NEW-LES = SPACES                                      
                       MOVE K110-KEY-FLD  TO WS-NEW-LES                         
                    ELSE                                                        
                       IF WS-NEW-LES(10:1) > K110-KEY-FLD(10:1)                 
                          MOVE K110-KEY-FLD  TO WS-EX-LES                       
                       ELSE                                                     
                          MOVE WS-NEW-LES    TO WS-EX-LES                       
                          MOVE K110-KEY-FLD  TO WS-NEW-LES                      
                       END-IF                                                   
                    END-IF                                                      
                    ADD 1 TO CNT-K110-READ                                      
                    PERFORM 4500-READ-AB03K030  THRU 4500-EXIT                  
                 ELSE                                                           
                    PERFORM 4350-START-BM06K115 THRU 4350-EXIT                  
                    MOVE 'Y'       TO    WS-EOF-K110                            
                 END-IF                                                         
                                                                                
            WHEN 10                                                             
            WHEN 23                                                             
                 MOVE 'Y'          TO    WS-EOF-K110                            
                 ADD 1             TO    CNT-K110-NOTFND                        
            WHEN OTHER                                                          
                 DISPLAY '**BM06K110 - ERROR READING : '                        
                         K110-STATUS                                            
                 MOVE K110-STATUS                 TO RETURN-CODE                
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4450-READNEXT-BM06K115.                                                  
      *--------------------------------------------------------------*          
                                                                                
           READ BM06K115 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'  TO  WS-EOF-K115                                    
           END-READ.                                                            
                                                                                
           EVALUATE K115-STATUS                                                 
            WHEN 00                                                             
            WHEN 02                                                             
                 DISPLAY K115-SCH-ACC-NO                                        
                 IF K115-SCH-ACC-NO(1:9) = F430-NUM-HDB-REF(1:9)                
                   IF WS-NEW-LES = SPACES                                       
                      MOVE K115-KEY-FLD  TO WS-NEW-LES                          
                   ELSE                                                         
                      IF WS-NEW-LES(10:1) > K115-KEY-FLD(10:1)                  
                        IF K115-KEY-FLD(10:1) > WS-EX-LES(10:1)                 
                          MOVE K115-KEY-FLD  TO WS-EX-LES                       
                        END-IF                                                  
                      ELSE                                                      
                        IF WS-NEW-LES(10:1) < K115-KEY-FLD(10:1)                
                         MOVE WS-NEW-LES    TO WS-EX-LES                        
                         MOVE K115-KEY-FLD  TO WS-NEW-LES                       
                        END-IF                                                  
                      END-IF                                                    
                   END-IF                                                       
                   ADD 1 TO CNT-K115-READ                                       
                   MOVE K115-KEY-FLD     TO K110-KEY-FLD                        
                   PERFORM 4500-READ-AB03K030  THRU 4500-EXIT                   
                 ELSE                                                           
                    MOVE 'Y'       TO    WS-EOF-K115                            
                 END-IF                                                         
            WHEN 10                                                             
            WHEN 23                                                             
                 MOVE 'Y'          TO    WS-EOF-K115                            
                 ADD 1             TO    CNT-K115-NOTFND                        
            WHEN OTHER                                                          
                 DISPLAY '**BM06K115 - ERROR READING : '                        
                         K115-STATUS                                            
                 MOVE K115-STATUS                 TO RETURN-CODE                
           END-EVALUATE.                                                        
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4500-READ-AB03K030.                                                      
      *------------------------------------------------------------             
                                                                                
           INITIALIZE                 AB03K030-REC.                             
           MOVE K110-KEY-FLD       TO K030-NUM-HDB-REF.                         
                                                                                
           READ AB03K030.                                                       
                                                                                
           EVALUATE K030-STATUS                                                 
              WHEN 00                                                           
                   IF K030-NUM-HDB-REF = F430-NUM-HDB-REF                       
                      MOVE 'Y'          TO WS-K030-FND                          
                   END-IF                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                   MOVE 'N'             TO WS-K030-FND                          
              WHEN OTHER                                                        
                   DISPLAY 'READ AB03K030 ERROR ' K030-STATUS                   
                   MOVE K030-STATUS                 TO RETURN-CODE              
                   GO TO 9000-CLOSE-FILES                                       
            END-EVALUATE.                                                       
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       8000-WRITE-AB03F021.                                                     
      *===============================================================*         
                                                                                
           ACCEPT WS-CUR-TIME FROM TIME.                                        
           MOVE SPACES                   TO AB03F021-REC.                       
           MOVE 'C'                      TO F021-CDE-TRANS-FIS.                 
           MOVE 'OT'                     TO F021-CDE-REASON-FOR-CHGE.           
           MOVE 'BP13'                   TO F021-CDE-SYSTEM.                    
           MOVE 'D'                      TO F021-CDE-ACTION.                    
           MOVE WS-CUR-DATE              TO F021-DTE-UPDTE                      
                                            F021-DTE-CANCEL-WEF.                
           MOVE WS-CUR-TIME-X            TO F021-DATA-ENTRY-TIME.               
           MOVE WS-NEW-LES               TO WS-NEW-LESSEE.                      
           MOVE WS-NEW-LESSEE            TO F021-NUM-HDB-REF.                   
           MOVE WS-EX-LES                TO WS-EX-LESSEE.                       
           MOVE WS-EX-LESSEE             TO F021-NUM-HDB-REF-EXLESSEE.          
                                                                                
           WRITE AB03F021-REC.                                                  
           ADD 1                          TO CNT-F021-WRITE.                    
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       9000-CLOSE-FILES.                                                        
      *===============================================================*         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CD5C'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F430 READ          : ' CNT-F430-READ.            
           DISPLAY 'NO. OF BM06K110 READ          : ' CNT-K110-READ.            
           DISPLAY 'NO. OF BM06K110 NOT FND       : ' CNT-K110-NOTFND.          
           DISPLAY 'NO. OF BM06K115 READ          : ' CNT-K115-READ.            
           DISPLAY 'NO. OF BM06K115 NOT FND       : ' CNT-K115-NOTFND.          
           DISPLAY 'NO. OF RECS WRITTEN           : ' CNT-F021-WRITE.           
                                                                                
           CLOSE SY02F001                                                       
                 BP13F430                                                       
                 BM06K110                                                       
                 BM06K115                                                       
                 AB03F021.                                                      
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BM06K110 , STATUS = ' K110-STATUS          
           END-IF.                                                              
                                                                                
           IF K115-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BM06K115 , STATUS = ' K115-STATUS          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
