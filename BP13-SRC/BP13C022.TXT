      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C022.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   17/04/04.                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO MATCH JB-REGN NO OF SOC MASTER WITH F520   *                
      *          AND UPDATE IT WITH THE CORRECT REGN NO        *                
      *                                                        *                
      *     INPUT FILE:                                        *                
      *      1. BP13F520                                       *                
      *      2. BP13F800                                       *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *      1. BP13K800                                       *                
      *                                                        *                
      * CHG-NO   DATE   BY  DESCRIPTION                        *                
      * ------- ------  --  -----------                        *                
      * BP132496 150404 SK  NEW PROGRAM                        *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F520.                                                           
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05  WS-READ-F800        PIC 9(8)    VALUE 0.                         
           05  WS-READ-F520        PIC 9(8)    VALUE 0.                         
           05  WS-K800-NFND        PIC 9(8)    VALUE 0.                         
           05  WS-MATCH            PIC 9(8)    VALUE 0.                         
           05  WS-NOT-MATCH        PIC 9(8)    VALUE 0.                         
           05  WS-UPD-K800         PIC 9(8)    VALUE 0.                         
           05  WS-F800-EOF         PIC X(1)    VALUE 'N'.                       
           05  WS-F520-EOF         PIC X(1)    VALUE 'N'.                       
           05  WS-K800-STATUS      PIC 9(2)    VALUE 0.                         
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05 WS-SYS-CCYY          PIC X(04).                                   
           05 WS-SYS-MM            PIC X(02).                                   
           05 WS-SYS-DD            PIC X(02).                                   
                                                                                
       01  WS-EDITED-DATE          PIC X(10).                                   
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F520                                                 
                I-O    BP13K800.                                                
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K800-STATUS  TO RETURN-CODE                            
                 DISPLAY 'OPEN BP13K800 FAIL. STATUS=' WS-K800-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
           PERFORM 1000-READ-F800     THRU 1000-EXIT.                           
           PERFORM 1100-READ-F520     THRU 1100-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL ( F800-NUM-JT-BALLOT-REGN = HIGH-VALUES AND            
                           F520-NUM-REF = HIGH-VALUES)                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-READ-F800.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F800                                                        
                AT END MOVE 'Y'  TO WS-F800-EOF                                 
                MOVE HIGH-VALUES TO F800-NUM-JT-BALLOT-REGN                     
                GO TO 1000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F800.                                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1100-READ-F520.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F520                                                        
                AT END MOVE 'Y'  TO WS-F520-EOF                                 
                MOVE HIGH-VALUES TO F520-NUM-REF                                
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F520.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-PROCESS.                                                            
      *--------------------------------------------------------*                
                                                                                
           IF F520-NUM-REF = F800-NUM-JT-BALLOT-REGN                            
              ADD 1 TO WS-MATCH                                                 
              PERFORM 3000-READ-K800     THRU 3000-EXIT                         
              PERFORM 1100-READ-F520     THRU 1100-EXIT                         
              PERFORM 1000-READ-F800     THRU 1000-EXIT                         
           ELSE                                                                 
             IF F520-NUM-REF < F800-NUM-JT-BALLOT-REGN                          
                PERFORM 1100-READ-F520     THRU 1100-EXIT                       
             ELSE                                                               
                ADD 1 TO WS-NOT-MATCH                                           
                PERFORM 1000-READ-F800     THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-K800.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES     TO K800-NUM-REGN.                                
           MOVE F800-NUM-REGN  TO K800-NUM-REGN.                                
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 PERFORM 4000-UPDATE-K800 THRU 4000-EXIT                        
              WHEN 23                                                           
                 ADD 1 TO WS-K800-NFND                                          
                 DISPLAY 'REGN NOT FOUND IN BP13K800. REGN : '                  
                     F800-NUM-REGN                                              
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K800, STATUS = '                   
                         WS-K800-STATUS                                         
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-UPDATE-K800.                                                        
      *--------------------------------------------------------*                
                                                                                
           DISPLAY 'BEFORE UPDATE '.                                            
           DISPLAY 'K800-NUM-REGN : ' K800-NUM-REGN.                            
           DISPLAY 'K800-NUM-JT-BALLOT-REGN : ' K800-NUM-JT-BALLOT-REGN.        
                                                                                
           MOVE F520-NUM-REGN  TO K800-NUM-JT-BALLOT-REGN.                      
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-UPD-K800                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR UPDATING BP13K800, STATUS = '                  
                         WS-K800-STATUS                                         
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
           DISPLAY 'AFTER UPDATE '.                                             
           DISPLAY 'K800-NUM-REGN : ' K800-NUM-REGN.                            
           DISPLAY 'K800-NUM-JT-BALLOT-REGN : ' K800-NUM-JT-BALLOT-REGN.        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '   BP13C022      '.                                         
           DISPLAY '   DATE       :  ' WS-EDITED-DATE.                          
           DISPLAY SPACES.                                                      
           DISPLAY '   BP13F800 READ      : '  WS-READ-F800.                    
           DISPLAY '   BP13F520 READ      : '  WS-READ-F520.                    
           DISPLAY '   BP13K800 UPDATE    : '  WS-UPD-K800.                     
           DISPLAY '   MATCH RECORD       : '  WS-MATCH.                        
           DISPLAY '   NOT MATCH          : '  WS-NOT-MATCH.                    
           DISPLAY '********************  '.                                    
                                                                                
           CLOSE BP13F520                                                       
                 BP13K800                                                       
                 BP13F800.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
