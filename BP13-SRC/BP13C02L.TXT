       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C02L.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  27/08/2004.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO GENERATE REPORT ON MCPS POSTAL CODE.     *           
      *                 SIMILAR TO BP13C021 (FOR DBSS USE (INT))    *           
      *                                                             *           
      *   INPUT      :  BP13F725 -- SEQUENTIAL FILE                 *           
      *                 BP13F730 -- REQ FILE                        *           
      *   OUTPUT     :  BP13L02L                                    *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132583 27/08/2004 SD10 NEW PROGRAM                        *           
      * BP133323 22/07/2008 IL5  UPDATE PROJECT NAME                *           
      * BP133424 15/10/2008 IL5  UPDATE PROJECT NAME                *           
      * BP133542 20/03/2008 ZDD1 CHANGE OUTPUT  FILE                *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F725 ASSIGN TO BP13F725.                                  
           SELECT BP13F730 ASSIGN TO BP13F730                                   
                     FILE STATUS IS WS-F730-STAT.                               
           SELECT BP13L02L ASSIGN TO BP13L02L                                   
                     FILE STATUS IS WS-L02L-STAT.                               
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F725       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 211 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F725.                                                           
                                                                                
       FD  BP13F730       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 500 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F730.                                                           
                                                                                
       FD   BP13L02L                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13L02L-REC           PIC X(132).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F725-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F730-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-L02L-WRITE           PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-EOF-F725             PIC X       VALUE SPACE.                     
       01  WS-EOF-F730             PIC X       VALUE SPACE.                     
       01  WS-L02L-LINECTR         PIC 9(2)    VALUE 60.                        
       01  WS-L02L-PAGECTR         PIC 9(5)    VALUE ZEROES.                    
       01  WS-L02L-SNO             PIC 9(3)    VALUE ZEROES.                    
       01  WS-CTR                  PIC 9(5)    VALUE ZEROES.                    
       01  WS-REGN                 PIC X(8)    VALUE SPACES.                    
       01  WS-L02L-STAT            PIC 9(02)   VALUE ZEROES.                    
       01  WS-F730-STAT            PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC   PIC 9(2).                                    
                   15  WS-SYS-YY   PIC 9(2).                                    
               10  WS-SYS-MM       PIC 9(2).                                    
               10  WS-SYS-DD       PIC 9(2).                                    
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-MM    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-YY    PIC X(4)    VALUE SPACES.                    
                                                                                
      *============================================================*            
      *         BP13L02L -   R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L02L-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L02L'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L02L-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L02L-PAGE-NO        PIC ZZZZ9.                                   
                                                                                
       01  L02L-HEADER-02.                                                      
           05  FILLER              PIC X(10)     VALUE                          
               'PROJECT : '.                                                    
           05  L02L-PROJ-NAME      PIC X(22)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
               'DAILY REPORT ON MCPS REQUEST WHERE PARENT/CHILD'.               
                                                                                
       01  L02L-HEADER-2B.                                                      
           05  FILLER              PIC X(43)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
               'RESIDENCE IS AN HDB FLAT BUT IS MORE THAN 2KM '.                
                                                                                
       01  L02L-HEADER-2C.                                                      
           05  FILLER              PIC X(54)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
               'OR NOT IN SAME TOWN                           '.                
                                                                                
       01  L02L-HEADER-03.                                                      
           05  FILLER              PIC X(03)     VALUE 'SNO'.                   
           05  FILLER              PIC X         VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'APPL REGN'.             
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'APPL POSTAL'.           
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'MHA POSTAL'.            
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'BLK '.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'STREET'.                
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REMARKS'.               
                                                                                
       01  L02L-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L02L-DETAILS.                                                        
           05  L02L-SNO            PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02L-REGNO          PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02L-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L02L-F730-POSTAL    PIC X(06).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L02L-F725-POSTAL    PIC X(06).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L02L-ADDR1          PIC X(05).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02L-ADDR2          PIC X(32).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02L-ADDR3          PIC X(20).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02L-REMARKS        PIC X(11).                                   
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 3000-READ-F725     THRU 3000-EXIT.                           
           PERFORM 4000-READ-F730     THRU 4000-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
              UNTIL WS-EOF-F730 = 'Y' OR                                        
                    WS-EOF-F725 = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F725                                                 
                       BP13F730                                                 
                OUTPUT BP13L02L.                                                
           MOVE 'N' TO WS-EOF-F725.                                             
           MOVE 'N' TO WS-EOF-F730.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           MOVE   WS-EDITED-DATE      TO L02L-RUN-DATE.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-PROCESS.                                                            
      *****************************************************************         
                                                                                
                                                                                
      *  MATCH REGN-NO AND NRIC AND POSTAL CDE                                  
           IF F730-NUM-REGN = F725-NUM-REGN                                     
              MOVE SPACES                  TO L02L-DETAILS                      
              MOVE F730-NUM-REGN           TO L02L-REGNO                        
              MOVE F725-NUM-NRIC           TO L02L-NRIC                         
      *       MOVE F725-NME-PRINCIPAL      TO L02L-NAME                         
              MOVE F725-NUM-BLK-HSE        TO L02L-ADDR1                        
              MOVE F725-NME-STREET         TO L02L-ADDR2                        
              IF F725-NUM-STOREY NOT = SPACE AND LOW-VALUES                     
                 STRING '#'                                                     
                        F725-NUM-STOREY '-'                                     
                        F725-NUM-UNIT                                           
                        DELIMITED BY SIZE                                       
                        INTO L02L-ADDR3                                         
                 END-STRING                                                     
              ELSE                                                              
                  MOVE SPACES TO L02L-ADDR3                                     
              END-IF                                                            
              IF F725-NUM-NRIC = F730-NUM-RES-NRIC1 OR                          
                                 F730-NUM-RES-NRIC2                             
                 IF ((F725-CDE-POSTAL = F730-NUM-MCPS-POSTAL) AND               
                     (F725-NUM-NRIC = F730-NUM-RES-NRIC1))                      
                    MOVE 'P1 / C1'         TO L02L-REMARKS                      
                    MOVE F730-NUM-MCPS-POSTAL TO L02L-F730-POSTAL               
                    MOVE F725-CDE-POSTAL      TO L02L-F725-POSTAL               
                    PERFORM 5000-PRINT-RPT THRU 5000-EXIT                       
                    PERFORM 3000-READ-F725 THRU 3000-EXIT                       
                 ELSE                                                           
                 IF ((F725-CDE-POSTAL = F730-NUM-MCPS-POSTAL) AND               
                     (F725-NUM-NRIC = F730-NUM-RES-NRIC2))                      
                    MOVE 'P2 / C2'         TO L02L-REMARKS                      
                    MOVE F730-NUM-MCPS-POSTAL TO L02L-F730-POSTAL               
                    MOVE F725-CDE-POSTAL      TO L02L-F725-POSTAL               
                    PERFORM 5000-PRINT-RPT THRU 5000-EXIT                       
                    PERFORM 3000-READ-F725 THRU 3000-EXIT                       
                 ELSE                                                           
                    MOVE 'NOT MATCHED'     TO L02L-REMARKS                      
                    MOVE F730-NUM-MCPS-POSTAL TO L02L-F730-POSTAL               
                    MOVE F725-CDE-POSTAL      TO L02L-F725-POSTAL               
                    PERFORM 5000-PRINT-RPT THRU 5000-EXIT                       
                    PERFORM 3000-READ-F725 THRU 3000-EXIT                       
                 END-IF                                                         
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 3000-READ-F725    THRU 3000-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              IF F730-NUM-REGN < F725-NUM-REGN                                  
                 PERFORM 4000-READ-F730    THRU 4000-EXIT                       
              ELSE                                                              
                 PERFORM 3000-READ-F725    THRU 3000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-F725.                                                          
      *****************************************************************         
                                                                                
           READ BP13F725 AT END MOVE 'Y' TO WS-EOF-F725                         
                GO TO 3000-EXIT.                                                
            ADD 1 TO WS-F725-READ.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-F730.                                                          
      *****************************************************************         
                                                                                
           READ BP13F730 AT END MOVE 'Y' TO WS-EOF-F730                         
                GO TO 4000-EXIT.                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-PRINT-RPT.                                                          
      *****************************************************************         
                                                                                
                                                                                
           IF F730-NUM-REGN NOT = WS-REGN                                       
             ADD 1                     TO WS-L02L-SNO                           
             MOVE WS-L02L-SNO          TO L02L-SNO                              
             MOVE F730-NUM-REGN        TO WS-REGN                               
           ELSE                                                                 
             MOVE SPACES               TO L02L-SNO                              
           END-IF.                                                              
                                                                                
           IF WS-L02L-LINECTR >= 53                                             
              PERFORM 6000-CHECK-PROJ-NAME  THRU 6000-EXIT                      
              ADD                  1 TO WS-L02L-PAGECTR                         
              MOVE WS-L02L-PAGECTR   TO L02L-PAGE-NO                            
              WRITE BP13L02L-REC   FROM L02L-HEADER-01 AFTER PAGE               
              WRITE BP13L02L-REC   FROM L02L-HEADER-02                          
              WRITE BP13L02L-REC   FROM L02L-HEADER-2B                          
              WRITE BP13L02L-REC   FROM L02L-HEADER-2C                          
              WRITE BP13L02L-REC   FROM L02L-HEADER-03 AFTER 2                  
              WRITE BP13L02L-REC   FROM L02L-HEADER-04                          
              WRITE BP13L02L-REC   FROM L02L-DETAILS                            
              MOVE                 7 TO WS-L02L-LINECTR                         
           ELSE                                                                 
              ADD                  1 TO WS-L02L-LINECTR                         
              WRITE BP13L02L-REC   FROM L02L-DETAILS                            
           END-IF.                                                              
           ADD 1 TO WS-L02L-WRITE.                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6000-CHECK-PROJ-NAME.                                                    
      *****************************************************************         
           IF F730-CDE-NT1 = 'TP'                                               
              MOVE 'THE PEAK' TO L02L-PROJ-NAME                                 
           ELSE                                                                 
              IF F730-CDE-NT1 = 'TAP'                                           
                MOVE 'PARC LUMIERE' TO L02L-PROJ-NAME                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY '*-----------  BP13C02L ------------*'.                      
           DISPLAY ' F725-READ       ' WS-F725-READ.                            
           DISPLAY ' F730-READ       ' WS-F730-READ.                            
           DISPLAY ' L02L-WRITE      ' WS-L02L-WRITE.                           
           CLOSE      BP13F725                                                  
                      BP13F730                                                  
                      BP13L02L.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
