       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB81.                                                 
      *AUTHOR.        ZDD1                                                      
      *DATE-WRITTEN.  18DEC2009.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. UPDATE NUMBER OF CHANCES                         *                
      *                                                        *                
      *                                                        *                
      *                                                        *                
      *    INPUT FILES  : BP13F730                             *                
      *    I-O            BP13K730                             *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP13XXXX ZDD 20091218  NEW PGM                          *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K730-KEY-NEW                               
                  FILE STATUS     IS WS-K730-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F730-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-K730-FLAG-FOUND             PIC X(1)  VALUE 'N'.              
           05  WS-K730-STATUS                 PIC X(2)  VALUE SPACES.           
           05  WS-F730-READ                   PIC 9(6)  VALUE ZEROS.            
           05  WS-K730-FOUND                  PIC 9(6)  VALUE ZEROS.            
           05  WS-K730-NOT-FOUND              PIC 9(6)  VALUE ZEROS.            
           05  WS-K730-UPDATED                PIC 9(6)  VALUE ZEROS.            
           05  WS-SP                          PIC 9(2)  VALUE ZEROS.            
           05  WS-4P                          PIC 9(2)  VALUE ZEROS.            
           05  WS-CHANCES                     PIC 9(2)  VALUE ZEROS.            
           05  WS-PRIO-SP                     PIC 9(2)  VALUE ZEROS.            
           05  WS-PRIO-4P                     PIC 9(2)  VALUE ZEROS.            
                                                                                
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F730         THRU 2000-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-F730      THRU 3000-EXIT                        
                  UNTIL WS-F730-EOF    = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F730                                                 
                I-O    BP13K730.                                                
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 OPEN ERROR, STATUS IS ' WS-K730-STATUS          
              MOVE WS-K730-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F730.                                                          
      ****************************************************************          
                                                                                
           READ BP13F730                                                        
              AT END MOVE 'Y' TO WS-F730-EOF                                    
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-F730-READ.                              
                                                                                
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2500-READ-BP13K730.                                                      
      ****************************************************************          
                                                                                
           MOVE  'N'           TO WS-K730-FLAG-FOUND.                           
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
             WHEN 00                                                            
               ADD 1     TO WS-K730-FOUND                                       
               MOVE 'Y'  TO WS-K730-FLAG-FOUND                                  
             WHEN 23                                                            
               MOVE 'N'  TO WS-K730-FLAG-FOUND                                  
               ADD 1     TO WS-K730-NOT-FOUND                                   
               DISPLAY 'RECORD NOT FOUND' K730-KEY-FLD                          
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BP13K730'                                 
               MOVE WS-K730-STATUS TO RETURN-CODE                               
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F730.                                                       
      ****************************************************************          
                                                                                
           MOVE  SPACES                      TO K730-KEY-NEW.                   
           MOVE  F730-KEY-NEW                TO K730-KEY-NEW.                   
           PERFORM  2500-READ-BP13K730       THRU 2500-EXIT.                    
                                                                                
           MOVE ZEROES TO WS-PRIO-SP WS-PRIO-4P.                                
                                                                                
           IF WS-K730-FLAG-FOUND = 'Y'                                          
             MOVE K730-NUM-RANDOM-PRIORITY    TO WS-PRIO-SP                     
             MOVE  SPACES                     TO K730-KEY-NEW                   
             MOVE K730-NUM-JB-REGN            TO K730-KEY-NEW                   
             PERFORM  2500-READ-BP13K730      THRU 2500-EXIT                    
             IF WS-K730-FLAG-FOUND = 'Y'                                        
                MOVE K730-NUM-RANDOM-PRIORITY TO WS-PRIO-4P                     
             END-IF                                                             
             PERFORM  4000-PROCESS-RECORD     THRU 4000-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM  2000-READ-F730            THRU 2000-EXIT.                   
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-PROCESS-RECORD.                                                     
      ****************************************************************          
                                                                                
           IF WS-PRIO-SP(1:1) = 0                                               
              MOVE WS-PRIO-SP(2:1) TO WS-SP                                     
           ELSE                                                                 
              MOVE WS-PRIO-SP      TO WS-SP                                     
           END-IF.                                                              
                                                                                
           IF WS-PRIO-4P(1:1) = 0                                               
              MOVE WS-PRIO-4P(2:1) TO WS-4P                                     
           ELSE                                                                 
              MOVE WS-PRIO-4P  TO WS-4P                                         
           END-IF.                                                              
                                                                                
           COMPUTE WS-CHANCES = WS-SP * WS-4P.                                  
                                                                                
           MOVE  SPACES                 TO K730-KEY-NEW.                        
           MOVE  F730-KEY-NEW           TO K730-KEY-NEW.                        
           PERFORM  2500-READ-BP13K730  THRU 2500-EXIT.                         
           PERFORM 5000-UPDATE-BP13K730 THRU 5000-EXIT.                         
                                                                                
           MOVE  SPACES                 TO K730-KEY-NEW.                        
           MOVE  K730-NUM-JB-REGN       TO K730-KEY-NEW.                        
           PERFORM  2500-READ-BP13K730  THRU 2500-EXIT.                         
           PERFORM 5000-UPDATE-BP13K730 THRU 5000-EXIT.                         
                                                                                
       4000-EXIT.                                                               
                                                                                
      ****************************************************************          
       5000-UPDATE-BP13K730.                                                    
      ****************************************************************          
                                                                                
           MOVE WS-CHANCES TO K730-NUM-RANDOM-PRIORITY.                         
                                                                                
            REWRITE BP13K730-REC.                                               
                                                                                
            EVALUATE WS-K730-STATUS                                             
                                                                                
            WHEN 00                                                             
               ADD 1 TO WS-K730-UPDATED                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR REWRITING BP13K730. STATUS IS '                   
                        WS-K730-STATUS                                          
               MOVE WS-K730-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13F730 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F730 RECORDS READ        = ' WS-F730-READ.              
           DISPLAY 'NO OF REC FOUND BP13K730     = ' WS-K730-FOUND.             
           DISPLAY 'NO OF REC NOT FOUND BP13K730 = ' WS-K730-NOT-FOUND.         
           DISPLAY 'NO OF REC UPDATED BP13K730   = ' WS-K730-UPDATED.           
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F730                                                       
                 BP13K730.                                                      
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 CLOSE ERROR, STATUS IS ' WS-K730-STATUS         
              MOVE WS-K730-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
