       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C41M.                                                    
      *DATE-WRITTEN. 03 SEP 2018.                                               
      *AUTHOR.       DEEPIKA NALLAM.                                            
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO CREATE A REPORT OF THE INTEREST RATE AND *            
      *                OUTSTANDING BALANCE.                        *            
      *  INPUT    :    BP13F410                                    *            
      *           :    AB03K030                                    *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP137449 20180903 DN8   NEW PROGRAM                        *            
      * BP137489 20180927 DN8   TO INCLUDE MAIL STATEMENTS OF THE  *            
      *                         RECIPIENTS IN PROGRAM                           
      * BP138757 20210615 KV8   TO REMOVE LEE ADDED BERNICE        *            
      * BP139030 20220107 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139451 20230227 KV8   TO REMOVE BERNICE & SERENA ADDED                
      *                         TO Shamsurianor & SIM XUE YING                  
      * BP139276 20240405 KAC1  REPLACE LIM KOK CHUN BY            *            
      *                                 KATHIJAH_BEE_ALI_MOHAMED   *            
      * BP139175 20240429 KAC1  REPL Bernice_JH_GAN@hdb.gov.sg WITH*            
      *                              Iris_LL_KOR@hdb.gov.sg        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT BP13F410  ASSIGN          TO BP13F410                         
                            ORGANIZATION    IS SEQUENTIAL                       
                            FILE STATUS     IS WS-F410-STATUS.                  
      *                                                                         
           SELECT AB03K030  ASSIGN          TO AB03K030                         
                            ORGANIZATION    IS INDEXED                          
                            ACCESS MODE     IS DYNAMIC                          
                            RECORD KEY      IS K030-NUM-HDB-REF                 
                            FILE STATUS     IS WS-AB03-STATUS.                  
      *                                                                         
           SELECT BP13F411  ASSIGN          TO BP13F411                         
                            ORGANIZATION    IS SEQUENTIAL                       
                            FILE STATUS     IS WS-F411-STATUS.                  
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13F410                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
      *                                                                         
       FD   BP13F411                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 0250 CHARACTERS.                                    
       01   BP13F411-RECORD            PIC X(250).                              
      *                                                                         
       FD AB03K030.                                                             
       COPY AB03K030.                                                           
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *-------------------------*                                               
       01 WS-F410-STATUS               PIC 9(02).                               
       01 WS-F411-STATUS               PIC 9(02).                               
       01 WS-AB03-STATUS               PIC 9(02).                               
       01 WS-MESSAGE                   PIC X(80) VALUE SPACES.                  
       01 WS-RETURN-CODE               PIC X(02) VALUE SPACES.                  
       01 WS-KEY                       PIC X(11) VALUE SPACES.                  
       01 WS-EOF                       PIC X(01) VALUE 'N'.                     
       01 WS-RECORD-FOUND              PIC X(01) VALUE SPACES.                  
       01 WS-FIRST-REPORT              PIC X(01) VALUE 'Y'.                     
       01 WS-RPT-COUNT                 PIC 9(07) VALUE ZEROES.                  
       01 WS-READ-COUNT                PIC 9(07) VALUE ZEROES.                  
       01 WS-TM-COUNT                  PIC 9(07) VALUE ZEROES.                  
       01 WS-NOT-COUNT                 PIC 9(07) VALUE ZEROES.                  
       01 WS-CUR-DATE.                                                          
          05  WS-CUR-CCYY              PIC 9(04).                               
          05  WS-CUR-MM                PIC 9(02).                               
          05  WS-CUR-DD                PIC 9(02).                               
      *                                                                         
       01 WS-HEADER1-RPT.                                                       
          05 FILLER                    PIC X(10) VALUE SPACES.                  
          05 FILLER                    PIC X(75)                                
               VALUE 'REPORT ON INTEREST RATES AND OUTSTANDING BALANCE          
      -        'FOR TEMP LOAN '.                                                
          05 FILLER                    PIC X(25) VALUE SPACES.                  
      *                                                                         
       01 WS-HEADER-REC.                                                        
          05 HEADER-0                  PIC X(07) VALUE 'S.NO   '.               
          05 HEADER-1                  PIC X(40) VALUE                          
              'SALES REGISTRATION   NEW FLAT SCHEME    '.                       
          05 HEADER-2                  PIC X(40) VALUE                          
              'KEY COLLECTION DATE   HDB LOAN GRANTED  '.                       
          05 HEADER-3                  PIC X(40) VALUE                          
              'INTEREST RATE   OUTSTANDING LOAN BALANCE'.                       
      *                                                                         
       01 WS-MAIL-HEADER.                                                       
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(60)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT4             PIC X(60)                               
               VALUE 'RCPT TO:<Iris_LL_KOR@hdb.gov.sg>'.                        
      *        VALUE 'RCPT TO:<LEE_LI_PENG@HDB.GOV.SG>'.                        
           05  WS-MAIL-RPT5             PIC X(60)                               
               VALUE 'RCPT TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT6             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT7             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT8             PIC X(60)                               
               VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT8A            PIC X(60)                               
               VALUE 'TO:<Iris_LL_KOR@hdb.gov.sg>'.                             
      *        VALUE 'TO:<LEE_LI_PENG@HDB.GOV.SG>'.                             
           05  WS-MAIL-RPT9             PIC X(60)                               
               VALUE 'TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT9A            PIC X(60)                               
               VALUE 'TO:<Shamsurianor_SIBENGAT@hdb.gov.sg>'.                   
           05  WS-MAIL-RPT9B            PIC X(60)                               
               VALUE 'TO:<SIM_Xue_Ying@hdb.gov.sg>'.                            
           05  WS-MAIL-RPT10            PIC X(80)                               
               VALUE 'SUBJECT : REPORT ON INTEREST RATES AND OUTSTANDING        
      -        'BALANCE FOR TEMP LOAN'.                                         
           05  WS-MAIL-RPT11            PIC X(41)                               
               VALUE 'Mime-Version: 1.0                        '.               
           05  WS-MAIL-RPT12            PIC X(80)                               
               VALUE 'Content-type: multipart/mixed; boundary="Mime.Part        
      -         '.Boundary"'.                                                   
           05  WS-MAIL-RPT13            PIC X(80)                               
               VALUE 'This is a multi-part message in MIME format.'.            
           05  WS-MAIL-RPT14            PIC X(80)                               
               VALUE '--Mime.Part.Boundary                        '.            
           05  WS-MAIL-RPT15            PIC X(80)                               
               VALUE 'Content-Type: text/plain; charset="iso-8859-1"'.          
           05  WS-MAIL-RPT16            PIC X(80)                               
               VALUE 'Content-Transfer-Encoding: 7bit               '.          
           05  WS-MAIL-RPT17            PIC X(80)                               
               VALUE 'Content-disposition: attachment;              '.          
           05  WS-MAIL-RPT18            PIC X(80)                               
               VALUE '           FILENAME="REPORT.TXT"              '.          
      *                                                                         
       01   WS-DETAIL-RPT.                                                      
            05 WS-SNO-DTL               PIC ZZ99   VALUE ZEROES.                
            05 FILLER                   PIC X(03)  VALUE SPACES.                
            05 SALES-REG                PIC X(08).                              
            05 FILLER                   PIC X(13)  VALUE SPACES.                
            05 NEW-FLAT-SCHM            PIC X(11).                              
            05 FILLER                   PIC X(09)  VALUE SPACES.                
            05 COLL-DATE                PIC X(08).                              
            05 FILLER                   PIC X(15)  VALUE SPACES.                
            05 HDB-LOAN-GRAN            PIC Z,ZZZ,ZZ9.99.                       
            05 FILLER                   PIC X(08) VALUE SPACES.                 
            05 INTEREST-RATE            PIC Z9.99.                              
            05 FILLER                   PIC X(14)  VALUE SPACES.                
            05 OUTS-LN-BAL              PIC Z,ZZZ,ZZ9.99.                       
            05 FILLER                   PIC X(128) VALUE SPACES.                
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *---------------*                                                         
       0000-MAIN-PARA.                                                          
      *---------------*                                                         
           PERFORM 1000-OPEN-PARA.                                              
           PERFORM 2000-PROCESS-PARA  UNTIL WS-EOF = 'Y'.                       
           WRITE BP13F411-RECORD FROM WS-MAIL-RPT14                             
           PERFORM 4000-CLOSE-PARA.                                             
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
       0000-EXIT.                                                               
           EXIT.                                                                
      *---------------*                                                         
       1000-OPEN-PARA.                                                          
      *---------------*                                                         
           OPEN INPUT BP13F410                                                  
                      AB03K030                                                  
               OUTPUT BP13F411                                                  
      *                                                                         
                DISPLAY WS-F410-STATUS ';' WS-AB03-STATUS ';'                   
                                                                                
                MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-CUR-DATE                
      *                                                                         
                EVALUATE TRUE                                                   
      *                                                                         
                  WHEN  WS-AB03-STATUS NOT EQUAL TO 00                          
                   MOVE 'ERROR OPENING THE AB03K030   ' TO WS-MESSAGE           
                   MOVE WS-AB03-STATUS TO WS-RETURN-CODE                        
                   PERFORM 5000-ERROR-PARA THRU 5000-EXIT                       
      *                                                                         
                  WHEN  OTHER                                                   
                   DISPLAY 'SUCCESSFUL IN OPENING THE FILES'                    
      *                                                                         
                END-EVALUATE.                                                   
      *                                                                         
       1000-EXIT.                                                               
           EXIT.                                                                
      *------------------*                                                      
       2000-PROCESS-PARA.                                                       
      *------------------*                                                      
           READ BP13F410                                                        
              AT END MOVE 'Y' TO WS-EOF                                         
              NOT AT END                                                        
              ADD 1                               TO WS-READ-COUNT              
               IF F410-NUM-CONTRA-TYPE = 'TM'                                   
              ADD 1                               TO WS-TM-COUNT                
                PERFORM 2200-READ-AB03K030      THRU 2200-EXIT                  
               END-IF                                                           
           END-READ.                                                            
      *                                                                         
       2000-EXIT.                                                               
           EXIT.                                                                
      *-------------------*                                                     
       2200-READ-AB03K030.                                                      
      *-------------------*                                                     
              INITIALIZE K030-NUM-HDB-REF                                       
              MOVE F410-NUM-SCH-ACC                TO K030-NUM-HDB-REF          
              READ AB03K030                                                     
               KEY IS K030-NUM-HDB-REF                                          
               MOVE   K030-NUM-HDB-REF             TO  WS-KEY                   
      *                                                                         
            EVALUATE TRUE                                                       
              WHEN WS-AB03-STATUS = 00                                          
                MOVE  'Y'                          TO WS-RECORD-FOUND           
                ADD 1                              TO WS-RPT-COUNT              
                PERFORM 2300-CREATE-F411-REPORT  THRU 2300-EXIT                 
                WRITE   BP13F411-RECORD          FROM WS-DETAIL-RPT             
                                                                                
                WHEN WS-AB03-STATUS = 23                                        
                 MOVE 'N'                          TO WS-RECORD-FOUND           
                 INITIALIZE                           AB03K030-REC              
                 ADD 1                             TO WS-NOT-COUNT              
      *                                                                         
                WHEN OTHER                                                      
                 MOVE 'ERROR READING THE AB03K030' TO WS-MESSAGE                
                 MOVE WS-AB03-STATUS               TO WS-RETURN-CODE            
                 PERFORM 5000-ERROR-PARA         THRU 5000-EXIT                 
      *                                                                         
            END-EVALUATE.                                                       
      *                                                                         
       2200-EXIT.                                                               
           EXIT.                                                                
      *                                                                         
      *------------------------*                                                
       2300-CREATE-F411-REPORT.                                                 
      *------------------------*                                                
            IF WS-FIRST-REPORT = 'Y'                                            
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT1                          
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT2                          
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT3                          
      *        WRITE BP13F411-RECORD FROM WS-MAIL-RPT4                          
      *        WRITE BP13F411-RECORD FROM WS-MAIL-RPT5                          
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT6                          
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT7                          
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT8                          
      *        WRITE BP13F411-RECORD FROM WS-MAIL-RPT8A                         
      *        WRITE BP13F411-RECORD FROM WS-MAIL-RPT9                          
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT9A                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT9B                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT10                         
               STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                   
                      DELIMITED BY SIZE INTO WS-DATE-RPT                        
               WRITE BP13F411-RECORD FROM WS-DATE-RPT-DTL                       
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT11                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT12                         
               MOVE SPACES TO  BP13F411-RECORD                                  
               WRITE   BP13F411-RECORD                                          
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT13                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT14                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT15                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT16                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT17                         
               WRITE BP13F411-RECORD FROM WS-MAIL-RPT18                         
               MOVE    'N'             TO WS-FIRST-REPORT                       
               MOVE SPACES             TO BP13F411-RECORD                       
               WRITE BP13F411-RECORD                                            
               WRITE BP13F411-RECORD FROM WS-HEADER1-RPT                        
               MOVE SPACES             TO BP13F411-RECORD                       
               WRITE BP13F411-RECORD                                            
               WRITE BP13F411-RECORD  FROM  WS-HEADER-REC                       
            END-IF.                                                             
                 MOVE WS-RPT-COUNT             TO WS-SNO-DTL.                   
                 MOVE F410-NUM-REGN            TO SALES-REG                     
                 MOVE F410-NUM-SCH-ACC         TO NEW-FLAT-SCHM                 
                 MOVE F410-DTE-KEY-ISSUE       TO COLL-DATE                     
                 MOVE K030-AMT-LOAN-GRANTD     TO HDB-LOAN-GRAN                 
                 MOVE F410-NUM-INTEREST-RATE   TO INTEREST-RATE                 
                 MOVE K030-AMT-LOAN-BAL        TO OUTS-LN-BAL.                  
      *                                                                         
       2300-EXIT.                                                               
           EXIT.                                                                
      *----------------*                                                        
       4000-CLOSE-PARA.                                                         
      *----------------*                                                        
                                                                                
           DISPLAY 'TOTAL RECORDS READ FROM F410 FILE   : '                     
                   WS-READ-COUNT.                                               
           DISPLAY 'TOTAL RECORD WITH CONTRA TYPE "TM"  : '                     
                   WS-TM-COUNT.                                                 
           DISPLAY 'TOTAL RECORD WRITTEN TO REPORT FILE : '                     
                   WS-RPT-COUNT.                                                
           DISPLAY 'RECORDS NOT FOUND IN AB03K030  FILE : '                     
                   WS-NOT-COUNT.                                                
                                                                                
           CLOSE BP13F410                                                       
                 AB03K030                                                       
                 BP13F411                                                       
      *                                                                         
                EVALUATE TRUE                                                   
                  WHEN  WS-AB03-STATUS NOT EQUAL TO 00                          
                   MOVE 'ERROR CLOSING THE AB03K030 ' TO WS-MESSAGE             
                   MOVE WS-AB03-STATUS                TO WS-RETURN-CODE         
                   PERFORM 5000-ERROR-PARA          THRU 5000-EXIT              
      *                                                                         
                  WHEN  OTHER                                                   
                   DISPLAY 'SUCCESSFUL IN CLOSING THE FILES'                    
      *                                                                         
                END-EVALUATE.                                                   
      *                                                                         
       4000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       5000-ERROR-PARA.                                                         
      *----------------------------------------------------------               
            DISPLAY WS-MESSAGE ';' WS-RETURN-CODE.                              
      *                                                                         
       5000-EXIT.                                                               
           EXIT.                                                                
      *                                                                         
