      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID. BP13C31P.                                                    
       AUTHOR. AJITH KUMAR T.                                                   
       DATE-WRITTEN. 26TH JULY 1999.                                            
                                                                                
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *  OBJECTIVE :                                                *           
      *    1.   TO READ AG07F640 FOR VALUE DATE, IBG REF AND CREATE *           
      *         BB14F140 FOR PASSING TO IRSA.                       *           
      *    2.   UPDATE BP13K625 ON PAYMENT INFO.                    *           
      *                                                             *           
      *  FILES :                                                    *           
      *  INPUT  -    AG07F640                                       *           
      *  I-O    -    BP13K625  (BP13.K625.STAMPFEE)                 *           
      *    O    -    BB14F140  (BP13.BB14.F140.SENTIRAS)            *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG-NO   DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---  -----------                          *           
      * BP130696 26071999 AKT  CREATE STAMPING FILE FOR IRAS        *           
      * BP130806 20032000 CLT  CONVERT LEASE TERMS TO MONTHS.       *           
      * BP130883 07082000 RAS  TO CHANGE BP13C909 TO BP13C913       *           
      * BP131058 27072001 MCC  TO UPD DATE OF PRINCIPLE AGREEMENT   *           
      * BP132185 30072002 MALC UPDTE F140-DTE-DOC-PREP BASE ON K625 *           
      * BP132729 28102005 JF4  TO SKIP AG07 REJECTED CASES          *           
      * BP135096 16102013 JGO1 UPDATE VOUCHER FROM 'SU' TO 'SS'     *           
      * BP138916 21102022 ZAR7 UPDATE CALLING ON BP13C913           *           
      * BP138916 28102022 ZAR7 PASSING OF DETAIL AND PARTY          *           
      * BP138916 07112022 ZAR7 CHANGE SD ACCT                       *           
      *=============================================================*           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
                                                                                
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB14F140     ASSIGN   TO       BB14F140                       
                  ORGANIZATION     IS  SEQUENTIAL.                              
                                                                                
           SELECT AG07F640     ASSIGN   TO       AG07F640                       
                  ORGANIZATION     IS  SEQUENTIAL.                              
                                                                                
           SELECT SY02F001     ASSIGN   TO SY02F001.                            
                                                                                
           SELECT BP13K625     ASSIGN   TO      BP13K625                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K625-KEY-FLD                             
                  ALTERNATE RECORD KEY IS K625-ALT-KEY1                         
                                   WITH DUPLICATES                              
                  ALTERNATE RECORD KEY IS K625-ALT-KEY2                         
                                   WITH DUPLICATES                              
                  FILE STATUS      IS  WS-K625-STATUS.                          
                                                                                
           SELECT BM06K120     ASSIGN   TO      BM06K120                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K120-KEY-FLD                             
                  FILE STATUS      IS  WS-K120-STATUS.                          
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
      *---------------*                                                         
       FILE SECTION.                                                            
      *---------------*                                                         
                                                                                
       FD   BB14F140                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  400 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BB14F140.                                                           
                                                                                
       FD   AG07F640                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  100 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AG07F640.                                                           
                                                                                
       FD   BP13K625                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K625.                                                           
                                                                                
       FD   BM06K120                                                            
            RECORD CONTAINS  700 CHARACTERS.                                    
       COPY BM06K120.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K625-STATUS         PIC X(02).                                
           05  WS-K120-STATUS         PIC X(02).                                
                                                                                
       01  WS-EOF-VARIABLES.                                                    
           05  WS-K625-EOF            PIC X VALUE 'N'.                          
           05  WS-F640-EOF            PIC X VALUE 'N'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F140-TOT-WRITE      PIC 9(08)    VALUE ZEROES.                
           05  WS-F140-BAT-WRITE      PIC 9(08)    VALUE ZEROES.                
           05  WS-F140-DET-WRITE      PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-READ           PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-WRITE          PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-FND            PIC 9(08)    VALUE ZEROES.                
           05  WS-K625-NOT-FND        PIC 9(08)    VALUE ZEROES.                
           05  WS-F640-READ           PIC 9(08)    VALUE ZEROES.                
           05  WS-F640-SKIP           PIC 9(08)    VALUE ZEROES.                
           05  WS-CTR                 PIC 9(03)    VALUE ZEROES.                
           05  WS-CTR1                PIC 9(03)    VALUE ZEROES.                
           05  WS-NUM-LEASE-TERM1     PIC 9(04)V99 VALUE ZEROES.                
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY                PIC 9(04).                                
           05  WS-MM                  PIC 9(02).                                
           05  WS-DD                  PIC 9(02).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-BATCH           PIC X(20).                                
           05  WS-TOT-AMT-STAMP       PIC 9(16)V99 VALUE ZEROES.                
           05  WS-BP13C913             PIC X(8)        VALUE 'BP13C913'.        
                                                                                
           05  WS-F140-HSEMAIN.                                                 
               10  WS-F140-HSE-MAIN       PIC X(4).                             
               10  WS-F140-HSE-SUB.                                             
                   15  WS-F140-HSE-SUB1   PIC X.                                
                   15  WS-F140-HSE-SUB2   PIC XX.                               
                                                                                
       01  WS-DETAIL-TABLE.                                                     
           05  WS-DETAIL       OCCURS    200          TIMES.                    
               10  WS-NUM-HDB-REF          PIC X(11).                           
               10  WS-NUM-DOC              PIC X(03).                           
               10  WS-DTE-DOC-PREPARE      PIC X(08).                           
               10  WS-NUM-DOC-TYP          PIC X(01).                           
               10  WS-AMT-PURCHASE-PRICE   PIC 9(13)V99.                        
               10  WS-AMT-ANNUAL-RENT      PIC 9(13)V99.                        
               10  WS-NUM-INCREASE-SCCC    PIC X(1).                            
               10  WS-AMT-LOAN             PIC 9(13)V99.                        
               10  WS-AMT-STAMP-DUTY       PIC 9(13)V99.                        
               10  WS-AMT-STAMP-DUTY-DUP   PIC 9(13)V99.                        
               10  WS-NUM-LEASE-TERM       PIC 9(04)V99.                        
               10  WS-NUM-REGN             PIC X(08).                           
                                                                                
       01  SQL-CODE                    PIC 9(4).                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *================================================================*        
       0000-CONTROL.                                                            
      *================================================================*        
      *---------------------------------                                        
      *   OPENS PROCESSS AND CLOSES FILES                                       
      *----------------------------------                                       
            PERFORM  1000-OPEN-FILES    THRU     1000-EXIT.                     
                                                                                
            PERFORM  2000-READ-AG07F640 THRU     2000-EXIT                      
                     UNTIL WS-F640-EOF = 'Y'.                                   
                                                                                
            PERFORM  9000-CLOSE-FILES   THRU     9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
      *------------------------------------                                     
      *   OPENS FILES & CHECKS FILE STATUS                                      
      *------------------------------------                                     
           OPEN                                                                 
                   INPUT       AG07F640                                         
                               BM06K120                                         
                               SY02F001                                         
                   OUTPUT      BB14F140                                         
                   I-O         BP13K625.                                        
                                                                                
           IF    WS-K625-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K625' WS-K625-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF    WS-K120-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BM06K120' WS-K120-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
      *----------------------*                                                  
      *   GETTING SYSTEM DATE                                                   
      *----------------------*                                                  
           READ SY02F001                                                        
           AT END                                                               
              DISPLAY 'EMPTY FILE - SY02F001 '                                  
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT.                         
                                                                                
           MOVE F001-DTE-CURRENT       TO WS-DATE.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2000-READ-AG07F640.                                                      
      *================================================================*        
      *-------------------*                                                     
      *   READS INPUT FILE                                                      
      *-------------------*                                                     
            READ  AG07F640 AT END MOVE 'Y' TO    WS-F640-EOF                    
                  GO TO    2000-EXIT.                                           
                                                                                
            ADD   1  TO    WS-F640-READ.                                        
                                                                                
            IF F640-NUM-REJECT NOT = SPACES                                     
               ADD   1           TO WS-F640-SKIP                                
               CONTINUE                                                         
            ELSE                                                                
               MOVE F640-NUM-VR  TO K625-ALT-KEY1                               
               MOVE 'N'          TO WS-K625-EOF                                 
               PERFORM 2100-START-BP13K625   THRU 2100-EXIT                     
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2100-START-BP13K625.                                                     
      *================================================================*        
            START BP13K625 KEY = K625-ALT-KEY1.                                 
                                                                                
            EVALUATE  WS-K625-STATUS                                            
               WHEN '00'                                                        
                     PERFORM 2200-READNEXT-BP13K625 THRU 2200-EXIT              
               WHEN '10'                                                        
               WHEN '23'                                                        
                     DISPLAY 'VOUCHER NUMBER NOT FOUND IN BP13K625 '            
                              F640-NUM-VR                                       
                     ADD 1     TO WS-K625-NOT-FND                               
                     GO TO 2100-EXIT                                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WHILE STARTING BROWSE-BP13K625 '             
                    MOVE WS-K625-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-FILES                                    
            END-EVALUATE.                                                       
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2200-READNEXT-BP13K625.                                                  
      *================================================================*        
            PERFORM 2500-READ-BP13K625    THRU 2500-EXIT                        
                    UNTIL WS-K625-EOF = 'Y'.                                    
                                                                                
            PERFORM 2600-BB14F140-BATCH-WRITE  THRU 2600-EXIT.                  
                                                                                
            MOVE ZEROES TO WS-CTR1.                                             
                                                                                
            PERFORM 2700-BB14F140-DETAIL-WRITE THRU 2700-EXIT                   
                    UNTIL WS-CTR1 = WS-CTR.                                     
                                                                                
            MOVE ZEROES TO WS-TOT-AMT-STAMP.                                    
                                                                                
            PERFORM 2950-INITIALIZE-TABLE    THRU 2950-EXIT                     
                    VARYING WS-CTR   FROM 1 BY 1                                
                    UNTIL WS-CTR > 200.                                         
                                                                                
            MOVE ZEROES TO WS-CTR.                                              
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       2300-READ-BM06K120.                                                      
      *================================================================*        
            READ  BM06K120.                                                     
                                                                                
            EVALUATE  WS-K120-STATUS                                            
               WHEN '00'                                                        
                     CONTINUE                                                   
               WHEN '10'                                                        
               WHEN '23'                                                        
                     MOVE SPACES   TO K120-REC                                  
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K120               '             
                    MOVE WS-K120-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-FILES                                    
            END-EVALUATE.                                                       
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2500-READ-BP13K625.                                                      
      *================================================================*        
            READ BP13K625 NEXT AT END MOVE 'Y' TO WS-K625-EOF                   
               GO TO 2500-EXIT.                                                 
                                                                                
            IF K625-ALT-KEY1 NOT = F640-NUM-VR                                  
               MOVE 'Y'  TO WS-K625-EOF                                         
               GO TO 2500-EXIT                                                  
            END-IF.                                                             
                                                                                
            ADD 1 TO WS-K625-READ.                                              
            ADD 1 TO WS-CTR.                                                    
                                                                                
      *------------------------------------                                     
      *   ACCUMULATES STAMP DUTY                                                
      *------------------------------------                                     
            IF  K625-AMT-STAMP-DUTY NOT NUMERIC MOVE ZEROES TO                  
                K625-AMT-STAMP-DUTY                                             
            END-IF.                                                             
                                                                                
            IF  K625-AMT-STAMP-DUTY-DUP NOT NUMERIC MOVE ZEROES TO              
                K625-AMT-STAMP-DUTY-DUP                                         
            END-IF.                                                             
                                                                                
            ADD K625-AMT-STAMP-DUTY      TO WS-TOT-AMT-STAMP.                   
            ADD K625-AMT-STAMP-DUTY-DUP  TO WS-TOT-AMT-STAMP.                   
                                                                                
      *------------------------------------------------------                   
      *   GETTING THE VALUE OF BATCH-NUM TO WRITE TO 'BATCH'                    
      *------------------------------------------------------                   
                                                                                
            MOVE K625-NUM-BATCH          TO WS-NUM-BATCH.                       
                                                                                
      *------------------------------------                                     
      *   TO WRITE TO K640 'DETAIL'                                             
      *------------------------------------                                     
                                                                                
            MOVE K625-NUM-HDB-REF        TO WS-NUM-HDB-REF(WS-CTR).             
            MOVE K625-NUM-DOC            TO WS-NUM-DOC(WS-CTR).                 
                                                                                
      *-----------------------------------------------------------------        
      *   K625-DTE-DOC-PREPARE IS DERIVED FROM                                  
      *  ++ SOCA ++                                                             
      *   1) AO RAISED ==> CREATE BP13K300                                      
      *   2) RUN BP13C301  MOVES F300-DTE-AGRM-ORDER TO                         
      *                    K800-DTE-PRINCIPAL-SALES-AGMT                        
      *   3) BP13B320  ==> MOVES K800-DTE-AGRMT-FOR-LEASE TO                    
      *                    COMM1-DTE-AGREEMENT-FOR-LEASE.                       
      *   4) BP13B321  ==> MOVES COMM1-DTE-AGREEMENT-FOR-LEASE TO SCREEN        
      *                                                     (PROTECTED)         
      *  ++ SUBTRANSFER ++                                                      
      *     DTE-SUB-TRANSFER IS SAME AS DTE-AGREEMENT-FOR-LEASE                 
      *   1) BP13B800  ==> MOVES K800-DTE-AGRMT-FOR-LEASE TO                    
      *                    COM12-DTE-ORIG-AFL.                                  
      *   2) BP13B805  ==> MOVES COM12-DTE-ORIG-AFL TO SCREEN(ORIG AFL)         
      *                    MOVES CURRENT DATE       TO SCREEN(AFL DTE)          
      *                    MOVES COM12-REC TO K880-REC       (PROTECTED)        
      *   3) RUN BP13C350  MOVES F880-DTE-AGRMT-FOR-LEASE TO                    
      *                    K800-DTE-AGREEMENT-FOR-LEASE                         
      *   4) RUN BP13C356  MOVES F880-DTE-SUB-TRANSFER TO                       
      *                    K330-DTE-AGRM-ORDER                                  
      *   5) RUN BP13C355  MOVES K330-DTE-AGRM-ORDER TO                         
      *                    K625-DTE-DOC-PREPARE                                 
      *  ++ TP ++                                                               
      *   1) BP13B401  ==> MOVES K800-DTE-AGRMT-FOR-LEASE TO                    
      *      BP13B641      COMM3-DTE-PRINCIPAL-SALES-AGMT                       
      *                    LAST PANEL MOVES COMM3-REC TO BP13K400               
      *  ++ SA/TP ++                                                            
      *   1) BP13B612  ==> MOVES COMM3-DTE-SO-EXECUTION TO                      
      *                    K400-DTE-AGRMT-FOR-LEASE                             
      *   2) BP13B608  ==> MOVES COMM3-DTE-SO-EXECUTION TO                      
      *      BP13B685      K400-DTE-AGRMT-FOR-LEASE                             
      *-----------------------------------------------------------------        
                                                                                
            MOVE K625-DTE-DOC-PREPARE    TO WS-DTE-DOC-PREPARE(WS-CTR).         
            MOVE K625-NUM-DOC-TYP        TO WS-NUM-DOC-TYP(WS-CTR).             
            MOVE K625-AMT-PURCHASE-PRICE TO                                     
                                  WS-AMT-PURCHASE-PRICE(WS-CTR).                
            MOVE K625-AMT-ANNUAL-RENT    TO WS-AMT-ANNUAL-RENT(WS-CTR).         
            MOVE K625-NUM-INCREASE-SCCC  TO                                     
                                  WS-NUM-INCREASE-SCCC(WS-CTR).                 
            MOVE K625-AMT-LOAN           TO WS-AMT-LOAN(WS-CTR).                
            MOVE K625-AMT-STAMP-DUTY     TO WS-AMT-STAMP-DUTY(WS-CTR).          
            MOVE K625-AMT-STAMP-DUTY-DUP TO                                     
                                         WS-AMT-STAMP-DUTY-DUP(WS-CTR).         
                                                                                
            MOVE K625-NUM-LEASE-TERM TO WS-NUM-LEASE-TERM1.                     
                                                                                
            IF WS-NUM-LEASE-TERM1 = 9900.00                                     
               MOVE 1188.00              TO WS-NUM-LEASE-TERM(WS-CTR)           
            ELSE                                                                
            IF WS-NUM-LEASE-TERM1 = 3000.00                                     
               MOVE 360.00               TO WS-NUM-LEASE-TERM(WS-CTR)           
            ELSE                                                                
               MOVE WS-NUM-LEASE-TERM1   TO WS-NUM-LEASE-TERM(WS-CTR)           
            END-IF.                                                             
                                                                                
            MOVE K625-NUM-REGN           TO WS-NUM-REGN(WS-CTR).                
                                                                                
      *----------------------------------------                                 
      *   TO UPDATE BP13K625 REC WITH AG07 INFO                                 
      *----------------------------------------                                 
      *     STRING F640-NUM-BANK-PAY-FROM,  F640-NUM-IBG-SERIAL                 
      *            DELIMITED BY SIZE INTO   K625-NUM-IBG-REF.                   
                                                                                
            MOVE F640-TXT-IBG-SERIAL     TO K625-NUM-IBG-REF.                   
            MOVE F640-DTE-IBG-VALUE      TO K625-DTE-IBG-VALUE.                 
            MOVE WS-DATE                 TO K625-DTE-BATCH-SENT.                
                                                                                
            REWRITE      BP13K625-REC.                                          
                                                                                
            ADD 1 TO WS-K625-WRITE.                                             
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2600-BB14F140-BATCH-WRITE.                                               
      *================================================================*        
            MOVE SPACES                 TO BB14F140-BATCH.                      
            INITIALIZE                     BB14F140-BATCH.                      
            MOVE 'BATCH'                TO F140-NUM-REC-TYP OF                  
                                           BB14F140-BATCH.                      
            MOVE  WS-NUM-BATCH          TO F140-NUM-BATCH.                      
                                                                                
      *     STRING F640-NUM-BANK-PAY-FROM,  F640-NUM-IBG-SERIAL                 
      *            DELIMITED BY SIZE INTO   F140-NUM-IBG-REF.                   
                                                                                
            MOVE  F640-TXT-IBG-SERIAL   TO F140-NUM-IBG-REF.                    
            MOVE  F640-DTE-IBG-VALUE    TO F140-DTE-VALUE.                      
            MOVE  WS-TOT-AMT-STAMP      TO F140-AMT-STMP-TOTAL.                 
      *     MOVE 'SU'                   TO F140-NUM-DEPT-SYS.                   
      *     MOVE 'SS'                   TO F140-NUM-DEPT-SYS.                   
            MOVE 'A79999997C'           TO F140-NUM-DEPT-SYS.                   
            MOVE  WS-CTR                TO F140-QTY-ROW-TOTAL.                  
                                                                                
            COMPUTE F140-QTY-ROW-TOTAL = F140-QTY-ROW-TOTAL + 1.                
                                                                                
            WRITE BB14F140-BATCH.                                               
                                                                                
            ADD 1 TO WS-F140-TOT-WRITE.                                         
            ADD 1 TO WS-F140-BAT-WRITE.                                         
                                                                                
       2600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2700-BB14F140-DETAIL-WRITE.                                              
      *================================================================*        
            MOVE SPACES                          TO BB14F140-DETAIL.            
            INITIALIZE                              BB14F140-DETAIL             
            ADD 1 TO WS-CTR1.                                                   
            MOVE 'DETAIL'                        TO F140-NUM-REC-TYP            
                                 OF BB14F140-DETAIL.                            
            MOVE  WS-NUM-HDB-REF(WS-CTR1)        TO F140-NUM-BATCH.             
            MOVE  WS-NUM-DOC(WS-CTR1)            TO F140-NUM-DOC-TYPE.          
            MOVE  WS-DTE-DOC-PREPARE(WS-CTR1)    TO F140-DTE-DOC-PREP.          
            MOVE  WS-NUM-DOC-TYP(WS-CTR1)        TO F140-NUM-DOC-CAT.           
            MOVE  'B'                            TO F140-NUM-PPTY-TYPE.         
            MOVE  WS-AMT-PURCHASE-PRICE(WS-CTR1)                                
                                                 TO F140-AMT-CONSID.            
            MOVE  WS-AMT-ANNUAL-RENT(WS-CTR1) TO                                
                                                 F140-AMT-RENT-ANNUAL.          
            MOVE  WS-NUM-INCREASE-SCCC(WS-CTR1)  TO F140-NUM-SCCC-INCR.         
            MOVE  WS-AMT-LOAN(WS-CTR1)           TO                             
                                                 F140-AMT-LOAN-GRANTED.         
                                                                                
            IF WS-AMT-STAMP-DUTY(WS-CTR1) = ZEROES                              
               MOVE WS-AMT-STAMP-DUTY-DUP(WS-CTR1)  TO F140-AMT-STAMP           
            ELSE                                                                
               MOVE WS-AMT-STAMP-DUTY(WS-CTR1)      TO F140-AMT-STAMP           
            END-IF.                                                             
                                                                                
            MOVE WS-NUM-LEASE-TERM(WS-CTR1)      TO F140-NUM-LEASE-TERM.        
            MOVE ZEROES            TO  F140-ADDTNL-BYR-STAMP-DUTY               
            MOVE '1'               TO  F140-MASTER-PLAN-ZONING                  
            MOVE SPACE             TO  F140-MK-TS                               
                                                                                
      *------------------------------------                                     
      *      CALL BP13C913 TO GET ADDRESS                                       
      *------------------------------------                                     
                                                                                
            MOVE  SPACES                  TO  BP13COMM8-REC.                    
            INITIALIZE                        BP13COMM8-REC.                    
                                                                                
            MOVE  WS-NUM-HDB-REF(WS-CTR1) TO  WS-LINK-REC                       
                                              K120-KEY-FLD.                     
                                                                                
            PERFORM 2800-CALL-BP13C913 THRU  2800-EXIT.                         
                                                                                
            IF COMM8-CDE-SYSERR = 0                                             
               PERFORM 2900-MOVE-ADDR-DETLS THRU 2900-EXIT                      
            END-IF.                                                             
                                                                                
      ***TESTING   READ BM06K120                                                
      *     PERFORM 2300-READ-BM06K120   THRU 2300-EXIT                         
      *        MOVE COMM8-NUM-UNIT-MAIN   TO  WS-F140-HSE-MAIN.                 
      *        MOVE COMM8-NUM-UNIT-SUB    TO  WS-F140-HSE-SUB.                  
      *        MOVE K120-MAIN-UNIT        TO  F140-NUM-UNIT.                    
      *        MOVE K120-BLK-NO           TO  F140-NUM-BLK-HSE.                 
      *        MOVE K120-STREET-NAME      TO  F140-NME-STREET.                  
      *        MOVE K120-LEVEL-NO         TO  F140-NUM-LVL.                     
      *        MOVE K120-POSTAL-CODE      TO  F140-NUM-POSTAL.                  
                                                                                
            WRITE BB14F140-DETAIL.                                              
                                                                                
            ADD 1 TO WS-F140-TOT-WRITE.                                         
            ADD 1 TO WS-F140-DET-WRITE.                                         
                                                                                
       2700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2800-CALL-BP13C913.                                                      
      *================================================================*        
            CALL WS-BP13C913    USING  WS-LINK-REC,                             
                                       BP13COMM8-REC.                           
                                                                                
            IF COMM8-CDE-SYSERR NOT = 0                                         
               MOVE COMM8-CDE-SYSERR TO SQL-CODE                                
               IF COMM8-CDE-SYSERR = 100                                        
                  DISPLAY 'SCHEME-ACCT-NO = ' WS-LINK-REC                       
                  DISPLAY 'SCH-ACC NOT FOUND INF PIDB TABLE   ' SQL-CODE        
               ELSE                                                             
                  DISPLAY 'ERROR IN READING PIDB TABLE   ' SQL-CODE.            
                                                                                
       2800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2900-MOVE-ADDR-DETLS.                                                    
      *================================================================*        
               MOVE COMM8-NUM-UNIT-MAIN   TO  WS-F140-HSE-MAIN.                 
               MOVE COMM8-NUM-UNIT-SUB    TO  WS-F140-HSE-SUB.                  
               MOVE WS-F140-HSEMAIN       TO  F140-NUM-UNIT.                    
               MOVE COMM8-NUM-BLK         TO  F140-NUM-BLK-HSE.                 
               MOVE COMM8-NME-STREET      TO  F140-NME-STREET.                  
               MOVE COMM8-NUM-LEVEL       TO  F140-NUM-LVL.                     
               MOVE COMM8-NUM-POSTAL-CODE TO  F140-NUM-POSTAL.                  
                                                                                
       2900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2950-INITIALIZE-TABLE.                                                   
      *================================================================*        
            MOVE SPACES TO   WS-NUM-HDB-REF(WS-CTR).                            
            MOVE SPACES TO   WS-NUM-DOC(WS-CTR).                                
            MOVE SPACES TO   WS-DTE-DOC-PREPARE(WS-CTR).                        
            MOVE SPACES TO   WS-NUM-DOC-TYP(WS-CTR).                            
            MOVE ZEROES TO   WS-AMT-PURCHASE-PRICE(WS-CTR).                     
            MOVE ZEROES TO   WS-AMT-ANNUAL-RENT(WS-CTR).                        
            MOVE SPACES TO   WS-NUM-INCREASE-SCCC(WS-CTR).                      
            MOVE ZEROES TO   WS-AMT-LOAN(WS-CTR)                                
            MOVE ZEROES TO   WS-AMT-STAMP-DUTY(WS-CTR)                          
            MOVE ZEROES TO   WS-AMT-STAMP-DUTY-DUP(WS-CTR)                      
            MOVE ZEROES TO   WS-NUM-LEASE-TERM(WS-CTR)                          
            MOVE SPACES TO   WS-NUM-REGN(WS-CTR).                               
                                                                                
       2950-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           CLOSE     BP13K625                                                   
                     BB14F140                                                   
                     SY02F001                                                   
                     BM06K120                                                   
                     AG07F640.                                                  
                                                                                
           DISPLAY    '**** BP13C310 ****'.                                     
           DISPLAY    'AG07F640 TOT REC READ    : ' WS-F640-READ.               
           DISPLAY    'AG07F640 TOT REC SKIP    : ' WS-F640-SKIP.               
           DISPLAY    'BP13K625 TOT REC READ    : ' WS-K625-READ.               
           DISPLAY    'BP13K625 RECORDS WRITTEN : ' WS-K625-WRITE.              
           DISPLAY    'BP13K625 RECORDS NOT FND : ' WS-K625-NOT-FND.            
           DISPLAY    'BB14F140 TOT REC WRITTEN : ' WS-F140-TOT-WRITE.          
           DISPLAY    'BB14F140 BAT REC WRITTEN : ' WS-F140-BAT-WRITE.          
           DISPLAY    'BB14F140 DET REC WRITTEN : ' WS-F140-DET-WRITE.          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
