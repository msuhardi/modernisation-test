       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45F.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  04/11/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO GET ALL BATCHES WHICH      *            
      *                BOOKING PERIOD HAS CLOSED THE DAY BEFORE    *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC20                               *            
      * OUTPUT FILES :  1.) BP13FC2A                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136580 KSJ3 04/11/2016 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC20  ASSIGN        TO BP13FC20.                          
           SELECT BP13FC2A  ASSIGN        TO BP13FC2A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC20                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13FC20.                                                       
                                                                                
       FD  BP13FC2A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
       01  BP13FC2A-REC                  PIC X(150).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC20-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC20-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC2A-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC20-EOF               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
           05  WS-DTE-ADD                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-ADD-INT            PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-PREV               PIC 9(8)   VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13FC20-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC20-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC20                                                 
                OUTPUT BP13FC2A.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
      *-------------------------------------------------------------            
      *    GET PREVIOUS DATE                                                    
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-8              TO   WS-DTE-ADD.                     
           COMPUTE WS-DTE-ADD-INT = FUNCTION INTEGER-OF-DATE                    
                                    (WS-DTE-ADD).                               
           SUBTRACT 1                      FROM WS-DTE-ADD-INT.                 
           COMPUTE WS-DTE-ADD     = FUNCTION DATE-OF-INTEGER                    
                                    (WS-DTE-ADD-INT).                           
           MOVE WS-DTE-ADD                 TO   WS-DTE-PREV.                    
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC20-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC20                                                        
              AT END MOVE 'Y'              TO   WS-FC20-EOF.                    
                                                                                
           IF WS-FC20-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-FC20-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF FC20-DTE-BOOK-PD-END = WS-DTE-PREV                                
              WRITE BP13FC2A-REC           FROM BP13FC20-REC                    
              ADD 1                        TO   WS-CNT-FC2A-WRITE               
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-FC20-SKIP                
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13FC20-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC20                                                       
                 BP13FC2A.                                                      
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45F CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FC20 READ      : ' WS-CNT-FC20-READ.          
           DISPLAY ' 2. NO OF BP13FC20 SKIPPED   : ' WS-CNT-FC20-SKIP.          
           DISPLAY ' 3. NO OF BP13FC2A WRITTEN   : ' WS-CNT-FC2A-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
