       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C200.                                                 
       AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  NOV2003.                                                  
      *****************************************************************         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *****************************************************************         
      * OBJECTIVES:  TO UPDATE BB14K580                               *         
      *---------------------------------------------------------------*         
      * INPUT:  BB14F580                                              *         
      * I-O  :  BB14K580                                              *         
      *****************************************************************         
      * CHG REQ#  DATE        BY    DESCRIPTION                       *         
      * --------  ----------  ----  --------------------------------- *         
      * BP132432  13/11/2003  MTD   NEW PROGRAM.                      *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB14F580       ASSIGN TO BB14F580.                            
                                                                                
           SELECT BB14K580       ASSIGN TO BB14K580                             
                                 ORGANIZATION IS INDEXED                        
                                 ACCESS MODE  IS DYNAMIC                        
                                 RECORD KEY   IS K580-KEY-FLD                   
                                 FILE STATUS  IS WS-K580-STATUS.                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BB14F580                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY BB14F580.                                                           
                                                                                
       FD  BB14K580                                                             
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY BB14K580.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-DATE             PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-WORK-VAR.                                                         
           05  WS-END-OF-F580-SW       PIC X(01)  VALUE SPACE.                  
               88  C-END-OF-F580       VALUE 'Y'.                               
           05  WS-READ-CTR             PIC 9(08) VALUE ZEROES.                  
           05  WS-UPD-CTR              PIC 9(08) VALUE ZEROES.                  
           05  WS-K580-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F580.                                               
           PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-OPEN-FILES.                                                         
      *--------------------------------*                                        
                                                                                
           OPEN INPUT  BB14F580                                                 
                I-O    BB14K580.                                                
                                                                                
           IF WS-K580-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'OPEN ERROR BB14K580 ' WS-K580-STATUS                     
              MOVE WS-K580-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BB14F580 AT END                                                 
                MOVE 'Y'   TO  WS-END-OF-F580-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1  TO WS-READ-CTR.                                              
                                                                                
           PERFORM 2100-READ-BB14K580        THRU 2100-EXIT.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2100-READ-BB14K580.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE F580-KEY-FLD     TO K580-KEY-FLD.                               
                                                                                
           READ BB14K580.                                                       
                                                                                
           EVALUATE WS-K580-STATUS                                              
              WHEN 00                                                           
                 PERFORM 2110-REWRITE-K580    THRU  2110-EXIT                   
              WHEN 23                                                           
                 DISPLAY 'BB14K580 REC NOT FND,KEY: ' K580-KEY-FLD              
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BB14K580,STAT: ' WS-K580-STATUS         
                 MOVE WS-K580-STATUS    TO RETURN-CODE                          
                 PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2110-REWRITE-K580.                                                       
      *--------------------------------*                                        
                                                                                
           MOVE F580-DTE-UNIT-DELIVERY   TO K580-DTE-UNIT-DELIVERY.             
                                                                                
           REWRITE BB14K580-REC.                                                
           IF WS-K580-STATUS = ZEROES                                           
              ADD  1  TO WS-UPD-CTR                                             
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BB14K580 : ' WS-K580-STATUS                
              DISPLAY 'K580-KEY-FLD          : ' K580-KEY-FLD                   
              MOVE WS-K580-STATUS    TO RETURN-CODE                             
              PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                    
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C200'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BB14F580 RECORDS READ    : ' WS-READ-CTR.                   
           DISPLAY 'BB14K580 RECORDS UPDATED : ' WS-UPD-CTR.                    
                                                                                
           CLOSE BB14F580                                                       
                 BB14K580.                                                      
                                                                                
           IF WS-K580-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR BB14K580 ' WS-K580-STATUS                    
              MOVE WS-K580-STATUS         TO  RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
