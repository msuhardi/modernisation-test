       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH28.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  21JUL2014.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. GET NRIC FOR RE-ASSIGNED OIC                     *                
      *    2. DELETE BP13K022 IF REGN-NO NOT FOUND IN BP13K034 *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP13CH28 ZDD 20100927  NEW PGM                          *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F022 ASSIGN TO BP13F022.                                  
                                                                                
           SELECT BP13F034 ASSIGN TO BP13F034.                                  
                                                                                
           SELECT P13F022A ASSIGN TO P13F022A.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F022                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13F022.                                                           
                                                                                
       FD   BP13F034                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS  30  CHARACTERS.                                    
       COPY BP13F034.                                                           
                                                                                
       FD   P13F022A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100  CHARACTERS.                                    
       01   P13F022A-REC   PIC X(100).                                          
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F022-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F034-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-FLAG-F034                   PIC X(1)  VALUE 'N'.              
           05  WS-F034-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K022-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-F034-READ                   PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-F022               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-F034               PIC 9(6)  VALUE ZEROS.            
           05  WS-F022A-WRITE                 PIC 9(6)  VALUE ZEROS.            
           05  WS-K022-DELETED                PIC 9(6)  VALUE ZEROS.            
                                                                                
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F022         THRU 2000-EXIT.                       
           PERFORM 2100-READ-F034         THRU 2100-EXIT.                       
           PERFORM 3000-PROCESS-MATCH     THRU 3000-EXIT                        
                  UNTIL WS-F022-EOF-SW = 'Y' OR                                 
                        WS-F034-EOF-SW = 'Y'.                                   
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F022                                                 
                       BP13F034                                                 
                  I-O  BP13K022                                                 
                OUTPUT P13F022A.                                                
                                                                                
           IF WS-F034-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13F034 OPEN ERROR, STATUS IS ' WS-F034-STATUS          
              MOVE WS-F034-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F022.                                                          
      ****************************************************************          
                                                                                
           READ BP13F022                                                        
              AT END MOVE 'Y' TO WS-F022-EOF-SW                                 
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-F022.                          
                                                                                
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-F034.                                                          
      ****************************************************************          
                                                                                
           READ BP13F034                                                        
              AT END MOVE 'Y' TO WS-F034-EOF-SW                                 
              GO TO 2100-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-F034.                          
                                                                                
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-MATCH.                                                      
      ****************************************************************          
                                                                                
           IF F022-NUM-REGN  = F034-NUM-REGN                                    
              MOVE SPACES        TO P13F022A-REC                                
              INITIALIZE            P13F022A-REC                                
              MOVE F034-NUM-NRIC TO BP13F022-REC(27:9)                          
              MOVE BP13F022-REC  TO P13F022A-REC                                
              WRITE P13F022A-REC                                                
              ADD 1              TO WS-F022A-WRITE                              
              PERFORM 2000-READ-F022    THRU 2000-EXIT                          
              PERFORM 2100-READ-F034    THRU 2100-EXIT                          
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F022-NUM-REGN  > F034-NUM-REGN                                    
              PERFORM 2100-READ-F034    THRU 2100-EXIT                          
              GO TO 3000-EXIT                                                   
           END-IF                                                               
                                                                                
           IF F022-NUM-REGN  < F034-NUM-REGN                                    
              MOVE SPACES               TO  BP13K022-REC                        
              MOVE F022-KEY-FLD         TO  K022-KEY-FLD                        
              PERFORM 3100-PROCESS-K022 THRU 3100-EXIT                          
              PERFORM 2000-READ-F022    THRU 2000-EXIT                          
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-PROCESS-K022.                                                       
      ****************************************************************          
                                                                                
           READ BP13K022.                                                       
                                                                                
           EVALUATE WS-K022-STATUS                                              
             WHEN 00                                                            
                PERFORM 3200-DELETE-K022  THRU 3200-EXIT                        
             WHEN 23                                                            
              DISPLAY 'REG NUM NOT FOUND  BP13K022. STATUS IS '                 
                       WS-K022-STATUS '/' K022-NUM-REGN                         
                                                                                
             WHEN OTHER                                                         
              DISPLAY 'ERROR WITH READ OF BP13K022. STATUS IS '                 
                       WS-K022-STATUS                                           
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
             END-EVALUATE.                                                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3200-DELETE-K022.                                                        
      ****************************************************************          
           DISPLAY 'DELETED : ' K022-KEY-FLD.                                   
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           IF WS-K022-STATUS NOT  = 00 AND 23                                   
              DISPLAY 'ERROR DELETING THE RECORD - K022..STAT'                  
              WS-K022-STATUS '/' K022-NUM-REGN                                  
              MOVE WS-K022-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-K022-DELETED.                                            
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CH28 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F022 RECORDS READ        = ' WS-CNT-READ-F022.          
           DISPLAY 'BP13F034 RECORDS READ        = ' WS-CNT-READ-F034.          
           DISPLAY 'NO OF OUTPUT CASES           = ' WS-F022A-WRITE.            
           DISPLAY 'NO OF DELETED CASES IN K022  = ' WS-K022-DELETED.           
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F022                                                       
                 BP13F034                                                       
                 BP13K022                                                       
                 P13F022A.                                                      
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 CLOSE ERROR, STATUS IS ' WS-K022-STATUS         
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
