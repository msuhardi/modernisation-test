       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CI01.                                                 
       AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  14/05/2015.                                               
      * ===============================================================*        
      * BP13 SYSTEM OF COMMITMENT                                      *        
      * ===============================================================*        
      * OBJECTIVE: TO UPDATE THE FIELDS KL00-DTE-READ-EMAIL-MYHDBPAGE  *        
      *            AND KL00-TME-READ-EMAIL-MYHDBPAGE                   *        
      *                                                                *        
      *   INPUT FILE  :  1. BP13KL05                                   *        
      *                                                                *        
      *   I/O FILE    :  1. BP13KL00                                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *   BP135791  RJB1  14/05/15 NEW PROGRAM                         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KL00  ASSIGN        TO BP13KL00                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS KL00-KEY-FLD                    
                            FILE STATUS      IS WS-KL00-STATUS.                 
                                                                                
           SELECT BP13KL05  ASSIGN        TO BP13KL05.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13KL05                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13KL05.                                                           
                                                                                
       FD  BP13KL00                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KL00.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY             PIC 9(04).                               
           05  WS-CUR-MM               PIC 9(02).                               
           05  WS-CUR-DD               PIC 9(02).                               
                                                                                
       01  WS-COUNT.                                                            
           05  WS-KL05-INPUT           PIC 9(08) VALUE 0.                       
           05  WS-KL00-FND             PIC 9(08) VALUE 0.                       
           05  WS-KL05-OUT             PIC 9(08) VALUE 0.                       
           05  WS-WRITTEN              PIC 9(08) VALUE 0.                       
                                                                                
       01  WS-TIME                     PIC 9(08) VALUE 0.                       
       01  WS-KL05-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-FOUND-KL00               PIC X(01) VALUE 'N'.                     
       01  WS-FILE-STATUS.                                                      
           05 WS-KL00-STATUS           PIC 99    VALUE ZEROS.                   
                                                                                
       01  VARIABLES.                                                           
           05 WS-STAGE                 PIC X(5).                                
              88 VALID-STAGE                     VALUE 'SA   ', 'TP   ',        
                                                 'SATP '.                       
           05 WS-DOC-TYPE              PIC X(5).                                
              88 VALID-DOC-TY                    VALUE 'SOCA ', 'TP   ',        
                                                 'SR   ', 'SP   '.              
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-KL05-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13KL05                                                 
                I-O    BP13KL00.                                                
                                                                                
           IF WS-KL00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KL00 ERROR ' WS-KL00-STATUS                  
              MOVE     WS-KL00-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           INITIALIZE  WS-COUNT.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13KL05                                                       
                 AT END MOVE 'Y'  TO WS-KL05-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-KL05-EOF NOT = 'Y'                                            
               ADD 1 TO WS-KL05-INPUT                                           
               MOVE KL05-NUM-STAGE    TO WS-STAGE                               
               MOVE KL05-NUM-DOC-TYPE TO WS-DOC-TYPE                            
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE SPACES  TO KL00-KEY-FLD.                                        
           INITIALIZE      WS-FOUND-KL00 KL00-KEY-FLD.                          
                                                                                
           IF KL05-DTE-READ-EMAIL-MYHDBPAGE NOT = SPACES AND LOW-VALUES         
              IF VALID-STAGE AND VALID-DOC-TY                                   
                 MOVE KL05-KEY-FLD(1:18) TO KL00-KEY-FLD                        
                 PERFORM 3500-READ-BP13KL00                                     
                    THRU 3500-EXIT                                              
                 IF WS-FOUND-KL00 = 'Y'                                         
                    MOVE KL05-DTE-READ-EMAIL-MYHDBPAGE TO                       
                         KL00-DTE-READ-EMAIL-MYHDBPAGE                          
                    MOVE KL05-TME-READ-EMAIL-MYHDBPAGE TO                       
                         KL00-TME-READ-EMAIL-MYHDBPAGE                          
                    PERFORM 4000-REWRITE-BP13KL00                               
                       THRU 4000-EXIT                                           
                 ELSE                                                           
                    ADD 1 TO WS-KL05-OUT                                        
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'NOT VALID STAGE AND DOC TYPE : ' KL05-NUM-REGN        
                 ADD 1 TO WS-KL05-OUT                                           
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-KL05-OUT                                              
              DISPLAY 'DATE READ EMAIL IS BLANK : ' KL05-NUM-REGN               
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-READ-BP13KL00.                                                      
      *================================================================*        
                                                                                
           READ BP13KL00.                                                       
                                                                                
           EVALUATE WS-KL00-STATUS                                              
              WHEN 00                                                           
                MOVE 'Y' TO WS-FOUND-KL00                                       
              WHEN 23                                                           
                MOVE 'N' TO WS-FOUND-KL00                                       
                DISPLAY 'RECORD NOT FOUND IN KL00 : ' KL00-KEY-FLD              
              WHEN OTHER                                                        
                MOVE  WS-KL00-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13KL00 FILE : ' WS-KL00-STATUS         
                DISPLAY 'KL00-NUM-REGN = ' KL00-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-REWRITE-BP13KL00.                                                   
      *================================================================*        
           REWRITE BP13KL00-LETTER.                                             
                                                                                
           IF WS-KL00-STATUS = '00'                                             
              ADD 1 TO WS-WRITTEN                                               
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BP13KL00 FILE : ' WS-KL00-STATUS           
              DISPLAY 'KL00-NUM-REGN = ' KL00-NUM-REGN                          
              MOVE WS-KL00-STATUS         TO RETURN-CODE                        
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'PROGRAM: BP13CI01'.                                         
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-KL05-INPUT.               
           DISPLAY 'TOTAL RECORDS NOT PROCESSED: ' WS-KL05-OUT.                 
           DISPLAY 'TOTAL RECORD WRITTEN       : ' WS-WRITTEN.                  
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13KL05                                                       
                 BP13KL00.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
