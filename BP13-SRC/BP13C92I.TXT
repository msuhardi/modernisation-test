       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C92I.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  12/05/2017.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVES  : COPY RECORDS FROM BP13F762 TO BP13K76A      *           
      *                                                             *           
      *   INPUT FILES : BP13F762 - RECORD TO BE PROCESSED           *           
      *   I-O FILE    : BP13K76A - FILE TO BE UPDATED               *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ   DATE      BY    DESCRIPTIONS                      *           
      * --------  --------  ----  --------------------------------- *           
      * BP136832  12052017  SMR2  NEW PROGRAM                       *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F762 ASSIGN       TO BP13F762.                            
                                                                                
           SELECT BP13K76A ASSIGN       TO BP13K76A                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K76A-KEY-FLD                         
                           FILE STATUS  IS WS-K76A-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F762                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F762.                                                           
                                                                                
       FD   BP13K76A                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13K76A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K76A-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F762-EOF                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F762-READ                PIC 9(06)   VALUE ZEROES.            
           05  WS-K76A-FND                 PIC 9(06)   VALUE ZEROES.            
           05  WS-K76A-WRITE               PIC 9(06)   VALUE ZEROES.            
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                     PIC X(08)   VALUE SPACES.            
           05  WS-DATE-EDIT                PIC X(10)   VALUE SPACES.            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F762       THRU 2000-EXIT                      
             UNTIL WS-F762-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F762                                                 
                I-O    BP13K76A.                                                
                                                                                
           IF WS-K76A-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K76A ERROR ' WS-K76A-STATUS                  
              MOVE WS-K76A-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(1:4) '-' WS-DATE(5:2) '-' WS-DATE(7:2)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13F762.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F762 AT END                                                 
                MOVE 'Y'                    TO WS-F762-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F762-READ.                    
                                                                                
           PERFORM 3000-READ-BP13K76A    THRU 3000-EXIT.                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-READ-BP13K76A.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K76A-REC.                    
           INITIALIZE                          BP13K76A-REC.                    
                                                                                
           MOVE F762-NUM-SALE-MODE          TO K76A-NUM-SALE-MODE-HIST.         
           MOVE F762-DTE-BALLOT             TO K76A-DTE-BALLOT-HIST.            
           MOVE F762-NUM-CHOICE             TO K76A-NUM-CHOICE-HIST.            
           MOVE F762-NUM-FT                 TO K76A-NUM-FT-HIST.                
                                                                                
           READ BP13K76A.                                                       
                                                                                
           EVALUATE WS-K76A-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K76A-FND                      
                                                                                
           WHEN 23                                                              
                PERFORM 4000-WRITE-BP13K76A    THRU 4000-EXIT                   
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K76A : ' WS-K76A-STATUS              
                MOVE WS-K76A-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4000-WRITE-BP13K76A.                                                     
      *--------------------*                                                    
                                                                                
           MOVE BP13F762-REC                TO BP13K76A-REC(1:200).             
           MOVE SPACES                      TO BP13K76A-REC(201:86).            
                                                                                
           IF K76A-NUM-APPL     IS NOT NUMERIC                                  
              MOVE ZEROS TO K76A-NUM-APPL                                       
           END-IF.                                                              
           IF K76A-NUM-BALLOTED IS NOT NUMERIC                                  
              MOVE ZEROS TO K76A-NUM-BALLOTED                                   
           END-IF.                                                              
           IF K76A-NUM-CANCEL   IS NOT NUMERIC                                  
              MOVE ZEROS TO K76A-NUM-CANCEL                                     
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-22 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-22                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-23 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-23                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-23P IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-23P                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-24 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-24                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-25 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-25                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-32 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-32                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-33 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-33                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-33P IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-33P                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-34 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-34                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-35 IS NOT NUMERIC                              
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-35                               
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P2 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P2                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P3 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P3                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P3P IS NOT NUMERIC                            
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P3P                             
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P4 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P4                              
           END-IF.                                                              
           IF K76A-NUM-APPL-MGPS-3P5 IS NOT NUMERIC                             
              MOVE ZEROS TO K76A-NUM-APPL-MGPS-3P5                              
           END-IF.                                                              
                                                                                
           WRITE BP13K76A-REC.                                                  
                                                                                
           EVALUATE WS-K76A-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
                ADD 1                       TO WS-K76A-WRITE                    
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR WRITING TO BP13K76A. STATUS IS - '               
                        WS-K76A-STATUS                                          
                MOVE WS-K76A-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*=============================================*'.           
           DISPLAY '  BP13C92I CONTROL TOTAL       : ' WS-DATE-EDIT.            
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '  NO OF BP13F762 RECS READ     : ' WS-F762-READ.            
           DISPLAY '  NO OF BP13K76A RECS FOUND    : ' WS-K76A-FND.             
           DISPLAY '  NO OF BP13K76A RECS WRITTEN  : ' WS-K76A-WRITE.           
           DISPLAY '*=============================================*'.           
                                                                                
           CLOSE BP13F762                                                       
                 BP13K76A.                                                      
                                                                                
           IF WS-K76A-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K76A ERROR ' WS-K76A-STATUS                  
              MOVE WS-K76A-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
