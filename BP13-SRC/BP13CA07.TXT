      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CA07.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  17 APRIL 2006.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE SOC AND FIS AFTER INTERNET BOOKING*           
      *                 OF DBSS FLAT BY DEVELOPER (ACCEPTANCE)      *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FA40 -- SORTED TRANSACTION FILE                   *           
      *   2.  BP13KB20 -- DBS REQUEST FILE                          *           
      *   3.  BP13KA30 -- UNIT INFORMATION FILE                     *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13K800 -- SOC MASTER FILE                           *           
      *   2.  BP13K840 -- SOC APPLICATION HISTORY FILE              *           
      *   3.  BM06K110 -- FLAT INVENTORY FILE                       *           
      *   4.  BM06K100 -- BLOCK SUMMARY FILE                        *           
      *   5.  BM06KD00 -- HOUSING GRANT, SCHACC-NO                  *           
      *   6.  BP13KD15 -- DBSS TP FILE                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132876 13/04/2006 SD10 NEW PROGRAM                        *           
      * BP133091 16/10/2007 RB12 EXPAND KD00 LENGTH FR 1200 TO 1600 *           
      * BP133290 07/03/2008 RB12 MOVE KB20 PROJ CODE AND NUM RACE   *           
      * BP133403 02/12/2008 ESA1 UPDATE KD15                        *           
      * BP133647 06/07/2009 JB8  TO UPDATE KD00-DTE-DUE-SEND-SNP    *           
      * BP134067 10/05/2011 LSB  TO UPDATE K800-NUM-PAYMENT-SCHEME  *           
      * BP134215 27/06/2011 JB8  TO CATER FOR EC RECORDS            *           
      * BP134277 19/07/2011 CT2  TO PASS 'P' FOR PAYMENT SCHEME     *           
      *                          IF IT'S SPACES                     *           
      * BP134808 08/05/2013 ESA1 TO ADD UPDATE OF DTE-ALLOCN & SEL  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KB20 ASSIGN TO BP13KB20                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KB20-KEY-FLD                               
                  FILE STATUS     IS BP13KB20-STATUS.                           
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD00-KEY-FLD                               
                  FILE STATUS     IS BP13KD00-STATUS.                           
                                                                                
           SELECT BP13FA40 ASSIGN TO BP13FA40.                                  
           SELECT P13FA40A ASSIGN TO P13FA40A.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K840-NUM-REGN                              
                  FILE STATUS     IS BP13K840-STATUS.                           
                                                                                
           SELECT BP13KA30 ASSIGN TO BP13KA30                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA30-KEY-FLD                               
                  ALTERNATE KEY   IS KA30-ALT-KEY                               
                  FILE STATUS     IS BP13KA30-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K100-KEY-FLD                               
                  FILE STATUS     IS BM06K100-STATUS.                           
                                                                                
           SELECT BP13KD15 ASSIGN TO BP13KD15                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD15-KEY-FLD                               
                  FILE STATUS     IS BP13KD15-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K840                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K840.                                                           
                                                                                
       FD   BP13KA30                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA30.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BP13KD15.                                                           
                                                                                
       FD   BP13FA40                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FA40.                                                           
                                                                                
       FD   P13FA40A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
                                                                                
       01 P13FA40A-REC  PIC X(150).                                             
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       COPY AV02COMM.                                                           
                                                                                
       01  CNT-FA40-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA30-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA30-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KB20-SCHACC-NMTCH  PIC 9(08) VALUE ZEROES.                       
       01  CNT-KB20-SCHACC-BLANK  PIC 9(08) VALUE ZEROES.                       
       01  CNT-KB20-SCHACC-FND    PIC 9(08) VALUE ZEROES.                       
       01  CNT-KB20-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-KB20-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K800-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K800-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K800-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-K840-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K840-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-K840-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-K110-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K110-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K110-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-K100-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K100-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K100-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-DUP-REC       PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-WRITE-ERROR   PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-FA40-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  WS-FA40-EOF            PIC X     VALUE SPACE.                        
       01  WS-KA30-FOUND          PIC X     VALUE SPACE.                        
       01  BP13KB20-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD00-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K800-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K840-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KA30-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BM06K110-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BM06K100-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD15-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  RM-PTR                 PIC 9(02) VALUE ZEROES.                       
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-CURRENT-TIME        PIC 9(08) VALUE ZEROES.                       
       01  WS-DTE-DUE-SNP         PIC 9(08) VALUE ZEROES.                       
       01  WS-DTE-ACCEPT          PIC 9(08) VALUE ZEROES.                       
       01  WS-NUM-ACCEPT          PIC 9(07) VALUE ZEROES.                       
       01  WS-DATE-VALID          PIC X(01) VALUE SPACE.                        
           88  DATE-VALID         VALUE 'Y'.                                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-FA40    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-FA40-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FA40                                                 
                       BP13KB20                                                 
                       BP13KA30                                                 
                I-O    BP13K800                                                 
                       BP13K840                                                 
                       BP13KD00                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13KD15                                                 
               OUTPUT  P13FA40A.                                                
                                                                                
           CALL 'OAV02001' USING LINK-REC.                                      
                                                                                
           IF BP13KB20-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13KB20 : ' BP13KB20-STATUS             
              MOVE BP13KB20-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KA30-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13KA30 : ' BP13KA30-STATUS             
              MOVE BP13KA30-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K840-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K840 : ' BP13K840-STATUS             
              MOVE BP13K840-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BM06K100 : ' BM06K100-STATUS             
              MOVE BM06K100-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD00-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD15-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13KD15 : ' BP13KD15-STATUS             
              MOVE BP13KD15-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-FA40.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FA40 AT END                                                 
                MOVE HIGH-VALUES TO BP13FA40-TRANS-REC                          
                MOVE 'Y' TO WS-FA40-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-FA40-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF FA40-REGN-NO NOT = SPACES AND LOW-VALUES AND ZEROES               
              PERFORM 3100-READ-BP13KB20  THRU 3100-EXIT                        
              IF KB20-NUM-SCH-ACC = SPACES OR LOW-VALUES OR ZEROES              
                 ADD 1   TO CNT-KB20-SCHACC-BLANK                               
              ELSE                                                              
                 IF KB20-NUM-SCH-ACC = FA40-SCH-ACC                             
                    ADD 1   TO CNT-KB20-SCHACC-FND                              
                    PERFORM 3200-PROCESS-REC THRU 3200-EXIT                     
                 ELSE                                                           
                    ADD 1   TO CNT-KB20-SCHACC-NMTCH                            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FA40    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13KB20.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO KB20-KEY-FLD.                              
           MOVE FA40-REGN-NO      TO KB20-NUM-REGN.                             
           READ BP13KB20.                                                       
                                                                                
           IF BP13KB20-STATUS = 00                                              
              ADD 1    TO CNT-KB20-READ                                         
           ELSE                                                                 
              IF BP13KB20-STATUS = 23                                           
                 ADD 1        TO CNT-KB20-NOTFND                                
                 DISPLAY 'RECORD NOT FOUND IN BP13KB20 : ' FA40-REGN-NO         
                 MOVE SPACES  TO BP13KB20-REC                                   
                 INITIALIZE      BP13KB20-REC                                   
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KB20 : ' BP13KB20-STATUS          
                 MOVE BP13KB20-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3200-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3300-READ-BP13KA30       THRU 3300-EXIT                      
                                                                                
           IF WS-KA30-FOUND = 'Y'                                               
             IF FA40-REGN-NO(1:1) = '5'                                         
                PERFORM 3400-READ-BP13K800    THRU 3400-EXIT                    
                PERFORM 3500-READ-BP13K840    THRU 3500-EXIT                    
                PERFORM 3600-READ-BM06K110    THRU 3600-EXIT                    
                PERFORM 3700-READ-BM06K100    THRU 3700-EXIT                    
                PERFORM 3800-READ-BP13KD00    THRU 3800-EXIT                    
             END-IF                                                             
           ELSE                                                                 
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3900-READ-BP13KD15       THRU 3900-EXIT.                     
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3300-READ-BP13KA30.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO KA30-ALT-KEY.                              
           MOVE FA40-REGN-NO      TO KA30-NUM-REGN.                             
           READ BP13KA30 KEY IS KA30-ALT-KEY.                                   
                                                                                
           IF BP13KA30-STATUS = 00                                              
              ADD 1    TO CNT-KA30-READ                                         
              MOVE 'Y'  TO WS-KA30-FOUND                                        
           ELSE                                                                 
              IF BP13KA30-STATUS = 23                                           
                 MOVE 'N'  TO WS-KA30-FOUND                                     
                 ADD 1        TO CNT-KA30-NOTFND                                
                 DISPLAY 'RECORD NOT FND IN BP13KA30 : '                        
                          BP13KA30-STATUS '/' KA30-ALT-KEY                      
                 WRITE P13FA40A-REC FROM BP13FA40-TRANS-REC                     
                 ADD 1        TO CNT-FA40-WRITE                                 
                 MOVE SPACES      TO BP13KA30-REC                               
      *          MOVE BP13KA30-STATUS     TO RETURN-CODE                        
      *          PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KA30 : ' BP13KA30-STATUS          
                 MOVE BP13KA30-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3400-READ-BP13K800.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO K800-NUM-REGN.                             
           MOVE FA40-REGN-NO      TO K800-NUM-REGN.                             
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00                                              
              ADD 1    TO CNT-K800-FND                                          
                                                                                
              PERFORM 4000-GET-DUE-SEND-SNP   THRU 4000-EXIT                    
                                                                                
              PERFORM 3410-MOVE-DATA-BP13K800 THRU 3410-EXIT                    
              PERFORM 3420-UPDATE-BP13K800    THRU 3420-EXIT                    
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 ADD 1        TO CNT-K800-NOTFND                                
                 DISPLAY 'RECORD NOT FOUND IN BP13K800 : ' FA40-REGN-NO         
                 MOVE SPACES  TO BP13K800-MASTER                                
                 INITIALIZE      BP13K800-MASTER                                
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS          
                 MOVE BP13K800-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3410-MOVE-DATA-BP13K800.                                                 
      *------------------------*                                                
                                                                                
           MOVE KA30-NUM-FLAT-TYPE(3:1)  TO K800-NUM-FLAT-TYPE.                 
           MOVE KA30-NUM-SCH-ACC         TO K800-NUM-SCH-ACC.                   
           MOVE KA30-DTE-ACCEPT          TO K800-NUM-ALLOC-DATE.                
           MOVE 'AI'                     TO K800-NUM-ALLOC-TAG.                 
                                                                                
           IF FA40-CDE-NT NOT = SPACES AND LOW-VALUES                           
              MOVE FA40-CDE-NT      TO K800-NUM-NEW-TOWN                        
           ELSE                                                                 
              MOVE SPACES           TO K800-NUM-NEW-TOWN                        
           END-IF.                                                              
                                                                                
           MOVE WS-DATE             TO K800-DTE-UPDATE.                         
                                                                                
           IF KB20-NUM-AHG-REQ = 'Y'                                            
              MOVE KA30-DTE-ACCEPT  TO K800-DTE-AHG-REQ                         
           END-IF.                                                              
                                                                                
           MOVE KB20-NUM-PROJECT    TO K800-NUM-DBSS-PRJCT.                     
           IF KB20-NUM-PAYMENT-SCHEME = SPACES OR LOW-VALUES                    
              MOVE 'P'              TO K800-NUM-PAYMENT-SCHEME                  
           ELSE                                                                 
              MOVE KB20-NUM-PAYMENT-SCHEME                                      
                                    TO K800-NUM-PAYMENT-SCHEME                  
           END-IF.                                                              
                                                                                
           EVALUATE KB20-NUM-RACE                                               
              WHEN 'M'                                                          
                 MOVE  1            TO K800-NUM-CAT                             
              WHEN 'C'                                                          
                 MOVE  2            TO K800-NUM-CAT                             
              WHEN 'I'                                                          
                 MOVE  3            TO K800-NUM-CAT                             
              WHEN 'O'                                                          
                 MOVE  4            TO K800-NUM-CAT                             
           END-EVALUATE.                                                        
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3420-UPDATE-BP13K800.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF BP13K800-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K800-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13K800 : ' BP13K800-STATUS            
              MOVE BP13K800-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3420-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3500-READ-BP13K840.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO K840-KEY-FLD.                              
           MOVE FA40-REGN-NO      TO K840-NUM-REGN.                             
           READ BP13K840.                                                       
                                                                                
           IF BP13K840-STATUS = 00                                              
              ADD 1    TO CNT-K840-FND                                          
              PERFORM 3510-MOVE-DATA-BP13K840 THRU 3510-EXIT                    
              PERFORM 3520-UPDATE-BP13K840    THRU 3520-EXIT                    
           ELSE                                                                 
              IF BP13K840-STATUS = 23                                           
                 PERFORM 3530-CREATE-BP13K840 THRU 3530-EXIT                    
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K840 : ' BP13K840-STATUS          
                 MOVE BP13K840-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3510-MOVE-DATA-BP13K840.                                                 
      *------------------------*                                                
                                                                                
           MOVE KA30-NUM-FLAT-TYPE(3:1)  TO K840-NUM-FLAT-TYPE.                 
           MOVE KA30-NUM-SCH-ACC         TO K840-NUM-SCH-ACC.                   
           MOVE K800-NUM-IDP-SCHEME      TO K840-NUM-IDP-SCH.                   
           MOVE K800-NUM-ALLO-SCHEME     TO K840-NUM-ALLOC-SCH.                 
                                                                                
           IF FA40-CDE-NT NOT = SPACES AND LOW-VALUES                           
              MOVE FA40-CDE-NT      TO K840-NUM-NEW-TOWN                        
           ELSE                                                                 
              MOVE SPACES           TO K840-NUM-NEW-TOWN                        
           END-IF.                                                              
                                                                                
           MOVE WS-DATE             TO K840-DTE-UPDATE.                         
                                                                                
           PERFORM 3511-CHECK-HISTORY THRU 3511-EXIT.                           
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3511-CHECK-HISTORY.                                                      
      *--------------------*                                                    
                                                                                
           IF K840-DTE-ACCEPT1 = SPACES OR LOW-VALUES                           
              MOVE KA30-DTE-ACCEPT      TO K840-DTE-ACCEPT1                     
              MOVE KA30-NUM-SCH-ACC     TO K840-NUM-ACCEPT-FLAT1                
           ELSE                                                                 
              IF K840-DTE-ACCEPT2 = SPACES OR LOW-VALUES                        
                 MOVE KA30-DTE-ACCEPT   TO K840-DTE-ACCEPT2                     
                 MOVE KA30-NUM-SCH-ACC  TO K840-NUM-ACCEPT-FLAT2                
              ELSE                                                              
                 IF K840-DTE-ACCEPT3 = SPACES OR LOW-VALUES                     
                  MOVE KA30-DTE-ACCEPT  TO K840-DTE-ACCEPT3                     
                  MOVE KA30-NUM-SCH-ACC TO K840-NUM-ACCEPT-FLAT3                
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3511-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3520-UPDATE-BP13K840.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE BP13K840-REC.                                                
                                                                                
           IF BP13K840-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K840-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13K840 : ' BP13K840-STATUS            
              MOVE BP13K840-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3530-CREATE-BP13K840.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES               TO BP13K840-REC.                           
           MOVE K800-NUM-REGN        TO K840-NUM-REGN.                          
           MOVE K800-NUM-NRIC1       TO K840-NUM-NRIC1.                         
                                                                                
           IF K800-NUM-NRIC2 = SPACES OR LOW-VALUES                             
              STRING '#' K800-NUM-REGN                                          
                     DELIMITED BY SIZE INTO K840-NUM-NRIC2                      
              END-STRING                                                        
           ELSE                                                                 
              MOVE K800-NUM-NRIC2        TO K840-NUM-NRIC2                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 = SPACES OR LOW-VALUES                             
              STRING '#' K800-NUM-REGN                                          
                     DELIMITED BY SIZE INTO K840-NUM-NRIC3                      
              END-STRING                                                        
           ELSE                                                                 
              MOVE K800-NUM-NRIC3        TO K840-NUM-NRIC3                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 = SPACES OR LOW-VALUES                             
              STRING '#' K800-NUM-REGN                                          
                     DELIMITED BY SIZE INTO K840-NUM-NRIC4                      
              END-STRING                                                        
           ELSE                                                                 
              MOVE K800-NUM-NRIC4        TO K840-NUM-NRIC4                      
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-NEW-TOWN    TO K840-NUM-NEW-TOWN.                      
           MOVE K800-NUM-FLAT-TYPE   TO K840-NUM-FLAT-TYPE.                     
           MOVE K800-NUM-IDP-SCHEME  TO K840-NUM-IDP-SCH.                       
           MOVE K800-NUM-ALLO-SCHEME TO K840-NUM-ALLOC-SCH.                     
           MOVE KA30-DTE-ACCEPT      TO K840-DTE-ACCEPT1.                       
           MOVE KA30-NUM-SCH-ACC     TO K840-NUM-ACCEPT-FLAT1                   
                                        K840-NUM-SCH-ACC.                       
           MOVE WS-DATE              TO K840-DTE-UPDATE.                        
                                                                                
           WRITE BP13K840-REC.                                                  
                                                                                
           IF BP13K840-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K840-WRITE                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING - BP13K840 : ' BP13K840-STATUS             
              MOVE BP13K840-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3530-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3600-READ-BM06K110.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO K110-KEY-FLD.                              
           MOVE FA40-SCH-ACC      TO K110-KEY-FLD.                              
           READ BM06K110.                                                       
                                                                                
           IF BM06K110-STATUS = 00                                              
              ADD 1    TO CNT-K110-FND                                          
              PERFORM 3610-MOVE-DATA-BM06K110 THRU 3610-EXIT                    
              PERFORM 3620-UPDATE-BM06K110    THRU 3620-EXIT                    
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 ADD 1        TO CNT-K110-NOTFND                                
                 MOVE SPACES  TO K110-REC                                       
                 INITIALIZE      K110-REC                                       
                 DISPLAY 'RECORD NOT FOUND IN BM06K110 : '                      
                          FA40-SCH-ACC                                          
              ELSE                                                              
                 DISPLAY 'ERROR READING - BM06K110 : ' BM06K110-STATUS          
                 MOVE BM06K110-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3610-MOVE-DATA-BM06K110.                                                 
      *------------------------*                                                
                                                                                
           MOVE FA40-REGN-NO            TO K110-REGN-NO.                        
           IF FA40-REGN-NO(1:1) = '5'                                           
              MOVE 'DBS'                TO K110-NUM-SELECTION                   
           END-IF.                                                              
           MOVE KA30-DTE-ACCEPT         TO K110-DTE-ACCEPTANCE.                 
           IF K110-DTE-ALLOCN = SPACES OR LOW-VALUES OR ZEROES                  
              MOVE KA30-DTE-ACCEPT         TO K110-DTE-ALLOCN                   
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-ALLO-SCHEME    TO K110-ALLOC-SCH.                      
           MOVE KA30-NUM-ETHNIC         TO K110-CATEGORY.                       
           MOVE 'A'                     TO K110-ALLOCN-STATUS.                  
           MOVE 'I'                     TO K110-ALLOCN-TYPE.                    
                                                                                
           IF KA30-AMT-TRNSCT-SELL-PRICE IS NUMERIC                             
              MOVE KA30-AMT-TRNSCT-SELL-PRICE                                   
                TO K110-PREV-SELLING-PRICE                                      
                   K110-CURR-SELLING-PRICE                                      
                   K110-1ST-OFFER-SELLING-PRICE                                 
           ELSE                                                                 
              MOVE ZEROES      TO K110-PREV-SELLING-PRICE                       
                                  K110-CURR-SELLING-PRICE                       
                                  K110-1ST-OFFER-SELLING-PRICE                  
           END-IF.                                                              
                                                                                
       3610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3620-UPDATE-BM06K110.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE K110-REC.                                                    
                                                                                
           IF BM06K110-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K110-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BM06K110 : ' BM06K110-STATUS            
              MOVE BM06K110-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3620-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3700-READ-BM06K100.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES             TO K100-KEY-FLD.                             
           MOVE K110-ESTATE        TO K100-ESTATE.                              
           MOVE K110-NEIGHBOURHOOD TO K100-NEIGHBOURHOOD.                       
           MOVE K110-CONTRACT-NO   TO K100-CONTRACT-NO.                         
           MOVE K110-BLK-NO        TO K100-BLK-NO.                              
           READ BM06K100.                                                       
                                                                                
           IF BM06K100-STATUS = 00                                              
              ADD 1    TO CNT-K100-FND                                          
              PERFORM 3710-MOVE-DATA-BM06K100 THRU 3710-EXIT                    
              PERFORM 3720-UPDATE-BM06K100    THRU 3720-EXIT                    
           ELSE                                                                 
              IF BM06K100-STATUS = 23                                           
                 ADD 1        TO CNT-K100-NOTFND                                
                 MOVE SPACES  TO K100-REC                                       
                 INITIALIZE      K100-REC                                       
                 DISPLAY 'RECORD NOT FOUND IN BM06K100 : ' K110-AIX1            
              ELSE                                                              
                 DISPLAY 'ERROR READING - BM06K100 : ' BM06K100-STATUS          
                 MOVE BM06K100-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3710-MOVE-DATA-BM06K100.                                                 
      *------------------------*                                                
                                                                                
           EVALUATE FA40-CDE-FLAT-TYPE                                          
               WHEN '1 '                                                        
                     MOVE  1 TO RM-PTR                                          
               WHEN '2 '                                                        
                     MOVE  2 TO RM-PTR                                          
               WHEN '3 '                                                        
                     MOVE  3 TO RM-PTR                                          
               WHEN '4 '                                                        
                     MOVE  4 TO RM-PTR                                          
               WHEN '5 '                                                        
                     MOVE  6 TO RM-PTR                                          
               WHEN '6 '                                                        
                     MOVE  7 TO RM-PTR                                          
               WHEN '7 '                                                        
                     MOVE  8 TO RM-PTR                                          
               WHEN '8 '                                                        
                     MOVE  9 TO RM-PTR                                          
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT 1 FROM K100-BAL-ALLOCN (RM-PTR).                            
                                                                                
           IF KA30-NUM-ETHNIC = '1'                                             
              ADD 1 TO K100-CAT1-ACCEPT (RM-PTR)                                
           END-IF.                                                              
                                                                                
           IF KA30-NUM-ETHNIC = '2'                                             
              ADD 1 TO K100-CAT2-ACCEPT (RM-PTR)                                
           END-IF.                                                              
                                                                                
           IF KA30-NUM-ETHNIC = '3'                                             
              ADD 1 TO K100-CAT3-ACCEPT (RM-PTR)                                
           END-IF.                                                              
                                                                                
       3710-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3720-UPDATE-BM06K100.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE K100-REC.                                                    
                                                                                
           IF BM06K100-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K100-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BM06K100 : ' BM06K100-STATUS            
              MOVE BM06K100-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3720-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3800-READ-BP13KD00.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES             TO KD00-KEY-FLD.                             
           MOVE K110-REGN-NO       TO KD00-NUM-REGN.                            
           READ BP13KD00.                                                       
                                                                                
           IF BP13KD00-STATUS = 00                                              
              ADD 1    TO CNT-KD00-FND                                          
              PERFORM 3810-MOVE-DATA-BP13KD00 THRU 3810-EXIT                    
              PERFORM 3820-REWRITE-BP13KD00   THRU 3820-EXIT                    
           ELSE                                                                 
              IF BP13KD00-STATUS = 23                                           
                 ADD 1        TO CNT-KD00-NOTFND                                
                 MOVE SPACES       TO BP13KD00-REC                              
                 INITIALIZE           BP13KD00-REC                              
                 PERFORM 3810-MOVE-DATA-BP13KD00 THRU 3810-EXIT                 
                 PERFORM 3830-WRITE-KD00         THRU 3830-EXIT                 
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS          
                 MOVE BP13KD00-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3810-MOVE-DATA-BP13KD00.                                                 
      *------------------------*                                                
                                                                                
           ACCEPT WS-CURRENT-TIME   FROM TIME.                                  
           MOVE WS-DATE               TO KD00-DTE-UPDATE.                       
           MOVE WS-CURRENT-TIME       TO KD00-TME-UPDATE.                       
           MOVE KA30-NUM-SCH-ACC      TO KD00-NUM-SCH-ACCNT.                    
           MOVE KA30-DTE-ACCEPT       TO KD00-DTE-OPTION.                       
           MOVE WS-DTE-DUE-SNP        TO KD00-DTE-DUE-SEND-SNP.                 
           MOVE KA30-NUM-REGN         TO KD00-NUM-REGN.                         
           MOVE KB20-NUM-PROJECT      TO KD00-NUM-PROJECT.                      
           MOVE K110-CURR-SELLING-PRICE                                         
                                      TO KD00-AMT-SELL-PRICE.                   
           MOVE KA30-AMT-OPTION-FEE   TO KD00-AMT-OPTION.                       
           IF KB20-NUM-CPFHG-REQ = 'Y'                                          
              MOVE KB20-NUM-CPFHG-REQ      TO KD00-NUM-HSGNT-REQ                
              MOVE KB20-NUM-CPFHG-PRIORITY TO KD00-NUM-HSGNT-ELIG               
              MOVE KA30-DTE-ACCEPT         TO KD00-DTE-HSGNT-CPFREQ             
              MOVE KB20-NUM-CHILD1-PARENT-UIN                                   
                                        TO KD00-NUM-HSGNT-PARENT-NRIC1          
              MOVE KB20-NUM-CHILD2-PARENT-UIN                                   
                                        TO KD00-NUM-HSGNT-PARENT-NRIC2          
           END-IF.                                                              
                                                                                
       3810-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3820-REWRITE-BP13KD00.                                                   
      *----------------------*                                                  
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF BP13KD00-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-KD00-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13KD00 : ' BP13KD00-STATUS            
              MOVE BP13KD00-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3820-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       3830-WRITE-KD00.                                                         
      *----------------*                                                        
                                                                                
           WRITE BP13KD00-REC.                                                  
                                                                                
           EVALUATE BP13KD00-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-KD00-WRITE                                     
               WHEN 22                                                          
                    ADD 1 TO CNT-KD00-DUP-REC                                   
                    DISPLAY "KD00 DUPLICATE REC FND, STATUS:"                   
                            BP13KD00-STATUS                                     
                    DISPLAY "KD00-KEY-FLD  = "  K800-NUM-REGN                   
               WHEN 02                                                          
                    ADD 1 TO CNT-KD00-WRITE                                     
                    DISPLAY "BP13KD00 DUP SCH-ACC: " KD00-NUM-SCH-ACCNT         
               WHEN OTHER                                                       
                    ADD 1 TO CNT-KD00-WRITE-ERROR                               
                    MOVE BP13KD00-STATUS         TO RETURN-CODE                 
                    DISPLAY "BP13KD00 READ FILE ERROR:" BP13KD00-STATUS         
                    PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3830-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3900-READ-BP13KD15.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES              TO KD15-KEY-FLD.                            
           MOVE KA30-NUM-PRJCT      TO KD15-NUM-PRJCT.                          
           MOVE KA30-NUM-BLK        TO KD15-NUM-BLK.                            
           MOVE KA30-NUM-MAIN-UNIT  TO KD15-NUM-MAIN-UNIT.                      
           MOVE KA30-NUM-SUB-UNIT   TO KD15-NUM-SUB-UNIT.                       
           MOVE KA30-NUM-LEVEL      TO KD15-NUM-LEVEL.                          
           READ BP13KD15.                                                       
                                                                                
           IF BP13KD15-STATUS = 00                                              
              ADD 1    TO CNT-KD15-FND                                          
              PERFORM 3910-MOVE-DATA-BP13KD15 THRU 3910-EXIT                    
              PERFORM 3920-UPDATE-BP13KD15    THRU 3920-EXIT                    
           ELSE                                                                 
              IF BP13KD15-STATUS = 23                                           
                 ADD 1        TO CNT-KD15-NOTFND                                
                 DISPLAY 'RECORD NOT FOUND IN BP13KD15 : ' KD15-KEY-FLD         
                 MOVE SPACES  TO BP13KD15-REC                                   
                 INITIALIZE      BP13KD15-REC                                   
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD15 : ' BP13KD15-STATUS          
                 MOVE BP13KD15-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3910-MOVE-DATA-BP13KD15.                                                 
      *------------------------*                                                
                                                                                
           MOVE KA30-NUM-PRJCT           TO KD15-NUM-PRJCT-KEY1                 
                                            KD15-NUM-PRJCT-KEY3.                
           MOVE KA30-NUM-REGN            TO KD15-NUM-REGN                       
                                            KD15-NUM-REGN3.                     
           MOVE KA30-NUM-SCH-ACC         TO KD15-NUM-SCH-ACC.                   
           MOVE KA30-CDE-UNIT-TYP        TO KD15-CDE-UNIT-TYP.                  
           MOVE KA30-DTE-ACCEPT          TO KD15-DTE-OPTION.                    
           MOVE WS-DATE                  TO KD15-DTE-UPDATE.                    
           MOVE WS-CURRENT-TIME          TO KD15-TME-UPDATE.                    
           MOVE 'BP13CA07'               TO KD15-NUM-USERID.                    
                                                                                
       3910-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3920-UPDATE-BP13KD15.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE BP13KD15-REC.                                                
                                                                                
           IF BP13KD15-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-KD15-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13KD15 : ' BP13KD15-STATUS            
              MOVE BP13KD15-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3920-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       4000-GET-DUE-SEND-SNP.                                                   
      *----------------------*                                                  
                                                                                
           MOVE KA30-DTE-ACCEPT                 TO WS-DTE-ACCEPT                
           COMPUTE WS-NUM-ACCEPT =                                              
                   FUNCTION INTEGER-OF-DATE (WS-DTE-ACCEPT)                     
           COMPUTE WS-NUM-ACCEPT = WS-NUM-ACCEPT + 56                           
                                                                                
           MOVE 'N'                             TO WS-DATE-VALID.               
                                                                                
           PERFORM UNTIL DATE-VALID                                             
              COMPUTE WS-DTE-DUE-SNP =                                          
                      FUNCTION DATE-OF-INTEGER (WS-NUM-ACCEPT)                  
              PERFORM 4100-CHECK-WEEKDAY      THRU 4100-EXIT                    
              IF WS-RETURN = 'SAT' OR 'SUN'                                     
                 COMPUTE WS-NUM-ACCEPT = WS-NUM-ACCEPT - 1                      
              ELSE                                                              
                 PERFORM 4200-CHECK-HOLIDAY   THRU 4200-EXIT                    
                 IF WS-RETURN = 'HOL'                                           
                    COMPUTE WS-NUM-ACCEPT = WS-NUM-ACCEPT - 1                   
                 ELSE                                                           
                    MOVE 'Y'                    TO WS-DATE-VALID                
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4100-CHECK-WEEKDAY.                                                      
      *-------------------*                                                     
                                                                                
           MOVE 'DW'                          TO WS-IN-OPTION                   
           MOVE WS-DTE-DUE-SNP                TO WS-IN-DATE                     
                                                                                
           CALL 'AV02C001' USING LINK-REC.                                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4200-CHECK-HOLIDAY.                                                      
      *-------------------*                                                     
                                                                                
           MOVE WS-DTE-DUE-SNP          TO WS-IN-DATE.                          
           MOVE 'HD'                    TO WS-IN-OPTION.                        
                                                                                
           CALL 'AV02C002'  USING LINK-REC.                                     
                                                                                
           IF WS-RETURN = 'EOP' OR 'EID' OR 'EIF' OR 'EIR'                      
              DISPLAY 'ERROR IN READING HOLIDAY FILE, RETURN CODE IS '          
                      WS-RETURN                                                 
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CA07 *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13FA40 : ' CNT-FA40-READ.           
           DISPLAY 'NO OF RECS READ FROM  BP13KA30 : ' CNT-KA30-READ.           
           DISPLAY 'NO OF RECS NOTFND  IN BP13KA30 : ' CNT-KA30-NOTFND.         
           DISPLAY 'NO OF RECS READ FROM  BP13KB20 : ' CNT-KB20-READ.           
           DISPLAY 'NO OF RECS NOTFND  IN BP13KB20 : ' CNT-KB20-NOTFND.         
           DISPLAY 'NO OF RECS W/ SCHACC  BP13KB20 : '                          
                    CNT-KB20-SCHACC-FND.                                        
           DISPLAY 'NO OF RECS W/O SCHACC BP13KB20 : '                          
                    CNT-KB20-SCHACC-BLANK.                                      
           DISPLAY 'NO OF RECS SCHACC NOT MATCHED IN BP13KB20 : '               
                    CNT-KB20-SCHACC-NMTCH.                                      
           DISPLAY 'NO OF RECS FOUND   IN BP13K800 : ' CNT-K800-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BP13K800 : ' CNT-K800-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BP13K800 : ' CNT-K800-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BP13K840 : ' CNT-K840-FND.            
           DISPLAY 'NO OF RECS WRITE   IN BP13K840 : ' CNT-K840-WRITE.          
           DISPLAY 'NO OF RECS UPDATED IN BP13K840 : ' CNT-K840-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BM06K110 : ' CNT-K110-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BM06K110 : ' CNT-K110-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BM06K110 : ' CNT-K110-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BM06K100 : ' CNT-K100-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BM06K100 : ' CNT-K100-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BM06K100 : ' CNT-K100-REWRITE.        
           DISPLAY 'NO OF RECS NOTFND  IN BP13KD00 : ' CNT-KD00-NOTFND.         
           DISPLAY 'NO OF RECS WRITE   IN BP13KD00 : ' CNT-KD00-WRITE.          
           DISPLAY 'NO OF RECS FOUND   IN BP13KD00 : ' CNT-KD00-FND.            
           DISPLAY 'NO OF RECS UPDATED IN BP13KD00 : ' CNT-KD00-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BP13KD15 : ' CNT-KD15-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BP13KD15 : ' CNT-KD15-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BP13KD15 : ' CNT-KD15-REWRITE.        
           DISPLAY 'NO OF DUPLICATE RECS  BP13KD00 : '                          
                    CNT-KD00-DUP-REC.                                           
           DISPLAY 'NO OF WRITE ERROR  IN BP13KD00 : '                          
                    CNT-KD00-WRITE-ERROR.                                       
           DISPLAY 'NO OF NOT FOUND IN BP13KA30    : ' CNT-FA40-WRITE.          
                                                                                
           CLOSE      BP13FA40                                                  
                      P13FA40A                                                  
                      BP13KB20                                                  
                      BP13KA30                                                  
                      BP13K800                                                  
                      BP13K840                                                  
                      BP13KD00                                                  
                      BM06K110                                                  
                      BM06K100                                                  
                      BP13KD15.                                                 
                                                                                
           IF BP13KB20-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13KB20 : ' BP13KB20-STATUS             
              MOVE BP13KB20-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KA30-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13KA30 : ' BP13KA30-STATUS             
              MOVE BP13KA30-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K840-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13K840 : ' BP13K840-STATUS             
              MOVE BP13K840-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD00-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BM06K100 : ' BM06K100-STATUS             
              MOVE BM06K100-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD15-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13KD15 : ' BP13KD15-STATUS             
              MOVE BP13KD15-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
