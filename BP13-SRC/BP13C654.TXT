       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C654.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
      *DATE-WRITTEN.  04/04/92.                                                 
                                                                                
      **********************************************************                
      *   OBJECTIVE    :                                       *                
      *      TO DELETE REFUND CASES AFTER CASE IS INTERFACE    *                
      *      WITH VOUCHER.                                     *                
      *                                                        *                
      *    INPUT FILE :                                        *                
      *        1. BP13T.F651.INTVRCH  (BP13F651)               *                
      *    I-O   FILE :                                        *                
      *        1. BP13T.K650.PREPVRCH (BP13K650)               *                
      *    REPORT :                                            *                
      *        1. BP13L654   - ERROR LISTING                   *                
      *                                                        *                
      *    REVISION HISTORY :                                  *                
      *--------------------------------------------------------*                
      * CHG-NO   BY  DATE    DESCRIPTION                       *                
      *--------------------------------------------------------*                
      * XXXXXXXX RML 930806  EXPAND REGN                       *                
      * BP130418 SGK 981012  CHG Y2K CHANGES                   *                
      * BP132155 MCC 020605  DELETE F650 & K650 COPYBOOK       *                
      * BP135616 JGO1090315  CHANGE LENGTH OF F650/K650 TO 500 *                
      **********************************************************                
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT    DIVISION.                                                 
      *-----------------------*                                                 
       CONFIGURATION  SECTION.                                                  
      *-----------------------*                                                 
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F650 ASSIGN       TO BP13F650.                            
           SELECT BP13F651 ASSIGN       TO BP13F651.                            
           SELECT BP13K650 ASSIGN       TO BP13K650                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS DYNAMIC                               
                           RECORD KEY IS K650-KEY-FLD                           
                           FILE STATUS IS WS-K650-STATUS.                       
           SELECT BP13L654 ASSIGN       TO BP13L654.                            
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
       FD  BP13F651           BLOCK  CONTAINS    0 RECORDS                      
                              RECORD CONTAINS  500 CHARACTERS                   
                              LABEL  RECORDS STANDARD.                          
       COPY BP13F651.                                                           
                                                                                
       FD  BP13F650           BLOCK  CONTAINS    0 RECORDS                      
                              RECORD CONTAINS  500 CHARACTERS                   
                              LABEL  RECORDS STANDARD.                          
       01   BP13F650-REC      PIC X(500).                                       
                                                                                
       FD  BP13K650           RECORD CONTAINS 500 CHARACTERS.                   
       01  BP13K650-REC.                                                        
           05 K650-KEY-FLD.                                                     
              10 K650-NUM-REGN    PIC X(8).                                     
           05 FILLER              PIC X(177).                                   
           05 K650-AMT-REFUND     PIC S9(5)V99.                                 
           05 FILLER              PIC X(308).                                   
                                                                                
       FD  BP13L654           RECORD CONTAINS 132 CHARACTERS                    
                              LABEL RECORDS  OMITTED.                           
       01  BP13L654-REC       PIC X(132).                                       
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-FLG.                                                              
           05  WS-EOF-F651          PIC X(3)     VALUE 'NO '.                   
               88  END-OF-F651                   VALUE 'YES'.                   
                                                                                
       01  WS-CNT.                                                              
           05  WS-COUNT            PIC 9(5)      VALUE ZERO.                    
           05  WS-CNT-F651         PIC 9(5)      VALUE ZERO.                    
           05  WS-CNT-K650         PIC 9(5)      VALUE ZERO.                    
           05  WS-CNT-PROC         PIC 9(5)      VALUE ZERO.                    
           05  WS-CNT-F650-WRITE   PIC 9(5)      VALUE ZERO.                    
           05  WS-LINE             PIC 9(2)      VALUE 60.                      
           05  WS-PAGE             PIC 9(4)      VALUE ZEROES.                  
           05  WS-K650-STATUS      PIC 99        VALUE ZEROES.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CC          PIC X(2)      VALUE SPACES.                  
           05  WS-CURR-YY          PIC X(2)      VALUE SPACES.                  
           05  WS-CURR-MM          PIC X(2)      VALUE SPACES.                  
           05  WS-CURR-DD          PIC X(2)      VALUE SPACES.                  
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD        PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-MM        PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE '/'.                     
           05  WS-EDITED-CC        PIC X(2)      VALUE SPACES.                  
           05  WS-EDITED-YY        PIC X(2)      VALUE SPACES.                  
                                                                                
       01  WS-K650-KEY.                                                         
           05  WS-K650-RESERVE     PIC X         VALUE SPACE.                   
           05  WS-K650-REGN        PIC X(8)      VALUE SPACES.                  
                                                                                
      *----------------------------------------*                                
      *   FORMAT OF THE ERROR   LISTING        *                                
      *----------------------------------------*                                
                                                                                
       01  WS-L654-HDG1.                                                        
           05  FILLER              PIC X(8)      VALUE 'BP13L654'.              
           05  FILLER              PIC X(39)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
           'S Y S T E M   O F   C O M M I T M E N T'.                           
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE : '.               
           05  L654-DATE           PIC X(10).                                   
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L654-PAGE           PIC 9(3).                                    
                                                                                
       01  WS-L654-HDG2.                                                        
           05  FILLER              PIC X(44)     VALUE SPACES.                  
           05  FILLER              PIC X(45)     VALUE                          
                 'ERROR REPORT FOR FILE BP13K650 WHILE DELETION'.               
                                                                                
       01  WS-L654-HDG3.                                                        
           05  FILLER              PIC X         VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'REGN   '.               
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'REMARK'.                
                                                                                
       01  WS-L654-DTL.                                                         
           05  FILLER              PIC X         VALUE SPACES.                  
           05  L654-REGNO          PIC X(8).                                    
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  L654-REMARK         PIC X(30).                                   
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE      DIVISION.                                                 
      *-----------------------*                                                 
                                                                                
      ***----------***                                                          
       000-CONTROL.                                                             
      ***----------***                                                          
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           PERFORM 200-READ-F651       THRU 200-EXIT.                           
           PERFORM 300-MAIN-ROUTINE    THRU 300-EXIT UNTIL                      
                   END-OF-F651.                                                 
           PERFORM 700-DISPLAY-TOTAL   THRU 700-EXIT.                           
           PERFORM 800-CLOSE-FILES     THRU 800-CLOSE-EXIT.                     
                                                                                
       000-EXIT.                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ***-----------***                                                         
       100-OPEN-FILES.                                                          
      ***-----------***                                                         
           OPEN INPUT BP13F651                                                  
                  I-O BP13K650                                                  
               OUTPUT BP13F650                                                  
                      BP13L654.                                                 
                                                                                
           MOVE  'NO'          TO WS-EOF-F651.                                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***----------***                                                          
       200-READ-F651.                                                           
      ***----------***                                                          
           READ BP13F651 AT END                                                 
           MOVE  'YES' TO WS-EOF-F651                                           
           GO TO 200-EXIT.                                                      
           ADD 1 TO WS-CNT-F651.                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***----------***                                                          
       250-READ-K650.                                                           
      ***----------***                                                          
           MOVE F651-KEY-FLD       TO K650-KEY-FLD.                             
           READ  BP13K650.                                                      
                                                                                
           IF WS-K650-STATUS = 00                                               
              IF K650-AMT-REFUND NOT NUMERIC                                    
                 MOVE ZEROS  TO K650-AMT-REFUND                                 
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN BP13K650 ' WS-K650-STATUS               
              DISPLAY 'K650-NUM-REGN ' K650-NUM-REGN.                           
                                                                                
       250-K650-EXIT.                                                           
           EXIT.                                                                
                                                                                
                                                                                
      ***---------------***                                                     
       300-MAIN-ROUTINE.                                                        
      ***---------------***                                                     
           IF F651-NUM-REGN = SPACES OR LOW-VALUES                              
              PERFORM 200-READ-F651     THRU 200-EXIT                           
              GO TO 300-EXIT                                                    
           ELSE                                                                 
              PERFORM 250-READ-K650     THRU 250-K650-EXIT.                     
                                                                                
           IF WS-K650-STATUS = 00                                               
              ADD 1 TO WS-CNT-K650                                              
              IF F651-AMT-REFUND  = K650-AMT-REFUND                             
                 PERFORM 450-WRITE-F650  THRU 450-EXIT                          
                 PERFORM 400-DELETE-K650 THRU 400-DELETE-EXIT                   
              ELSE                                                              
                 MOVE 'REFUND AMOUNT NOT MATCHING    ' TO L654-REMARK           
                 PERFORM 500-ERROR-LISTING THRU 500-EXIT                        
           ELSE                                                                 
              IF WS-K650-STATUS = 23                                            
                 MOVE 'RECORD NOT FOUND IN BP13K650  ' TO L654-REMARK           
                 PERFORM 500-ERROR-LISTING THRU 500-EXIT.                       
                                                                                
           PERFORM 200-READ-F651     THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***---------------***                                                     
       400-DELETE-K650.                                                         
      ***---------------***                                                     
           DELETE BP13K650.                                                     
                                                                                
           IF WS-K650-STATUS = 00                                               
              ADD   1                   TO WS-CNT-PROC                          
           ELSE                                                                 
              DISPLAY                                                           
              'ERROR WHILE DELETING -- BP13K650 = ' WS-K650-STATUS              
              MOVE WS-K650-STATUS    TO RETURN-CODE.                            
                                                                                
       400-DELETE-EXIT.                                                         
           EXIT.                                                                
                                                                                
                                                                                
      ***---------------***                                                     
       450-WRITE-F650.                                                          
      ***---------------***                                                     
           MOVE   BP13K650-REC TO BP13F650-REC.                                 
           WRITE  BP13F650-REC.                                                 
           ADD   1             TO WS-CNT-F650-WRITE.                            
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***---------------***                                                     
       500-ERROR-LISTING.                                                       
      ***---------------***                                                     
           IF WS-LINE > 55                                                      
             PERFORM 600-PRINT-HEADING THRU 600-EXIT.                           
                                                                                
           ADD     1                   TO WS-LINE.                              
           MOVE  F651-NUM-REGN         TO L654-REGNO.                           
           WRITE   BP13L654-REC        FROM WS-L654-DTL AFTER 1.                
           MOVE SPACES TO BP13L654-REC.                                         
           ADD 1 TO WS-COUNT.                                                   
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***---------------***                                                     
       600-PRINT-HEADING.                                                       
      ***---------------***                                                     
           ADD     1               TO   WS-PAGE.                                
           MOVE    WS-PAGE         TO   L654-PAGE.                              
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                   
                                                                                
           MOVE    WS-CURR-CC      TO WS-EDITED-CC.                             
           MOVE    WS-CURR-YY      TO WS-EDITED-YY.                             
           MOVE    WS-CURR-MM      TO WS-EDITED-MM.                             
           MOVE    WS-CURR-DD      TO WS-EDITED-DD.                             
           MOVE    WS-EDITED-DATE  TO L654-DATE.                                
                                                                                
           WRITE   BP13L654-REC FROM    WS-L654-HDG1 AFTER PAGE.                
           WRITE   BP13L654-REC FROM    WS-L654-HDG2 AFTER 1.                   
           WRITE   BP13L654-REC FROM    WS-L654-HDG3 AFTER 2.                   
           MOVE    SPACES          TO   BP13L654-REC.                           
           WRITE   BP13L654-REC                   AFTER  1.                     
           MOVE    5               TO   WS-LINE.                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***---------------***                                                     
       700-DISPLAY-TOTAL.                                                       
      ***---------------***                                                     
           DISPLAY  'PROGRAM ID  :  BP13C654'.                                  
           DISPLAY  ' '.                                                        
           DISPLAY                                                              
           'NO OF RECORDS READ FROM BP13F651    = ' WS-CNT-F651.                
           DISPLAY                                                              
           'NO OF RECORDS DELETED FROM BP13K650 = ' WS-CNT-PROC.                
           DISPLAY                                                              
           'NO OF RECORDS WRITTEN TO   BP13F650 = ' WS-CNT-F650-WRITE.          
           DISPLAY                                                              
           'NO OF ERROR RECORDS PRINTED         = ' WS-COUNT.                   
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***---------------***                                                     
       800-CLOSE-FILES.                                                         
      ***---------------***                                                     
           CLOSE BP13F651                                                       
                 BP13K650                                                       
                 BP13F650                                                       
                 BP13L654.                                                      
                                                                                
       800-CLOSE-EXIT.                                                          
           EXIT.                                                                
