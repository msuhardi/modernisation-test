      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C484.                                                 
      *AUTHOR.        MALC.                                                     
      *DATE-WRITTEN.  19MAY2001.                                                
                                                                                
      *========================================================*                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *                                                        *                
      *    INPUT  FILE  : BP13F410                             *                
      *                   BP13K800                             *                
      *                                                        *                
      *    LISTING      : BP13F41A - DEBTOR CLEARED CASES, NEED*                
      *                              FOR REPORTING, < 1 MTH    *                
      *                   BP13F41B - DEBTOR CLEARED AND REPORT *                
      *                              PRINTED CASES, > 1 MTH    *                
      *                              TO BE DELETED IN BP13C423 *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ====== ===================================*                
      *BP130998 MALC 190501 MONITOR THE DELETION OF SA FLAT    *                
      *                     FROM BP13K800/BP13K820.            *                
      *BP131039 CLT  150801 NOT TO WRITE TO BP13F41B IF        *                
      *                     AMT NOT CLEARED.                   *                
      *========================================================*                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13F41A ASSIGN TO BP13F41A.                                  
                                                                                
           SELECT BP13F41B ASSIGN TO BP13F41B.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K410-NUM-REGN                              
                  FILE STATUS     IS WS-K410-STATUS.                            
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13F41A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   BP13F41A-REC      PIC X(2000).                                      
                                                                                
       FD   BP13F41B                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   BP13F41B-REC      PIC X(2000).                                      
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K800-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-K410-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-CHAR-NOT-SPACE              PIC X     VALUE 'N'.              
                                                                                
       01  WS-WORKING-VAR.                                                      
           05  WS-DTE-KEY-ISSUE               PIC 9(8)  VALUE ZEROS.            
           05  WS-DAYS-KEY-ISSUE              PIC 9(8)  VALUE ZEROS.            
           05  WS-CUR-DAYS                    PIC 9(8)  VALUE ZEROS.            
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURRENT-CCYY            PIC 9999.                         
               10  WS-CURRENT-MM              PIC 99.                           
               10  WS-CURRENT-DD              PIC 99.                           
           05  WS-CURRENT-DATE-9 REDEFINES WS-CURRENT-DATE PIC 9(8).            
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CTR                         PIC 999   VALUE ZEROS.            
           05  WS-REP-CTR                     PIC 999   VALUE ZEROS.            
           05  WS-CNT-NFND-K800               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-NFND-K410               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-SKIP                    PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRIT-K410               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-DEL-K410                PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-REWR-K410               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-F410               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRIT-F41A               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRIT-F41B               PIC 9(6)  VALUE ZEROS.            
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS-BP13F410  THRU 2000-EXIT                        
             UNTIL (WS-F410-EOF-SW = 'Y').                                      
                                                                                
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
           OPEN INPUT  BP13F410                                                 
                       BP13K800                                                 
                I-O    BP13K410                                                 
                OUTPUT BP13F41A                                                 
                       BP13F41B.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS IS ' WS-K800-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 OPEN ERROR, STATUS IS ' WS-K410-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           COMPUTE WS-CUR-DAYS =                                                
                   FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE-9) + 0.             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-PROCESS-BP13F410.                                                   
      ****************************************************************          
           READ BP13F410                                                        
              AT END     MOVE 'Y'        TO WS-F410-EOF-SW                      
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD +1                        TO WS-CNT-READ-F410.                   
                                                                                
           PERFORM 3000-READ-BP13K800 THRU 3000-EXIT.                           
           PERFORM 4000-READ-BP13K410 THRU 4000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-READ-BP13K800.                                                      
      ****************************************************************          
           MOVE F410-NUM-REGN  TO K800-NUM-REGN.                                
           READ BP13K800   KEY IS K800-NUM-REGN.                                
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              ADD +1  TO WS-CNT-NFND-K800                                       
              DISPLAY 'BP13K800 RECORD NOT FOUND, KEY IS ' K800-NUM-REGN        
              GO TO 2000-EXIT                                                   
           ELSE                                                                 
              DISPLAY 'BP13K800 READ ERROR, STATUS IS ' WS-K800-STATUS          
              DISPLAY 'K800 KEY-FLD-' K800-NUM-REGN                             
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4000-READ-BP13K410.                                                      
      ****************************************************************          
           DISPLAY '4000-READ-K410'.                                            
           DISPLAY 'F410-NUM-REGN = ' F410-NUM-REGN.                            
                                                                                
           MOVE F410-NUM-REGN  TO K410-NUM-REGN.                                
           READ BP13K410   KEY IS K410-NUM-REGN.                                
                                                                                
           DISPLAY 'WS-K410-STATUS = ' WS-K410-STATUS.                          
                                                                                
           IF F410-AMT-REFUND-CD NOT NUMERIC                                    
              MOVE ZEROES TO F410-AMT-REFUND-CD                                 
           END-IF.                                                              
                                                                                
           IF F410-AMT-REFUND-BK NOT NUMERIC                                    
              MOVE ZEROES TO F410-AMT-REFUND-BK                                 
           END-IF.                                                              
                                                                                
           IF F410-AMT-REFUND-ADV-COL NOT NUMERIC                               
              MOVE ZEROES TO F410-AMT-REFUND-ADV-COL                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-DEBTOR NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-SOC-DEBTOR                                
           END-IF.                                                              
                                                                                
           IF F410-DTE-KEY-ISSUE NOT NUMERIC                                    
              MOVE ZEROES TO F410-DTE-KEY-ISSUE                                 
           END-IF.                                                              
                                                                                
           MOVE F410-DTE-KEY-ISSUE  TO WS-DTE-KEY-ISSUE.                        
           IF F410-DTE-KEY-ISSUE > ZEROES                                       
              COMPUTE WS-DAYS-KEY-ISSUE =                                       
                   FUNCTION INTEGER-OF-DATE(WS-DTE-KEY-ISSUE) + 60              
           ELSE                                                                 
              MOVE ZEROES           TO WS-DAYS-KEY-ISSUE                        
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS = 00                                               
              PERFORM 4100-WRITE-K410-00    THRU 4100-EXIT                      
           ELSE                                                                 
              IF WS-K410-STATUS = 23                                            
                 PERFORM 4200-WRITE-K410-23 THRU 4200-EXIT                      
              ELSE                                                              
                 DISPLAY 'BP13K410 READ ERROR, STATUS IS '                      
                          WS-K410-STATUS                                        
                 DISPLAY 'K410 KEY-FLD-' K410-NUM-REGN                          
                 GO TO   9999-CLOSE-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4100-WRITE-K410-00.                                                      
      ****************************************************************          
           DISPLAY 'F410-NUM-REGN           = ' F410-NUM-REGN.                  
           DISPLAY 'F410-AMT-REFUND-CD      = ' F410-AMT-REFUND-CD.             
           DISPLAY 'F410-AMT-REFUND-BK      = ' F410-AMT-REFUND-BK.             
           DISPLAY 'F410-AMT-REFUND-ADV-COL = ' F410-AMT-REFUND-ADV-COL.        
           DISPLAY 'SOC DETBOR              = ' K800-AMT-SOC-DEBTOR.            
           DISPLAY 'SOC CASH                = ' K800-AMT-SOC-CASH.              
           DISPLAY 'CUR DAY                 = ' WS-CUR-DAYS.                    
           DISPLAY 'KI  DAY + 60            = ' WS-DAYS-KEY-ISSUE.              
                                                                                
           IF K800-AMT-SOC-DEBTOR = ZEROES                                      
              IF WS-CUR-DAYS >=  WS-DAYS-KEY-ISSUE                              
                 IF F410-AMT-REFUND-CD         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-BK         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-ADV-COL    NOT = ZEROES                     
                    WRITE BP13F41A-REC FROM BP13F410-REC                        
                    ADD      01           TO   WS-CNT-WRIT-F41A                 
                 END-IF                                                         
                 IF K800-AMT-BK-PAID           > ZEROES OR                      
                    K800-AMT-CD                > ZEROES OR                      
                    K800-AMT-SOC-CASH          > ZEROES OR                      
                    K800-AMT-CPF1-PAID         > ZEROES OR                      
                    K800-AMT-CPF2-PAID         > ZEROES OR                      
                    K800-AMT-CPF3-PAID         > ZEROES OR                      
                    K800-AMT-CPF4-PAID         > ZEROES OR                      
                    K800-AMT-REGN-DEPOSIT-CASH > ZEROES OR                      
                    K800-AMT-REGN-DEPOSIT-CPF1 > ZEROES OR                      
                    K800-AMT-REGN-DEPOSIT-CPF2 > ZEROES OR                      
                    K800-AMT-REGN-DEPOSIT-CPF3 > ZEROES OR                      
                    K800-AMT-REGN-DEPOSIT-CPF4 > ZEROES                         
                    DISPLAY 'AMT NOT CLEARED, CANNOT DROPPED !'                 
                    CONTINUE                                                    
                 ELSE                                                           
                    DISPLAY 'AMT CLEARED !!'                                    
                    WRITE BP13F41B-REC FROM BP13F410-REC                        
                    ADD         01           TO   WS-CNT-WRIT-F41B              
                    DELETE BP13K410                                             
                    ADD         01           TO   WS-CNT-DEL-K410               
                 END-IF                                                         
              ELSE                                                              
                 IF F410-AMT-REFUND-CD         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-BK         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-ADV-COL    NOT = ZEROES                     
                    WRITE BP13F41A-REC FROM BP13F410-REC                        
                    ADD      01           TO   WS-CNT-WRIT-F41A                 
                    MOVE ZEROES TO K410-AMT-REFUND-CD                           
                                   K410-AMT-REFUND-BK                           
                                   K410-AMT-REFUND-ADV-COL                      
                    REWRITE BP13K410-REC                                        
                    ADD 1 TO WS-CNT-REWR-K410                                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4200-WRITE-K410-23.                                                      
      ****************************************************************          
           DISPLAY 'F410-NUM-REGN           = ' F410-NUM-REGN.                  
           DISPLAY 'F410-AMT-REFUND-CD      = ' F410-AMT-REFUND-CD.             
           DISPLAY 'F410-AMT-REFUND-BK      = ' F410-AMT-REFUND-BK.             
           DISPLAY 'F410-AMT-REFUND-ADV-COL = ' F410-AMT-REFUND-ADV-COL.        
           DISPLAY 'SOC DETBOR              = ' K800-AMT-SOC-DEBTOR.            
           DISPLAY 'CUR DAY                 = ' WS-CUR-DAYS.                    
           DISPLAY 'KI  DAY + 60            = ' WS-DAYS-KEY-ISSUE.              
                                                                                
           ADD 1 TO WS-CNT-NFND-K410.                                           
                                                                                
           IF K800-AMT-SOC-DEBTOR = ZEROES                                      
              IF WS-CUR-DAYS >= WS-DAYS-KEY-ISSUE                               
                 IF F410-AMT-REFUND-CD         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-BK         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-ADV-COL    NOT = ZEROES                     
                    WRITE BP13F41A-REC FROM BP13F410-REC                        
                    ADD      01           TO   WS-CNT-WRIT-F41A                 
                 END-IF                                                         
                 WRITE BP13F41B-REC FROM BP13F410-REC                           
                 ADD         01           TO   WS-CNT-WRIT-F41B                 
              ELSE                                                              
                 IF F410-AMT-REFUND-CD         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-BK         NOT = ZEROES  OR                 
                    F410-AMT-REFUND-ADV-COL    NOT = ZEROES                     
                    WRITE BP13F41A-REC FROM BP13F410-REC                        
                    ADD      01           TO   WS-CNT-WRIT-F41A                 
                    MOVE ZEROES TO F410-AMT-REFUND-CD                           
                                   F410-AMT-REFUND-BK                           
                                   F410-AMT-REFUND-ADV-COL                      
                 END-IF                                                         
                 WRITE BP13K410-REC FROM BP13F410-REC                           
                 ADD 1 TO WS-CNT-WRIT-K410                                      
              END-IF                                                            
           ELSE                                                                 
              WRITE BP13K410-REC FROM BP13F410-REC                              
              ADD 1 TO WS-CNT-WRIT-K410                                         
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : '  WS-CURRENT-DATE                               
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C484 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F410 RECORDS READ        = ' WS-CNT-READ-F410.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13K800 RECORDS NOT FOUND   = ' WS-CNT-NFND-K800.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13K410 RECORDS NOT FOUND   = ' WS-CNT-NFND-K410.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13K410 RECORDS WRITTEN     = ' WS-CNT-WRIT-K410.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13K410 RECORDS REWRITTEN   = ' WS-CNT-REWR-K410.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13K410 RECORDS DELETED     = ' WS-CNT-DEL-K410.           
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F41A RECORDS WRITTEN     = ' WS-CNT-WRIT-F41A.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F41B RECORDS WRITTEN     = ' WS-CNT-WRIT-F41B.          
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F410                                                       
                 BP13K800                                                       
                 BP13K410                                                       
                 BP13F41A                                                       
                 BP13F41B.                                                      
           STOP RUN.                                                            
