       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C809.                                                    
      *AUTHOR. LSB.                                                             
      *DATE-WRITTEN : 31.08.94                                                  
      *-------------------------------------------------------------*           
      *  SYSTEM OF COMMITMENT (BP13)                                *           
      *-------------------------------------------------------------*           
      *  OBJECTIVE  :                                               *           
      *      PROGRAM BASED ON BP13F500 SEQUENCE FILE WHICH ALREADY  *           
      *      TAGGED AS UPG OR UP3 UPGRADER TO MATCH SOC OCCUPIER.   *           
      *      IF MATCH OUTPUT OCCUPIER REC FOR DETERMINE 5 YRS       *           
      *      OCCUPATION PERIOD.                                     *           
      *                                                             *           
      * NOTE : SIMILAR PROGRAM IS BP13C804 FOR MATCHING OF          *           
      *        PARENT/CHILD IN JCL BP13A534                         *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC DATE    DESCRIPTION                            *           
      * -------- --- ------  -----------                            *           
      * N940014  YCH 111094  CHANGE OF KEY FIELD                    *           
      * C950230  LSB 110295  DO NOT UPDATE DTE-REGN, ALREADY UPD IN *           
      *                      BP13C577 (BP13A537/BP13A500 JCL)       *           
      * BP130445 SSS 050298  EXPAND F558 RECL FROM 150 TO 400       *           
      * BP130652 NOR 190299  EXPAND F500 RECL FROM 300 TO 500       *           
      * BP130897 AKT 040800  EXPAND F558 RECL FROM 400 TO 600       *           
      *-------------------------------------------------------------*           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F558                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 600 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F558.                                                           
                                                                                
       FD  BP13F500                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F500.                                                           
                                                                                
       FD  P13F558O                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 600 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
                                                                                
       01  P13F558O-REC            PIC X(600).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-SUB             PIC 9(2)    VALUE ZERO.                      
       01  WS-F558-STORED          PIC 9(2)    VALUE ZERO.                      
       01  WS-RESALE-FND           PIC X(1)    VALUE 'N'.                       
                                                                                
       01  WS-F558-REGN.                                                        
           05  WS-F558-REF         PIC X(6)    VALUE SPACES.                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
                                                                                
       77  WS-EOF-F500             PIC X(3).                                    
           88  EOF-OF-F500                     VALUE 'YES'.                     
       77  WS-EOF-F558             PIC X(3).                                    
           88  EOF-OF-F558                     VALUE 'YES'.                     
                                                                                
       COPY WSF558.                                                             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F500 THRU 200-EXIT.                                 
           PERFORM 300-READ-F558 THRU 300-EXIT.                                 
           PERFORM 400-PROCESS   THRU 400-EXIT                                  
                   UNTIL EOF-OF-F500 AND EOF-OF-F558.                           
           PERFORM 900-FINALIZATION THRU 900-EXIT.                              
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BP13F500                                                 
                       BP13F558                                                 
                OUTPUT P13F558O.                                                
           MOVE 'NO' TO WS-EOF-F500.                                            
           MOVE 'NO' TO WS-EOF-F558.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F500.                                                           
           READ BP13F500 AT END MOVE 'YES' TO WS-EOF-F500                       
                                MOVE HIGH-VALUE TO F500-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F500-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F558.                                                           
           READ BP13F558 AT END MOVE 'YES' TO WS-EOF-F558                       
                                MOVE HIGH-VALUE TO F558-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F558-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REGN                                               *         
      *****************************************************************         
                                                                                
       400-PROCESS.                                                             
           MOVE 1      TO WS-F558-SUB.                                          
           MOVE 'N'    TO WS-RESALE-FND.                                        
           MOVE 0      TO WS-F558-STORED.                                       
           MOVE SPACES TO WS-F558-RECORD.                                       
                                                                                
           IF F500-NUM-REGN    = F558-NUM-REGN                                  
                PERFORM 450-STORE-F558   THRU 450-EXIT                          
                      UNTIL EOF-OF-F558 OR                                      
                      F558-NUM-REGN NOT = F500-NUM-REGN                         
                PERFORM 460-DETERMINE-OUTPUT THRU 460-EXIT                      
           ELSE                                                                 
           IF F500-NUM-REGN   < F558-NUM-REGN                                   
                ADD 1 TO WS-F500-NOTMATCH                                       
                PERFORM 200-READ-F500 THRU 200-EXIT                             
           ELSE                                                                 
                ADD 1 TO WS-F558-NOTMATCH                                       
                PERFORM 300-READ-F558 THRU 300-EXIT.                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-STORE-F558.                                                          
           MOVE BP13F558-REC TO WS-F558-REC (WS-F558-SUB).                      
                                                                                
           ADD 1 TO WS-F558-SUB.                                                
           ADD 1 TO WS-F558-STORED.                                             
           PERFORM 300-READ-F558 THRU 300-EXIT.                                 
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       460-DETERMINE-OUTPUT.                                                    
           PERFORM 500-OUTPUT-MATCH THRU 500-EXIT                               
                      VARYING WS-F558-SUB FROM 1 BY 1 UNTIL                     
                      WS-F558-SUB > WS-F558-STORED.                             
                                                                                
           PERFORM 200-READ-F500 THRU 200-EXIT.                                 
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-OUTPUT-MATCH.                                                        
           ADD 1 TO WS-MATCH.                                                   
           MOVE WS-F558-REC (WS-F558-SUB) TO P13F558O-REC.                      
           WRITE P13F558O-REC.                                                  
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*--------BP13C809  -----------*'.                         
             DISPLAY '* UPG-REQUEST MATCH OCCUP REC FOR '.                      
             DISPLAY '* FOR FIVE YEARS OCCUPATION PERIOD'.                      
             DISPLAY ' BP13F500-READ      ' WS-F500-READ.                       
             DISPLAY ' BP13F558-READ      ' WS-F558-READ.                       
             DISPLAY ' MATCH              ' WS-MATCH.                           
             DISPLAY ' F500-NOT-MATCH     ' WS-F500-NOTMATCH.                   
             DISPLAY ' F558-NOT-MATCH     ' WS-F558-NOTMATCH.                   
                                                                                
           CLOSE      BP13F500                                                  
                      BP13F558                                                  
                      P13F558O.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
