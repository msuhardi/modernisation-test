       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C678.                                                    
      *AUTHOR. RHB.                                                             
      *DATE-WRITTEN. 27/03/97.                                                  
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      * 1. FOR BOOKED FLAT CASES, CHECK FIS KEY AVAILABLE DATE,*                
      *    OUTPUT RECORDS TO CLEAR FILE SYSTEM FOR PRINTING    *                
      *    PRE-ELIGIBILITY REPORT.                             *                
      *                                                        *                
      * INPUT : BP13F310                                       *                
      *                                                        *                
      * I-O FILE : BM06K110 (SOC MASTER)                       *                
      *                                                        *                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP130268 LSB 970404 - NEW PGM                          *                
      * BP130418 SIA 980320 - CHGE BM06K211 TO BM06K110        *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310   ASSIGN       TO BP13F310.                          
                                                                                
           SELECT BM06K110   ASSIGN       TO BM06K110                           
                             ACCESS       IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K110-KEY-FLD                       
                             FILE STATUS  IS WS-K110-STATUS.                    
                                                                                
           SELECT P13F310A   ASSIGN       TO P13F310A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F310                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F310.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   P13F310A                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F310A-REC      PIC X(150).                                       
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                      
       01  WS-K110-EOF             PIC X     VALUE SPACE.                       
       01  WS-CUR-TIME             PIC X(07) VALUE ZEROES.                      
       01  WS-F310-READ            PIC 9(08) VALUE ZEROES.                      
       01  WS-F310-EOF             PIC X     VALUE SPACE.                       
       01  WS-F310-WRITE           PIC 9(08) VALUE ZEROES.                      
       01  WS-K110-READ            PIC 9(08) VALUE ZEROES.                      
       01  WS-K110-NOT-FOUND       PIC 9(08) VALUE ZEROES.                      
       01  WS-K110-FOUND           PIC X(01) VALUE SPACES.                      
                                                                                
           COPY PRIOSCH.                                                        
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-YY                PIC 9(02).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-CC            PIC 9(02).                                   
           05 WS-SYS-YY            PIC 9(02).                                   
           05 WS-SYS-MM            PIC 9(02).                                   
           05 WS-SYS-DD            PIC 9(02).                                   
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD           PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-MM           PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-YY           PIC 9(02).                                   
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F310-REC THRU 200-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL WS-F310-EOF = 'Y'.                                     
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BM06K110                                                 
                OUTPUT P13F310A.                                                
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K110 OPEN ERROR '                                        
              DISPLAY 'FILE STATUS IS :' WS-K110-STATUS                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CUR-DATE FROM DATE.                                        
                                                                                
           MOVE WS-YY   TO WS-EDIT-YY  WS-SYS-YY                                
           MOVE WS-MM   TO WS-EDIT-MM  WS-SYS-MM                                
           MOVE WS-DD   TO WS-EDIT-DD  WS-SYS-DD.                               
                                                                                
           IF WS-SYS-YY > 93                                                    
              MOVE  19  TO WS-SYS-CC                                            
           ELSE                                                                 
              MOVE  20  TO WS-SYS-CC                                            
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-F310-REC.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F310 AT END                                                 
                MOVE 'Y' TO WS-F310-EOF                                         
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-F310-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================*                          
      *       DOCUMENTATION                          *                          
      *==============================================*                          
                                                                                
      *---------------*                                                         
       300-PROCESS-REC.                                                         
      *---------------*                                                         
                                                                                
           PERFORM 400-READ-BM06K110   THRU 400-EXIT.                           
                                                                                
           IF WS-K110-FOUND = 'Y'                                               
                                                                                
              IF K110-DTE-KEY-AVAIL NOT = SPACES AND ZEROES                     
                 WRITE P13F310A-REC FROM BP13F310-TRANS-REC                     
                 ADD   1  TO WS-F310-WRITE                                      
              END-IF                                                            
                                                                                
           ELSE                                                                 
              DISPLAY 'K110 RECORD NOT FOUND : ' K110-SCH-ACC-NO                
              ADD   1        TO WS-K110-NOT-FOUND                               
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F310-REC   THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       400-READ-BM06K110.                                                       
      *-----------------*                                                       
                                                                                
           MOVE 'Y'            TO WS-K110-FOUND.                                
           MOVE F310-SCH-ACC   TO K110-KEY-FLD.                                 
                                                                                
           READ  BM06K110.                                                      
                                                                                
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
                ADD  1         TO WS-K110-READ                                  
           WHEN OTHER                                                           
                MOVE 'N'       TO WS-K110-FOUND                                 
                DISPLAY 'K110-FILE STATUS IS  : ' WS-K110-STATUS                
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           ACCEPT WS-CUR-DATE FROM DATE.                                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C583 '.                                    
           DISPLAY '**********************'.                                    
           DISPLAY ' '.                                                         
           DISPLAY 'CURR-DATE  : ' FUNCTION CURRENT-DATE(1:8).                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F310   ' WS-F310-READ.              
           DISPLAY 'NO OF RECORDS FOUND IN  K110   ' WS-K110-READ.              
           DISPLAY 'NO OF RECORDS NOT FOUND K110   ' WS-K110-NOT-FOUND.         
           DISPLAY 'NO OF RECORDS WRITTEN TO F310A ' WS-F310-WRITE.             
                                                                                
           CLOSE BM06K110                                                       
                 P13F310A                                                       
                 BP13F310.                                                      
                                                                                
           IF WS-K110-STATUS EQUAL 00 OR 97                                     
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'K110 CLOSE ERROR '                                       
              DISPLAY 'FILE STATUS IS : ' WS-K110-STATUS                        
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
