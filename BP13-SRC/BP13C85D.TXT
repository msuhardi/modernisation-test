       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C85D.                                                  
      *DATE-WRITTEN. 07 NOV 2012.                                               
      *AUTHOR.       ALWYN BENNY.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE: MONTHLY REPORT OF GRANT RECOVERY CASES          *            
      *----------------------------------------------------------  *            
      *CHG REF   DATE       BY   DESCRIPTION                       *            
      *-------- --------   ---- -----------                        *            
      * BP134506 07112012  AB9  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
      *-------------------------------------------------------------            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F130  ASSIGN        TO BP13F130.                          
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
                                                                                
           SELECT BP13K893 ASSIGN         TO BP13K893                           
                           ORGANIZATION   IS INDEXED                            
                           ACCESS MODE    IS DYNAMIC                            
                           RECORD KEY     IS K893-KEY-FLD                       
                           FILE STATUS    IS BP13K893-STATUS.                   
                                                                                
           SELECT BP13KD05  ASSIGN        TO BP13KD05                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS KD05-KEY-FLD                       
                            FILE STATUS   IS BP13KD05-STATUS.                   
                                                                                
           SELECT BP13K022  ASSIGN       TO BP13K022                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K022-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K022-NUM-REGN               
                            WITH DUPLICATES                                     
                            FILE STATUS  IS BP13K022-STATUS.                    
                                                                                
           SELECT BP13F85D ASSIGN TO BP13F85D.                                  
           SELECT P13F85DA ASSIGN TO P13F85DA.                                  
           SELECT P13F85DB ASSIGN TO P13F85DB.                                  
           SELECT BP13L85D ASSIGN TO BP13L85D.                                  
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
                                                                                
       FD  BP13F130                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150  CHARACTERS.                                     
       COPY BP13F130.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD  BP13KD05                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100  CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  BP13F85D                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 500  CHARACTERS.                                     
       01  BP13F85D-REC                   PIC X(500).                           
                                                                                
       FD  P13F85DA                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  F85DA-REC.                                                           
           05 F85DA-SNO                    PIC X(06).                           
           05 FILLER                       PIC X(01).                           
           05 F85DA-REGNO                  PIC X(08).                           
           05 FILLER                       PIC X(01).                           
           05 F85DA-NRIC                   PIC X(09).                           
           05 FILLER                       PIC X(01).                           
           05 F85DA-AHG-AMT                PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85DA-AHG-AMT-REC            PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85DA-SHG-AMT                PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85DA-SHG-AMT-REC            PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85DA-GRANT-QUATUM           PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85DA-GRANT-RECOVER          PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85DA-GRANT-BAL              PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85DA-DTE-CANCEL             PIC X(10).                           
           05 FILLER                       PIC X(04).                           
           05 F85DA-OIC                    PIC X(05).                           
           05 FILLER                       PIC X(64).                           
                                                                                
       FD  P13F85DB                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01 F85DB-REC                        PIC X(200).                          
                                                                                
       FD  BP13L85D                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC                      PIC X(132).                           
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F130-READ             PIC 9(6)   VALUE ZERO.                      
       01  WS-K800-READ             PIC 9(6)   VALUE ZERO.                      
       01  WS-K800-NOTFD            PIC 9(6)   VALUE ZERO.                      
       01  CNT-F85DA-REC            PIC 9(6)   VALUE ZERO.                      
       01  CNT-F85DB-REC            PIC 9(6)   VALUE ZERO.                      
       01  CNT-F85D-REC             PIC 9(6)   VALUE ZERO.                      
       01  CNT-KIV-CASES            PIC 9(6)   VALUE ZERO.                      
       01  WS-F130-EOF              PIC X      VALUE 'N'.                       
       01  WS-K893-EOF              PIC X(01)  VALUE SPACES.                    
           88  K893-EOF                        VALUE 'Y'.                       
       01  WS-K800-REC-FND          PIC X      VALUE SPACE.                     
       01  WS-CSM-FOUND             PIC X      VALUE SPACE.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K800-STATUS      PIC 9(2)       VALUE ZEROES.                
           05  BP13K893-STATUS      PIC 9(2)       VALUE ZEROES.                
           05  BP13KD05-STATUS      PIC 9(2)       VALUE ZEROES.                
           05  BP13K022-STATUS      PIC 9(2)       VALUE ZEROES.                
                                                                                
       01 WS-AMT-VAR.                                                           
          05  WS-GRANT-QUANTUM       PIC 9(07)V99 VALUE ZEROES.                 
          05  WS-GRANT-RECOVERY      PIC 9(07)V99 VALUE ZEROES.                 
          05  WS-GRANT-BAL-AMT       PIC 9(07)V99 VALUE ZEROES.                 
                                                                                
       01  WS-NRIC-LIST.                                                        
           05  WS-I                   PIC 9(02) VALUE ZEROS.                    
           05  WS-NRIC OCCURS 4 TIMES PIC X(09) VALUE SPACES.                   
                                                                                
       01 WS-PREV-REGN              PIC X(08) VALUE SPACES.                     
       01 WS-PREB-CSM               PIC X(5)   VALUE SPACES.                    
       01 WS-POSB-CSM               PIC X(5)   VALUE SPACES.                    
       01 WS-CSM-NKNME              PIC X(5)   VALUE SPACES.                    
                                                                                
       01  F85DA-HDG.                                                           
           05 HDGDA-SNO                    PIC X(06) VALUE 'S/NO'.              
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-REGNO                  PIC X(08) VALUE ' REG_NO '.          
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-NRIC                   PIC X(09) VALUE '  NRIC   '.         
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-AHG-AMT                PIC X(12) VALUE                      
                                               '  AHG_GRANT '.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-AHG-AMT-REC            PIC X(12) VALUE                      
                                               '  AHG_RECRY '.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-SHG-AMT                PIC X(12) VALUE                      
                                               '  SHG_GRANT '.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-SHG-AMT-REC            PIC X(12) VALUE                      
                                               '  SHG_RECRY '.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-GRANT-QUATUM           PIC X(12) VALUE                      
                                               ' GRANT_QUATM'.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-GRANT-RECOVER          PIC X(12) VALUE                      
                                               ' GRANT_RECRY'.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-GRANT-BAL              PIC X(12) VALUE                      
                                               ' GRANT_BAL  '.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-DTE-CANCEL             PIC X(12) VALUE                      
                                               'CANCEL_DATE '.                  
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 HDGDA-DTE-CANCEL             PIC X(05) VALUE                      
                                               ' OIC '.                         
           05 FILLER                       PIC X(65) VALUE SPACES.              
                                                                                
       01  WS-LINE-CNT                    PIC 99         VALUE 66.              
       01  WS-PAGE-CNT                    PIC 9(5)       VALUE ZERO.            
                                                                                
       01  WS-DTE-CUR.                                                          
           05  WS-CC                      PIC 99.                               
           05  WS-YY                      PIC 99.                               
           05  WS-MM                      PIC 99.                               
           05  WS-DD                      PIC 99.                               
                                                                                
       01  L85D-PR-HEAD-01.                                                     
           05  L85D-RPT-HDG        PIC X(8)      VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'DATE: '.                
           05  L85D-DATE           PIC X(10).                                   
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE :'.                
           05  L85D-PAGE           PIC ZZ9 .                                    
                                                                                
       01  L85D-PR-HEAD-02.                                                     
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(40)     VALUE                          
               ' ERROR LIST OF CASES                   '.                       
           05  FILLER              PIC X(32)     VALUE                          
                  '                                '.                           
           05  FILLER              PIC X(22)     VALUE SPACES.                  
                                                                                
                                                                                
       01  L85D-PR-HEAD-03.                                                     
           05  FILLER              PIC X(27)                                    
                  VALUE ' S/NO REGN NO      NRIC    '.                          
           05  FILLER              PIC X(34)                                    
                  VALUE '          AHG GRANT   SHG GRANT   '.                   
           05  FILLER              PIC X(28)                                    
                  VALUE '   AHG RECVRY  SHG RECVRY '.                           
           05  FILLER              PIC X(40)                                    
                  VALUE 'CAN STAT ALLOC TAG  CAN DATE     OIC'.                 
           05  FILLER              PIC X(21)  VALUE SPACES.                     
                                                                                
       01  L85D-PR-DETAILS.                                                     
           05  L85D-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  L85D-REG-NO         PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85D-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(10)     VALUE SPACES .                 
           05  L85D-AHG-AMT        PIC $$$$$99.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85D-SHG-AMT        PIC $$$$$99.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85D-AHG-RECRY      PIC $$$$$99.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85D-SHG-RECRY      PIC $$$$$99.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85D-CAN-STAT       PIC X(01).                                   
           05  FILLER              PIC X(12)     VALUE SPACES .                 
           05  L85D-ALLO-TAG       PIC X(02).                                   
           05  FILLER              PIC X(04)     VALUE SPACES .                 
           05  L85D-CAN-DATE       PIC X(10).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85D-OIC            PIC X(05).                                   
           05  FILLER              PIC X(16)     VALUE SPACES .                 
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F130  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-F130   THRU 3000-EXIT                           
                   UNTIL WS-F130-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F130                                                 
                       BP13KD05                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K022                                                 
                OUTPUT P13F85DA                                                 
                       P13F85DB                                                 
                       BP13F85D                                                 
                       BP13L85D.                                                
                                                                                
           IF BP13K800-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS            
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF BP13K893-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K893 : ' BP13K893-STATUS            
               MOVE BP13K893-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF BP13KD05-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13KD05 : ' BP13KD05-STATUS            
               MOVE BP13KD05-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF BP13K022-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS            
               MOVE BP13K022-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F130.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F130 AT END                                                 
                MOVE 'Y' TO WS-F130-EOF                                         
                NOT AT END                                                      
                  ADD 1 TO WS-F130-READ                                         
                  IF WS-F130-READ = 1                                           
                     MOVE F130-NUM-REGN              TO  WS-PREV-REGN           
                  END-IF                                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-F130.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-READ-BP13K800      THRU 3100-EXIT.                      
           IF WS-K800-REC-FND = 'Y'                                             
              PERFORM 3400-READ-BP13K022   THRU 3400-EXIT                       
              PERFORM 3500-PROCESS         THRU 3500-EXIT UNTIL                 
                      F130-NUM-REGN NOT = WS-PREV-REGN OR                       
                      WS-F130-EOF       = 'Y'                                   
              MOVE    F130-NUM-REGN       TO   WS-PREV-REGN                     
           ELSE                                                                 
              PERFORM 2000-READ-BP13F130  THRU  2000-EXIT UNTIL                 
                      F130-NUM-REGN NOT = WS-PREV-REGN OR                       
                      WS-F130-EOF       = 'Y'                                   
              MOVE    F130-NUM-REGN       TO   WS-PREV-REGN                     
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
              MOVE 'N'                   TO WS-K800-REC-FND.                    
              MOVE F130-NUM-REGN         TO K800-NUM-REGN.                      
                                                                                
              READ BP13K800.                                                    
                                                                                
              EVALUATE BP13K800-STATUS                                          
              WHEN '00'                                                         
                    MOVE 'Y'             TO    WS-K800-REC-FND                  
                    ADD  1               TO    WS-K800-READ                     
              WHEN  '23'                                                        
                     MOVE SPACES         TO K893-KEY-FLD                        
                     MOVE K800-NUM-REGN  TO K893-NUM-REGN-HIST                  
                     PERFORM 3200-START-BP13K893  THRU 3200-EXIT                
                     IF NOT K893-EOF                                            
                       PERFORM 3300-READ-BP13K893 THRU 3300-EXIT                
                       IF K893-NUM-REGN-HIST = K800-NUM-REGN                    
                          MOVE BP13K893-MASTER(1:2000) TO                       
                                                  BP13K800-MASTER               
                          MOVE 'Y'            TO   WS-K800-REC-FND              
                          ADD  1              TO   WS-K800-READ                 
                       END-IF                                                   
                     END-IF                                                     
              WHEN  OTHER                                                       
                     MOVE 'N'     TO     WS-K800-REC-FND                        
                                         WS-K800-REC-FND                        
                     MOVE  BP13K800-STATUS TO RETURN-CODE                       
                     PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                  
              END-EVALUATE.                                                     
                                                                                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3200-START-BP13K893.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                               TO WS-K893-EOF.               
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           IF BP13K893-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K893-STATUS = 23 OR 10                                     
                 MOVE 'Y'                         TO WS-K893-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K893 - ERROR STARTING : ' BP13K893-STATUS         
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3300-READ-BP13K893.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K893-EOF                                      
                GO          TO 3300-EXIT                                        
           END-READ.                                                            
                                                                                
           IF BP13K893-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K893-STATUS = 23 OR 10                                     
                 MOVE 'Y'                         TO WS-K893-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K893 - ERROR READING NEXT : '                     
                         BP13K893-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3400-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE K800-NUM-REGN TO K022-NUM-REGN.                                 
           MOVE SPACES        TO WS-PREB-CSM WS-POSB-CSM WS-CSM-NKNME           
                                             WS-CSM-FOUND.                      
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF BP13K022-STATUS = 00 OR 02                                        
              MOVE 'Y'                     TO   WS-CSM-FOUND                    
              PERFORM  3450-GET-CSM        THRU 3450-EXIT                       
           ELSE                                                                 
           IF BP13K022-STATUS = 23                                              
              DISPLAY '> CSM FOR ' K022-NUM-REGN ' NOT FOUND.'                  
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K022, STATUS ' BP13K022-STATUS         
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3450-GET-CSM.                                                            
      *-------------------------------------------------------------            
                                                                                
            IF K022-NUM-OIC NOT = SPACES AND LOW-VALUES                         
               IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                      
                  MOVE K022-NUM-OIC        TO WS-PREB-CSM                       
               ELSE                                                             
                  MOVE K022-NUM-OIC-APPL   TO WS-PREB-CSM                       
               END-IF                                                           
            END-IF.                                                             
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE SPACES                          TO WS-POSB-CSM               
           ELSE                                                                 
              MOVE K022-NUM-OIC                    TO WS-POSB-CSM               
           END-IF.                                                              
           IF K800-NUM-PROV-TAG = 'Y'                                           
              MOVE SPACES         TO    WS-POSB-CSM                             
              MOVE WS-PREB-CSM    TO    WS-CSM-NKNME                            
           ELSE                                                                 
              IF WS-POSB-CSM NOT = SPACES                                       
                 MOVE WS-POSB-CSM  TO   WS-CSM-NKNME                            
              ELSE                                                              
                 MOVE WS-PREB-CSM  TO   WS-CSM-NKNME                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3450-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3500-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-COMPUTE-GRANT       THRU  4000-EXIT.                    
           PERFORM 2000-READ-BP13F130       THRU  2000-EXIT.                    
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-COMPUTE-GRANT.                                                      
      *-------------------------------------------------------------            
                                                                                
             MOVE K800-NUM-NRIC1                TO WS-NRIC(1).                  
             MOVE K800-NUM-NRIC2                TO WS-NRIC(2).                  
             MOVE K800-NUM-NRIC3                TO WS-NRIC(3).                  
             MOVE K800-NUM-NRIC4                TO WS-NRIC(4).                  
                                                                                
             PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                    
                IF WS-NRIC(WS-I) NOT = SPACES AND LOW-VALUES                    
                   MOVE F130-NUM-REGN           TO KD05-NUM-REGN                
                   MOVE WS-NRIC(WS-I)           TO KD05-NUM-NRIC                
                   MOVE ZEROS                   TO WS-GRANT-QUANTUM             
                                                   WS-GRANT-RECOVERY            
                                                   WS-GRANT-BAL-AMT             
                   PERFORM 4100-READ-BP13KD05  THRU 4100-EXIT                   
                   COMPUTE WS-GRANT-QUANTUM    = KD05-AMT-AHG-GRANT +           
                                                 KD05-AMT-SHG-GRANT             
                   COMPUTE WS-GRANT-RECOVERY   =                                
                                        KD05-AMT-AHG-GRANT-RECOVER +            
                                        KD05-AMT-SHG-GRANT-RECOVER              
                   COMPUTE WS-GRANT-BAL-AMT  =  WS-GRANT-QUANTUM -              
                                                WS-GRANT-RECOVERY               
                   IF (K800-NUM-STATUS = 'C') AND                               
                      (K800-NUM-ALLOC-TAG = 'RI' OR 'RG')                       
                      PERFORM 4050-WRITE-OUTPUT   THRU  4050-EXIT               
                   ELSE                                                         
                     IF WS-GRANT-BAL-AMT NOT = ZEROS AND                        
                        (K800-NUM-STATUS = 'C')                                 
                        PERFORM 800-ERROR-LIST           THRU  800-EXIT         
                     END-IF                                                     
                   END-IF                                                       
               END-IF                                                           
            END-PERFORM.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4050-WRITE-OUTPUT.                                                       
      *-------------------------------------------------------------            
                                                                                
            IF WS-GRANT-BAL-AMT NOT = ZEROES                                    
               EVALUATE K800-NUM-ALLOC-TAG                                      
                 WHEN 'RI'                                                      
                   PERFORM 4200-WRITE-REPORT-F85DA THRU 4200-EXIT               
                 WHEN 'RG'                                                      
                   PERFORM 4300-WRITE-REPORT-F85DB THRU 4300-EXIT               
               END-EVALUATE                                                     
            ELSE                                                                
               IF (K800-NUM-ALLOC-TAG = 'RI' OR 'RG') AND                       
                  (KD05-AMT-AHG-GRANT > 0   OR                                  
                   KD05-AMT-SHG-GRANT > 0 )                                     
                   PERFORM 4400-WRITE-BP13F85D     THRU 4400-EXIT               
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4100-READ-BP13KD05.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD05.                                                       
                                                                                
           EVALUATE BP13KD05-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE ZEROES       TO KD05-AMT-AHG-GRANT                      
                                        KD05-AMT-AHG-GRANT-RECOVER              
                                        KD05-AMT-SHG-GRANT                      
                                        KD05-AMT-SHG-GRANT-RECOVER              
              WHEN OTHER                                                        
                 DISPLAY '__ERROR READING BP13KD05 , KEY : '                    
                          KD05-KEY-FLD                                          
                         ' , STATUS : ' BP13KD05-STATUS                         
                 MOVE BP13KD05-STATUS           TO RETURN-CODE                  
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           IF KD05-AMT-AHG-GRANT         NOT NUMERIC                            
              MOVE ZEROES                 TO KD05-AMT-AHG-GRANT.                
           IF KD05-AMT-AHG-GRANT-RECOVER NOT NUMERIC                            
              MOVE ZEROES                 TO KD05-AMT-AHG-GRANT-RECOVER.        
           IF KD05-AMT-SHG-GRANT         NOT NUMERIC                            
              MOVE ZEROES                 TO KD05-AMT-SHG-GRANT.                
           IF KD05-AMT-SHG-GRANT-RECOVER NOT NUMERIC                            
              MOVE ZEROES                 TO KD05-AMT-SHG-GRANT-RECOVER.        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4200-WRITE-REPORT-F85DA.                                                 
      *-------------------------------------------------------------            
                                                                                
           ADD 1                      TO  CNT-F85DA-REC.                        
           IF CNT-F85DA-REC = 1                                                 
              WRITE F85DA-REC         FROM F85DA-HDG                            
           END-IF.                                                              
           MOVE SPACES                TO  F85DA-REC.                            
           INITIALIZE                     F85DA-REC.                            
           MOVE  CNT-F85DA-REC        TO  F85DA-SNO.                            
           PERFORM 4250-WRITE-DETAIL  THRU 4250-EXIT.                           
                                                                                
           WRITE F85DA-REC.                                                     
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4250-WRITE-DETAIL.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE  KD05-NUM-REGN        TO F85DA-REGNO.                           
           MOVE  KD05-NUM-NRIC        TO F85DA-NRIC.                            
           MOVE  KD05-AMT-AHG-GRANT   TO F85DA-AHG-AMT.                         
           MOVE  KD05-AMT-SHG-GRANT   TO F85DA-SHG-AMT.                         
           MOVE  KD05-AMT-AHG-GRANT-RECOVER                                     
                                      TO F85DA-AHG-AMT-REC.                     
           MOVE  KD05-AMT-SHG-GRANT-RECOVER                                     
                                      TO F85DA-SHG-AMT-REC.                     
           MOVE  WS-GRANT-QUANTUM     TO F85DA-GRANT-QUATUM.                    
           MOVE  WS-GRANT-RECOVERY    TO F85DA-GRANT-RECOVER.                   
           MOVE  WS-GRANT-BAL-AMT     TO F85DA-GRANT-BAL.                       
           MOVE  WS-CSM-NKNME         TO F85DA-OIC.                             
           IF K800-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROS             
              STRING K800-DTE-CANCEL (7:2) '-'                                  
                  K800-DTE-CANCEL (5:2) '-'                                     
                  K800-DTE-CANCEL (1:4) DELIMITED BY SIZE INTO                  
                  F85DA-DTE-CANCEL                                              
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES              TO F85DA-DTE-CANCEL                      
           END-IF.                                                              
                                                                                
       4250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4300-WRITE-REPORT-F85DB.                                                 
      *-------------------------------------------------------------            
                                                                                
           ADD 1                      TO  CNT-F85DB-REC.                        
           IF CNT-F85DB-REC = 1                                                 
              WRITE F85DB-REC         FROM F85DA-HDG                            
           END-IF.                                                              
           MOVE SPACES                TO  F85DB-REC F85DA-REC.                  
           INITIALIZE                     F85DB-REC F85DA-REC.                  
           MOVE  CNT-F85DB-REC        TO  F85DA-SNO.                            
           PERFORM 4250-WRITE-DETAIL  THRU 4250-EXIT.                           
                                                                                
           WRITE F85DB-REC            FROM F85DA-REC.                           
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4400-WRITE-BP13F85D.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO  BP13F85D-REC.                         
           INITIALIZE                     BP13F85D-REC.                         
           WRITE    BP13F85D-REC    FROM BP13KD05-REC.                          
           ADD       1                TO CNT-F85D-REC.                          
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       800-ERROR-LIST.                                                          
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CNT  > 46                                                 
               MOVE 1 TO WS-LINE-CNT                                            
               PERFORM 900-ERROR-HEADING THRU 900-EXIT                          
           ELSE                                                                 
               ADD 1 TO WS-LINE-CNT.                                            
                                                                                
           ADD 1 TO CNT-KIV-CASES.                                              
           MOVE  CNT-KIV-CASES         TO L85D-SNO.                             
           MOVE  WS-PREV-REGN          TO L85D-REG-NO.                          
           MOVE  KD05-NUM-NRIC         TO L85D-NRIC.                            
           MOVE  KD05-AMT-AHG-GRANT    TO L85D-AHG-AMT.                         
           MOVE  KD05-AMT-SHG-GRANT    TO L85D-SHG-AMT.                         
           MOVE  KD05-AMT-AHG-GRANT-RECOVER                                     
                                      TO L85D-AHG-RECRY.                        
           MOVE  KD05-AMT-SHG-GRANT-RECOVER                                     
                                       TO L85D-SHG-RECRY.                       
           MOVE  K800-NUM-STATUS       TO L85D-CAN-STAT.                        
           MOVE  K800-NUM-ALLOC-TAG    TO L85D-ALLO-TAG.                        
           MOVE  WS-CSM-NKNME          TO L85D-OIC.                             
           IF K800-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROS             
              STRING K800-DTE-CANCEL (7:2) '-'                                  
                  K800-DTE-CANCEL (5:2) '-'                                     
                  K800-DTE-CANCEL (1:4) DELIMITED BY SIZE INTO                  
                  L85D-CAN-DATE                                                 
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES              TO L85D-CAN-DATE                         
           END-IF.                                                              
           WRITE PRINT-REC  FROM L85D-PR-DETAILS.                               
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       900-ERROR-HEADING.                                                       
      *-------------------------------------------------------------            
                                                                                
            ADD 1         TO WS-PAGE-CNT.                                       
            MOVE WS-PAGE-CNT  TO L85D-PAGE.                                     
            MOVE SPACES   TO PRINT-REC                                          
            WRITE PRINT-REC  AFTER PAGE.                                        
            MOVE 'BP13L85D'              TO L85D-RPT-HDG.                       
            WRITE PRINT-REC  FROM L85D-PR-HEAD-01 AFTER 2.                      
            WRITE PRINT-REC  FROM L85D-PR-HEAD-02  AFTER 3.                     
            WRITE PRINT-REC  FROM L85D-PR-HEAD-03  AFTER 3.                     
            MOVE ALL '-'  TO PRINT-REC.                                         
            WRITE PRINT-REC.                                                    
            MOVE SPACES   TO PRINT-REC.                                         
            WRITE PRINT-REC  AFTER 1.                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
                                                                                
           DISPLAY '- BP13C85D CONTROL TOTAL --------'.                         
           DISPLAY ' NO OF REC READ FROM BP13F130  = '                          
                     WS-F130-READ.                                              
           DISPLAY ' NO OF CASES WRITTEN TO P13F85DA   = '                      
                     CNT-F85DA-REC.                                             
           DISPLAY ' NO OF CASES WRITTEN TO P13F85DB   = '                      
                     CNT-F85DB-REC.                                             
           DISPLAY ' NO OF CASES WRITTEN TO BP13F85D   = '                      
                     CNT-F85D-REC.                                              
           DISPLAY ' NO OF CASES IN THE ERROR LIST     = '                      
                     CNT-KIV-CASES.                                             
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F130                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K022                                                 
                       BP13KD05                                                 
                       BP13F85D                                                 
                       P13F85DA                                                 
                       P13F85DB                                                 
                       BP13L85D.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 - ERROR CLOSING : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS              TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF BP13K893-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K893 - ERROR CLOSING : ' BP13K893-STATUS             
              MOVE BP13K893-STATUS             TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF BP13KD05-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13KD05 - ERROR CLOSING : ' BP13KD05-STATUS             
              MOVE BP13KD05-STATUS             TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 - ERROR CLOSING : ' BP13K022-STATUS             
              MOVE BP13K022-STATUS             TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
