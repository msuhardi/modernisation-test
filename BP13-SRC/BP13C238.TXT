       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C238.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  OCT 07 2009.                                              
      * ============================================================            
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  - TO PROVIDE DAILY INTERFACE FILE FOR MAMS ON NPL            
      *              CASES USED FOR PASSING ACTIVE NPL CASES                    
      *                                                                         
      * INPUT FILES:  BP13F800                                                  
      *               BM06K110                                                  
      * OUTPUT     :  BC03F818 - INTERFACE FILE FOR MAMS                        
      * ------------------------------------------------------------            
      * REF NO    DATE  BY  DESCRIPTIONS                                        
      * -------- ------ --- ------------                                        
      * BP133725 071009 ESA1 NEW PGM                                            
      * BP133997 051010 ESA1 ADD READING OF BM06K110                            
      * ============================================================            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
                                                                                
           SELECT BC03F818 ASSIGN        TO BC03F818.                           
                                                                                
           SELECT BM06K110 ASSIGN        TO BM06K110                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K110-KEY-FLD                        
                           FILE STATUS   IS BM06K110-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BP13F800.                                                           
                                                                                
       FD   BC03F818                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BC03F818.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 WS-FILE-STATUS.                                                       
          05 BM06K110-STATUS            PIC 9(02) VALUE 0.                      
       01 WS-F800-EOF                   PIC X     VALUE 'N'.                    
       01 WS-CNT-F800-READ              PIC 9(07) VALUE 0.                      
       01 WS-CNT-F818-WRITE             PIC 9(07) VALUE 0.                      
       01 WS-SELLING-PRICE              PIC 9(08) VALUE 0.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                         
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9999-WRAP-UP          THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F800                                                
                        BM06K110                                                
                OUTPUT  BC03F818.                                               
                                                                                
            IF BM06K110-STATUS  NOT =  00 AND 97                                
               DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS            
               MOVE     BM06K110-STATUS      TO RETURN-CODE                     
               PERFORM  9999-WRAP-UP       THRU 9999-EXIT                       
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
            READ BP13F800 AT END                                                
                 MOVE 'Y'          TO WS-F800-EOF                               
                 GO TO 2000-EXIT                                                
            END-READ.                                                           
                                                                                
            ADD  1                 TO WS-CNT-F800-READ.                         
                                                                                
            IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES                          
               MOVE ZEROES         TO WS-SELLING-PRICE                          
            ELSE                                                                
               PERFORM 2500-READ-BM06K110 THRU  2500-EXIT                       
            END-IF.                                                             
                                                                                
            PERFORM 3000-WRITE-BC03F818   THRU  3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2500-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F800-NUM-SCH-ACC   TO K110-KEY-FLD.                             
                                                                                
           READ BM06K110.                                                       
           EVALUATE BM06K110-STATUS                                             
           WHEN 00                                                              
                MOVE K110-CURR-SELLING-PRICE TO WS-SELLING-PRICE                
           WHEN 23                                                              
                MOVE ZEROES        TO WS-SELLING-PRICE                          
                DISPLAY 'REC NOT FOUND IN BM06K110'                             
                      ', K110-SCH-ACC-NO : ' K110-SCH-ACC-NO                    
           WHEN OTHER                                                           
                DISPLAY 'BM06K110 READ ERROR,STATUS : ' BM06K110-STATUS         
                      ', K110 KEY FLD : ' K110-KEY-FLD                          
                MOVE BM06K110-STATUS       TO RETURN-CODE                       
                PERFORM 9999-WRAP-UP      THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-WRITE-BC03F818.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                 TO BC03F818-REC.                         
           MOVE F800-NUM-REGN          TO F818-NUM-REGN.                        
           MOVE F800-NUM-SCH-ACC       TO F818-NUM-HDB-REF.                     
           MOVE F800-DTE-BALLOT        TO F818-DTE-BALLOT.                      
           MOVE F800-NUM-NT-FT-QUEUE   TO F818-NUM-QUEUE.                       
           MOVE F800-DTE-BK-APPT       TO F818-DTE-SCHD-BOOKING.                
           MOVE F800-TME-BK-APPT       TO F818-TME-SCHD-BOOKING.                
           MOVE F800-DTE-ACCEPTANCE    TO F818-DTE-ACCEPTANCE.                  
           MOVE F800-DTE-AO-APPT       TO F818-DTE-TP-SCHD.                     
           MOVE WS-SELLING-PRICE       TO F818-AMT-SELL-PRICE.                  
           WRITE BC03F818-REC.                                                  
           ADD 1  TO WS-CNT-F818-WRITE.                                         
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9999-WRAP-UP.                                                            
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C238 CONTROL TOTAL -------------'.                    
           DISPLAY ' TOTAL BP13F800 REC READ     : ' WS-CNT-F800-READ.          
           DISPLAY ' TOTAL BC03F818 RECS WRITE   : ' WS-CNT-F818-WRITE.         
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 BC03F818.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
