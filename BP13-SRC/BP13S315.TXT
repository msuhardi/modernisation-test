 /*===========================================================*/                
 /*       SAS PROGRAM TO PRINT SUMMARY AGEING REPORT ON       */                
 /*                   COMMITMENT DEPOSIT                      */                
 /* CHG-NO   BY   ON    - DESCRIPTION                         */                
 /* ---------------------------------                         */                
 /* BP130084 LSB 960228 - NEW PGM                             */                
 /* BP130130 EVM 960716 - ADD HEADING TITLE                   */                
 /* BP130130 LJL 960826 - TO REMOVE THE '<' AND '>' SIGNS.    */                
 /* BP130275 LJL 970430 - TO CHANGE HANDOVER DATE TO KEY      */                
 /*                       AVAILABLE DATE.                     */                
 /* BP130447 LSB 980104 - NEW LAYOUT BP13F142                 */                
 /*===========================================================*/                
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
    PROC FORMAT;                                                                
      PICTURE AMTEDT LOW-<0='000,000,000,000.00DR'                              
                         (FILL=' ' PREFIX='$' MULT=100.00000000001)             
                     0-HIGH='000,000,000,000.00CR'                              
                         (FILL=' ' PREFIX='$' MULT=100.00000000001);            
                                                                                
  DATA BP13F142 ;                                                               
     INFILE BP13F142;                                                           
     INPUT  @01  REGNO    $CHAR8.                                               
            @223 AMTCDMT  ZD9.2                                                 
            @249 MONTH    3.                                                    
            @252 HANDOVER $CHAR1.;                                              
                                                                                
     FORMAT AMTCDMT AMTEDT.;                                                    
                                                                                
     PROC SORT; BY MONTH;                                                       
                                                                                
     DATA _NULLS_;                                                              
       SET BP13F142 END=EOF;                                                    
       BY MONTH;                                                                
                                                                                
       FILE BP13L315 NOTITLES HEADER=HH;                                        
                                                                                
       IF FIRST.MONTH THEN DO;                                                  
          TAG_Y=0; TAG_NY=0;                                                    
          T_AMTY=0;  T_AMTNY=0; T_TAG=0; T_AMT=0;                               
          END;                                                                  
                                                                                
       IF HANDOVER = 'Y' THEN DO;                                               
          TAG_Y+1;  T_AMTY+AMTCDMT;  GR_TAGY+1;  GR_AMTY+AMTCDMT;               
          END;                                                                  
       ELSE DO;                                                                 
          TAG_NY+1; T_AMTNY+AMTCDMT; GR_TAGNY+1; GR_AMTNY+AMTCDMT;              
          END;                                                                  
                                                                                
       IF LAST.MONTH THEN                                                       
          DO;                                                                   
              MAX_MTH=MONTH+1;                                                  
              T_TAG=TAG_Y+TAG_NY;                                               
              T_AMT=T_AMTY+T_AMTNY;                                             
              PUT /@26  ' '                                                     
                  @28  MONTH  3.                                                
                  @33  '  ';                                                    
              PUT @20  'FLATS KEY AVAILABLE'                                    
                  @58  TAG_Y   6.                                               
                  @94  T_AMTY AMTEDT.;                                          
              PUT @20  'FLATS KEY NOT AVAILABLE'                                
                  @58  TAG_NY   6.                                              
                  @94  T_AMTNY AMTEDT.;                                         
              PUT @20  'TOTAL :              '                                  
                  @58  T_TAG  6.                                                
                  @94  T_AMT AMTEDT.;                                           
          END;                                                                  
       IF EOF THEN DO;                                                          
          GR_TAG=GR_TAGY+GR_TAGNY;                                              
          GR_AMT=GR_AMTY+GR_AMTNY;                                              
          PUT @54  '================='  @89  '=====================';           
          PUT @20  'FLATS KEY AVAILABLE'                                        
              @56   GR_TAGY 8.   @94  GR_AMTY AMTEDT.;                          
          PUT @20  'FLATS KEY NOT AVAILABLE'                                    
              @56   GR_TAGNY 8.  @94  GR_AMTNY AMTEDT.;                         
          PUT @20  'GRAND TOTAL :        '                                      
              @56   GR_TAG 8.    @94  GR_AMT AMTEDT.;                           
          END;                                                                  
        RETURN;                                                                 
     HH:                                                                        
       PAGE+1;                                                                  
       CURRENT=DATE();                                                          
       PUT @1   'BP13L315      HDB3'                                            
           @41  'S Y S T E M   O F   C O M M I T M E N T'                       
           @90  'DATE : '      @98  CURRENT DDMMYY10.                           
           @120 'PAGE : '      @127 PAGE 3.;                                    
       PUT @37  'SUMMARY  AGEING  REPORT  ON  COMMITMENT  DEPOSIT';             
       PUT @48  'FROM SALES AGREEMENT DATE'                                     
           @90  'USER : FINANCE DEPT'/;                                         
       PUT @25  'BALANCE (MONTH)'                                               
           @55  'NUMBER OF CASES'                                               
           @92  '  AMOUNT ($)  ';                                               
       PUT @24  '-----------------'                                             
           @54  '-----------------'                                             
           @89  '---------------------';                                        
                                                                                
       RETURN;                                                                  
