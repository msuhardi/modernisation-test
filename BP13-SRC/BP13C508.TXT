       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C508.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  22/03/2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *      PRINT REPORT ON DATE OF APPOINTMENT FOR BO            *            
      *                                                            *            
      *  INPUT FILES: 1. BP13L507                                  *            
      *  OUTPUT     : 1. BP27L508                                  *            
      *                                                            *            
      * BP133026 BA2  22032007 - NEW PROGRAM                       *            
      * BP133026 SSS2 29032007 - FLAT TYPE 3                       *            
      * BP133129 BA3  16082007 - REMOVE TITLE FOR REFERENCE ON     *            
      *                          BOOKING APPOINTMENT.              *            
      * BP133364 RB12 26062008 - CHANGE 'MONTHLY' TO 'QUARTERLY'   *            
      * BP133430 LMS1 10092008 - NEW SELECTION TYPE WIH            *            
      * BP133071 RB12 23102008 - CATER FOR SA FOR 3R               *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13L507 ASSIGN        TO BP13L507.                           
                                                                                
           SELECT BP13L508 ASSIGN        TO BP13L508.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13L507                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       01  BP13L507-REC.                                                        
           05 L507-NUM-SELTYPE           PIC X(03).                     00161105
           05 L507-DTE-BALLOT            PIC X(06).                     00161105
           05 L507-NME-OCCP              PIC X(66).                             
           05 L507-NUM-REGN              PIC X(08).                             
           05 L507-NUM-NRIC              PIC X(09).                             
           05 L507-NUM-NT-FT-QUEUE       PIC X(05).                             
           05 L507-DTE-BK-APPT           PIC X(08).                             
           05 L507-TME-BK-APPT           PIC X(04).                             
           05 L507-TME-BK-APPT-NUM  REDEFINES L507-TME-BK-APPT.                 
              10 L507-TME-BK-APPT-HH     PIC 9(02).                             
              10 L507-TME-BK-APPT-MM     PIC 9(02).                             
                                                                                
           05 L507-NUM-FLAT-TYPE         PIC X(02).                             
           05 FILLER                     PIC X(39).                             
                                                                                
                                                                                
       FD  BP13L508                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  L508-PRTREC                   PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-L507-EOF               PIC X         VALUE 'N'.               
                                                                                
       01  WS-CNT-VAR.                                                          
           05  WS-L507-READ              PIC 9(5) COMP VALUE ZEROES.            
           05  WS-L508-PRINT             PIC 9(5) COMP VALUE ZEROES.            
           05  WS-PAGE-CNT               PIC 9(5) COMP VALUE ZEROES.            
           05  WS-LINE-CNT               PIC 9(2)      VALUE 56.                
                                                                                
       01  WS-DATE                       PIC X(8)      VALUE SPACES.            
       01  WS-PRT-DATE                   PIC X(8)      VALUE SPACES.            
                                                                                
       01  WS-PREV.                                                             
           05  WS-PREV-BALL-DTE          PIC X(6)      VALUE SPACES.            
           05  WS-PREV-FT-NT             PIC X(3)      VALUE SPACES.            
                                                                                
       77  WS-TME-BK-APPT-HH             PIC 99        VALUE ZEROES.            
                                                                                
      ***************************************************************           
      *    WS-TABLE-MONTH                                           *           
      ***************************************************************           
       01  WS-MTH.                                                              
           02  FILLER        PIC X(5)   VALUE '01JAN'.                          
           02  FILLER        PIC X(5)   VALUE '02FEB'.                          
           02  FILLER        PIC X(5)   VALUE '03MAR'.                          
           02  FILLER        PIC X(5)   VALUE '04APR'.                          
           02  FILLER        PIC X(5)   VALUE '05MAY'.                          
           02  FILLER        PIC X(5)   VALUE '06JUN'.                          
           02  FILLER        PIC X(5)   VALUE '07JUL'.                          
           02  FILLER        PIC X(5)   VALUE '08AUG'.                          
           02  FILLER        PIC X(5)   VALUE '09SEP'.                          
           02  FILLER        PIC X(5)   VALUE '10OCT'.                          
           02  FILLER        PIC X(5)   VALUE '11NOV'.                          
           02  FILLER        PIC X(5)   VALUE '12DEC'.                          
                                                                                
       01  WS-MTH-TAB REDEFINES WS-MTH.                                         
           05  WS-MTH-DETAIL OCCURS 12  TIMES INDEXED BY WS-IND.                
               10  WS-MTH-CODE      PIC X(2).                                   
               10  WS-MTH-DEF       PIC X(3).                                   
                                                                                
      ***************************************************************           
      *    WS-REPORT VARIBLE                                        *           
      ***************************************************************           
       01  L508-HEADER1.                                                        
           05  FILLER              PIC X(08)  VALUE 'BP13L508'.                 
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'HDBCAT3'.                  
           05  FILLER              PIC X(17)  VALUE SPACES.                     
           05  L508-RPT-NAME       PIC X(59)  VALUE SPACES.                     
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'DATE : '.                  
           05  L508-DATE           PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAGE : '.                  
           05  L508-PAGE           PIC Z(3)9  VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE SPACES.                     
                                                                                
       01  L508-HEADER2.                                                        
           05  FILLER              PIC X(38)  VALUE SPACES.                     
           05  L508-HDR2-TXT       PIC X(50)  VALUE SPACES.                     
           05  FILLER              PIC X(44)  VALUE SPACES.                     
                                                                                
       01  L508-HEADER3.                                                        
           05  FILLER              PIC X(100)  VALUE SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'SELECTION'.                
           05  FILLER              PIC X(08)  VALUE SPACES.                     
           05  L508-H3-FT          PIC X(09)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE SPACES.                     
                                                                                
       01  L508-HEADER4.                                                        
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE 'NAME'.                     
           05  FILLER              PIC X(57)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'NRIC-NO   '.               
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'REGN-NO '.                 
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'QUEUE-NO'.                 
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  FILLER              PIC X(11)  VALUE 'DATE & TIME'.              
           05  FILLER              PIC X(07)  VALUE SPACES.                     
           05  L508-H4-FT          PIC X(09)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE SPACES.                     
                                                                                
       01  L508-DETAIL.                                                         
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  L508-NME-APPL       PIC X(60)    VALUE SPACES.                   
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  L508-NRIC-APPL      PIC X(09)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  L508-REGN           PIC X(08)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE SPACES.                   
           05  L508-QUEUE-NO       PIC X(05)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  L508-APPOINTMENT    PIC X(20)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE SPACES.                   
           05  L508-FLAT-TYPE      PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE SPACES.                   
                                                                                
       01  L508-DASH-LINE.                                                      
           05  FILLER              PIC X(2)   VALUE SPACES.                     
           05  FILLER              PIC X(124) VALUE ALL '-'.                    
           05  FILLER              PIC X(6)   VALUE SPACES.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 0100-OPEN-ROUTINE            THRU 0100-EXIT.                 
           PERFORM 0200-READ-BP13L507           THRU 0200-EXIT.                 
           MOVE   L507-NUM-SELTYPE  TO  WS-PREV-FT-NT.                  00161105
           MOVE   L507-DTE-BALLOT   TO  WS-PREV-BALL-DTE.               00161105
           PERFORM 0300-MAIN-PROCESS            THRU 0300-EXIT                  
                   UNTIL WS-L507-EOF  = 'Y'.                                    
           PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0100-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13L507                                                 
                OUTPUT BP13L508.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
                                                                                
           STRING WS-DATE(7:2) '/'                                              
                  WS-DATE(5:2) '/'                                              
                  WS-DATE(1:4)                                                  
              DELIMITED BY SIZE             INTO L508-DATE.                     
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0200-READ-BP13L507.                                                      
      *-------------------------------------------------------------            
           READ BP13L507                                                        
                AT END MOVE 'Y'         TO WS-L507-EOF                          
                GO TO 0200-EXIT.                                                
           ADD  +1                      TO WS-L507-READ.                        
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0300-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           IF  L507-NUM-SELTYPE  NOT =  WS-PREV-FT-NT     OR            00161105
               L507-DTE-BALLOT   NOT =  WS-PREV-BALL-DTE  OR            00161105
               WS-LINE-CNT > 55                                                 
               PERFORM 0600-WRITE-HEAD       THRU 0600-EXIT                     
           END-IF.                                                              
           MOVE L507-NME-OCCP             TO  L508-NME-APPL.                    
           MOVE L507-NUM-REGN             TO  L508-REGN.                        
           IF  L507-NUM-NRIC NOT = SPACES AND LOW-VALUES                        
               STRING L507-NUM-NRIC(1:1) 'XXX' L507-NUM-NRIC(5:5)               
                      DELIMITED BY SIZE INTO L508-NRIC-APPL                     
           END-IF.                                                              
           MOVE L507-NUM-NT-FT-QUEUE      TO  L508-QUEUE-NO.                    
                                                                                
           IF L507-DTE-BK-APPT(5:2) NOT = SPACES AND LOW-VALUES                 
              SET WS-IND                TO 1                                    
              SEARCH WS-MTH-DETAIL                                              
                  AT END                                                        
                     DISPLAY 'MONTH NOT FND - ' L507-DTE-BK-APPT(5:2)           
                             ', REGN IS ' L507-NUM-REGN                         
                  WHEN WS-MTH-CODE (WS-IND) = L507-DTE-BK-APPT(5:2)             
                    STRING L507-DTE-BK-APPT(7:2) ' '                            
                           WS-MTH-DEF (WS-IND) ' '                              
                           L507-DTE-BK-APPT(3:2)                                
                           DELIMITED BY SIZE INTO L508-APPOINTMENT              
           ELSE                                                                 
               MOVE '         '  TO L508-APPOINTMENT                            
           END-IF.                                                              
                                                                                
           IF L507-TME-BK-APPT NOT = SPACES AND LOW-VALUES                      
              IF L507-TME-BK-APPT(1:2) = '12'                                   
                 STRING L508-APPOINTMENT(1:9) ' @ '                             
                        L507-TME-BK-APPT(1:2) ':'                               
                        L507-TME-BK-APPT(3:2) ' PM'                             
                        DELIMITED BY SIZE INTO L508-APPOINTMENT                 
              ELSE                                                              
                 IF L507-TME-BK-APPT(1:2) > '12'                                
                    COMPUTE WS-TME-BK-APPT-HH =                                 
                                         L507-TME-BK-APPT-HH - 12               
                    STRING L508-APPOINTMENT(1:9) ' @ '                          
                           WS-TME-BK-APPT-HH ':'                                
                           L507-TME-BK-APPT(3:2) ' PM'                          
                           DELIMITED BY SIZE INTO L508-APPOINTMENT              
                 ELSE                                                           
                    STRING L508-APPOINTMENT(1:9) ' @ '                          
                           L507-TME-BK-APPT(1:2) ':'                            
                           L507-TME-BK-APPT(3:2) ' AM'                          
                           DELIMITED BY SIZE INTO L508-APPOINTMENT              
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF L507-NUM-SELTYPE = '3R '                                          
              IF L507-NUM-FLAT-TYPE = '1A'                                      
                 MOVE 'SA' TO L508-FLAT-TYPE                                    
              ELSE                                                              
                 MOVE L507-NUM-FLAT-TYPE TO L508-FLAT-TYPE                      
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES                TO L508-FLAT-TYPE                      
           END-IF.                                                              
           WRITE L508-PRTREC FROM L508-DETAIL.                                  
                                                                                
           ADD  +1                       TO WS-L508-PRINT.                      
           ADD  +1                       TO WS-LINE-CNT.                        
           MOVE L507-NUM-SELTYPE         TO WS-PREV-FT-NT.                      
           MOVE L507-DTE-BALLOT          TO WS-PREV-BALL-DTE.                   
           PERFORM 0200-READ-BP13L507    THRU 0200-EXIT.                        
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0600-WRITE-HEAD.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES          TO L508-RPT-NAME.                               
           EVALUATE L507-NUM-SELTYPE                                            
             WHEN 'WIA'                                                         
              MOVE 'BI-MONTHLY SALE OF 4-ROOM AND BIGGER FLATS (SECTOR A        
      -       ')' TO L508-RPT-NAME                                              
             WHEN 'WIB'                                                         
              STRING 'BI-MONTHLY SALE OF 4-ROOM AND BIGGER FLATS'               
                     '(SECTOR B)' DELIMITED BY SIZE INTO L508-RPT-NAME          
             WHEN 'WIC'                                                         
              STRING 'BI-MONTHLY SALE OF 4-ROOM AND BIGGER FLATS'               
                     '(SECTOR C)' DELIMITED BY SIZE INTO L508-RPT-NAME          
             WHEN '3R '                                                         
              MOVE '     QUARTERLY SALE OF 3-ROOM AND SMALLER FLATS'            
                                  TO L508-RPT-NAME                              
              MOVE 'INDICATED'    TO L508-H3-FT                                 
              MOVE 'FLAT TYPE'    TO L508-H4-FT                                 
             WHEN 'WIH'                                                         
              STRING 'HALF-YEARLY SALE OF 3-ROOM PREMIUM, 4-ROOM'               
                     ' AND BIGGER FLATS'                                        
              DELIMITED BY SIZE INTO L508-RPT-NAME                              
           END-EVALUATE.                                                        
                                                                                
           IF L507-DTE-BALLOT(5:2) NOT = LOW-VALUES AND SPACES                  
              SET WS-IND                TO 1                                    
              SEARCH WS-MTH-DETAIL                                              
                  AT END                                                        
                     DISPLAY 'MONTH NOT FOUND - ' L507-DTE-BALLOT(5:2)          
                  WHEN WS-MTH-CODE (WS-IND) = L507-DTE-BALLOT(5:2)              
                     STRING WS-MTH-DEF (WS-IND) ' '                             
                            L507-DTE-BALLOT(1:4) DELIMITED BY SIZE INTO         
                            WS-PRT-DATE                                         
           END-IF.                                                              
                                                                                
           IF L507-NUM-SELTYPE = '3R ' AND L507-NUM-FLAT-TYPE = '1A'            
              STRING 'BALLOTING RESULTS FOR '                                   
                      WS-PRT-DATE ' '                                           
                     '(STUDIO APARTMENT)'                                       
                 DELIMITED BY SIZE        INTO   L508-HDR2-TXT                  
           ELSE                                                                 
              STRING '      BALLOTING RESULTS FOR '                             
                      WS-PRT-DATE ' '                                           
                 DELIMITED BY SIZE        INTO   L508-HDR2-TXT                  
           END-IF.                                                              
                                                                                
           ADD +1                           TO   WS-PAGE-CNT.                   
           MOVE WS-PAGE-CNT                 TO   L508-PAGE.                     
                                                                                
           MOVE SPACES                TO L508-PRTREC.                           
           WRITE L508-PRTREC FROM L508-HEADER1 AFTER PAGE.                      
           WRITE L508-PRTREC FROM L508-HEADER2.                                 
           WRITE L508-PRTREC FROM L508-HEADER3 AFTER 2.                         
           WRITE L508-PRTREC FROM L508-HEADER4.                                 
           WRITE L508-PRTREC FROM L508-DASH-LINE.                               
                                                                                
           MOVE    6                  TO  WS-LINE-CNT.                          
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' - BP13C508 CONTROL TOTALS -----'.                          
           DISPLAY ' '.                                                         
           DISPLAY '  NO OF L507 READ  (BP13L507): ' WS-L507-READ.              
           DISPLAY '  NO OF RECORDS PRINT        : ' WS-L508-PRINT.             
                                                                                
           CLOSE BP13L507                                                       
                 BP13L508.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
