  /*---------------------------------------------------------*/                 
  /* THIS PROGRAM PRINTS THE DETAIL RECORDS THAT HAVE CHANGED*/                 
  /* THE ALLOCATION SCHEME FOR SERS FLAT AFTER ACCEPTANCE OF */                 
  /* FLAT. THE UPDATING IS DINE BY ERU THRU SOCE ONLINE PANEL*/                 
  /* THE CHAGNE OF ALLOC IF AFFECT CUR-SELLING PRICE, REPORT */                 
  /* WILL BE PRINTED FOR PPU AND ERU.                        */                 
  /*                                                         */                 
  /*  CHG REQ    DATE   OIC   DESCRIPTION                    */                 
  /* --------- -------- ---   -----------                    */                 
  /* BP130626  29/06/99 JMS   NEW PROG.                      */                 
  /* BP132618  08/12/04 ROM1  BYPASS CASES WITH BLANK HDB-REF*/                 
  /*---------------------------------------------------------*/                 
                                                                                
  PROC FORMAT;                                                                  
                                                                                
       PICTURE DOLR_PR LOW-<0 = '00,000,009.99'                                 
                   (FILL=' ' PREFIX='$' MULT=100.00000000001)                   
                       0 = '00,000,009.99'                                      
                   (FILL=' ' PREFIX='$' MULT=100.00000000001)                   
                 0-HIGH  = '00,000,009.99'                                      
                   (FILL=' ' PREFIX='$' MULT=100.00000000001);                  
                                                                                
       PICTURE DOLR_DS LOW-<0 = '000,009.99'                                    
                   (FILL=' ' PREFIX='$' MULT=100.000000001)                     
                            0 = '000,009.99'                                    
                   (FILL=' ' PREFIX='$' MULT=100.000000001)                     
                      0-HIGH  = '000,009.99'                                    
                   (FILL=' ' PREFIX='$' MULT=100.000000001);                    
                                                                                
                                                                                
  DATA BP13F310;                                                                
    INFILE BP13F310 END=EOF;                                                    
      INPUT    @1   REGN     $CHAR08.                                           
               @17  HDB_REF  $CHAR11.                                           
                    @17 SCH  $CHAR04.                                           
                    @21 ACC  $CHAR04.                                           
                    @25 CDG  $CHAR01.                                           
                    @26 LSE  $CHAR02.                                           
               @64  FLT_TYP  $CHAR02.                                           
               @66  ADDRESS  $CHAR46.                                           
                    @66 BLK  $CHAR05.                                           
                    @71 STRT $CHAR20.                                           
                    @91 LVL  $CHAR02.                                           
                    @94 MAIN $CHAR05.                                           
               @99  POST     $CHAR06.                                           
               @126 OCS_TAG  $CHAR01.                                           
               @127 ALLOC    $CHAR03.;                                          
                                                                                
      IF HDB_REF ^= '           ';                                              
                                                                                
  DATA _NULL_;                                                                  
    SET BP13F310 END=EOF;                                                       
    LENGTH K110KEY $11;                                                         
     K110KEY = HDB_REF;                                                         
     RC = 0;                                                                    
    INFILE BM06K110 VSAM KEY = K110KEY FEEDBACK = RC;                           
      INPUT@;                                                                   
      IF RC = 0 THEN                                                            
         INPUT    @112 CUR_PR   ZD8.                                            
                  @120 OFF_PR   ZD8.                                            
                  @131 OCS_DIS  ZD6.                                            
                  @137 FLT_DIS  ZD6.                                            
                  @143 CSH_GRT  ZD6.                                            
                  @299 ACPT_DTE $CHAR8.                                         
                       @299 DTE_CY $CHAR4.                                      
                       @303 DTE_MM $CHAR2.                                      
                       @305 DTE_DD $CHAR2.;                                     
      ELSE                                                                      
         _ERROR_ = 0;                                                           
                                                                                
                                                                                
     FILE BP13L208 N=PS LINESLEFT=L PS=55 LS=132 HEADER=HH NOTITLES;            
                                                                                
     ADDR1 = 'BLK'||BLK||' #'||LVL||'-'||MAIN;                                  
     ADDR2 = STRT||' S('||POST||')';                                            
                                                                                
     SCH_ACCT = SCH||'-'||ACC||'-'||CDG||'-'||LSE;                              
                                                                                
     DTE_ACPT = DTE_DD||'/'||DTE_MM||'/'||DTE_CY;                               
                                                                                
     IF _N_ = 1 THEN                                                            
     DO;                                                                        
       S_NO = 0;                                                                
     END;                                                                       
       S_NO + 1;                                                                
                                                                                
          PUT @1   S_NO     Z4.                                                 
              @6   REGN     $CHAR08.                                            
              @22  OFF_PR   DOLR_PR.                                            
              @37  CUR_PR   DOLR_PR.                                            
              @52  FLT_DIS  DOLR_DS.                                            
              @64  OCS_DIS  DOLR_DS.                                            
              @76  ADDR1    $CHAR25.                                            
              @103 DTE_ACPT $CHAR10.                                            
              @116 ALLOC    $CHAR3.                                             
              @123 FLT_TYP  $CHAR2.                                             
              @129 OCS_TAG  $CHAR1.;                                            
                                                                                
          PUT @06  SCH_ACCT $CHAR14.                                            
              @52  CSH_GRT  DOLR_PR.                                            
              @76  ADDR2    $CHAR30.//;                                         
                                                                                
      RETURN;                                                                   
                                                                                
   HH:                                                                          
      CURRENT = TODAY();                                                        
      PAGE + 1;                                                                 
      PUT @1   'BP13L208'                                                       
          @12  'HDBCAT3'                                                        
          @45  'S Y S T E M   O F   C O M M I T M E N T'                        
          @95  'DATE :'            @102 CURRENT DDMMYY10.                       
          @116 'PAGE :'            @123 PAGE/                                   
          @30  'REPORT ON UPDATE OF ALLOCATION SCHEME AND CURRENT'              
          @80  'SELLING PRICE'                                                  
          @95  'USER : ALLOCATION TEAM'/                                        
          @40  'FOR SERS APPLICATION AFTER ACCEPTANCE OF FLAT'//                
          @1   'S/NO'                                                           
          @6   'REGN NO'                                                        
          @27  'OFFER'                                                          
          @40  'CURRENT'                                                        
          @54  'SERS  DISC'                                                     
          @66  'OCS DISC'                                                       
          @76  'FLAT ADDRESS'                                                   
          @103 'ACCEPTANCE'                                                     
          @115 'ALLOC'                                                          
          @122 'FLAT'                                                           
          @128 'OCS'/                                                           
          @6   'SCH ACC NO'                                                     
          @22  'SELLING PRICE'                                                  
          @37  'SELLING PRICE'                                                  
          @52  'CASH GRANT'                                                     
          @103 'DATE'                                                           
          @116 'SCH'                                                            
          @122 'TYPE'                                                           
          @128 'TAG'/                                                           
          @1   '--------------------------------------------------'             
          @51  '--------------------------------------------------'             
          @101 '-------------------------------'//;                             
                                                                                
      RETURN;                                                                   
/*                                                                              
