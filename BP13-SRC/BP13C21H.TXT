      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21H.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  07 AUGUST 2007.                                           
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO PRODUCE A REPORT FOR TALLYING BP13K240   *           
      *                 BY SALES MODE, NEW TOWN AND FLAT TYPE       *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13K240 -- FLAT HEADER FILE                          *           
      *   1.  BP13K249 -- FLAT AVAILABILITY FILE                    *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133094 07/08/2007 RB12 NEW PROGRAM                        *           
      * BP133161 26/09/2007 RB12 REPLACE ALT-KEY3 WITH ALT-KEY5     *           
      * BP133152 27/09/2007 RB12 INCREASE CAT AVAIL FIELD LENGTH    *           
      *                          FROM 3 BYTES TO 4 BYTES            *           
      * BP133367 25/04/2008 RB12 INCREASE K249 LENGTH               *           
      * BP135415 13/08/2014 ESA1 EXTEND BP13K249 TO 450             *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K240 ASSIGN TO BP13K240                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K240-KEY-FLD                               
                  ALTERNATE KEY   IS K240-ALT-KEY5                              
                  FILE STATUS     IS BP13K240-STATUS.                           
                                                                                
           SELECT BP13K249 ASSIGN TO BP13K249                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K249-KEY-FLD                               
                  FILE STATUS     IS BP13K249-STATUS.                           
                                                                                
           SELECT BP13L21H ASSIGN TO BP13L21H.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K240                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K240.                                                           
                                                                                
       FD   BP13K249                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K249.                                                           
                                                                                
       FD   BP13L21H                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   L21H-OUTREC                   PIC X(132).                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-SNO                 PIC 9(04)  VALUE ZEROES.                      
       01  WS-CURR-DATE           PIC X(08)  VALUE ZEROES.                      
       01  WS-PREV-KEY            PIC X(38)  VALUE SPACES.                      
       01  WS-SAVE-FLAT-TYPE      PIC X(09)  VALUE SPACES.                      
       01  WS-SAVE-DTE-BALLOT     PIC X(06)  VALUE SPACES.                      
       01  WS-AVAIL               PIC 9(04)  VALUE ZEROES.                      
       01  WS-AVAIL-CAT1          PIC 9(03)  VALUE ZEROES.                      
       01  WS-AVAIL-CAT2          PIC 9(03)  VALUE ZEROES.                      
       01  WS-AVAIL-CAT3          PIC 9(03)  VALUE ZEROES.                      
       01  WS-TOTAL-OFFER         PIC 9(04)  VALUE ZEROES.                      
       01  WS-TOTAL-AVAIL         PIC 9(04)  VALUE ZEROES.                      
       01  WS-TOTAL-AVAIL-CAT1    PIC 9(04)  VALUE ZEROES.                      
       01  WS-TOTAL-AVAIL-CAT2    PIC 9(04)  VALUE ZEROES.                      
       01  WS-TOTAL-AVAIL-CAT3    PIC 9(04)  VALUE ZEROES.                      
       01  WS-K240-READ-CNT       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K240-READ-ERR       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K249-READ-CNT       PIC 9(08)  VALUE ZEROES.                      
       01  WS-K249-NOT-FND        PIC 9(08)  VALUE ZEROES.                      
       01  WS-K249-READ-ERR       PIC 9(08)  VALUE ZEROES.                      
       01  WS-L21H-PRNT-CTR       PIC 9(08)  VALUE ZEROES.                      
       01  WS-MISMATCH-CTR        PIC 9(08)  VALUE ZEROES.                      
       01  BP13K240-STATUS        PIC 9(02)  VALUE ZEROES.                      
       01  BP13K249-STATUS        PIC 9(02)  VALUE ZEROES.                      
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-K240-EOF-FLAG   PIC X VALUE 'N'.                              
               88 WS-K240-EOF-YES       VALUE 'Y'.                              
               88 WS-K240-EOF-NO        VALUE 'N'.                              
           05  WS-FIRST-PAGE-FLAG PIC X VALUE 'Y'.                              
               88 WS-FIRST-PAGE-YES     VALUE 'Y'.                              
               88 WS-FIRST-PAGE-NO      VALUE 'N'.                              
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-SEQ-CNT         PIC 9(05)  VALUE ZEROES.                      
           05  WS-PAGE-CNT        PIC 9(05)  VALUE 0.                           
           05  WS-LINE-CNT        PIC 9(02)  VALUE 0.                           
                                                                                
       01  WS-BLANK-LINE          PIC X(132) VALUE SPACES.                      
                                                                                
       01  WS-DASHED-LINE.                                                      
           05  WS-DASH            PIC X(132) VALUE ALL '-'.                     
                                                                                
       01  L21H-HEADER.                                                         
           05  FILLER             PIC X(8)   VALUE 'BP13L21H'.                  
           05  FILLER             PIC X(2)   VALUE SPACES.                      
           05  FILLER             PIC X(4)   VALUE 'HDB3'.                      
           05  FILLER             PIC X(28)  VALUE SPACES.                      
           05  L21H-HEADER-TEXT   PIC X(41)  VALUE                              
                 '      FLAT AND ETHNIC AVAILABILITY       '.                   
           05  FILLER             PIC X(26)  VALUE SPACES.                      
           05  FILLER             PIC X(7)   VALUE 'DATE : '.                   
           05  L21H-DATE          PIC X(10)  VALUE SPACES.                      
           05  FILLER             PIC X(6)   VALUE SPACES.                      
                                                                                
       01  L21H-HEADER2.                                                        
           05  FILLER             PIC X(109) VALUE SPACES.                      
           05  FILLER             PIC X(7)   VALUE 'PAGE : '.                   
           05  L21H-PAGE-NUM      PIC ZZ9    VALUE ZEROES.                      
           05  FILLER             PIC X(13)  VALUE SPACES.                      
                                                                                
       01  L21H-COLUMN-HEADER1.                                                 
           05  FILLER             PIC X(10)  VALUE SPACES.                      
           05  FILLER             PIC X(49) VALUE                               
                'SNO   SALE  NEW TOWN             FT  BLOCK  TOTAL'.            
           05  FILLER             PIC X(05)  VALUE SPACES.                      
           05  FILLER             PIC X(48) VALUE                               
                'MAXIMUM        SELECTED      TOTAL     AVAILABLE'.             
           05  FILLER             PIC X(04)  VALUE SPACES.                      
           05  FILLER             PIC X(08)  VALUE 'MISMATCH'.                  
           05  FILLER             PIC X(08)  VALUE SPACES.                      
                                                                                
       01  L21H-COLUMN-HEADER2.                                                 
           05  FILLER             PIC X(16)  VALUE SPACES.                      
           05  FILLER             PIC X(43) VALUE                               
                'MODE                                  OFFER'.                  
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  FILLER             PIC X(37) VALUE                               
                'CAT1 CAT2 CAT3  CAT1 CAT2 CAT3  AVAIL'.                        
           05  FILLER             PIC X(17) VALUE                               
                ' CAT1  CAT2  CAT3'.                                            
           05  FILLER             PIC X(17) VALUE SPACES.                       
                                                                                
       01  L21H-DETAIL.                                                         
           05  FILLER             PIC X(10)  VALUE SPACES.                      
           05  L21H-SNO           PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-SALE-MODE     PIC X(03)  VALUE SPACES.                      
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  L21H-NEW-TOWN      PIC X(20)  VALUE SPACES.                      
           05  FILLER             PIC X(01)  VALUE '/'.                         
           05  L21H-FLAT-TYPE     PIC X(02)  VALUE SPACES.                      
           05  FILLER             PIC X(01)  VALUE '/'.                         
           05  L21H-BLOCK         PIC X(05)  VALUE SPACES.                      
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-TOTAL-OFFER   PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  L21H-MAX-CAT1      PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-MAX-CAT2      PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-MAX-CAT3      PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  L21H-TOT-CAT1      PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-TOT-CAT2      PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-TOT-CAT3      PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  L21H-TOTAL-AVAIL   PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(04)  VALUE SPACES.                      
           05  L21H-AVAIL-CAT1    PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  L21H-AVAIL-CAT2    PIC ZZ9    VALUE ZERO.                        
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  L21H-AVAIL-CAT3    PIC ZZ9    VALUE ZERO.                        
                                                                                
       01  L21H-TOTAL-LINE.                                                     
           05  FILLER             PIC X(45)  VALUE SPACES.                      
           05  FILLER             PIC X(06)  VALUE 'TOTAL:'.                    
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-OFFER-TOTAL   PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(35)  VALUE SPACES.                      
           05  L21H-AVAIL-TOTAL   PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  L21H-AVLCAT1-TOTAL PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-AVLCAT2-TOTAL PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-AVLCAT3-TOTAL PIC ZZZ9   VALUE ZERO.                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  L21H-MISMATCH-IND  PIC X(01)  VALUE SPACES.                      
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS  THRU 1000-EXIT.                            
           PERFORM 2000-STARTBR-K240 THRU 2000-EXIT                             
           PERFORM 3000-PROCESS-K240 THRU 3000-EXIT                             
             UNTIL WS-K240-EOF-YES.                                             
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13K240                                                 
                       BP13K249                                                 
                OUTPUT BP13L21H.                                                
                                                                                
           IF BP13K240-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K240 : ' BP13K240-STATUS             
              MOVE BP13K240-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K249-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K249 : ' BP13K249-STATUS             
              MOVE BP13K249-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
           STRING WS-CURR-DATE(7:2) '/'                                         
                  WS-CURR-DATE(5:2) '/'                                         
                  WS-CURR-DATE(1:4)                                             
              DELIMITED BY SIZE INTO L21H-DATE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       2000-STARTBR-K240.                                                       
      *--------------------*                                                    
                                                                                
           MOVE SPACES TO K240-ALT-KEY5.                                        
                                                                                
           START BP13K240 KEY IS GREATER OR EQUAL K240-ALT-KEY5                 
             END-START.                                                         
                                                                                
           EVALUATE BP13K240-STATUS                                             
                                                                                
              WHEN 00                                                           
                 CONTINUE                                                       
                                                                                
              WHEN 10                                                           
                 DISPLAY 'K240 END OF FILE'                                     
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
                                                                                
              WHEN 23                                                           
                 DISPLAY 'KEY NOT FOUND IN START OF K240: '                     
                          K240-ALT-KEY5                                         
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'START BROWSE ERROR, BP13K240-STATUS: '                
                                         BP13K240-STATUS                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-K240.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13K240 NEXT RECORD                                            
             AT END                                                             
                SET WS-K240-EOF-YES TO TRUE                                     
                PERFORM 3100-PRINT-TOTALS THRU 3100-EXIT                        
                GO TO 3000-EXIT.                                                
                                                                                
                                                                                
           IF K240-CDE-HEADER NOT EQUAL '01'                                    
              GO TO 3000-EXIT                                                   
           ELSE                                                                 
              ADD 1 TO WS-K240-READ-CNT                                         
           END-IF.                                                              
                                                                                
           IF K240-ALT-KEY5 NOT EQUAL WS-PREV-KEY                               
              IF WS-PREV-KEY NOT EQUAL SPACES                                   
                 PERFORM 3100-PRINT-TOTALS THRU 3100-EXIT                       
                 MOVE ZEROES TO WS-TOTAL-OFFER                                  
                                WS-TOTAL-AVAIL                                  
                                WS-TOTAL-AVAIL-CAT1                             
                                WS-TOTAL-AVAIL-CAT2                             
                                WS-TOTAL-AVAIL-CAT3                             
              END-IF                                                            
              MOVE K240-ALT-KEY5 TO WS-PREV-KEY                                 
           END-IF.                                                              
                                                                                
           PERFORM 3200-MOVE-K240-FIELDS THRU 3200-EXIT.                        
           PERFORM 3300-COMPUTE-AVAIL    THRU 3300-EXIT.                        
           PERFORM 3400-PRINT-L21H       THRU 3400-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3100-PRINT-TOTALS.                                                       
      *------------------*                                                      
                                                                                
           MOVE WS-TOTAL-OFFER      TO L21H-OFFER-TOTAL.                        
           MOVE WS-TOTAL-AVAIL      TO L21H-AVAIL-TOTAL.                        
           MOVE WS-TOTAL-AVAIL-CAT1 TO L21H-AVLCAT1-TOTAL.                      
           MOVE WS-TOTAL-AVAIL-CAT2 TO L21H-AVLCAT2-TOTAL.                      
           MOVE WS-TOTAL-AVAIL-CAT3 TO L21H-AVLCAT3-TOTAL.                      
                                                                                
           PERFORM 3150-READ-BP13K249 THRU 3150-EXIT.                           
                                                                                
           IF WS-LINE-CNT > 54 OR WS-FIRST-PAGE-YES                             
             PERFORM 3410-PRINT-HEADING THRU 3410-EXIT                          
             SET WS-FIRST-PAGE-NO    TO TRUE                                    
           END-IF.                                                              
                                                                                
           WRITE L21H-OUTREC      FROM WS-DASHED-LINE.                          
           WRITE L21H-OUTREC      FROM L21H-TOTAL-LINE.                         
           WRITE L21H-OUTREC      FROM WS-BLANK-LINE.                           
           ADD   3                  TO WS-LINE-CNT.                             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3150-READ-BP13K249.                                                      
      *------------------*                                                      
                                                                                
           MOVE L21H-NEW-TOWN       TO K249-NUM-NEW-TOWN.                       
           MOVE WS-SAVE-FLAT-TYPE   TO K249-NUM-FLAT-TYPE.                      
           MOVE L21H-SALE-MODE      TO K249-NUM-SELECTION.                      
           MOVE WS-SAVE-DTE-BALLOT  TO K249-DTE-BALLOT.                         
                                                                                
           READ BP13K249.                                                       
                                                                                
           EVALUATE BP13K249-STATUS                                             
             WHEN 00                                                            
                PERFORM 3155-MATCH-TOTALS THRU 3155-EXIT                        
                ADD 1 TO WS-K249-READ-CNT                                       
             WHEN 23                                                            
                DISPLAY 'BP13K249 - REC NOT FOUND: ' K249-KEY-FLD               
                ADD 1 TO WS-K249-NOT-FND                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K249 - READ ERROR: ' BP13K249-STATUS               
                ADD 1 TO WS-K249-READ-ERR                                       
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3155-MATCH-TOTALS.                                                       
      *-----------------*                                                       
                                                                                
           MOVE SPACES TO L21H-MISMATCH-IND.                                    
                                                                                
           IF WS-TOTAL-OFFER NOT = K249-NUM-TOTAL                               
              MOVE '*' TO L21H-MISMATCH-IND                                     
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-AVAIL NOT = K249-NUM-AVAIL-TOTAL                         
              MOVE '*' TO L21H-MISMATCH-IND                                     
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-AVAIL-CAT1 NOT = K249-NUM-AVAIL-CAT1                     
              MOVE '*' TO L21H-MISMATCH-IND                                     
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-AVAIL-CAT2 NOT = K249-NUM-AVAIL-CAT2                     
              MOVE '*' TO L21H-MISMATCH-IND                                     
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-AVAIL-CAT3 NOT = K249-NUM-AVAIL-CAT3                     
              MOVE '*' TO L21H-MISMATCH-IND                                     
           END-IF.                                                              
                                                                                
           IF L21H-MISMATCH-IND = '*'                                           
              DISPLAY 'K249-KEY-FLD: ' K249-KEY-FLD                             
              ADD 1 TO WS-MISMATCH-CTR                                          
              MOVE '99' TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
       3155-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3200-MOVE-K240-FIELDS.                                                   
      *---------------------*                                                   
                                                                                
           MOVE SPACES              TO L21H-DETAIL.                             
                                                                                
           MOVE K240-NUM-SELECTION3 TO L21H-SALE-MODE.                          
           MOVE K240-NUM-NEW-TOWN3  TO L21H-NEW-TOWN.                           
           MOVE K240-NUM-FLAT-TYPE  TO L21H-FLAT-TYPE.                          
           MOVE K240-NUM-FLAT-TYPE3 TO WS-SAVE-FLAT-TYPE.                       
           MOVE K240-DTE-BALLOT     TO WS-SAVE-DTE-BALLOT.                      
           MOVE K240-NUM-BLK3       TO L21H-BLOCK.                              
           MOVE K240-NUM-TOTAL      TO L21H-TOTAL-OFFER.                        
           MOVE K240-NUM-MAX-CAT1   TO L21H-MAX-CAT1.                           
           MOVE K240-NUM-MAX-CAT2   TO L21H-MAX-CAT2.                           
           MOVE K240-NUM-MAX-CAT3   TO L21H-MAX-CAT3.                           
           MOVE K240-NUM-TOT-CAT1   TO L21H-TOT-CAT1.                           
           MOVE K240-NUM-TOT-CAT2   TO L21H-TOT-CAT2.                           
           MOVE K240-NUM-TOT-CAT3   TO L21H-TOT-CAT3.                           
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3300-COMPUTE-AVAIL.                                                      
      *-------------------*                                                     
                                                                                
           COMPUTE WS-AVAIL = K240-NUM-TOTAL                                    
                            - K240-NUM-TOT-CAT1                                 
                            - K240-NUM-TOT-CAT2                                 
                            - K240-NUM-TOT-CAT3.                                
                                                                                
           COMPUTE WS-AVAIL-CAT1 = K240-NUM-MAX-CAT1                            
                                 - K240-NUM-TOT-CAT1.                           
                                                                                
           IF WS-AVAIL-CAT1 > WS-AVAIL                                          
              MOVE WS-AVAIL TO WS-AVAIL-CAT1                                    
           END-IF.                                                              
                                                                                
           COMPUTE WS-AVAIL-CAT2 = K240-NUM-MAX-CAT2                            
                                 - K240-NUM-TOT-CAT2.                           
                                                                                
           IF WS-AVAIL-CAT2 > WS-AVAIL                                          
              MOVE WS-AVAIL TO WS-AVAIL-CAT2                                    
           END-IF.                                                              
                                                                                
           COMPUTE WS-AVAIL-CAT3 = K240-NUM-MAX-CAT3                            
                                 - K240-NUM-TOT-CAT3.                           
                                                                                
           IF WS-AVAIL-CAT3 > WS-AVAIL                                          
              MOVE WS-AVAIL TO WS-AVAIL-CAT3                                    
           END-IF.                                                              
                                                                                
           MOVE WS-AVAIL        TO L21H-TOTAL-AVAIL.                            
           MOVE WS-AVAIL-CAT1   TO L21H-AVAIL-CAT1.                             
           MOVE WS-AVAIL-CAT2   TO L21H-AVAIL-CAT2.                             
           MOVE WS-AVAIL-CAT3   TO L21H-AVAIL-CAT3.                             
                                                                                
           ADD K240-NUM-TOTAL   TO WS-TOTAL-OFFER.                              
           ADD WS-AVAIL         TO WS-TOTAL-AVAIL.                              
           ADD WS-AVAIL-CAT1    TO WS-TOTAL-AVAIL-CAT1.                         
           ADD WS-AVAIL-CAT2    TO WS-TOTAL-AVAIL-CAT2.                         
           ADD WS-AVAIL-CAT3    TO WS-TOTAL-AVAIL-CAT3.                         
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       3400-PRINT-L21H.                                                         
      *----------------*                                                        
                                                                                
           MOVE SPACES TO L21H-OUTREC.                                          
                                                                                
           IF WS-LINE-CNT > 54 OR WS-FIRST-PAGE-YES                             
             PERFORM 3410-PRINT-HEADING THRU 3410-EXIT                          
             SET WS-FIRST-PAGE-NO    TO TRUE                                    
           END-IF.                                                              
                                                                                
           ADD 1                      TO WS-SNO.                                
           MOVE WS-SNO                TO L21H-SNO.                              
                                                                                
           WRITE L21H-OUTREC  FROM L21H-DETAIL.                                 
                                                                                
           ADD 1 TO WS-L21H-PRNT-CTR.                                           
           ADD 1 TO WS-LINE-CNT.                                                
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3410-PRINT-HEADING.                                                      
      *------------------*                                                      
                                                                                
           ADD   1             TO   WS-PAGE-CNT.                                
           MOVE  WS-PAGE-CNT   TO   L21H-PAGE-NUM.                              
                                                                                
           WRITE L21H-OUTREC   FROM L21H-HEADER AFTER PAGE.                     
           WRITE L21H-OUTREC   FROM L21H-HEADER2.                               
           WRITE L21H-OUTREC   FROM WS-BLANK-LINE.                              
           WRITE L21H-OUTREC   FROM L21H-COLUMN-HEADER1.                        
           WRITE L21H-OUTREC   FROM L21H-COLUMN-HEADER2.                        
           WRITE L21H-OUTREC   FROM WS-DASHED-LINE.                             
           MOVE  6             TO   WS-LINE-CNT.                                
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21H  *************'.                           
           DISPLAY 'RECS READ    FROM  BP13K240   : ' WS-K240-READ-CNT.         
           DISPLAY 'RECS PRINTED   TO BP13L21H    : ' WS-L21H-PRNT-CTR.         
           DISPLAY '                               '                            
           DISPLAY 'RECS READ    FROM  BP13K249   : ' WS-K249-READ-CNT.         
           DISPLAY 'RECS NOT FOUND IN  BP13K249   : ' WS-K249-NOT-FND.          
           DISPLAY 'READ ERRORS    IN  BP13K249   : ' WS-K249-READ-ERR.         
           DISPLAY '                               '                            
           DISPLAY 'MISMATCH ERRORS FOR K240/K249 : ' WS-MISMATCH-CTR.          
                                                                                
           CLOSE BP13K240                                                       
                 BP13K249                                                       
                 BP13L21H.                                                      
                                                                                
           IF BP13K240-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSE ERROR, BP13K240-STATUS ' BP13K240-STATUS            
            MOVE BP13K240-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K249-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSE ERROR, BP13K249-STATUS ' BP13K249-STATUS            
            MOVE BP13K249-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
