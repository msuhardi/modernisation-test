      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CE13.                                                 
       AUTHOR.        MRN1.                                                     
       DATE-WRITTEN.  05072012.                                                 
      *===============================================================*         
      *   OBJECTIVE:  REPORT 3: DISBURSEMENT & RECOVERY OF AHG GRANT  *         
      *                         FOR DBSS CASES                        *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F800                                  *         
      *                  2. BP13KD09                                  *         
      *     REPORT FILE:                                              *         
      *                  1. P13LE13A                                  *         
      *                  2. P13LE13B                                  *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134970 05/07/2012 MRN1 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F800  ASSIGN        TO BP13F800.                         
                                                                                
           SELECT  BP13KD09  ASSIGN        TO BP13KD09                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD09-KEY-FLD                      
                             FILE STATUS   IS WS-KD09-STAT.                     
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STAT.                      
                                                                                
           SELECT  P13LE13A  ASSIGN        TO P13LE13A.                         
           SELECT  P13LE13B  ASSIGN        TO P13LE13B.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13KD09                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13KD09.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  P13LE13A                                                             
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13LE13A-REC                  PIC X(133).                            
                                                                                
       FD  P13LE13B                                                             
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13LE13B-REC                  PIC X(133).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F800-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-KD09             PIC X(1)  VALUE 'N'.                     
           05  WS-KD09-FOUND-TAG       PIC X(1)  VALUE 'N'.                     
           05  WS-KD09-STAT            PIC 99    VALUE ZEROES.                  
           05  WS-K022-STAT            PIC 99    VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ            PIC 9(8)  VALUE ZEROES.                  
           05  WS-SNO-A                PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO-B                PIC 9(7)  VALUE ZEROES.                  
           05  WS-NUM-CASES            PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-CURR-DATE.                                                     
              10 WS-CURR-YY            PIC 9(04)    VALUE ZEROES.               
              10 WS-CURR-MM            PIC 9(02)    VALUE ZEROES.               
              10 WS-CURR-DD            PIC 9(02)    VALUE ZEROES.               
           05 WS-DTE-CANCEL-PREV       PIC X(08)    VALUE SPACES.               
           05 WS-DTE-CANCEL.                                                    
              10 WS-DTE-CAN-YY         PIC 9(04)    VALUE ZEROES.               
              10 WS-DTE-CAN-MM         PIC 9(02)    VALUE ZEROES.               
              10 WS-DTE-CAN-DD         PIC 9(02)    VALUE ZEROES.               
           05 WS-MONTHS-OUT            PIC 9(03)    VALUE ZEROES.               
           05 WS-AMOUNT-OUT            PIC 9(08)V99 VALUE ZEROES.               
           05 WS-TOTAL-AMOUNT-OUT      PIC 9(08)    VALUE ZEROES.               
           05 WS-GRANT-SUM             PIC 9(08)    VALUE ZEROES.               
           05 WS-RECOVER-SUM           PIC 9(08)    VALUE ZEROES.               
           05 WS-YY-DIFF               PIC 9(04)    VALUE ZEROES.               
           05 WS-MM-DIFF               PIC 9(02)    VALUE ZEROES.               
           05 WS-LE13A-WRITE           PIC 9(08)    VALUE ZEROES.               
           05 WS-LE13B-WRITE           PIC 9(08)    VALUE ZEROES.               
           05 WS-KD09-FND              PIC 9(08)    VALUE ZEROES.               
           05 WS-KD09-NTFND            PIC 9(08)    VALUE ZEROES.               
           05 WS-NFND-K022             PIC 9(08)    VALUE ZEROES.               
           05 WS-BYPASS                PIC 9(08)    VALUE ZEROES.               
                                                                                
       01  WS-HEADER-LINE1A.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '  S/N   MONTHS-OUTSTANDING    NO. OF CASES    AMOU'.            
           05  FILLER                 PIC X(14) VALUE                           
               'NT-OUTSTANDING'.                                                
                                                                                
       01  WS-HEADER-LINE2A.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '------  ------------------    ------------    ----'.            
           05  FILLER                 PIC X(14) VALUE                           
               '--------------'.                                                
                                                                                
       01  WS-LE13A-DETAIL.                                                     
           05 WS-LE13A-SNO                  PIC ZZZZZ9.                         
           05 FILLER                        PIC X(09) VALUE SPACES.             
           05 WS-LE13A-MONTHS-OUT           PIC X(03).                          
           05 FILLER                        PIC X(13) VALUE SPACES.             
           05 WS-LE13A-NUM-CASES            PIC ZZZZZ9.                         
           05 FILLER                        PIC X(13) VALUE SPACES.             
           05 WS-LE13A-AMT-OUT              PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(04) VALUE SPACES.             
                                                                                
       01  WS-HEADER-LINE1B.                                                    
           05  FILLER                 PIC X(40) VALUE                           
               '  S/N   MTH-OUT  REGNO   DTE-CANCEL   AM'.                      
           05  FILLER                 PIC X(50) VALUE                           
               'T-OUT   OIC   AHG-GRANT AHG-RECOVR SHG-GRANT  SHG-'.            
           05  FILLER                 PIC X(06) VALUE                           
               'RECOVR'.                                                        
                                                                                
       01  WS-HEADER-LINE2B.                                                    
           05  FILLER                 PIC X(40) VALUE                           
               '------  ------- -------- ---------- ----'.                      
           05  FILLER                 PIC X(50) VALUE                           
               '------ ----- ---------- ---------- ---------- ----'.            
           05  FILLER                 PIC X(06) VALUE                           
               '------'.                                                        
                                                                                
       01  WS-LE13B-DETAIL.                                                     
           05 WS-LE13B-SNO                  PIC ZZZZZ9.                         
           05 FILLER                        PIC X(04) VALUE SPACES.             
           05 WS-LE13B-MONTHS-OUT           PIC X(03).                          
           05 FILLER                        PIC X(03) VALUE SPACES.             
           05 WS-LE13B-REGNO                PIC X(08).                          
           05 FILLER                        PIC X(02) VALUE SPACES.             
           05 WS-LE13B-DTE-CANCEL           PIC X(08).                          
           05 FILLER                        PIC X(02) VALUE SPACES.             
           05 WS-LE13B-AMT-OUT              PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE13B-OIC                  PIC X(05).                          
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE13B-AHG-GRANT            PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE13B-AHG-RECOVER          PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE13B-SHG-GRANT            PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE13B-SHG-RECOVER          PIC ZZZ,ZZ9.99.                     
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
           MOVE F800-DTE-CANCEL            TO WS-DTE-CANCEL.                    
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-F800-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13KD09                                                 
                       BP13K022                                                 
               OUTPUT  P13LE13A                                                 
                       P13LE13B.                                                
                                                                                
           IF WS-KD09-STAT NOT = 00 AND 97                                      
              DISPLAY 'BP13KD09 - ERROR OPENING : ' WS-KD09-STAT                
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR READING BP13K022 - STAT ' WS-K022-STAT             
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-CURR-DATE.                   
                                                                                
           WRITE P13LE13A-REC FROM WS-HEADER-LINE1A.                            
           WRITE P13LE13A-REC FROM WS-HEADER-LINE2A.                            
                                                                                
           WRITE P13LE13B-REC FROM WS-HEADER-LINE1B.                            
           WRITE P13LE13B-REC FROM WS-HEADER-LINE2B.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800           AT END                                       
                MOVE 'Y'           TO WS-F800-EOF                               
                IF WS-NUM-CASES NOT = ZEROES                                    
                   PERFORM 4500-MOVE-SUMMARY THRU 4500-EXIT                     
                   ADD 1 TO WS-SNO-A                                            
                   MOVE WS-SNO-A                 TO WS-LE13A-SNO                
                   WRITE P13LE13A-REC FROM WS-LE13A-DETAIL                      
                   ADD 1 TO WS-LE13A-WRITE                                      
                   MOVE ZEROES TO WS-NUM-CASES, WS-TOTAL-AMOUNT-OUT             
                END-IF                                                          
                GO                 TO 2000-EXIT.                                
                                                                                
           IF F800-NUM-REGN = SPACES OR LOW-VALUES                              
              GO TO 2000-READ-BP13F800.                                         
                                                                                
           ADD  1                  TO WS-F800-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE F800-NUM-REGN         TO KD09-NUM-REGN.                         
           PERFORM 3400-READ-BP13KD09 THRU 3400-EXIT.                           
                                                                                
           IF WS-NUM-CASES NOT = ZEROES                                         
              IF F800-DTE-CANCEL(1:6) NOT = WS-DTE-CANCEL-PREV(1:6)             
                 PERFORM 4500-MOVE-SUMMARY THRU 4500-EXIT                       
                 ADD 1 TO WS-SNO-A                                              
                 MOVE WS-SNO-A                 TO WS-LE13A-SNO                  
                 WRITE P13LE13A-REC FROM WS-LE13A-DETAIL                        
                 ADD 1 TO WS-LE13A-WRITE                                        
                 MOVE ZEROES TO WS-NUM-CASES, WS-TOTAL-AMOUNT-OUT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-KD09-FOUND-TAG = 'Y'                                           
              IF WS-NUM-CASES = ZEROES                                          
                 PERFORM 3600-COMPUTE-MONTHS-OUT THRU 3600-EXIT                 
                 PERFORM 3800-GET-OIC            THRU 3800-EXIT                 
              END-IF                                                            
              PERFORM 3500-CHECK-NON-NUMERIC  THRU 3500-EXIT                    
              COMPUTE WS-GRANT-SUM   = KD09-AMT-AHG-GRANT +                     
                                       KD09-AMT-SHG-GRANT                       
              COMPUTE WS-RECOVER-SUM = KD09-AMT-AHG-RECOVER +                   
                                       KD09-AMT-SHG-RECOVER                     
              IF WS-GRANT-SUM > WS-RECOVER-SUM                                  
                  PERFORM 3700-COMPUTE-AMOUNT-OUT THRU 3700-EXIT                
                  PERFORM 4000-MOVE-DETAIL THRU 4000-EXIT                       
                  ADD 1 TO WS-SNO-B, WS-NUM-CASES                               
                  MOVE WS-SNO-B                 TO WS-LE13B-SNO                 
                  WRITE P13LE13B-REC FROM WS-LE13B-DETAIL                       
                  ADD 1 TO WS-LE13B-WRITE                                       
              ELSE                                                              
                ADD 1 TO WS-BYPASS                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F800-DTE-CANCEL TO WS-DTE-CANCEL-PREV                           
           PERFORM 2000-READ-BP13F800 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BP13KD09.                                                      
      ******************************************************************        
                                                                                
           READ BP13KD09.                                                       
                                                                                
           EVALUATE WS-KD09-STAT                                                
               WHEN 00                                                          
                    MOVE 'Y' TO WS-KD09-FOUND-TAG                               
                    ADD 1    TO WS-KD09-FND                                     
               WHEN 23                                                          
                    MOVE 'N' TO WS-KD09-FOUND-TAG                               
                    ADD 1    TO WS-KD09-NTFND                                   
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13KD09 : ' WS-KD09-STAT            
                            ' REGN = ' KD09-KEY-FLD                             
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-CHECK-NON-NUMERIC.                                                  
      ******************************************************************        
           IF KD09-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-AHG-GRANT                                 
           END-IF.                                                              
                                                                                
           IF KD09-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-SHG-GRANT                                 
           END-IF.                                                              
                                                                                
           IF KD09-AMT-AHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-AHG-RECOVER                               
           END-IF.                                                              
                                                                                
           IF KD09-AMT-SHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-SHG-RECOVER                               
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-COMPUTE-MONTHS-OUT.                                                 
      ******************************************************************        
           MOVE F800-DTE-CANCEL            TO WS-DTE-CANCEL.                    
           MOVE ZEROES                     TO WS-MONTHS-OUT.                    
                                                                                
           IF WS-CURR-DATE > WS-DTE-CANCEL                                      
              IF WS-CURR-MM >= WS-DTE-CAN-MM                                    
                 COMPUTE WS-YY-DIFF = WS-CURR-YY - WS-DTE-CAN-YY                
                 COMPUTE WS-MM-DIFF = WS-CURR-MM - WS-DTE-CAN-MM                
              ELSE                                                              
                 COMPUTE WS-YY-DIFF = (WS-CURR-YY - 1)  - WS-DTE-CAN-YY         
                 COMPUTE WS-MM-DIFF = (WS-CURR-MM + 12) - WS-DTE-CAN-MM         
              END-IF                                                            
              COMPUTE WS-MONTHS-OUT = (WS-YY-DIFF * 12) + WS-MM-DIFF            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3700-COMPUTE-AMOUNT-OUT.                                                 
      ******************************************************************        
           MOVE ZEROES TO WS-AMOUNT-OUT.                                        
                                                                                
           COMPUTE WS-AMOUNT-OUT =                                              
                  (KD09-AMT-AHG-GRANT   - KD09-AMT-AHG-RECOVER) +               
                  (KD09-AMT-SHG-GRANT   - KD09-AMT-SHG-RECOVER)                 
                                                                                
           ADD WS-AMOUNT-OUT TO WS-TOTAL-AMOUNT-OUT.                            
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3800-GET-OIC.                                                            
      ******************************************************************        
                                                                                
           MOVE SPACES               TO BP13K022-REC.                           
           MOVE F800-NUM-REGN        TO K022-NUM-REGN.                          
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN                                   
             EVALUATE WS-K022-STAT                                              
                WHEN 00                                                         
                WHEN 02                                                         
                  CONTINUE                                                      
                WHEN 23                                                         
                  ADD 1 TO WS-NFND-K022                                         
                  DISPLAY 'K022 RECORD NOT FOUND - ' K022-NUM-REGN              
                WHEN OTHER                                                      
                  DISPLAY 'ERROR READING BP13K022 - STAT ' WS-K022-STAT         
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
             END-EVALUATE.                                                      
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-MOVE-DETAIL.                                                        
      ******************************************************************        
           INITIALIZE     WS-LE13B-DETAIL.                                      
           MOVE SPACES TO WS-LE13B-DETAIL.                                      
                                                                                
           MOVE WS-MONTHS-OUT             TO WS-LE13B-MONTHS-OUT.               
           MOVE F800-NUM-REGN             TO WS-LE13B-REGNO.                    
           MOVE WS-AMOUNT-OUT             TO WS-LE13B-AMT-OUT.                  
           MOVE K022-NUM-OIC              TO WS-LE13B-OIC.                      
           MOVE KD09-AMT-AHG-GRANT        TO WS-LE13B-AHG-GRANT.                
           MOVE KD09-AMT-AHG-RECOVER      TO WS-LE13B-AHG-RECOVER.              
           MOVE KD09-AMT-SHG-GRANT        TO WS-LE13B-SHG-GRANT.                
           MOVE KD09-AMT-SHG-RECOVER      TO WS-LE13B-SHG-RECOVER.              
           MOVE WS-DTE-CANCEL             TO WS-LE13B-DTE-CANCEL.               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4500-MOVE-SUMMARY.                                                       
      ******************************************************************        
           INITIALIZE WS-LE13A-DETAIL.                                          
                                                                                
           MOVE WS-MONTHS-OUT             TO WS-LE13A-MONTHS-OUT.               
           MOVE WS-NUM-CASES              TO WS-LE13A-NUM-CASES.                
           MOVE WS-TOTAL-AMOUNT-OUT       TO WS-LE13A-AMT-OUT.                  
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CE13'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F800 RECORDS READ............. : '            
                    WS-F800-READ.                                               
           DISPLAY '(2) NO OF DETAIL WRITTEN.................... : '            
                    WS-LE13B-WRITE.                                             
           DISPLAY '(3) NO OF SUMMARY WRITTEN................... : '            
                    WS-LE13A-WRITE.                                             
           DISPLAY '(4) NO OF REGNO FOUND IN KD09............... : '            
                    WS-KD09-FND.                                                
           DISPLAY '(5) NO OF REGNO NOT FOUND IN KD09........... : '            
                    WS-KD09-NTFND.                                              
           DISPLAY '(6) NO OF INVALID CASES BYPASSED............ : '            
                    WS-BYPASS.                                                  
                                                                                
           CLOSE    BP13F800                                                    
                    BP13KD09                                                    
                    BP13K022                                                    
                    P13LE13A                                                    
                    P13LE13B.                                                   
                                                                                
           IF WS-KD09-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR CLOSING BP13KD09 - STAT ' WS-KD09-STAT             
           END-IF.                                                              
                                                                                
           IF WS-K022-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR CLOSING BP13K022 - STAT ' WS-K022-STAT             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
