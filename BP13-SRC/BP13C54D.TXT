       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C54D.                                                 
       AUTHOR.        TRM1.                                                     
       DATE-WRITTEN.  15/12/2008.                                               
      *************************************************************             
      *                SYSTEM OF COMMITMENT (BP13)                *             
      * ========================================================= *             
      *  PROGRAM : BP13C54D                                       *             
      *                                                           *             
      *  INPUT   : BP13K205                                       *             
      *            BP13K757                                       *             
      *            BP13K730                                       *             
      *            BP13K595                                       *             
      *                                                           *             
      *  OUTPUT  : BP13F500                                       *             
      *            BP13L54D                                       *             
      *            BP13L54E                                       *             
      *            BP13L54F                                       *             
      *            BP13L54G                                       *             
      * ========================================================= *             
      *  REVISION HISTORY :                                       *             
      *  CHGE-NO   OIC   DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------  *             
      *  BP133484  TRM1  15/12/2008  NEW PROGRAM                  *             
      *  BP136880  SMR2  11/07/2017  ADD DATE & TIME REQUEST      *             
      *  BP137102  AM25  18/12/2018  CHANGE K205 TO SEQ FILE AND  *             
      *                              REMOVE K205 READ UNTIL       *             
      *  BP139280  EAA2  06/11/2023  EXPAND BP13K757 FROM 3000 TO *             
      *                              4000                         *             
      *************************************************************             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F205     ASSIGN       TO BP13F205.                        
                                                                                
           SELECT BP13K757     ASSIGN       TO BP13K757                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS SEQUENTIAL                       
                               RECORD KEY   IS K757-KEY-FLD                     
                               FILE STATUS  IS WS-K757-STATUS.                  
                                                                                
           SELECT BP13K730     ASSIGN       TO BP13K730                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K730-KEY-NEW                     
                               FILE STATUS  IS WS-K730-STATUS.                  
                                                                                
           SELECT BP13K595     ASSIGN       TO BP13K595                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS DYNAMIC                          
                               RECORD KEY   IS K595-KEY-FLD                     
                               FILE STATUS  IS WS-K595-STATUS.                  
                                                                                
           SELECT BP13F500     ASSIGN       TO BP13F500.                        
           SELECT BP13L54D     ASSIGN       TO BP13L54D.                        
           SELECT BP13L54E     ASSIGN       TO BP13L54E.                        
           SELECT BP13L54F     ASSIGN       TO BP13L54F.                        
           SELECT BP13L54G     ASSIGN       TO BP13L54G.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F205                                                            
            BLOCK              CONTAINS 0 RECORDS                               
            RECORD             CONTAINS 80 CHARACTERS                           
            LABEL              RECORDS  ARE STANDARD                            
            RECORDING          MODE     IS F.                                   
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K757                                                            
            RECORD             CONTAINS 4000 CHARACTERS.                        
       COPY BP13K757.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD             CONTAINS 500 CHARACTERS.                         
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD             CONTAINS 500 CHARACTERS.                         
       COPY BP13K595.                                                           
                                                                                
       FD   BP13F500                                                            
            BLOCK              CONTAINS 0   RECORDS                             
            RECORD             CONTAINS 500 CHARACTERS                          
            LABEL              RECORDS  ARE STANDARD                            
            RECORDING          MODE     IS  F.                                  
       COPY BP13F500.                                                           
                                                                                
       FD   BP13L54D                                                            
            BLOCK              CONTAINS 0   RECORDS                             
            RECORD             CONTAINS 132 CHARACTERS                          
            LABEL              RECORDS  ARE OMITTED                             
            RECORDING          MODE     IS  F.                                  
       01   BP13L54D-REC                PIC X(132).                             
                                                                                
       FD   BP13L54E                                                            
            BLOCK              CONTAINS 0   RECORDS                             
            RECORD             CONTAINS 132 CHARACTERS                          
            LABEL              RECORDS  ARE OMITTED                             
            RECORDING          MODE     IS  F.                                  
       01   BP13L54E-REC                PIC X(132).                             
                                                                                
       FD   BP13L54F                                                            
            BLOCK              CONTAINS 0   RECORDS                             
            RECORD             CONTAINS 132 CHARACTERS                          
            LABEL              RECORDS  ARE OMITTED                             
            RECORDING          MODE     IS  F.                                  
       01   BP13L54F-REC                PIC X(132).                             
                                                                                
       FD   BP13L54G                                                            
            BLOCK              CONTAINS 0   RECORDS                             
            RECORD             CONTAINS 132 CHARACTERS                          
            LABEL              RECORDS  ARE OMITTED                             
            RECORDING          MODE     IS  F.                                  
       01   BP13L54G-REC                PIC X(132).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-START                 PIC 9(8)    VALUE 0.                    
           05  WS-END                   PIC 9(8)    VALUE 0.                    
           05  WS-DATE                  PIC 9(8)    VALUE 0.                    
           05  WS-ZSTART                PIC X(3)    VALUE SPACES.               
           05  WS-ZEND                  PIC X(3)    VALUE SPACES.               
           05  WS-ZONE                  PIC X(3)    VALUE SPACES.               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K757-STATUS           PIC XX      VALUE SPACES.               
           05  WS-K730-STATUS           PIC XX      VALUE SPACES.               
           05  WS-K595-STATUS           PIC XX      VALUE SPACES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-K757-EOF              PIC X       VALUE 'N'.                  
           05  WS-K595-EOF              PIC X       VALUE 'N'.                  
           05  WS-K757-FOUND            PIC X       VALUE 'N'.                  
           05  WS-K730-FOUND            PIC X       VALUE 'N'.                  
           05  WS-K595-FOUND            PIC X       VALUE 'N'.                  
                                                                                
       01  WS-CTRS.                                                             
           05  WS-F205-READ-CTR         PIC 9(8)    VALUE 0.                    
           05  WS-K757-READ-CTR         PIC 9(8)    VALUE 0.                    
           05  WS-K757-FOUND-CTR        PIC 9(8)    VALUE 0.                    
           05  WS-K730-FOUND-CTR        PIC 9(8)    VALUE 0.                    
           05  WS-K730-NOTFOUND-CTR     PIC 9(8)    VALUE 0.                    
           05  WS-K595-FOUND-CTR        PIC 9(8)    VALUE 0.                    
           05  WS-K595-NOTFOUND-CTR     PIC 9(8)    VALUE 0.                    
           05  WS-F500-WRITE-CTR        PIC 9(8)    VALUE 0.                    
           05  WS-L54D-PAGE             PIC 9(5)    VALUE 0.                    
           05  WS-L54E-PAGE             PIC 9(5)    VALUE 0.                    
           05  WS-L54F-PAGE             PIC 9(5)    VALUE 0.                    
           05  WS-L54G-PAGE             PIC 9(5)    VALUE 0.                    
           05  WS-L54D-LINE             PIC 9(2)    VALUE 60.                   
           05  WS-L54E-LINE             PIC 9(2)    VALUE 60.                   
           05  WS-L54F-LINE             PIC 9(2)    VALUE 60.                   
           05  WS-L54G-LINE             PIC 9(2)    VALUE 60.                   
           05  WS-L54D-SN               PIC 9(5)    VALUE 0.                    
           05  WS-L54E-SN               PIC 9(5)    VALUE 0.                    
           05  WS-L54F-SN               PIC 9(5)    VALUE 0.                    
           05  WS-L54G-SN               PIC 9(5)    VALUE 0.                    
           05  WS-L54D-CTR              PIC 9(8)    VALUE 0.                    
           05  WS-L54E-CTR              PIC 9(8)    VALUE 0.                    
           05  WS-L54F-CTR              PIC 9(8)    VALUE 0.                    
           05  WS-L54G-CTR              PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC                PIC 99      VALUE 0.                    
           05 WS-CURR-YY                PIC 99      VALUE 0.                    
           05 WS-CURR-MM                PIC 99      VALUE 0.                    
           05 WS-CURR-DD                PIC 99      VALUE 0.                    
                                                                                
       01  WS-H0.                                                               
           05  WS-H0-REPORT             PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE 'HDB3'.               
           05  FILLER                   PIC X(21)   VALUE SPACES.               
           05  FILLER                   PIC X(39)   VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(16)   VALUE SPACES.               
           05  FILLER                   PIC X(6)    VALUE 'DATE: '.             
           05  WS-H0-CURR-DD            PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X       VALUE '/'.                  
           05  WS-H0-CURR-MM            PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X       VALUE '/'.                  
           05  WS-H0-CURR-CC            PIC X(2)    VALUE SPACES.               
           05  WS-H0-CURR-YY            PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(6)    VALUE 'PAGE: '.             
           05  WS-H0-PAGE               PIC ZZZZ9   VALUE SPACES.               
                                                                                
       01  WS-BP13L54D.                                                         
           05  FILLER                   PIC X(40)   VALUE SPACES.               
           05  FILLER                   PIC X(42)   VALUE                       
               'LIST OF APPLICATIONS FOUND IN REQUEST FILE'.                    
           05  FILLER                   PIC X(50)   VALUE SPACES.               
                                                                                
       01  WS-BP13L54E.                                                         
           05  FILLER                   PIC X(34)   VALUE SPACES.               
           05  FILLER                   PIC X(53)   VALUE                       
               'LIST OF CANCELLED APPLICATIONS                       '.         
           05  FILLER                   PIC X(45)   VALUE SPACES.               
                                                                                
       01  WS-BP13L54F.                                                         
           05  FILLER                   PIC X(34)   VALUE SPACES.               
           05  FILLER                   PIC X(53)   VALUE                       
               'LIST OF CANCELLED APPLICATIONS INCLUDED IN BALLOT    '.         
           05  FILLER                   PIC X(45)   VALUE SPACES.               
                                                                                
       01  WS-BP13L54G.                                                         
           05  FILLER                   PIC X(34)   VALUE SPACES.               
           05  FILLER                   PIC X(53)   VALUE                       
               'LIST OF CANCELLED APPLICATIONS NOT INCLUDED IN BALLOT'.         
           05  FILLER                   PIC X(45)   VALUE SPACES.               
                                                                                
       01  WS-H1                        PIC X(132)  VALUE SPACES.               
                                                                                
       01  WS-H2.                                                               
           05  FILLER                   PIC X(15)   VALUE                       
               'SALES MODE   : '.                                               
           05  WS-H2-MODE               PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(114)  VALUE SPACES.               
                                                                                
       01  WS-H3.                                                               
           05  FILLER                   PIC X(15)   VALUE                       
               'BALLOT DATE  : '.                                               
           05  WS-H3-BALLOT             PIC X(6)    VALUE SPACES.               
           05  FILLER                   PIC X(111)  VALUE SPACES.               
                                                                                
       01  WS-H4.                                                               
           05  FILLER                   PIC X(44)   VALUE                       
               'S/N      REGN NO    HA1 NRIC   NEW      FLAT'.                  
           05  FILLER                   PIC X(88)   VALUE SPACES.               
                                                                                
       01  WS-H4A.                                                              
           05  FILLER                   PIC X(44)   VALUE                       
               'S/N      REGN NO    HA1 NRIC   NEW      FLAT'.                  
           05  FILLER                   PIC X(20)   VALUE                       
               '   REQUEST   REQUEST'.                                          
           05  FILLER                   PIC X(68)   VALUE SPACES.               
                                                                                
       01  WS-H5.                                                               
           05  FILLER                   PIC X(31)   VALUE SPACES.               
           05  FILLER                   PIC X(13)   VALUE                       
               'TOWN     TYPE'.                                                 
           05  FILLER                   PIC X(88)   VALUE SPACES.               
                                                                                
       01  WS-H5A.                                                              
           05  FILLER                   PIC X(31)   VALUE SPACES.               
           05  FILLER                   PIC X(30)   VALUE                       
               'TOWN     TYPE   DATE      TIME'.                                
           05  FILLER                   PIC X(71)   VALUE SPACES.               
                                                                                
       01  WS-DETAIL.                                                           
           05  WS-SN                    PIC X(5)    VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  WS-REGNO                 PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  WS-NRIC                  PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC XX      VALUE SPACES.               
           05  WS-NT                    PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(7)    VALUE SPACES.               
           05  WS-FT                    PIC XX      VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  WS-DTE-REQ               PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  WS-TME-REQ               PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(67)   VALUE SPACES.               
                                                                                
       01  WS-BLANK                     PIC X(132)  VALUE SPACES.               
                                                                                
       PROCEDURE DIVISION.                                                      
      ************                                                              
       0000-MAIN.                                                               
      ************                                                              
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-MAIN-ROUTINE    THRU 2000-EXIT                          
             UNTIL WS-K757-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
           OPEN INPUT  BP13F205                                                 
                       BP13K757                                                 
                       BP13K730                                                 
                       BP13K595                                                 
                OUTPUT BP13F500                                                 
                       BP13L54D                                                 
                       BP13L54E                                                 
                       BP13L54F                                                 
                       BP13L54G.                                                
                                                                                
           IF WS-K757-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K757: STATUS - '                    
              WS-K757-STATUS                                                    
              MOVE WS-K757-STATUS         TO RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K730: STATUS - '                    
              WS-K730-STATUS                                                    
              MOVE WS-K730-STATUS         TO RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           IF WS-K595-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K595: STATUS - '                    
              WS-K595-STATUS                                                    
              MOVE WS-K595-STATUS         TO RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE SPACES                    TO F205-REC                           
                                             BP13K757-REC                       
                                             BP13K730-REC                       
                                             BP13K595-REC                       
                                             BP13F500-REC                       
                                             BP13L54D-REC                       
                                             BP13L54E-REC                       
                                             BP13L54F-REC                       
                                             BP13L54G-REC.                      
                                                                                
           INITIALIZE                        F205-REC                           
                                             BP13K757-REC                       
                                             BP13K730-REC                       
                                             BP13K595-REC                       
                                             BP13F500-REC                       
                                             BP13L54D-REC                       
                                             BP13L54E-REC                       
                                             BP13L54F-REC                       
                                             BP13L54G-REC.                      
                                                                                
           MOVE FUNCTION CURRENT-DATE     TO WS-CURR-DATE.                      
           MOVE WS-CURR-DD                TO WS-H0-CURR-DD.                     
           MOVE WS-CURR-MM                TO WS-H0-CURR-MM.                     
           MOVE WS-CURR-CC                TO WS-H0-CURR-CC.                     
           MOVE WS-CURR-YY                TO WS-H0-CURR-YY.                     
                                                                                
           PERFORM 3000-READ-BP13F205   THRU 3000-EXIT.                         
           MOVE F205-NUM-SELECTION        TO WS-H2-MODE.                        
           MOVE F205-DTE-BALLOT           TO WS-H3-BALLOT.                      
           PERFORM 4000-READ-BP13K757   THRU 4000-EXIT.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       2000-MAIN-ROUTINE.                                                       
      ********************                                                      
           MOVE 'N'                       TO WS-K757-FOUND.                     
           IF F205-NUM-SELECTION = K757-NUM-SALES-MODE AND                      
                 K757-NUM-BALLOT = 'Y'                                          
              MOVE F205-DTE-ALLOCN        TO WS-START                           
              MOVE F205-DTE-ALLOCN-NEW    TO WS-END                             
              MOVE K757-DTE-REQUESTED     TO WS-DATE                            
              IF WS-DATE >= WS-START AND WS-DATE <= WS-END                      
                 IF F205-NUM-SELECTION = 'BTO'                                  
                    MOVE F205-DTE-RESELECTION(1:3)   TO WS-ZSTART               
                    MOVE F205-DTE-RESELECTION(4:3)   TO WS-ZEND                 
                    MOVE K757-NUM-ESTATE-ZONE        TO WS-ZONE                 
                    IF WS-ZONE >= WS-ZSTART AND WS-ZONE <= WS-ZEND              
                       MOVE 'Y'                      TO WS-K757-FOUND           
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'                         TO WS-K757-FOUND           
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-K757-FOUND = 'Y'                                            
                 ADD 1                          TO WS-K757-FOUND-CTR            
                 MOVE 'N'                       TO WS-K730-FOUND                
                 MOVE K757-NUM-REGN             TO K730-NUM-REGN                
                 PERFORM 4100-READ-BP13K730   THRU 4100-EXIT                    
                 IF WS-K730-FOUND = 'Y'                                         
                    ADD 1                          TO WS-K730-FOUND-CTR         
                    MOVE 'BP13L54D'                TO WS-H0-REPORT              
                    PERFORM 6100-WRITE-BP13L54D  THRU 6100-EXIT                 
                    PERFORM 8000-WRITE-BP13F500  THRU 8000-EXIT                 
                 ELSE                                                           
                    ADD 1                       TO WS-K730-NOTFOUND-CTR         
                    MOVE 'BP13L54E'             TO WS-H0-REPORT                 
                    PERFORM 6200-WRITE-BP13L54E THRU 6200-EXIT                  
                    MOVE 'N'                       TO WS-K595-FOUND             
                                                      WS-K595-EOF               
                    MOVE LOW-VALUES                TO K595-KEY-FLD              
                    MOVE K757-NUM-REGN             TO K595-NUM-REGN-REF         
                    PERFORM 5100-START-BP13K595  THRU 5100-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           PERFORM 4000-READ-BP13K757        THRU 4000-EXIT.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3000-READ-BP13F205.                                                      
      *********************                                                     
           READ BP13F205                                                        
             AT END                                                             
             PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                          
           END-READ.                                                            
                                                                                
           ADD 1                          TO WS-F205-READ-CTR.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K757.                                                      
      *********************                                                     
           READ BP13K757                                                        
             AT END MOVE 'Y'              TO WS-K757-EOF                        
             GO TO 4000-EXIT                                                    
           END-READ.                                                            
                                                                                
           ADD 1                          TO WS-K757-READ-CTR.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4100-READ-BP13K730.                                                      
      *********************                                                     
           READ BP13K730.                                                       
           EVALUATE WS-K730-STATUS                                              
           WHEN '00'                                                            
                MOVE 'Y'                     TO WS-K730-FOUND                   
           WHEN '10'                                                            
           WHEN '23'                                                            
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'BP13K730 READ ERROR: STATUS - '                        
                WS-K730-STATUS                                                  
                MOVE WS-K730-STATUS          TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       5100-START-BP13K595.                                                     
      **********************                                                    
           START BP13K595 KEY >= K595-KEY-FLD.                                  
           EVALUATE WS-K595-STATUS                                              
           WHEN '00'                                                            
                PERFORM 5200-READNEXT-BP13K595   THRU 5200-EXIT                 
                  UNTIL WS-K595-EOF = 'Y' OR                                    
                        K595-NUM-REGN-REF NOT = K757-NUM-REGN                   
           WHEN '10'                                                            
           WHEN '23'                                                            
                ADD 1                         TO WS-K595-NOTFOUND-CTR           
                MOVE 'BP13L54G'               TO WS-H0-REPORT                   
                PERFORM 6400-WRITE-BP13L54G THRU 6400-EXIT                      
           WHEN OTHER                                                           
                DISPLAY 'BP13K595 START ERROR: STATUS - '                       
                WS-K595-STATUS                                                  
                MOVE WS-K595-STATUS                TO RETURN-CODE               
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************                                                 
       5200-READNEXT-BP13K595.                                                  
      *************************                                                 
           READ BP13K595 NEXT RECORD.                                           
           EVALUATE WS-K595-STATUS                                              
           WHEN '00'                                                            
                IF K595-NUM-REGN-REF(1:8) = K757-NUM-REGN                       
                   IF K595-CDE-REQUEST-STATUS = 'C'                             
                      IF WS-K595-FOUND NOT = 'Y'                                
                         MOVE 'Y'                 TO WS-K595-FOUND              
                         ADD 1                    TO WS-K595-FOUND-CTR          
                         MOVE 'BP13L54F'               TO WS-H0-REPORT          
                         PERFORM 6300-WRITE-BP13L54F THRU 6300-EXIT             
                         PERFORM 8000-WRITE-BP13F500 THRU 8000-EXIT             
                      END-IF                                                    
                   END-IF                                                       
                ELSE                                                            
                   IF WS-K595-FOUND NOT = 'Y'                                   
                      ADD 1                     TO WS-K595-NOTFOUND-CTR         
                      MOVE 'BP13L54G'               TO WS-H0-REPORT             
                      PERFORM 6400-WRITE-BP13L54G THRU 6400-EXIT                
                   END-IF                                                       
                END-IF                                                          
           WHEN '10'                                                            
           WHEN '23'                                                            
                MOVE 'Y'                      TO WS-K595-EOF                    
                IF WS-K595-FOUND NOT = 'Y'                                      
                   ADD 1                        TO WS-K595-NOTFOUND-CTR         
                   MOVE 'BP13L54G'               TO WS-H0-REPORT                
                   PERFORM 6400-WRITE-BP13L54G THRU 6400-EXIT                   
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'ERROR IN READING BP13K595: STATUS - '                  
                WS-K595-STATUS                                                  
                MOVE WS-K595-STATUS          TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       6100-WRITE-BP13L54D.                                                     
      **********************                                                    
           IF WS-L54D-LINE > 55                                                 
              PERFORM 7000-WRITE-HEADER   THRU 7000-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-L54D-SN.                      
           MOVE WS-L54D-SN                  TO WS-SN.                           
                                                                                
           MOVE K730-NUM-REGN               TO WS-REGNO.                        
           MOVE K730-NUM-NRIC1              TO WS-NRIC.                         
           MOVE K730-CDE-NT1                TO WS-NT.                           
           MOVE K730-CDE-FLAT-TYPE          TO WS-FT.                           
           MOVE K757-DTE-REQUESTED          TO WS-DTE-REQ.                      
           MOVE K757-TME-REQUESTED          TO WS-TME-REQ.                      
                                                                                
           WRITE BP13L54D-REC             FROM WS-DETAIL.                       
           ADD 1                            TO WS-L54D-CTR.                     
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       6200-WRITE-BP13L54E.                                                     
      **********************                                                    
           IF WS-L54E-LINE > 55                                                 
              PERFORM 7000-WRITE-HEADER   THRU 7000-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-L54E-SN.                      
           MOVE WS-L54E-SN                  TO WS-SN.                           
                                                                                
           MOVE K757-NUM-REGN               TO WS-REGNO.                        
           MOVE K757-NUM-APPLT1-UIN         TO WS-NRIC.                         
           MOVE K757-NUM-ESTATE-ZONE        TO WS-NT.                           
           MOVE K757-NUM-FLAT-TYPE          TO WS-FT.                           
           MOVE K757-DTE-REQUESTED          TO WS-DTE-REQ.                      
           MOVE K757-TME-REQUESTED          TO WS-TME-REQ.                      
                                                                                
           WRITE BP13L54E-REC             FROM WS-DETAIL.                       
           ADD 1                            TO WS-L54E-CTR.                     
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       6300-WRITE-BP13L54F.                                                     
      **********************                                                    
           IF WS-L54F-LINE > 55                                                 
              PERFORM 7000-WRITE-HEADER   THRU 7000-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-L54F-SN.                      
           MOVE WS-L54F-SN                  TO WS-SN.                           
                                                                                
           MOVE K595-NUM-REGN               TO WS-REGNO.                        
           MOVE K595-NUM-NRIC1              TO WS-NRIC.                         
           MOVE K595-CDE-NT1                TO WS-NT.                           
           MOVE K595-CDE-FLAT-TYPE          TO WS-FT.                           
           MOVE SPACES                      TO WS-DTE-REQ                       
                                               WS-TME-REQ.                      
                                                                                
           WRITE BP13L54F-REC             FROM WS-DETAIL.                       
           ADD 1                            TO WS-L54F-CTR.                     
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       6400-WRITE-BP13L54G.                                                     
      **********************                                                    
           IF WS-L54G-LINE > 55                                                 
              PERFORM 7000-WRITE-HEADER   THRU 7000-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-L54G-SN.                      
           MOVE WS-L54G-SN                  TO WS-SN.                           
                                                                                
           MOVE K757-NUM-REGN               TO WS-REGNO.                        
           MOVE K757-NUM-APPLT1-UIN         TO WS-NRIC.                         
           MOVE K757-NUM-ESTATE-ZONE        TO WS-NT.                           
           MOVE K757-NUM-FLAT-TYPE          TO WS-FT.                           
           MOVE SPACES                      TO WS-DTE-REQ                       
                                               WS-TME-REQ.                      
                                                                                
           WRITE BP13L54G-REC             FROM WS-DETAIL.                       
           ADD 1                            TO WS-L54G-CTR.                     
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       7000-WRITE-HEADER.                                                       
      ********************                                                      
           EVALUATE WS-H0-REPORT                                                
           WHEN 'BP13L54D'                                                      
                ADD 1                     TO WS-L54D-PAGE                       
                MOVE WS-L54D-PAGE         TO WS-H0-PAGE                         
                MOVE WS-BP13L54D          TO WS-H1                              
                WRITE BP13L54D-REC      FROM WS-H0 AFTER PAGE                   
                WRITE BP13L54D-REC      FROM WS-BLANK                           
                WRITE BP13L54D-REC      FROM WS-H1                              
                WRITE BP13L54D-REC      FROM WS-BLANK                           
                WRITE BP13L54D-REC      FROM WS-H2                              
                WRITE BP13L54D-REC      FROM WS-H3                              
                WRITE BP13L54D-REC      FROM WS-BLANK                           
                WRITE BP13L54D-REC      FROM WS-H4A                             
                WRITE BP13L54D-REC      FROM WS-H5A                             
                MOVE 9                    TO WS-L54D-LINE                       
                                                                                
           WHEN 'BP13L54E'                                                      
                ADD 1                     TO WS-L54E-PAGE                       
                MOVE WS-L54E-PAGE         TO WS-H0-PAGE                         
                MOVE WS-BP13L54E          TO WS-H1                              
                WRITE BP13L54E-REC      FROM WS-H0 AFTER PAGE                   
                WRITE BP13L54E-REC      FROM WS-BLANK                           
                WRITE BP13L54E-REC      FROM WS-H1                              
                WRITE BP13L54E-REC      FROM WS-BLANK                           
                WRITE BP13L54E-REC      FROM WS-H2                              
                WRITE BP13L54E-REC      FROM WS-H3                              
                WRITE BP13L54E-REC      FROM WS-BLANK                           
                WRITE BP13L54E-REC      FROM WS-H4A                             
                WRITE BP13L54E-REC      FROM WS-H5A                             
                MOVE 9                    TO WS-L54E-LINE                       
                                                                                
           WHEN 'BP13L54F'                                                      
                ADD 1                     TO WS-L54F-PAGE                       
                MOVE WS-L54F-PAGE         TO WS-H0-PAGE                         
                MOVE WS-BP13L54F          TO WS-H1                              
                WRITE BP13L54F-REC      FROM WS-H0 AFTER PAGE                   
                WRITE BP13L54F-REC      FROM WS-BLANK                           
                WRITE BP13L54F-REC      FROM WS-H1                              
                WRITE BP13L54F-REC      FROM WS-BLANK                           
                WRITE BP13L54F-REC      FROM WS-H2                              
                WRITE BP13L54F-REC      FROM WS-H3                              
                WRITE BP13L54F-REC      FROM WS-BLANK                           
                WRITE BP13L54F-REC      FROM WS-H4                              
                WRITE BP13L54F-REC      FROM WS-H5                              
                MOVE 9                    TO WS-L54F-LINE                       
                                                                                
           WHEN 'BP13L54G'                                                      
                ADD 1                     TO WS-L54G-PAGE                       
                MOVE WS-L54G-PAGE         TO WS-H0-PAGE                         
                MOVE WS-BP13L54G          TO WS-H1                              
                WRITE BP13L54G-REC      FROM WS-H0 AFTER PAGE                   
                WRITE BP13L54G-REC      FROM WS-BLANK                           
                WRITE BP13L54G-REC      FROM WS-H1                              
                WRITE BP13L54G-REC      FROM WS-BLANK                           
                WRITE BP13L54G-REC      FROM WS-H2                              
                WRITE BP13L54G-REC      FROM WS-H3                              
                WRITE BP13L54G-REC      FROM WS-BLANK                           
                WRITE BP13L54G-REC      FROM WS-H4                              
                WRITE BP13L54G-REC      FROM WS-H5                              
                MOVE 9                    TO WS-L54G-LINE                       
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13F500.                                                     
      **********************                                                    
           IF WS-K730-FOUND = 'Y'                                               
              WRITE BP13F500-REC        FROM BP13K730-REC                       
           ELSE                                                                 
              WRITE BP13F500-REC        FROM BP13K595-REC                       
           END-IF.                                                              
                                                                                
           ADD 1                          TO WS-F500-WRITE-CTR.                 
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       9000-CLOSE-FILES.                                                        
      *****************                                                         
           CLOSE BP13F205                                                       
                 BP13K757                                                       
                 BP13K730                                                       
                 BP13K595                                                       
                 BP13F500                                                       
                 BP13L54D                                                       
                 BP13L54E                                                       
                 BP13L54F                                                       
                 BP13L54G.                                                      
                                                                                
           IF WS-K757-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K757: STATUS - '                    
              WS-K757-STATUS                                                    
              MOVE WS-K757-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K730: STATUS - '                    
              WS-K730-STATUS                                                    
              MOVE WS-K730-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-K595-STATUS NOT = '00'                                         
              DISPLAY 'ERROR IN CLOSING BP13K595: STATUS - '                    
              WS-K595-STATUS                                                    
              MOVE WS-K595-STATUS         TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           DISPLAY '**********************************************'.            
           DISPLAY '              PROGRAM : BP13C54D              '.            
           DISPLAY '**********************************************'.            
           DISPLAY ' NO. OF BP13F205 RECORDS READ      : '                      
                     WS-F205-READ-CTR.                                          
           DISPLAY ' NO. OF BP13K757 RECORDS READ      : '                      
                     WS-K757-READ-CTR.                                          
           DISPLAY ' NO. OF BP13K757 RECORDS FOUND     : '                      
                     WS-K757-FOUND-CTR.                                         
           DISPLAY ' NO. OF BP13K730 RECORDS FOUND     : '                      
                     WS-K730-FOUND-CTR.                                         
           DISPLAY ' NO. OF BP13K730 RECORDS NOT FOUND : '                      
                     WS-K730-NOTFOUND-CTR.                                      
           DISPLAY ' NO. OF BP13K595 RECORDS FOUND     : '                      
                     WS-K595-FOUND-CTR.                                         
           DISPLAY ' NO. OF BP13K595 RECORDS NOT FOUND : '                      
                     WS-K595-NOTFOUND-CTR.                                      
           DISPLAY ' BP13F500 RECORDS WRITTEN          : '                      
                     WS-F500-WRITE-CTR.                                         
           DISPLAY ' BP13L54D RECORDS WRITTEN          : '                      
                     WS-L54D-CTR.                                               
           DISPLAY ' BP13L54E RECORDS WRITTEN          : '                      
                     WS-L54E-CTR.                                               
           DISPLAY ' BP13L54F RECORDS WRITTEN          : '                      
                     WS-L54F-CTR.                                               
           DISPLAY ' BP13L54G RECORDS WRITTEN          : '                      
                     WS-L54G-CTR.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
