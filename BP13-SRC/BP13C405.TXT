       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.            BP13C405.                                         
       AUTHOR.                ROMAR ANDREW S MARALIT.                           
      *DATE-WRITTEN.          11/02/1999.                                       
                                                                                
      ******************************************************************        
      *                    SYSTEM OF COMMITMENT (BP13)                 *        
      *================================================================*        
      *  OBJECTIVE:  1.  TO PRINT DAILY AUDIT LOG FOR REJECTION OF     *        
      *                  SALES ORDER                                   *        
      *================================================================*        
      *  FILES DESCRIPTION:-                                           *        
      *                 ACCESS       REC                               *        
      *  DD NAME  I/O/E  MODE        LEN   KEY           REMARKS       *        
      *  -------  ----- ------       ---   ---           -------       *        
      * BP13F310    I   SEQ          150   F310-KEY-FLD  SEQ           *        
      * BP13L405    O   SEQ          132                 REPORT        *        
      *================================================================*        
      *  MODIFICATIONS :                                               *        
      *  CHGE REQ #  DATE          AUTHOR         REMARKS              *        
      *  ----------  ----          ------         -------              *        
      *  BP130632    11/02/1999    RASM           NEW PROGRAM          *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13L405 ASSIGN TO BP13L405.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F310.                                                           
                                                                                
       FD  BP13L405        BLOCK  CONTAINS 0   RECORDS                          
                           RECORD CONTAINS 132 CHARACTERS                       
                           LABEL  RECORD   IS  OMITTED                          
                           DATA   RECORD   IS  BP13L405-REC                     
                           RECORDING MODE IS F.                                 
       01  BP13L405-REC                            PIC X(132).                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05  WS-DTE-CC                           PIC X(2).                    
           05  WS-DTE-CURRENT.                                                  
               10  WS-DTE-YY                       PIC X(2).                    
               10  WS-DTE-MM                       PIC X(2).                    
               10  WS-DTE-DD                       PIC X(2).                    
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  BP13F310-STATUS                     PIC 9(2) VALUE ZEROS.        
           05  WS-F310-EOF                         PIC X    VALUE 'N'.          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-L405-CNT-PAGE                    PIC 9(4) VALUE ZEROS.        
           05  WS-L405-CNT-LINE                    PIC 9(5) VALUE ZEROS.        
           05  WS-L405-CNT-REC                     PIC 9(5) VALUE ZEROS.        
           05  WS-F310-CNT-READ                    PIC 9(5) VALUE ZEROS.        
                                                                                
       01  WS-PAGE-HEADING1.                                                    
           05  FILLER                              PIC X(14)                    
               VALUE 'BP13L405'.                                                
           05  FILLER                              PIC X(42)                    
               VALUE 'HDB3'.                                                    
           05  FILLER                              PIC X(44)                    
               VALUE 'SYSTEM OF COMMITMENT'.                                    
           05  FILLER                              PIC X(7)                     
               VALUE 'DATE : '.                                                 
           05  L405-DTE-HDR-DD                     PIC X(2).                    
           05  FILLER                              PIC X    VALUE '/'.          
           05  L405-DTE-HDR-MM                     PIC X(2).                    
           05  FILLER                              PIC X    VALUE '/'.          
           05  L405-DTE-HDR-CC                     PIC X(2).                    
           05  L405-DTE-HDR-YY                     PIC X(5).                    
           05  FILLER                              PIC X(7)                     
               VALUE 'PAGE : '.                                                 
           05  L405-PAGE-NUM                       PIC ZZZ9.                    
           05  FILLER                              PIC X(2)                     
               VALUE SPACES.                                                    
                                                                                
       01  WS-PAGE-HEADING2.                                                    
           05  FILLER                              PIC X(30)                    
               VALUE SPACES.                                                    
           05  FILLER                              PIC X(103)                   
               VALUE 'DAILY AUDIT LOG FOR REJECTION OF SALES ORDER AFTER        
      -              ' LEGAL DOCS PRINTED'.                                     
                                                                                
       01  WS-PAGE-HEADING3.                                                    
           05  FILLER                              PIC X(50)                    
               VALUE SPACES.                                                    
           05  FILLER                              PIC X(83)                    
               VALUE '(SALES UNIT - AGREEMENT TEAM)'.                           
                                                                                
       01  WS-PAGE-COL-HEADING.                                                 
           05  FILLER                              PIC X(10)                    
               VALUE SPACES.                                                    
           05  FILLER                              PIC X(20)                    
               VALUE 'S/NO'.                                                    
           05  FILLER                              PIC X(23)                    
               VALUE 'REGN NO'.                                                 
           05  FILLER                              PIC X(26)                    
               VALUE 'SCHEME ACCT'.                                             
           05  FILLER                              PIC X(28)                    
               VALUE 'DATE REJECTED'.                                           
           05  FILLER                              PIC X(26)                    
               VALUE 'USER-ID'.                                                 
                                                                                
       01  WS-HORIZONTAL-LINE.                                                  
           05  FILLER                              PIC X(132)                   
               VALUE ALL '-'.                                                   
                                                                                
       01  WS-PAGE-DATA-LINE.                                                   
           05  FILLER                              PIC X(10)                    
               VALUE SPACES.                                                    
           05  L405-NUM-SERIAL                     PIC ZZZZZ.                   
           05  FILLER                              PIC X(15)                    
               VALUE SPACES.                                                    
           05  L405-NUM-REGN                       PIC X(8).                    
           05  FILLER                              PIC X(15)                    
               VALUE SPACES.                                                    
           05  L405-NUM-SCHEME-ACCT                PIC X(11).                   
           05  FILLER                              PIC X(15)                    
               VALUE SPACES.                                                    
           05  L405-DTE-REJ-DD                     PIC X(2).                    
           05  L405-DTE-SEP1                       PIC X.                       
           05  L405-DTE-REJ-MM                     PIC X(2).                    
           05  L405-DTE-SEP2                       PIC X.                       
           05  L405-DTE-REJ-CCYY                   PIC X(4).                    
           05  FILLER                              PIC X(18)                    
               VALUE SPACES.                                                    
           05  L405-NUM-USERID                     PIC X(8).                    
           05  FILLER                              PIC X(18)                    
               VALUE SPACES.                                                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-MAIN-ROUTINE THRU 2000-EXIT.                            
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F310                                                 
                OUTPUT BP13L405.                                                
                                                                                
           ACCEPT WS-DTE-CCYYMMDD FROM DATE YYYYMMDD.                           
                                                                                
           MOVE WS-DTE-DD TO L405-DTE-HDR-DD.                                   
           MOVE WS-DTE-MM TO L405-DTE-HDR-MM.                                   
           MOVE WS-DTE-CC TO L405-DTE-HDR-CC.                                   
           MOVE WS-DTE-YY TO L405-DTE-HDR-YY.                                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM 3000-PRINT-HEADERS THRU 3000-EXIT.                           
                                                                                
           READ BP13F310 AT END                                                 
                         MOVE 'Y' TO WS-F310-EOF                                
                         GO TO 2000-EXIT                                        
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F310-CNT-READ.                                           
                                                                                
           PERFORM 4000-PRINT-DETAILS THRU 4000-EXIT                            
                   UNTIL (WS-F310-EOF = 'Y').                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PRINT-HEADERS.                                                      
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  PRINTS THE PAGE HEADER.                                       *        
      *----------------------------------------------------------------*        
           ADD  1                TO WS-L405-CNT-PAGE.                           
           MOVE WS-L405-CNT-PAGE TO L405-PAGE-NUM.                              
           MOVE 1                TO WS-L405-CNT-LINE.                           
                                                                                
           WRITE BP13L405-REC    FROM WS-PAGE-HEADING1                          
                 AFTER ADVANCING PAGE.                                          
           WRITE BP13L405-REC    FROM WS-PAGE-HEADING2                          
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L405-REC    FROM WS-PAGE-HEADING3                          
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L405-REC    FROM WS-PAGE-COL-HEADING                       
                 AFTER ADVANCING 2 LINES.                                       
           WRITE BP13L405-REC    FROM WS-HORIZONTAL-LINE                        
                 AFTER ADVANCING 1 LINE.                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-PRINT-DETAILS.                                                      
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  READ THE SEQUENTIAL FILE THEN PRINT THE RECORD.               *        
      *----------------------------------------------------------------*        
           MOVE SPACES                  TO WS-PAGE-DATA-LINE.                   
           ADD  1                       TO WS-L405-CNT-REC.                     
           MOVE WS-L405-CNT-REC         TO L405-NUM-SERIAL.                     
           MOVE F310-REGN-NO            TO L405-NUM-REGN.                       
           MOVE F310-SCH-ACC            TO L405-NUM-SCHEME-ACCT.                
           MOVE F310-DTE-UPDATE(7:2)    TO L405-DTE-REJ-DD.                     
           MOVE F310-DTE-UPDATE(5:2)    TO L405-DTE-REJ-MM.                     
           MOVE F310-DTE-UPDATE(1:4)    TO L405-DTE-REJ-CCYY.                   
           MOVE F310-USERID             TO L405-NUM-USERID.                     
                                                                                
           MOVE '/'                     TO L405-DTE-SEP1                        
                                           L405-DTE-SEP2.                       
                                                                                
           WRITE BP13L405-REC FROM WS-PAGE-DATA-LINE                            
                 AFTER ADVANCING 1 LINE.                                        
                                                                                
           ADD 1                   TO WS-L405-CNT-LINE.                         
                                                                                
           IF WS-L405-CNT-LINE > 50                                             
              WRITE BP13L405-REC FROM WS-HORIZONTAL-LINE                        
                    AFTER ADVANCING 1 LINE                                      
              PERFORM 3000-PRINT-HEADERS THRU 3000-EXIT                         
           END-IF.                                                              
                                                                                
           READ BP13F310 AT END                                                 
                        MOVE 'Y' TO WS-F310-EOF                                 
                        GO TO 4000-EXIT                                         
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F310-CNT-READ.                                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
                                                                                
           WRITE BP13L405-REC FROM WS-HORIZONTAL-LINE                           
                 AFTER ADVANCING 1 LINE                                         
                                                                                
           DISPLAY '                                  '.                        
           DISPLAY ' *****  BP13C405  *****           '.                        
           DISPLAY '                                  '.                        
           DISPLAY ' NO. OF REC READ FROM F310  = ' WS-F310-CNT-READ.           
           DISPLAY ' NO. OF REC PRINTED         = ' WS-L405-CNT-REC.            
                                                                                
           CLOSE BP13F310                                                       
                 BP13L405.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
      * >>>>>>>>>>>>>>>>>>>>>>> END OF BP13C405 <<<<<<<<<<<<<<<<<<<<<< *        
      *----------------------------------------------------------------*        
