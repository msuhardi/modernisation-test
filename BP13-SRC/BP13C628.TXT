       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C628.                                                    
       AUTHOR. KRISTINE LANTIN.                                                 
       DATE-WRITTEN. 18-11-2016.                                                
                                                                                
      *---------------------------------------------------------------*         
      * SOC (BP13)                                                    *         
      *---------------------------------------------------------------*         
      * PROGRAM   : BP13C628                                          *         
      * AUTHOR    : KVL                                               *         
      * OBJECTIVE : TO PRINT REPORT ON REFUND STAMP DUTY BATCH NOT    *         
      *             PROCESSED BY IRAS AFTER 7 DAYS.                   *         
      *                                                               *         
      *  FILES :                                                      *         
      *  INPUT  - BP13F62A                                            *         
      *  OUTPUT - BP13L628                                            *         
      *                                                               *         
      *----------------------------------------------------------------         
      *                                                               *         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---  -----------                            *         
      * BP136508 18112016 KVL1 NEW PROGRAM                            *         
      * BP136538 19122016 KVL1 REMOVE WRITE AFTER SYNTAX THAT CREATES *         
      *                        AN FBA OUTPUT FILE ATTRIBUTE           *         
      * BP136560 28122016 KVL1 TO INCLUDE ALEX LIM IN THE RECIPIENTS  *         
      * BP136851 20062017 PCL4 TO UPDATE EMAIL RECIPIENTS             *         
      * BP137050 10112017 PCL4 TO UPDATE EMAIL RECIPIENTS             *         
      * BP137155 08012018 PCL4 TO UPDATE EMAIL RECIPIENTS             *         
      * BP138757 23072021 KV8  TO REPLACE KATHIJAH TO SUZANNE         *         
      * BP139175 12092022 KAC1 ADDED JIALING HE AND KATHIJAH BEE ALI M*         
      *----------------------------------------------------------------         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F62A ASSIGN TO BP13F62A.                                  
                                                                                
           SELECT BP13L628 ASSIGN TO BP13L628.                                  
                                                                                
                                                                                
      *---------------------*                                                   
       DATA DIVISION.                                                           
      *---------------------*                                                   
       FILE SECTION.                                                            
                                                                                
       FD BP13F62A                                                              
            RECORD CONTAINS 1200 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F62A.                                                           
                                                                                
       FD BP13L628                                                              
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01  BP13L628-REC             PIC X(132).                                 
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-EOF-VARIABLES.                                                    
           05  WS-F62A-EOF            PIC X VALUE 'N'.                          
           05  WS-PRT-FLAG            PIC X VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F62A-READ           PIC 9(08) VALUE ZEROES.                   
           05  WS-L628-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-LINE-CTR            PIC 9(02) VALUE 99.                       
           05  WS-PAGE-CTR            PIC 9(04) VALUE ZEROES.                   
           05  WS-SLNO                PIC 9(04) VALUE 1.                        
           05  WS-TOT-REC-PRINTED     PIC 9(04) VALUE ZEROES.                   
                                                                                
       01  WS-DATE                    PIC 9(08) VALUE ZEROES.                   
                                                                                
       01  WS-DATE1.                                                            
           05  WS-CCYY                PIC X(04).                                
           05  WS-MM                  PIC X(02).                                
           05  WS-DD                  PIC X(02).                                
                                                                                
       01  WS-DATE-SENT.                                                        
           05  WS-CCYY-S              PIC X(04).                                
           05  WS-MM-S                PIC X(02).                                
           05  WS-DD-S                PIC X(02).                                
                                                                                
       01 WS-CUR-DATE.                                                          
           05  WS-CUR-CCYY            PIC 9(04).                                
           05  WS-CUR-MM              PIC 9(02).                                
           05  WS-CUR-DD              PIC 9(02).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-REGN            PIC X(13) VALUE SPACES.                   
           05  WS-HDB-REF             PIC X(11) VALUE SPACES.                   
                                                                                
       01  WS-EMAIL-RPT-VARIABLES.                                              
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(60)                               
               VALUE 'RCPT TO:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.         
           05  WS-MAIL-RPT4             PIC X(60) VALUE                         
              'RCPT TO:<NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@HDB.GOV.SG>'.        
           05  WS-MAIL-RPT5             PIC X(60)                               
               VALUE 'RCPT TO:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.            
           05  WS-MAIL-RPT6             PIC X(40)                               
               VALUE 'RCPT TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT6A            PIC X(60)                               
      *        VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.           
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
           05  WS-MAIL-RPT6B            PIC X(60)                               
               VALUE 'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT6C            PIC X(60)                               
               VALUE 'RCPT TO:<Kathijah_Bee_ALI_MOHAMED@HDB.GOV.SG>'.           
           05  WS-MAIL-RPT8             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT9             PIC X(40)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT10            PIC X(60)                               
               VALUE 'TO:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.              
           05  WS-MAIL-RPT11            PIC X(60) VALUE                         
              'TO:<NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@HDB.GOV.SG>'.             
           05  WS-MAIL-RPT12            PIC X(60)                               
               VALUE 'TO:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.                 
           05  WS-MAIL-RPT12A           PIC X(60)                               
      *        VALUE 'TO:<Kathijah_Bee_ALI_MOHAMED@HDB.GOV.SG>'.                
               VALUE 'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT13            PIC X(40)                               
               VALUE 'CC:<SERENA_JM_TEH@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT13B           PIC X(40)                               
               VALUE 'CC:<HE_Jialing@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT13C           PIC X(40)                               
               VALUE 'CC:<Kathijah_Bee_ALI_MOHAMED@HDB.GOV.SG>'.                
           05  WS-MAIL-RPT15            PIC X(100) VALUE                        
               'SUBJECT : REPORT ON REFUND STAMP DUTY BATCH NOT PROCESSE        
      -                  'D BY IRAS AFTER 7 DAYS'.                              
                                                                                
       01  WS-L628-HDNG1.                                                       
           05  FILLER                 PIC X(08)  VALUE 'BP13L628'.              
           05  FILLER                 PIC X(04)  VALUE SPACES.                  
           05  FILLER                 PIC X(08)  VALUE 'HDB3'.                  
           05  FILLER                 PIC X(18)  VALUE SPACES.                  
           05  FILLER                 PIC X(39)  VALUE                          
                'S Y S T E M   O F   C O M M I T M E N T'.                      
           05  FILLER                 PIC X(15)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'DATE: '.                
           05  WS-L628-DATE           PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'PAGE: '.                
           05  WS-L628-CNT-PAGE       PIC ZZZZ9  VALUE ZERO.                    
                                                                                
       01  WS-L628-HDNG2.                                                       
           05  FILLER                 PIC X(30) VALUE SPACES.                   
           05  FILLER                 PIC X(68) VALUE                           
              'REPORT ON REFUND STAMP DUTY BATCH NOT PROCESSED BY IRAS A        
      -       'FTER 7 DAYS'.                                                    
           05  FILLER                 PIC X(41) VALUE SPACES.                   
                                                                                
       01  WS-L628-HDNG3.                                                       
           05  FILLER  PIC X(04) VALUE 'S/NO'.                                  
           05  FILLER  PIC X(04) VALUE SPACES.                                  
           05  FILLER  PIC X(21) VALUE 'Date HDB Sent to IRAS'.                 
           05  FILLER  PIC X(08) VALUE SPACES.                                  
           05  FILLER  PIC X(11) VALUE 'Regn Number'.                           
           05  FILLER  PIC X(10) VALUE SPACES.                                  
           05  FILLER  PIC X(07) VALUE 'HDB Ref'.                               
           05  FILLER  PIC X(10) VALUE SPACES.                                  
           05  FILLER  PIC X(16) VALUE 'Total CPF refund'.                      
           05  FILLER  PIC X(04) VALUE SPACES.                                  
           05  FILLER  PIC X(17) VALUE 'Total Cash refund'.                     
           05  FILLER  PIC X(04) VALUE SPACES.                                  
                                                                                
       01  WS-L628-LINE               PIC X(132) VALUE ALL '-'.                 
                                                                                
       01  WS-L628-DET1.                                                        
           05  WS-L628-CNT            PIC ZZZ99  VALUE ZEROES.                  
           05  FILLER                 PIC X(08)  VALUE SPACES.                  
           05  WS-L628-DTE-SENT       PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(16)  VALUE SPACES.                  
           05  WS-L628-NUM-REGN       PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(09)  VALUE SPACES.                  
           05  WS-L628-HDB-REF        PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(03)  VALUE SPACES.                  
           05  WS-L628-CPF-REF        PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99                  
                                      VALUE ZEROES.                             
           05  FILLER                 PIC X(01)  VALUE SPACES.                  
           05  WS-L628-CASH-REF       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99                  
                                      VALUE ZEROES.                             
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
       01  WS-L628-NULL-REPORT.                                                 
           05  FILLER                 PIC X(30)  VALUE SPACES.                  
           05  WS-L628-MESSAGE        PIC X(50)  VALUE SPACES.                  
           05  FILLER                 PIC X(52)  VALUE SPACES.                  
       01  WS-HDNG-LINE               PIC X(132) VALUE ALL '-'.                 
       01  WS-BLANK-LINE              PIC X(132) VALUE SPACES.                  
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *================================================================*        
       0000-CONTROL.                                                            
      *================================================================*        
                                                                                
            PERFORM 1000-OPEN-FILES       THRU  1000-EXIT.                      
                                                                                
            PERFORM 2000-READ-BP13F62A    THRU  2000-EXIT.                      
            PERFORM 3000-PROCESS-BP13F62A THRU  3000-EXIT                       
                    UNTIL WS-F62A-EOF = 'Y'.                                    
                                                                                
            PERFORM 9000-CLOSE-FILES      THRU  9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
            OPEN INPUT     BP13F62A                                             
                 OUTPUT    BP13L628.                                            
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-DATE.                       
            MOVE WS-DATE     TO WS-DATE1.                                       
            STRING WS-DD, '/', WS-MM, '/', WS-CCYY                              
                DELIMITED BY SIZE INTO WS-L628-DATE.                            
                                                                                
            PERFORM 5000-HEADER-EMAIL  THRU 5000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2000-READ-BP13F62A.                                                      
      *================================================================*        
                                                                                
           READ BP13F62A AT END MOVE 'Y' TO WS-F62A-EOF                         
                           GO TO 2000-EXIT.                                     
                                                                                
              ADD 1 TO WS-F62A-READ.                                            
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-BP13F62A.                                                   
      *================================================================*        
                                                                                
           IF WS-LINE-CTR > 30                                                  
              PERFORM 4000-PRINT-HDNG      THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
           ADD 1                           TO WS-LINE-CTR.                      
                                                                                
           MOVE WS-SLNO                    TO WS-L628-CNT.                      
                                                                                
           MOVE F62A-DTE-SENT-TO-IRAS      TO WS-DATE-SENT                      
           STRING WS-DD-S, '/', WS-MM-S, '/', WS-CCYY-S                         
               DELIMITED BY SIZE INTO WS-L628-DTE-SENT.                         
                                                                                
           MOVE F62A-NUM-HDB-REF           TO WS-L628-HDB-REF.                  
           MOVE F62A-NUM-REGN              TO WS-L628-NUM-REGN.                 
           MOVE F62A-AMT-TOTAL-REFUND-CPF  TO WS-L628-CPF-REF.                  
           MOVE F62A-AMT-TOTAL-REFUND-CASH TO WS-L628-CASH-REF.                 
           WRITE BP13L628-REC              FROM WS-L628-DET1.                   
           ADD 1                           TO WS-L628-WRITE.                    
                                                                                
           ADD 1                           TO WS-SLNO.                          
           ADD 1                           TO WS-TOT-REC-PRINTED.               
           PERFORM 2000-READ-BP13F62A      THRU  2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       4000-PRINT-HDNG.                                                         
      *================================================================*        
            ADD   1            TO   WS-PAGE-CTR.                                
            MOVE  WS-PAGE-CTR  TO   WS-L628-CNT-PAGE.                           
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L628-REC FROM WS-L628-HDNG1.                              
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L628-REC FROM WS-L628-HDNG2.                              
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L628-REC FROM WS-L628-HDNG3.                              
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L628-REC FROM WS-HDNG-LINE.                               
            WRITE BP13L628-REC FROM WS-BLANK-LINE.                              
            MOVE  12           TO   WS-LINE-CTR.                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-HEADER-EMAIL.                                                       
      *================================================================*        
                                                                                
            WRITE BP13L628-REC FROM WS-MAIL-RPT1.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT2.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT3.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT4.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT5.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT6.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT6A.                              
            WRITE BP13L628-REC FROM WS-MAIL-RPT6B.                              
            WRITE BP13L628-REC FROM WS-MAIL-RPT6C.                              
            WRITE BP13L628-REC FROM WS-MAIL-RPT8.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT9.                               
            WRITE BP13L628-REC FROM WS-MAIL-RPT10.                              
            WRITE BP13L628-REC FROM WS-MAIL-RPT11.                              
            WRITE BP13L628-REC FROM WS-MAIL-RPT12.                              
            WRITE BP13L628-REC FROM WS-MAIL-RPT12A.                             
            WRITE BP13L628-REC FROM WS-MAIL-RPT13.                              
            WRITE BP13L628-REC FROM WS-MAIL-RPT13B.                             
            WRITE BP13L628-REC FROM WS-MAIL-RPT13C.                             
            WRITE BP13L628-REC FROM WS-MAIL-RPT15.                              
            MOVE WS-L628-DATE    TO WS-DATE-RPT.                                
            WRITE BP13L628-REC FROM WS-DATE-RPT-DTL.                            
                                                                                
            MOVE SPACES TO  BP13L628-REC.                                       
            WRITE   BP13L628-REC.                                               
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           IF WS-L628-WRITE = ZEROES                                            
              PERFORM 4000-PRINT-HDNG   THRU  4000-EXIT                         
              WRITE BP13L628-REC FROM WS-BLANK-LINE                             
              WRITE BP13L628-REC FROM WS-BLANK-LINE                             
              MOVE 'NO RECORDS WITH IRAS SEND DATE > 7 DAYS ' TO                
                 WS-L628-MESSAGE                                                
              WRITE BP13L628-REC       FROM WS-L628-NULL-REPORT                 
           END-IF.                                                              
                                                                                
           CLOSE BP13F62A BP13L628.                                             
                                                                                
           DISPLAY '** BP13C628 **'.                                            
           DISPLAY 'F62A RECORDS READ    : ' WS-F62A-READ.                      
           DISPLAY 'L628 RECORDS WRITTEN : ' WS-L628-WRITE.                     
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
