      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CA16.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  27 AUGUST 2010.                                           
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO CREATES BP13KA30 FROM BP13KA63 AND       *           
      *                 BP13FA10.                                   *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13KA63 -- SORTED INTERFACE PIDB FILE                *           
      *   2.  BP13FA10 -- SORTED PROJECT INFORMATION FILE           *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KA30 -- UNIT INFORMATION FILE                     *           
      *   2.  BP13KD15 -- SOC - DBSS TP FILE                                    
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP13XXXX 27/08/2010 PCL3 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KA30 ASSIGN TO BP13KA30                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA30-KEY-FLD                               
                  FILE STATUS     IS BP13KA30-STATUS.                           
                                                                                
           SELECT BP13KA63 ASSIGN TO BP13KA63                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA63-KEY-FLD                               
                  FILE STATUS     IS BP13KA63-STATUS.                           
                                                                                
           SELECT BP13FA10   ASSIGN       TO BP13FA10.                          
                                                                                
           SELECT BP13KD15 ASSIGN TO BP13KD15                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD15-KEY-FLD                               
                  FILE STATUS     IS BP13KD15-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KA30                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA30.                                                           
                                                                                
       FD   BP13KA63                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA63.                                                           
                                                                                
       FD   BP13FA10                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  800 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FA10.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BP13KD15.                                                           
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
        01 WS-RIGHT-JUST.                                                       
           05 WS-UNIT                PIC X(04)    VALUE SPACES.                 
           05 WS-UNIT1               PIC X(04)    VALUE SPACES.                 
           05 WS-SUB                 PIC 9(01)    VALUE ZEROES.                 
           05 WS-SUB1                PIC 9(01)    VALUE ZEROES.                 
           05 WS-CNT                 PIC 9(01)    VALUE ZEROES.                 
                                                                                
       01  CNT-KD15-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA30-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-FA10-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA63-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-KA30-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-FND           PIC 9(08) VALUE ZEROES.                       
       01  WS-FA10-EOF            PIC X     VALUE SPACE.                        
       01  WS-KA63-EOF            PIC X     VALUE SPACE.                        
       01  BP13KA30-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KA63-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD15-STATUS        PIC 9(02) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13FA10    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-REC      THRU 3000-EXIT                         
                   UNTIL WS-FA10-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FA10                                                 
                       BP13KA63                                                 
                I-O    BP13KA30                                                 
                       BP13KD15.                                                
                                                                                
           IF BP13KA30-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KA30 : ' BP13KA30-STATUS             
              MOVE BP13KA30-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KA63-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KA63 : ' BP13KA63-STATUS             
              MOVE BP13KA63-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD15-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD15 : ' BP13KD15-STATUS             
              MOVE BP13KD15-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FA10.                                                      
      ******************************************************************        
                                                                                
           READ BP13FA10 AT END                                                 
                MOVE HIGH-VALUES TO BP13FA10-REC                                
                MOVE 'Y'         TO WS-FA10-EOF                                 
                              GO TO 2000-EXIT.                                  
           ADD 1                 TO CNT-FA10-READ.                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-REC.                                                        
      ******************************************************************        
                                                                                
           PERFORM 3100-STARTBR-BP13KA63    THRU 3100-EXIT.                     
           PERFORM 2000-READ-BP13FA10       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-STARTBR-BP13KA63.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13KA63-REC.              
           INITIALIZE                                BP13KA63-REC.              
                                                                                
           MOVE 'N'                               TO WS-KA63-EOF.               
                                                                                
           MOVE FA10-NUM-PRJCT                    TO KA63-NUM-PROJECT.          
                                                                                
           START BP13KA63 KEY >= KA63-NUM-PROJECT.                              
                                                                                
           EVALUATE BP13KA63-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3200-READNEXT-BP13KA63 THRU 3200-EXIT               
                    PERFORM 3300-PROCESS-BP13KA63  THRU 3300-EXIT               
                            UNTIL WS-KA63-EOF = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13KA63 : ' BP13KA63-STATUS             
                          ' KEY : ' KA63-NUM-PROJECT                            
                  PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READNEXT-BP13KA63.                                                  
      ******************************************************************        
                                                                                
           READ BP13KA63 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-KA63-EOF.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-PROCESS-BP13KA63.                                                   
      ******************************************************************        
                                                                                
           IF FA10-NUM-PRJCT = KA63-NUM-PROJECT                                 
              ADD 1                                  TO CNT-KA63-READ           
              PERFORM 3400-PROCESS-BP13KA30        THRU 3400-EXIT               
              PERFORM 3600-PROCESS-BP13KD15        THRU 3600-EXIT               
              PERFORM 3200-READNEXT-BP13KA63       THRU 3200-EXIT               
           ELSE                                                                 
              MOVE 'Y'                               TO WS-KA63-EOF             
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-PROCESS-BP13KA30.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                          TO BP13KA30-REC.                
           INITIALIZE                              BP13KA30-REC.                
                                                                                
           IF   KA63-NUM-PROJECT  = 'E04'                                       
           MOVE 'T04'                           TO KA30-NUM-PRJCT               
           ELSE                                                                 
           MOVE KA63-NUM-PROJECT                TO KA30-NUM-PRJCT.              
           MOVE KA63-NUM-BLK                    TO KA30-NUM-BLK.                
           MOVE KA63-NUM-LEVEL                  TO KA30-NUM-LEVEL.              
           MOVE KA63-NUM-SUB-UNIT               TO KA30-NUM-SUB-UNIT.           
           MOVE KA63-NUM-MAIN-UNIT              TO WS-UNIT.                     
                                                                                
           PERFORM 3450-RIGHT-JUSTIFY-TXT     THRU 3450-EXIT.                   
           MOVE WS-UNIT1                        TO KA30-NUM-MAIN-UNIT.          
                                                                                
           READ BP13KA30.                                                       
                                                                                
           IF BP13KA30-STATUS = 00                                              
              ADD 1                                     TO CNT-KA30-FND         
           ELSE                                                                 
              IF BP13KA30-STATUS = 23                                           
                 PERFORM 3500-WRITE-BP13KA30          THRU 3500-EXIT            
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KA30 : ' BP13KA30-STATUS          
                 MOVE BP13KA30-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3450-RIGHT-JUSTIFY-TXT.                                                  
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO WS-UNIT1.                         
           MOVE ZEROES                     TO WS-SUB1.                          
                                                                                
           PERFORM VARYING WS-SUB FROM 4 BY -1 UNTIL WS-SUB < 1                 
             IF WS-UNIT(WS-SUB:1) = SPACES OR LOW-VALUES                        
                ADD 1                      TO WS-SUB1                           
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           IF WS-SUB1 > 0                                                       
              COMPUTE WS-CNT = 4 - WS-SUB1                                      
              COMPUTE WS-SUB = WS-SUB1 + 1                                      
              MOVE SPACES                  TO WS-UNIT1(1:WS-SUB1)               
              MOVE WS-UNIT(1:WS-CNT)       TO WS-UNIT1(WS-SUB:WS-CNT)           
           ELSE                                                                 
              MOVE WS-UNIT                 TO WS-UNIT1                          
           END-IF.                                                              
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-WRITE-BP13KA30.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                          TO BP13KA30-REC.                
           INITIALIZE                              BP13KA30-REC.                
                                                                                
           IF   KA63-NUM-PROJECT  = 'E04'                                       
           MOVE 'T04'                           TO KA30-NUM-PRJCT               
           ELSE                                                                 
           MOVE KA63-NUM-PROJECT                TO KA30-NUM-PRJCT.              
           MOVE KA63-NUM-BLK                    TO KA30-NUM-BLK.                
           MOVE KA63-NUM-LEVEL                  TO KA30-NUM-LEVEL.              
           MOVE WS-UNIT1                        TO KA30-NUM-MAIN-UNIT.          
           MOVE KA63-NUM-SUB-UNIT               TO KA30-NUM-SUB-UNIT.           
           MOVE KA63-CDE-UNIT-TYP               TO KA30-CDE-UNIT-TYP.           
           MOVE KA63-NUM-TOTAL-FLR-AREA         TO KA30-NUM-FLOOR-AREA.         
           MOVE KA63-NUM-SHARE-VAL              TO KA30-NUM-SHARE-VAL.          
           MOVE KA63-NUM-DUAL-KEY               TO KA30-NUM-DUAL-KEY.           
           MOVE 'BP13CA06'                      TO KA30-NUM-USERID.             
           MOVE FUNCTION CURRENT-DATE(1:8)      TO KA30-DTE-UPDATE.             
           MOVE FUNCTION CURRENT-DATE(9:8)      TO KA30-TME-UPDATE.             
                                                                                
           STRING '##' KA30-NUM-LEVEL KA30-NUM-MAIN-UNIT                        
                       DELIMITED BY SIZE INTO KA30-NUM-REGN.                    
                                                                                
           STRING KA30-NUM-BLK KA30-NUM-LEVEL KA30-NUM-MAIN-UNIT                
                       DELIMITED BY SIZE INTO KA30-NUM-SCH-ACC.                 
                                                                                
           WRITE BP13KA30-REC.                                                  
                                                                                
           IF BP13KA30-STATUS = 00 OR 02                                        
              ADD 1                       TO CNT-KA30-WRITE                     
           ELSE                                                                 
              IF BP13KA30-STATUS = 22                                           
                 ADD 1                    TO CNT-KA30-FND                       
              ELSE                                                              
                 DISPLAY 'ERROR WRITING - BP13KA30 : ' BP13KA30-STATUS          
                 MOVE BP13KA30-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-PROCESS-BP13KD15.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES                         TO BP13KD15-REC.                 
           INITIALIZE                             BP13KD15-REC.                 
                                                                                
           MOVE SPACES                         TO KD15-KEY-FLD.                 
           IF   KA30-NUM-PRJCT  = 'E04' OR 'T04'                                
           MOVE 'E04'                           TO KD15-NUM-PRJCT               
           ELSE                                                                 
           MOVE KA30-NUM-PRJCT                 TO KD15-NUM-PRJCT.               
           MOVE KA30-NUM-BLK                   TO KD15-NUM-BLK.                 
           MOVE KA30-NUM-MAIN-UNIT             TO KD15-NUM-MAIN-UNIT.           
           MOVE KA30-NUM-SUB-UNIT              TO KD15-NUM-SUB-UNIT.            
           MOVE KA30-NUM-LEVEL                 TO KD15-NUM-LEVEL.               
                                                                                
           READ BP13KD15.                                                       
                                                                                
           IF BP13KD15-STATUS = 00                                              
              ADD 1                            TO CNT-KD15-FND                  
           ELSE                                                                 
              IF BP13KD15-STATUS = 23                                           
                 PERFORM 3700-MOVE-DATA-BP13KD15      THRU 3700-EXIT            
                 PERFORM 3800-UPDATE-BP13KD15         THRU 3800-EXIT            
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD15 : ' BP13KD15-STATUS          
                 MOVE BP13KD15-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3700-MOVE-DATA-BP13KD15.                                                 
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO BP13KD15-REC.                  
           INITIALIZE                            BP13KD15-REC.                  
                                                                                
           IF   KA30-NUM-PRJCT  = 'E04' OR 'T04'                                
           MOVE 'E04'                           TO KD15-NUM-PRJCT               
           ELSE                                                                 
           MOVE KA30-NUM-PRJCT                TO KD15-NUM-PRJCT.                
           MOVE KA30-NUM-BLK                  TO KD15-NUM-BLK.                  
           MOVE KA30-NUM-MAIN-UNIT            TO KD15-NUM-MAIN-UNIT.            
           MOVE KA30-NUM-SUB-UNIT             TO KD15-NUM-SUB-UNIT.             
           MOVE KA30-NUM-LEVEL                TO KD15-NUM-LEVEL.                
           MOVE KA30-NUM-PRJCT                TO KD15-NUM-PRJCT-KEY1            
           MOVE KD15-NUM-PRJCT                TO KD15-NUM-PRJCT-KEY1            
                                                 KD15-NUM-PRJCT-KEY3.           
           MOVE KA30-NUM-REGN                 TO KD15-NUM-REGN                  
                                                 KD15-NUM-REGN3                 
                                                 KD15-DTE-DBSS-CONFIRM.         
           STRING '###' KA30-NUM-REGN                                           
                          DELIMITED BY SIZE INTO KD15-NUM-SCH-ACC.              
                                                                                
           MOVE KA30-CDE-UNIT-TYP             TO KD15-CDE-UNIT-TYP.             
           MOVE FUNCTION CURRENT-DATE(1:8)    TO KD15-DTE-UPDATE.               
           MOVE FUNCTION CURRENT-DATE(9:8)    TO KD15-TME-UPDATE.               
           MOVE 'BP13CA16'                    TO KD15-NUM-USERID.               
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3800-UPDATE-BP13KD15.                                                    
      ******************************************************************        
                                                                                
           WRITE BP13KD15-REC.                                                  
                                                                                
           IF BP13KD15-STATUS = 00 OR 02                                        
              ADD 1       TO CNT-KD15-WRITE                                     
           ELSE                                                                 
              IF BP13KD15-STATUS = 22                                           
                 ADD 1    TO CNT-KD15-FND                                       
              ELSE                                                              
                 DISPLAY 'ERROR WRITING - BP13KD15 : ' BP13KD15-STATUS          
                 MOVE BP13KD15-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
                                                                                
           DISPLAY '******  BP13CA16 *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13FA10  : ' CNT-FA10-READ.          
           DISPLAY 'NO OF RECS WRITTEN TO BP13KD15  : ' CNT-KD15-WRITE.         
           DISPLAY 'NO OF RECS FOUND   IN BP13KD15  : ' CNT-KD15-FND.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13KA30  : ' CNT-KA30-WRITE.         
           DISPLAY 'NO OF RECS FOUND   IN BP13KA30  : ' CNT-KA30-FND.           
           DISPLAY 'NO OF RECS READ FROM  BP13KA63  : ' CNT-KA63-READ.          
           DISPLAY 'NO OF RECS FOUND   IN BP13KA30  : ' CNT-KA30-FND.           
                                                                                
           CLOSE BP13KA30                                                       
                 BP13KA63                                                       
                 BP13KD15                                                       
                 BP13FA10.                                                      
                                                                                
           IF BP13KA30-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KA30-STATUS ' BP13KA30-STATUS          
            MOVE BP13KA30-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KA63-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KA63-STATUS ' BP13KA63-STATUS          
            MOVE BP13KA63-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD15-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD15-STATUS ' BP13KD15-STATUS          
            MOVE BP13KD15-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
