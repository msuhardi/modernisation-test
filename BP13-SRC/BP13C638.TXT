       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C638.                                                 
      *AUTHOR.        LSB1.                                                     
      *DATE-WRITTEN.  08/09/1997.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *  OBJECTIVE : PROGRAM TO MATCH APPLN FILE BP13F520 WITH      *           
      *                BP13F558 AFTER DETERMINE 10YR CHECK, IF      *           
      *                TAG AS 'NO' THEN OUTPUT RECORD TO P13F558O.  *           
      *                                                             *           
      *    INPUT FILE  : BP13F558                                   *           
      *                : BP13F520                                   *           
      *   OUTPUT FILE  : P13F558O                                   *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG-NO  BY   DATE     DETAILS                               *           
      * -----------------------------                               *           
      * BP130377 LSB 970908 - NEW PGM                               *           
      * BP130455 SSS 980206 - EXPAND F558 RECL FROM 150 TO 400      *           
      * BP130652 NOR 990220 - EXPAND F520 RECL FROM 300 TO 500      *           
      * BP130897 AKT 000803 - EXPAND F558 RECL FROM 400 TO 600      *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558O                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13F558O-REC.                                                        
           05  FILLER                  PIC X(600).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-F520-READ                PIC 9(6)    VALUE ZEROS.              
           05 WS-F558O-WRITE              PIC 9(6)    VALUE ZEROS.              
           05 WS-F558-READ                PIC 9(6)    VALUE ZEROS.              
           05 WS-MR                       PIC 9(6)    VALUE ZEROS.              
           05 WS-NMR-F558                 PIC 9(6)    VALUE ZEROS.              
           05 WS-NMR-F520                 PIC 9(6)    VALUE ZEROS.              
                                                                                
       01  WS-EOF-F520                    PIC 9       VALUE ZERO.               
           88 EOF-F520                    VALUE 1.                              
                                                                                
       01  WS-EOF-F558                    PIC 9       VALUE ZERO.               
           88 EOF-F558                    VALUE 1.                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 210-READ-F520      THRU 210-EXIT.                            
           PERFORM 220-READ-F558      THRU 220-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL F520-NUM-REGN = HIGH-VALUES                                  
               AND F558-NUM-REGN = HIGH-VALUES                                  
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F520                                                 
                       BP13F558                                                 
               OUTPUT  P13F558O.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READ-F520.                                                           
      *-------------------------------------------------------------            
           READ BP13F520 AT END                                                 
                MOVE HIGH-VALUES TO F520-NUM-REGN                               
                GO TO 210-EXIT.                                                 
                                                                                
           ADD  1                       TO WS-F520-READ.                        
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       220-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUES TO F558-NUM-REGN                               
                GO TO 220-EXIT.                                                 
                                                                                
           ADD  1                       TO WS-F558-READ.                        
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RTN.                                                         
      *-------------------------------------------------------------            
           IF F520-NUM-REGN = F558-NUM-REGN                                     
              IF F520-NUM-5YR-OCCUPN = 'N'                                      
                 ADD 1 TO WS-MR                                                 
                 WRITE P13F558O-REC FROM BP13F558-REC                           
              END-IF                                                            
              PERFORM 220-READ-F558   THRU 220-EXIT                             
              PERFORM 210-READ-F520   THRU 210-EXIT                             
           ELSE                                                                 
           IF F520-NUM-REGN < F558-NUM-REGN                                     
              ADD 1 TO WS-NMR-F520                                              
              PERFORM 210-READ-F520   THRU 210-EXIT                             
           ELSE                                                                 
              ADD 1 TO WS-NMR-F558                                              
              PERFORM 220-READ-F558   THRU 220-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           CLOSE BP13F520                                                       
                 BP13F558                                                       
                 P13F558O.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C638 ================*'.              
           DISPLAY '     NO. OF F520 RECORDS READ  = ' WS-F520-READ.            
           DISPLAY '     NO. OF F558 RECORDS READ  = ' WS-F558-READ.            
           DISPLAY '     CASE #MATCH               = ' WS-MR.                   
           DISPLAY '     CASE #NMR F558            = ' WS-NMR-F558.             
           DISPLAY '     CASE #NMR F520            = ' WS-NMR-F520.             
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
