      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C038.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  06 JAN 2004.                                              
      *===============================================================*         
      *                SYSTEM OF COMMITMENT  (BP13)                   *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE :                                                  *         
      *      1. PRINT DEBARMENT REPORT FOR MARRIED CHILD PRIORITY     *         
      *         WITH CDE-DEBAR 'D'/'K'                                *         
      *---------------------------------------------------------------*         
      * CHG REF   BY    DATE        DESCRIPTION                       *         
      * --------  ---   ------      -----------                       *         
      * BP132463  JF4   06/01/2004  NEW PROGRAM                       *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
                                                                                
           SELECT BP13K853  ASSIGN       TO BP13K853                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K853-KEY-FLD                        
                            FILE STATUS  IS BP13K853-STATUS.                    
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS BP13K820-STATUS.                    
                                                                                
           SELECT BP04K010  ASSIGN       TO BP04K010                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K010-PRIME-KEY                      
                            FILE STATUS  IS BP04K010-STATUS.                    
                                                                                
           SELECT BP13L038  ASSIGN       TO BP13L038.                           
                                                                                
           SELECT P13L038A  ASSIGN       TO P13L038A.                           
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS   150 CHARACTERS.                                    
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K853                                                             
           RECORD CONTAINS   250 CHARACTERS.                                    
       COPY BP13K853.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS  2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS   400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD  BP04K010                                                             
           RECORD CONTAINS   300 CHARACTERS.                                    
       COPY P04K010.                                                            
                                                                                
       FD  BP13L038                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS   132 CHARACTERS.                                    
       01  BP13L038-REC      PIC X(132).                                        
                                                                                
       FD  P13L038A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS   132 CHARACTERS.                                    
       01  P13L038A-REC      PIC X(132).                                        
                                                                                
                                                                                
      *--------------------------*                                              
       WORKING-STORAGE SECTION.                                                 
      *--------------------------*                                              
       01 WS-FILE-STATUS-VARS.                                                  
          05 BP13K853-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP13K800-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP13K820-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP04K010-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP13F310-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13F310-EOF        VALUE 'Y'.                                  
          05 BP13K853-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13K853-EOF        VALUE 'Y'.                                  
          05 BP04K010-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP04K010-EOF        VALUE 'Y'.                                  
                                                                                
       01 WS-DATE-VARS.                                                         
          05 WS-CURR-DATE           PIC X(08)  VALUE SPACES.                    
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F310-READ-CNT       PIC 9(08)  VALUE ZEROES.                    
          05 WS-K853-NOT-CASES      PIC 9(08)  VALUE ZEROES.                    
          05 WS-K800-NOT-CASES      PIC 9(08)  VALUE ZEROES.                    
          05 WS-L038-WRITE-CNT      PIC 9(08)  VALUE ZEROES.                    
          05 WS-L038A-WRITE-CNT     PIC 9(08)  VALUE ZEROES.                    
                                                                                
       01 WS-OTHER-VARS.                                                        
          05 WS-CDE-DEBAR           PIC X(02)  VALUE SPACES.                    
          05 WS-NUM-SCH-ACC         PIC X(11)  VALUE SPACES.                    
          05 WS-NUM-NT              PIC X(03)  VALUE SPACES.                    
          05 WS-NUM-FT              PIC X(02)  VALUE SPACES.                    
          05 WS-NUM-QUEUE           PIC X(05)  VALUE SPACES.                    
          05 WS-CTR                 PIC 9(02)  VALUE ZEROES.                    
          05 WS-CTR1                PIC 9(02)  VALUE ZEROES.                    
          05 WS-D-CTR               PIC 9(02)  VALUE ZEROES.                    
          05 WS-K-CTR               PIC 9(02)  VALUE ZEROES.                    
          05 WS-MAX-LINES           PIC 9(02)  VALUE 55.                        
          05 WS-LINE-CNT            PIC 9(02)  VALUE 60.                        
          05 WS-LINE-CNT-A          PIC 9(02)  VALUE 60.                        
          05 WS-PAGE-CNT            PIC 9(04)  VALUE ZEROES.                    
          05 WS-PAGE-CNT-A          PIC 9(04)  VALUE ZEROES.                    
          05 WS-SLNO                PIC 9(04)  VALUE ZEROES.                    
          05 WS-SLNO-A              PIC 9(04)  VALUE ZEROES.                    
          05 WS-EDIT1               PIC Z(07)9.                                 
          05 WS-APPLN-TAG           PIC X(01)  VALUE SPACE.                     
             88 APPLICANTS-FND      VALUE 'Y'.                                  
          05 WS-PARENT-CHILD-TAG    PIC X(01)  VALUE SPACE.                     
             88 PARENT-CHILD-FND    VALUE 'Y'.                                  
                                                                                
       01 WS-APPLN-TABLE.                                                       
          05 WS-APPLN  OCCURS 4 TIMES.                                          
             10 WS-NME-APPLN        PIC X(30)  VALUE SPACES.                    
             10 WS-UIN-APPLN        PIC X(09)  VALUE SPACES.                    
                                                                                
       01 WS-D-OCCUP-TABLE.                                                     
          05 WS-D-OCCUP  OCCURS 10 TIMES.                                       
             10 WS-D-NME-OCCUP      PIC X(30)  VALUE SPACES.                    
             10 WS-D-UIN-OCCUP      PIC X(09)  VALUE SPACES.                    
             10 WS-D-DEB-REF        PIC X(13)  VALUE SPACES.                    
             10 WS-D-REASON-CDE     PIC X(02)  VALUE SPACES.                    
             10 WS-D-EFF-DATE       PIC X(08)  VALUE SPACES.                    
             10 WS-D-DEB-PERIOD     PIC X(03)  VALUE SPACES.                    
                                                                                
       01 WS-K-OCCUP-TABLE.                                                     
          05 WS-K-OCCUP  OCCURS 10 TIMES.                                       
             10 WS-K-NME-OCCUP      PIC X(30)  VALUE SPACES.                    
             10 WS-K-UIN-OCCUP      PIC X(09)  VALUE SPACES.                    
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 WS-REPORT-VARS.                                                       
          05 L038-HEADER-1.                                                     
             10 FILLER              PIC X(08)  VALUE 'BP13L038'.                
             10 FILLER              PIC X(05)  VALUE SPACES .                   
             10 FILLER              PIC X(08)  VALUE 'HDBCAT 3'.                
             10 FILLER              PIC X(22)  VALUE SPACES.                    
             10 FILLER              PIC X(40)  VALUE                            
                'S Y S T E M   O F   C O M M I T M E N T'.                      
             10 FILLER              PIC X(16)  VALUE SPACES.                    
             10 FILLER              PIC X(07)  VALUE 'DATE :'.                  
             10 L038-DATE           PIC X(10)  VALUE SPACES.                    
             10 FILLER              PIC X(04)  VALUE SPACES.                    
             10 FILLER              PIC X(07)  VALUE 'PAGE : '.                 
             10 L038-PAGE           PIC ZZZ9 .                                  
                                                                                
          05 L038A-HEADER-1.                                                    
             10 FILLER              PIC X(08)  VALUE 'P13L038A'.                
             10 FILLER              PIC X(05)  VALUE SPACES .                   
             10 FILLER              PIC X(08)  VALUE 'HDBCAT 3'.                
             10 FILLER              PIC X(22)  VALUE SPACES.                    
             10 FILLER              PIC X(40)  VALUE                            
                'S Y S T E M   O F   C O M M I T M E N T'.                      
             10 FILLER              PIC X(16)  VALUE SPACES.                    
             10 FILLER              PIC X(07)  VALUE 'DATE :'.                  
             10 L038A-DATE          PIC X(10)  VALUE SPACES.                    
             10 FILLER              PIC X(04)  VALUE SPACES.                    
             10 FILLER              PIC X(07)  VALUE 'PAGE : '.                 
             10 L038A-PAGE          PIC ZZZ9 .                                  
                                                                                
          05 L038-HEADER-2.                                                     
             10 FILLER              PIC X(37)  VALUE SPACES .                   
             10 FILLER              PIC X(50)  VALUE                            
                'DEBARMENT REPORT FOR MARRIED CHILD PRIORITY - ''D'''.          
             10 FILLER              PIC X(12)  VALUE SPACES .                   
             10 FILLER              PIC X(30)  VALUE                            
                'SALES UNIT - APPLICATION TEAM '.                               
                                                                                
          05 L038A-HEADER-2.                                                    
             10 FILLER              PIC X(37)  VALUE SPACES .                   
             10 FILLER              PIC X(50)  VALUE                            
                'DEBARMENT REPORT FOR MARRIED CHILD PRIORITY - ''K'''.          
             10 FILLER              PIC X(12)  VALUE SPACES .                   
             10 FILLER              PIC X(30)  VALUE                            
                'SALES UNIT - APPLICATION TEAM '.                               
                                                                                
          05 L038-HEADER-2A.                                                    
             10 FILLER              PIC X(22)  VALUE                            
                'S/NO  NT    FT   QUEUE'.                                       
             10 FILLER              PIC X(110) VALUE SPACES .                   
                                                                                
          05 L038-HEADER-3.                                                     
             10 FILLER              PIC X(06)  VALUE SPACES .                   
             10 FILLER              PIC X(28)  VALUE                            
                'REGN      NAME OF APPLICANTS'.                                 
             10 FILLER              PIC X(11)  VALUE SPACES .                   
             10 FILLER              PIC X(40)  VALUE                            
                'NRIC       NAME OF PARENTS/MARRIED CHILD'.                     
             10 FILLER              PIC X(02)  VALUE SPACES .                   
             10 FILLER              PIC X(46)  VALUE                            
                'I/C NO.  DEB-REF      REASON  EFF-DTE  PERIOD'.                
                                                                                
          05 L038A-HEADER-3.                                                    
             10 FILLER              PIC X(06)  VALUE SPACES .                   
             10 FILLER              PIC X(28)  VALUE                            
                'REGN      NAME OF APPLICANTS'.                                 
             10 FILLER              PIC X(11)  VALUE SPACES .                   
             10 FILLER              PIC X(40)  VALUE                            
                'NRIC       NAME OF PARENTS/MARRIED CHILD'.                     
             10 FILLER              PIC X(02)  VALUE SPACES .                   
             10 FILLER              PIC X(08)  VALUE                            
                'I/C NO. '.                                                     
                                                                                
          05 L038-HEADER-3A.                                                    
             10 FILLER              PIC X(15)  VALUE                            
                '   SCH-ACC-NO'.                                                
             10 FILLER              PIC X(115) VALUE SPACES .                   
                                                                                
          05 L038-DETAIL-0.                                                     
             10 L038-SEQ-NO         PIC ZZ99.                                   
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-NEW-TOWN       PIC X(03)  VALUE SPACES.                    
             10 FILLER              PIC X(03)  VALUE SPACES.                    
             10 L038-FLAT-TYPE      PIC X(02)  VALUE SPACES.                    
             10 FILLER              PIC X(03)  VALUE SPACES.                    
             10 L038-QUEUE          PIC X(05)  VALUE SPACES.                    
             10 FILLER              PIC X(110) VALUE SPACES.                    
                                                                                
          05 L038A-DETAIL-0.                                                    
             10 L038A-SEQ-NO        PIC ZZ99.                                   
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038A-NEW-TOWN      PIC X(03)  VALUE SPACES.                    
             10 FILLER              PIC X(03)  VALUE SPACES.                    
             10 L038A-FLAT-TYPE     PIC X(02)  VALUE SPACES.                    
             10 FILLER              PIC X(03)  VALUE SPACES.                    
             10 L038A-QUEUE         PIC X(05)  VALUE SPACES.                    
             10 FILLER              PIC X(110) VALUE SPACES.                    
                                                                                
          05 L038-DETAIL-1.                                                     
             10 L038-HDB-REF.                                                   
                15 FILLER           PIC X(03).                                  
                15 L038-SCH-ACC     PIC X(11).                                  
             10 L038-SEQ-REGN  REDEFINES L038-HDB-REF.                          
                15 FILLER           PIC X(06).                                  
                15 L038-NUM-REGN    PIC X(08).                                  
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-NME-APPLN      PIC X(27)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-UIN-APPLN      PIC X(09)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-NME-OCCP       PIC X(27)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-NUM-NRIC       PIC X(09)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-DEB-REF-NO     PIC X(13)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-DEB-CODE       PIC X(02)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-EFF-DATE       PIC X(10)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038-PERIOD         PIC X(03)  VALUE SPACES.                    
                                                                                
          05 L038A-DETAIL-1.                                                    
             10 L038A-HDB-REF.                                                  
                15 FILLER           PIC X(03).                                  
                15 L038A-SCH-ACC    PIC X(11).                                  
             10 L038A-SEQ-REGN  REDEFINES L038A-HDB-REF.                        
                15 FILLER           PIC X(06).                                  
                15 L038A-NUM-REGN   PIC X(08).                                  
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038A-NME-APPLN     PIC X(27)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038A-UIN-APPLN     PIC X(09)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038A-NME-OCCP      PIC X(27)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L038A-NUM-NRIC      PIC X(09)  VALUE SPACES.                    
                                                                                
       01 L038-PR-LINE              PIC X(132) VALUE ALL '-'.                   
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F310      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-DATA       THRU 3000-EXIT                       
                                       UNTIL BP13F310-EOF.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F310                                                
                        BP13K853                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP04K010                                                
                 OUTPUT BP13L038                                                
                        P13L038A.                                               
                                                                                
           IF BP13K853-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K853 OPEN ERROR, STATUS ' BP13K853-STATUS             
             MOVE BP13K853-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K800 OPEN ERROR, STATUS ' BP13K800-STATUS             
             MOVE BP13K800-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K820 OPEN ERROR, STATUS ' BP13K820-STATUS             
             MOVE BP13K820-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP04K010-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP04K010 OPEN ERROR, STATUS ' BP04K010-STATUS             
             MOVE BP04K010-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE(7:2), '/',                                       
                  WS-CURR-DATE(5:2), '/',                                       
                  WS-CURR-DATE(1:4)  DELIMITED BY SIZE                          
                  INTO L038-DATE.                                               
           MOVE  L038-DATE   TO L038A-DATE.                                     
                                                                                
           MOVE  '55'        TO WS-MAX-LINES.                                   
           MOVE  '60'        TO WS-LINE-CNT, WS-LINE-CNT-A.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F310.                                                      
      *---------------------------------------------------------------*         
           READ BP13F310 AT END                                                 
                          MOVE  'Y'   TO BP13F310-FLAG                          
                         NOT AT END                                             
                          ADD  1      TO WS-F310-READ-CNT                       
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           MOVE    SPACES     TO WS-CDE-DEBAR, WS-APPLN-TAG,                    
                                 WS-NUM-SCH-ACC, WS-PARENT-CHILD-TAG            
                                 WS-NUM-NT, WS-NUM-FT, WS-NUM-QUEUE.            
           MOVE    SPACES     TO WS-APPLN-TABLE, WS-D-OCCUP-TABLE,              
                                 WS-K-OCCUP-TABLE.                              
           MOVE    ZEROES     TO WS-D-CTR, WS-K-CTR.                            
                                                                                
           PERFORM 4000-RETRIEVE-APPLN          THRU 4000-EXIT.                 
                                                                                
           IF APPLICANTS-FND                                                    
              PERFORM 5000-GET-PARENT-CHILD     THRU 5000-EXIT                  
                                                                                
              IF PARENT-CHILD-FND                                               
                 PERFORM 7000-PRINT-REPORT      THRU 7000-EXIT                  
              ELSE                                                              
                 ADD     1    TO WS-K853-NOT-CASES                              
                 DISPLAY 'RECORD NOT FOUND IN BP13K853, REGN : '                
                                                        K853-NUM-REGN           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM  2000-READ-BP13F310          THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-RETRIEVE-APPLN.                                                     
      *---------------------------------------------------------------*         
           MOVE F310-REGN-NO     TO K800-NUM-REGN.                              
                                                                                
           PERFORM 4100-READ-BP13K800      THRU 4100-EXIT.                      
                                                                                
           IF APPLICANTS-FND                                                    
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4               
                 MOVE   K800-NUM-REGN        TO K820-NUM-REGN                   
                 EVALUATE WS-CTR                                                
                    WHEN 01                                                     
                       MOVE K800-NUM-NRIC1   TO K820-NUM-NRIC                   
                    WHEN 02                                                     
                       MOVE K800-NUM-NRIC2   TO K820-NUM-NRIC                   
                    WHEN 03                                                     
                       MOVE K800-NUM-NRIC3   TO K820-NUM-NRIC                   
                    WHEN 04                                                     
                       MOVE K800-NUM-NRIC4   TO K820-NUM-NRIC                   
                 END-EVALUATE                                                   
                                                                                
                 IF K820-NUM-NRIC NOT = SPACES AND LOW-VALUES                   
                    PERFORM 4200-READ-BP13K820  THRU 4200-EXIT                  
                    MOVE  K820-NME-OCCP      TO WS-NME-APPLN(WS-CTR)            
                    MOVE  K820-NUM-NRIC      TO WS-UIN-APPLN(WS-CTR)            
                 END-IF                                                         
                                                                                
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-READ-BP13K800.                                                      
      *---------------------------------------------------------------*         
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
             WHEN 00                                                            
               MOVE  'Y'     TO WS-APPLN-TAG                                    
               MOVE K800-NUM-SCH-ACC      TO WS-NUM-SCH-ACC                     
               MOVE K800-NUM-NEW-TOWN     TO WS-NUM-NT                          
               MOVE K800-NUM-FLAT-TYPE    TO WS-NUM-FT                          
               MOVE K800-NUM-NT-FT-QUEUE  TO WS-NUM-QUEUE                       
             WHEN 23                                                            
               ADD    1      TO WS-K800-NOT-CASES                               
               MOVE  SPACE   TO WS-APPLN-TAG                                    
               DISPLAY 'RECORD NOT FOUND IN BP13K800, REGN : '                  
                                                      K800-NUM-REGN             
             WHEN OTHER                                                         
               DISPLAY 'BP13K800 READ ERROR, STATUS ' BP13K800-STATUS           
                       '  REGN : ' K800-NUM-REGN                                
               MOVE BP13K800-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-READ-BP13K820.                                                      
      *---------------------------------------------------------------*         
           READ BP13K820.                                                       
                                                                                
           EVALUATE BP13K820-STATUS                                             
             WHEN 00                                                            
               CONTINUE                                                         
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY : '                   
                                                      K820-KEY-FLD              
             WHEN OTHER                                                         
               DISPLAY 'BP13K820 READ ERROR, STATUS ' BP13K820-STATUS           
                       '  KEY : ' K820-KEY-FLD                                  
               MOVE BP13K820-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-GET-PARENT-CHILD.                                                   
      *---------------------------------------------------------------*         
           MOVE   SPACES        TO K853-KEY-FLD.                                
           MOVE F310-REGN-NO    TO K853-NUM-REGN.                               
                                                                                
           PERFORM 5100-START-BP13K853       THRU 5100-EXIT.                    
                                                                                
           IF BP13K853-STATUS = 00                                              
              PERFORM 5300-READNXT-BP13K853  THRU 5300-EXIT UNTIL               
                (K853-NUM-REGN NOT = F310-REGN-NO) OR (BP13K853-EOF) OR         
                (WS-D-CTR > 9  AND WS-K-CTR > 9)                                
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5100-START-BP13K853.                                                     
      *---------------------------------------------------------------*         
           START BP13K853 KEY >= K853-KEY-FLD                                   
                                                                                
           EVALUATE BP13K853-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP13K853, REGN : '                  
                                                      K853-NUM-REGN             
             WHEN OTHER                                                         
               DISPLAY 'BP13K853 START ERROR, STATUS ' BP13K853-STATUS          
                       '  KEY : ' K853-KEY-FLD                                  
               MOVE BP13K853-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5300-READNXT-BP13K853.                                                   
      *---------------------------------------------------------------*         
           READ BP13K853 NEXT.                                                  
                                                                                
           EVALUATE BP13K853-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF K853-NUM-REGN = F310-REGN-NO                                  
                  MOVE  'Y'     TO WS-PARENT-CHILD-TAG                          
                  PERFORM 5500-CHK-DEBAR-CODE    THRU 5500-EXIT                 
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BP13K853-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K853 READNXT ERROR, STATUS '                        
                  BP13K853-STATUS  '  KEY : ' K853-KEY-FLD                      
               MOVE BP13K853-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5500-CHK-DEBAR-CODE.                                                     
      *---------------------------------------------------------------*         
           IF K853-CDE-DEBAR = 'D'                                              
              IF WS-D-CTR < 10                                                  
                 ADD   1     TO WS-D-CTR                                        
                 MOVE K853-NUM-NRIC        TO WS-D-UIN-OCCUP(WS-D-CTR)          
                 MOVE K853-NME-OCCP        TO WS-D-NME-OCCUP(WS-D-CTR)          
                 MOVE K853-DEB-REF-NO      TO WS-D-DEB-REF(WS-D-CTR)            
                 PERFORM 6000-CHK-DEBARMENT   THRU 6000-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              IF K853-CDE-DEBAR = 'K'                                           
                 IF WS-K-CTR < 10                                               
                    ADD   1     TO WS-K-CTR                                     
                    MOVE K853-NUM-NRIC     TO WS-K-UIN-OCCUP(WS-K-CTR)          
                    MOVE K853-NME-OCCP     TO WS-K-NME-OCCUP(WS-K-CTR)          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-CHK-DEBARMENT.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES    TO K010-PRIME-KEY.                                    
           MOVE WS-D-UIN-OCCUP(WS-D-CTR) TO K010-NUM-UINFIN.                    
           MOVE WS-D-DEB-REF(WS-D-CTR)   TO K010-DEB-REF-NO.                    
                                                                                
           PERFORM 6100-START-BP04K010       THRU 6100-EXIT.                    
                                                                                
           IF BP04K010-STATUS = 00                                              
              PERFORM 6300-READNXT-BP04K010  THRU 6300-EXIT                     
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6100-START-BP04K010.                                                     
      *---------------------------------------------------------------*         
           START BP04K010 KEY >= K010-PRIME-KEY.                                
                                                                                
           EVALUATE BP04K010-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP04K010, KEY : '                   
                                                    K010-PRIME-KEY              
             WHEN OTHER                                                         
               DISPLAY 'BP04K010 START ERROR, STATUS ' BP04K010-STATUS          
                       '  KEY : ' K010-PRIME-KEY                                
               MOVE BP04K010-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6300-READNXT-BP04K010.                                                   
      *---------------------------------------------------------------*         
           READ BP04K010 NEXT.                                                  
                                                                                
           EVALUATE BP04K010-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF (K010-NUM-UINFIN = WS-D-UIN-OCCUP(WS-D-CTR))  AND             
                  (K010-DEB-REF-NO = WS-D-DEB-REF(WS-D-CTR))                    
                  MOVE K010-REASON-CODE  TO WS-D-REASON-CDE(WS-D-CTR)           
                  MOVE K010-PERIOD-DEBAR TO WS-D-DEB-PERIOD(WS-D-CTR)           
                  IF (K010-EFF-DATE = LOW-VALUES OR SPACES)  OR                 
                     (K010-EFF-DATE NOT NUMERIC)                                
                     MOVE    ZEROES         TO WS-D-EFF-DATE(WS-D-CTR)          
                  ELSE                                                          
                     MOVE K010-EFF-DATE     TO WS-D-EFF-DATE(WS-D-CTR)          
                  END-IF                                                        
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BP04K010-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BP04K010 READNXT ERROR, STATUS '                        
                  BP04K010-STATUS  '  KEY : ' K010-PRIME-KEY                    
               MOVE BP04K010-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           IF WS-D-UIN-OCCUP(1) NOT = SPACES AND LOW-VALUES                     
              PERFORM 7100-PRINT-BP13L038   THRU 7100-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K-UIN-OCCUP(1) NOT = SPACES AND LOW-VALUES                     
              PERFORM 7500-PRINT-P13L038A   THRU 7500-EXIT                      
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7100-PRINT-BP13L038.                                                     
      *---------------------------------------------------------------*         
           PERFORM 7150-CHK-LINE-CTR    THRU 7150-EXIT.                         
           MOVE   SPACES        TO   L038-DETAIL-0.                             
           WRITE  BP13L038-REC  FROM L038-DETAIL-0.                             
           ADD  1     TO   WS-L038-WRITE-CNT                                    
                           WS-LINE-CNT.                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT NEW TOWN, FLAT TYPE AND QUEUE  *                                  
      *------------------------------------- *                                  
           MOVE  SPACES       TO  L038-DETAIL-0.                                
           ADD     1          TO  WS-SLNO.                                      
           MOVE WS-SLNO       TO  L038-SEQ-NO.                                  
           MOVE WS-NUM-NT     TO  L038-NEW-TOWN.                                
           MOVE WS-NUM-FT     TO  L038-FLAT-TYPE.                               
           MOVE WS-NUM-QUEUE  TO  L038-QUEUE.                                   
                                                                                
           PERFORM 7150-CHK-LINE-CTR    THRU 7150-EXIT.                         
           WRITE  BP13L038-REC  FROM L038-DETAIL-0.                             
           ADD  1     TO   WS-L038-WRITE-CNT                                    
                           WS-LINE-CNT.                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANT-1 AND OCCUPIER-1     *                                  
      *------------------------------------- *                                  
           MOVE    SPACES              TO   L038-DETAIL-1.                      
           MOVE F310-REGN-NO           TO   L038-NUM-REGN.                      
           MOVE WS-NME-APPLN(1)        TO   L038-NME-APPLN.                     
           MOVE WS-UIN-APPLN(1)        TO   L038-UIN-APPLN.                     
           MOVE WS-D-NME-OCCUP(1)      TO   L038-NME-OCCP.                      
           MOVE WS-D-UIN-OCCUP(1)      TO   L038-NUM-NRIC.                      
           MOVE WS-D-DEB-REF(1)        TO   L038-DEB-REF-NO.                    
           MOVE WS-D-REASON-CDE(1)     TO   L038-DEB-CODE.                      
           MOVE WS-D-DEB-PERIOD(1)     TO   L038-PERIOD.                        
           STRING WS-D-EFF-DATE(1)(7:2)  '/'                                    
                  WS-D-EFF-DATE(1)(5:2)  '/'                                    
                  WS-D-EFF-DATE(1)(1:4)                                         
                  DELIMITED BY SIZE    INTO L038-EFF-DATE.                      
                                                                                
           PERFORM 7150-CHK-LINE-CTR   THRU 7150-EXIT.                          
           WRITE  BP13L038-REC  FROM L038-DETAIL-1.                             
                                                                                
           ADD  1     TO   WS-LINE-CNT.                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANT-2 AND OCCUPIER-2     *                                  
      *------------------------------------- *                                  
           MOVE    SPACES            TO  L038-DETAIL-1.                         
           MOVE WS-NUM-SCH-ACC       TO  L038-SCH-ACC.                          
           IF (WS-UIN-APPLN(2) NOT = SPACES AND LOW-VALUES)                     
                                      OR                                        
              (WS-D-UIN-OCCUP(2) NOT = SPACES AND LOW-VALUES)                   
                                                                                
              MOVE WS-NME-APPLN(2)           TO  L038-NME-APPLN                 
              MOVE WS-UIN-APPLN(2)           TO  L038-UIN-APPLN                 
              MOVE WS-D-NME-OCCUP(2)         TO  L038-NME-OCCP                  
              MOVE WS-D-UIN-OCCUP(2)         TO  L038-NUM-NRIC                  
              MOVE WS-D-DEB-REF(2)           TO  L038-DEB-REF-NO                
              MOVE WS-D-REASON-CDE(2)        TO  L038-DEB-CODE                  
              MOVE WS-D-EFF-DATE(2)          TO  L038-EFF-DATE                  
              MOVE WS-D-DEB-PERIOD(2)        TO  L038-PERIOD                    
              STRING WS-D-EFF-DATE(2)(7:2)  '/'                                 
                     WS-D-EFF-DATE(2)(5:2)  '/'                                 
                     WS-D-EFF-DATE(2)(1:4)                                      
                     DELIMITED BY SIZE     INTO L038-EFF-DATE                   
           END-IF.                                                              
                                                                                
           PERFORM 7150-CHK-LINE-CTR   THRU 7150-EXIT.                          
           WRITE  BP13L038-REC  FROM L038-DETAIL-1.                             
                                                                                
           ADD  1     TO   WS-LINE-CNT.                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANTS AND OCCUPIERS (3-4) *                                  
      *------------------------------------- *                                  
           PERFORM VARYING WS-CTR1 FROM 3 BY 1 UNTIL WS-CTR1 > 4                
              IF (WS-UIN-APPLN(WS-CTR1) NOT = SPACES AND LOW-VALUES)            
                                      OR                                        
                 (WS-D-UIN-OCCUP(WS-CTR1) NOT = SPACES AND LOW-VALUES)          
                 MOVE  SPACES     TO  L038-DETAIL-1                             
                                                                                
                 MOVE WS-NME-APPLN(WS-CTR1)     TO  L038-NME-APPLN              
                 MOVE WS-UIN-APPLN(WS-CTR1)     TO  L038-UIN-APPLN              
                 MOVE WS-D-NME-OCCUP(WS-CTR1)   TO  L038-NME-OCCP               
                 MOVE WS-D-UIN-OCCUP(WS-CTR1)   TO  L038-NUM-NRIC               
                 MOVE WS-D-DEB-REF(WS-CTR1)     TO  L038-DEB-REF-NO             
                 MOVE WS-D-REASON-CDE(WS-CTR1)  TO  L038-DEB-CODE               
                 MOVE WS-D-EFF-DATE(WS-CTR1)    TO  L038-EFF-DATE               
                 MOVE WS-D-DEB-PERIOD(WS-CTR1)  TO  L038-PERIOD                 
                 STRING WS-D-EFF-DATE(WS-CTR1)(7:2)  '/'                        
                        WS-D-EFF-DATE(WS-CTR1)(5:2)  '/'                        
                        WS-D-EFF-DATE(WS-CTR1)(1:4)                             
                        DELIMITED BY SIZE     INTO L038-EFF-DATE                
                                                                                
                 PERFORM 7150-CHK-LINE-CTR    THRU 7150-EXIT                    
                 WRITE  BP13L038-REC  FROM L038-DETAIL-1                        
                                                                                
                 ADD  1     TO   WS-LINE-CNT                                    
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT REMAINING OCCUPIERS (5-10)     *                                  
      *------------------------------------- *                                  
           PERFORM VARYING WS-CTR1 FROM 5 BY 1 UNTIL WS-CTR1 > 10               
              IF (WS-D-UIN-OCCUP(WS-CTR1) NOT = SPACES AND LOW-VALUES)          
                 MOVE  SPACES    TO  L038-DETAIL-1                              
                                                                                
                 MOVE WS-D-NME-OCCUP(WS-CTR1)   TO  L038-NME-OCCP               
                 MOVE WS-D-UIN-OCCUP(WS-CTR1)   TO  L038-NUM-NRIC               
                 MOVE WS-D-DEB-REF(WS-CTR1)     TO  L038-DEB-REF-NO             
                 MOVE WS-D-REASON-CDE(WS-CTR1)  TO  L038-DEB-CODE               
                 MOVE WS-D-EFF-DATE(WS-CTR1)    TO  L038-EFF-DATE               
                 MOVE WS-D-DEB-PERIOD(WS-CTR1)  TO  L038-PERIOD                 
                 STRING WS-D-EFF-DATE(WS-CTR1)(7:2)  '/'                        
                        WS-D-EFF-DATE(WS-CTR1)(5:2)  '/'                        
                        WS-D-EFF-DATE(WS-CTR1)(1:4)                             
                        DELIMITED BY SIZE     INTO L038-EFF-DATE                
                                                                                
                 PERFORM 7150-CHK-LINE-CTR    THRU 7150-EXIT                    
                 WRITE  BP13L038-REC  FROM L038-DETAIL-1                        
                                                                                
                 ADD  1     TO   WS-LINE-CNT                                    
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7150-CHK-LINE-CTR.                                                       
      *---------------------------------------------------------------*         
           IF WS-LINE-CNT > WS-MAX-LINES                                        
              PERFORM 7200-PRINT-L038-HEADER   THRU 7200-EXIT                   
           END-IF.                                                              
                                                                                
       7150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7200-PRINT-L038-HEADER.                                                  
      *---------------------------------------------------------------*         
           ADD     1            TO   WS-PAGE-CNT.                               
           MOVE WS-PAGE-CNT     TO   L038-PAGE.                                 
           WRITE BP13L038-REC   FROM L038-HEADER-1 AFTER PAGE.                  
           WRITE BP13L038-REC   FROM L038-HEADER-2.                             
           WRITE BP13L038-REC   FROM L038-PR-LINE  AFTER 2 LINES.               
           WRITE BP13L038-REC   FROM L038-HEADER-2A.                            
           WRITE BP13L038-REC   FROM L038-HEADER-3.                             
           WRITE BP13L038-REC   FROM L038-HEADER-3A.                            
           WRITE BP13L038-REC   FROM L038-PR-LINE.                              
           MOVE    8            TO   WS-LINE-CNT.                               
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7500-PRINT-P13L038A.                                                     
      *---------------------------------------------------------------*         
           PERFORM 7550-CHK-LINE-CTR-A    THRU 7550-EXIT.                       
           MOVE   SPACES        TO   L038A-DETAIL-0.                            
           WRITE  P13L038A-REC  FROM L038A-DETAIL-0.                            
           ADD  1     TO   WS-L038A-WRITE-CNT                                   
                           WS-LINE-CNT-A.                                       
                                                                                
      *------------------------------------- *                                  
      * PRINT NEW TOWN, FLAT TYPE AND QUEUE  *                                  
      *------------------------------------- *                                  
           MOVE  SPACES       TO  L038A-DETAIL-0.                               
           ADD     1          TO  WS-SLNO-A.                                    
           MOVE WS-SLNO-A     TO  L038A-SEQ-NO.                                 
           MOVE WS-NUM-NT     TO  L038A-NEW-TOWN.                               
           MOVE WS-NUM-FT     TO  L038A-FLAT-TYPE.                              
           MOVE WS-NUM-QUEUE  TO  L038A-QUEUE.                                  
                                                                                
           PERFORM 7550-CHK-LINE-CTR-A   THRU 7550-EXIT.                        
           WRITE  P13L038A-REC  FROM L038A-DETAIL-0.                            
           ADD  1     TO   WS-L038A-WRITE-CNT                                   
                           WS-LINE-CNT-A.                                       
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANT-1 AND OCCUPIER-1     *                                  
      *------------------------------------- *                                  
           MOVE   SPACES        TO   L038A-DETAIL-1.                            
                                                                                
           MOVE F310-REGN-NO             TO   L038A-NUM-REGN.                   
           MOVE WS-NME-APPLN(1)          TO   L038A-NME-APPLN.                  
           MOVE WS-UIN-APPLN(1)          TO   L038A-UIN-APPLN.                  
           MOVE WS-K-NME-OCCUP(1)        TO   L038A-NME-OCCP.                   
           MOVE WS-K-UIN-OCCUP(1)        TO   L038A-NUM-NRIC.                   
                                                                                
           PERFORM 7550-CHK-LINE-CTR-A   THRU 7550-EXIT.                        
           WRITE  P13L038A-REC  FROM L038A-DETAIL-1.                            
                                                                                
           ADD  1     TO   WS-LINE-CNT-A.                                       
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANT-2 AND OCCUPIER-2     *                                  
      *------------------------------------- *                                  
           MOVE   SPACES        TO  L038A-DETAIL-1.                             
           MOVE WS-NUM-SCH-ACC       TO  L038A-SCH-ACC.                         
           IF (WS-UIN-APPLN(2) NOT = SPACES AND LOW-VALUES)                     
                                    OR                                          
              (WS-K-UIN-OCCUP(2) NOT = SPACES AND LOW-VALUES)                   
                                                                                
               MOVE WS-NME-APPLN(2)           TO  L038A-NME-APPLN               
               MOVE WS-UIN-APPLN(2)           TO  L038A-UIN-APPLN               
               MOVE WS-K-NME-OCCUP(2)         TO  L038A-NME-OCCP                
               MOVE WS-K-UIN-OCCUP(2)         TO  L038A-NUM-NRIC                
           END-IF.                                                              
                                                                                
           PERFORM 7550-CHK-LINE-CTR-A   THRU 7550-EXIT.                        
           WRITE  P13L038A-REC  FROM L038A-DETAIL-1.                            
                                                                                
           ADD  1     TO   WS-LINE-CNT-A.                                       
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANTS AND OCCUPIERS (3-4) *                                  
      *------------------------------------- *                                  
           PERFORM VARYING WS-CTR1 FROM 3 BY 1 UNTIL WS-CTR1 > 4                
              IF (WS-UIN-APPLN(WS-CTR1) NOT = SPACES AND LOW-VALUES)            
                                      OR                                        
                 (WS-K-UIN-OCCUP(WS-CTR1) NOT = SPACES AND LOW-VALUES)          
                 MOVE  SPACES   TO  L038A-DETAIL-1                              
                                                                                
                 MOVE WS-NME-APPLN(WS-CTR1)     TO  L038A-NME-APPLN             
                 MOVE WS-UIN-APPLN(WS-CTR1)     TO  L038A-UIN-APPLN             
                 MOVE WS-K-NME-OCCUP(WS-CTR1)   TO  L038A-NME-OCCP              
                 MOVE WS-K-UIN-OCCUP(WS-CTR1)   TO  L038A-NUM-NRIC              
                                                                                
                 PERFORM 7550-CHK-LINE-CTR-A    THRU 7550-EXIT                  
                 WRITE  P13L038A-REC  FROM L038A-DETAIL-1                       
                                                                                
                 ADD   1        TO   WS-LINE-CNT-A                              
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT REMAINING OCCUPIERS (5-10)     *                                  
      *------------------------------------- *                                  
           PERFORM VARYING WS-CTR1 FROM 5 BY 1 UNTIL WS-CTR1 > 10               
              IF (WS-K-UIN-OCCUP(WS-CTR1) NOT = SPACES AND LOW-VALUES)          
                 MOVE  SPACES   TO  L038A-DETAIL-1                              
                                                                                
                 MOVE WS-K-NME-OCCUP(WS-CTR1)   TO  L038A-NME-OCCP              
                 MOVE WS-K-UIN-OCCUP(WS-CTR1)   TO  L038A-NUM-NRIC              
                                                                                
                 PERFORM 7550-CHK-LINE-CTR-A    THRU 7550-EXIT                  
                 WRITE  P13L038A-REC  FROM L038A-DETAIL-1                       
                                                                                
                 ADD   1        TO   WS-LINE-CNT-A                              
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7550-CHK-LINE-CTR-A.                                                     
      *---------------------------------------------------------------*         
           IF WS-LINE-CNT-A > WS-MAX-LINES                                      
              PERFORM 7600-PRINT-L038A-HEADER   THRU 7600-EXIT                  
           END-IF.                                                              
                                                                                
       7550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7600-PRINT-L038A-HEADER.                                                 
      *---------------------------------------------------------------*         
           ADD     1            TO   WS-PAGE-CNT-A.                             
           MOVE WS-PAGE-CNT-A   TO   L038A-PAGE.                                
           WRITE P13L038A-REC   FROM L038A-HEADER-1 AFTER PAGE.                 
           WRITE P13L038A-REC   FROM L038A-HEADER-2.                            
           WRITE P13L038A-REC   FROM L038-PR-LINE   AFTER 2 LINES.              
           WRITE P13L038A-REC   FROM L038-HEADER-2A.                            
           WRITE P13L038A-REC   FROM L038A-HEADER-3.                            
           WRITE P13L038A-REC   FROM L038-HEADER-3A.                            
           WRITE P13L038A-REC   FROM L038-PR-LINE.                              
           MOVE    8            TO   WS-LINE-CNT-A.                             
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C038       ' L038-DATE.                 
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F310-READ-CNT      TO WS-EDIT1.                              
           DISPLAY 'NO OF RECS READ FROM BP13F310      : ' WS-EDIT1.            
           MOVE WS-K800-NOT-CASES     TO WS-EDIT1.                              
           DISPLAY 'NO OF CASES NOT FOUND IN BP13K800  : ' WS-EDIT1.            
           MOVE WS-K853-NOT-CASES     TO WS-EDIT1.                              
           DISPLAY 'NO OF CASES NOT FOUND IN BP13K853  : ' WS-EDIT1.            
           MOVE WS-L038-WRITE-CNT     TO WS-EDIT1.                              
           DISPLAY 'NO OF CASES PRINTED TO BP13L038    : ' WS-EDIT1.            
           MOVE WS-L038A-WRITE-CNT     TO WS-EDIT1.                             
           DISPLAY 'NO OF CASES PRINTED TO P13L038A    : ' WS-EDIT1.            
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F310                                                     
                   BP13K800                                                     
                   BP13K820                                                     
                   BP13K853                                                     
                   BP04K010                                                     
                   BP13L038                                                     
                   P13L038A.                                                    
                                                                                
           IF BP13K800-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K800 CLOSE ERROR, STATUS ' BP13K800-STATUS            
             MOVE BP13K800-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K820 CLOSE ERROR, STATUS ' BP13K820-STATUS            
             MOVE BP13K820-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF BP13K853-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K853 CLOSE ERROR, STATUS ' BP13K853-STATUS            
             MOVE BP13K853-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF BP04K010-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP04K010 CLOSE ERROR, STATUS ' BP04K010-STATUS            
             MOVE BP04K010-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
************************** END OF BP13C038 ****************************         
                                                                                
