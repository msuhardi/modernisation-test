      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C483.                                                 
      *AUTHOR.        MCC.                                                      
      *DATE-WRITTEN.  14APR2001.                                                
                                                                                
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *========================================================*                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO PRINT A DAILY REPORT ON REFUND OF BOOKING FEE/*                
      *       COMMITMENT DEPOSIT TO STUDIO APTS LESSEES AFTER  *                
      *       KEYS HAVE BEEN ISSUED AND SOC DEBTOR CLEARED.    *                
      *                                                        *                
      *    INPUT  FILE  : BP13F410 - TRANSACTION FILE          *                
      *                                                        *                
      *    LISTING      : BP13L483 - DAILY REPORT ON REFUND    *                
      *                              OF BKFEE/CD TO STDAPT     *                
      *                              LESSEES                   *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ====== ===================================*                
      *BP130998 MCC  140501 NEW PGM                            *                
      *========================================================*                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13L483 ASSIGN TO BP13L483.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
       FD   BP13L483                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L483-REC                 PIC X(132).                            
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF-SW                 PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F410               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-WRIT-L483               PIC 9(5)  VALUE ZEROS.            
           05  WS-L483-PAGE-CTR               PIC 9(04) VALUE ZEROS.            
           05  WS-L483-LINE-CTR               PIC 9(02) VALUE 56.               
           05  WS-L483-SNO-CTR                PIC 9(06) VALUE ZEROS.            
           05  WS-SUM                         PIC 9(02) VALUE ZEROS.            
                                                                                
       01  WS-SYSTEM-TIME                     PIC X(08).                        
       01  WS-RUN-DATE.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  L483-HDG1.                                                           
           05  FILLER                   PIC X(14) VALUE 'BP13L483'.             
           05  FILLER                   PIC X(42) VALUE SPACES.                 
           05  FILLER                   PIC X(20) VALUE                         
               'SYSTEM OF COMMITMENT'.                                          
           05  FILLER                   PIC X(15) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE 'DATE : '.              
           05  L483-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(06) VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'PAGE: '.               
           05  L483-PAGE-NO             PIC ZZZ,ZZ9 VALUE ZEROS.                
                                                                                
       01  L483-HDG2.                                                           
           05  FILLER                   PIC X(52) VALUE SPACES.                 
           05  FILLER                   PIC X(27) VALUE                         
              'SALES UNIT (AGREEMENT TEAM)'.                                    
                                                                                
       01  L483-HDG3.                                                           
           05  FILLER                   PIC X(36) VALUE SPACES.                 
           05  FILLER                   PIC X(61) VALUE                         
              'WEEKLY REPORT ON REFUND OF BOOKING FEE/COMM DEP/ADV COLLN        
      -       ' TO'.                                                            
                                                                                
       01  L483-HDG4.                                                           
           05  FILLER                   PIC X(56).                              
           05  FILLER                   PIC X(20) VALUE                         
              'STUDIO APTS LESSEES'.                                            
                                                                                
       01  L483-HDG5.                                                           
           05  FILLER               PIC X(77) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE '  REFUND   '.              
           05  FILLER               PIC X(19) VALUE SPACES.                     
           05  FILLER               PIC X(09) VALUE ' REFUND  '.                
                                                                                
       01  L483-HDG6.                                                           
           05  FILLER               PIC X(77) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE 'BOOKING FEE'.              
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(09) VALUE 'REFUND CD'.                
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(09) VALUE ' ADV COL '.                
                                                                                
       01  L483-HDG7.                                                           
           05  FILLER               PIC X(09) VALUE 'SERIAL NO'.                
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE 'REGN NO '.                 
           05  FILLER               PIC X(18) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE 'NAME'.                     
           05  FILLER               PIC X(18) VALUE SPACES.                     
           05  FILLER               PIC X(10) VALUE ' SO DATE '.                
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE ' AMOUNT($) '.              
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(09) VALUE 'AMOUNT($)'.                
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(09) VALUE 'AMOUNT($)'.                
                                                                                
       01  L483-HDG8.                                                           
           05  FILLER               PIC X(132) VALUE ALL '-'.                   
                                                                                
       01  WS-SPACES-L483           PIC X(132) VALUE SPACES.                    
                                                                                
       01  L483-DETAIL.                                                         
           05  FILLER                PIC X(01) VALUE SPACES.                    
           05  L483-SNO              PIC ZZZ,ZZ9.                               
           05  FILLER                PIC X(06) VALUE SPACES.                    
           05  L483-NUM-REGN         PIC X(08).                                 
           05  FILLER                PIC X(05) VALUE SPACES.                    
           05  L483-NAME             PIC X(30).                                 
           05  FILLER                PIC X(05) VALUE SPACES.                    
           05  L483-SO-DATE          PIC X(10).                                 
           05  FILLER                PIC X(07) VALUE SPACES.                    
           05  L483-REFUND-BK        PIC ZZZ9.99.                               
           05  FILLER                PIC X(07) VALUE SPACES.                    
           05  L483-REFUND-CD        PIC ZZZZZ9.99.                             
           05  FILLER                PIC X(05) VALUE SPACES.                    
           05  L483-REFUND-ADVCOL    PIC ZZZZZ9.99.                             
                                                                                
       01  L483-DETAIL2.                                                        
           05  FILLER                PIC X(27) VALUE SPACES.                    
           05  L483-OTH-NAME         PIC X(30).                                 
           05  FILLER                PIC X(75) VALUE SPACES.                    
                                                                                
       01  L483-NO-RECORDS.                                                     
           05  FILLER               PIC X(48) VALUE SPACES.                     
           05  FILLER               PIC X(35) VALUE                             
                 '* * *   N O   R E C O R D S   * * *'.                         
                                                                                
       01  L483-TRAILER.                                                        
           05  FILLER               PIC X(42) VALUE SPACES.                     
           05  FILLER               PIC X(40) VALUE                             
                 '* * *   E N D  O F  R E P O R T   * * *'.                     
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-F410         THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F410      THRU 3000-EXIT                        
             UNTIL WS-F410-EOF-SW = 'Y'.                                        
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
           OPEN INPUT  BP13F410                                                 
                OUTPUT BP13L483.                                                
                                                                                
           STRING  FUNCTION CURRENT-DATE(7:2) '/'                               
                   FUNCTION CURRENT-DATE(5:2) '/'                               
                   FUNCTION CURRENT-DATE(1:4)                                   
           DELIMITED BY SIZE INTO L483-DATE.                                    
                                                                                
           PERFORM 5000-PRINT-HDG  THRU 5000-EXIT.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-READ-F410.                                                          
      ****************************************************************          
           READ BP13F410                                                        
              AT END     MOVE 'Y'  TO WS-F410-EOF-SW                            
              GO TO 2000-EXIT.                                                  
                                                                                
           IF F410-AMT-REFUND-CD NOT NUMERIC                                    
              MOVE ZEROES TO F410-AMT-REFUND-CD.                                
                                                                                
           IF F410-AMT-REFUND-BK NOT NUMERIC                                    
              MOVE ZEROES TO F410-AMT-REFUND-BK.                                
                                                                                
           IF F410-AMT-REFUND-ADV-COL NOT NUMERIC                               
              MOVE ZEROES TO F410-AMT-REFUND-ADV-COL.                           
                                                                                
           ADD +1         TO WS-CNT-READ-F410.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F410.                                                       
      ****************************************************************          
           IF (F410-AMT-REFUND-CD      > ZEROES) OR                             
              (F410-AMT-REFUND-BK      > ZEROES) OR                             
              (F410-AMT-REFUND-ADV-COL > ZEROES)                                
              PERFORM 4000-WRITE-L483    THRU 4000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F410         THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4000-WRITE-L483.                                                         
      ****************************************************************          
           MOVE SPACES                TO BP13L483-REC.                          
           INITIALIZE                    BP13L483-REC.                          
                                                                                
           ADD  1                     TO WS-L483-SNO-CTR.                       
           MOVE WS-L483-SNO-CTR       TO L483-SNO.                              
                                                                                
           MOVE F410-NUM-REGN         TO L483-NUM-REGN.                         
                                                                                
           STRING  F410-DTE-SO-EXECUTION(7:2) '/'                               
                   F410-DTE-SO-EXECUTION(5:2) '/'                               
                   F410-DTE-SO-EXECUTION(1:4)                                   
           DELIMITED BY SIZE INTO L483-SO-DATE.                                 
                                                                                
           IF F410-NAME1 NOT = SPACES AND LOW-VALUES                            
              MOVE F410-NAME1          TO L483-NAME.                            
                                                                                
           IF F410-AMT-REFUND-CD > ZEROES                                       
              MOVE F410-AMT-REFUND-CD TO L483-REFUND-CD                         
           ELSE                                                                 
              MOVE ZEROES             TO L483-REFUND-CD                         
           END-IF.                                                              
                                                                                
           IF F410-AMT-REFUND-BK > ZEROES                                       
              MOVE F410-AMT-REFUND-BK TO L483-REFUND-BK                         
           ELSE                                                                 
              MOVE ZEROES             TO L483-REFUND-BK                         
           END-IF.                                                              
                                                                                
           IF F410-AMT-REFUND-ADV-COL > ZEROES                                  
              MOVE F410-AMT-REFUND-ADV-COL TO L483-REFUND-ADVCOL                
           ELSE                                                                 
              MOVE ZEROES                  TO L483-REFUND-ADVCOL                
           END-IF.                                                              
                                                                                
           IF WS-L483-LINE-CTR > 55                                             
              PERFORM 5000-PRINT-HDG  THRU 5000-EXIT                            
           END-IF.                                                              
                                                                                
           WRITE BP13L483-REC FROM L483-DETAIL AFTER 1.                         
           ADD  1  TO WS-L483-LINE-CTR.                                         
                                                                                
           IF WS-L483-LINE-CTR > 55                                             
              PERFORM 5000-PRINT-HDG  THRU 5000-EXIT                            
           END-IF.                                                              
                                                                                
           IF F410-NAME2 NOT = SPACES AND LOW-VALUES                            
              MOVE F410-NAME2          TO L483-OTH-NAME                         
              MOVE SPACES              TO BP13L483-REC                          
              WRITE BP13L483-REC FROM L483-DETAIL2 AFTER 1                      
              ADD  1                   TO WS-L483-LINE-CTR                      
           END-IF.                                                              
                                                                                
           IF WS-L483-LINE-CTR > 55                                             
              PERFORM 5000-PRINT-HDG  THRU 5000-EXIT                            
           END-IF.                                                              
                                                                                
           IF F410-NAME3 NOT = SPACES AND LOW-VALUES                            
              MOVE F410-NAME3          TO L483-OTH-NAME                         
              MOVE SPACES              TO BP13L483-REC                          
              WRITE BP13L483-REC FROM L483-DETAIL2 AFTER 1                      
              ADD  1                   TO WS-L483-LINE-CTR                      
           END-IF.                                                              
                                                                                
           IF WS-L483-LINE-CTR > 55                                             
              PERFORM 5000-PRINT-HDG  THRU 5000-EXIT                            
           END-IF.                                                              
                                                                                
           IF F410-NAME4 NOT = SPACES AND LOW-VALUES                            
              MOVE F410-NAME4          TO L483-OTH-NAME                         
              MOVE SPACES              TO BP13L483-REC                          
              WRITE BP13L483-REC FROM L483-DETAIL2 AFTER 1                      
              ADD  1                   TO WS-L483-LINE-CTR                      
           END-IF.                                                              
                                                                                
           ADD +1  TO WS-CNT-WRIT-L483.                                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       5000-PRINT-HDG.                                                          
      ****************************************************************          
           ADD 1                   TO WS-L483-PAGE-CTR.                         
           MOVE WS-L483-PAGE-CTR   TO L483-PAGE-NO.                             
           WRITE BP13L483-REC FROM L483-HDG1      AFTER PAGE.                   
           WRITE BP13L483-REC FROM L483-HDG2      AFTER 1.                      
           WRITE BP13L483-REC FROM WS-SPACES-L483 AFTER 1.                      
           WRITE BP13L483-REC FROM L483-HDG3      AFTER 1.                      
           WRITE BP13L483-REC FROM L483-HDG4      AFTER 1.                      
           WRITE BP13L483-REC FROM WS-SPACES-L483 AFTER 1.                      
           WRITE BP13L483-REC FROM L483-HDG5      AFTER 1.                      
           WRITE BP13L483-REC FROM L483-HDG6      AFTER 1.                      
           WRITE BP13L483-REC FROM L483-HDG7      AFTER 1.                      
           WRITE BP13L483-REC FROM L483-HDG8      AFTER 1.                      
           MOVE  11  TO WS-L483-LINE-CTR.                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
           IF WS-L483-LINE-CTR > 55                                             
              PERFORM 5000-PRINT-HDG  THRU 5000-EXIT                            
           END-IF.                                                              
                                                                                
           IF WS-CNT-WRIT-L483 = ZEROES                                         
              WRITE BP13L483-REC FROM L483-NO-RECORDS AFTER 3                   
           ELSE                                                                 
              WRITE BP13L483-REC FROM L483-TRAILER    AFTER 3                   
           END-IF.                                                              
                                                                                
           CLOSE BP13F410                                                       
                 BP13L483.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : '  L483-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C483 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F410 RECORDS READ        = ' WS-CNT-READ-F410.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13L483 RECORDS WRITTEN     = ' WS-CNT-WRIT-L483.          
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
