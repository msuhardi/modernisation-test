       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CB9D.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 23/07/2019.                                                
      *****************************************************************         
      *               SOC - SYSTEM OF COMMITMENT (BP13)               *         
      *===============================================================*         
      * OBJECTIVES : TO UPDATE CODE CITIZENSHIP HOUSEHOLD IN BP13K730 *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F730 I     SEQUENTIAL 500                                 *         
      * BP13K730 I/O   RANDOM     500  K730-NUM-REGN                  *         
      * BP13K825 I     DYNAMIC    200  K825-NUM-REGN                  *         
      * BP13F500 O     SEQUENTIAL 500                                 *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHGE REQ  DD/MM/YY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP137924  23/07/19  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN      TO BP13F730.                             
                                                                                
           SELECT BP13K730 ASSIGN      TO BP13K730                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K730-KEY-NEW                                     
              FILE STATUS   IS WS-K730-STATUS.                                  
                                                                                
           SELECT BP13K825 ASSIGN      TO BP13K825                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS DYNAMIC                                          
              RECORD KEY    IS K825-KEY-FLD                                     
              FILE STATUS   IS WS-K825-STATUS.                                  
                                                                                
           SELECT BP13F500 ASSIGN      TO BP13F500.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F730.                                                           
                                                                                
       FD  BP13K730                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K730.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K825.                                                           
                                                                                
       FD  BP13F500                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F500.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K730-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K825-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-HH-SCH               PIC X(3)        VALUE SPACES.            
           05  WS-HH-SCP               PIC X(3)        VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K730-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K825-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K730-UPDT            PIC 9(6)        VALUE ZEROES.            
           05  WS-F500-WRTE            PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F730-EOF             PIC X           VALUE 'N'.               
           05  WS-K825-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F730   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESSING THRU 3000-EXIT                          
              UNTIL WS-F730-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                I-O    BP13K730                                                 
                OUTPUT BP13F500.                                                
                                                                                
           IF (WS-K730-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K730 FAIL STATUS=' WS-K730-STATUS               
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K825-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K825 FAIL STATUS=' WS-K825-STATUS               
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F730.                                                      
      *****************************************************************         
                                                                                
           READ BP13F730                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F730-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F730-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           PERFORM 3100-STARTBR-BP13K825 THRU 3100-EXIT.                        
           PERFORM 4000-READ-BP13K730    THRU 4000-EXIT.                        
                                                                                
           PERFORM 2000-READ-BP13F730    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-STARTBR-BP13K825.                                                   
      *****************************************************************         
                                                                                
           MOVE 'N'                    TO WS-K825-EOF.                          
           MOVE SPACES                 TO WS-HH-SCH                             
                                          WS-HH-SCP.                            
                                                                                
           MOVE F730-NUM-REGN          TO K825-NUM-REGN.                        
                                                                                
           START BP13K825 KEY IS >= K825-NUM-REGN.                              
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3200-READNEXT-BP13K825 THRU 3200-EXIT                  
                    UNTIL WS-K825-EOF = 'Y'                                     
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'STARTBR BP13K825 FAIL STATUS=' WS-K825-STATUS         
                 MOVE WS-K825-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3200-READNEXT-BP13K825.                                                  
      *****************************************************************         
                                                                                
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN 00                                                           
                 IF F730-NUM-REGN = K825-NUM-REGN                               
                    ADD 1              TO WS-K825-READ                          
                                                                                
                    IF K825-NUM-RELATIONSHIP NOT = '00'                         
                       PERFORM 3250-CHECK-K825 THRU 3250-EXIT                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'           TO WS-K825-EOF                           
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'              TO WS-K825-EOF                           
              WHEN OTHER                                                        
                 DISPLAY 'READNEXT BP13K825 FAIL STATUS='                       
                                                    WS-K825-STATUS              
                 MOVE WS-K825-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3250-CHECK-K825.                                                         
      *****************************************************************         
                                                                                
           IF K825-NUM-CITIZENSHIP = '10' OR 'SC'                               
              MOVE 'SCH'               TO WS-HH-SCH                             
           ELSE                                                                 
              MOVE 'SCP'               TO WS-HH-SCP                             
           END-IF.                                                              
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-BP13K730.                                                      
      *****************************************************************         
                                                                                
           MOVE F730-NUM-REGN          TO K730-NUM-REGN.                        
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K730-READ                          
                                                                                
                 PERFORM 4100-UPDATE-BP13K730 THRU 4100-EXIT                    
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K730: ' F730-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K730 FAIL STATUS=' WS-K730-STATUS            
                 MOVE WS-K730-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-UPDATE-BP13K730.                                                    
      *****************************************************************         
                                                                                
           IF WS-HH-SCP = SPACES AND WS-HH-SCH NOT = SPACES                     
              MOVE 'SCH'               TO                                       
                                    K730-CDE-CITIZENSHIP-HOUSEHOLD              
           ELSE                                                                 
              IF WS-HH-SCP NOT = SPACES AND WS-HH-SCH = SPACES                  
                 MOVE 'SCP'            TO                                       
                                    K730-CDE-CITIZENSHIP-HOUSEHOLD              
              ELSE                                                              
                 MOVE SPACES           TO                                       
                                    K730-CDE-CITIZENSHIP-HOUSEHOLD              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 5000-WRITE-BP13F500 THRU 5000-EXIT.                          
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           IF WS-K730-STATUS = ZEROES                                           
              ADD 1                    TO WS-K730-UPDT                          
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K730 FAIL STATUS=' WS-K730-STATUS            
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-WRITE-BP13F500.                                                     
      *****************************************************************         
                                                                                
           WRITE BP13F500-REC FROM BP13K730-REC.                                
           ADD 1                       TO WS-F500-WRTE.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F730                                                       
                 BP13K730                                                       
                 BP13K825                                                       
                 BP13F500.                                                      
                                                                                
           IF WS-K730-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K730 FAIL STATUS=' WS-K730-STATUS              
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K825 FAIL STATUS=' WS-K825-STATUS              
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*******************************'.                           
           DISPLAY '* PROGRAM BP13CB9D            *'.                           
           DISPLAY '* CONTROL COUNTS              *'.                           
           DISPLAY '*-----------------------------*'.                           
           DISPLAY '* REC READ F730      : ' WS-F730-READ ' *'.                 
           DISPLAY '* REC READ K825      : ' WS-K825-READ ' *'.                 
           DISPLAY '* REC READ K730      : ' WS-K730-READ ' *'.                 
           DISPLAY '* REC UPDT K730      : ' WS-K730-UPDT ' *'.                 
           DISPLAY '* REC WRTE F500      : ' WS-F500-WRTE ' *'.                 
           DISPLAY '*******************************'.                           
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
