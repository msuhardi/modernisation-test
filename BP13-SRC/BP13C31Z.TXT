       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C31Z.                                                    
      *AUTHOR. RJB1.                                                            
      *DATE-WRITTEN. 08/11/16.                                                  
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (SOC)                             *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE : LISTING OF SOC DEBTORS >= 1 MONTH FROM  *                
      *                SALES AGREEMENT DATE                    *                
      *                                                        *                
      *     PATTERNED FROM THE REPORT CREATED BY BP13S319 BUT  *                
      *     INCLUDES THE FOLLOWING:                            *                
      *     - NEW COLUMNS FOR MONITORING THE NUMBER OF MONTHS  *                
      *       OUTSTANDING                                      *                
      *     - OIC (OFFICER IN CHARGE) OF THE CASES             *                
      *                                                        *                
      *  INPUT FILES:                                          *                
      *    1. BP13F142                                         *                
      *    2. BP13K022                                         *                
      *    3. BP13K024                                         *                
      *    4. SY02F001                                         *                
      *                                                        *                
      *  OUTPUT FILE:                                          *                
      *    1. BP13L31Z                                         *                
      *--------------------------------------------------------*                
      * CHG REF  BY   DATE      DESCRIPTION                    *                
      * -------- ---- --------  -----------                    *                
      * BP135548 RJB1 08/11/16  NEW PROGRAM                    *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K024   ASSIGN TO BP13K024                                 
                  ACCESS MODE  IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K024-KEY-FLD                                  
                  FILE STATUS  IS BP13K024-STATUS.                              
                                                                                
           SELECT BP13K022   ASSIGN TO BP13K022                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K022-KEY-FLD                                  
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS  IS BP13K022-STATUS.                              
                                                                                
           SELECT BP13F142  ASSIGN TO BP13F142.                                 
                                                                                
           SELECT SY02F001  ASSIGN TO SY02F001.                                 
           SELECT BP13L31Z  ASSIGN TO BP13L31Z.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F142                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F142.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD  BP13L31Z                                                             
           RECORD CONTAINS 170 CHARACTERS                                       
           LABEL  RECORDS  ARE OMITTED.                                         
       01  BP13L31Z-REC                 PIC X(170).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-F142-READ            PIC 9(08)    VALUE ZERO.                     
       01  WS-F142-OUTPUT          PIC 9(08)    VALUE ZERO.                     
       01  F142-EOF                PIC X        VALUE 'N'.                      
       01  F001-EOF                PIC X        VALUE 'N'.                      
       01  WS-CNT                  PIC 9(02)    VALUE 99.                       
       01  BP13K022-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BP13K024-STATUS         PIC 9(02)    VALUE ZEROES.                   
                                                                                
       01  WS-DATE.                                                             
           05  WS-DATE-CCYY        PIC 9(4)     VALUE ZEROES.                   
           05  WS-DATE-MM          PIC 9(2)     VALUE ZEROES.                   
           05  WS-DATE-DD          PIC 9(2)     VALUE ZEROES.                   
                                                                                
       01  WS-NUM-OIC                  PIC X(5)   VALUE SPACES.                 
       01  WS-K022-FND                 PIC X      VALUE 'N'.                    
       01  WS-TEAM                     PIC X      VALUE SPACES.                 
       01  WS-COMPUTE-MTH              PIC S9(2)  VALUE ZEROES.                 
       01  WS-PROCESS-DATE.                                                     
           05  WS-PROCESS-CCYY         PIC 9(4).                                
           05  WS-PROCESS-MM           PIC 9(2).                                
           05  WS-PROCESS-DD           PIC 9(2).                                
       01  WS-DTE-COMPUTE.                                                      
           05  WS-COMPUTE-CCYY         PIC 9(4).                                
           05  WS-COMPUTE-MM           PIC 9(2).                                
           05  WS-COMPUTE-DD           PIC 9(2).                                
       01  WS-MONTH-DIFF               PIC 9(3).                                
       01  WS-MONTH-DIFF-RED REDEFINES WS-MONTH-DIFF.                           
           05  WS-MONTH-DIFF-R         PIC X(3).                                
                                                                                
       01  L31Z-PR-HEAD-01.                                                     
           05  FILLER              PIC X(14)   VALUE 'BP13L31Z      '.          
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  L31Z-DATE           PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
                                                                                
       01  L31Z-PR-HEAD-02.                                                     
           05  FILLER              PIC X(29)   VALUE SPACES.                    
           05  FILLER              PIC X(40)   VALUE                            
           'LISTING OF SOC DEBTORS >= 1 MONTH AS AT '.                          
           05  L31Z-CD-DATE.                                                    
               10 L31Z-CD-DD          PIC X(2)    VALUE '01'.                   
               10 FILLER              PIC X       VALUE '/'.                    
               10 L31Z-CD-MM          PIC X(2)    VALUE SPACES.                 
               10 FILLER              PIC X       VALUE '/'.                    
               10 L31Z-CD-CCYY        PIC X(4)    VALUE SPACES.                 
                                                                                
       01  L31Z-PR-HEAD-03.                                                     
           05  FILLER              PIC X(50)   VALUE SPACES.                    
           05  FILLER              PIC X(25)   VALUE                            
           'FROM SALES AGREEMENT DATE'.                                         
           05  FILLER              PIC X(24)   VALUE SPACES.                    
           05  FILLER              PIC X(19)   VALUE                            
           'USER : FINANCE DEPT'.                                               
                                                                                
       01  L31Z-HDG5A.                                                          
           05  FILLER              PIC X(40)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'NO. OF    '.               
                                                                                
       01  L31Z-HDG5B.                                                          
           05  FILLER              PIC X(17)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'NO. OF    '.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE  SPACES.                    
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'MONTHS O/S'.               
                                                                                
       01  L31Z-HDG5C.                                                          
           05  FILLER              PIC X(17)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'MONTHS O/S'.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE  SPACES.                    
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'FROM KEY  '.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(49)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'TEAM:    '.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(11)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'TEAM:    '.                
                                                                                
       01  L31Z-HDG5D.                                                          
           05  FILLER              PIC X(03)  VALUE 'S/N'.                      
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'AGREEMENT '.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'FROM AGRMT'.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'KEY      '.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'AVAILABLE '.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(02)  VALUE 'NT'.                       
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(07)  VALUE 'REGN-NO'.                  
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(04)  VALUE 'NRIC'.                     
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(11)  VALUE 'SCH ACCT NO'.              
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(04)  VALUE 'NAME'.                     
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'CSM(PREB)'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'CSM(PREB)'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'CSM(POSB)'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'CSM(POSB)'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(06)  VALUE 'AMOUNT'.                   
                                                                                
       01  L31Z-HDG5E.                                                          
           05  FILLER              PIC X(05)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'ORDER DATE'.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'ORDER DATE'.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'AVAILABLE'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'DATE      '.               
           05  FILLER              PIC X(06)  VALUE  SPACES.                    
           05  FILLER              PIC X(07)  VALUE 'ADDRESS'.                  
                                                                                
       01  L31Z-PR-DETAIL.                                                      
           05  L31Z-SN             PIC 9(4)  VALUE ZEROS.                       
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-DTE-SA         PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-NOM-SA         PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-DTE-KEYAVA     PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-NOM-KEY        PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-NT             PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-REGNO          PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-NRIC           PIC X(09) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-SCHACC         PIC X(11) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-NAME           PIC X(66) VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-CSM-PREB       PIC X(5)  VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-TM-CSM-PREB    PIC X(1)  VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-CSM-POSB       PIC X(5)  VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-TM-CSM-POSB    PIC X(1)  VALUE SPACES.                      
           05  FILLER              PIC X(1)  VALUE ';'.                         
           05  L31Z-142-AMT        PIC Z,ZZZ,ZZ9.99- VALUE ZEROS.               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-READ-F142 THRU 2000-EXIT.                               
           PERFORM 4000-PRINT-ROUTINE   THRU 4000-EXIT                          
                   UNTIL F142-EOF = 'Y'.                                        
           PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                         
       0000-FINISH.                                                             
            EXIT.                                                               
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
            OPEN INPUT  SY02F001                                                
                        BP13F142                                                
                        BP13K022                                                
                        BP13K024                                                
                 OUTPUT BP13L31Z.                                               
                                                                                
            IF BP13K022-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K022-STATUS ' BP13K022-STATUS            
               MOVE BP13K022-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BP13K024-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K024-STATUS ' BP13K024-STATUS            
               MOVE BP13K024-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            READ SY02F001 AT END MOVE 'Y'  TO F001-EOF.                         
            IF F001-EOF = 'Y'                                                   
               DISPLAY 'RECORD NOT FOUND IN SY01F001 '                          
               DISPLAY 'PROCESSING TERMINATED '                                 
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            ELSE                                                                
               MOVE F001-DTE-CURRENT       TO WS-DATE                           
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-READ-F142.                                                          
      *-------------------------------------------------------------            
            READ BP13F142  AT END MOVE 'Y' TO F142-EOF                          
                 GO TO 2000-EXIT.                                               
            ADD 1 TO WS-F142-READ.                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-PRINT-ROUTINE.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES  TO L31Z-DTE-SA        L31Z-DTE-KEYAVA                  
                            L31Z-NRIC          L31Z-REGNO                       
                            L31Z-NOM-SA        L31Z-NOM-KEY                     
                            L31Z-SCHACC        L31Z-NAME                        
                            L31Z-CSM-PREB      L31Z-TM-CSM-PREB                 
                            L31Z-CSM-POSB      L31Z-TM-CSM-POSB                 
                            L31Z-NT.                                            
            MOVE ZEROES  TO L31Z-142-AMT.                                       
                                                                                
            IF WS-F142-OUTPUT < 1                                               
               STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)            
                      DELIMITED BY SIZE INTO L31Z-DATE                          
                                                                                
               WRITE BP13L31Z-REC FROM L31Z-PR-HEAD-01                          
                                                                                
               MOVE  WS-DATE-CCYY    TO L31Z-CD-CCYY                            
               MOVE  WS-DATE-MM      TO L31Z-CD-MM                              
                                                                                
               WRITE BP13L31Z-REC FROM L31Z-PR-HEAD-02                          
               WRITE BP13L31Z-REC FROM L31Z-PR-HEAD-03                          
               WRITE BP13L31Z-REC FROM L31Z-HDG5A                               
               WRITE BP13L31Z-REC FROM L31Z-HDG5B                               
               WRITE BP13L31Z-REC FROM L31Z-HDG5C                               
               WRITE BP13L31Z-REC FROM L31Z-HDG5D                               
               WRITE BP13L31Z-REC FROM L31Z-HDG5E                               
               MOVE  SPACES       TO   BP13L31Z-REC                             
               WRITE BP13L31Z-REC                                               
            END-IF.                                                             
                                                                                
                                                                                
            PERFORM 4120-READ-K022       THRU  4120-EXIT.                       
            IF WS-K022-FND = 'Y'                                                
               MOVE K022-NUM-OIC           TO  WS-NUM-OIC                       
                                               L31Z-CSM-POSB                    
               PERFORM 4130-READ-K024    THRU  4130-EXIT                        
               MOVE WS-TEAM                TO  L31Z-TM-CSM-POSB                 
               IF K022-NUM-OIC-APPL NOT = SPACES AND LOW-VALUES                 
                  MOVE K022-NUM-OIC-APPL   TO  WS-NUM-OIC                       
                                               L31Z-CSM-PREB                    
                  PERFORM 4130-READ-K024 THRU  4130-EXIT                        
                  MOVE WS-TEAM             TO  L31Z-TM-CSM-PREB                 
               END-IF                                                           
            END-IF.                                                             
                                                                                
            MOVE WS-F142-OUTPUT             TO L31Z-SN                          
            MOVE F142-DTE-SA                TO L31Z-DTE-SA                      
            IF F142-DTE-SA NOT = SPACES AND LOW-VALUES AND ZEROS                
               MOVE F142-DTE-SA             TO WS-DTE-COMPUTE                   
               MOVE ZEROES                  TO WS-COMPUTE-MTH                   
               PERFORM 4150-COMPUTE-MONTHS THRU  4150-EXIT                      
               MOVE WS-COMPUTE-MTH          TO WS-MONTH-DIFF                    
               MOVE WS-MONTH-DIFF-R         TO L31Z-NOM-SA                      
            END-IF.                                                             
                                                                                
            MOVE F142-DTE-KEY-AVAIL         TO L31Z-DTE-KEYAVA                  
            IF F142-DTE-KEY-AVAIL NOT = SPACES AND LOW-VALUES AND ZEROS         
               MOVE F142-DTE-KEY-AVAIL      TO WS-DTE-COMPUTE                   
               MOVE ZEROES                  TO WS-COMPUTE-MTH                   
               PERFORM 4150-COMPUTE-MONTHS THRU  4150-EXIT                      
               MOVE WS-COMPUTE-MTH          TO WS-MONTH-DIFF                    
               MOVE WS-MONTH-DIFF-R         TO L31Z-NOM-KEY                     
            END-IF.                                                             
                                                                                
            MOVE F142-NUM-NT                TO L31Z-NT                          
            MOVE F142-NUM-REGN              TO L31Z-REGNO                       
            MOVE F142-AMT-CASH              TO L31Z-142-AMT                     
            MOVE F142-NUM-NRIC1             TO L31Z-NRIC                        
            MOVE F142-NME-NAME1             TO L31Z-NAME                        
            MOVE F142-NUM-SCH-ACCT          TO L31Z-SCHACC                      
            MOVE SPACES TO BP13L31Z-REC                                         
            WRITE BP13L31Z-REC FROM L31Z-PR-DETAIL.                             
                                                                                
            ADD 1 TO WS-F142-OUTPUT                                             
            PERFORM 2000-READ-F142     THRU 2000-EXIT.                          
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4120-READ-K022.                                                          
      *------------------------*                                                
           MOVE SPACES                     TO BP13K022-REC.                     
           INITIALIZE                         BP13K022-REC.                     
           MOVE 'N'                        TO WS-K022-FND.                      
                                                                                
           MOVE F142-NUM-REGN              TO K022-NUM-REGN.                    
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K022-STATUS                                             
           WHEN  00                                                             
                 MOVE 'Y'                  TO WS-K022-FND                       
                                                                                
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT FOUND IN BP13K022 : '                      
                                                K022-NUM-REGN                   
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13K022 : ' BP13K022-STATUS            
                 DISPLAY 'K022 KEY FIELD         : ' K022-NUM-REGN              
                 MOVE     BP13K022-STATUS       TO   RETURN-CODE                
                 PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4120-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4130-READ-K024.                                                          
      *------------------------*                                                
           MOVE SPACES                     TO BP13K024-REC.                     
           INITIALIZE                         BP13K024-REC.                     
           MOVE SPACES                     TO WS-TEAM.                          
                                                                                
           MOVE WS-NUM-OIC                 TO K024-NUM-OIC.                     
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K024-STATUS                                             
           WHEN  00                                                             
                 MOVE K024-CDE-TEAM        TO WS-TEAM                           
                                                                                
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT FOUND IN BP13K024 : '                      
                                                K024-NUM-OIC                    
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13K022 : ' BP13K024-STATUS            
                 DISPLAY 'K024 OIC               : ' K024-NUM-OIC               
                 MOVE     BP13K024-STATUS       TO   RETURN-CODE                
                 PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4130-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4150-COMPUTE-MONTHS.                                                     
      *------------------------*                                                
           MOVE WS-DATE-MM    TO WS-PROCESS-MM.                                 
           MOVE WS-DATE-CCYY  TO WS-PROCESS-CCYY.                               
                                                                                
           IF WS-PROCESS-MM < WS-COMPUTE-MM                                     
              COMPUTE WS-PROCESS-MM = WS-PROCESS-MM + 12                        
              COMPUTE WS-PROCESS-CCYY = WS-DATE-CCYY - 1                        
           ELSE                                                                 
              IF WS-PROCESS-MM = WS-COMPUTE-MM                                  
                 MOVE 0               TO WS-PROCESS-MM.                         
                                                                                
           IF WS-PROCESS-MM = ZEROES                                            
              IF WS-PROCESS-CCYY > WS-COMPUTE-CCYY                              
              COMPUTE WS-COMPUTE-MTH = WS-PROCESS-MM +                          
                           ((WS-PROCESS-CCYY - WS-COMPUTE-CCYY) * 12)           
              ELSE                                                              
              COMPUTE WS-COMPUTE-MTH = 0                                        
           ELSE                                                                 
              COMPUTE WS-PROCESS-MM = WS-PROCESS-MM +                           
                           ((WS-PROCESS-CCYY - WS-COMPUTE-CCYY) * 12)           
           COMPUTE WS-COMPUTE-MTH = WS-PROCESS-MM - WS-COMPUTE-MM.              
                                                                                
           IF  WS-COMPUTE-MTH < 0                                               
               DISPLAY 'ERROR NEGATIVE FIGURES'.                                
                                                                                
       4150-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
             DISPLAY '- BP13C31Z CONTROL TOTAL ----------'.                     
             DISPLAY ' F142-READ      ' WS-F142-READ.                           
             DISPLAY ' F142-OUTPUT    ' WS-F142-OUTPUT.                         
             DISPLAY ' '.                                                       
                                                                                
           CLOSE      SY02F001                                                  
                      BP13F142                                                  
                      BP13K022                                                  
                      BP13K024                                                  
                      BP13L31Z.                                                 
                                                                                
           IF BP13K022-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K022-STATUS '                            
                        BP13K022-STATUS.                                        
                                                                                
           IF BP13K024-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K024-STATUS '                            
                        BP13K024-STATUS.                                        
                                                                                
             STOP RUN.                                                          
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
