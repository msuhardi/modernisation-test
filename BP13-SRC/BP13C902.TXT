      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C902.                                                 
      *AUTHOR.        CLT1.                                                     
      *DATE-WRITTEN.  03/11/2000.                                               
                                                                                
      ***************************************************************           
      *            SYSTEM OF COMMITMENT (BP13)                      *           
      *=============================================================*           
      *  OBJECTIVE  : TO RETRIEVE OFFICE NAME FROM PIDB             *           
      *                                                             *           
      *  INPUT      : OFFICE TYPE, OFFICE CDE, SEQ NO               *           
      *  OUTPUT     : OFFICE INFO                                   *           
      *  COMMAREA   : 3000 BYTES                                    *           
      *                                                             *           
      *  REMARKS    : NUM_SQNC IS HARDCODED AS '00' TO RETRIEVE     *           
      *               THE 1ST OFFICE INFORMATION ONLY.              *           
      *               PROGRAM WILL RETURN INFORMATION TO BP13COMM8  *           
      *               BASED ON WHETHER IT IS A 'TC' OR 'BO'.        *           
      ***************************************************************           
      * MODIFICATIONS :                                             *           
      *                                                             *           
      * CHGE REQ #  DATE        AUTHOR  REMARKS                     *           
      * ----------  ----------  ------  -------------------------   *           
      * BP130883    03/11/2000  CLT1    NEW PROGRAM.                *           
      ***************************************************************           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-OFFICE                PIC X(8).                           
           05  WS-NUM-TYPE-OFFICE           PIC X(15).                          
           05  WS-NUM-SQNC                  PIC X(2).                           
                                                                                
           EXEC SQL INCLUDE P13VOFFC END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       LINKAGE SECTION.                                                         
      *-----------------------*                                                 
       01  LINK-REC.                                                            
           05  LINK-OFFICE-INFO.                                                
               10  LINK-NUM-OFFICE          PIC X(8).                           
               10  LINK-NUM-TYPE-OFFICE     PIC X(15).                          
               10  LINK-NUM-SQNC            PIC X(2).                           
                                                                                
       COPY P13COMM4.                                                           
                                                                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION USING LINK-REC, BP13COMM4-REC.                        
      *------------------*                                                      
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                             CONTINUE END-EXEC.                                 
           EXEC SQL WHENEVER SQLERROR                                           
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
           EXEC SQL WHENEVER SQLWARNING                                         
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
                                                                                
                                                                                
      *-----------------------*                                                 
       0000-START-ROUTINE.                                                      
      *-----------------------*                                                 
           MOVE SPACES TO  BP13COMM4-REC.                                       
           INITIALIZE      BP13COMM4-REC.                                       
                                                                                
           MOVE LINK-NUM-OFFICE       TO WS-NUM-OFFICE                          
           MOVE LINK-NUM-TYPE-OFFICE  TO WS-NUM-TYPE-OFFICE                     
           MOVE LINK-NUM-SQNC         TO WS-NUM-SQNC                            
                                                                                
           MOVE SPACE            TO COMM4-CDE-ERROR.                            
           MOVE ZEROES           TO COMM4-CDE-SYSERR.                           
                                                                                
           PERFORM 1000-READ-OFFICE-TABLE THRU 1000-EXIT.                       
                                                                                
           EXEC SQL COMMIT END-EXEC.                                            
                                                                                
           GOBACK.                                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
      *    RETRIEVES OFFICE INFO USING THE OFFICE NUM, TYPE AND SEQ             
      *-------------------------------------------------------------*           
      *-----------------------*                                                 
       1000-READ-OFFICE-TABLE.                                                  
      *-----------------------*                                                 
                                                                                
           EXEC SQL SELECT NUM_OFFICE,                                          
                           NUM_TYPE_OFFICE,                                     
                           NUM_SQNC,                                            
                           NME_OFFICE_FULL,                                     
                           NUM_OFFICE_GL,                                       
                           NUM_BLDNG,                                           
                           TXT_ADDR1,                                           
                           TXT_ADDR2,                                           
                           NUM_LEVEL,                                           
                           NUM_UNIT,                                            
                           NUM_POST,                                            
                           NUM_TEL1,                                            
                           NUM_TEL2,                                            
                           NUM_FAX1,                                            
                           NUM_STATUS                                           
                    INTO :DCLOFFICE.NUM-OFFICE,                                 
                         :DCLOFFICE.NUM-TYPE-OFFICE,                            
                         :DCLOFFICE.NUM-SQNC,                                   
                         :DCLOFFICE.NME-OFFICE-FULL,                            
                         :DCLOFFICE.NUM-OFFICE-GL,                              
                         :DCLOFFICE.NUM-BLDNG,                                  
                         :DCLOFFICE.TXT-ADDR1,                                  
                         :DCLOFFICE.TXT-ADDR2,                                  
                         :DCLOFFICE.NUM-LEVEL,                                  
                         :DCLOFFICE.NUM-UNIT,                                   
                         :DCLOFFICE.NUM-POST,                                   
                         :DCLOFFICE.NUM-TEL1,                                   
                         :DCLOFFICE.NUM-TEL2,                                   
                         :DCLOFFICE.NUM-FAX1,                                   
                         :DCLOFFICE.NUM-STATUS                                  
                    FROM OFFICE                                                 
                    WHERE NUM_OFFICE = :WS-NUM-OFFICE                           
                      AND NUM_TYPE_OFFICE = :WS-NUM-TYPE-OFFICE                 
                      AND NUM_SQNC        = :WS-NUM-SQNC                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = WS-SQL-FOUND                                        
              DISPLAY '-----------------------------------------------'         
              DISPLAY 'REC NOTFD IN OFFICE !!'                                  
              DISPLAY 'NUM-OFFICE        = ' WS-NUM-OFFICE                      
              DISPLAY 'NUM-TYPE-OFFICE   = ' WS-NUM-TYPE-OFFICE                 
              DISPLAY 'NUM-SQNC          = ' WS-NUM-SQNC                        
              DISPLAY 'SQLCODE           = ' SQLCODE                            
              DISPLAY '-----------------------------------------------'         
              MOVE SQLCODE  TO  COMM4-CDE-SYSERR                                
              GO TO 1000-EXIT                                                   
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------*         
      *    BRANCH OFFICE                                                        
      *---------------------------------------------------------------*         
           IF WS-NUM-TYPE-OFFICE = 'BO'                                         
              MOVE NUM-SQNC            TO COMM4-NUM-SQNC-BO(1)                  
              MOVE NME-OFFICE-FULL     TO COMM4-NME-OFFICE-BO(1)                
              MOVE NUM-OFFICE-GL       TO COMM4-NUM-OFFICE-GL-BO(1)             
              MOVE NUM-BLDNG           TO COMM4-NUM-BLDNG-BO(1)                 
              MOVE TXT-ADDR1           TO COMM4-TXT-ADDR1-BO(1)                 
              MOVE TXT-ADDR2           TO COMM4-TXT-ADDR2-BO(1)                 
              MOVE NUM-LEVEL           TO COMM4-NUM-LEVEL-BO(1)                 
              MOVE NUM-UNIT            TO COMM4-NUM-UNIT-BO(1)                  
              MOVE NUM-POST            TO COMM4-NUM-POST-BO(1)                  
              MOVE NUM-TEL1            TO COMM4-NUM-TEL1-BO(1)                  
              MOVE NUM-TEL2            TO COMM4-NUM-TEL2-BO(1)                  
              MOVE NUM-FAX1            TO COMM4-NUM-FAX1-BO(1)                  
              MOVE NUM-STATUS          TO COMM4-NUM-STATUS-BO(1)                
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------*         
      *    TOWN COUNCIL                                                         
      *---------------------------------------------------------------*         
           IF WS-NUM-TYPE-OFFICE = 'TC'                                         
              MOVE NUM-SQNC            TO COMM4-NUM-SQNC-TC(1)                  
              MOVE NME-OFFICE-FULL     TO COMM4-NME-OFFICE-TC(1)                
              MOVE NUM-OFFICE-GL       TO COMM4-NUM-OFFICE-GL-TC(1)             
              MOVE NUM-BLDNG           TO COMM4-NUM-BLDNG-TC(1)                 
              MOVE TXT-ADDR1           TO COMM4-TXT-ADDR1-TC(1)                 
              MOVE TXT-ADDR2           TO COMM4-TXT-ADDR2-TC(1)                 
              MOVE NUM-LEVEL           TO COMM4-NUM-LEVEL-TC(1)                 
              MOVE NUM-UNIT            TO COMM4-NUM-UNIT-TC(1)                  
              MOVE NUM-POST            TO COMM4-NUM-POST-TC(1)                  
              MOVE NUM-TEL1            TO COMM4-NUM-TEL1-TC(1)                  
              MOVE NUM-TEL2            TO COMM4-NUM-TEL2-TC(1)                  
              MOVE NUM-FAX1            TO COMM4-NUM-FAX1-TC(1)                  
              MOVE NUM-STATUS          TO COMM4-NUM-STATUS-TC(1)                
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       9000-ABORT-PROCESS.                                                      
      *-----------------------*                                                 
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO COMM4-CDE-SYSERR.                              
           MOVE SQLCODE       TO RETURN-CODE.                                   
           GOBACK.                                                              
