      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
       PROGRAM-ID.    BP13C870.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  OCT 31 1995.                                              
                                                                                
      *****************************************************************         
      * OBJECTIVE  - TO EXTRACT THE CASES THAT DID NOT SELECT FLAT FOR          
      *              THE QUARTER.                                               
      * INPUT FILES:  BP13F205                                                  
      *               BP13F800                                                  
      * OUTPUT FILES: BP13FOUT                                                  
      * REPORT:       - NIL                                                     
      * SUBROUTINES   - NIL                                                     
      * IDENTICAL PGM -                                                         
      * CREATED BY BP130003                                                     
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      * BP130418 080798  SIA  CATER FOR Y2000                                   
      *****************************************************************         
                                                                                
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *===============================================================*         
       CONFIGURATION SECTION.                                                   
      *===============================================================*         
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *===============================================================*         
       INPUT-OUTPUT SECTION.                                                    
      *===============================================================*         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN        TO BP13F205.                           
                                                                                
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
                                                                                
           SELECT BP13FOUT ASSIGN        TO BP13FOUT.                           
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *===============================================================*         
       FILE SECTION.                                                            
      *===============================================================*         
                                                                                
       FD   BP13F205                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 80 CHARACTERS                                       
            BLOCK  CONTAINS  0 RECORDS.                                         
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BP13F800.                                                           
                                                                                
       FD   BP13FOUT                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS.                                        
       01   BP13FOUT-REC                 PIC X(2000).                           
                                                                                
      *===============================================================*         
       WORKING-STORAGE SECTION.                                                 
      *===============================================================*         
                                                                                
       01  WS-F205-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-F800-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-QTR-FND                    PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-Q-FILLERS                  PIC X(8)  VALUE '01040710'.            
       01  WS-QUARTERS REDEFINES WS-Q-FILLERS.                                  
           05 WS-QTRS  OCCURS 4 TIMES    PIC X(2).                              
       01  WS-Q-PTR                      PIC 9(2).                              
                                                                                
       01  WS-PREV-BALLOT                PIC X(6).                              
       01  WS-PREV-BALLOT-9 REDEFINES WS-PREV-BALLOT.                           
           05 WS-PREV-BALLOT-CCYY        PIC 9(4).                              
           05 WS-PREV-BALLOT-MM          PIC 9(2).                              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F205-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-F800-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-FOUT-WRITE          PIC 9(07) VALUE 0.                     
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-CONTROL.                                                            
      *===============================================================*         
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-F800-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       1000-START-UP.                                                           
      *===============================================================*         
                                                                                
            OPEN INPUT  BP13F205                                                
                        BP13F800                                                
                OUTPUT  BP13FOUT.                                               
                                                                                
                                                                                
            PERFORM 8100-READ-F205   THRU 8100-EXIT.                            
            PERFORM 8200-READ-F800   THRU 8200-EXIT.                            
                                                                                
            IF F205-DTE-BALLOT = SPACES                                         
               DISPLAY 'F205-DTE-BALLOT IS SPACES.'                             
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       2000-MAIN-ROUTINE.                                                       
      *===============================================================*         
      *---------------------------------------------------------------*         
      * READS BP13F800 AND PROCESS EACH RECORD UNTIL END OF FILE      *         
      *---------------------------------------------------------------*         
            PERFORM 2100-GET-PREV-QUARTER THRU 2100-EXIT.                       
            IF F800-DTE-BALLOT = F205-DTE-BALLOT OR                             
               F800-DTE-BALLOT = WS-PREV-BALLOT                                 
               PERFORM 8300-WRITE-FOUT   THRU 8300-EXIT                         
            END-IF.                                                             
            PERFORM 8200-READ-F800       THRU 8200-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       2100-GET-PREV-QUARTER.                                                   
      *===============================================================*         
                                                                                
           MOVE 'N'                  TO WS-QTR-FND.                             
           MOVE F205-DTE-BALLOT      TO WS-PREV-BALLOT.                         
           PERFORM VARYING WS-Q-PTR FROM 1 BY 1                                 
                     UNTIL WS-Q-PTR > 4 OR WS-QTR-FND = 'Y'                     
               IF WS-QTRS(WS-Q-PTR) = F205-DTE-BALLOT(5:2)                      
                  MOVE 'Y'        TO WS-QTR-FND                                 
                  SUBTRACT 1      FROM WS-Q-PTR                                 
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF WS-QTR-FND = 'Y'                                                  
              IF WS-Q-PTR > 1                                                   
                 SUBTRACT 1       FROM WS-Q-PTR                                 
                 MOVE WS-QTRS(WS-Q-PTR)  TO WS-PREV-BALLOT-MM                   
              ELSE                                                              
                 MOVE 4           TO WS-Q-PTR                                   
                 MOVE WS-QTRS(WS-Q-PTR)  TO WS-PREV-BALLOT-MM                   
                 SUBTRACT 1       FROM WS-PREV-BALLOT-CCYY                      
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'F205-DTE-BALLOT ERROR: ' F205-DTE-BALLOT                 
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8100-READ-F205.                                                          
      *===============================================================*         
                                                                                
           READ BP13F205                                                        
                AT END MOVE 'Y'  TO  WS-F205-EOF.                               
                                                                                
           IF WS-F205-EOF = 'Y'                                                 
              DISPLAY 'NO RECORD FOUND FOR BP13F205!'                           
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8200-READ-F800.                                                          
      *===============================================================*         
                                                                                
           READ BP13F800                                                        
                AT END MOVE 'Y'  TO  WS-F800-EOF.                               
                                                                                
           ADD 1                 TO WS-CNT-F800-READ.                           
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       8300-WRITE-FOUT.                                                         
      *===============================================================*         
                                                                                
           WRITE BP13FOUT-REC  FROM BP13F800-MASTER.                            
           ADD   1             TO WS-CNT-FOUT-WRITE.                            
                                                                                
       8300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       9900-HANDLE-ABEND.                                                       
      *******************                                                       
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* BP13C870 ABEND TOTALS ********'.                    
           DISPLAY 'PROGRAM-ID : BP13C870'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F800 RECS READ     = ' WS-CNT-F800-READ.          
           DISPLAY 'TOTAL BP13FOUT RECS WRITTEN  = ' WS-CNT-FOUT-WRITE.         
                                                                                
           CLOSE BP13F205 BP13F800 BP13FOUT.                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **************                                                            
       9999-WRAP-UP.                                                            
      **************                                                            
                                                                                
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C870'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F800 RECS READ     = ' WS-CNT-F800-READ.          
           DISPLAY 'TOTAL BP13FOUT RECS WRITTEN  = ' WS-CNT-FOUT-WRITE.         
                                                                                
           CLOSE BP13F800 BP13F205                                              
                 BP13FOUT.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
