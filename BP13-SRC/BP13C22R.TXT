       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22R.                                                 
       AUTHOR.        LLOYD CAPARRO.                                            
      *                                                                         
      ************************************************************              
      *               SYSTEM OF COMMITMENT (SOC)                 *              
      *==========================================================*              
      * DATE WRITTEN      : 05 APR 2005                          *              
      *                                                          *              
      * 1. OBJECTIVE      : UPDATE BP13K230 AND BP13K240 FOR     *              
      *                     EVERY RECORD FOUND IN BM06F110       *              
      *                                                          *              
      * 2. INPUT          : BM06F110                             *              
      *                                                          *              
      * 3. I/O            : BP13K230 - BP13.K230.FLATLIST        *              
      *                   : BP13K240 - BP13.K240.HEADFLAT        *              
      *----------------------------------------------------------*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  ======== ========== ==== ============================== *              
      *  BP132681 05/04/2005 CG7  NEW PROGRAM.                   *              
      *  BP135572 21/11/2014 ESA1 TO CATER FOR NEIGH&CNTRCT IN   *              
      *                           BP13K240 KEY-FLD               *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F110          ASSIGN TO BM06F110.                         
                                                                                
           SELECT BP13K230          ASSIGN TO BP13K230                          
                                    ACCESS MODE IS RANDOM                       
                                    ORGANIZATION IS INDEXED                     
                                    RECORD KEY IS K230-KEY-FLD                  
                                    STATUS IS WS-K230-STATUS.                   
                                                                                
           SELECT BP13K240          ASSIGN TO BP13K240                          
                                    ACCESS MODE IS RANDOM                       
                                    ORGANIZATION IS INDEXED                     
                                    RECORD KEY IS K240-KEY-FLD                  
                                    STATUS IS WS-K240-STATUS.                   
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
       FD   BM06F110                                                            
                                    BLOCK CONTAINS 0 RECORDS                    
                                    RECORD CONTAINS 500 CHARACTERS              
                                    RECORDING MODE IS F                         
                                    LABEL RECORD IS STANDARD.                   
       COPY BM06F110.                                                           
                                                                                
       FD   BP13K230                                                            
                                    RECORD CONTAINS 450 CHARACTERS.             
       COPY BP13K230.                                                           
                                                                                
       FD   BP13K240                                                            
                                    RECORD CONTAINS 450 CHARACTERS.             
       COPY BP13K240.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K230-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K240-STATUS            PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F110-EOF               PIC X(01)   VALUE 'N'.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F110-READ              PIC 9(06)  VALUE ZEROES.               
           05  WS-K230-FND               PIC 9(06)  VALUE ZEROES.               
           05  WS-K230-NOTFND            PIC 9(06)  VALUE ZEROES.               
           05  WS-K240-FND               PIC 9(06)  VALUE ZEROES.               
           05  WS-K240-NOTFND            PIC 9(06)  VALUE ZEROES.               
           05  WS-K230-REWRT             PIC 9(06)  VALUE ZEROES.               
           05  WS-K240-REWRT             PIC 9(06)  VALUE ZEROES.               
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(08)  VALUE SPACES.               
           05  WS-CURRENT-TIME           PIC X(09)  VALUE SPACES.               
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-CURRENT-TIME1  PIC X(08).                             
                   10  WS-GM             PIC X(01).                             
           05  FILLER                    PIC X(04)  VALUE SPACES.               
                                                                                
       01  WS-TEMPORARY-VARIABLES.                                              
           05  WS-RUN-DATE               PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORD THRU 2000-EXIT                           
             UNTIL WS-F110-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BM06F110                                                 
                I-O    BP13K230                                                 
                       BP13K240.                                                
                                                                                
           IF WS-K230-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K230 OPEN ERROR ' WS-K230-STATUS                     
              MOVE WS-K230-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K240-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K240 OPEN ERROR ' WS-K240-STATUS                     
              MOVE WS-K240-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BM06F110 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BM06F110.                                                      
      *************************************************************             
           READ BM06F110                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F110-EOF                         
                MOVE HIGH-VALUES         TO F110-KEY-FLD                        
            NOT AT END                                                          
                ADD  1                   TO WS-F110-READ                        
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORD.                                                     
      *************************************************************             
           MOVE SPACES                   TO K230-DETAIL-REC.                    
           INITIALIZE                       K230-DETAIL-REC.                    
                                                                                
           MOVE F110-KEY-FLD             TO K230-KEY-FLD                        
                                                                                
           READ BP13K230.                                                       
                                                                                
           EVALUATE WS-K230-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K230-FND                         
                                                                                
                    PERFORM 3000-READ-BP13K240 THRU 3000-EXIT                   
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'K230 REC NOT FOUND, KEY: ' K230-KEY-FLD            
                    ADD 1                TO WS-K230-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K230 READ ERROR ' WS-K230-STATUS               
                    MOVE WS-K230-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           PERFORM 1100-READ-BM06F110 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-READ-BP13K240.                                                      
      *************************************************************             
           MOVE SPACES                   TO K240-HEADER-REC.                    
           INITIALIZE                       K240-HEADER-REC.                    
                                                                                
           MOVE '01'                     TO K240-CDE-HEADER.                    
           MOVE K230-DTE-ALLOC           TO K240-DTE-ALLOC.                     
           MOVE F110-NEIGHBOURHOOD       TO K240-NUM-NEIGH.                     
           MOVE F110-CONTRACT-NO         TO K240-NUM-CNTRCT.                    
           MOVE K230-NUM-NEW-TOWN        TO K240-NUM-NEW-TOWN.                  
           MOVE K230-NUM-FLAT-TYPE       TO K240-NUM-FLAT-TYPE.                 
           MOVE K230-NUM-BLK             TO K240-NUM-BLK.                       
                                                                                
           READ BP13K240.                                                       
                                                                                
           EVALUATE WS-K240-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K240-FND                         
                                                                                
                    PERFORM 3100-PREP-UPD-K230-K240 THRU 3100-EXIT              
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'K240 REC NOT FOUND, KEY: ' K240-KEY-FLD            
                    ADD 1                TO WS-K240-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K240 READ ERROR ' WS-K240-STATUS               
                    MOVE WS-K240-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-PREP-UPD-K230-K240.                                                 
      *************************************************************             
      *----FOR BP13K240                                                         
           IF K230-NUM-CAT  =  '1'                                              
              SUBTRACT 1                 FROM K240-NUM-TOT-CAT1                 
           END-IF.                                                              
                                                                                
           IF K230-NUM-CAT  =  '2'                                              
              SUBTRACT 1                 FROM K240-NUM-TOT-CAT2                 
           END-IF.                                                              
                                                                                
           IF K230-NUM-CAT  =  '3' OR '4'                                       
              SUBTRACT 1                 FROM K240-NUM-TOT-CAT3                 
           END-IF.                                                              
                                                                                
           IF ((K240-NUM-TOT-CAT1  = ZEROES) AND                                
               (K240-NUM-MAX-CAT1 = ZEROES)) OR                                 
               (K240-NUM-TOT-CAT1 < K240-NUM-MAX-CAT1)                          
                MOVE SPACES              TO K240-NUM-CAT1-FULL                  
           END-IF.                                                              
                                                                                
           IF ((K240-NUM-TOT-CAT2  = ZEROES) AND                                
               (K240-NUM-MAX-CAT2 = ZEROES)) OR                                 
               (K240-NUM-TOT-CAT2 < K240-NUM-MAX-CAT2)                          
                MOVE SPACES              TO K240-NUM-CAT2-FULL                  
           END-IF.                                                              
                                                                                
           IF ((K240-NUM-TOT-CAT3  = ZEROES) AND                                
               (K240-NUM-MAX-CAT3 = ZEROES)) OR                                 
               (K240-NUM-TOT-CAT3 < K240-NUM-MAX-CAT3)                          
                MOVE SPACES              TO K240-NUM-CAT3-FULL                  
           END-IF.                                                              
                                                                                
           MOVE WS-CURRENT-DATE1         TO K240-DTE-UPDATE.                    
           MOVE WS-CURRENT-TIME1         TO K240-TME-UPDATE.                    
           MOVE 'BP13C22R'               TO K240-NUM-USERID.                    
                                                                                
           PERFORM 4100-REWRITE-BP13K240 THRU 4100-EXIT.                        
                                                                                
      *----FOR BP13K230                                                         
           MOVE 'B'                      TO K230-NUM-ACCEPT.                    
           MOVE SPACES                   TO K230-NUM-CAT.                       
           MOVE WS-CURRENT-DATE1         TO K230-DTE-ACCEPT.                    
           MOVE WS-CURRENT-TIME1         TO K230-TME-ACCEPT.                    
           MOVE 'BP13C22R'               TO K230-NUM-USERID.                    
                                                                                
           PERFORM 4000-REWRITE-BP13K230 THRU 4000-EXIT.                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       4000-REWRITE-BP13K230.                                                   
      *************************************************************             
           REWRITE K230-DETAIL-REC.                                             
                                                                                
           EVALUATE WS-K230-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K230-REWRT                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K230 REWRITE ERROR ' WS-K230-STATUS            
                    MOVE WS-K230-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       4100-REWRITE-BP13K240.                                                   
      *************************************************************             
           REWRITE K240-HEADER-REC.                                             
                                                                                
           EVALUATE WS-K240-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K240-REWRT                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K240 REWRITE ERROR ' WS-K240-STATUS            
                    MOVE WS-K240-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C22R CONTROL TOTAL -------*'.                  
           DISPLAY ' RUN DATE : ' WS-RUN-DATE.                                  
           DISPLAY '   '.                                                       
           DISPLAY ' RECS READ FROM BM06K110 : ' WS-F110-READ.                  
           DISPLAY ' RECS FOUND IN K230      : ' WS-K230-FND.                   
           DISPLAY ' RECS NOT FOUND IN K230  : ' WS-K230-NOTFND.                
           DISPLAY ' RECS FOUND IN K240      : ' WS-K240-FND.                   
           DISPLAY ' RECS NOT FOUND IN K240  : ' WS-K240-NOTFND.                
           DISPLAY ' RECS REWRITTEN TO K230  : ' WS-K230-REWRT.                 
           DISPLAY ' RECS REWRITTEN TO K240  : ' WS-K240-REWRT.                 
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BM06F110                                                       
                 BP13K230                                                       
                 BP13K240.                                                      
                                                                                
           IF WS-K230-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K230 CLOSE ERROR ' WS-K230-STATUS                    
              MOVE WS-K230-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K240-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K240 CLOSE ERROR ' WS-K240-STATUS                    
              MOVE WS-K240-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
