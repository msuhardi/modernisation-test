       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C38B.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  15/01/2010.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : CREATE MAIL FILE FOR 2ND CA TRANS ON MAILFILE    *        
      * BP134003  ESD1  160810  NEW PROGRAM                            *        
      * BP133884  ESD1  181010  CATER TO ADD THE BLK AND ESTATE        *        
      *                         FIELD ON THE MAIL REPORT               *        
      * BP134186  ESD1  130411  CATER TO CREATE MAILREPORT FOR SPU     *        
      * BP134225  ESD1  130511  CHANGE CSL13 to CAH1                   *        
      * BP134186  ESD1  090911  CATER TO ADD NEW REPORT FOR SPU        *        
      * BP135537  RJB1  250215  CHANGE THE EMAIL RECIPIENT FROM        *        
      *                         NICKNAME INTO COMPLETE NAME            *        
      * BP136210  PCL4  230916  REPORT ON NON-UPDATE FOR 2ND CA (SERS) *        
      *                         FOR SRS                                *        
      * BP137316  DN8   031018  TO INCLUDE PCD,HLE REFERENCE NUMBER    *        
      *                         KEY AVAIL DATE IN THE REPORTS.         *        
      * BP137626  DN8   290119  TO ADD ALLC-SCHEME IN THE REPORT ON    *        
      *                         NON-UPDATE FOR 2ND CA                  *        
      * BP137316  DN8   300119  CATER TO FIX BUGS- 2ND CA FOR SERS     *        
      * BP138047  KAC1  251119  RECOMPILE FOR BP27 COPYBOOK UPDATE     *        
      * BP139564  KAC1  030623 REPLACE LIM KOK CHUN TO ZANY *    * *   *        
      *                        REPLACE BERNICE GAN   TO HEE_WAN_MENG   *        
      *                        REPLACE kong yoke pheng to david soh*   *        
      * BP139175  KAC1  230424 REPLACE zany with Soffia_RAPHAY@hdb.gov.s        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP27K800  ASSIGN        TO BP27K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                   RECORD KEY IS K800-NUM-HLA OF BP27K800-MASTER                
                             FILE STATUS   IS WS-K80027-STATUS.                 
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BM06K100  ASSIGN        TO BM06K100                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K100-KEY-FLD                      
                             FILE STATUS   IS WS-K100-STATUS.                   
                                                                                
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
                                                                                
           SELECT MAILFILE  ASSIGN        TO MAILFILE.                          
           SELECT SERSFILE  ASSIGN        TO SERSFILE.                          
           SELECT RPT1FILE  ASSIGN        TO RPT1FILE.                          
                                                                                
           SELECT F800MAIL  ASSIGN        TO F800MAIL.                          
           SELECT F800SERS  ASSIGN        TO F800SERS.                          
           SELECT F800RPT1  ASSIGN        TO F800RPT1.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  MAILFILE                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 80 CHARACTERS.                                       
       01  MAILFILE-REC.                                                        
           05 MAIL-REGN       PIC X(8).                                         
           05 MAIL-DESC       PIC X(32).                                        
           05 MAIL-ESTBLK     PIC X(20).                                        
           05 FILLER          PIC X(20).                                        
                                                                                
       FD  SERSFILE                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
       01  SERSFILE-REC.                                                        
           05 WS-SERS-ADDRESS           PIC X(60).                              
           05 WS-NEW-FLAT-ADDRESS       PIC X(60).                              
           05 WS-SERS-REGN              PIC X(8).                               
           05 WS-SERS-ALLOC-MODE        PIC X(3).                               
           05 WS-SERS-PORT-TAG          PIC X(1).                               
           05 WS-SERS-LOAN-TAG          PIC X(1).                               
           05 WS-SERS-FINAL-ASSES       PIC X(1).                               
           05 WS-SERS-HLE-APPLN         PIC X(10).                              
           05 WS-SERS-HLE-STATUS        PIC X(03).                              
           05 WS-SERS-REASON            PIC X(32).                              
                                                                                
       FD  RPT1FILE                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
       01  RPT1FILE-REC.                                                        
           05 WS-RPT1-ADDRESS           PIC X(60).                              
           05 WS-NEW-RPT1-ADDRESS       PIC X(60).                              
           05 WS-RPT1-REGN              PIC X(8).                               
           05 WS-RPT1-ALLOC-MODE        PIC X(3).                               
           05 WS-RPT1-PORT-TAG          PIC X(1).                               
           05 WS-RPT1-LOAN-TAG          PIC X(1).                               
                                                                                
       FD  F800MAIL                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 80 CHARACTERS.                                       
       01  F800MAIL-REC                PIC X(80).                               
                                                                                
       FD  F800SERS                                                             
           RECORDING MODE IS F                                                  
      *    RECORD CONTAINS 250 CHARACTERS.                                      
           RECORD CONTAINS 300 CHARACTERS.                                      
       01  F800SERS-REC                PIC X(300).                              
                                                                                
       FD  F800RPT1                                                             
           RECORDING MODE IS F                                                  
      *    RECORD CONTAINS 200 CHARACTERS.                                      
           RECORD CONTAINS 250 CHARACTERS.                                      
       01  F800RPT1-REC                PIC X(250).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  VARIABLES.                                                           
           05  WS-F310-EOF                  PIC X      VALUE 'N'.               
           05  WS-SERS-EOF                  PIC X      VALUE 'N'.               
           05  WS-RPT1-EOF                  PIC X      VALUE 'N'.               
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
       01  WS-K800-STATUS                   PIC 9(2)   VALUE ZEROES.            
       01  WS-K100-STATUS                   PIC 9(2)   VALUE ZEROES.            
       01  WS-K110-STATUS                   PIC 9(2)   VALUE ZEROES.            
       01  WS-DTE-APPLN                     PIC X(8)   VALUE SPACES.            
       01  WS-SERS-DTE-PACD                 PIC X(8)   VALUE SPACES.            
       01  WS-DTE-PACD                      PIC X(16)  VALUE SPACES.            
       01  WS-SERS-KEY-AVAIL-DTE            PIC X(8)   VALUE SPACES.            
       01  WS-RPT1-DTE-PACD                 PIC X(8)   VALUE SPACES.            
       01  WS-RPT1-KEY-AVAIL-DTE            PIC X(8)   VALUE SPACES.            
       01  WS-K80027-STATUS                 PIC 9(2)   VALUE ZEROES.            
       01  WS-F800LEAS-COUNT                PIC 9(7)   VALUE ZERO.              
       01  WS-F800RPT-COUNT                 PIC 9(7)   VALUE ZERO.              
       01  WS-F800SER-COUNT                 PIC 9(7)   VALUE ZERO.              
       01  WS-F800RPT1-COUNT                PIC 9(7)   VALUE ZERO.              
       01  WS-FIRST-REPORT                  PIC X      VALUE 'Y'.               
       01  WS-FIRST-SERS                    PIC X      VALUE 'Y'.               
       01  WS-FIRST-RPT1                    PIC X      VALUE 'Y'.               
                                                                                
       01  WS-DATE                          PIC X(8)  VALUE SPACES.             
       01  WS-MONTH                         PIC X(3)  VALUE SPACES.             
       01  WS-QUARTER                       PIC X(11) VALUE SPACES.             
       01  WS-YEAR                          PIC 9(4).                           
       01  WS-MONTHS-IN-DIGITS-AND-WORDS.                                       
           05 FILLER                        PIC X(06) VALUE                     
             '01 JAN'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '02 FEB'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '03 MAR'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '04 APR'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '05 MAY'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '06 JUN'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '07 JUL'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '08 AUG'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '09 SEP'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '10 OCT'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '11 NOV'.                                                          
           05 FILLER                        PIC X(06) VALUE                     
             '12 DEC'.                                                          
       01  FILLER  REDEFINES  WS-MONTHS-IN-DIGITS-AND-WORDS.                    
           05 WS-MONTH-TABLE               OCCURS 12                            
                                           INDEXED BY WS-MTH-IND.               
              10 WS-MONTH-IN-DIGITS         PIC X(02).                          
              10 FILLER                     PIC X(01).                          
              10 WS-MONTH-IN-WORDS          PIC X(03).                          
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-HEADER1-RPT.                                                  
               10 FILLER                    PIC X(24) VALUE SPACES.             
               10 FILLER                    PIC X(31)                           
                              VALUE 'REPORT ON NON-UPDATE FOR 2ND CA'.          
               10 FILLER PIC X(25) VALUE SPACES.                                
                                                                                
           05  WS-HEADER1A-RPT.                                                 
               10 FILLER                    PIC X(45) VALUE SPACES.             
               10 FILLER                    PIC X(45)                           
                  VALUE 'REPORT ON NON-UPDATE FOR 2ND CA(SERS CASES)'.          
                                                                                
           05  WS-HEADER1B-RPT.                                                 
               10 FILLER                    PIC X(45) VALUE SPACES.             
               10 FILLER                    PIC X(55)                           
                  VALUE 'REPORT ON SUCCESSFUL UPDATE FOR 2ND CA (SERS CA        
      -                 'SES)'.                                                 
           05  WS-HEADER2-RPT.                                                  
               10 FILLER                  PIC X(05) VALUE SPACES.               
               10 FILLER                  PIC X(05) VALUE 'S/NO.'.              
               10 FILLER                  PIC X(04) VALUE SPACES.               
               10 FILLER                  PIC X(07) VALUE 'REGN NO'.            
               10 FILLER                  PIC X(04) VALUE SPACES.               
               10 FILLER                  PIC X(27)                             
                              VALUE 'BLK - ALLOC(MODE)/LOAN/PORT'.              
               10 FILLER                  PIC X(03) VALUE SPACES.               
               10 FILLER                  PIC X(22)                             
                              VALUE 'REASONS FOR NON-UPDATE'.                   
                                                                                
           05  WS-HEADER2A-RPT.                                                 
               10 FILLER                  PIC X(02) VALUE SPACES.               
               10 FILLER                  PIC X(05) VALUE 'S/NO.'.              
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(17)                             
                                      VALUE 'SERS FLAT ADDRESS'.                
               10 FILLER                  PIC X(44) VALUE SPACES.               
               10 FILLER                  PIC X(25)                             
                                      VALUE 'REPLACEMENT FLAT ADDRESS'.         
               10 FILLER                  PIC X(37) VALUE SPACES.               
               10 FILLER                  PIC X(10) VALUE '  PCD   '.           
               10 FILLER                  PIC X(10) VALUE SPACES.               
               10 FILLER                  PIC X(14)                             
                                      VALUE 'KEY-AVAIL-DATE'.                   
               10 FILLER                  PIC X(09) VALUE ' REGN NO '.          
               10 FILLER                  PIC X(12)                             
                                          VALUE ' ALLOC MODE '.                 
               10 FILLER                  PIC X(18)                             
                                       VALUE 'PORT TAG LOAN TAG '.              
               10 FILLER                  PIC X(13)                             
                                       VALUE ' FINAL ASSM  '.                   
               10 FILLER                  PIC X(16)                             
                                       VALUE 'HLE APPLN DATE  '.                
               10 FILLER                  PIC X(12)                             
                                       VALUE 'HLE STATUS  '.                    
               10 FILLER                  PIC X(24)                             
                                      VALUE 'REASONS FOR NON-UPDATE'.           
               10 FILLER                  PIC X(29) VALUE SPACES.               
                                                                                
           05  WS-HEADER2B-RPT.                                                 
               10 FILLER                  PIC X(02) VALUE SPACES.               
               10 FILLER                  PIC X(05) VALUE 'S/NO.'.              
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(17)                             
                                      VALUE 'SERS FLAT ADDRESS'.                
               10 FILLER                  PIC X(44) VALUE SPACES.               
               10 FILLER                  PIC X(25)                             
                                      VALUE 'REPLACEMENT FLAT ADDRESS'.         
               10 FILLER                  PIC X(37) VALUE SPACES.               
               10 FILLER                  PIC X(10) VALUE '  PCD   '.           
               10 FILLER                  PIC X(10) VALUE SPACES.               
               10 FILLER                  PIC X(14)                             
                                      VALUE 'KEY-AVAIL-DATE'.                   
               10 FILLER                  PIC X(09) VALUE ' REGN NO '.          
               10 FILLER                  PIC X(12)                             
                                          VALUE ' ALLOC MODE '.                 
               10 FILLER                  PIC X(18)                             
                                       VALUE 'PORT TAG LOAN TAG '.              
               10 FILLER                  PIC X(17)                             
                                       VALUE 'HLE REFERENCE NO '.               
               10 FILLER                  PIC X(29) VALUE SPACES.               
                                                                                
           05  WS-SERS-RPT.                                                     
               10 FILLER                    PIC X(02) VALUE SPACES.             
               10 WS-SERS-SNO-DTL           PIC Z99   VALUE ZERO.               
               10 FILLER                    PIC X(3)  VALUE SPACES.             
               10 WS-SERS-ADD-DTL           PIC X(60) VALUE SPACES.             
               10 FILLER                    PIC X(1)  VALUE SPACES.             
               10 WS-NEW-ADD-DTL            PIC X(60) VALUE SPACES.             
               10 FILLER                    PIC X(2)  VALUE SPACES.             
               10 WS-SERS-PCD-DTL           PIC X(16) VALUE SPACES.             
               10 FILLER                    PIC X(4)  VALUE SPACES.             
               10 WS-SERS-KEY-AVAIL-DTL     PIC X(10) VALUE SPACES.             
               10 FILLER                    PIC X(5)  VALUE SPACES.             
               10 WS-SERS-REGN-DTL          PIC X(8)  VALUE SPACES.             
               10 FILLER                    PIC X(5)  VALUE SPACES.             
               10 WS-SERS-ALLO-DTL          PIC X(3)  VALUE SPACES.             
               10 FILLER                    PIC X(9)  VALUE SPACES.             
               10 WS-SERS-PORT-DTL          PIC X(1)  VALUE SPACES.             
               10 FILLER                    PIC X(4)  VALUE SPACES.             
               10 WS-SERS-LOAN-DTL          PIC X(1)  VALUE SPACES.             
               10 FILLER                    PIC X(9)  VALUE SPACES.             
               10 WS-SERS-FINAL-ASSM-DTL    PIC X(1)  VALUE SPACES.             
               10 FILLER                    PIC X(13) VALUE SPACES.             
               10 WS-SERS-APPNDTE-DTL       PIC X(10) VALUE SPACES.             
               10 FILLER                    PIC X(08) VALUE SPACES.             
               10 WS-SERS-STAT-DTL          PIC X(3)  VALUE SPACES.             
               10 FILLER                    PIC X(05) VALUE SPACES.             
               10 WS-SERS-REASON-DTL        PIC X(32) VALUE SPACES.             
               10 FILLER                    PIC X(22) VALUE SPACES.             
                                                                                
           05  WS-RPT1-RPT.                                                     
               10 FILLER                    PIC X(02) VALUE SPACES.             
               10 WS-RPT1-SNO-DTL           PIC Z99   VALUE ZERO.               
               10 FILLER                    PIC X(3) VALUE SPACES.              
               10 WS-RPT1-ADD-DTL           PIC X(60)  VALUE SPACES.            
               10 FILLER                    PIC X(1)  VALUE SPACES.             
               10 WS-NEW-RPT1-ADD-DTL       PIC X(60) VALUE SPACES.             
               10 FILLER                    PIC X(2)  VALUE SPACES.             
               10 WS-RPT1-PCD-DTL           PIC X(16) VALUE SPACES.             
               10 FILLER                    PIC X(4)  VALUE SPACES.             
               10 WS-RPT1-AVAIL-DTE-DTL     PIC X(10) VALUE SPACES.             
               10 FILLER                    PIC X(5)  VALUE SPACES.             
               10 WS-RPT1-REGN-DTL          PIC X(8) VALUE SPACES.              
               10 FILLER                    PIC X(4)  VALUE SPACES.             
               10 WS-RPT1-ALLO-DTL          PIC X(3) VALUE SPACES.              
               10 FILLER                    PIC X(9)  VALUE SPACES.             
               10 WS-RPT1-PORT-DTL          PIC X(1) VALUE SPACES.              
               10 FILLER                    PIC X(5) VALUE SPACES.              
               10 WS-RPT1-LOAN-DTL          PIC X(1) VALUE SPACES.              
               10 FILLER                    PIC X(8) VALUE SPACES.              
               10 WS-RPT1-HLE-REF-NO-DTL    PIC X(9) VALUE SPACES.              
               10 FILLER                    PIC X(36) VALUE SPACES.             
                                                                                
           05  WS-DETAIL-RPT.                                                   
               10 FILLER                    PIC X(05) VALUE SPACES.             
               10 WS-SNO-DTL                PIC Z99   VALUE ZERO.               
               10 FILLER                    PIC X(6) VALUE SPACES.              
               10 WS-REGN-DTL               PIC X(8)  VALUE SPACES.             
               10 FILLER                    PIC X(2)  VALUE SPACES.             
               10 WS-ESTBLK-DTL             PIC X(21) VALUE SPACES.             
               10 FILLER                    PIC X(4)  VALUE SPACES.             
               10 WS-REASON-DTL             PIC X(32) VALUE SPACES.             
                                                                                
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(60)                               
               VALUE 'RCPT TO:<DAVID_FG_SOH@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT3A            PIC X(60)                               
               VALUE 'RCPT TO:<CHENG_AH_HONG@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT4             PIC X(60)                               
               VALUE 'RCPT TO:<TENG_SIEW_HONG@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT4A            PIC X(60)                               
               VALUE 'RCPT TO:<HEE_WAN_MENG@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT5             PIC X(60)                               
               VALUE 'RCPT TO:<ASMAH_AMIR@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT6             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT7             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT8             PIC X(60)                               
               VALUE 'TO:<DAVID_FG_SOH@HDB.GOV.SG>'.                            
           05  WS-MAIL-RPT8A            PIC X(60)                               
               VALUE 'TO:<CHENG_AH_HONG@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT9             PIC X(60)                               
               VALUE 'CC:<TENG_SIEW_HONG@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT9A            PIC X(60)                               
               VALUE 'RCPT TO:<HEE_WAN_MENG@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT10            PIC X(60)                               
               VALUE 'CC:<ASMAH_AMIR@HDB.GOV.SG>'.                              
           05  WS-MAIL-RPT11            PIC X(80)                               
               VALUE 'SUBJECT : REPORT ON NON-UPDATE FOR 2ND CA(SERS CAS        
      -        'ES) FOR SRS'.                                                   
           05  WS-MAIL-RPT11A           PIC X(80)                               
               VALUE 'SUBJECT : REPORT ON SUCCESSFUL UPDATE FOR 2ND CA(S        
      -        'ERS CASES) FOR SRS'.                                            
           05  WS-MAIL-RPT11B           PIC X(80)                               
               VALUE 'SUBJECT : REPORT ON NON-UPDATE FOR 2ND CA       '.        
           05  WS-MAIL-RPT12            PIC X(41)                               
               VALUE 'Mime-Version: 1.0                        '.               
           05  WS-MAIL-RPT13            PIC X(80)                               
               VALUE 'Content-type: multipart/mixed; boundary="Mime.Part        
      -         '.Boundary"'.                                                   
           05  WS-MAIL-RPT14            PIC X(80)                               
               VALUE 'This is a multi-part message in MIME format.'.            
           05  WS-MAIL-RPT15            PIC X(80)                               
               VALUE '--Mime.Part.Boundary                        '.            
           05  WS-MAIL-RPT16            PIC X(80)                               
               VALUE 'Content-Type: text/plain; charset="iso-8859-1"'.          
           05  WS-MAIL-RPT17            PIC X(80)                               
               VALUE 'Content-Transfer-Encoding: 7bit               '.          
           05  WS-MAIL-RPT18            PIC X(80)                               
               VALUE 'Content-disposition: attachment;              '.          
           05  WS-MAIL-RPT19            PIC X(80)                               
               VALUE '           FILENAME="UNSUCCESSFUL.TXT"        '.          
           05  WS-MAIL-RPT19A           PIC X(80)                               
               VALUE '           FILENAME="SUCCESSFUL.TXT"          '.          
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-PROCESS-INDATA  THRU 2000-EXIT.                         
           PERFORM 2100-READ-SERSFILE   THRU 2100-EXIT.                         
           PERFORM 2200-READ-RPT1FILE   THRU 2200-EXIT.                         
           PERFORM 3000-GET-ALL-TRANS   THRU 3000-EXIT                          
           UNTIL WS-F310-EOF = 'Y' AND WS-SERS-EOF = 'Y' AND                    
                 WS-RPT1-EOF = 'Y'.                                             
           WRITE   F800SERS-REC FROM WS-MAIL-RPT15.                             
           WRITE   F800RPT1-REC FROM WS-MAIL-RPT15.                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  MAILFILE                                                 
                       SERSFILE                                                 
                       RPT1FILE                                                 
                       BP13K800                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BP27K800                                                 
                OUTPUT F800MAIL                                                 
                       F800SERS                                                 
                       F800RPT1.                                                
                                                                                
           INITIALIZE  MAILFILE-REC                                             
                       SERSFILE-REC                                             
                       RPT1FILE-REC                                             
                       F800MAIL-REC                                             
                       F800SERS-REC                                             
                       F800RPT1-REC.                                            
                                                                                
           IF WS-K80027-STATUS NOT = 00 AND 97                                  
              DISPLAY 'BP27K800 - ERROR OPENING : ' WS-K80027-STATUS            
              MOVE WS-K80027-STATUS                 TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR OPENING : ' WS-K100-STATUS              
              MOVE WS-K100-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       1100-READ-BP13K800.                                                      
      *=============================================================*           
                                                                                
           MOVE SPACES                           TO BP13K800-MASTER             
                                                    BP27K800-MASTER.            
           INITIALIZE                               BP13K800-MASTER             
                                                    BP27K800-MASTER.            
                                                                                
           MOVE WS-SERS-REGN                     TO K800-NUM-REGN.              
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    PERFORM 1200-READ-BP27K800 THRU 1200-EXIT                   
                    PERFORM 1300-READ-BM06K110 THRU 1300-EXIT                   
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO BP13K800-MASTER                              
                    INITIALIZE     BP13K800-MASTER                              
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY:' K800-NUM-REGN                           
                                                                                
                    MOVE WS-K800-STATUS          TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================*           
       1150-READ-RPT1-BP13K800.                                                 
      *=============================================================*           
                                                                                
           MOVE SPACES                           TO BP13K800-MASTER             
           INITIALIZE                               BP13K800-MASTER.            
                                                                                
           MOVE WS-RPT1-REGN                     TO K800-NUM-REGN.              
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    PERFORM 1300-READ-BM06K110 THRU 1300-EXIT                   
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO BP13K800-MASTER                              
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY:'            K800-NUM-REGN                
                                                                                
                    MOVE WS-K800-STATUS          TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       1150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================*           
       1200-READ-BP27K800.                                                      
      *=============================================================*           
                                                                                
           MOVE SPACES                   TO BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
                                                                                
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
                             TO K800-NUM-HLA OF BP27K800-MASTER.                
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE WS-K80027-STATUS                                            
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO BP27K800-MASTER                              
                    INITIALIZE     BP27K800-MASTER                              
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP27K800 READ ERROR ' WS-K80027-STATUS             
                    DISPLAY 'K800 KEY:' K800-NUM-HLA OF BP27K800-MASTER         
                                                                                
                    MOVE WS-K80027-STATUS        TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================*           
       1300-READ-BM06K110.                                                      
      *=============================================================*           
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE K800-NUM-SCH-ACC OF BP13K800-MASTER                             
                             TO K110-SCH-ACC-NO.                                
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    PERFORM 1400-READ-BM06K100 THRU 1400-EXIT                   
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO K110-REC                                     
                    INITIALIZE     K110-REC                                     
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR ' WS-K110-STATUS               
                    DISPLAY 'K110 KEY:' K110-KEY-FLD                            
                                                                                
                    MOVE WS-K110-STATUS        TO RETURN-CODE                   
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       1300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================*           
       1400-READ-BM06K100.                                                      
      *=============================================================*           
                                                                                
           MOVE SPACES                   TO K100-REC.                           
           INITIALIZE                       K100-REC.                           
                                                                                
           MOVE K110-ESTATE          TO K100-ESTATE                             
           MOVE K110-NEIGHBOURHOOD   TO K100-NEIGHBOURHOOD                      
           MOVE K110-CONTRACT-NO     TO K100-CONTRACT-NO                        
           MOVE K110-BLK-NO          TO K100-BLK-NO                             
                                                                                
           READ BM06K100 KEY IS K100-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
                  CONTINUE                                                      
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO K100-REC                                     
                    INITIALIZE     K100-REC                                     
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K100 READ ERROR ' WS-K100-STATUS               
                    DISPLAY 'K100 KEY:' K100-KEY-FLD                            
                                                                                
                    MOVE WS-K100-STATUS        TO RETURN-CODE                   
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       1400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================*           
       2000-PROCESS-INDATA.                                                     
      *=============================================================*           
                                                                                
            READ MAILFILE                                                       
                 AT END MOVE 'Y'  TO WS-F310-EOF                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2100-READ-SERSFILE.                                                      
      *=============================================================*           
                                                                                
            READ SERSFILE                                                       
                 AT END MOVE 'Y'  TO WS-SERS-EOF                                
            END-READ.                                                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2200-READ-RPT1FILE.                                                      
      *=============================================================*           
                                                                                
            READ RPT1FILE                                                       
                 AT END MOVE 'Y'  TO WS-RPT1-EOF                                
            END-READ.                                                           
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-GET-ALL-TRANS.                                                      
      *=============================================================*           
                                                                                
           IF WS-F310-EOF  = 'Y'                                                
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 4000-REPORT-OR-LEAS  THRU 4000-EXIT                       
              PERFORM 2000-PROCESS-INDATA  THRU 2000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SERS-EOF = 'Y'                                                 
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 5000-CREATE-SERS     THRU 5000-EXIT                       
              PERFORM 2100-READ-SERSFILE   THRU 2100-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-RPT1-EOF = 'Y'                                                 
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 5600-CREATE-RPT1     THRU 5600-EXIT                       
              PERFORM 2200-READ-RPT1FILE   THRU 2200-EXIT                       
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-REPORT-OR-LEAS.                                                     
      *================================================================*        
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
           ADD   1   TO   WS-F800RPT-COUNT                                      
           PERFORM 4500-CREATE-REPORT   THRU  4500-EXIT                         
           WRITE  F800MAIL-REC  FROM  WS-DETAIL-RPT.                            
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
            IF WS-FIRST-REPORT = 'Y'                                            
               WRITE F800MAIL-REC FROM WS-MAIL-RPT1                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT2                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT3                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT4                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT4A                            
               WRITE F800MAIL-REC FROM WS-MAIL-RPT5                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT6                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT7                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT8                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT9                             
               WRITE F800MAIL-REC FROM WS-MAIL-RPT9A                            
               WRITE F800MAIL-REC FROM WS-MAIL-RPT10                            
               WRITE F800MAIL-REC FROM WS-MAIL-RPT11B                           
               STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                   
                      DELIMITED BY SIZE INTO WS-DATE-RPT                        
               WRITE F800MAIL-REC FROM WS-DATE-RPT-DTL                          
               MOVE    'N'   TO  WS-FIRST-REPORT                                
               MOVE SPACES TO  F800MAIL-REC                                     
               WRITE   F800MAIL-REC                                             
               WRITE   F800MAIL-REC   FROM  WS-HEADER1-RPT                      
               MOVE SPACES TO  F800MAIL-REC                                     
               WRITE   F800MAIL-REC                                             
               WRITE   F800MAIL-REC   FROM  WS-HEADER2-RPT                      
            END-IF.                                                             
                                                                                
            MOVE WS-F800RPT-COUNT    TO WS-SNO-DTL.                             
            MOVE MAIL-REGN           TO WS-REGN-DTL.                            
            MOVE MAIL-DESC           TO WS-REASON-DTL.                          
            MOVE MAIL-ESTBLK         TO WS-ESTBLK-DTL.                          
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-CREATE-SERS.                                                        
      *================================================================*        
                                                                                
           ADD   1   TO   WS-F800SER-COUNT                                      
           PERFORM 5500-CREATE-SERS-RPT THRU  5500-EXIT                         
           WRITE  F800SERS-REC  FROM  WS-SERS-RPT.                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5500-CREATE-SERS-RPT.                                                    
      *================================================================*        
                                                                                
            IF WS-FIRST-SERS   = 'Y'                                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT1                             
               WRITE F800SERS-REC FROM WS-MAIL-RPT2                             
               WRITE F800SERS-REC FROM WS-MAIL-RPT3A                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT6                             
               WRITE F800SERS-REC FROM WS-MAIL-RPT7                             
               WRITE F800SERS-REC FROM WS-MAIL-RPT8A                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT11                            
               STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                   
                      DELIMITED BY SIZE INTO WS-DATE-RPT                        
               WRITE F800SERS-REC FROM WS-DATE-RPT-DTL                          
               WRITE F800SERS-REC FROM WS-MAIL-RPT12                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT13                            
               MOVE SPACES TO  F800SERS-REC                                     
               WRITE   F800SERS-REC                                             
               WRITE F800SERS-REC FROM WS-MAIL-RPT14                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT15                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT16                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT17                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT18                            
               WRITE F800SERS-REC FROM WS-MAIL-RPT19                            
               MOVE SPACES TO  F800SERS-REC                                     
               WRITE   F800SERS-REC                                             
               MOVE    'N'   TO  WS-FIRST-SERS                                  
               WRITE   F800SERS-REC   FROM  WS-HEADER1A-RPT                     
               MOVE SPACES TO  F800SERS-REC                                     
               WRITE   F800SERS-REC                                             
               WRITE   F800SERS-REC   FROM  WS-HEADER2A-RPT                     
            END-IF.                                                             
                                                                                
            PERFORM 1100-READ-BP13K800 THRU 1100-EXIT.                          
                                                                                
            MOVE WS-F800SER-COUNT      TO WS-SERS-SNO-DTL.                      
            MOVE WS-SERS-ADDRESS       TO WS-SERS-ADD-DTL.                      
            MOVE WS-NEW-FLAT-ADDRESS   TO WS-NEW-ADD-DTL.                       
            MOVE WS-SERS-REGN          TO WS-SERS-REGN-DTL.                     
            MOVE WS-SERS-ALLOC-MODE    TO WS-SERS-ALLO-DTL.                     
            MOVE WS-SERS-PORT-TAG      TO WS-SERS-PORT-DTL.                     
            MOVE WS-SERS-LOAN-TAG      TO WS-SERS-LOAN-DTL.                     
            MOVE WS-SERS-FINAL-ASSES   TO WS-SERS-FINAL-ASSM-DTL.               
                                                                                
            MOVE SPACES                TO WS-SERS-APPNDTE-DTL.                  
            MOVE K800-NUM-HLA-STATUS OF BP13K800-MASTER                         
                                       TO WS-SERS-STAT-DTL.                     
                                                                                
            MOVE K800-DTE-APPLN OF BP27K800-MASTER                              
                                       TO WS-DTE-APPLN.                         
                                                                                
            IF WS-DTE-APPLN NOT = SPACES AND LOW-VALUES AND ZEROES              
               STRING WS-DTE-APPLN(7:2) '/'                                     
                      WS-DTE-APPLN(5:2) '/'                                     
                      WS-DTE-APPLN(1:4)                                         
                 DELIMITED BY SIZE INTO WS-SERS-APPNDTE-DTL                     
                                                                                
            END-IF.                                                             
                                                                                
            MOVE WS-SERS-REASON        TO WS-SERS-REASON-DTL                    
                                                                                
            IF K100-NUM-PACD = 'M'                                              
                PERFORM 5800-MONTHLY-PACD   THRU 5800-EXIT                      
            ELSE                                                                
               IF K100-NUM-PACD = 'Q'                                           
                PERFORM 5900-QUARTERLY-PACD   THRU 5900-EXIT                    
               END-IF                                                           
            END-IF.                                                             
                                                                                
      *     MOVE K100-DTE-PACD-QTR TO WS-SERS-DTE-PACD.                         
            MOVE SPACES            TO WS-SERS-PCD-DTL.                          
            MOVE WS-DTE-PACD       TO WS-SERS-PCD-DTL.                          
                                                                                
            MOVE K100-DTE-KEY-AVAIL TO WS-SERS-KEY-AVAIL-DTE.                   
            MOVE SPACES             TO WS-SERS-KEY-AVAIL-DTL.                   
                                                                                
            IF  WS-SERS-KEY-AVAIL-DTE                                           
                            NOT = SPACES AND LOW-VALUES AND ZEROES              
               STRING WS-SERS-KEY-AVAIL-DTE(7:2) '/'                            
                      WS-SERS-KEY-AVAIL-DTE(5:2) '/'                            
                      WS-SERS-KEY-AVAIL-DTE(1:4)                                
                 DELIMITED BY SIZE INTO WS-SERS-KEY-AVAIL-DTL                   
            END-IF.                                                             
                                                                                
            INITIALIZE WS-SERS-KEY-AVAIL-DTE    WS-DTE-PACD                     
                       K100-REC.                                                
                                                                                
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5600-CREATE-RPT1.                                                        
      *================================================================*        
                                                                                
           ADD   1   TO   WS-F800RPT1-COUNT                                     
           PERFORM 5700-CREATE-RPT1-RPT THRU  5700-EXIT                         
           WRITE  F800RPT1-REC  FROM  WS-RPT1-RPT.                              
                                                                                
       5600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5700-CREATE-RPT1-RPT.                                                    
      *================================================================*        
                                                                                
            IF WS-FIRST-RPT1   = 'Y'                                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT1                             
               WRITE F800RPT1-REC FROM WS-MAIL-RPT2                             
               WRITE F800RPT1-REC FROM WS-MAIL-RPT3A                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT6                             
               WRITE F800RPT1-REC FROM WS-MAIL-RPT7                             
               WRITE F800RPT1-REC FROM WS-MAIL-RPT8A                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT11A                           
               STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                   
                      DELIMITED BY SIZE INTO WS-DATE-RPT                        
               WRITE F800RPT1-REC FROM WS-DATE-RPT-DTL                          
               WRITE F800RPT1-REC FROM WS-MAIL-RPT12                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT13                            
               MOVE SPACES TO  F800RPT1-REC                                     
               WRITE   F800RPT1-REC                                             
               WRITE F800RPT1-REC FROM WS-MAIL-RPT14                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT15                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT16                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT17                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT18                            
               WRITE F800RPT1-REC FROM WS-MAIL-RPT19A                           
               MOVE SPACES TO  F800RPT1-REC                                     
               WRITE   F800RPT1-REC                                             
               MOVE    'N'   TO  WS-FIRST-RPT1                                  
               WRITE   F800RPT1-REC   FROM  WS-HEADER1B-RPT                     
               MOVE SPACES TO  F800RPT1-REC                                     
               WRITE   F800RPT1-REC                                             
               WRITE   F800RPT1-REC   FROM  WS-HEADER2B-RPT                     
            END-IF.                                                             
                                                                                
            MOVE WS-F800RPT1-COUNT     TO WS-RPT1-SNO-DTL.                      
            MOVE WS-RPT1-ADDRESS       TO WS-RPT1-ADD-DTL.                      
            MOVE WS-NEW-RPT1-ADDRESS   TO WS-NEW-RPT1-ADD-DTL.                  
            MOVE WS-RPT1-REGN          TO WS-RPT1-REGN-DTL.                     
            MOVE WS-RPT1-ALLOC-MODE    TO WS-RPT1-ALLO-DTL.                     
            MOVE WS-RPT1-PORT-TAG      TO WS-RPT1-PORT-DTL.                     
            MOVE WS-RPT1-LOAN-TAG      TO WS-RPT1-LOAN-DTL.                     
                                                                                
            PERFORM 1150-READ-RPT1-BP13K800 THRU 1150-EXIT.                     
                                                                                
            IF K100-NUM-PACD = 'M'                                              
                PERFORM 5800-MONTHLY-PACD   THRU 5800-EXIT                      
            ELSE                                                                
               IF K100-NUM-PACD = 'Q'                                           
                PERFORM 5900-QUARTERLY-PACD   THRU 5900-EXIT                    
               END-IF                                                           
            END-IF.                                                             
                                                                                
      *     MOVE K100-DTE-PACD-QTR TO WS-RPT1-DTE-PACD.                         
            MOVE SPACES            TO WS-RPT1-PCD-DTL.                          
            MOVE WS-DTE-PACD       TO WS-RPT1-PCD-DTL.                          
                                                                                
            MOVE K100-DTE-KEY-AVAIL TO WS-RPT1-KEY-AVAIL-DTE.                   
            MOVE SPACES             TO WS-RPT1-AVAIL-DTE-DTL.                   
                                                                                
            IF  WS-RPT1-KEY-AVAIL-DTE                                           
                            NOT = SPACES AND LOW-VALUES AND ZEROES              
               STRING WS-RPT1-KEY-AVAIL-DTE(7:2) '/'                            
                      WS-RPT1-KEY-AVAIL-DTE(5:2) '/'                            
                      WS-RPT1-KEY-AVAIL-DTE(1:4)                                
                 DELIMITED BY SIZE INTO WS-RPT1-AVAIL-DTE-DTL                   
            END-IF.                                                             
                                                                                
      *     MOVE K800-NUM-SCH-ACC(1:9) TO WS-RPT1-HLE-REF-NO-DTL.               
            MOVE K800-NUM-HLA OF BP13K800-MASTER                                
                                       TO WS-RPT1-HLE-REF-NO-DTL.               
                                                                                
            INITIALIZE WS-RPT1-KEY-AVAIL-DTE    WS-DTE-PACD                     
                       K100-REC.                                                
       5700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5800-MONTHLY-PACD.                                                       
      *================================================================*        
           SET WS-MTH-IND TO 1.                                                 
                                                                                
           SEARCH WS-MONTH-TABLE                                                
               AT END                                                           
               MOVE '   ' TO WS-MONTH                                           
            WHEN WS-MONTH-IN-DIGITS(WS-MTH-IND) = K100-DTE-PACD-QTR(5:2)        
               MOVE WS-MONTH-IN-WORDS(WS-MTH-IND) TO WS-MONTH                   
           END-SEARCH.                                                          
                                                                                
           MOVE K100-DTE-PACD-QTR(1:4)       TO WS-YEAR.                        
                                                                                
           STRING WS-MONTH ' ' WS-YEAR                                          
                  DELIMITED BY SIZE                                             
                  INTO WS-DTE-PACD                                              
           END-STRING.                                                          
                                                                                
       5800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5900-QUARTERLY-PACD.                                                     
      *================================================================*        
           IF K100-DTE-PACD-QTR(5:2) = '01' OR '02' OR '03'                     
              MOVE '1ST QUARTER' TO WS-QUARTER                                  
           ELSE                                                                 
              IF K100-DTE-PACD-QTR(5:2) = '04' OR '05' OR '06'                  
                 MOVE '2ND QUARTER' TO WS-QUARTER                               
              ELSE                                                              
                 IF K100-DTE-PACD-QTR(5:2) = '07' OR '08' OR '09'               
                    MOVE '3RD QUARTER' TO WS-QUARTER                            
                 ELSE                                                           
                    MOVE '4TH QUARTER' TO WS-QUARTER                            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K100-DTE-PACD-QTR(1:4)       TO WS-YEAR.                        
                                                                                
           STRING WS-QUARTER ' ' WS-YEAR                                        
                  DELIMITED BY SIZE                                             
                  INTO WS-DTE-PACD                                              
           END-STRING.                                                          
                                                                                
       5900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
                                                                                
           DISPLAY 'TOTAL RECORD WRITTEN TO REPORT FILE : '                     
                   WS-F800RPT-COUNT.                                            
                                                                                
           CLOSE MAILFILE                                                       
                 SERSFILE                                                       
                 RPT1FILE                                                       
                 F800MAIL                                                       
                 F800SERS                                                       
                 F800RPT1                                                       
                 BP13K800                                                       
                 BM06K100                                                       
                 BM06K110                                                       
                 BP27K800.                                                      
                                                                                
           IF WS-K80027-STATUS NOT = 00 AND 97                                  
              DISPLAY 'BP27K800 - ERROR CLOSING : ' WS-K80027-STATUS            
              MOVE WS-K80027-STATUS                 TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR CLOSING : ' WS-K100-STATUS              
              MOVE WS-K100-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
