       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB82.                                                 
      *AUTHOR.        ZDD1                                                      
      *DATE-WRITTEN.  12JAN2010.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO HOUSEKEEP BP13KH10                            *                
      *                                                        *                
      *                                                        *                
      *                                                        *                
      *    INPUT FILES  : BP13FH10                             *                
      *    I-O          : BP13KH10                             *                
      *    OUTPUT       : BP13KH12                             *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP133818 ZDD 20100112  NEW PGM                          *                
      *BP135039 IMC 20130926  CATER FOR FTS, FT2T, ST CASES    *                
      *BP135785 FNP 20150514  CHECK FOR FT CASES LAST          *                
      *BP139545 FKH 20231025  POLICY CHANGE STARTING FROM      *                
      *                       202310                           *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FH10 ASSIGN TO BP13FH10.                                  
                                                                                
           SELECT BP13KH10 ASSIGN TO BP13KH10                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH10-KEY-FLD                               
                  FILE STATUS     IS WS-KH10-STATUS.                            
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13KH12 ASSIGN TO BP13KH12                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH12-KEY-FLD                               
                  FILE STATUS     IS WS-KH12-STATUS.                            
                                                                                
           SELECT P13FH10A ASSIGN TO P13FH10A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FH10                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0  RECORDS                                        
            LABEL RECORDS   ARE  STANDARD                                       
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13FH10.                                                           
                                                                                
       FD   BP13KH10                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13KH10.                                                           
                                                                                
       FD   BP13KH12                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13KH12.                                                           
                                                                                
       FD   P13FH10A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0  RECORDS                                        
            LABEL RECORDS   ARE  STANDARD                                       
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   P13FH10A-REC         PIC X(2000).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-FH10-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-KH10-FLAG-FOUND             PIC X(1)  VALUE 'N'.              
           05  WS-PROCESSED                   PIC X(1)  VALUE 'N'.              
           05  WS-FND                         PIC X(1)  VALUE 'N'.              
           05  WS-NUM-HH                      PIC X(1)  VALUE SPACES.           
           05  WS-NUM-BHH                     PIC X(1)  VALUE SPACES.           
           05  WS-DELETE                      PIC X(3)  VALUE SPACES.           
           05  WS-DTE-EXPIRY                  PIC X(8)  VALUE SPACES.           
           05  WS-KH10-STATUS                 PIC X(2)  VALUE SPACES.           
           05  WS-KH12-STATUS                 PIC X(2)  VALUE SPACES.           
           05  WS-KH10-REWRITE                PIC 9(6)  VALUE ZEROS.            
           05  WS-FH10-READ                   PIC 9(6)  VALUE ZEROS.            
           05  WS-KH10-FOUND                  PIC 9(6)  VALUE ZEROS.            
           05  WS-KH10-NOT-FOUND              PIC 9(6)  VALUE ZEROS.            
           05  WS-KH10-DELETED                PIC 9(6)  VALUE ZEROS.            
           05  WS-FH12-WRITTEN                PIC 9(6)  VALUE ZEROS.            
           05  WS-FH10-BYPASS                 PIC 9(6)  VALUE ZEROS.            
           05  WS-NON-SELN                    PIC 9(2)  VALUE ZEROES.           
           05  WS-SUBTRACT                    PIC 9(2)  VALUE ZEROES.           
           05  WS-CNT                         PIC 9(2)  VALUE ZEROES.           
           05  WS-FND-CNT                     PIC 9(2)  VALUE ZEROES.           
           05  WS-NEXT                        PIC 9(2)  VALUE ZEROES.           
           05  WS-CNT-KH12                    PIC 9(2)  VALUE ZEROES.           
           05  WS-CNT-H10A                    PIC 9(6)  VALUE ZEROES.           
      *BP139545 START                                                           
           05  WS-FTPMC-FLAG                  PIC X     VALUE SPACES.           
      *BP139545 END                                                             
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-FH10         THRU 2000-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-FH10      THRU 3000-EXIT                        
                  UNTIL WS-FH10-EOF    = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FH10                                                 
                       SY02F001                                                 
                I-O    BP13KH10                                                 
                       BP13KH12                                                 
                OUTPUT P13FH10A.                                                
                                                                                
           IF WS-KH10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KH10 OPEN ERROR, STATUS IS ' WS-KH10-STATUS          
              MOVE WS-KH10-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-KH12-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KH12 OPEN ERROR, STATUS IS ' WS-KH12-STATUS          
              MOVE WS-KH12-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-FH10.                                                          
      ****************************************************************          
                                                                                
           READ BP13FH10                                                        
              AT END MOVE 'Y' TO WS-FH10-EOF                                    
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-FH10-READ.                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2500-READ-BP13KH10.                                                      
      ****************************************************************          
                                                                                
           MOVE  'N'           TO WS-KH10-FLAG-FOUND.                           
           MOVE  SPACES        TO BP13KH10-REC.                                 
           MOVE  FH10-KEY-FLD  TO KH10-KEY-FLD.                                 
                                                                                
           READ BP13KH10.                                                       
                                                                                
           EVALUATE WS-KH10-STATUS                                              
             WHEN 00                                                            
               ADD 1     TO WS-KH10-FOUND                                       
               MOVE 'Y'  TO WS-KH10-FLAG-FOUND                                  
             WHEN 23                                                            
               MOVE 'N'  TO WS-KH10-FLAG-FOUND                                  
               ADD 1     TO WS-KH10-NOT-FOUND                                   
               DISPLAY 'RECORD NOT FOUND' KH10-KEY-FLD                          
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BP13KH10'                                 
               MOVE WS-KH10-STATUS TO RETURN-CODE                               
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-FH10.                                                       
      ****************************************************************          
           INITIALIZE WS-SUBTRACT                                               
                                                                                
           MOVE 'N'   TO WS-PROCESSED                                           
                                                                                
           PERFORM  2500-READ-BP13KH10       THRU 2500-EXIT.                    
                                                                                
           IF WS-KH10-FLAG-FOUND = 'Y'                                          
      * FT                                                                      
             IF KH10-DTE-DEBAR-FT-HH NOT =  SPACES AND LOW-VALUES               
                  AND 'CCYYMMDD'                                                
               IF F001-DTE-CURRENT >=  KH10-DTE-DEBAR-EXPIRY-FT-HH              
                  PERFORM  5100-WRITE-P13FH10A    THRU 5100-EXIT                
                  PERFORM  3400-PREPARE-BP13KH12  THRU 3400-EXIT                
                  MOVE KH10-DTE-DEBAR-FT-HH   TO  WS-DTE-EXPIRY                 
                  MOVE 'FT'           TO WS-DELETE                              
                  PERFORM  3100-DELETE-HISTORY    THRU 3100-EXIT                
                    UNTIL  WS-PROCESSED = 'Y'                                   
                                                                                
                  IF KH10-NUM-NON-SELN-FT  NOT NUMERIC                          
                     MOVE ZEROES           TO KH10-NUM-NON-SELN-FT              
                  ELSE                                                          
                     MOVE KH10-NUM-NON-SELN-FT    TO WS-NON-SELN                
                     COMPUTE WS-NON-SELN          =  WS-NON-SELN -              
                                                     WS-SUBTRACT                
                     MOVE WS-NON-SELN        TO KH10-NUM-NON-SELN-FT            
                     INITIALIZE WS-SUBTRACT                                     
                                WS-NON-SELN                                     
                  END-IF                                                        
                                                                                
                  MOVE KH10-DTE-DEBAR-FT-HH TO KH12-DTE-DEBAR-FT-HH             
                  MOVE KH10-DTE-DEBAR-EXPIRY-FT-HH                              
                    TO KH12-DTE-DEBAR-EXPIRY-FT-HH                              
                                                                                
      * SHIFT UP DEBARMENT DATE                                                 
                  MOVE KH10-DTE-DEBAR-FT-HG  TO KH10-DTE-DEBAR-FT-HH            
                  MOVE KH10-DTE-DEBAR-EXPIRY-FT-HG                              
                    TO KH10-DTE-DEBAR-EXPIRY-FT-HH                              
                                                                                
                  INITIALIZE KH10-DTE-DEBAR-FT-HG                               
                             KH10-DTE-DEBAR-EXPIRY-FT-HG                        
                                                                                
                  PERFORM  4100-REWRITE-BP13KH10  THRU 4100-EXIT                
                  PERFORM  4000-WRITE-BP13KH12    THRU 4000-EXIT                
               END-IF                                                           
             END-IF                                                             
                                                                                
      * FT                                                                      
             IF (KH10-DTE-DEBAR-FT-HG NOT =  SPACES AND LOW-VALUES              
                 AND 'CCYYMMDD')                                                
               IF F001-DTE-CURRENT >=  KH10-DTE-DEBAR-EXPIRY-FT-HG              
                  PERFORM  5100-WRITE-P13FH10A    THRU 5100-EXIT                
                  PERFORM  3400-PREPARE-BP13KH12  THRU 3400-EXIT                
                  MOVE KH10-DTE-DEBAR-FT-HG       TO WS-DTE-EXPIRY              
                  MOVE 'N'   TO WS-PROCESSED                                    
                  MOVE 'FT'           TO WS-DELETE                              
                  PERFORM  3100-DELETE-HISTORY    THRU 3100-EXIT                
                    UNTIL  WS-PROCESSED = 'Y'                                   
                                                                                
                  IF KH10-NUM-NON-SELN-FT  NOT NUMERIC                          
                     MOVE ZEROES           TO KH10-NUM-NON-SELN-FT              
                  ELSE                                                          
                     MOVE KH10-NUM-NON-SELN-FT    TO WS-NON-SELN                
                     COMPUTE WS-NON-SELN          =  WS-NON-SELN -              
                                                     WS-SUBTRACT                
                     MOVE WS-NON-SELN        TO KH10-NUM-NON-SELN-FT            
                     INITIALIZE WS-SUBTRACT                                     
                                WS-NON-SELN                                     
                  END-IF                                                        
                                                                                
                  MOVE KH10-DTE-DEBAR-FT-HG TO KH12-DTE-DEBAR-FT-HG             
                  MOVE KH10-DTE-DEBAR-EXPIRY-FT-HG                              
                    TO KH12-DTE-DEBAR-EXPIRY-FT-HG                              
                                                                                
                  INITIALIZE KH10-DTE-DEBAR-FT-HG                               
                             KH10-DTE-DEBAR-EXPIRY-FT-HG                        
                                                                                
                  PERFORM  4100-REWRITE-BP13KH10  THRU 4100-EXIT                
                  PERFORM  4000-WRITE-BP13KH12    THRU 4000-EXIT                
               END-IF                                                           
             END-IF                                                             
                                                                                
      * FT2T                                                                    
             IF (KH10-DTE-DEBAR-FT-HH NOT =  SPACES AND LOW-VALUES              
                  AND 'CCYYMMDD')                                               
               IF F001-DTE-CURRENT >=  KH10-DTE-DEBAR-EXPIRY-FT-HH              
                  PERFORM  5100-WRITE-P13FH10A    THRU 5100-EXIT                
                  PERFORM  3400-PREPARE-BP13KH12  THRU 3400-EXIT                
                  MOVE KH10-DTE-DEBAR-FT-HH       TO WS-DTE-EXPIRY              
                  MOVE 'N'   TO WS-PROCESSED                                    
                  MOVE 'F2T'          TO WS-DELETE                              
                  PERFORM  3100-DELETE-HISTORY    THRU 3100-EXIT                
                    UNTIL  WS-PROCESSED = 'Y'                                   
                                                                                
                  IF KH10-NUM-NON-SELN-FT  NOT NUMERIC                          
                     MOVE ZEROES           TO KH10-NUM-NON-SELN-FT              
                  ELSE                                                          
                     MOVE KH10-NUM-NON-SELN-FT    TO WS-NON-SELN                
                     COMPUTE WS-NON-SELN          =  WS-NON-SELN -              
                                                     WS-SUBTRACT                
                     MOVE WS-NON-SELN        TO KH10-NUM-NON-SELN-FT            
                     INITIALIZE WS-SUBTRACT                                     
                                WS-NON-SELN                                     
                  END-IF                                                        
                                                                                
                  MOVE KH10-DTE-DEBAR-FT-HH TO KH12-DTE-DEBAR-FT-HH             
                  MOVE KH10-DTE-DEBAR-EXPIRY-FT-HH                              
                    TO KH12-DTE-DEBAR-EXPIRY-FT-HH                              
                                                                                
                  INITIALIZE KH10-DTE-DEBAR-FT-HH                               
                             KH10-DTE-DEBAR-EXPIRY-FT-HH                        
                                                                                
                  PERFORM  4100-REWRITE-BP13KH10  THRU 4100-EXIT                
                  PERFORM  4000-WRITE-BP13KH12    THRU 4000-EXIT                
               END-IF                                                           
             END-IF                                                             
                                                                                
      * FT2T                                                                    
             IF (KH10-DTE-DEBAR-FT-HG NOT =  SPACES AND LOW-VALUES              
                  AND 'CCYYMMDD')                                               
               IF F001-DTE-CURRENT >=  KH10-DTE-DEBAR-EXPIRY-FT-HG              
                  PERFORM  5100-WRITE-P13FH10A    THRU 5100-EXIT                
                  PERFORM  3400-PREPARE-BP13KH12  THRU 3400-EXIT                
                  MOVE KH10-DTE-DEBAR-FT-HG       TO WS-DTE-EXPIRY              
                  MOVE 'N'   TO WS-PROCESSED                                    
                  MOVE 'F2T'          TO WS-DELETE                              
                  PERFORM  3100-DELETE-HISTORY    THRU 3100-EXIT                
                    UNTIL  WS-PROCESSED = 'Y'                                   
                                                                                
                  IF KH10-NUM-NON-SELN-FT  NOT NUMERIC                          
                     MOVE ZEROES           TO KH10-NUM-NON-SELN-FT              
                  ELSE                                                          
                     MOVE KH10-NUM-NON-SELN-FT    TO WS-NON-SELN                
                     COMPUTE WS-NON-SELN          =  WS-NON-SELN -              
                                                     WS-SUBTRACT                
                     MOVE WS-NON-SELN        TO KH10-NUM-NON-SELN-FT            
                     INITIALIZE WS-SUBTRACT                                     
                                WS-NON-SELN                                     
                  END-IF                                                        
                                                                                
                  MOVE KH10-DTE-DEBAR-FT-HG TO KH12-DTE-DEBAR-FT-HH             
                  MOVE KH10-DTE-DEBAR-EXPIRY-FT-HG                              
                    TO KH12-DTE-DEBAR-EXPIRY-FT-HG                              
                                                                                
                  INITIALIZE KH10-DTE-DEBAR-FT-HG                               
                             KH10-DTE-DEBAR-EXPIRY-FT-HG                        
                                                                                
                  PERFORM  4100-REWRITE-BP13KH10  THRU 4100-EXIT                
                  PERFORM  4000-WRITE-BP13KH12    THRU 4000-EXIT                
               END-IF                                                           
             END-IF                                                             
                                                                                
      * ST                                                                      
             IF (KH10-DTE-DEBAR-ST NOT =  SPACES AND LOW-VALUES                 
                  AND 'CCYYMMDD')                                               
               IF F001-DTE-CURRENT >=  KH10-DTE-DEBAR-EXPIRY-ST                 
                  PERFORM  5100-WRITE-P13FH10A    THRU 5100-EXIT                
                  PERFORM  3400-PREPARE-BP13KH12  THRU 3400-EXIT                
                  MOVE KH10-DTE-DEBAR-ST         TO WS-DTE-EXPIRY               
                  MOVE BP13KH10-REC(1:1976)   TO  BP13KH12-REC(1:1976)          
                  MOVE 'N'   TO WS-PROCESSED                                    
                  MOVE 'ST'           TO WS-DELETE                              
                  PERFORM  3100-DELETE-HISTORY    THRU 3100-EXIT                
                    UNTIL  WS-PROCESSED = 'Y'                                   
                                                                                
                  IF KH10-NUM-NON-SELN-ST  NOT NUMERIC                          
                     MOVE ZEROES           TO KH10-NUM-NON-SELN-ST              
                  ELSE                                                          
                     MOVE KH10-NUM-NON-SELN-ST    TO WS-NON-SELN                
                     COMPUTE WS-NON-SELN          =  WS-NON-SELN -              
                                                     WS-SUBTRACT                
                     MOVE WS-NON-SELN        TO KH10-NUM-NON-SELN-ST            
                     INITIALIZE WS-SUBTRACT                                     
                                WS-NON-SELN                                     
                  END-IF                                                        
                                                                                
                  MOVE KH10-DTE-DEBAR-ST    TO KH12-DTE-DEBAR-ST                
                  MOVE KH10-DTE-DEBAR-EXPIRY-ST                                 
                    TO KH12-DTE-DEBAR-EXPIRY-ST                                 
                                                                                
                  INITIALIZE KH10-DTE-DEBAR-ST                                  
                             KH10-DTE-DEBAR-EXPIRY-ST                           
                                                                                
                  PERFORM  4100-REWRITE-BP13KH10  THRU 4100-EXIT                
                  PERFORM  4000-WRITE-BP13KH12    THRU 4000-EXIT                
               END-IF                                                           
             END-IF                                                             
                                                                                
      * FTS                                                                     
             IF (KH10-DTE-DEBAR-FTS NOT =  SPACES AND LOW-VALUES                
                  AND 'CCYYMMDD')                                               
               IF F001-DTE-CURRENT >=  KH10-DTE-DEBAR-EXPIRY-FTS                
                  PERFORM  5100-WRITE-P13FH10A    THRU 5100-EXIT                
                  PERFORM  3400-PREPARE-BP13KH12  THRU 3400-EXIT                
                  MOVE KH10-DTE-DEBAR-FTS         TO WS-DTE-EXPIRY              
                  MOVE 'N'   TO WS-PROCESSED                                    
                  MOVE 'FTS'          TO WS-DELETE                              
                  PERFORM  3100-DELETE-HISTORY    THRU 3100-EXIT                
                    UNTIL  WS-PROCESSED = 'Y'                                   
                                                                                
                  IF KH10-NUM-NON-SELN-FTS NOT NUMERIC                          
                     MOVE ZEROES           TO KH10-NUM-NON-SELN-FTS             
                  ELSE                                                          
                     MOVE KH10-NUM-NON-SELN-FTS   TO WS-NON-SELN                
                     COMPUTE WS-NON-SELN          =  WS-NON-SELN -              
                                                     WS-SUBTRACT                
                     MOVE WS-NON-SELN        TO KH10-NUM-NON-SELN-FTS           
                     INITIALIZE WS-SUBTRACT                                     
                                WS-NON-SELN                                     
                  END-IF                                                        
                                                                                
                  MOVE KH10-DTE-DEBAR-FTS   TO KH12-DTE-DEBAR-FTS               
                  MOVE KH10-DTE-DEBAR-EXPIRY-FTS                                
                    TO KH12-DTE-DEBAR-EXPIRY-FTS                                
                                                                                
                  INITIALIZE KH10-DTE-DEBAR-FTS                                 
                             KH10-DTE-DEBAR-EXPIRY-FTS                          
                                                                                
                  PERFORM  4100-REWRITE-BP13KH10  THRU 4100-EXIT                
                  PERFORM  4000-WRITE-BP13KH12    THRU 4000-EXIT                
               END-IF                                                           
                                                                                
             END-IF                                                             
                                                                                
      *BP139545 START                                                           
                                                                                
      * FTPMC                                                                   
             IF (KH10-DTE-DEBAR-FTPMC NOT = SPACES AND LOW-VALUES AND           
                                            'CCYYMMDD')                         
               IF F001-DTE-CURRENT >= KH10-DTE-DEBAR-EXPIRY-FTPMC               
                  PERFORM 5100-WRITE-P13FH10A THRU 5100-EXIT                    
                  PERFORM 3400-PREPARE-BP13KH12 THRU 3400-EXIT                  
                  MOVE KH10-DTE-DEBAR-FTPMC TO WS-DTE-EXPIRY                    
                  MOVE 'N' TO WS-PROCESSED                                      
                  MOVE 'FTP' TO WS-DELETE                                       
                  PERFORM 3100-DELETE-HISTORY THRU 3100-EXIT                    
                    UNTIL WS-PROCESSED = 'Y'                                    
                                                                                
                  IF KH10-NUM-NON-SELN-FTPMC NOT NUMERIC                        
                     MOVE ZEROES TO KH10-NUM-NON-SELN-FTPMC                     
                  ELSE                                                          
                     MOVE KH10-NUM-NON-SELN-FTPMC TO WS-NON-SELN                
                     COMPUTE WS-NON-SELN = WS-NON-SELN - WS-SUBTRACT            
                                                                                
                     MOVE WS-NON-SELN TO KH10-NUM-NON-SELN-FTPMC                
                     INITIALIZE WS-SUBTRACT                                     
                                WS-NON-SELN                                     
                  END-IF                                                        
                                                                                
                  MOVE KH10-DTE-DEBAR-FTPMC TO KH12-DTE-DEBAR-FTPMC             
                  MOVE KH10-DTE-DEBAR-EXPIRY-FTPMC                              
                    TO KH12-DTE-DEBAR-EXPIRY-FTPMC                              
                                                                                
                  INITIALIZE KH10-DTE-DEBAR-FTPMC                               
                             KH10-DTE-DEBAR-EXPIRY-FTPMC                        
                                                                                
                  PERFORM 4100-REWRITE-BP13KH10 THRU 4100-EXIT                  
                  PERFORM 4000-WRITE-BP13KH12   THRU 4000-EXIT                  
               END-IF                                                           
                                                                                
             END-IF                                                             
                                                                                
      *BP139545 END                                                             
                                                                                
             IF KH10-NUM-REGN(1) = SPACES OR LOW-VALUES                         
                PERFORM  5000-DELETE-BP13KH10  THRU 5000-EXIT                   
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM  2000-READ-FH10           THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-DELETE-HISTORY.                                                     
      ****************************************************************          
                                                                                
           MOVE ZEROES           TO WS-NEXT                                     
                                    WS-CNT.                                     
           MOVE 'N'              TO WS-FND.                                     
           MOVE 1                TO WS-FND-CNT.                                 
                                                                                
           PERFORM 3200-FIND-RECORD        THRU 3200-EXIT                       
             UNTIL WS-FND = 'Y'                                                 
                OR WS-FND-CNT > 15                                              
                                                                                
           IF WS-FND = 'Y'                                                      
                                                                                
              MOVE KH10-BOOKING-TABLE(WS-FND-CNT)                               
                TO KH12-BOOKING-TABLE(WS-CNT-KH12)                              
                                                                                
              INITIALIZE KH10-BOOKING-TABLE(WS-FND-CNT)                         
                                                                                
              MOVE WS-FND-CNT   TO WS-NEXT                                      
                                   WS-CNT                                       
              ADD  1            TO WS-NEXT                                      
                                   WS-SUBTRACT                                  
                                   WS-CNT-KH12                                  
                                                                                
              PERFORM 3300-SHIFT-UP-RECORDS   THRU 3300-EXIT                    
                UNTIL WS-NEXT > 15                                              
           END-IF.                                                              
                                                                                
           IF WS-FND-CNT  >= 15                                                 
              MOVE 'Y'          TO WS-PROCESSED                                 
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3200-FIND-RECORD.                                                        
      ****************************************************************          
                                                                                
           INITIALIZE WS-NUM-HH                                                 
                      WS-NUM-BHH                                                
      *BP139545 START                                                           
                      WS-FTPMC-FLAG.                                            
      *BP139545 END                                                             
                                                                                
           MOVE KH10-NUM-HOUSEHOLD(WS-FND-CNT)        TO WS-NUM-HH              
           MOVE KH10-NUM-BALLOT-HOUSEHOLD(WS-FND-CNT) TO WS-NUM-BHH             
                                                                                
      *    IF WS-NUM-BHH = SPACES OR LOW-VALUES                                 
      *       MOVE 'H'         TO WS-NUM-BHH                                    
      *       MOVE WS-NUM-HH   TO WS-NUM-BHH                                    
      *    END-IF                                                               
                                                                                
      *BP139545 START                                                           
           IF KH10-NUM-FTPMC(WS-FND-CNT) NOT = 'N' AND SPACE                    
              MOVE 'Y' TO WS-FTPMC-FLAG                                         
           ELSE                                                                 
              MOVE 'N' TO WS-FTPMC-FLAG                                         
           END-IF                                                               
      *BP139545 END                                                             
                                                                                
           EVALUATE TRUE                                                        
           WHEN WS-DELETE = 'F2T'                                               
                IF ((WS-NUM-HH              = 'G' AND                           
                     WS-NUM-BHH             = 'F' AND                           
                     KH10-NUM-FT2T(WS-FND-CNT) = 'Y')   OR                      
                    (WS-NUM-HH              = 'G' AND                           
                     WS-NUM-BHH             = 'G' AND                           
                     KH10-NUM-FT2T(WS-FND-CNT) = 'Y')) AND                      
                     KH10-DTE-APPT(WS-FND-CNT) <= WS-DTE-EXPIRY                 
      *BP139545 START                                                           
                     AND WS-FTPMC-FLAG = 'N'                                    
      *BP139545 END                                                             
                   MOVE 'Y'             TO WS-FND                               
                   GO TO 3200-EXIT                                              
                END-IF                                                          
           WHEN WS-DELETE = 'ST '                                               
                IF WS-NUM-HH              = 'G' AND                             
                   WS-NUM-BHH             = 'G' AND                             
                   KH10-DTE-APPT(WS-FND-CNT) <= WS-DTE-EXPIRY                   
                   MOVE 'Y'             TO WS-FND                               
                   GO TO 3200-EXIT                                              
                END-IF                                                          
           WHEN WS-DELETE = 'FTS'                                               
                IF KH10-NUM-ALLOC-SCHEME(WS-FND-CNT)     = 'FTS' AND            
                   KH10-DTE-APPT(WS-FND-CNT) <= WS-DTE-EXPIRY                   
                   MOVE 'Y'             TO WS-FND                               
                   GO TO 3200-EXIT                                              
                END-IF                                                          
           WHEN WS-DELETE = 'FT '                                               
      *         IF ((WS-NUM-HH              = 'H' OR 'T'   AND                  
      *              WS-NUM-BHH             = 'H' OR 'T')   OR                  
      *             (WS-NUM-HH              = 'H' OR 'T'   AND                  
      *              WS-NUM-BHH             = 'G')) AND                         
                IF (WS-NUM-HH               = 'H' OR 'T' OR 'G') AND            
                     KH10-DTE-APPT(WS-FND-CNT) <= WS-DTE-EXPIRY                 
      *BP139545 START                                                           
                     AND WS-FTPMC-FLAG = 'N'                                    
      *BP139545 END                                                             
                   MOVE 'Y'             TO WS-FND                               
                   GO TO 3200-EXIT                                              
                END-IF                                                          
                                                                                
      *BP139545 START                                                           
                                                                                
           WHEN WS-DELETE = 'FTP'                                               
                IF ((WS-NUM-HH              = 'G' AND                           
                     WS-NUM-BHH             = 'F' AND                           
                     KH10-NUM-FT2T(WS-FND-CNT) = 'Y') OR                        
                    (WS-NUM-HH              = 'H' AND                           
                     WS-NUM-BHH             = 'H' ) AND                         
                     WS-FTPMC-FLAG          = 'Y' )                             
                     AND KH10-DTE-APPT(WS-FND-CNT) <= WS-DTE-EXPIRY             
                   MOVE 'Y' TO WS-FND                                           
                   GO TO 3200-EXIT                                              
                END-IF                                                          
                                                                                
      *BP139545 END                                                             
                                                                                
           END-EVALUATE.                                                        
                                                                                
           ADD  1        TO WS-FND-CNT.                                         
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3300-SHIFT-UP-RECORDS.                                                   
      ****************************************************************          
           MOVE KH10-BOOKING-TABLE(WS-NEXT)                                     
             TO KH10-BOOKING-TABLE(WS-CNT)                                      
                                                                                
           INITIALIZE KH10-BOOKING-TABLE(WS-NEXT)                               
                                                                                
           ADD  1        TO WS-CNT                                              
                            WS-NEXT.                                            
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3400-PREPARE-BP13KH12.                                                   
      ****************************************************************          
           MOVE BP13KH10-REC(1:1976)   TO  BP13KH12-REC(1:1976)                 
                                                                                
           INITIALIZE KH12-BOOKING-TABLE(1)                                     
                      KH12-BOOKING-TABLE(2)                                     
                      KH12-BOOKING-TABLE(3)                                     
                      KH12-BOOKING-TABLE(4)                                     
                      KH12-BOOKING-TABLE(5)                                     
                      KH12-BOOKING-TABLE(6)                                     
                      KH12-BOOKING-TABLE(7)                                     
                      KH12-BOOKING-TABLE(8)                                     
                      KH12-BOOKING-TABLE(9)                                     
                      KH12-BOOKING-TABLE(10)                                    
                      KH12-BOOKING-TABLE(11)                                    
                      KH12-BOOKING-TABLE(12)                                    
                      KH12-BOOKING-TABLE(13)                                    
                      KH12-BOOKING-TABLE(14)                                    
                      KH12-BOOKING-TABLE(15)                                    
                      KH12-DTE-DEBAR-FT-HH                                      
                      KH12-DTE-DEBAR-EXPIRY-FT-HH                               
                      KH12-DTE-DEBAR-FT-HG                                      
                      KH12-DTE-DEBAR-EXPIRY-FT-HG                               
                      KH12-DTE-DEBAR-ST                                         
                      KH12-DTE-DEBAR-EXPIRY-ST                                  
                      KH12-DTE-DEBAR-FTS                                        
                      KH12-DTE-DEBAR-EXPIRY-FTS                                 
      *BP139545 START                                                           
                      KH12-DTE-DEBAR-FTPMC                                      
                      KH12-DTE-DEBAR-EXPIRY-FTPMC.                              
      *BP139545 END                                                             
                                                                                
            MOVE 1 TO WS-CNT-KH12.                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-WRITE-BP13KH12.                                                     
      ****************************************************************          
                                                                                
           MOVE KH10-NUM-UIN           TO  KH12-NUM-NRIC.                       
           MOVE WS-SYS-DATE            TO  KH12-DTE-UPDATE.                     
           MOVE WS-SYS-TIME            TO  KH12-TME-UPDATE.                     
                                                                                
                                                                                
           WRITE BP13KH12-REC.                                                  
                                                                                
           IF WS-KH12-STATUS NOT = 00 AND 22                                    
               DISPLAY 'ERROR WRITING  BP13KH12. STATUS IS '                    
                        WS-KH12-STATUS                                          
               MOVE WS-KH12-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
           INITIALIZE BP13KH12-REC.                                             
                                                                                
           ADD 1 TO WS-FH12-WRITTEN.                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4100-REWRITE-BP13KH10.                                                   
      ****************************************************************          
                                                                                
           MOVE WS-SYS-DATE   TO   KH10-DTE-UPDATE.                             
           MOVE WS-SYS-TIME   TO   KH10-TME-UPDATE.                             
                                                                                
                                                                                
           REWRITE BP13KH10-REC.                                                
                                                                                
           IF  WS-KH10-STATUS NOT =  00                                         
               DISPLAY 'ERROR REWRITING BP13KH10. STATUS IS '                   
                        WS-KH10-STATUS                                          
               MOVE WS-KH10-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-KH10-REWRITE.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-DELETE-BP13KH10.                                                    
      ****************************************************************          
                                                                                
                                                                                
            DELETE  BP13KH10.                                                   
                                                                                
            IF WS-KH10-STATUS NOT =  00                                         
               DISPLAY 'ERROR DELETING  BP13KH10. STATUS IS '                   
                        WS-KH10-STATUS                                          
               MOVE WS-KH10-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            ADD 1 TO WS-KH10-DELETED.                                           
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5100-WRITE-P13FH10A.                                                     
      ****************************************************************          
            INITIALIZE P13FH10A-REC.                                            
            MOVE BP13KH10-REC  TO   P13FH10A-REC.                               
                                                                                
            MOVE WS-SYS-TIME   TO   P13FH10A-REC(1993:8).                       
            MOVE WS-SYS-DATE   TO   P13FH10A-REC(1985:8).                       
                                                                                
            WRITE P13FH10A-REC.                                                 
            ADD 1 TO WS-CNT-H10A.                                               
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CB82 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13FH10 RECORDS READ        = ' WS-FH10-READ.              
           DISPLAY 'NO OF REC FOUND BP13KH10     = ' WS-KH10-FOUND.             
           DISPLAY 'NO OF REC NOT FOUND BP13KH10 = ' WS-KH10-NOT-FOUND.         
           DISPLAY 'NO OF REC DELETED BP13KH10   = ' WS-KH10-DELETED.           
           DISPLAY 'NO OF REC REWRITE BP13KH10   = ' WS-KH10-REWRITE.           
           DISPLAY 'NO OF REC WRITTEN BP13KH12   = ' WS-FH12-WRITTEN.           
           DISPLAY 'NO OF REC BYPASS  BP13FH10   = ' WS-FH10-BYPASS.            
           DISPLAY 'NO OF REC WRITTEN P13FH10A   = ' WS-CNT-H10A.               
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13FH10                                                       
                 BP13KH10                                                       
                 BP13KH12                                                       
                 P13FH10A.                                                      
                                                                                
           IF WS-KH10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KH10 CLOSE ERROR, STATUS IS ' WS-KH10-STATUS         
              MOVE WS-KH10-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-KH12-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KH12 CLOSE ERROR, STATUS IS ' WS-KH12-STATUS         
              MOVE WS-KH12-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
