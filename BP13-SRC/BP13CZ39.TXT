       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ39.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  20 APR 2018.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  GET APPLICATIONS IN THE LAST 6 MTH BASED ON   *                
      *          DTE-BALLOT, GET TOTAL RECORD AND AVE SELL PRICE                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP137387 FNP1 200418  NEW PROGRAM                      *                
      * BP137387 KR13 140219  RUN 1ST/2ND QTR BASED ON CUR-MNTH*                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120 ASSIGN TO BM06F120.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BB14K500     ASSIGN        TO BB14K500                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K500-KEY-FLD                    
                               FILE STATUS   IS WS-K500-STATUS.                 
                                                                                
           SELECT BM06K130     ASSIGN        TO BM06K130                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K130-KEY-FLD                    
                               FILE STATUS   IS WS-K130-STATUS.                 
                                                                                
           SELECT BM06K510     ASSIGN        TO BM06K510                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K510-ESTCDE                     
                               FILE STATUS   IS WS-K510-STATUS.                 
                                                                                
           SELECT BP13F81A     ASSIGN        TO BP13F81A.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BM06F120                                                            
            RECORD CONTAINS  700 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS  60 CHARACTERS.                                     
       COPY BM06K510.                                                           
                                                                                
       FD   BM06K130                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BM06K130.                                                           
                                                                                
       FD  BP13F81A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       COPY BP13F81A.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K500-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K130-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K510-STATUS       PIC 9(2)    VALUE ZEROES.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F120-READ          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K500-FND          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K130-FND          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K510-FND          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F81A-WRITE         PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-F120-EOF          PIC X(1)     VALUE ' '.                     
           05  WS-DTE-BALLOT.                                                   
               10  WS-BAL-CCYY      PIC 9(04).                                  
               10  WS-BAL-MM        PIC 9(02).                                  
           05  WS-DTE-CURRENT       PIC 9(08).                                  
           05  WS-INT-CUR           PIC 9(07)    VALUE ZEROES.                  
           05  WS-CUT-OFF           PIC 9(08)    VALUE ZEROES.                  
           05  WS-TOT-RECORD        PIC 9(05)    VALUE ZEROES.                  
           05  WS-TOT-PRICE         PIC 9(09)    VALUE ZEROES.                  
           05  WS-AVE-PRICE         PIC 9(8)V99  VALUE ZEROES.                  
           05  WS-F813-NUM-ZONE     PIC X(03)    VALUE SPACES.                  
           05  WS-F120-NUM-ZONE     PIC X(03)    VALUE SPACES.                  
           05  WS-PREV-FT           PIC X(02)    VALUE SPACES.                  
           05  WS-SCH               PIC X(04)    VALUE SPACES.                  
           05  WS-BLK               PIC X(05)    VALUE SPACES.                  
           05  WS-POSTAL            PIC X(06)    VALUE SPACES.                  
           05  WS-DTE-BAL           PIC X(06)    VALUE SPACES.                  
           05  WS-MODE              PIC X(03)    VALUE SPACES.                  
           05  WS-NME-EST           PIC X(40)    VALUE SPACES.                  
           05  WS-NME-PROJ          PIC X(50)    VALUE SPACES.                  
           05  WS-MAT-TAG           PIC X(01)    VALUE SPACES.                  
           05  WS-NUM-TOWN          PIC X(03)    VALUE SPACES.                  
           05  DATE-OK              PIC X(01)    VALUE SPACES.                  
           05  1ST-TIME             PIC X(01)    VALUE 'Y'.                     
                                                                                
       01  WS-F120-PREV-KEY.                                                    
           05  WS-PREV-ESTATE          PIC X(3)  VALUE SPACES.                  
           05  WS-PREV-NEIGHBOURHOOD   PIC X(4)  VALUE SPACES.                  
           05  WS-PREV-CONTRACT-NO     PIC X(4)  VALUE SPACES.                  
           05  WS-PREV-BLK-NO          PIC X(5)  VALUE SPACES.                  
           05  WS-PREV-NUM-FT          PIC 99    VALUE ZEROES.                  
                                                                                
       01  WS-F120-KEY.                                                         
           05  WS-ESTATE               PIC X(3)  VALUE SPACES.                  
           05  WS-NEIGHBOURHOOD        PIC X(4)  VALUE SPACES.                  
           05  WS-CONTRACT-NO          PIC X(4)  VALUE SPACES.                  
           05  WS-BLK-NO               PIC X(5)  VALUE SPACES.                  
           05  WS-NUM-FT               PIC 99    VALUE ZEROES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1200-READ-BM06F120   THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F120-KEY = HIGH-VALUES.                                   
           IF WS-F120-KEY = HIGH-VALUES                                         
              PERFORM 7000-WRITE-F81A   THRU 7000-EXIT                          
           END-IF.                                                              
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BM06F120                                                 
                       BB14K500                                                 
                       BM06K130                                                 
                       BM06K510                                                 
                       SY02F001                                                 
                OUTPUT BP13F81A.                                                
                                                                                
           IF WS-K500-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB14K500 OPEN ERROR ' WS-K500-STATUS                     
              MOVE WS-K500-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K510 OPEN ERROR ' WS-K510-STATUS                     
              MOVE WS-K510-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K130 OPEN ERROR ' WS-K130-STATUS                     
              MOVE WS-K130-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1200-READ-BM06F120.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BM06F120 AT END                                                 
                MOVE HIGH-VALUES   TO WS-F120-KEY                               
                GO TO 1200-EXIT.                                                
                                                                                
           ADD 1 TO WS-F120-READ.                                               
                                                                                
           MOVE F120-ESTATE              TO WS-ESTATE                           
           MOVE F120-NEIGHBOURHOOD       TO WS-NEIGHBOURHOOD                    
           MOVE F120-CONTRACT-NO         TO WS-CONTRACT-NO                      
           MOVE F120-BLK-NO              TO WS-BLK-NO                           
           MOVE F120-CDE-TYP-ACTUSE      TO WS-NUM-FT.                          
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           MOVE 'N'                             TO DATE-OK.                     
                                                                                
           PERFORM 2100-DATE-CHK                THRU 2100-EXIT                  
                                                                                
           IF DATE-OK = 'Y'                                                     
              IF 1ST-TIME = 'Y'                                                 
                 MOVE WS-F120-KEY               TO WS-F120-PREV-KEY             
              END-IF                                                            
              IF WS-F120-KEY = WS-F120-PREV-KEY                                 
                 PERFORM 2500-GET-SELLING-PRICE THRU 2500-EXIT                  
                 PERFORM 1200-READ-BM06F120     THRU 1200-EXIT                  
              ELSE                                                              
                 PERFORM 7000-WRITE-F81A        THRU 7000-EXIT                  
                 MOVE WS-F120-KEY               TO  WS-F120-PREV-KEY            
                 PERFORM 2500-GET-SELLING-PRICE THRU 2500-EXIT                  
                 PERFORM 1200-READ-BM06F120     THRU 1200-EXIT                  
              END-IF                                                            
              MOVE 'N'                          TO 1ST-TIME                     
           ELSE                                                                 
              PERFORM 1200-READ-BM06F120        THRU 1200-EXIT                  
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       2100-DATE-CHK.                                                           
      *------------------------------------------------------------             
                                                                                
           IF F001-CURR-MM > 6                                                  
              IF F120-NUM-ALLOCN-QTR(5:2) <= '06'                               
                 MOVE 'Y'              TO DATE-OK                               
              END-IF                                                            
           ELSE                                                                 
              IF F120-NUM-ALLOCN-QTR(5:2) > '06'                                
                 MOVE 'Y'              TO DATE-OK                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       2500-GET-SELLING-PRICE.                                                  
      *------------------------------------------------------------             
                                                                                
           IF F120-1ST-OFFER-SELLING-PRICE NOT NUMERIC                          
              MOVE ZEROES         TO F120-1ST-OFFER-SELLING-PRICE               
           END-IF.                                                              
                                                                                
           ADD 1                  TO WS-TOT-RECORD                              
           COMPUTE WS-TOT-PRICE = WS-TOT-PRICE +                                
                                  F120-1ST-OFFER-SELLING-PRICE.                 
                                                                                
            MOVE F120-NUM-ALLOCN-QTR     TO WS-DTE-BAL.                         
            MOVE F120-NUM-SELECTION      TO WS-MODE.                            
            MOVE F120-POSTAL-CODE        TO WS-POSTAL.                          
            MOVE F120-NUM-NT             TO WS-NUM-TOWN.                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3000-READ-BB14K500.                                                      
      *------------------------------------------------------------             
           MOVE SPACES                TO K500-CDE-BLK.                          
           MOVE K130-CDE-BLK          TO K500-CDE-BLK.                          
                                                                                
           READ BB14K500.                                                       
                                                                                
           EVALUATE WS-K500-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K500-DTE-BLDG-COMPLETE   TO F81A-NUM-ECD               
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES              TO F81A-NUM-ECD                    
               WHEN OTHER                                                       
                    DISPLAY 'BB14K500 READ ERROR ' WS-K500-STATUS               
                    DISPLAY 'K500 KEY: ' K500-KEY-FLD                           
                    MOVE WS-K500-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       4000-READ-BM06K130.                                                      
      *------------------------------------------------------------             
           MOVE SPACES                  TO K130-KEY-FLD.                        
           MOVE WS-PREV-ESTATE          TO K130-ESTATE.                         
           MOVE WS-PREV-NEIGHBOURHOOD   TO K130-NEIGHBOURHOOD                   
           MOVE WS-PREV-CONTRACT-NO     TO K130-CONTRACT-NO                     
           MOVE WS-PREV-BLK-NO          TO K130-BLK-NO.                         
                                                                                
           READ BM06K130.                                                       
                                                                                
           EVALUATE WS-K130-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3000-READ-BB14K500  THRU 3000-EXIT                  
               WHEN 23                                                          
                    MOVE SPACES              TO K130-NME-LOCAL-PRCNCT           
                    DISPLAY 'RECORD NOT FOUND IN BM06K130, KEY IS '             
                             K130-KEY-FLD                                       
               WHEN OTHER                                                       
                    DISPLAY 'BM06K130 READ ERROR ' WS-K130-STATUS               
                    MOVE WS-K130-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       4500-READ-BM06K510.                                                      
      *------------------------------------------------------------             
           MOVE ZEROES                TO K510-ESTCDE.                           
           MOVE WS-PREV-ESTATE        TO K510-ESTCDE.                           
                                                                                
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
               WHEN 00                                                          
                    MOVE K510-NUM-MATURE-ESTATE TO  WS-MAT-TAG                  
                    MOVE K510-ESTATE-NAME       TO  F81A-NUM-ESTATE             
               WHEN 23                                                          
                    MOVE SPACES                 TO  WS-MAT-TAG                  
                    MOVE SPACES                 TO  F81A-NUM-ESTATE             
               WHEN OTHER                                                       
                    DISPLAY 'BM06K510 READ ERROR ' WS-K510-STATUS               
                    MOVE WS-K510-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       7000-WRITE-F81A.                                                         
      *------------------------------------------------------------             
           MOVE SPACES                  TO BP13F81A-REC.                        
                                                                                
           PERFORM 4000-READ-BM06K130    THRU 4000-EXIT.                        
                                                                                
           IF WS-TOT-RECORD NOT = ZEROES                                        
              COMPUTE WS-AVE-PRICE = WS-TOT-PRICE / WS-TOT-RECORD               
           ELSE                                                                 
              COMPUTE WS-AVE-PRICE = ZEROES                                     
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-BAL              TO F81A-DTE-BALLOT.                     
           MOVE WS-MODE                 TO F81A-NUM-ALLO-CAT.                   
           MOVE K130-NME-LOCAL-PRCNCT   TO F81A-NME-PROJECT.                    
                                                                                
                                                                                
           MOVE WS-POSTAL               TO F81A-NUM-POSTAL                      
           MOVE WS-NUM-TOWN             TO F81A-NUM-TOWN                        
           MOVE WS-PREV-NUM-FT          TO F81A-NUM-FLAT-TYPE                   
           MOVE WS-PREV-BLK-NO          TO F81A-NUM-BLK-NO                      
           MOVE WS-TOT-RECORD           TO F81A-NUM-FLAT-SUPPLY                 
           MOVE WS-AVE-PRICE            TO F81A-AMT-AVE-PRICE                   
                                                                                
           PERFORM 4500-READ-BM06K510   THRU 4500-EXIT.                         
                                                                                
           IF WS-MAT-TAG = 'Y'                                                  
              MOVE 'MATURE    '         TO F81A-NUM-MATURE-TAG                  
           ELSE                                                                 
              IF WS-MAT-TAG = 'N'                                               
                 MOVE 'NON-MATURE'      TO F81A-NUM-MATURE-TAG                  
              ELSE                                                              
                 MOVE SPACES            TO F81A-NUM-MATURE-TAG                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE BP13F81A-REC.                                                  
           ADD  1                       TO WS-F81A-WRITE.                       
                                                                                
           MOVE ZEROES                  TO WS-TOT-RECORD                        
                                           WS-TOT-PRICE                         
                                           WS-AVE-PRICE.                        
                                                                                
           MOVE SPACES                  TO WS-DTE-BAL                           
                                           WS-POSTAL                            
                                           WS-MODE                              
                                           WS-NME-EST                           
                                           WS-MAT-TAG.                          
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13CZ39 END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F120    ' WS-F120-READ.             
           DISPLAY 'NO OF RECORDS WRITTEN TO F81A   ' WS-F81A-WRITE.            
                                                                                
           CLOSE BM06K130                                                       
                 BB14K500                                                       
                 BM06F120                                                       
                 BM06K510                                                       
                 SY02F001                                                       
                 BP13F81A.                                                      
                                                                                
           IF WS-K500-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB14K500 CLOSE ERROR ' WS-K500-STATUS                    
              MOVE WS-K500-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K130 CLOSE ERROR ' WS-K130-STATUS                    
              MOVE WS-K130-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K510 CLOSE ERROR ' WS-K510-STATUS                    
              MOVE WS-K510-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
