       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C719.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  : CHECK NEW-TOWN REQUEST = NEW-TOWN      *                
      *                 RESIDE BASED ON REF-NUM AND NRIC.      *                
      *                                                        *                
      *    REMARKS    : SIMILAR TO BP13C759                    *                
      *                                                        *                
      *                                                        *                
      * C930252 LSB 930319 - CHG ON MULITIPLE RDB ADDR         *                
      * C9309B4 LSB 931001 - BP13K015 FOR NT-WITH-LAND         *                
      * SOC-PH9 GG  941003 - TO REMOVE K015-CDE-NT-WITH-LAND & *                
      *                      K015-CDE-ALLOC-SCH.               *                
      *BP130630 NOR 990204 - CHGE F715 TO F716 EXPAND TO 350   *                
      *BP130652 NOR 990219 - EXPAND F500 RECL FROM 300 TO 500  *                
      *BP132538 SK  040527   INCLUDE BP13K767                  *                
      *BP132650 SK  050113   REMOVE  BP13K015                  *                
      *BP133610 ESA1090511 - TO REPLACE BP13K767 W/ BP13K816   *                
      *BP135254 SMR 140415 - TO REPLACE BP13K816 W/ BP13K813   *                
      *BP135944 SMR 150915 - REMOVE REFERENCE TO CDE-NT6/7/8   *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K500    ASSIGN TO BP13K500                                
                       ORGANIZATION  IS INDEXED                                 
                       ACCESS  MODE  IS RANDOM                                  
                       RECORD  KEY   IS K500-KEY-NEW                            
                       FILE STATUS   IS BP13K500-STATUS.                        
                                                                                
           SELECT BP13K813 ASSIGN  TO  BP13K813                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  DYNAMIC                                  
                     RECORD KEY    IS  K813-KEY-FLD                             
                     FILE STATUS   IS  BP13K813-STATUS.                         
                                                                                
           SELECT BP13F716    ASSIGN TO BP13F716.                               
           SELECT BP13OUT     ASSIGN TO BP13OUT.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  BP13K500           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 500 CHARACTERS                    
                              LABEL RECORDS ARE STANDARD.                       
       COPY BP13K500.                                                           
                                                                                
                                                                                
       FD  BP13K813           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 1000 CHARACTERS                   
                              LABEL RECORDS ARE STANDARD.                       
                                                                                
       COPY BP13K813.                                                           
                                                                                
       FD  BP13F716           LABEL RECORDS ARE STANDARD                        
                              RECORD CONTAINS 350 CHARACTERS                    
                              BLOCK CONTAINS 0 RECORDS.                         
       COPY BP13F716.                                                           
       FD  BP13OUT            LABEL RECORDS ARE STANDARD                        
                              RECORD CONTAINS 350 CHARACTERS                    
                              BLOCK CONTAINS 0 RECORDS.                         
       01  BP13OUT-REC        PIC X(350).                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CNT-K500-READ          PIC 9(8) VALUE 0.                      
           05  WS-CNT-F716-READ          PIC 9(8) VALUE 0.                      
           05  WS-REQ-NT                 PIC X(3) VALUE SPACES.                 
           05  WS-CNT-WRITE              PIC 9(8) VALUE 0.                      
           05  WS-K500-NOTFD             PIC 9(8) VALUE 0.                      
           05  WS-PREV-NRIC              PIC X(9) VALUE SPACES.                 
           05  WS-NT-LAND                PIC X(1) VALUE SPACES.                 
           05  WS-NT                     PIC X(3) VALUE SPACES.                 
           05  BP13K500-STATUS           PIC 9(2) VALUE 0.                      
           05  BP13K813-STATUS           PIC 9(2) VALUE 0.                      
           05  WS-F716-EOF               PIC X    VALUE 'N'.                    
               88 F716-EOF                        VALUE 'Y'.                    
                                                                                
       01  WS-CURRENT-DATE.                                                     
      *    05  WS-CURR-CC                PIC XX   VALUE SPACES.                 
           05  WS-DTE-CURRENT.                                                  
               10  WS-CURR-YY            PIC XXXX VALUE SPACES.                 
               10  WS-CURR-MM            PIC XX   VALUE SPACES.                 
               10  WS-CURR-DD            PIC XX   VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-CONTROL-RTN.                                                        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-READ-BP13F716    THRU 2000-EXIT.                        
                                                                                
           PERFORM 3000-MAIN-ROUTINE     THRU 3000-EXIT                         
             UNTIL F716-EOF                                                     
              OR   F716-NUM-NRIC = HIGH-VALUE.                                  
                                                                                
           PERFORM 9999-CLOSE-FILES      THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  BP13F716                                                 
                       BP13K500                                                 
                       BP13K813                                                 
               OUTPUT  BP13OUT.                                                 
                                                                                
           IF BP13K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K500, STATUS : '                       
                                     BP13K500-STATUS                            
              MOVE BP13K500-STATUS       TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                        
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K813, STATUS : '                       
                                     BP13K813-STATUS                            
              MOVE BP13K813-STATUS       TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-DTE-CURRENT.                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F716.                                                      
                                                                                
           READ BP13F716                 AT END                                 
           MOVE 'Y'                      TO WS-F716-EOF                         
           MOVE HIGH-VALUE               TO F716-NUM-NRIC                       
           GO TO 2000-EXIT.                                                     
                                                                                
           ADD 1                         TO WS-CNT-F716-READ.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      * IF NT-RESIDE IS NOT MATURE NT, BYPASS                                   
      * IF REN, READ BP13K500 TO GET NT-REQ = NT-RESIDE                         
      * OTHERS, CHECK CDE-NT1 = NT-RESIDE                                       
      *                                                                         
       3000-MAIN-ROUTINE.                                                       
                                                                                
           MOVE F716-NUM-NRIC TO WS-PREV-NRIC.                                  
           PERFORM 4000-READ-BP13K500  THRU 4000-EXIT                           
                                                                                
           EVALUATE K500-NUM-ALLO-CAT                                           
              WHEN 'BTO'                                                        
                 PERFORM 3300-CHECK-NT-NORMAL-SCH                               
                                             THRU 3300-EXIT                     
              WHEN 'BE '                                                        
                 PERFORM 5000-CHECK-SAME-NT  THRU 5000-EXIT                     
              WHEN 'DBS'                                                        
                 PERFORM 5000-CHECK-SAME-NT  THRU 5000-EXIT                     
              WHEN OTHER                                                        
                 PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                   
                   UNTIL F716-NUM-NRIC NOT = WS-PREV-NRIC                       
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      * IF THE LATEST RDB REC NT MATCHED THEN OUTPUT, AND CONTINUE              
      * TO CHECK THE NEXT REC                                                   
      *                                                                         
      * IF THE LATEST RDB REC NT UNMATCHED THEN BYPASS ALL REC, DO NOT          
      * CONSIDER IF THE NEXT REC WITH NT MATCHED                                
      *                                                                         
                                                                                
       3300-CHECK-NT-NORMAL-SCH.                                                
                                                                                
           MOVE F716-CDE-NT-REQ     TO WS-NT.                                   
           PERFORM 6500-READ-K813   THRU 6500-EXIT.                             
                                                                                
           IF F716-CDE-NT-RESIDE = WS-NT                                        
              WRITE BP13OUT-REC FROM BP13F716-REC                               
              ADD 1 TO WS-CNT-WRITE                                             
              PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                      
              PERFORM 7000-CHECK-NEXT-F716  THRU 7000-EXIT                      
                UNTIL F716-NUM-NRIC NOT = WS-PREV-NRIC                          
           ELSE                                                                 
              PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                      
                UNTIL F716-NUM-NRIC NOT = WS-PREV-NRIC                          
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      * IF REN, READ BP13K500 TO GET NT-REQ = NT-RESIDE                         
      *                                                                         
       4000-READ-BP13K500.                                                      
                                                                                
           MOVE SPACES                   TO BP13K500-REC.                       
           INITIALIZE                       BP13K500-REC.                       
           MOVE F716-NUM-REGN            TO K500-KEY-NEW.                       
                                                                                
           READ BP13K500.                                                       
                                                                                
           IF BP13K500-STATUS = 00 OR 02                                        
              ADD 1                      TO WS-CNT-K500-READ                    
           ELSE                                                                 
              IF BP13K500-STATUS = 23                                           
                 ADD 1 TO WS-K500-NOTFD                                         
                 DISPLAY 'RECORD NOT FOUND IN BP13K500. REGN : '                
                          F716-NUM-REGN                                         
              ELSE                                                              
                 DISPLAY 'ERROR IN READING BP13K500      : '                    
                          F716-NUM-REGN ' ' BP13K500-STATUS                     
                 MOVE BP13K500-STATUS    TO RETURN-CODE                         
                 PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-CHECK-SAME-NT.                                                      
                                                                                
           MOVE SPACES               TO WS-REQ-NT.                              
           PERFORM 6000-CHECK-REN-NT THRU 6000-EXIT                             
           MOVE WS-REQ-NT            TO WS-NT.                                  
                                                                                
           IF F716-CDE-NT-RESIDE = WS-NT                                        
              MOVE WS-REQ-NT TO F716-CDE-NT-REQ                                 
              WRITE BP13OUT-REC FROM BP13F716-REC                               
              ADD 1 TO WS-CNT-WRITE                                             
              PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                      
              PERFORM 7000-CHECK-NEXT-F716 THRU 7000-EXIT                       
                UNTIL  F716-NUM-NRIC NOT = WS-PREV-NRIC                         
           ELSE                                                                 
              PERFORM 2000-READ-BP13F716    THRU 2000-EXIT                      
                 UNTIL  F716-NUM-NRIC NOT = WS-PREV-NRIC                        
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      * IF REN, CHECK THRU THE 8 CDE-NT                                         
      *                                                                         
       6000-CHECK-REN-NT.                                                       
                                                                                
           IF F716-CDE-NT-RESIDE = K500-CDE-NT1                                 
              MOVE K500-CDE-NT1 TO WS-REQ-NT                                    
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K500-CDE-NT2                                 
              MOVE K500-CDE-NT2 TO WS-REQ-NT                                    
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K500-CDE-NT3                                 
              MOVE K500-CDE-NT3 TO WS-REQ-NT                                    
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K500-CDE-NT4                                 
              MOVE K500-CDE-NT4 TO WS-REQ-NT                                    
           ELSE                                                                 
           IF F716-CDE-NT-RESIDE = K500-CDE-NT5                                 
              MOVE K500-CDE-NT5 TO WS-REQ-NT                                    
           ELSE                                                                 
              MOVE SPACES       TO WS-REQ-NT.                                   
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6500-READ-K813.                                                          
                                                                                
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
           MOVE WS-NT                       TO K813-NUM-ZONE.                   
           MOVE K500-CDE-FLAT-TYPE          TO K813-NUM-FLAT-TYPE.              
           MOVE K500-DTE-BALLOT             TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
                                                                                
           IF BP13K813-STATUS = 00                                              
              MOVE K813-CDE-NT              TO WS-NT                            
           ELSE                                                                 
              IF BP13K813-STATUS = 23                                           
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K813,STATUS ' BP13K813-STATUS          
                 MOVE BP13K813-STATUS    TO RETURN-CODE                         
                 GO TO 9999-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *                                                                         
      * CHECK FOR MULTIPLE REC IN RDB                                           
      *                                                                         
       7000-CHECK-NEXT-F716.                                                    
                                                                                
           IF F716-NUM-NRIC = WS-PREV-NRIC                                      
              WRITE BP13OUT-REC FROM BP13F716-REC                               
              ADD 1 TO WS-CNT-WRITE                                             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F716    THRU 2000-EXIT.                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
                                                                                
           CLOSE BP13F716                                                       
                 BP13K500                                                       
                 BP13OUT.                                                       
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>>> BP13C719 <<<<<<<<<<<<<<<<<<<<<<<'.        
           DISPLAY '------ CHECK NT-REQUESTED = NT-RESIDE  ------ '.            
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF RECORDS READ FROM BP13K500    : '                     
                                                      WS-CNT-K500-READ.         
           DISPLAY 'NO OF RECORDS READ FROM BP13F716    : '                     
                                                      WS-CNT-F716-READ.         
           DISPLAY 'NO OF RECORDS NOT FOUND BP13K500    : '                     
                                                      WS-K500-NOTFD.            
           DISPLAY 'NO OF RECORDS WRITTEN TO BP13OUT    : '                     
                                               WS-CNT-WRITE.                    
           DISPLAY '  '.                                                        
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
