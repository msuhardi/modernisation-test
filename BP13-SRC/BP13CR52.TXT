       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CR52.                                                 
      *AUTHOR.                                                                  
      *DATE-WRITTEN.  09/04/2021.                                               
      * ========================================================== *            
      * SYSTEMS OF COMMITMENT (BP13)                               *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    GENERATE REPORT                                         *            
      *                                                            *            
      *  INPUT     :  1. BP13F410                                  *            
      *  OUTPUT    :  1. BP13RPT1                                  *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP138652 09042021 zar7  NEW PROGRAM                        *            
      * BP138652 12042021 zar7  add display statement              *            
      * BP138652 19042021 zar7  CALL BP13C913 DYNAMICALLY          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F410 ASSIGN       TO BP13F410.                            
           SELECT BP13RPT1 ASSIGN       TO BP13RPT1.                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F410                                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD                                          
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
                                                                                
       FD  BP13RPT1                                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 200 CHARACTERS.                                      
       01  RPT1-REC.                                                            
           05 RPT1-REGN                      PIC X(08).                         
           05 RPT1-FILLER1                   PIC X(1).                          
           05 RPT1-SCH-ACCT                  PIC X(11).                         
           05 RPT1-FILLER2                   PIC X(1).                          
           05 RPT1-ADDR                      PIC X(60).                         
           05 RPT1-FILLER3                   PIC X(1).                          
           05 RPT1-DTE-AO                    PIC X(10).                         
           05 RPT1-FILLER4                   PIC X(1).                          
           05 RPT1-DTE-TP                    PIC X(10).                         
           05 RPT1-FILLER5                   PIC X(1).                          
           05 RPT1-LOAN-AMT                  PIC ZZZZZZZ.99.                    
           05 FILLER                         PIC X(86).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-EOF-BP13F410           PIC X      VALUE SPACES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ              PIC 9(6)   VALUE ZEROES.               
           05  WS-RPT1-WRITE             PIC 9(6)   VALUE ZEROES.               
           05  WS-PIDB-READ              PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-SQL-CODE                   PIC 9(4).                              
       01  WS-BP13C913                   PIC X(8)   VALUE 'BP13C913'.           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(05).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F410        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-BP13F410     THRU  3000-EXIT                   
                         UNTIL WS-EOF-BP13F410 = 'Y'.                           
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F410                                                
                 OUTPUT BP13RPT1.                                               
                                                                                
           STRING 'REPORT TO MONITOR OUTSTANDING CASES'                         
              DELIMITED BY SIZE INTO RPT1-REC.                                  
             WRITE RPT1-REC.                                                    
                                                                                
             MOVE SPACES TO RPT1-REC.                                           
                                                                                
           STRING                                                               
             'REGN NUM'                                                         
             ';'                                                                
             'SCH ACCT '                                                        
             ';'                                                                
             'ADDRESS   '                                                       
             ';'                                                                
             'AO DATE   '                                                       
             ';'                                                                
             'TP DATE   '                                                       
             ';'                                                                
             'LOAN AMT  '                                                       
             DELIMITED BY SIZE INTO RPT1-REC.                                   
             WRITE RPT1-REC.                                                    
                                                                                
             MOVE SPACES TO RPT1-REC.                                           
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13F410.                                                      
      *-------------------------------------------------------------            
           READ BP13F410                                                        
                AT END MOVE 'Y' TO WS-EOF-BP13F410                              
                GO TO  2000-EXIT.                                               
                                                                                
           ADD 1 TO WS-F410-READ.                                               
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-PROCESS-BP13F410.                                                   
      *-------------------------------------------------------------            
                                                                                
            IF F410-NUM-REGN = SPACES OR LOW-VALUES                             
               PERFORM 2000-READ-BP13F410    THRU 2000-EXIT                     
               GO TO 3000-EXIT.                                                 
                                                                                
            MOVE SPACES                 TO RPT1-REC.                            
                                                                                
            PERFORM 4000-GET-FLAT-ADDR  THRU 4000-EXIT.                         
                                                                                
            PERFORM 3500-WRITE-OUTPUT     THRU 3500-EXIT                        
            PERFORM 2000-READ-BP13F410    THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3500-WRITE-OUTPUT.                                                       
      *-------------------------------------------------------------            
                                                                                
            MOVE  ';'                  to RPT1-FILLER1                          
                                          RPT1-FILLER2                          
                                          RPT1-FILLER3                          
                                          RPT1-FILLER4                          
                                          RPT1-FILLER5.                         
                                                                                
            MOVE F410-NUM-REGN         TO RPT1-REGN                             
            MOVE F410-NUM-SCH-ACC      TO RPT1-SCH-ACCT                         
            MOVE F410-DTE-ARGORDER     TO RPT1-DTE-AO                           
            MOVE F410-DTE-KEY-ISSUE    TO RPT1-DTE-TP                           
            IF F410-AMT-LOAN NOT NUMERIC                                        
               MOVE ZEROES             TO RPT1-LOAN-AMT                         
            ELSE                                                                
               MOVE F410-AMT-LOAN      TO RPT1-LOAN-AMT                         
            END-IF.                                                             
                                                                                
            WRITE RPT1-REC.                                                     
            ADD 1 TO WS-RPT1-WRITE.                                             
                                                                                
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       4000-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                   TO BP13COMM8-REC                       
                                            WS-LINK-REC.                        
           INITIALIZE                       BP13COMM8-REC                       
                                            WS-LINK-REC.                        
                                                                                
           MOVE F410-NUM-SCH-ACC      TO  WS-LINK-REC.                          
           DISPLAY  'F410 - ' F410-NUM-SCH-ACC(1:9)                             
                   ' LINK - ' WS-LINK-REC.                                      
                                                                                
           CALL WS-BP13C913 USING WS-LINK-REC, BP13COMM8-REC                    
           END-CALL.                                                            
                                                                                
           DISPLAY 'SYSERR - ' COMM8-CDE-SYSERR.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR  TO WS-SQL-CODE                             
              IF COMM8-CDE-SYSERR = 100                                         
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         F410-NUM-SCH-ACC ', REGN : ' F410-NUM-REGN             
                         ' SQL CDE - ' WS-SQL-CODE                              
              ELSE                                                              
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' F410-NUM-SCH-ACC                            
                        ', REGN : ' F410-NUM-REGN                               
                         ' SQL CDE - ' WS-SQL-CODE                              
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-PIDB-READ                                                
           STRING 'BLK '  COMM8-NUM-BLK  '  '                                   
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO RPT1-ADDR.                                          
                                                                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' - BP13CR52 CONTROL TOTALS -----'.                         
            DISPLAY ' '.                                                        
            DISPLAY '  NO BP13F410 READ     = ' WS-F410-READ.                   
            DISPLAY '  NO PIDB     READ     = ' WS-PIDB-READ.                   
            DISPLAY '  NO BP13RPT1 WRITTEN  = ' WS-RPT1-WRITE.                  
            DISPLAY ' '.                                                        
                                                                                
            CLOSE BP13F410                                                      
                  BP13RPT1.                                                     
                                                                                
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
