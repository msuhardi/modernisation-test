       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC95.                                                 
       AUTHOR.        EDEN DINGAL.                                              
       DATE-WRITTEN.  02/11/2007.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO CREATE AC08F660                    *                
      *                  FOR GIRO TO CHECK CASH PAYMENT BEFORE *                
      *                  SECOND GIRO DEDUCTION.                *                
      *                                                        *                
      *    INPUT FILES : BP13F136                              *                
      *                  SY02F001                              *                
      *    OUTPUT FILES: AC08F660                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * ------    --     ----      -----------                 *                
      * BP133184  ED1   20071102   NEW PGM                     *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F136 ASSIGN       TO BP13F136.                            
           SELECT AC08F660 ASSIGN       TO AC08F660.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F136                                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F136.                                                           
                                                                                
       FD  AC08F660                                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AC08F660.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-DATE                     PIC X(8)  VALUE SPACES.               
          05  WS-TIME                     PIC X(8)  VALUE SPACES.               
                                                                                
       01 WS-SYS-DATE.                                                          
          05  WS-SYS-CCYY                 PIC 9(4).                             
          05  WS-SYS-MM                   PIC 9(2).                             
          05  WS-SYS-DD                   PIC 9(2).                             
                                                                                
       01  F660-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  NO-F660-WRITE                  PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-F136-READ                   PIC 9(5)  VALUE ZERO.                 
                                                                                
       01  WS-EOF-F136                    PIC X     VALUE SPACE.                
           88 EOF-F136                    VALUE 'Y'.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           MOVE FUNCTION CURRENT-DATE    TO  WS-CURR-DATE.                      
           PERFORM 2000-READ-BP13F136    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-AC08F660 THRU 3000-EXIT                         
             UNTIL EOF-F136.                                                    
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F136                                                 
                       SY02F001                                                 
               OUTPUT  AC08F660.                                                
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT TO WS-SYS-DATE.                                
           DISPLAY 'SYSTEM DATE : ' WS-SYS-DATE.                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F136.                                                      
      *---------------------------------------------------------------*         
           READ BP13F136 AT END                                                 
                MOVE 'Y' TO WS-EOF-F136                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-F136-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-AC08F660.                                                   
      *---------------------------------------------------------------*         
           IF F136-CDE-TRANS-TYPE = '11'                                        
              PERFORM 3100-CREATE-AC08F660 THRU 3100-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F136    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-CREATE-AC08F660.                                                    
      *---------------------------------------------------------------*         
           MOVE SPACES TO  AC08F660-REC.                                        
           INITIALIZE      AC08F660-REC.                                        
                                                                                
           MOVE F136-NUM-SCH-ACCT          TO F660-NUM-HDBREF.                  
           MOVE 'BP13'                     TO F660-NUM-SYS-ID.                  
           MOVE 'BP13'                     TO F660-NUM-TXN-ID.                  
           MOVE 'COLL'                     TO F660-NUM-PAYMT-TYPE.              
           MOVE 'COLL'                     TO F660-TXT-DESC-1.                  
           MOVE F136-NUM-RECPT-JRNO        TO F660-NUM-RCPT.                    
           MOVE F136-CDE-CRDR              TO F660-NUM-CRDR.                    
           MOVE F136-CDE-TRANS-TYPE        TO F660-CDE-TRAN.                    
           MOVE F136-DTE-TRANS             TO F660-DTE-TXN.                     
           MOVE WS-SYS-DATE                TO F660-DTE-CREATE.                  
                                                                                
           IF F136-AMT-RECEIPT IS NOT NUMERIC                                   
              MOVE 0 TO F136-AMT-RECEIPT                                        
           END-IF.                                                              
                                                                                
           MOVE F136-AMT-RECEIPT TO F660-AMT-TXN.                               
                                                                                
           WRITE AC08F660-REC.                                                  
                                                                                
           ADD 1   TO NO-F660-WRITE.                                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
           CLOSE BP13F136                                                       
                 AC08F660.                                                      
                                                                                
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CC95 ================*'.              
           DISPLAY '   NO. OF F136 RECORDS READ    = ' WS-F136-READ.            
           DISPLAY '   NO. OF F660 RECORDS CREATED = ' NO-F660-WRITE.           
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
