       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C39O.                                                  
      *AUTHOR.       LIZ PATIAG.                                                
      *DATE-WRITTEN. 11 OCT 2016.                                               
      ************************************************************              
      *                                                          *              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *                                                          *              
      *  SYSTEM ID   : BP13                                      *              
      *                                                          *              
      *  OBJECTIVE   : TO MATCH BP13FB05 WITH BP13INP  TO GET    *              
      *                THE RECORDS TO BE DELETED AND TO BE KEPT  *              
      *                                                          *              
      *  INPUT FILES :                                           *              
      *  1. BP13FB05                                             *              
      *  2. BP13INP - LIST OF REGN NO FROM F800,F730,F757        *              
      *                                                          *              
      *  OUTPUT FILES:                                           *              
      *  1. P13FB05A - KEEP RECORDS                              *              
      *  2. P13FB05B - DELETE RECORDS                            *              
      *                                                          *              
      * CHG-NO    BY    ON   - DESCRIPTION                       *              
      * ----------------------------------                       *              
      * BP134985 FNP1 111016 - NEW PGM                           *              
      * BP136458 FNP1 021116 - READ INP AGAIN AFTER SAME REG NO  *              
      *                        HAS FINISHED PROCESSING           *              
      ************************************************************              
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FB05  ASSIGN       TO BP13FB05.                           
           SELECT BP13INP   ASSIGN       TO BP13INP.                            
                                                                                
           SELECT BP13K410  ASSIGN       TO BP13K410                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K410-KEY-FLD                        
                            FILE STATUS  IS K410-STATUS.                        
                                                                                
           SELECT P13FB05A  ASSIGN       TO P13FB05A.                           
           SELECT P13FB05B  ASSIGN       TO P13FB05B.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13FB05                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13KB05.                                                           
                                                                                
       FD  BP13INP                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 20  CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13INP-REC.                                                         
           05  INP-NUM-REGN          PIC X(08).                                 
           05  INP-DTE-CANCEL        PIC X(08).                                 
           05  FILLER                PIC X(04).                                 
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD  P13FB05A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13FB05A-REC                PIC X(400).                              
                                                                                
       FD  P13FB05B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13FB05B-REC                PIC X(400).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-BP13FB05-READ            PIC 9(08) VALUE ZEROES.              
           05  WS-BP13INP-READ             PIC 9(08) VALUE ZEROES.              
           05  WS-P13FB05A-WRITE           PIC 9(08) VALUE ZEROES.              
           05  WS-P13FB05B-WRITE           PIC 9(08) VALUE ZEROES.              
           05  WS-K410-FND                 PIC 9(08) VALUE ZEROES.              
           05  WS-MATCH                    PIC 9(08) VALUE ZEROES.              
           05  WS-UNMATCH                  PIC 9(08) VALUE ZEROES.              
           05  WS-BYPASS                   PIC 9(08) VALUE ZEROES.              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-BP13FB05-EOF             PIC X(01) VALUE 'N'.                 
           05  WS-BP13INP-EOF              PIC X(01) VALUE 'N'.                 
           05  WS-FB05-END                 PIC X(01) VALUE 'N'.                 
           05  WS-INP-END                  PIC X(01) VALUE 'N'.                 
                                                                                
       01  WS-SYSTEM-DATE                 PIC X(08).                            
                                                                                
       01  WS-DATE-CCYYMM.                                                      
           05 WS-CCYYMM1.                                                       
              10 WS-CCYY1-NUM             PIC 9(04).                            
              10 WS-MM1-NUM               PIC 9(02).                            
           05 WS-CCYYMM2.                                                       
              10 WS-CCYY2-NUM             PIC 9(04).                            
              10 WS-MM2-NUM               PIC 9(02).                            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-REGN                PIC X(08) VALUE SPACES.              
           05  WS-DIFF-MON                 PIC 9(03) VALUE ZEROES.              
           05  K410-STATUS                 PIC 9(02) VALUE ZEROES.              
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13FB05   THRU 2000-EXIT.                         
           PERFORM 3000-READ-BP13INP    THRU 3000-EXIT.                         
           PERFORM 4000-PROCESS-RECORD  THRU 4000-EXIT                          
             UNTIL KB05-NUM-REGN   = HIGH-VALUES AND                            
                   INP-NUM-REGN    = HIGH-VALUES.                               
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-OPEN-FILES.                                                         
      *--------------------------------*                                        
                                                                                
           OPEN INPUT  BP13FB05                                                 
                       BP13INP                                                  
                       BP13K410                                                 
               OUTPUT  P13FB05A                                                 
                       P13FB05B.                                                
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K410 , STATUS = ' K410-STATUS          
              MOVE K410-STATUS               TO RETURN-CODE                     
              GO TO 9999-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
           MOVE WS-SYSTEM-DATE(1:6)     TO WS-CCYYMM2.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-READ-BP13FB05.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13FB05                                                        
             AT END                                                             
                MOVE HIGH-VALUES TO KB05-NUM-REGN                               
                MOVE 'Y'         TO WS-FB05-END                                 
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-BP13FB05-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-READ-BP13INP.                                                       
      *--------------------------------*                                        
                                                                                
           READ BP13INP                                                         
             AT END                                                             
                MOVE HIGH-VALUES TO INP-NUM-REGN                                
                MOVE 'Y'         TO WS-INP-END                                  
             GO TO 3000-EXIT.                                                   
                                                                                
           ADD 1           TO WS-BP13INP-READ.                                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-CHECK-DTE-CANCEL.                                                   
      *--------------------------------*                                        
      ***BYPASS RECORDS WITH DATE-CANCEL > 5 YEARS                              
           IF INP-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE ZEROES              TO WS-CCYYMM1                            
                                          WS-DIFF-MON                           
              MOVE INP-DTE-CANCEL(1:6) TO WS-CCYYMM1                            
              IF WS-CCYY1-NUM = WS-CCYY2-NUM                                    
                 COMPUTE WS-DIFF-MON =                                          
                            (WS-MM2-NUM - WS-MM1-NUM)                           
              ELSE                                                              
                  COMPUTE WS-DIFF-MON =                                         
                           ((WS-CCYY2-NUM * 12) + WS-MM2-NUM) -                 
                           ((WS-CCYY1-NUM * 12) + WS-MM1-NUM)                   
              END-IF                                                            
              IF WS-DIFF-MON > 60                                               
                 PERFORM 3000-READ-BP13INP  THRU 3000-EXIT                      
                 ADD 1 TO WS-BYPASS                                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-PROCESS-RECORD.                                                     
      *--------------------------------*                                        
           PERFORM 3100-CHECK-DTE-CANCEL    THRU 3100-EXIT                      
                                                                                
           DISPLAY 'KB05: ' KB05-NUM-REGN ' INP: ' INP-NUM-REGN                 
           IF KB05-NUM-REGN    = INP-NUM-REGN                                   
              ADD 1                      TO WS-MATCH                            
              MOVE KB05-NUM-REGN         TO WS-PREV-REGN                        
              PERFORM 5000-PROCESS-SAME-REGN  THRU 5000-EXIT                    
                 UNTIL KB05-NUM-REGN NOT = WS-PREV-REGN                         
                    OR KB05-NUM-REGN NOT = INP-NUM-REGN                         
                    OR WS-FB05-END = 'Y'                                        
                    OR WS-INP-END  = 'Y'                                        
                                                                                
              PERFORM 3000-READ-BP13INP THRU 3000-EXIT                          
           ELSE                                                                 
              IF KB05-NUM-REGN    > INP-NUM-REGN                                
                 ADD 1                      TO WS-UNMATCH                       
                 PERFORM 3000-READ-BP13INP THRU 3000-EXIT                       
              ELSE                                                              
                 ADD 1                      TO WS-UNMATCH                       
                 PERFORM 5100-READ-BP13K410 THRU 5100-EXIT                      
                 PERFORM 2000-READ-BP13FB05 THRU 2000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-PROCESS-SAME-REGN.                                                  
      *--------------------------------*                                        
                                                                                
           DISPLAY '5000-KB05: ' KB05-NUM-REGN ' INP: ' INP-NUM-REGN            
           MOVE SPACES             TO P13FB05A-REC.                             
                                                                                
           WRITE P13FB05A-REC      FROM BP13KB05-REC.                           
                                                                                
           ADD 1                   TO WS-P13FB05A-WRITE.                        
                                                                                
           PERFORM 2000-READ-BP13FB05 THRU 2000-EXIT.                           
      *    PERFORM 3000-READ-BP13INP THRU 3000-EXIT.                            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5100-READ-BP13K410.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE SPACES             TO K410-KEY-FLD.                             
           MOVE KB05-NUM-REGN      TO K410-NUM-REGN.                            
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE K410-STATUS                                                 
              WHEN 00                                                           
                 PERFORM 5200-CHECK-KEY-ISSUE      THRU 5200-EXIT               
                 ADD 1 TO WS-K410-FND                                           
              WHEN 23                                                           
      *          DISPLAY 'RECORD NOT FOUND IN BP13K410, KEY: '                  
      *                K410-NUM-REGN                                            
                 MOVE SPACES             TO P13FB05B-REC                        
                 WRITE P13FB05B-REC      FROM BP13KB05-REC                      
                 ADD 1                   TO WS-P13FB05B-WRITE                   
              WHEN OTHER                                                        
                 MOVE K410-STATUS        TO RETURN-CODE                         
                 DISPLAY 'ERROR READING BP13K410, STATUS IS '                   
                       K410-STATUS ' , KEY IS : ' K410-NUM-REGN                 
                 PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5200-CHECK-KEY-ISSUE.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE ZEROES             TO WS-CCYYMM1                                
                                      WS-DIFF-MON.                              
                                                                                
           IF K410-DTE-KEY-ISSUE NOT NUMERIC                                    
              MOVE ZEROES TO K410-DTE-KEY-ISSUE                                 
           END-IF.                                                              
                                                                                
           MOVE K410-DTE-KEY-ISSUE(1:6) TO WS-CCYYMM1.                          
                                                                                
           IF WS-CCYY1-NUM = WS-CCYY2-NUM                                       
              COMPUTE WS-DIFF-MON =                                             
                         (WS-MM2-NUM - WS-MM1-NUM)                              
           ELSE                                                                 
               COMPUTE WS-DIFF-MON =                                            
                        ((WS-CCYY2-NUM * 12) + WS-MM2-NUM) -                    
                        ((WS-CCYY1-NUM * 12) + WS-MM1-NUM)                      
           END-IF.                                                              
                                                                                
           IF WS-DIFF-MON < 60                                                  
              MOVE SPACES             TO P13FB05A-REC                           
              WRITE P13FB05A-REC      FROM BP13KB05-REC                         
              ADD 1                   TO WS-P13FB05A-WRITE                      
           ELSE                                                                 
              MOVE SPACES             TO P13FB05B-REC                           
              WRITE P13FB05B-REC      FROM BP13KB05-REC                         
              ADD 1                   TO WS-P13FB05B-WRITE                      
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9999-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           CLOSE      BP13FB05                                                  
                      BP13INP                                                   
                      BP13K410                                                  
                      P13FB05A                                                  
                      P13FB05B.                                                 
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '**************************'.                                
           DISPLAY 'PROGRAM ID     :  BP13C39O'.                                
           DISPLAY '**************************'.                                
           DISPLAY 'NO OF REC READ FROM BP13FB05 : ' WS-BP13FB05-READ.          
           DISPLAY 'NO OF REC READ FROM BP13INP  : ' WS-BP13INP-READ.           
           DISPLAY 'NO OF REC BYPASS FROM BP13INP: ' WS-BYPASS.                 
           DISPLAY 'NO OF REC FND  FROM BP13K410 : ' WS-K410-FND.               
           DISPLAY 'NO OF MATCHED RECORDS        : ' WS-MATCH.                  
           DISPLAY 'NO OF UNMATCHED RECORDS      : ' WS-UNMATCH.                
           DISPLAY 'RECORDS WRITTEN IN P13FB05A  : ' WS-P13FB05A-WRITE.         
           DISPLAY 'RECORDS WRITTEN IN P13FB05B  : ' WS-P13FB05B-WRITE.         
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K410 , STATUS = ' K410-STATUS          
              MOVE K410-STATUS               TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
