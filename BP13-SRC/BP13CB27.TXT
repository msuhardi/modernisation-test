       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB27.                                                 
       AUTHOR.        EDEN ROSE DINGAL.                                         
       DATE-WRITTEN.  24/06/20007.                                              
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : THIS PRGM SEARCHES THE INPUT FILE AND *                
      *                  OUTPUTS THE NO. OF UNSUCC ATTEMPTS    *                
      *                                                        *                
      *                : FOR ABORTIVE BTO, COUNT SUCC CASE AS  *                
      *                  1 UNSUCC ATTEMPT                      *                
      *                                                        *                
      *    INPUT  FILE  : BP13F595  - ALL CASES                *                
      *    OUTPUT FILES : BP13F593  - UNSUCC (S)               *                
      *                   P13F593A  - UNSUCC (R)               *                
      *                   P13F593B  - UNSUCC (U)               *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * BP133127 ED1 240807    NEW PROGRAM                     *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595 ASSIGN TO BP13F595.                                  
           SELECT BP13F593 ASSIGN TO BP13F593.                                  
           SELECT P13F593A ASSIGN TO P13F593A.                                  
           SELECT P13F593B ASSIGN TO P13F593B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F595                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F595.                                                           
                                                                                
       FD   BP13F593                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F593.                                                           
                                                                                
       FD   P13F593A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01 P13F593A-REC     PIC X(2000).                                         
                                                                                
       FD   P13F593B                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01 P13F593B-REC     PIC X(2000).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F595-READ                PIC 9(7)    VALUE ZEROS.              
           05 WS-F593A-WRITE              PIC 9(7)    VALUE ZEROS.              
           05 WS-F593B-WRITE              PIC 9(7)    VALUE ZEROS.              
           05 WS-F593C-WRITE              PIC 9(7)    VALUE ZEROS.              
                                                                                
       01  WS-SUCC-FD                     PIC X       VALUE 'N'.                
       01  WS-EOF-F595                    PIC X       VALUE 'N'.                
       01  WS-LAST-STATUS                 PIC X       VALUE SPACE.              
       01  WS-LAST-DTE                    PIC X(6)    VALUE SPACES.             
       01  WS-LAST-HH                     PIC X(1)    VALUE SPACES.             
       01  WS-LAST-NRIC                   PIC X(9)    VALUE SPACES.             
       01  WS-LAST-ALLO-CAT               PIC X(3)    VALUE SPACES.             
       01  WS-PREV-MODE                   PIC X(3)    VALUE SPACES.             
       01  WS-PREV-NRIC                   PIC X(9)    VALUE SPACES.             
       01  WS-PREV-REF                    PIC X(8)    VALUE SPACES.             
       01  WS-PREV-RANDOM-KEY             PIC X(27)   VALUE SPACES.             
       01  WS-PREV-DTE-BALLOT             PIC X(8)    VALUE SPACES.             
       01  WS-PREV-STATUS                 PIC X(1)    VALUE SPACES.             
       01  WS-PREV-F595-REC               PIC X(500)  VALUE SPACES.             
       01  WS-PREV-F595-REC-FT            PIC X(500)  VALUE SPACES.             
       01  WS-PREV-F595-REC-ST            PIC X(500)  VALUE SPACES.             
       01  WS-F595-NUM-NRIC               PIC X(9)    VALUE SPACES.             
       01  WS-CNT                         PIC 9(2)    VALUE ZEROS.              
       01  WS-CTR                         PIC 9(6)    VALUE ZEROS.              
       01  WS-CTR-CHAR REDEFINES WS-CTR.                                        
           05 WS-CTR-X                    PIC ZZZZZ9.                           
       01  WS-SUCC-CNT                    PIC 9(2)    VALUE ZEROS.              
       01  WS-UNSUCC-CNT                  PIC 9(2)    VALUE ZEROS.              
       01  WS-UNSUCC-FT                   PIC 9(2)    VALUE ZEROS.              
       01  WS-UNSUCC-ST                   PIC 9(2)    VALUE ZEROS.              
       01  WS-UNSUCC-ST-BE                PIC 9(2)    VALUE ZEROS.              
       01  WS-UNSUCC-ST-BTO               PIC 9(2)    VALUE ZEROS.              
       01  WS-UNSUCC-ST-DBS               PIC 9(2)    VALUE ZEROS.              
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************                                                        
       000-MAIN-ROUTINE.                                                        
      ******************                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F595      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F595 = 'Y'.                                           
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       100-INITIALIZATION.                                                      
      ********************                                                      
           OPEN INPUT  BP13F595                                                 
                OUTPUT BP13F593                                                 
                       P13F593A                                                 
                       P13F593B.                                                
                                                                                
           MOVE ZEROS                   TO WS-COUNTERS.                         
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       200-READ-F595.                                                           
      *****************                                                         
           READ BP13F595 AT END                                                 
                MOVE 'Y'             TO WS-EOF-F595                             
                MOVE HIGH-VALUES     TO F595-NUM-REGN                           
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1                    TO WS-F595-READ.                           
           MOVE BP13F595-REC(484:9)  TO WS-F595-NUM-NRIC.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       300-PROCESS-RTN.                                                         
      *****************                                                         
           MOVE SPACES                TO WS-PREV-RANDOM-KEY.                    
           MOVE SPACES                TO WS-PREV-REF.                           
           MOVE SPACES                TO WS-PREV-MODE.                          
           MOVE SPACES                TO WS-PREV-DTE-BALLOT.                    
           MOVE SPACES                TO WS-PREV-STATUS.                        
           INITIALIZE                 BP13F593-REC.                             
           MOVE WS-F595-NUM-NRIC   TO WS-PREV-NRIC.                             
           MOVE 'N'                TO WS-SUCC-FD.                               
           MOVE ZEROS              TO WS-SUCC-CNT.                              
           MOVE ZEROS              TO WS-UNSUCC-CNT.                            
           MOVE ZEROS              TO WS-UNSUCC-FT.                             
           MOVE ZEROS              TO WS-UNSUCC-ST.                             
           MOVE ZEROS              TO WS-UNSUCC-ST-BE.                          
           MOVE ZEROS              TO WS-UNSUCC-ST-BTO.                         
           MOVE ZEROS              TO WS-UNSUCC-ST-DBS.                         
           MOVE SPACES             TO BP13F593-REC.                             
           INITIALIZE                 BP13F593-REC.                             
                                                                                
           PERFORM 400-MATCHED-RECORD   THRU 400-EXIT UNTIL                     
              WS-F595-NUM-NRIC NOT =  WS-PREV-NRIC                              
                    OR WS-EOF-F595 = 'Y'.                                       
                                                                                
           IF WS-UNSUCC-CNT > 0                                                 
              PERFORM 500-WRITE-RECORD  THRU 500-EXIT                           
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       400-MATCHED-RECORD.                                                      
      ********************                                                      
      *    IF F595-NUM-ABORT          = 'Y' AND                                 
      *       F595-CDE-REQUEST-STATUS = 'S'                                     
      *       MOVE 'U' TO F595-CDE-REQUEST-STATUS                               
      *    END-IF.                                                              
                                                                                
           IF (WS-F595-NUM-NRIC = WS-PREV-NRIC)                                 
              IF F595-CDE-REQUEST-STATUS NOT = 'S' AND 'R'                      
                                                                                
                  IF (F595-NUM-REF  = WS-PREV-REF OR                            
                      F595-NUM-RANDOM(1:27)  = WS-PREV-RANDOM-KEY)              
                      CONTINUE                                                  
                  ELSE                                                          
                    IF WS-SUCC-FD = 'N'                                         
                       ADD  1                       TO WS-UNSUCC-CNT            
                       MOVE F595-NUM-NRIC1          TO WS-LAST-NRIC             
                       MOVE F595-CDE-REQUEST-STATUS TO WS-LAST-STATUS           
                       MOVE F595-DTE-BALLOT         TO WS-LAST-DTE              
                       MOVE F595-CDE-HOUSEHOLD      TO WS-LAST-HH               
                       MOVE F595-NUM-ALLO-CAT       TO WS-LAST-ALLO-CAT         
                       PERFORM 410-PROCESS-UNSUCC   THRU 410-EXIT               
                       PERFORM 420-LOG-UNSUCC-REC   THRU 420-EXIT               
                    END-IF                                                      
                  END-IF                                                        
                                                                                
              ELSE                                                              
                                                                                
                 IF WS-SUCC-FD = 'N'                                            
                    MOVE F595-NUM-NRIC1          TO  WS-LAST-NRIC               
                    MOVE F595-CDE-REQUEST-STATUS TO  WS-LAST-STATUS             
                    MOVE F595-DTE-BALLOT         TO  WS-LAST-DTE                
                    MOVE F595-CDE-HOUSEHOLD      TO  WS-LAST-HH                 
                    MOVE F595-NUM-ALLO-CAT       TO WS-LAST-ALLO-CAT            
                    ADD  1                TO WS-UNSUCC-CNT                      
                                                                                
                    IF (WS-SUCC-FD              =  'N'        ) AND             
                       (F595-NUM-REF          = WS-PREV-REF OR                  
                        F595-NUM-RANDOM(1:27) = WS-PREV-RANDOM-KEY)             
                        PERFORM 405-PROCESS-DUP-SUCC THRU 405-EXIT              
                    ELSE                                                        
                        PERFORM 410-PROCESS-UNSUCC   THRU 410-EXIT              
                    END-IF                                                      
                                                                                
                    PERFORM 420-LOG-UNSUCC-REC   THRU 420-EXIT                  
                    ADD   1    TO WS-SUCC-CNT                                   
                    MOVE 'Y'   TO WS-SUCC-FD                                    
                 END-IF                                                         
                                                                                
              END-IF                                                            
           ELSE                                                                 
                                                                                
              IF WS-SUCC-FD = 'N'                                               
                 IF (F595-NUM-REF          = WS-PREV-REF OR                     
                     F595-NUM-RANDOM(1:27) = WS-PREV-RANDOM-KEY)                
                     PERFORM 405-PROCESS-DUP-SUCC THRU 405-EXIT                 
                 ELSE                                                           
                     PERFORM 410-PROCESS-UNSUCC   THRU 410-EXIT                 
                 END-IF                                                         
                                                                                
                 PERFORM 420-LOG-UNSUCC-REC   THRU 420-EXIT                     
                                                                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F595            THRU 200-EXIT.                      
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       405-PROCESS-DUP-SUCC.                                                    
      **********************                                                    
      * DEDUCT 1 FROM COUNT IF PRECEDING DUPREC IS UNSUCC                       
      *-----------------------------------------------------------              
           IF WS-PREV-MODE = 'BE '                                              
              COMPUTE WS-UNSUCC-ST-BE  = WS-UNSUCC-ST-BE - 1                    
           ELSE                                                                 
           IF WS-PREV-MODE = 'BTO'                                              
              COMPUTE WS-UNSUCC-ST-BTO = WS-UNSUCC-ST-BTO - 1                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-CNT        = WS-UNSUCC-CNT.                               
           COMPUTE WS-UNSUCC-CNT = WS-UNSUCC-CNT - 1.                           
                                                                                
      * EMPTY F593-RECORD                                                       
      *---------------------                                                    
            MOVE SPACES TO F593-NUM-REGN          (WS-CNT)                      
                           F593-NUM-NRIC1         (WS-CNT)                      
                           F593-NUM-NRIC2         (WS-CNT)                      
                           F593-NUM-NRIC3         (WS-CNT)                      
                           F593-NUM-NRIC4         (WS-CNT)                      
                           F593-DTE-BALLOT        (WS-CNT)                      
                           F593-NUM-ALLO-CAT      (WS-CNT)                      
                           F593-CDE-NT            (WS-CNT)                      
                           F593-CDE-FLAT-TYPE     (WS-CNT)                      
                           F593-CDE-HOUSEHOLD     (WS-CNT)                      
                           F593-CDE-MCPS-TAG      (WS-CNT)                      
                           F593-CDE-REQUEST-STATUS(WS-CNT)                      
                           F593-DTE-REQUEST       (WS-CNT).                     
                                                                                
       405-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       410-PROCESS-UNSUCC.                                                      
      ********************                                                      
           MOVE F595-NUM-REF            TO WS-PREV-REF.                         
           MOVE F595-NUM-RANDOM(1:27)   TO WS-PREV-RANDOM-KEY.                  
           MOVE F595-NUM-ALLO-CAT       TO WS-PREV-MODE.                        
           MOVE F595-DTE-BALLOT         TO WS-PREV-DTE-BALLOT.                  
           MOVE F595-CDE-REQUEST-STATUS TO WS-PREV-STATUS.                      
                                                                                
           IF F595-CDE-HOUSEHOLD = 'H' OR 'T'                                   
              ADD 1        TO WS-UNSUCC-FT                                      
           ELSE                                                                 
              ADD 1     TO WS-UNSUCC-ST                                         
           END-IF.                                                              
                                                                                
           IF F595-NUM-ALLO-CAT = 'BE ' OR 'DBS'                                
              ADD 1     TO WS-UNSUCC-ST-BE                                      
           ELSE                                                                 
             IF F595-NUM-ALLO-CAT = 'BTO'                                       
                ADD 1     TO WS-UNSUCC-ST-BTO                                   
             ELSE                                                               
               IF F595-NUM-ALLO-CAT = '   '                                     
                ADD 1     TO WS-UNSUCC-ST-DBS                                   
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       420-LOG-UNSUCC-REC.                                                      
      *********************                                                     
           MOVE WS-F595-NUM-NRIC    TO F593-NUM-NRIC.                           
                                                                                
           IF F595-NUM-ABORT = LOW-VALUES                                       
              MOVE SPACE TO F595-NUM-ABORT                                      
           END-IF.                                                              
                                                                                
      * WRITE 20 RECORDS                                                        
      *------------------                                                       
           IF WS-UNSUCC-CNT <= 20                                               
              MOVE F595-NUM-REGN    TO F593-NUM-REGN(WS-UNSUCC-CNT)             
              MOVE F595-NUM-NRIC1   TO F593-NUM-NRIC1(WS-UNSUCC-CNT)            
                                                                                
              IF F595-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE F595-NUM-NRIC2   TO F593-NUM-NRIC2(WS-UNSUCC-CNT)         
              END-IF                                                            
              IF F595-NUM-NRIC3(1:1) NOT = '#'                                  
                 MOVE F595-NUM-NRIC3   TO F593-NUM-NRIC3(WS-UNSUCC-CNT)         
              END-IF                                                            
              IF F595-NUM-NRIC4(1:1) NOT = '#'                                  
                 MOVE F595-NUM-NRIC4   TO F593-NUM-NRIC4(WS-UNSUCC-CNT)         
              END-IF                                                            
                                                                                
              MOVE F595-DTE-BALLOT  TO F593-DTE-BALLOT(WS-UNSUCC-CNT)           
              MOVE F595-CDE-NT1     TO F593-CDE-NT    (WS-UNSUCC-CNT)           
              MOVE F595-CDE-REQUEST-STATUS  TO                                  
                   F593-CDE-REQUEST-STATUS(WS-UNSUCC-CNT)                       
              MOVE F595-DTE-REQUEST         TO                                  
                   F593-DTE-REQUEST       (WS-UNSUCC-CNT)                       
              MOVE F595-NUM-ALLO-CAT        TO                                  
                   F593-NUM-ALLO-CAT      (WS-UNSUCC-CNT)                       
              MOVE F595-CDE-FLAT-TYPE       TO                                  
                   F593-CDE-FLAT-TYPE     (WS-UNSUCC-CNT)                       
              MOVE F595-CDE-HOUSEHOLD       TO                                  
                   F593-CDE-HOUSEHOLD     (WS-UNSUCC-CNT)                       
              MOVE F595-NUM-ABORT           TO                                  
                   F593-NUM-ABORT         (WS-UNSUCC-CNT)                       
                                                                                
      * DETERMINE MCPS TAG                                                      
      *--------------------                                                     
              IF (F595-NUM-MCPS NOT = SPACE AND LOW-VALUE) AND                  
                 (F595-CDE-RESIDENT-HA1 NOT = 'H' AND                           
                  F595-CDE-RESIDENT-HA1 NOT = 'P' AND                           
                  F595-CDE-RESIDENT-HA1 NOT = 'M' )                             
                  MOVE 'Y'    TO   F593-CDE-MCPS-TAG(WS-UNSUCC-CNT)             
              ELSE                                                              
                  MOVE 'N'    TO   F593-CDE-MCPS-TAG(WS-UNSUCC-CNT)             
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       500-WRITE-RECORD.                                                        
      **********************                                                    
           MOVE WS-LAST-NRIC        TO F593-NUM-LAST-NRIC.                      
           MOVE WS-LAST-DTE         TO F593-DTE-LAST-QTR.                       
           MOVE WS-LAST-HH          TO F593-CDE-LAST-HH.                        
           MOVE WS-LAST-ALLO-CAT    TO F593-NUM-LAST-ALLO-CAT.                  
           MOVE WS-UNSUCC-CNT       TO F593-NUM-UNSUCC-TOT.                     
           MOVE WS-UNSUCC-ST-BE     TO F593-NUM-UNSUCC-BE.                      
           MOVE WS-UNSUCC-ST-BTO    TO F593-NUM-UNSUCC-BTO.                     
           MOVE WS-UNSUCC-ST-DBS    TO F593-NUM-UNSUCC-DBS.                     
           MOVE WS-LAST-STATUS      TO F593-NUM-LAST-STATUS.                    
                                                                                
           IF WS-LAST-STATUS = 'S'                                              
               WRITE                   BP13F593-REC                             
               ADD  1               TO WS-F593A-WRITE                           
           ELSE                                                                 
           IF WS-LAST-STATUS = 'R'                                              
               MOVE BP13F593-REC    TO P13F593A-REC                             
               WRITE                   P13F593A-REC                             
               ADD  1               TO WS-F593B-WRITE                           
               INITIALIZE              BP13F593-REC                             
           ELSE                                                                 
               MOVE BP13F593-REC    TO P13F593B-REC                             
               WRITE                   P13F593B-REC                             
               ADD  1               TO WS-F593C-WRITE                           
               INITIALIZE              BP13F593-REC                             
            END-IF.                                                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F595                                                       
                 BP13F593                                                       
                 P13F593A                                                       
                 P13F593B.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CB27 ================*'.              
           DISPLAY '  NO. OF F595 RECORDS READ   = ' WS-F595-READ.              
           DISPLAY '  NO. OF F593A RECORDS (S)   = ' WS-F593A-WRITE.            
           DISPLAY '  NO. OF F593B RECORDS (R)   = ' WS-F593B-WRITE.            
           DISPLAY '  NO. OF F593C RECORDS (U)   = ' WS-F593C-WRITE.            
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
                                                                                
