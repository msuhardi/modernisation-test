      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.   BP13C208.                                                  
       AUTHOR.       MJ16.                                                      
      *DATE-WRITTEN. 09/01/2004.                                                
      *REMARKS.                                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :   SYSTEM OF COMMITMENT   (SOC)                *        
      *                                                                *        
      *   SYSTEM ID    :   BP13                                        *        
      *                                                                *        
      *   OBJECTIVE    :   1. TO UPDATE BP13K800 FROM BP13F200         *        
      *                                                                *        
      *   INPUT FILE   :   1. BP13F200                                 *        
      *                                                                *        
      *   I-O   FILE   :   1. BP13K800                                 *        
      *                                                                *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *  --------  ---  ------  -------------------------------------  *        
      *  BP13XXX   MJ16 090104  NEW PROGRAM                            *        
      *  BP134226  ESA1 030511  CATER FOR NEW QUEUE NUMBER FORMAT      *        
      *  BP134513  ESA1 170412  CATER FOR MGPS                         *        
      *  BP136241  EL27 180516  ADD CHECKING OF SPACES FOR Q PREFIX    *        
      *  BP136442  ESA1 260916  TO ADD UPD OF K800-NUM-FLATLIST-BALANCE*        
      *  BP136671  KAM4 120417  ADD CHECK TO F200-NUM-FLATLIST-BALANCE *        
      *  BP137114  ESA1 260318  TO CORRECT UPDATE OF MGPS              *        
      ******************************************************************        
                                                                                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
      *============*                                                            
       FILE SECTION.                                                            
      *============*                                                            
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
      *=======================*                                                 
       WORKING-STORAGE SECTION.                                                 
      *=======================*                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  BP13K800-STATUS            PIC 99     VALUE 00.                  
           05  WS-EOF-F200                PIC X      VALUE 'N'.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F200-CNT                PIC 9(6)   VALUE ZEROS.               
           05  WS-UPD-K800-CNT            PIC 9(6)   VALUE ZEROS.               
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
            PERFORM  100-OPEN-ROUTINE      THRU     100-EXIT.                   
            PERFORM  200-READ-BP13F200     THRU     200-EXIT.                   
            PERFORM  400-MAIN-ROUTINE      THRU     400-EXIT                    
                         UNTIL WS-EOF-F200 = 'Y'.                               
            PERFORM  900-CLOSE-ROUTINE     THRU     900-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F200                                                 
                I-O    BP13K800.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K800, STATUS ' BP13K800-STATUS         
              MOVE     BP13K800-STATUS   TO   RETURN-CODE                       
              PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F200.                                                       
      ******************************************************************        
           READ BP13F200                                                        
                AT END MOVE  'Y' TO  WS-EOF-F200                                
                       GO TO 200-EXIT.                                          
                                                                                
           ADD  1  TO  WS-F200-CNT.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           MOVE F200-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
           EVALUATE BP13K800-STATUS                                             
            WHEN 00                                                             
              PERFORM 500-REWRITE-BP13K800   THRU  500-EXIT                     
            WHEN 23                                                             
              DISPLAY 'BP13K800 REC NOT FOUND, REGN  = ' F200-NUM-REGN          
            WHEN OTHER                                                          
              DISPLAY 'ERROR READ-UPD BP13K800, STAT = ' BP13K800-STATUS        
              DISPLAY '               K800-NUM-REGN  = ' F200-NUM-REGN          
              MOVE     BP13K800-STATUS TO  RETURN-CODE                          
              PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-EVALUATE.                                                        
                                                                                
           PERFORM    200-READ-BP13F200 THRU  200-EXIT.                         
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-REWRITE-BP13K800.                                                    
      ******************************************************************        
           IF F200-NUM-FLATLIST-BALANCE NOT = SPACES AND LOW-VALUES             
              MOVE F200-NUM-FLATLIST-BALANCE                                    
                                          TO K800-NUM-FLATLIST-BALANCE          
           END-IF.                                                              
           MOVE    F200-DTE-BKAPPMT-DATE  TO  K800-DTE-BK-APPT.                 
           MOVE    F200-TME-BKAPPMT       TO  K800-TME-BK-APPT.                 
           IF      F200-NUM-HHTY-QUEUE-SERIAL (1:1) NOT = SPACES THEN           
             MOVE  F200-NUM-HHTY-QUEUE-SERIAL (1:1) TO                          
                   K800-NUM-NT-FT-QUEUE (1:1)                                   
           END-IF.                                                              
           IF      F200-NUM-HHTY-QUEUE-SERIAL (2:1) NOT NUMERIC                 
           AND     F200-NUM-HHTY-QUEUE-SERIAL (2:1) NOT = SPACES THEN           
             MOVE  F200-NUM-HHTY-QUEUE-SERIAL (2:1) TO                          
                   K800-NUM-NT-FT-QUEUE (2:1)                                   
           END-IF.                                                              
           IF K800-NUM-MGPS = 'P' OR 'C'                                        
              MOVE F200-DTE-BKAPPMT-DATE-MGPS TO                                
                   K800-DTE-BK-APPT-MGPS                                        
              MOVE F200-TME-BKAPPMT-MGPS      TO K800-TME-BK-APPT-MGPS          
              IF F200-NUM-MGPS-QUEUE-SERIAL (1:1) NOT = SPACES THEN             
                 MOVE  F200-NUM-MGPS-QUEUE-SERIAL (1:1) TO                      
                       K800-NUM-QUEUE-MGPS (1:1)                                
              END-IF                                                            
              IF F200-NUM-MGPS-QUEUE-SERIAL (2:1) NOT NUMERIC AND               
                 F200-NUM-MGPS-QUEUE-SERIAL (2:1) NOT = SPACES THEN             
                 MOVE  F200-NUM-MGPS-QUEUE-SERIAL (2:1) TO                      
                       K800-NUM-QUEUE-MGPS (2:1)                                
              END-IF                                                            
           END-IF.                                                              
           MOVE    F200-DTE-RESELECTION TO                                      
                   K800-DTE-RESELECTION.                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE     TO  K800-DTE-UPDATE.                  
                                                                                
           MOVE    F200-DTE-ALLOC         TO  K800-NUM-ALLOC-DATE.              
           MOVE    'FN'                   TO  K800-NUM-ALLOC-TAG.               
                                                                                
           REWRITE   BP13K800-MASTER.                                           
                                                                                
           IF BP13K800-STATUS = 00                                              
              ADD  1  TO  WS-UPD-K800-CNT                                       
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING BP13K800, STAT = ' BP13K800-STATUS        
              DISPLAY '               K800-NUM-REGN  = ' K800-NUM-REGN          
              MOVE   BP13K800-STATUS  TO  RETURN-CODE                           
              PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C208 SUMMARY --------------*'.             
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F200 REC READ    = ' WS-F200-CNT.                 
           DISPLAY 'NO OF BP13F800 REC UPDATED = ' WS-UPD-K800-CNT.             
                                                                                
           CLOSE BP13F200                                                       
                 BP13K800.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' BP13K800-STATUS         
              MOVE     BP13K800-STATUS   TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
