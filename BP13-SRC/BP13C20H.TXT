       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C20H.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  06/11/2007.                                               
      * ========================================================== *            
      * SYSTEM NAME : SYSTEM OF COMMITMENT (BP13)                  *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE: 1) TO EXTRACT ALL MATCHED FIS CASES.            *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP13XXXX 20060717 LMS   NEW PGM                            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE IS DYNAMIC                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K110-KEY-FLD                                    
                  ALTERNATE RECORD KEY IS K110-AIX1                             
                  FILE STATUS IS WS-K110-STATUS.                                
                                                                                
           SELECT BM06F110 ASSIGN TO BM06F110.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F205                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       FD  BM06F110                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F110.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FLAG                  PIC X(1)  VALUE SPACES.                     
       01  WS-BP13F205-EOF          PIC X(1)  VALUE SPACES.                     
       01  WS-K110-STATUS           PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-F205-READ             PIC 9(7)  VALUE ZEROS.                      
       01  WS-K110-READ             PIC 9(7)  VALUE ZEROS.                      
       01  WS-F110-WRITE            PIC 9(7)  VALUE ZEROS.                      
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-DATE              PIC X(8).                                   
           05  WS-CURRENT-TIME      PIC X(9).                                   
           05  FILLER               PIC X(4).                                   
       01  WS-CURR-TIME.                                                        
           05  WS-CURR-TIME1        PIC X(9).                                   
           05  WS-TIME REDEFINES WS-CURR-TIME1.                                 
              10  WS-HH             PIC 9(2).                                   
              10  WS-MM             PIC 9(2).                                   
              10  WS-SS             PIC 9(2).                                   
              10  WS-MS             PIC 9(2).                                   
              10  WS-GM             PIC 9(1).                                   
       01  WS-GM-CNT                PIC S9(8) COMP VALUE ZEROS.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE       THRU 1000-EXIT.                   
           PERFORM 2000-READ-BP13F205         THRU 2000-EXIT.                   
           PERFORM 3000-PROCESS-BP13F205      THRU 3000-EXIT                    
             UNTIL WS-BP13F205-EOF = 'Y'.                                       
           PERFORM 9000-CLOSE-ROUTINE         THRU 9000-EXIT.                   
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BM06K110                                                 
                OUTPUT BM06F110.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F205.                                                      
      *-------------------------------------------------------------            
           READ BP13F205 AT END                                                 
              MOVE 'Y'  TO  WS-BP13F205-EOF                                     
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1        TO  WS-F205-READ.                                       
           MOVE  SPACES TO  WS-FLAG.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-BP13F205.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES  TO  K110-AIX1                                           
                            K110-KEY-FLD.                                       
                                                                                
           PERFORM 3200-PROCESS-BM06K110        THRU 3200-EXIT.                 
                                                                                
           PERFORM 2000-READ-BP13F205           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-PROCESS-BM06K110.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE  F205-KEY-FLD (1:11)     TO   K110-AIX1 (1:11).                 
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           IF WS-K110-STATUS = 00 OR 02                                         
              PERFORM UNTIL WS-FLAG = 'Y'                                       
                 PERFORM 3400-READ-BM06K110  THRU 3400-EXIT                     
              END-PERFORM                                                       
                                                                                
           ELSE                                                                 
              DISPLAY 'ERROR START READ BM06K110 ' WS-K110-STATUS               
              MOVE 'Y'     TO WS-FLAG                                           
                                                                                
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-FLAG                                      
                GO TO 3400-EXIT.                                                
                                                                                
           IF WS-K110-STATUS = 00 OR 02                                         
              IF K110-AIX1(1:11) = F205-KEY-FLD(1:11)                           
                 IF K110-ALLOCN-STATUS = 'R'                                    
                    ADD 1 TO WS-K110-READ                                       
                    PERFORM 6000-OUTPUT-FILE   THRU 6000-EXIT                   
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'    TO   WS-FLAG                                       
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'    TO   WS-FLAG                                          
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-OUTPUT-FILE.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE K110-REC                      TO F110-REC.                      
                                                                                
           WRITE F110-REC.                                                      
           ADD 1 TO WS-F110-WRITE.                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           CLOSE BP13F205                                                       
                 BM06K110                                                       
                 BM06F110.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
               MOVE WS-K110-STATUS TO RETURN-CODE                               
               DISPLAY 'CLOSING ERROR, K110-STATUS ' WS-K110-STATUS.            
                                                                                
                                                                                
           DISPLAY ' - BP13C20H CONTROL TOTALS ------'.                         
           DISPLAY ' '.                                                         
           DISPLAY ' BP13F205 READ    : ' WS-F205-READ.                         
           DISPLAY ' BM06K110 READ    : ' WS-K110-READ.                         
           DISPLAY ' BM06F110 WRITE   : ' WS-F110-WRITE.                        
           DISPLAY ' '.                                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
