      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C22A.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  09 JAN 2004.                                              
      *===============================================================*         
      *                SYSTEM OF COMMITMENT  (BP13)                   *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE :                                                  *         
      *      1. CHANGE SELECTION TYPE BASED ON ALLOCATION DATE IN     *         
      *         BM06K110, BM06F800, BP13K270, BP13K240, BP13K230      *         
      *---------------------------------------------------------------*         
      * CHG REF   BY    DATE        DESCRIPTION                       *         
      * --------  ---   ------      -----------                       *         
      * BP132485  JF4   09/01/2004  NEW PROGRAM                       *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN       TO BP13F205.                           
                                                                                
           SELECT BM06F800  ASSIGN       TO BM06F800.                           
                                                                                
           SELECT BP13K230  ASSIGN       TO BP13K230                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K230-KEY-FLD                        
                            FILE STATUS  IS BP13K230-STATUS.                    
                                                                                
           SELECT BP13K240  ASSIGN       TO BP13K240                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K240-KEY-FLD                        
                            FILE STATUS  IS BP13K240-STATUS.                    
                                                                                
           SELECT BP13K270  ASSIGN       TO BP13K270                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K270-KEY-FLD                        
                            FILE STATUS  IS BP13K270-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS BM06K110-STATUS.                    
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS    80 CHARACTERS.                                    
       COPY BP13F205.                                                           
                                                                                
       FD  BM06F800                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS   800 CHARACTERS.                                    
       COPY BM06F800.                                                           
                                                                                
       FD  BP13K230                                                             
           RECORD CONTAINS   450 CHARACTERS.                                    
       COPY BP13K230.                                                           
                                                                                
       FD  BP13K240                                                             
           RECORD CONTAINS   450 CHARACTERS.                                    
       COPY BP13K240.                                                           
                                                                                
       FD  BP13K270                                                             
           RECORD CONTAINS   250 CHARACTERS.                                    
       COPY BP13K270.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS   500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
                                                                                
      *--------------------------*                                              
       WORKING-STORAGE SECTION.                                                 
      *--------------------------*                                              
       01 WS-FILE-STATUS-VARS.                                                  
          05 BP13K230-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP13K240-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP13K270-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BM06K110-STATUS        PIC 9(02)  VALUE ZEROES.                    
                                                                                
          05 BP13F205-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13F205-EOF        VALUE 'Y'.                                  
          05 BM06F800-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BM06F800-EOF        VALUE 'Y'.                                  
          05 BP13K230-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13K230-EOF        VALUE 'Y'.                                  
          05 BP13K240-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13K240-EOF        VALUE 'Y'.                                  
          05 BP13K270-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13K270-EOF        VALUE 'Y'.                                  
          05 BM06K110-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BM06K110-EOF        VALUE 'Y'.                                  
                                                                                
       01 WS-DATE-VARS.                                                         
          05 WS-CURR-DATE           PIC X(08)  VALUE SPACES.                    
          05 WS-EDIT-DATE           PIC X(10)  VALUE SPACES.                    
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F205-READ-CNT       PIC 9(08)  VALUE ZEROES.                    
          05 WS-K230-UPD-CNT        PIC 9(08)  VALUE ZEROES.                    
          05 WS-K240-UPD-CNT        PIC 9(08)  VALUE ZEROES.                    
          05 WS-K270-UPD-CNT        PIC 9(08)  VALUE ZEROES.                    
          05 WS-F800-UPD-CNT        PIC 9(08)  VALUE ZEROES.                    
          05 WS-K110-UPD-CNT        PIC 9(08)  VALUE ZEROES.                    
                                                                                
       01 WS-EDIT1                  PIC Z(7)9.                                  
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F205      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-DATA       THRU 3000-EXIT                       
                         UNTIL BP13F205-EOF.                                    
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F205                                                
                 I-O    BM06F800                                                
                        BP13K230                                                
                        BP13K240                                                
                        BP13K270                                                
                        BM06K110.                                               
                                                                                
           IF BP13K230-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K230 OPEN ERROR, STATUS ' BP13K230-STATUS             
             MOVE BP13K230-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP13K240-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K240 OPEN ERROR, STATUS ' BP13K240-STATUS             
             MOVE BP13K240-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP13K270-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K270 OPEN ERROR, STATUS ' BP13K270-STATUS             
             MOVE BP13K270-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BM06K110 OPEN ERROR, STATUS ' BM06K110-STATUS             
             MOVE BM06K110-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE(7:2), '/',                                       
                  WS-CURR-DATE(5:2), '/',                                       
                  WS-CURR-DATE(1:4)  DELIMITED BY SIZE                          
                  INTO WS-EDIT-DATE.                                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F205.                                                      
      *---------------------------------------------------------------*         
           READ BP13F205 AT END                                                 
                          MOVE  'Y'   TO BP13F205-FLAG                          
                         NOT AT END                                             
                          ADD  1      TO WS-F205-READ-CNT                       
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           IF F205-NUM-OPTION = 'ST'                                            
              PERFORM 4000-UPDATE-BM06F800    THRU 4000-EXIT                    
              PERFORM 5000-UPDATE-BP13K230    THRU 5000-EXIT                    
              PERFORM 6000-UPDATE-BP13K240    THRU 6000-EXIT                    
              PERFORM 7000-UPDATE-BP13K270    THRU 7000-EXIT                    
              PERFORM 8000-UPDATE-BM06K110    THRU 8000-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F205         THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-UPDATE-BM06F800.                                                    
      *---------------------------------------------------------------*         
           PERFORM 4100-READ-BM06F800     THRU 4100-EXIT.                       
           PERFORM 4200-CHK-SEL-ALLOC     THRU 4200-EXIT                        
                                          UNTIL BM06F800-EOF.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-READ-BM06F800.                                                      
      *---------------------------------------------------------------*         
           READ BM06F800 AT END                                                 
                          MOVE  'Y'   TO BM06F800-FLAG                          
           END-READ.                                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-CHK-SEL-ALLOC.                                                      
      *---------------------------------------------------------------*         
           IF (F800-DTE-ALLOCN     =  F205-DTE-ALLOCN)  AND                     
              (F800-NUM-SELECTION  =  F205-NUM-SELECTION)                       
                                                                                
               DISPLAY 'BM06F800 KEY-FLD    : ' F800-KEY-FLD                    
               DISPLAY 'SELECTION TYPE (BF) : ' F800-NUM-SELECTION              
               MOVE  F205-NUM-SELECTION-NEW  TO F800-NUM-SELECTION              
                                                F800-CDE-SELTY                  
               DISPLAY 'SELECTION TYPE (AF) : ' F800-NUM-SELECTION              
               REWRITE F800-REC                                                 
               ADD  1      TO WS-F800-UPD-CNT                                   
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-BM06F800     THRU 4100-EXIT.                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-UPDATE-BP13K230.                                                    
      *---------------------------------------------------------------*         
           MOVE SPACES  TO  K230-KEY-FLD.                                       
           PERFORM 5100-START-BP13K230    THRU 5100-EXIT.                       
           IF BP13K230-STATUS = 00                                              
              PERFORM 5200-READNXT-BP13K230     THRU 5200-EXIT                  
                                          UNTIL BP13K230-EOF                    
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5100-START-BP13K230.                                                     
      *---------------------------------------------------------------*         
           START BP13K230 KEY >= K230-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K230-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'NO RECORDS FOUND IN BP13K230 '                          
             WHEN OTHER                                                         
               DISPLAY 'BP13K230 START ERROR, STATUS ' BP13K230-STATUS          
               MOVE BP13K230-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5200-READNXT-BP13K230.                                                   
      *---------------------------------------------------------------*         
           READ BP13K230 NEXT.                                                  
                                                                                
           EVALUATE BP13K230-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF (K230-DTE-ALLOC      =  F205-DTE-ALLOCN)  AND                 
                  (K230-NUM-SELECTION  =  F205-NUM-SELECTION)                   
                  PERFORM 5300-REWRITE-BP13K230   THRU 5300-EXIT                
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BP13K230-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K230 READNXT ERROR, STATUS '                        
                  BP13K230-STATUS  '  KEY : ' K230-KEY-FLD                      
               MOVE BP13K230-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5300-REWRITE-BP13K230.                                                   
      *---------------------------------------------------------------*         
           DISPLAY 'BP13K230 KEY-FLD    : ' K230-KEY-FLD.                       
           DISPLAY 'SELECTION TYPE (BF) : ' K230-NUM-SELECTION.                 
           MOVE  F205-NUM-SELECTION-NEW  TO K230-NUM-SELECTION                  
                                            K230-NUM-SELECTION2                 
                                            K230-NUM-SELECTION4.                
           DISPLAY 'SELECTION TYPE (AF) : ' K230-NUM-SELECTION.                 
                                                                                
           REWRITE K230-DETAIL-REC.                                             
                                                                                
           EVALUATE BP13K230-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               ADD  1      TO WS-K230-UPD-CNT                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K230 REWRITE ERROR, STATUS '                        
                  BP13K230-STATUS  '  KEY : ' K230-KEY-FLD                      
               MOVE BP13K230-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-UPDATE-BP13K240.                                                    
      *---------------------------------------------------------------*         
           MOVE SPACES  TO  K240-KEY-FLD.                                       
           PERFORM 6100-START-BP13K240    THRU 6100-EXIT.                       
           IF BP13K240-STATUS = 00                                              
              PERFORM 6200-READNXT-BP13K240     THRU 6200-EXIT                  
                                          UNTIL BP13K240-EOF                    
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6100-START-BP13K240.                                                     
      *---------------------------------------------------------------*         
           START BP13K240 KEY >= K240-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K240-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'NO RECORDS FOUND IN BP13K240 '                          
             WHEN OTHER                                                         
               DISPLAY 'BP13K240 START ERROR, STATUS ' BP13K240-STATUS          
               MOVE BP13K240-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6200-READNXT-BP13K240.                                                   
      *---------------------------------------------------------------*         
           READ BP13K240 NEXT.                                                  
                                                                                
           EVALUATE BP13K240-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF (K240-DTE-ALLOC      =  F205-DTE-ALLOCN)  AND                 
                  (K240-NUM-SELECTION  =  F205-NUM-SELECTION)                   
                  PERFORM 6300-REWRITE-BP13K240   THRU 6300-EXIT                
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BP13K240-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K240 READNXT ERROR, STATUS '                        
                  BP13K240-STATUS  '  KEY : ' K240-KEY-FLD                      
               MOVE BP13K240-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6300-REWRITE-BP13K240.                                                   
      *---------------------------------------------------------------*         
           DISPLAY 'BP13K240 KEY-FLD    : ' K240-KEY-FLD.                       
           DISPLAY 'SELECTION TYPE (BF) : ' K240-NUM-SELECTION.                 
           MOVE  F205-NUM-SELECTION-NEW  TO K240-NUM-SELECTION                  
                                            K240-NUM-SELECTION2                 
                                            K240-NUM-SELECTION3                 
                                            K240-NUM-SELECTION4.                
           DISPLAY 'SELECTION TYPE (AF) : ' K240-NUM-SELECTION.                 
                                                                                
           REWRITE K240-HEADER-REC.                                             
                                                                                
           EVALUATE BP13K240-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               ADD  1      TO WS-K240-UPD-CNT                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K240 REWRITE ERROR, STATUS '                        
                  BP13K240-STATUS  '  KEY : ' K240-KEY-FLD                      
               MOVE BP13K240-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7000-UPDATE-BP13K270.                                                    
      *---------------------------------------------------------------*         
           MOVE   SPACES           TO  K270-KEY-FLD.                            
           MOVE  F205-DTE-ALLOCN   TO  K270-ALLOCN-DATE.                        
           PERFORM 7100-START-BP13K270       THRU 7100-EXIT.                    
                                                                                
           IF BP13K270-STATUS = 00                                              
              PERFORM 7200-READNXT-BP13K270  THRU 7200-EXIT UNTIL               
                (K270-ALLOCN-DATE  NOT =  F205-DTE-ALLOCN)  OR                  
                (BP13K270-EOF)                                                  
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7100-START-BP13K270.                                                     
      *---------------------------------------------------------------*         
           START BP13K270 KEY >= K270-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K270-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP13K270, KEY : '                   
                                                      K270-KEY-FLD              
             WHEN OTHER                                                         
               DISPLAY 'BP13K270 START ERROR, STATUS ' BP13K270-STATUS          
               MOVE BP13K270-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7200-READNXT-BP13K270.                                                   
      *---------------------------------------------------------------*         
           READ BP13K270 NEXT.                                                  
                                                                                
           EVALUATE BP13K270-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF (K270-ALLOCN-DATE    =  F205-DTE-ALLOCN)  AND                 
                  (K270-CDE-SELTY      =  F205-NUM-SELECTION)                   
                  PERFORM 7300-REWRITE-BP13K270   THRU 7300-EXIT                
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BP13K270-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K270 READNXT ERROR, STATUS '                        
                  BP13K270-STATUS  '  KEY : ' K270-KEY-FLD                      
               MOVE BP13K270-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7300-REWRITE-BP13K270.                                                   
      *---------------------------------------------------------------*         
           DISPLAY 'BP13K270 KEY-FLD    : ' K270-KEY-FLD.                       
           DISPLAY 'SELECTION TYPE (BF) : ' K270-CDE-SELTY.                     
           MOVE  F205-NUM-SELECTION-NEW  TO K270-CDE-SELTY.                     
           DISPLAY 'SELECTION TYPE (AF) : ' K270-CDE-SELTY.                     
                                                                                
           REWRITE K270-REC.                                                    
                                                                                
           EVALUATE BP13K270-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               ADD  1      TO WS-K270-UPD-CNT                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K270 REWRITE ERROR, STATUS '                        
                  BP13K270-STATUS  '  KEY : ' K270-KEY-FLD                      
               MOVE BP13K270-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       8000-UPDATE-BM06K110.                                                    
      *---------------------------------------------------------------*         
           MOVE SPACES  TO  K110-KEY-FLD.                                       
           PERFORM 8100-START-BM06K110    THRU 8100-EXIT.                       
           IF BM06K110-STATUS = 00                                              
              PERFORM 8200-READNXT-BM06K110     THRU 8200-EXIT                  
                                                UNTIL BM06K110-EOF              
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       8100-START-BM06K110.                                                     
      *---------------------------------------------------------------*         
           START BM06K110 KEY >= K110-KEY-FLD.                                  
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'NO RECORDS FOUND IN BM06K110 '                          
             WHEN OTHER                                                         
               DISPLAY 'BM06K110 START ERROR, STATUS ' BM06K110-STATUS          
               MOVE BM06K110-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       8200-READNXT-BM06K110.                                                   
      *---------------------------------------------------------------*         
           READ BM06K110 NEXT.                                                  
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF (K110-DTE-ALLOCN     =  F205-DTE-ALLOCN)  AND                 
                  (K110-NUM-SELECTION  =  F205-NUM-SELECTION)                   
                  PERFORM 8300-REWRITE-BM06K110   THRU 8300-EXIT                
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BM06K110-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BM06K110 READNXT ERROR, STATUS '                        
                  BM06K110-STATUS  '  KEY : ' K110-KEY-FLD                      
               MOVE BM06K110-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       8300-REWRITE-BM06K110.                                                   
      *---------------------------------------------------------------*         
           DISPLAY 'BM06K110 KEY-FLD    : ' K110-KEY-FLD.                       
           DISPLAY 'SELECTION TYPE (BF) : ' K110-NUM-SELECTION.                 
           MOVE  F205-NUM-SELECTION-NEW  TO K110-NUM-SELECTION.                 
           DISPLAY 'SELECTION TYPE (AF) : ' K110-NUM-SELECTION.                 
                                                                                
           REWRITE K110-REC.                                                    
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               ADD  1      TO WS-K110-UPD-CNT                                   
             WHEN OTHER                                                         
               DISPLAY 'BM06K110 REWRITE ERROR, STATUS '                        
                  BM06K110-STATUS  '  KEY : ' K110-KEY-FLD                      
               MOVE BM06K110-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C22A       ' WS-EDIT-DATE.              
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F205-READ-CNT      TO WS-EDIT1.                              
           DISPLAY ' NO OF RECS READ FROM BP13F205   : ' WS-EDIT1.              
           MOVE WS-K230-UPD-CNT       TO WS-EDIT1.                              
           DISPLAY ' NO OF RECS UPDATED IN BP13K230  : ' WS-EDIT1.              
           MOVE WS-K240-UPD-CNT       TO WS-EDIT1.                              
           DISPLAY ' NO OF RECS UPDATED IN BP13K240  : ' WS-EDIT1.              
           MOVE WS-K270-UPD-CNT       TO WS-EDIT1.                              
           DISPLAY ' NO OF RECS UPDATED IN BP13K270  : ' WS-EDIT1.              
           MOVE WS-F800-UPD-CNT       TO WS-EDIT1.                              
           DISPLAY ' NO OF RECS UPDATED IN BM06F800  : ' WS-EDIT1.              
           MOVE WS-K110-UPD-CNT       TO WS-EDIT1.                              
           DISPLAY ' NO OF RECS UPDATED IN BM06K110  : ' WS-EDIT1.              
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F205                                                     
                   BM06F800                                                     
                   BP13K230                                                     
                   BP13K240                                                     
                   BP13K270                                                     
                   BM06K110.                                                    
                                                                                
           IF BP13K230-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K230 CLOSE ERROR, STATUS ' BP13K230-STATUS            
             MOVE BP13K230-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF BP13K240-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K240 CLOSE ERROR, STATUS ' BP13K240-STATUS            
             MOVE BP13K240-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF BP13K270-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K270 CLOSE ERROR, STATUS ' BP13K270-STATUS            
             MOVE BP13K270-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BM06K110 CLOSE ERROR, STATUS ' BM06K110-STATUS            
             MOVE BM06K110-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
************************** END OF BP13C22A ****************************         
                                                                                
