      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38I.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  NOVEMBER 20, 2013.                                        
      *===============================================================*         
      * OBJECTIVE  :  PRINT TRANSACTION CASES FOR BCP USE             *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F454                                     *         
      *               2. BP13K800                                     *         
      *               2. BP13K820                                     *         
      *               2. BP13K857                                     *         
      * OUTPUT FILE:  1. BP13L38I                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP135181 07/03/2014 PCL4 NEW PROGRAM.                         *         
      * BP135160 27/03/2014 PCL4 TO INITIALIZE FIELDS BEFORE PRINTING *         
      * BP135537 06/05/2015 RJB1 TO REMOVE DEAD CODE WS-REPORT-DTL    *         
      * BP136744 24/05/2017 PCL4 TO CATER FOR SL/PL APPT TYPE         *         
      * BP138242 27/03/2020 KV5  TO INCLUDED CONDITIONS TO CHECK      *         
      * BP138242 03/04/2020 KTM3 TO CHANGE RANGE FROM 2MTH TO ALL     *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F454  ASSIGN        TO BP13F454.                         
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
           SELECT  BP13K857  ASSIGN        TO BP13K857                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-NUM-REGN                     
                             FILE STATUS   IS WS-K857-STATUS.                   
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
           SELECT BP13K480   ASSIGN        TO BP13K480                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS RANDOM                            
                             RECORD KEY    IS K480-KEY-FLD                      
                             FILE STATUS   IS WS-K480-STATUS.                   
                                                                                
           SELECT  BP13L38I  ASSIGN        TO BP13L38I.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F454                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F454.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K480                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K480.                                                           
                                                                                
       FD   BP13L38I                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS V.                                                
       01   BP13L38I-REC               PIC X(200).                              
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F454             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K857-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K480-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F454-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38I-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-REGN              PIC X(8)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-PRINT-DATE            PIC 9(8)  VALUE ZEROES.                  
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-PRIO-DTE1.                                                         
          05  WS-PRIO-CCYY1                PIC 9(04).                           
          05  WS-PRIO-MM1                  PIC 9(02).                           
          05  WS-PRIO-DD1                  PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  DATE-REC.                                                            
           05  WS-DATE1.                                                        
               10  WS-CCYY-YEAR            PIC 9(4).                            
                   88  VALID-YEAR             VALUE 1900 THRU 2100.             
               10  WS-MM-MONTH             PIC 9(2).                            
                   88  31-DAY-MONTH           VALUE 01, 03, 05, 07, 08,         
                                                    10, 12.                     
                   88  30-DAY-MONTH           VALUE 04, 06, 09, 11.             
                   88  28-29-DAY-MONTH        VALUE 02.                         
                   88  VALID-MONTH            VALUE 01 THRU 12.                 
               10  WS-DD-DAY               PIC 9(2).                            
                   88  VALID-DAY              VALUE 01 THRU 31.                 
                                                                                
       01  WS-DATE-IN-OUT.                                                      
           05  IN-DATE                     PIC 9(8).                            
           05  IN-MONTH                    PIC 9(2).                            
           05  OUT-DATE                    PIC 9(8).                            
                                                                                
       01 WS-REM                           PIC 9(3).                            
       01 WS-YEAR                          PIC 9(4).                            
       01 WS-COMM-LEN                      PIC 9(2)     VALUE 18.               
       01 WS-APPT-TYPE                     PIC X(02).                           
       01 WS-DATE.                                                              
          05  WS-CCYY                      PIC 9(04).                           
          05  WS-MM                        PIC 9(02).                           
          05  WS-DD                        PIC 9(02).                           
       01  WS-L38I-HEADER          PIC X(130)    VALUE SPACES.                  
                                                                                
       01  L38I-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38I'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38I-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38I-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  L38I-HEADER02.                                                       
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE                          
               'REPORT ON SCHEDULE BCP CASES                   '.               
                                                                                
       01  L38I-HEADER2A.                                                       
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(37)     VALUE                          
               'DETAILS OF THE TRANSACTION CASES FOR '.                         
           05  L38I-DTE-TRANS      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' - '.                   
           05  L38I-DTE-TRANS1     PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(37)     VALUE                          
               ' (BCP USE)'.                                                    
                                                                                
       01  L38I-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(09)    VALUE 'NRIC    '.               
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(30)    VALUE 'HA1 NAME '.              
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(08)    VALUE 'TEL NO.'.                
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(08)    VALUE 'HP NO.'.                 
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(10)    VALUE 'OFFICE NO.'.             
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(40)    VALUE 'EMAIL'.                  
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(09)    VALUE 'SOC TYPE'.               
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(09)    VALUE 'HQMS TYPE'.              
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(10)    VALUE 'DATE'.                   
           05  FILLER              PIC X(01)    VALUE ';'.                      
           05  FILLER              PIC X(05)    VALUE 'TIME'.                   
           05  FILLER              PIC X(01)    VALUE ';'.                      
                                                                                
       01  L38I-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38I-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER1             PIC X(01)        VALUE SPACES.               
           05  L38I-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER2             PIC X(01)        VALUE SPACES.               
           05  L38I-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER3             PIC X(01)        VALUE SPACES.               
           05  L38I-NAME1          PIC X(30)        VALUE SPACES.               
           05  FILLER4             PIC X(01)        VALUE SPACES.               
           05  L38I-TEL            PIC X(08)        VALUE SPACES.               
           05  FILLER5             PIC X(01)        VALUE SPACES.               
           05  L38I-HP             PIC X(08)        VALUE SPACES.               
           05  FILLER6             PIC X(01)        VALUE SPACES.               
           05  L38I-OFFICE         PIC X(10)        VALUE SPACES.               
           05  FILLER7             PIC X(01)        VALUE SPACES.               
           05  L38I-EMAIL          PIC X(40)        VALUE SPACES.               
           05  FILLER8             PIC X(02)        VALUE SPACES.               
           05  L38I-TYPE           PIC X(08)        VALUE SPACES.               
           05  FILLER9             PIC X(01)        VALUE SPACES.               
           05  L38I-HQMSTYPE       PIC X(09)        VALUE SPACES.               
           05  FILLER10            PIC X(01)        VALUE SPACES.               
           05  L38I-DATE           PIC X(10)        VALUE SPACES.               
           05  FILLER11            PIC X(01)        VALUE SPACES.               
           05  L38I-TIME           PIC X(05)        VALUE SPACES.               
           05  FILLER12            PIC X(01)        VALUE SPACES.               
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F454      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F454 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F454                                                 
                       BP13K820                                                 
                       BP13K800                                                 
                       BP13K857                                                 
                       BP13K480                                                 
              OUTPUT   BP13L38I.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR OPENING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7100-GET-DATE           THRU 7100-EXIT.                      
           PERFORM 1005-CHECK-IN-OUT       THRU 1005-EXIT                       
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1005-CHECK-IN-OUT.                                                       
      ******************************************************************        
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO IN-DATE                          
           MOVE IN-DATE                    TO DATE-REC                          
           PERFORM 1010-CHECKDATE          THRU 1010-EXIT.                      
           PERFORM 1020-VERIFY-DATE        THRU 1020-EXIT.                      
           ADD 12        TO  WS-MM-MONTH.                                       
           IF NOT VALID-MONTH                                                   
               SUBTRACT 12 FROM WS-MM-MONTH                                     
               ADD       1 TO   WS-CCYY-YEAR.                                   
           MOVE DATE-REC                   TO OUT-DATE                          
           MOVE OUT-DATE                   TO WS-PRIO-DTE.                      
                                                                                
       1005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1010-CHECKDATE.                                                          
      ******************************************************************        
                                                                                
           IF WS-DATE1 IS NUMERIC                                               
                     AND VALID-DAY                                              
                     AND VALID-MONTH                                            
                     AND VALID-YEAR                                             
                         NEXT SENTENCE                                          
           ELSE                                                                 
              DISPLAY 'INVALID INPUT DATE : ' DATE-REC                          
              ADD  IN-MONTH TO  WS-MM-MONTH                                     
              MOVE DATE-REC TO OUT-DATE                                         
           END-IF.                                                              
                                                                                
       1010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1020-VERIFY-DATE.                                                        
      ******************************************************************        
                                                                                
           IF 31-DAY-MONTH                                                      
               IF WS-DD-DAY IS NOT LESS THAN 32                                 
                   MOVE 31 TO WS-DD-DAY.                                        
                                                                                
           IF 30-DAY-MONTH                                                      
               IF WS-DD-DAY IS NOT LESS THAN 31                                 
                   MOVE 30 TO WS-DD-DAY.                                        
                                                                                
           IF 28-29-DAY-MONTH                                                   
               IF  WS-DD-DAY IS LESS THAN 29                                    
                   GO TO 1020-EXIT                                              
               ELSE                                                             
                   PERFORM 1030-FEBRUARY.                                       
                                                                                
       1020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1030-FEBRUARY.                                                           
      ******************************************************************        
                                                                                
           DIVIDE 4 INTO WS-CCYY-YEAR GIVING WS-YEAR REMAINDER WS-REM.          
                                                                                
           IF WS-REM IS EQUAL TO ZERO AND WS-DD-DAY IS GREATER THAN 29          
                MOVE 29 TO WS-DD-DAY.                                           
                                                                                
           IF WS-REM IS NOT EQUAL TO ZERO                                       
                                  AND WS-DD-DAY IS GREATER THAN 28              
                   MOVE 28 TO WS-DD-DAY.                                        
                                                                                
       1030-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F454.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES             TO WS-APPT-TYPE                              
           READ BP13F454           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F454                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F454-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER                     
                                            BP13K820-REC.                       
           INITIALIZE                       BP13K800-MASTER                     
                                            BP13K820-REC.                       
                                                                                
           MOVE F454-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD  1                       TO WS-K800-FND                 
                    PERFORM 2200-READ-BP13K820   THRU 2200-EXIT                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                       TO WS-K800-NFND                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2200-READ-BP13K820.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE F454-NUM-REGN            TO K820-NUM-REGN.                      
           MOVE K800-NUM-NRIC1           TO K820-NUM-NRIC.                      
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ ERROR ' WS-K820-STATUS               
                    DISPLAY 'K820 KEY: ' K820-NUM-REGN                          
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2300-READ-BP13K857.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K857-REC.                       
           INITIALIZE                       BP13K857-REC.                       
                                                                                
           MOVE F454-NUM-REGN            TO K857-NUM-REGN.                      
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K857 READ ERROR ' WS-K857-STATUS               
                    DISPLAY 'K857 KEY: ' K857-NUM-REGN                          
                    MOVE WS-K857-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2400-READ-BP13K480.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K480-REC.                       
           INITIALIZE                       BP13K480-REC.                       
                                                                                
           MOVE F454-NUM-REGN            TO K480-KEY-FLD.                       
                                                                                
           READ BP13K480.                                                       
                                                                                
           EVALUATE WS-K480-STATUS                                              
               WHEN 00                                                          
                    PERFORM 2500-MOVE-APPT     THRU 2500-EXIT                   
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K840 READ ERROR ' WS-K480-STATUS               
                    DISPLAY 'K840 KEY: ' K480-NUM-REGN                          
                    MOVE WS-K480-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2450-LOAN-ATYPE.                                                         
      *************************************************************             
                                                                                
           IF K800-NUM-LOAN-TAG = 'B'                                           
                EVALUATE F454-CDE-APPT-TYPE                                     
                    WHEN 'SA'                                                   
                         EVALUATE F454-CDE-SUB-APPT-TYPE                        
                             WHEN 'A'                                           
                                  MOVE 'BA'   TO WS-APPT-TYPE                   
                             WHEN 'S'                                           
                                  MOVE 'BS'   TO WS-APPT-TYPE                   
                         END-EVALUATE                                           
                    WHEN 'TP'                                                   
                         EVALUATE F454-CDE-SUB-APPT-TYPE                        
                             WHEN 'T'                                           
                                  MOVE 'BT'   TO WS-APPT-TYPE                   
                             WHEN 'R'                                           
                                  MOVE 'BR'   TO WS-APPT-TYPE                   
                         END-EVALUATE                                           
                    WHEN OTHER                                                  
                         MOVE F454-CDE-APPT-TYPE  TO WS-APPT-TYPE               
                END-EVALUATE                                                    
           ELSE                                                                 
               MOVE F454-CDE-APPT-TYPE        TO WS-APPT-TYPE                   
           END-IF.                                                              
                                                                                
           .                                                                    
       2450-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2500-MOVE-APPT.                                                          
      *************************************************************             
                                                                                
              EVALUATE K480-NUM-CONTRA-TYPE                                     
                  WHEN 'TM'                                                     
                       IF K800-CDE-APPT-TYPE = 'TP'                             
                          MOVE 'TM'              TO WS-APPT-TYPE                
                       END-IF                                                   
                  WHEN 'PM'                                                     
                       IF K800-CDE-APPT-TYPE = 'SP'                             
                          MOVE 'PM'              TO WS-APPT-TYPE                
                       END-IF                                                   
                  WHEN 'TU'                                                     
                  WHEN 'DU'                                                     
                       IF K800-NUM-BUY-SELL-TAG = 'B'                           
                          IF K800-CDE-APPT-TYPE = 'TP' OR 'TL'                  
                             MOVE 'CL'           TO WS-APPT-TYPE                
                          ELSE                                                  
                            IF K800-CDE-APPT-TYPE = 'SP' OR 'SL'                
                               MOVE 'CA'         TO WS-APPT-TYPE                
                            END-IF                                              
                          END-IF                                                
                       ELSE                                                     
                         IF K800-CDE-APPT-TYPE = 'TP'                           
                            MOVE 'CT'            TO WS-APPT-TYPE                
                         END-IF                                                 
                       END-IF                                                   
                  WHEN 'CP'                                                     
                       IF K800-CDE-APPT-TYPE = 'SP'                             
                          MOVE 'CP'              TO WS-APPT-TYPE                
                       END-IF                                                   
              END-EVALUATE                                                      
           .                                                                    
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF (F454-DTE-APPT >= WS-PRIO-DTE1) AND                               
              (F454-DTE-APPT <= WS-PRIO-DTE )                                   
                                                                                
              IF F454-NUM-REGN NOT = WS-NUM-REGN                                
                 MOVE F454-NUM-REGN             TO WS-NUM-REGN                  
                                                                                
                 IF WS-PRINT-DATE = ZEROES                                      
                    MOVE F454-DTE-APPT          TO WS-PRINT-DATE                
                 END-IF                                                         
                                                                                
                 IF F454-DTE-APPT NOT = WS-PRINT-DATE                           
                    MOVE F454-DTE-APPT          TO WS-PRINT-DATE                
                                                                                
                    MOVE ZEROES                 TO WS-SNO                       
                    MOVE SPACES                 TO WS-L38I-HEADER               
                    WRITE BP13L38I-REC        FROM WS-L38I-HEADER               
                 END-IF                                                         
                                                                                
                 PERFORM 2100-READ-BP13K800     THRU 2100-EXIT                  
                 PERFORM 2300-READ-BP13K857     THRU 2300-EXIT                  
                 PERFORM 2450-LOAN-ATYPE        THRU 2450-EXIT                  
                 PERFORM 2400-READ-BP13K480     THRU 2400-EXIT                  
                 PERFORM 4000-PROCESS-RECORDS   THRU 4000-EXIT                  
                                                                                
                                                                                
              END-IF                                                            
                                                                                
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F454           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO L38I-DETAIL01                   
           INITIALIZE                           L38I-DETAIL01.                  
                                                                                
           MOVE ';'                       TO FILLER1 FILLER2 FILLER3            
                                             FILLER4 FILLER5 FILLER6            
                                             FILLER7 FILLER8 FILLER9            
                                             FILLER10 FILLER11 FILLER12.        
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L38I-SN.                           
           MOVE F454-NUM-REGN             TO L38I-REGN.                         
           MOVE K800-NUM-NRIC1            TO L38I-NRIC.                         
                                                                                
           IF K820-NME-OCCP = SPACES OR LOW-VALUES                              
              MOVE SPACES                 TO K820-NME-OCCP                      
           END-IF.                                                              
                                                                                
           IF K820-NUM-PHONE = SPACES OR LOW-VALUES                             
              MOVE SPACES                 TO K820-NUM-PHONE                     
           END-IF.                                                              
                                                                                
           IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES                            
              MOVE SPACES                 TO K820-NUM-HP-PGR                    
           END-IF.                                                              
                                                                                
           IF K820-NUM-PHONE-OFF = SPACES OR LOW-VALUES                         
              MOVE SPACES                 TO K820-NUM-PHONE-OFF                 
           END-IF.                                                              
                                                                                
           IF K857-NUM-EMAIL = SPACES OR LOW-VALUES                             
              MOVE SPACES                 TO K857-NUM-EMAIL                     
           END-IF.                                                              
                                                                                
           MOVE K820-NME-OCCP             TO L38I-NAME1.                        
           MOVE K820-NUM-PHONE            TO L38I-TEL.                          
           MOVE K820-NUM-HP-PGR           TO L38I-HP.                           
           MOVE K820-NUM-PHONE-OFF        TO L38I-OFFICE.                       
           MOVE K857-NUM-EMAIL            TO L38I-EMAIL.                        
           MOVE F454-CDE-APPT-TYPE        TO L38I-TYPE.                         
           MOVE WS-APPT-TYPE              TO L38I-HQMSTYPE.                     
                                                                                
                                                                                
           STRING F454-DTE-APPT(7:2) '/'                                        
                  F454-DTE-APPT(5:2) '/'                                        
                  F454-DTE-APPT(1:4)                                            
              DELIMITED BY SIZE INTO L38I-DATE.                                 
                                                                                
                                                                                
           STRING F454-TME-APPT(1:2) ':'                                        
                  F454-TME-APPT(3:2)                                            
              DELIMITED BY SIZE INTO L38I-TIME.                                 
                                                                                
           PERFORM 5000-PRINT-REPORT    THRU 5000-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
                                                                                
           WRITE BP13L38I-REC               FROM L38I-DETAIL01.                 
           ADD 1                              TO WS-CNT-L38I-WRITE.             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
                                                                                
           STRING WS-PRIO-DTE1(7:2) '/'                                         
                  WS-PRIO-DTE1(5:2) '/'                                         
                  WS-PRIO-DTE1(1:4)                                             
              DELIMITED BY SIZE INTO L38I-DATE-HDR.                             
                                                                                
           STRING WS-PRIO-DTE(7:2) '/'                                          
                  WS-PRIO-DTE(5:2) '/'                                          
                  WS-PRIO-DTE(1:4)                                              
              DELIMITED BY SIZE INTO L38I-DTE-TRANS1.                           
                                                                                
           MOVE L38I-DATE-HDR                  TO L38I-DTE-TRANS.               
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38I-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38I-PAGE-HDR.                
                                                                                
           MOVE SPACES             TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE 'FOR HDB LOAN / NO LOAN :' TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         SA - SOCA '      TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         SL - SOCA + LA'  TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         SP - SATP    '   TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         SR - SATP (SERS)   '   TO WS-L38I-HEADER.             
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         TP - TP      '   TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         TL - TP + LA  '  TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         PL - SATP + LA'  TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
                                                                                
           MOVE '         TR - SUBTRANSFER'   TO WS-L38I-HEADER.                
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         LA - LOAN AGREEMENT FOR 2ND CON LOAN'                 
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE SPACES             TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE 'FOR BANK LOAN :'             TO WS-L38I-HEADER.                
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         BA - SOCA'          TO WS-L38I-HEADER.                
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         BT - TP  '          TO WS-L38I-HEADER.                
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         BS - SOCA (SERS)'   TO WS-L38I-HEADER.                
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         BR - TP (SERS)'     TO WS-L38I-HEADER.                
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE SPACES             TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE 'HQMS TYPE :             ' TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         SA - SIGN AGREEMENT FOR LEASE'                        
                                           TO WS-L38I-HEADER.                   
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         SP - SIGN AGREEMENT FOR LEASE AND KEYS COLL'        
                  'ECTION'                                                      
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         SR -  SIGN AGREEMENT FOR LEASE AND KEYS COL'        
                  'LECTION ' '(' 'SERS' ')'                                     
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         TP - KEYS COLLECTION' TO WS-L38I-HEADER.              
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         WT - SELF BOOK KEYS COLLECTION'                       
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         TL - KEYS COLLECTION AND LOAN AGREEMENT'              
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         PL - SIGN AGREEMENT FOR LEASE, KEYS COLLECT'        
                  'ION AND LOAN AGREEMENT'                                      
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         PR - SIGN AGREEMENT FOR LEASE, KEYS COLLECT'        
                  'ION AND LOAN AGREEMENT ' '(' 'SERS' ')'                      
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         TR - SUB TRANSFER'    TO WS-L38I-HEADER.              
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         LA - LOAN AGREEMENT'  TO WS-L38I-HEADER.              
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         SL - SIGN AGREEMENT FOR LEASE AND LOAN AGRE'        
                  'EMENT'                                                       
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         BA - SIGN AGREEMENT FOR LEASE (BANK LOAN)'            
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         BT - KEYS COLLECTION (BANK LOAN)'                     
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         BS - SIGN AGREEMENT FOR LEASE (SERS - BANK '        
                  'LOAN)'                                                       
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         BR - KEYS COLLECTION (SERS - BANK LOAN)'              
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         TM - TEMP LOAN FOR KEYS COLLECTION'                   
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         PM - TEMP LOAN FOR SIGN AGREEMENT FOR LEASE'        
                  ' AND KEYS COLLECTION'                                        
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE '         CT - CONTRA + KEYS COLLECTION'                        
                                   TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         CP - CONTRA + SIGN AGREEMENT FOR LEASE AND '        
                  'KEYS COLLECTION'                                             
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         CL -  CONTRA + KEYS COLLECTION AND LOAN AGR'        
                  'EEMENT'                                                      
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           STRING '         CA - CONTRA + SIGN AGREEMENT FOR LEASE, KEY'        
                  'S COLLECTION AND LOAN AGREEMENT'                             
              DELIMITED BY SIZE INTO WS-L38I-HEADER.                            
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE SPACES             TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
           MOVE SPACES             TO WS-L38I-HEADER.                           
           WRITE BP13L38I-REC    FROM WS-L38I-HEADER.                           
                                                                                
                                                                                
           WRITE BP13L38I-REC    FROM L38I-HEADER01 AFTER PAGE.                 
           WRITE BP13L38I-REC    FROM L38I-HEADER02.                            
           WRITE BP13L38I-REC    FROM L38I-HEADER2A.                            
           WRITE BP13L38I-REC    FROM L38I-HEADER03 AFTER 2.                    
                                                                                
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-GET-DATE.                                                           
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-PRIOR-DTE.                
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
      *    ADD 14 TO  WS-NUM-DTE-APPT.                                          
                                                                                
      *    COMPUTE  WS-PRIOR-DTE =                                              
      *             FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
      *    MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
                                                                                
      ***** +1 DAY                                                              
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-PRIOR-DTE.                
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           ADD 1 TO  WS-NUM-DTE-APPT.                                           
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE1.                            
                                                                                
           DISPLAY WS-PRIO-DTE " ---- " WS-PRIO-DTE1.                           
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38I'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F454 RECORDS READ............. : '            
                    WS-F454-READ.                                               
           DISPLAY '(2) NO OF BP13L38I RECORDS WRITTEN.......... : '            
                    WS-CNT-L38I-WRITE.                                          
                                                                                
           CLOSE    BP13F454                                                    
                    BP13K800                                                    
                    BP13K857                                                    
                    BP13K820                                                    
                    BP13K480                                                    
                    BP13L38I.                                                   
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : ' WS-K820-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR CLOSING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
