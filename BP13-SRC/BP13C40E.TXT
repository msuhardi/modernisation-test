      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C40E.                                                 
       AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
       DATE-WRITTEN.  NOV. 18, 2019.                                            
      *===============================================================*         
      * OBJECTIVE  :  TO UPDATE THE K40I FILE                         *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. BP13F40I                                     *         
      *               2. BP13K800                                     *         
      *   I-O FILE :  1. BP13K40I                                     *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137990 18/11/2019 KAC1 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F40I  ASSIGN        TO BP13F40I.                         
                                                                                
           SELECT  BP13K40I  ASSIGN        TO BP13K40I                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K40I-NUM-REGN                     
                             FILE STATUS   IS WS-K40I-STATUS.                   
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F40I                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       COPY BP13F40I.                                                           
                                                                                
       FD   BP13K40I                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K40I.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-K40I-STATUS              PIC 9(02) VALUE ZEROES.              
           05  WS-K800-STATUS              PIC 9(02) VALUE ZEROES.              
           05  WS-F40I-EOF                 PIC X(01) VALUE 'N'.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F40I-READ-CNT            PIC 9(7)  VALUE ZEROES.              
           05  WS-K800-FND-CNT             PIC 9(7)  VALUE ZEROES.              
           05  WS-K800-NFND-CNT            PIC 9(7)  VALUE ZEROES.              
           05  WS-K40I-FND-CNT             PIC 9(7)  VALUE ZEROES.              
           05  WS-K40I-UPD-CNT             PIC 9(7)  VALUE ZEROES.              
           05  WS-K40I-NFND-CNT            PIC 9(7)  VALUE ZEROES.              
                                                                                
       01 WS-VARIABLES.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F40I      THRU 2000-EXIT                       
           PERFORM 3000-READ-BP13K800      THRU 3000-EXIT                       
             UNTIL WS-F40I-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F40I                                                 
                       BP13K800                                                 
                I-O    BP13K40I.                                                
                                                                                
                                                                                
           IF WS-K40I-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40I - ERROR OPENING : ' WS-K40I-STATUS              
              MOVE WS-K40I-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F40I.                                                      
      ******************************************************************        
           READ BP13F40I AT END                                                 
                MOVE 'Y' TO WS-F40I-EOF                                         
                GO TO  2000-EXIT.                                               
                                                                                
           ADD  1                  TO WS-F40I-READ-CNT.                         
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-READ-BP13K800.                                                      
      *************************************************************             
           MOVE SPACES                             TO BP13K40I-REC              
                                                      BP13K800-MASTER.          
           INITIALIZE                                 BP13K40I-REC              
                                                      BP13K800-MASTER.          
           MOVE F40I-NUM-REGN                      TO K800-NUM-REGN.            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD  1                         TO WS-K800-FND-CNT           
                    PERFORM 4000-READ-BP13K40I   THRU 4000-EXIT                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                         TO WS-K800-NFND-CNT          
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS            TO RETURN-CODE               
                    PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                 
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F40I            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-READ-BP13K40I.                                                      
      *************************************************************             
           MOVE K800-NUM-REGN                      TO K40I-NUM-REGN.            
                                                                                
           READ BP13K40I.                                                       
           EVALUATE WS-K40I-STATUS                                              
               WHEN 00                                                          
                    PERFORM 4500-UPDATE-BP13K40I THRU 4500-EXIT                 
               WHEN 23                                                          
                    ADD 1                          TO WS-K40I-NFND-CNT          
                    DISPLAY 'K40I - NOTFND KEY : ' K40I-NUM-REGN                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING OF BP13K40I : ' K40I-NUM-REGN        
                            'STATUS(', WS-K40I-STATUS ')'                       
                    MOVE WS-K40I-STATUS            TO RETURN-CODE               
                    PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                 
           END-EVALUATE.                                                        
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4500-UPDATE-BP13K40I.                                                    
      *************************************************************             
           MOVE K800-DTE-SO-APPT                   TO K40I-DTE-TP-APPT.         
                                                                                
           REWRITE BP13K40I-REC.                                                
                                                                                
           IF WS-K40I-STATUS = 00 OR 02                                         
              ADD 1                                TO WS-K40I-UPD-CNT           
           ELSE                                                                 
              DISPLAY 'BP13K40I - REWRITE ERROR, KEY : ' K40I-NUM-REGN          
                      ' , STATUS (' WS-K40I-STATUS ')'                          
              MOVE WS-K40I-STATUS                  TO RETURN-CODE               
              PERFORM 9999-CLOSE-ROUTINE         THRU 9999-EXIT                 
           END-IF.                                                              
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C40E'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F40I (READ)............ : '                   
                    WS-F40I-READ-CNT.                                           
                                                                                
           DISPLAY '(2) NO OF BP13K800 (FOUND)........... : '                   
                    WS-K800-FND-CNT.                                            
                                                                                
           DISPLAY '(3) NO OF BP13K800 (NOTFND).......... : '                   
                    WS-K800-NFND-CNT.                                           
                                                                                
           DISPLAY '(4) NO OF BP13K40I (UPDATED)......... : '                   
                    WS-K40I-UPD-CNT.                                            
                                                                                
           DISPLAY '(5) NO OF BP13K40I (NOTFND).......... : '                   
                    WS-K40I-NFND-CNT.                                           
                                                                                
           CLOSE    BP13K800                                                    
                    BP13K40I.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K40I-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40I - ERROR CLOSING : ' WS-K40I-STATUS              
              MOVE WS-K40I-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
