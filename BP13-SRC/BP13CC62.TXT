       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC62.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  14/02/2007.                                               
      *****************************************************************         
      * OBJECTIVE  - TO EXTRACT INFORMATION FROM BP13FD00 AND AB03F089          
      *              WRITE INTO BP13FD79.                                       
      *                                                                         
      * SUBROUTINE    - NIL                                                     
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      * BP132934 140207  BA2  NEW PRG.                                          
      * BP133091 171007  RB12 EXPAND BP13FD00 FROM 1200 TO 1600 BYTES           
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT  BP13FD00  ASSIGN  TO   BP13FD00.                             
                                                                                
           SELECT  AB03F089  ASSIGN  TO   AB03F089.                             
                                                                                
           SELECT  BP13FD79  ASSIGN  TO   BP13FD79.                             
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD00                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 1600 CHARACTERS         
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
       COPY BP13FD00.                                                           
                                                                                
       FD  AB03F089                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 250 CHARACTERS          
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
       COPY AB03F089.                                                           
                                                                                
       FD  BP13FD79                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 100 CHARACTERS          
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
       COPY BP13FD79.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FD00                  PIC 9(7)  VALUE 0.                  
           05  WS-CNT-F089                  PIC 9(7)  VALUE 0.                  
           05  WS-CNT-FD79                  PIC 9(7)  VALUE 0.                  
           05  WS-CNT-MTCH                  PIC 9(7)  VALUE 0.                  
           05  WS-CNT-UNMTCH                PIC 9(7)  VALUE 0.                  
                                                                                
       01  WS-FD00-KEY.                                                         
           05  WS-FD00-NUM-SCH-ACCNT        PIC X(09).                          
           05  WS-FD00-CDE-LESSEE           PIC X(02).                          
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       0000-MAIN-PARA.                                                          
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-PROCESS-BP13FD00 THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-AB03F089 THRU 3000-EXIT.                        
           PERFORM 4000-PROCESS-MATCHING THRU 4000-EXIT                         
                UNTIL WS-FD00-KEY = HIGH-VALUES                                 
                  AND F089-NUM-SCH-ACCNT = HIGH-VALUES.                         
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
       1000-START-UP.                                                           
                                                                                
           OPEN INPUT  BP13FD00                                                 
                       AB03F089                                                 
               OUTPUT  BP13FD79.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       2000-PROCESS-BP13FD00.                                                   
                                                                                
           READ BP13FD00 AT END                                                 
                MOVE HIGH-VALUES TO WS-FD00-KEY                                 
                GO TO 2000-EXIT.                                                
           MOVE FD00-NUM-SCH-ACCNT TO WS-FD00-KEY.                              
           ADD 1 TO WS-CNT-FD00.                                                
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       3000-PROCESS-AB03F089.                                                   
                                                                                
           READ AB03F089 AT END                                                 
                MOVE HIGH-VALUES TO F089-NUM-SCH-ACCNT                          
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-CNT-F089.                                                
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       4000-PROCESS-MATCHING.                                                   
                                                                                
           IF WS-FD00-NUM-SCH-ACCNT < F089-NUM-SCH-ACCNT                        
              ADD 1 TO WS-CNT-UNMTCH                                            
              PERFORM 2000-PROCESS-BP13FD00 THRU 2000-EXIT                      
           ELSE                                                                 
           IF WS-FD00-NUM-SCH-ACCNT > F089-NUM-SCH-ACCNT                        
              PERFORM 3000-PROCESS-AB03F089 THRU 3000-EXIT                      
           ELSE                                                                 
              PERFORM 4100-PROCESS-MATCHED  THRU 4100-EXIT                      
                UNTIL WS-FD00-NUM-SCH-ACCNT NOT = F089-NUM-SCH-ACCNT            
              PERFORM 3000-PROCESS-AB03F089 THRU 3000-EXIT.                     
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       4100-PROCESS-MATCHED.                                                    
                                                                                
           ADD 1 TO WS-CNT-MTCH.                                                
           MOVE SPACES TO BP13FD79-REC.                                         
           INITIALIZE BP13FD79-REC.                                             
           MOVE WS-FD00-KEY              TO FD79-NUM-HDB-REF.                   
           MOVE FD00-NUM-REGN            TO FD79-NUM-REGN.                      
           MOVE FD00-CDE-LOAN-SCH        TO FD79-CDE-LOAN-SCH.                  
           MOVE FD00-CDE-INT             TO FD79-CDE-INT.                       
           MOVE FD00-AMT-INSTAL          TO FD79-AMT-INST.                      
           MOVE FD00-AMT-NEXT-INSTAL     TO FD79-AMT-INST-NEXT.                 
           MOVE FD00-AMT-ARR-INSTAL      TO FD79-AMT-ARR-INST.                  
      *    MOVE FD00-AMT-ARR-SC          TO FD79-AMT-ARR-SC.                    
           MOVE FD00-AMT-LOAN-BAL        TO FD79-AMT-LOAN-BAL.                  
           MOVE FD00-CDE-ACCT-STAT       TO FD79-CDE-ACCT-STAT.                 
           MOVE F089-CDE-BUC             TO FD79-CDE-BUC.                       
           WRITE BP13FD79-REC.                                                  
           ADD 1 TO WS-CNT-FD79.                                                
           PERFORM 2000-PROCESS-BP13FD00 THRU 2000-EXIT.                        
                                                                                
       4100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       9999-WRAP-UP.                                                            
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS *******'.                          
           DISPLAY 'PROGRAM-ID : BP13CC62'.                                     
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY 'NUMBER OF AB03F089 RECORDS READ     : '                     
                    WS-CNT-F089.                                                
           DISPLAY 'NUMBER OF BP13FD00 RECORDS READ     : '                     
                    WS-CNT-FD00.                                                
           DISPLAY '       -  MATCHED CASES             : '                     
                    WS-CNT-MTCH.                                                
           DISPLAY '       -  UNMATCHED CASES           : '                     
                    WS-CNT-UNMTCH.                                              
           DISPLAY 'NUMBER OF BP13FD79 RECORDS WRITTEN  : '                     
                    WS-CNT-FD79.                                                
                                                                                
           CLOSE BP13FD00                                                       
                 AB03F089                                                       
                 BP13FD79.                                                      
                                                                                
           STOP RUN.                                                            
