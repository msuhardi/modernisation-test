       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY71.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  21/06/10.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                    *                
      *                                                        *                
      *  SYSTEM ID   : BP13                                    *                
      *                                                        *                
      *  OBJECTIVE   : REPORT ON ESTIMATED KEY ISSUE DATE      *                
      *                                                        *                
      *  CHG-NO  BY   DATE   DESCRIPTION                       *                
      * -------- ---- ------ -------------------               *                
      * BP133930 PCL3 210610 NEW PROGRAM                       *                
      * BP134273 PCL3 010711 TO ADD CHECK ON FINANCIAL YEAR.   *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BM06F110 ASSIGN TO BM06F110.                                  
           SELECT BP13LY71 ASSIGN TO BP13LY71.                                  
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                           ORGANIZATION IS INDEXED                              
                            ACCESS MODE IS RANDOM                               
                             RECORD KEY IS KD00-KEY-FLD                         
                            FILE STATUS IS KD00-STATUS.                         
                                                                                
           SELECT BP13K640 ASSIGN TO BP13K640                                   
                           ORGANIZATION IS INDEXED                              
                            ACCESS MODE IS RANDOM                               
                             RECORD KEY IS K640-KEY-FLD                         
                            FILE STATUS IS K640-STATUS.                         
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                           ORGANIZATION IS INDEXED                              
                            ACCESS MODE IS RANDOM                               
                             RECORD KEY IS K410-KEY-FLD                         
                            FILE STATUS IS K410-STATUS.                         
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                           ORGANIZATION IS INDEXED                              
                            ACCESS MODE IS RANDOM                               
                             RECORD KEY IS K100-KEY-FLD                         
                            FILE STATUS IS K100-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13K640                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K640.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BM06F110                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F110.                                                           
                                                                                
       FD   BP13LY71                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   BP13LY71-REC.                                                       
            05 LY71-NUM-REGN                  PIC X(8).                         
            05 LY71-SCH-ACC-NO                PIC X(11).                        
            05 LY71-EST                       PIC X(3).                         
            05 LY71-NEIGH                     PIC X(4).                         
            05 LY71-CNTR-NO                   PIC X(4).                         
            05 LY71-BLK-NO                    PIC X(5).                         
            05 LY71-DTE-PCD                   PIC X(6).                         
            05 LY71-NUM-ALLO-CAT              PIC X(3).                         
            05 LY71-NUM-LOAN-TAG              PIC X(1).                         
            05 LY71-NUM-BUY-SELL              PIC X(1).                         
            05 LY71-NUM-LOAN-GRANTED          PIC 9(7).                         
            05 LY71-NUM-SELLING-PRICE         PIC 9(8).                         
            05 LY71-NUM-PCD-CODE              PIC X(8).                         
            05 LY71-NUM-NRIC1                 PIC X(9).                         
            05 LY71-NUM-NRIC2                 PIC X(9).                         
            05 LY71-NUM-NRIC3                 PIC X(9).                         
            05 LY71-NUM-NRIC4                 PIC X(9).                         
            05 FILLER                         PIC X(95).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  KD00-STATUS                PIC 99      VALUE ZEROES.             
           05  K100-STATUS                PIC 99      VALUE ZEROES.             
           05  K410-STATUS                PIC 99      VALUE ZEROES.             
           05  K640-STATUS                PIC 99      VALUE ZEROES.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F800-EOF                PIC X(1)    VALUE 'N'.                
           05  WS-F110-EOF                PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-CURR-DTE.                                                         
           05  WS-CURR-YY                 PIC 9(4)    VALUE ZEROES.             
           05  WS-CURR-MM                 PIC 9(2)    VALUE ZEROES.             
           05  WS-CURR-DD                 PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-FY-YEAR.                                                          
           05  WS-CURR-YY1                PIC 9(4)    VALUE ZEROES.             
           05  WS-CURR-YY2                PIC 9(4)    VALUE ZEROES.             
           05  WS-CURR-YY3                PIC 9(4)    VALUE ZEROES.             
           05  WS-CURR-YY4                PIC 9(4)    VALUE ZEROES.             
           05  WS-CURR-YY5                PIC 9(4)    VALUE ZEROES.             
           05  WS-CURR-YY6                PIC 9(4)    VALUE ZEROES.             
           05  WS-CURR-YY7                PIC 9(4)    VALUE ZEROES.             
                                                                                
       01  WS-FY-DTE1.                                                          
           05  WS-FY-YY1A                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM1A                 PIC X(2)    VALUE '04'.               
           05  WS-FY-YY1B                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM1B                 PIC X(2)    VALUE '03'.               
                                                                                
       01  WS-FY-DTE2.                                                          
           05  WS-FY-YY2A                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM2A                 PIC X(2)    VALUE '04'.               
           05  WS-FY-YY2B                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM2B                 PIC X(2)    VALUE '03'.               
                                                                                
       01  WS-FY-DTE3.                                                          
           05  WS-FY-YY3A                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM3A                 PIC X(2)    VALUE '04'.               
           05  WS-FY-YY3B                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM3B                 PIC X(2)    VALUE '03'.               
                                                                                
       01  WS-FY-DTE4.                                                          
           05  WS-FY-YY4A                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM4A                 PIC X(2)    VALUE '04'.               
           05  WS-FY-YY4B                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM4B                 PIC X(2)    VALUE '03'.               
                                                                                
       01  WS-FY-DTE5.                                                          
           05  WS-FY-YY5A                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM5A                 PIC X(2)    VALUE '04'.               
           05  WS-FY-YY5B                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM5B                 PIC X(2)    VALUE '03'.               
                                                                                
       01  WS-FY-DTE6.                                                          
           05  WS-FY-YY6A                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM6A                 PIC X(2)    VALUE '04'.               
           05  WS-FY-YY6B                 PIC X(4)    VALUE ZEROES.             
           05  WS-FY-MM6B                 PIC X(2)    VALUE '03'.               
                                                                                
       01  WS-FY-CDE                      PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ               PIC 9(07)   VALUE ZEROES.             
           05  WS-F110-READ               PIC 9(07)   VALUE ZEROES.             
           05  WS-CNT-WRITE               PIC 9(07)   VALUE ZEROES.             
           05  WS-CNT-FND                 PIC 9(07)   VALUE ZEROES.             
           05  WS-CNT-NFND1               PIC 9(07)   VALUE ZEROES.             
           05  WS-CNT-NFND2               PIC 9(07)   VALUE ZEROES.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPENING-ROUTINE      THRU 1000-EXIT.                    
           PERFORM 2000-READ-BP13F800        THRU 2000-EXIT.                    
           PERFORM 3000-READ-BM06F110        THRU 3000-EXIT.                    
           PERFORM 4000-MAIN-PROCESS         THRU 4000-EXIT                     
             UNTIL F800-NUM-SCH-ACC = HIGH-VALUES                               
               AND F110-SCH-ACC-NO  = HIGH-VALUES.                              
           PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06F110                                                 
                       BP13KD00                                                 
                       BP13K410                                                 
                       BP13K640                                                 
                       BM06K100                                                 
                OUTPUT BP13LY71.                                                
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13KD00 , STATUS = ' KD00-STATUS          
              MOVE KD00-STATUS               TO RETURN-CODE                     
              PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K410 , STATUS = ' K410-STATUS          
              MOVE K410-STATUS               TO RETURN-CODE                     
              PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF K640-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K640 , STATUS = ' K640-STATUS          
              MOVE K640-STATUS               TO RETURN-CODE                     
              PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BM06K100 , STATUS = ' K100-STATUS          
              MOVE K100-STATUS               TO RETURN-CODE                     
              PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-CURR-DTE.                    
                                                                                
           MOVE WS-CURR-YY                   TO WS-CURR-YY1                     
                                                WS-CURR-YY2                     
                                                WS-CURR-YY3                     
                                                WS-CURR-YY4                     
                                                WS-CURR-YY5                     
                                                WS-CURR-YY6                     
                                                WS-CURR-YY7.                    
                                                                                
           ADD 1                             TO WS-CURR-YY2.                    
           ADD 2                             TO WS-CURR-YY3.                    
           ADD 3                             TO WS-CURR-YY4.                    
           ADD 4                             TO WS-CURR-YY5.                    
           ADD 5                             TO WS-CURR-YY6.                    
           ADD 6                             TO WS-CURR-YY7.                    
                                                                                
           MOVE WS-CURR-YY1                  TO WS-FY-YY1A.                     
           MOVE WS-CURR-YY2                  TO WS-FY-YY1B.                     
                                                                                
           MOVE WS-CURR-YY2                  TO WS-FY-YY2A.                     
           MOVE WS-CURR-YY3                  TO WS-FY-YY2B.                     
                                                                                
           MOVE WS-CURR-YY3                  TO WS-FY-YY3A.                     
           MOVE WS-CURR-YY4                  TO WS-FY-YY3B.                     
                                                                                
           MOVE WS-CURR-YY4                  TO WS-FY-YY4A.                     
           MOVE WS-CURR-YY5                  TO WS-FY-YY4B.                     
                                                                                
           MOVE WS-CURR-YY5                  TO WS-FY-YY5A.                     
           MOVE WS-CURR-YY6                  TO WS-FY-YY5B.                     
                                                                                
           MOVE WS-CURR-YY6                  TO WS-FY-YY6A.                     
           MOVE WS-CURR-YY7                  TO WS-FY-YY6B.                     
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F800-EOF                         
                MOVE HIGH-VALUES         TO F800-NUM-SCH-ACC                    
                GO                       TO 2000-EXIT                           
           END-READ.                                                            
                                                                                
           ADD   1 TO WS-F800-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BM06F110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F110                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F110-EOF                         
                MOVE HIGH-VALUES         TO F110-SCH-ACC-NO                     
                GO                       TO 3000-EXIT                           
           END-READ.                                                            
                                                                                
           ADD   1 TO WS-F110-READ.                                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F800-NUM-SCH-ACC = F110-SCH-ACC-NO                                
              ADD 1                                 TO WS-CNT-FND               
              PERFORM 5000-PROCESS-BP13LY71       THRU 5000-EXIT                
              PERFORM 2000-READ-BP13F800          THRU 2000-EXIT                
              PERFORM 3000-READ-BM06F110          THRU 3000-EXIT                
           ELSE                                                                 
              IF F800-NUM-SCH-ACC < F110-SCH-ACC-NO                             
                  ADD 1                             TO WS-CNT-NFND1             
                  PERFORM 2000-READ-BP13F800      THRU 2000-EXIT                
              ELSE                                                              
                  ADD 1                             TO WS-CNT-NFND2             
                  PERFORM 3000-READ-BM06F110      THRU 3000-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-PROCESS-BP13LY71.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                          TO BP13LY71-REC.                
           INITIALIZE                              BP13LY71-REC.                
                                                                                
           PERFORM 5100-MOVE-BP13LY71         THRU 5100-EXIT.                   
           PERFORM 5200-READ-BM06K100         THRU 5200-EXIT.                   
                                                                                
           IF F800-NUM-ALLO-CAT = 'DBS'                                         
              PERFORM 5300-READ-BP13KD00      THRU 5300-EXIT                    
           ELSE                                                                 
              IF F110-DTE-KEY-ISSUED = ZEROES OR SPACES OR LOW-VALUES           
                 PERFORM 5400-READ-BP13K640   THRU 5400-EXIT                    
              ELSE                                                              
                 PERFORM 5500-READ-BP13K410   THRU 5500-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM 6000-WRITE-BP13LY71        THRU 6000-EXIT.                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-MOVE-BP13LY71.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF F110-CURR-SELLING-PRICE NOT NUMERIC                               
              MOVE ZEROES                     TO F110-CURR-SELLING-PRICE        
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-REGN                 TO LY71-NUM-REGN.                 
           MOVE F800-NUM-SCH-ACC              TO LY71-SCH-ACC-NO.               
           MOVE F110-ESTATE                   TO LY71-EST.                      
           MOVE F110-NEIGHBOURHOOD            TO LY71-NEIGH.                    
           MOVE F110-CONTRACT-NO              TO LY71-CNTR-NO.                  
           MOVE F110-BLK-NO                   TO LY71-BLK-NO.                   
                                                                                
           IF F800-NUM-ALLO-CAT = 'BE ' OR 'BTO' OR 'DBS' OR '3R '              
                                        OR 'REN'                                
              MOVE F800-NUM-ALLO-CAT          TO LY71-NUM-ALLO-CAT              
           ELSE                                                                 
              IF F800-NUM-ALLO-CAT(1:1) = 'W'                                   
                 MOVE 'WIS'                   TO LY71-NUM-ALLO-CAT              
              ELSE                                                              
                 MOVE 'OTH'                   TO LY71-NUM-ALLO-CAT              
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-LOAN-TAG             TO LY71-NUM-LOAN-TAG.             
           MOVE F800-NUM-BUY-SELL-TAG         TO LY71-NUM-BUY-SELL.             
           MOVE F110-CURR-SELLING-PRICE       TO LY71-NUM-SELLING-PRICE.        
           MOVE F800-NUM-NRIC1                TO LY71-NUM-NRIC1.                
           MOVE F800-NUM-NRIC2                TO LY71-NUM-NRIC2.                
           MOVE F800-NUM-NRIC3                TO LY71-NUM-NRIC3.                
           MOVE F800-NUM-NRIC4                TO LY71-NUM-NRIC4.                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5200-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                             TO K100-REC.                 
           INITIALIZE                                 K100-REC.                 
                                                                                
           MOVE F110-AIX1                          TO K100-KEY-FLD.             
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE K100-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 5250-PROCESS-DTE-PCD THRU 5250-EXIT                 
                                                                                
               WHEN 23                                                          
                    MOVE 'OTHERS'                  TO LY71-NUM-PCD-CODE         
                    MOVE ZEROES                    TO LY71-DTE-PCD              
                                                                                
               WHEN OTHER                                                       
                    MOVE K100-STATUS                  TO RETURN-CODE            
                    DISPLAY 'ERROR READING BM06K100: ' K100-STATUS              
                            ' KEY : ' K100-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5250-PROCESS-DTE-PCD.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF K100-DTE-PACD-QTR NOT = SPACES AND LOW-VALUES AND ZEROES          
              MOVE  K100-DTE-PACD-QTR           TO LY71-DTE-PCD                 
                                                                                
              IF K100-DTE-PACD-QTR >= WS-FY-YY1A AND                            
                 K100-DTE-PACD-QTR <= WS-FY-YY1B                                
                 STRING 'FY ' WS-FY-YY1A(3:2) '/' WS-FY-YY1B(3:2)               
                       DELIMITED BY SIZE INTO LY71-NUM-PCD-CODE                 
              ELSE IF K100-DTE-PACD-QTR >= WS-FY-YY2A AND                       
                      K100-DTE-PACD-QTR <= WS-FY-YY2B                           
                 STRING 'FY ' WS-FY-YY2A(3:2) '/' WS-FY-YY2B(3:2)               
                       DELIMITED BY SIZE INTO LY71-NUM-PCD-CODE                 
              ELSE IF K100-DTE-PACD-QTR >= WS-FY-YY3A AND                       
                      K100-DTE-PACD-QTR <= WS-FY-YY3B                           
                 STRING 'FY ' WS-FY-YY3A(3:2) '/' WS-FY-YY3B(3:2)               
                       DELIMITED BY SIZE INTO LY71-NUM-PCD-CODE                 
              ELSE IF K100-DTE-PACD-QTR >= WS-FY-YY4A AND                       
                      K100-DTE-PACD-QTR <= WS-FY-YY4B                           
                 STRING 'FY ' WS-FY-YY4A(3:2) '/' WS-FY-YY4B(3:2)               
                       DELIMITED BY SIZE INTO LY71-NUM-PCD-CODE                 
              ELSE IF K100-DTE-PACD-QTR >= WS-FY-YY5A AND                       
                      K100-DTE-PACD-QTR <= WS-FY-YY5B                           
                 STRING 'FY ' WS-FY-YY5A(3:2) '/' WS-FY-YY5B(3:2)               
                       DELIMITED BY SIZE INTO LY71-NUM-PCD-CODE                 
              ELSE IF K100-DTE-PACD-QTR >= WS-FY-YY6A AND                       
                      K100-DTE-PACD-QTR <= WS-FY-YY6B                           
                 STRING 'FY ' WS-FY-YY6A(3:2) '/' WS-FY-YY6B(3:2)               
                       DELIMITED BY SIZE INTO LY71-NUM-PCD-CODE                 
              ELSE                                                              
                 MOVE 'OTHERS'                  TO LY71-NUM-PCD-CODE            
              END-IF                                                            
           ELSE                                                                 
              MOVE 'OTHERS'                     TO LY71-NUM-PCD-CODE            
              MOVE ZEROES                       TO LY71-DTE-PCD                 
           END-IF.                                                              
                                                                                
       5250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5300-READ-BP13KD00.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                             TO BP13KD00-REC.             
           INITIALIZE                                 BP13KD00-REC.             
                                                                                
           MOVE F800-NUM-REGN                      TO KD00-KEY-FLD.             
                                                                                
           READ BP13KD00.                                                       
                                                                                
           EVALUATE KD00-STATUS                                                 
               WHEN 00                                                          
                    IF KD00-AMT-MAX-LOAN NOT NUMERIC                            
                       MOVE ZEROES             TO KD00-AMT-MAX-LOAN             
                    END-IF                                                      
                                                                                
                    IF KD00-AMT-LOAN-GRANTED NOT NUMERIC                        
                       MOVE ZEROES             TO KD00-AMT-LOAN-GRANTED         
                    END-IF                                                      
                                                                                
                    IF F110-DTE-KEY-ISSUED = ZEROES OR LOW-VALUES OR            
                                                       SPACES                   
                       MOVE KD00-AMT-LOAN-GRANTED                               
                                               TO LY71-NUM-LOAN-GRANTED         
                    ELSE                                                        
                       MOVE KD00-AMT-MAX-LOAN  TO LY71-NUM-LOAN-GRANTED         
                    END-IF                                                      
               WHEN 23                                                          
                    MOVE ZEROES                TO LY71-NUM-LOAN-GRANTED         
                                                                                
               WHEN OTHER                                                       
                    MOVE KD00-STATUS                  TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13KD00: ' KD00-STATUS              
                            ' KEY : ' KD00-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5400-READ-BP13K640.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                           TO BP13K640-TRANS-REC.         
           INITIALIZE                               BP13K640-TRANS-REC.         
                                                                                
           MOVE F800-NUM-REGN                    TO K640-KEY-FLD.               
                                                                                
           READ BP13K640.                                                       
                                                                                
           EVALUATE K640-STATUS                                                 
               WHEN 00                                                          
                    IF K640-AMT-MAX-LOAN NOT NUMERIC                            
                       MOVE ZEROES             TO K640-AMT-MAX-LOAN             
                    END-IF                                                      
                                                                                
                    MOVE K640-AMT-MAX-LOAN     TO LY71-NUM-LOAN-GRANTED         
                                                                                
               WHEN 23                                                          
                    MOVE ZEROES                TO LY71-NUM-LOAN-GRANTED         
                                                                                
               WHEN OTHER                                                       
                    MOVE K640-STATUS                  TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K640: ' K640-STATUS              
                            ' KEY : ' K640-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5500-READ-BP13K410.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                             TO BP13K410-REC.             
           INITIALIZE                                 BP13K410-REC.             
                                                                                
           MOVE F800-NUM-REGN                      TO K410-KEY-FLD.             
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE K410-STATUS                                                 
               WHEN 00                                                          
                    IF K410-AMT-LOAN NOT NUMERIC                                
                       MOVE ZEROES             TO K410-AMT-LOAN                 
                    END-IF                                                      
                                                                                
                    MOVE K410-AMT-LOAN         TO LY71-NUM-LOAN-GRANTED         
                                                                                
               WHEN 23                                                          
                    MOVE ZEROES                TO LY71-NUM-LOAN-GRANTED         
                                                                                
               WHEN OTHER                                                       
                    MOVE K410-STATUS                  TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K410: ' K410-STATUS              
                            ' KEY : ' K410-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-BP13LY71.                                                     
      *-------------------------------------------------------------            
                                                                                
           WRITE BP13LY71-REC.                                                  
           ADD 1                         TO WS-CNT-WRITE.                       
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '------- BP13CY71 -------------'.                            
           DISPLAY 'NO OF BP13F800 READ    (BP13F800) : '                       
                                               WS-F800-READ.                    
           DISPLAY 'NO OF BM06F110 READ    (BM06F110) : '                       
                                               WS-F110-READ.                    
           DISPLAY 'NO OF RECS MATCHED                : '                       
                                               WS-CNT-FND.                      
           DISPLAY 'NO OF RECS NOT FOUND   (BP13F800) : '                       
                                               WS-CNT-NFND1.                    
           DISPLAY 'NO OF RECS NOT FOUND   (BM06F110) : '                       
                                               WS-CNT-NFND2.                    
           DISPLAY 'NO OF BM06LY71 WRITTEN (BM06LY71) : '                       
                                               WS-CNT-WRITE.                    
                                                                                
           CLOSE BP13F800                                                       
                 BM06F110                                                       
                 BP13KD00                                                       
                 BP13K410                                                       
                 BP13K640                                                       
                 BM06K100                                                       
                 BP13LY71.                                                      
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13KD00 , STATUS = ' KD00-STATUS          
              MOVE KD00-STATUS               TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K410 , STATUS = ' K410-STATUS          
              MOVE K410-STATUS               TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF K640-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K640 , STATUS = ' K640-STATUS          
              MOVE K640-STATUS               TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BM06K100 , STATUS = ' K100-STATUS          
              MOVE K100-STATUS               TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
