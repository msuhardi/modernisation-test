      *----------------------------------------------------------*              
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C165.                                                  
      *----------------------------------------------------------*              
                                                                                
      *----------------------------------------------------------*              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *                                                          *              
      *  OBJECTIVE   : READ BP13F730 & UPDATE THE TAGS (1-17 CHR)*              
      *                OUTPUT TO P13F730A TO PRINT THE BE/BTO    *              
      *                DALILY REPORT                             *              
      *                                                          *              
      *  INPUT FILES :                                           *              
      *  1. BP13F730                                             *              
      *                                                          *              
      *  OUTPUT FILES:                                           *              
      *  1. P13F730A                                             *              
      *                                                          *              
      * CHG-REQ# BY   DATE      DESCRIPTION                      *              
      * -------- ---- --------  ---------------------------------*              
      * BP132646 SK   22122004  NEW PGM                          *              
      *               28122004  MOVE THE ORIGINAL F730 VALUES    *              
      * BP133611 ZDD1 20032009  ADD 2 FIELDS FOR AHG AND MCP     *              
      *                         PARENT                           *              
      *----------------------------------------------------------*              
                                                                                
      *----------------------------------------------------------*              
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *----------------------------------------------------------*              
                                                                                
      *----------------------------------------------------------*              
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *----------------------------------------------------------*              
                                                                                
           SELECT BP13F730  ASSIGN       TO BP13F730.                           
           SELECT P13F730A  ASSIGN       TO P13F730A.                           
                                                                                
      *----------------------------------------------------------*              
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *----------------------------------------------------------*              
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13F730-REC.                                                        
           05 F730-NUM-10YR           PIC X(01).                                
           05 F730-NUM-PORT           PIC X(01).                                
           05 F730-NUM-PPO            PIC X(01).                                
           05 F730-NUM-MCP            PIC X(01).                                
           05 F730-NUM-TCP            PIC X(01).                                
           05 F730-NUM-DUAL           PIC X(01).                                
           05 F730-NUM-CODE33         PIC X(01).                                
           05 F730-NUM-CODE71         PIC X(01).                                
           05 F730-NUM-CODE77         PIC X(01).                                
           05 F730-NUM-TRF            PIC X(01).                                
           05 F730-NUM-SLS            PIC X(01).                                
           05 F730-NUM-MAM            PIC X(01).                                
           05 F730-NUM-PC             PIC X(01).                                
           05 F730-NUM-BLANK          PIC X(01).                                
           05 F730-NUM-HDB01          PIC X(01).                                
           05 F730-NUM-SPOUSE         PIC X(01).                                
           05 F730-NUM-DEBAR          PIC X(01).                                
           05 F730-NUM-AHG            PIC X(01).                                
           05 F730-NUM-MCP-PAR        PIC X(01).                                
           05 F730-NUM-HA1-NOT-SC     PIC X(01).                                
           05 F730-NUM-ALL-NOT-SC     PIC X(01).                                
           05 F730-NUM-AGE            PIC X(01).                                
           05 F730-NUM-DEBAR1         PIC X(01).                                
           05 FILLER                  PIC X(99).                                
           05 F730-NUM-REGN           PIC X(08).                                
           05 FILLER                  PIC X(370).                               
                                                                                
       FD  P13F730A                                                             
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13F730A-REC               PIC X(500).                               
                                                                                
      *----------------------------------------------------------*              
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------*              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F730-READ           PIC 9(08) VALUE ZEROES.                   
           05  WS-F730-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-F730-EOF            PIC X(01) VALUE 'N'.                      
           05  WS-LAST-REGN           PIC X(08) VALUE SPACES.                   
                                                                                
           05  WS-SYS-DATE            PIC X(08) VALUE SPACES.                   
           05  WS-EDIT-DATE           PIC X(10) VALUE SPACES.                   
                                                                                
       01  WS-TEMP-REC.                                                         
           05 WS-NUM-10YR             PIC X(01).                                
           05 WS-NUM-PORT             PIC X(01).                                
           05 WS-NUM-PPO              PIC X(01).                                
           05 WS-NUM-MCP              PIC X(01).                                
           05 WS-NUM-TCP              PIC X(01).                                
           05 WS-NUM-DUAL             PIC X(01).                                
           05 WS-NUM-CODE33           PIC X(01).                                
           05 WS-NUM-CODE71           PIC X(01).                                
           05 WS-NUM-CODE77           PIC X(01).                                
           05 WS-NUM-TRF              PIC X(01).                                
           05 WS-NUM-SLS              PIC X(01).                                
           05 WS-NUM-MAM              PIC X(01).                                
           05 WS-NUM-PC               PIC X(01).                                
           05 WS-NUM-BLANK            PIC X(01).                                
           05 WS-NUM-HDB01            PIC X(01).                                
           05 WS-NUM-SPOUSE           PIC X(01).                                
           05 WS-NUM-DEBAR            PIC X(01).                                
           05 WS-NUM-AHG              PIC X(01).                                
           05 WS-NUM-MCP-PAR          PIC X(01).                                
           05 WS-NUM-HA1-NOT-SC       PIC X(01).                                
           05 WS-NUM-ALL-NOT-SC       PIC X(01).                                
           05 WS-NUM-AGE              PIC X(01).                                
           05 WS-NUM-DEBAR1           PIC X(01).                                
           05 FILLER                  PIC X(477).                               
                                                                                
      *----------------------------------------------------------*              
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------*              
      *----------------------------------------------------------*              
       0000-MAIN.                                                               
      *----------------------------------------------------------*              
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F730   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-RECORD  THRU 3000-EXIT                          
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------*              
                                                                                
           OPEN INPUT  BP13F730                                                 
               OUTPUT  P13F730A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE.                           
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE                                             
                  INTO WS-EDIT-DATE                                             
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
       2000-READ-BP13F730.                                                      
      *----------------------------------------------------------*              
                                                                                
           READ BP13F730                                                        
              AT END                                                            
                 MOVE 'Y' TO WS-F730-EOF                                        
              NOT AT END                                                        
                 ADD 1    TO WS-F730-READ                                       
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
       3000-PROCESS-RECORD.                                                     
      *----------------------------------------------------------*              
                                                                                
           MOVE F730-NUM-REGN         TO WS-LAST-REGN.                          
           MOVE SPACES                TO WS-TEMP-REC.                           
           MOVE BP13F730-REC          TO WS-TEMP-REC.                           
                                                                                
           PERFORM 4000-PROCESS       THRU 4000-EXIT                            
              UNTIL F730-NUM-REGN NOT = WS-LAST-REGN                            
                 OR WS-F730-EOF = 'Y'                                           
           PERFORM 5000-WRITE-P13F730A  THRU 5000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
       4000-PROCESS.                                                            
      *----------------------------------------------------------*              
                                                                                
           IF F730-NUM-10YR   NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-10YR    TO WS-NUM-10YR                              
           END-IF.                                                              
                                                                                
           IF F730-NUM-PORT   NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-PORT    TO WS-NUM-PORT                              
           END-IF.                                                              
                                                                                
           IF F730-NUM-PPO    NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-PPO     TO WS-NUM-PPO                               
           END-IF.                                                              
                                                                                
           IF F730-NUM-MCP    NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-MCP     TO WS-NUM-MCP                               
           END-IF.                                                              
                                                                                
           IF F730-NUM-TCP    NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-TCP     TO WS-NUM-TCP                               
           END-IF.                                                              
                                                                                
           IF F730-NUM-DUAL   NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-DUAL    TO WS-NUM-DUAL                              
           END-IF.                                                              
                                                                                
           IF F730-NUM-CODE33 NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-CODE33  TO WS-NUM-CODE33                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-CODE71 NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-CODE71  TO WS-NUM-CODE71                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-CODE77 NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-CODE77  TO WS-NUM-CODE77                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-TRF    NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-TRF     TO WS-NUM-TRF                               
           END-IF.                                                              
                                                                                
           IF F730-NUM-SLS    NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-SLS     TO WS-NUM-SLS                               
           END-IF.                                                              
                                                                                
           IF F730-NUM-MAM    NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-MAM     TO WS-NUM-MAM                               
           END-IF.                                                              
                                                                                
           IF F730-NUM-PC     NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-PC      TO WS-NUM-PC                                
           END-IF.                                                              
                                                                                
           IF F730-NUM-BLANK  NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-BLANK   TO WS-NUM-BLANK                             
           END-IF.                                                              
                                                                                
           IF F730-NUM-HDB01  NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-HDB01   TO WS-NUM-HDB01                             
           END-IF.                                                              
                                                                                
           IF F730-NUM-SPOUSE NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-SPOUSE  TO WS-NUM-SPOUSE                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-DEBAR  NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-DEBAR   TO WS-NUM-DEBAR                             
           END-IF.                                                              
                                                                                
           IF F730-NUM-AHG    NOT = SPACES AND LOW-VALUES                       
              MOVE F730-NUM-AHG     TO WS-NUM-AHG                               
           END-IF.                                                              
                                                                                
           IF F730-NUM-MCP-PAR NOT = SPACES AND LOW-VALUES                      
              MOVE F730-NUM-MCP-PAR TO WS-NUM-MCP-PAR                           
           END-IF.                                                              
                                                                                
           IF F730-NUM-HA1-NOT-SC NOT  = SPACES AND LOW-VALUES                  
              MOVE F730-NUM-HA1-NOT-SC TO WS-NUM-HA1-NOT-SC                     
           END-IF.                                                              
                                                                                
           IF F730-NUM-ALL-NOT-SC NOT  = SPACES AND LOW-VALUES                  
              MOVE F730-NUM-ALL-NOT-SC TO WS-NUM-ALL-NOT-SC                     
           END-IF.                                                              
                                                                                
           IF F730-NUM-AGE    NOT  = SPACES AND LOW-VALUES                      
              MOVE F730-NUM-AGE        TO WS-NUM-AGE                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-DEBAR1  NOT = SPACES AND LOW-VALUES                      
              MOVE F730-NUM-DEBAR1   TO WS-NUM-DEBAR1                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730  THRU 2000-EXIT.                          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
       5000-WRITE-P13F730A.                                                     
      *----------------------------------------------------------*              
                                                                                
           WRITE P13F730A-REC  FROM WS-TEMP-REC.                                
           ADD 1  TO WS-F730-WRITE.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------*              
       9999-CLOSE-FILES.                                                        
      *----------------------------------------------------------*              
                                                                                
           CLOSE  BP13F730                                                      
                  P13F730A.                                                     
                                                                                
           DISPLAY '*----- BP13C165 -----*'.                                    
           DISPLAY 'DATE : ' WS-EDIT-DATE.                                      
           DISPLAY '*--------------------*'.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'RECS. READ FROM BP13F730 : ' WS-F730-READ.                  
           DISPLAY 'WRITE INTO      P13F730A : ' WS-F730-WRITE.                 
           DISPLAY SPACES.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
