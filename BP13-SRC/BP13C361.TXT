      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C361.                                                 
       AUTHOR.        SGK.                                                      
       DATE-WRITTEN.  24/12/97.                                                 
                                                                                
      ******************************************************************        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS A PROGRAM TO CREATE INTERFACE FILE*        
      *                      TO PASS TO CPFB ON PROPERTY ADDRESS AFTER *        
      *                      BOOKING EXERCISE.                         *        
      *   INPUT FILES :  1.  BP13F310  - TRANSACTION FILE              *        
      *                  2.  BP13K800  -  SOC APPLICATION MASTER       *        
      *                  3.  BP13K820  -  OCCUPIER FILE                *        
      *                                                                *        
      *   OUTPUT FILE :  1.  AB02F898  - TRANSACTION FILE              *        
      *   LISTINGS    :  1.  CONTROL LIST   -   BP13L361               *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      *   BP130455  SGK   24/12/97  NEW PGM                            *        
      *   BP130455  SGK   31/04/98  TO ADD '1R'IN REGN NO FOR FS CASES *        
      *                             AND PREFIX 'S' TO ALL CPF ACCOUNTS *        
      *   BP130418  RHB   29/06/98  Y2K COMPLIANCE CHANGES.            *        
      *   BP130986  CKK   27/02/01  CONVERT BP13F001 TO AB02F898       *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS BP13K820-STATUS.                   
           SELECT AB02F898  ASSIGN        TO AB02F898.                          
           SELECT BP13L361  ASSIGN        TO BP13L361.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   AB02F898                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 130 CHARACTERS.                                     
       COPY AB02F898.                                                           
                                                                                
       FD  BP13L361                                                             
           RECORDING MODE   IS  F                                               
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  PRINT-REC-1                  PIC X(132).                             
                                                                                
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  BP13K800-STATUS              PIC 99    VALUE 0.                      
       01  BP13K820-STATUS              PIC 99    VALUE 0.                      
       01  CNT-F310-READ                PIC 9(3)  VALUE 0.                      
       01  CNT-K800-READ                PIC 9(3)  VALUE 0.                      
       01  CNT-K820-READ                PIC 9(3)  VALUE 0.                      
       01  CNT-F898-WRITE               PIC 9(4)  VALUE 0.                      
       01  CNT-L361A-WRITE              PIC 9(3)  VALUE 0.                      
       01  CNT-LINE-ON-PAGE             PIC 9(3)  VALUE 66.                     
       01  CNT-PRINT-L361               PIC 9(3)  VALUE 1.                      
       01  NO-OF-PAGE                   PIC 9(3)  VALUE 0.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F310-EOF              PIC X(1)  VALUE 'N'.                    
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-CC-CURR               PIC 9(2)  VALUE 00.                     
           05  WS-YY-CURR               PIC 9(2)  VALUE 00.                     
           05  WS-MM-CURR               PIC 9(2)  VALUE 00.                     
           05  WS-DD-CURR               PIC 9(2)  VALUE 00.                     
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD             PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X     VALUE '/'.                    
           05  WS-EDITED-MM             PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X     VALUE '/'.                    
           05  WS-EDITED-CC             PIC X(2)  VALUE SPACES.                 
           05  WS-EDITED-YY             PIC X(2)  VALUE SPACES.                 
                                                                                
                                                                                
       01  BLANK-LINE          PIC X(80)  VALUE SPACES.                         
                                                                                
                                                                                
      *-----------------------------------------------------------*             
      *    WS FOR CONTROL LISTING                                 *             
      *-----------------------------------------------------------*             
                                                                                
       01  L361-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L361'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  PRINT-DATE-1             PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  PRINT-PAGE               PIC ZZZ9.                               
                                                                                
       01  L361-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(28) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
                'LIST OF CASES TO INFORM CPFB ON THE PROPERTY ADDRESS AF        
      -    'TER BOOKING EXERCISE'.                                              
                                                                                
       01  L361-PR-HEAD-03.                                                     
           05  FILLER                 PIC X(05) VALUE ' S-NO'.                  
           05  FILLER                 PIC X(02) VALUE SPACES.                   
           05  FILLER                 PIC X(10) VALUE 'REGN NO.  '.             
           05  FILLER                 PIC X(04) VALUE SPACES.                   
           05  FILLER                 PIC X(09) VALUE 'CPF-NO   '.              
           05  FILLER                 PIC X(04) VALUE SPACES.                   
           05  FILLER                 PIC X(14) VALUE 'SCHEME-ACCT-NO'.         
           05  FILLER                 PIC X(04) VALUE SPACES.                   
           05  FILLER                 PIC X(25) VALUE 'ADDRESS'.                
           05  FILLER                 PIC X(16) VALUE SPACES.                   
                                                                                
       01  L361-PR-DETAILS.                                                     
           05  L361-NUMBER            PIC ZZZZ9.                                
           05  FILLER                 PIC X(2)  VALUE SPACES.                   
           05  L361-REGN-NO           PIC X(10) VALUE SPACES.                   
           05  FILLER                 PIC X(4)  VALUE SPACES.                   
           05  L361-CPF-NO            PIC X(9)  VALUE SPACES.                   
           05  FILLER                 PIC X(03) VALUE SPACES.                   
           05  L361-SCH-ACCT          PIC X(14) VALUE SPACES.                   
           05  FILLER                 PIC X(04) VALUE SPACES.                   
           05  L361-NEW-FLAT-ADDR .                                             
               10  L361-BLKL          PIC X(3)  VALUE 'BLK'.                    
               10  L361-BLK-NO        PIC X(5)  VALUE ' XXX'.                   
               10  FILLER             PIC X(1)  VALUE SPACES.                   
               10  FILLER             PIC X(1)  VALUE '#'.                      
               10  L361-UNIT-NO.                                                
                   15  L361-LEVEL-NO  PIC X(3)  VALUE SPACES.                   
                   15  L361-DASH      PIC X     VALUE '-'.                      
                   15  L361-MAIN-UNIT PIC X(5)  VALUE SPACES.                   
               10  FILLER             PIC X(2)  VALUE SPACES.                   
               10  L361-ST-NAME       PIC X(20) VALUE SPACES.                   
               10  FILLER             PIC X(1)  VALUE SPACE.                    
               10  L361-COUNTRY-NME2  PIC X(10) VALUE 'SINGAPORE'.              
               10  L361-POST-CDE      PIC X(6)  VALUE SPACES.                   
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
                                                                                
      ******************************************************************        
       100-MAIN-ROUTINE.                                                        
      ******************************************************************        
           PERFORM  200-OPEN-FILES        THRU    200-EXIT.                     
           PERFORM  300-READ-BP13F310     THRU    300-EXIT                      
           PERFORM  310-READ-INPUT        THRU    310-EXIT                      
                        UNTIL WS-F310-EOF = 'Y'.                                
           PERFORM  1000-CLOSE-ROUTINE    THRU    1000-EXIT.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                OUTPUT BP13L361                                                 
                       AB02F898.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K800-STATUS ' BP13K800-STATUS        
               MOVE     BP13K800-STATUS TO RETURN-CODE                          
               PERFORM  1000-CLOSE-ROUTINE.                                     
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BP13K820-STATUS ' BP13K820-STATUS        
               MOVE     BP13K820-STATUS TO RETURN-CODE                          
               PERFORM  1000-CLOSE-ROUTINE.                                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-READ-BP13F310.                                                       
      ******************************************************************        
      *--------------------------------------------------------*                
      *    READ BP13F300 UNTIL EOF                             *                
      *--------------------------------------------------------*                
           READ BP13F310                                                        
                AT END MOVE 'Y'  TO  WS-F310-EOF                                
                       GO TO 300-EXIT.                                          
           ADD  1  TO CNT-F310-READ.                                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       310-READ-INPUT.                                                          
      ******************************************************************        
            PERFORM 500-READ-K800      THRU  500-EXIT.                          
            PERFORM 300-READ-BP13F310  THRU  300-EXIT.                          
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-K800.                                                           
      ******************************************************************        
           MOVE F310-REGN-NO  TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00                                              
              MOVE   SPACES               TO   AB02F898-REC                     
                                               L361-PR-DETAILS                  
              IF K800-AMT-REGN-DEPOSIT-CASH IS NOT NUMERIC                      
                 MOVE  ZERO      TO     K800-AMT-REGN-DEPOSIT-CASH              
              END-IF                                                            
              IF K800-AMT-REGN-DEPOSIT-CPF1 IS NOT NUMERIC                      
                 MOVE  ZERO      TO     K800-AMT-REGN-DEPOSIT-CPF1              
              END-IF                                                            
              IF K800-AMT-REGN-DEPOSIT-CPF2 IS NOT NUMERIC                      
                 MOVE  ZERO      TO     K800-AMT-REGN-DEPOSIT-CPF2              
              END-IF                                                            
              IF K800-AMT-REGN-DEPOSIT-CPF3 IS NOT NUMERIC                      
                 MOVE  ZERO      TO     K800-AMT-REGN-DEPOSIT-CPF3              
              END-IF                                                            
              IF K800-AMT-REGN-DEPOSIT-CPF4 IS NOT NUMERIC                      
                 MOVE  ZERO      TO     K800-AMT-REGN-DEPOSIT-CPF4              
              END-IF                                                            
              IF K800-NUM-SCH-ACC NOT = SPACES AND                              
                 (K800-AMT-REGN-DEPOSIT-CASH > ZERO  OR                         
                  K800-AMT-REGN-DEPOSIT-CPF1 > ZERO  OR                         
                  K800-AMT-REGN-DEPOSIT-CPF2 > ZERO  OR                         
                  K800-AMT-REGN-DEPOSIT-CPF3 > ZERO  OR                         
                  K800-AMT-REGN-DEPOSIT-CPF4 > ZERO)                            
                 STRING '1R'   K800-NUM-REGN DELIMITED BY SIZE                  
                                        INTO   F898-NUM-REGN                    
                 MOVE   F898-NUM-REGN     TO   L361-REGN-NO                     
              ELSE                                                              
                 MOVE   K800-NUM-REGN     TO   F898-NUM-REGN                    
                                               L361-REGN-NO                     
              END-IF                                                            
              MOVE   '0'                  TO   F898-NUM-HDB-REF-PREFIX          
              MOVE   K800-NUM-SCH         TO   F898-NUM-SCH                     
              MOVE   K800-NUM-ACC         TO   F898-NUM-ACCNT                   
              MOVE   K800-NUM-ACC-CK      TO   F898-CDE-CDG                     
              MOVE   K800-NUM-LESSEE-CODE TO   F898-CDE-LESSEE                  
                                                                                
              STRING K800-NUM-SCH         '-'                                   
                     K800-NUM-ACC         '-'                                   
                     K800-NUM-ACC-CK      '-'                                   
                     K800-NUM-LESSEE-CODE DELIMITED BY SIZE                     
                     INTO L361-SCH-ACCT                                         
                                                                                
              PERFORM  600-PROCESS-BP13F310 THRU 600-EXIT                       
                                                                                
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 DISPLAY 'K800 RECORD NOT FOUND, K800-NUM-REGN = '              
                          K800-NUM-REGN                                         
              ELSE                                                              
                 DISPLAY 'ERROR READING, BP13K800 = ' BP13K800-STATUS           
                 MOVE     BP13K800-STATUS TO RETURN-CODE                        
                 PERFORM  1000-CLOSE-ROUTINE.                                   
                                                                                
           ADD 1 TO CNT-K800-READ.                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-PROCESS-BP13F310.                                                    
      ******************************************************************        
                                                                                
           MOVE 'BLK'                   TO L361-BLKL.                           
           MOVE F310-NUM-BLK            TO L361-BLK-NO                          
                                           F898-NUM-BLK.                        
                                                                                
           MOVE F310-NME-STREET         TO L361-ST-NAME                         
                                           F898-NME-STREET.                     
                                                                                
           MOVE F310-NUM-LEVEL          TO L361-LEVEL-NO                        
                                           F898-NUM-HSE-LVL.                    
                                                                                
           MOVE '-'                     TO L361-DASH.                           
                                                                                
           MOVE F310-NUM-MAIN           TO L361-MAIN-UNIT.                      
                                                                                
           MOVE F310-NUM-MAIN(1:4)      TO F898-NUM-HSE-U-MAIN.                 
                                                                                
           IF F310-NUM-MAIN(5:1) = SPACES OR LOW-VALUES OR ZEROES               
              MOVE SPACES                TO  F898-NUM-HSE-U-SUB                 
           ELSE                                                                 
              MOVE F310-NUM-MAIN(5:1)    TO  F898-NUM-HSE-U-SUB                 
           END-IF.                                                              
                                                                                
           IF   F310-NUM-POSTAL = SPACES OR LOW-VALUES OR ZEROES                
                MOVE '.'                TO L361-COUNTRY-NME2                    
                MOVE SPACES             TO L361-POST-CDE                        
                                            F898-NUM-POST-CDE                   
           ELSE                                                                 
                MOVE 'SINGAPORE'        TO L361-COUNTRY-NME2                    
                MOVE F310-NUM-POSTAL    TO L361-POST-CDE                        
                                            F898-NUM-POST-CDE                   
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-NRIC1          TO K820-NUM-NRIC.                       
                                                                                
           PERFORM 700-READ-BP13K820  THRU 700-EXIT.                            
                                                                                
           IF BP13K820-STATUS = 00                                              
              IF K820-NUM-CPF NOT = SPACES AND LOW-VALUES                       
                 MOVE  K820-NUM-CPF     TO L361-CPF-NO                          
                                           F898-NUM-CPF-ACCT1                   
              END-IF                                                            
           ELSE                                                                 
              MOVE  SPACES           TO L361-CPF-NO                             
                                        F898-NUM-CPF-ACCT1                      
           END-IF.                                                              
                                                                                
           PERFORM 800-WRITE-CONTROL-LIST THRU 800-EXIT.                        
           MOVE SPACES TO L361-PR-DETAILS.                                      
                                                                                
           IF K800-NUM-NRIC2 NOT =  SPACES AND LOW-VALUES                       
              MOVE K800-NUM-NRIC2       TO K820-NUM-NRIC                        
              PERFORM 700-READ-BP13K820 THRU 700-EXIT                           
                                                                                
              IF BP13K820-STATUS = 00                                           
                 IF K820-NUM-CPF NOT = SPACES AND LOW-VALUES                    
                    MOVE  K820-NUM-CPF     TO L361-CPF-NO                       
                                              F898-NUM-CPF-ACCT2                
                    PERFORM 800-WRITE-CONTROL-LIST THRU 800-EXIT                
                    MOVE SPACES TO L361-PR-DETAILS                              
                 END-IF                                                         
              ELSE                                                              
                 MOVE  SPACES           TO L361-CPF-NO                          
                                           F898-NUM-CPF-ACCT2                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT =  SPACES AND LOW-VALUES                       
              MOVE K800-NUM-NRIC3       TO K820-NUM-NRIC                        
              PERFORM 700-READ-BP13K820 THRU 700-EXIT                           
                                                                                
              IF BP13K820-STATUS = 00                                           
                 IF K820-NUM-CPF NOT = SPACES AND LOW-VALUES                    
                    MOVE  K820-NUM-CPF     TO L361-CPF-NO                       
                                              F898-NUM-CPF-ACCT3                
                    PERFORM 800-WRITE-CONTROL-LIST THRU 800-EXIT                
                    MOVE SPACES TO L361-PR-DETAILS                              
                 END-IF                                                         
              ELSE                                                              
                 MOVE  SPACES           TO L361-CPF-NO                          
                                           F898-NUM-CPF-ACCT3                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT =  SPACES AND LOW-VALUES                       
              MOVE K800-NUM-NRIC4       TO K820-NUM-NRIC                        
              PERFORM 700-READ-BP13K820 THRU 700-EXIT                           
                                                                                
              IF BP13K820-STATUS = 00                                           
                 IF K820-NUM-CPF NOT = SPACES AND LOW-VALUES                    
                    MOVE  K820-NUM-CPF     TO L361-CPF-NO                       
                                              F898-NUM-CPF-ACCT4                
                    PERFORM 800-WRITE-CONTROL-LIST THRU 800-EXIT                
                    MOVE SPACES TO L361-PR-DETAILS                              
                 END-IF                                                         
              ELSE                                                              
                 MOVE  SPACES           TO L361-CPF-NO                          
                                           F898-NUM-CPF-ACCT4                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE   AB02F898-REC.                                                
                                                                                
           ADD     1                    TO  CNT-F898-WRITE.                     
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-READ-BP13K820.                                                       
      ******************************************************************        
      *--------------------------------------------------------*                
      *  READ BP13K820 APPLICANT FILE TO GET CPF NO            *                
      *--------------------------------------------------------*                
           MOVE F310-REGN-NO  TO K820-NUM-REGN.                                 
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF BP13K820-STATUS = 23                                              
              DISPLAY 'K820 REC NOT FOUND, K820-NUM-REGN = '                    
                       K820-NUM-REGN                                            
           ELSE                                                                 
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'ERROR READING, BP13K820 = ' BP13K820-STATUS              
              MOVE     BP13K820-STATUS TO RETURN-CODE                           
              PERFORM  1000-CLOSE-ROUTINE.                                      
                                                                                
           ADD 1 TO CNT-K820-READ.                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-WRITE-CONTROL-LIST.                                                  
      ******************************************************************        
      *--------------------------------------------------------*                
      *    PRINT ERROR LIST DETAIL                             *                
      *--------------------------------------------------------*                
           IF CNT-LINE-ON-PAGE > 46                                             
              PERFORM 850-PRINT-HEADING  THROUGH  850-EXIT                      
              MOVE  1  TO CNT-LINE-ON-PAGE.                                     
                                                                                
           MOVE  SPACES            TO    PRINT-REC-1.                           
                                                                                
           MOVE  CNT-PRINT-L361    TO    L361-NUMBER.                           
           WRITE PRINT-REC-1       FROM  L361-PR-DETAILS AFTER 1.               
                                                                                
           ADD   1                 TO    CNT-LINE-ON-PAGE.                      
           ADD   1                 TO    CNT-PRINT-L361.                        
           MOVE  SPACES            TO    L361-PR-DETAILS.                       
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       850-PRINT-HEADING.                                                       
      ******************************************************************        
      *--------------------------------------------------------*                
      *    PRINT CONTROL LIST HEADING LINE                     *                
      *--------------------------------------------------------*                
           ADD     1                 TO     NO-OF-PAGE.                         
           MOVE    NO-OF-PAGE        TO     PRINT-PAGE.                         
           MOVE FUNCTION CURRENT-DATE TO WS-DTE-CURRENT.                        
           MOVE    WS-CC-CURR        TO     WS-EDITED-CC.                       
           MOVE    WS-YY-CURR        TO     WS-EDITED-YY.                       
           MOVE    WS-MM-CURR        TO     WS-EDITED-MM.                       
           MOVE    WS-DD-CURR        TO     WS-EDITED-DD.                       
           MOVE    WS-EDITED-DATE    TO     PRINT-DATE-1.                       
           MOVE    SPACES            TO     PRINT-REC-1.                        
           WRITE   PRINT-REC-1       AFTER  PAGE.                               
           WRITE   PRINT-REC-1       FROM   L361-PR-HEAD-01 AFTER 4.            
           WRITE   PRINT-REC-1       FROM   L361-PR-HEAD-02 AFTER 2.            
           WRITE   PRINT-REC-1       FROM   L361-PR-HEAD-03 AFTER 4.            
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 AB02F898                                                       
                 BP13L361.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS.        
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K820-STATUS ' BP13K820-STATUS.        
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C361 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM DAILY TRANS   (BP13F310) = '        
                    CNT-F310-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM SOC MASTER    (BP13K800) = '        
                    CNT-K800-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM OCCUPIER FILE (BP13K820) = '        
                    CNT-K820-READ.                                              
           DISPLAY 'NO OF REC.WRTN IN INTERFACE (AB02F898) = '                  
                    CNT-F898-WRITE.                                             
           DISPLAY 'NO OF LETTERS WRITTEN       (BP13L361) = '                  
                    CNT-L361A-WRITE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
