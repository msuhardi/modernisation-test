      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C472.                                                 
      *AUTHOR.        JAI.                                                      
      *DATE-WRITTEN.  MARCH 28 1995.                                            
                                                                                
      *****************************************************************         
      * OBJECTIVE  - TO UPDATE ACCEPT OR REJECT CASES FROM CPF TO               
      *              BP13K600                                                   
      *----------------------------------------------------------------         
      * REF NO      DATE    BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------     ----   ---  -----------------------               *         
      * BP130986  28/02/01 CKK  REPLACE BP13F436 WITH AB02F865        *         
      * BP136711  03/03/17 KSJ3 WRITE REJECTED RECORDS TO BP13K607    *         
      * BP136685  03/04/17 KSJ3 HANDLE DUPLICATE RECORDS              *         
      * BP136755  04/05/17 KSJ3 DELETE ACCEPTED AND REJECTED RECORDS  *         
      *                         FROM BP13K60F                         *         
      * BP137229  08/03/18 PCL4 TO CATER FOR DUPLICATE RECS OF AB02F865         
      *****************************************************************         
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K600 ASSIGN        TO BP13K600                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K600-KEY-FLD                        
                           ALTERNATE KEY IS K600-NUM-SNO                        
                           FILE STATUS   IS K600-STATUS.                        
                                                                                
           SELECT BP13K607 ASSIGN        TO BP13K607                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K607-KEY-FLD                        
                           FILE STATUS   IS K607-STATUS.                        
                                                                                
           SELECT BP13K60F ASSIGN        TO BP13K60F                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K60F-KEY-FLD                        
                           FILE STATUS   IS K60F-STATUS.                        
                                                                                
           SELECT AB02F865 ASSIGN       TO AB02F865.                            
                                                                                
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13K600.                                                           
       COPY BP13K600.                                                           
                                                                                
       FD   BP13K607.                                                           
       COPY BP13K607.                                                           
                                                                                
       FD   BP13K60F.                                                           
       COPY BP13K60F.                                                           
                                                                                
       FD  AB02F865                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 240 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       COPY AB02F865.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K600-STATUS                   PIC 9(02) VALUE 0.                     
       01  K607-STATUS                   PIC 9(02) VALUE 0.                     
       01  K60F-STATUS                   PIC 9(02) VALUE 0.                     
       01  WS-F865-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-K60F-FND                   PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F865-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-F865-BYPASS         PIC 9(07) VALUE 0.                     
           05 WS-CNT-K600-WRT            PIC 9(07) VALUE 0.                     
           05 WS-CNT-K607-WRT            PIC 9(07) VALUE 0.                     
           05 WS-CNT-K607-UPD            PIC 9(07) VALUE 0.                     
           05 WS-CNT-K60F-DEL            PIC 9(07) VALUE 0.                     
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *************                                                             
       0000-CONTROL.                                                            
      *************                                                             
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-F865-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **************                                                            
       1000-START-UP.                                                           
      **************                                                            
           OPEN I-O    BP13K600                                                 
                       BP13K607                                                 
                       BP13K60F                                                 
                INPUT  SY02F001                                                 
                       AB02F865.                                                
                                                                                
           IF  K600-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K600 ' K600-STATUS                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
           END-IF.                                                              
                                                                                
           IF  K607-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K607 ' K607-STATUS                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
           END-IF.                                                              
                                                                                
           IF  K60F-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K60F ' K60F-STATUS                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *******************                                                       
       2000-MAIN-ROUTINE.                                                       
      *******************                                                       
      *---------------------------------------------------------------*         
      * READS AB02F865 AND PROCESS EACH RECORD UNTIL END OF FILE      *         
      *---------------------------------------------------------------*         
           READ AB02F865 AT END                                                 
                MOVE 'Y' TO WS-F865-EOF                                         
                GO       TO 2000-EXIT                                           
                END-READ.                                                       
                                                                                
           ADD  1                           TO WS-CNT-F865-READ.                
                                                                                
           MOVE F865-NUM-SNO                TO K600-NUM-SNO.                    
                                                                                
           READ BP13K600 KEY IS K600-NUM-SNO.                                   
                                                                                
           EVALUATE K600-STATUS                                                 
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-F865-BYPASS               
                    DISPLAY 'RECORD NOT FOUND : ' F865-NUM-SNO                  
                    GO TO 2000-EXIT                                             
               WHEN OTHER                                                       
                    MOVE K600-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BP13K600, STATUS = '                 
                             K600-STATUS                                        
                    PERFORM 9999-WRAP-UP THRU 9999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
           MOVE F865-TXT-REJECT1         TO K600-TXT-REJECT1.                   
           MOVE F865-TXT-REJECT2         TO K600-TXT-REJECT2.                   
           MOVE F865-TXT-REJECT3         TO K600-TXT-REJECT3.                   
           MOVE F865-TXT-REJECT4         TO K600-TXT-REJECT4.                   
           MOVE F865-TXT-REJECT5         TO K600-TXT-REJECT5.                   
                                                                                
           EVALUATE F865-CDE-STATUS                                             
              WHEN 'A'                                                          
                 MOVE F001-DTE-CURRENT      TO K600-DTE-ACCEPTED                
                 MOVE SPACES                TO K600-DTE-REJECTED                
                 PERFORM 3100-SYNC-BP13K60F THRU 3100-EXIT                      
              WHEN 'R'                                                          
                 MOVE F001-DTE-CURRENT      TO K600-DTE-REJECTED                
                 PERFORM 3100-SYNC-BP13K60F THRU 3100-EXIT                      
              WHEN 'P'                                                          
                 IF K600-DTE-CPF-ACK = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE F001-DTE-CURRENT   TO K600-DTE-CPF-ACK                 
                 END-IF                                                         
                 IF K600-TXT-REJECT1 = SPACES                                   
                    MOVE 'PENDING'          TO K600-TXT-REJECT1                 
                 END-IF                                                         
           END-EVALUATE.                                                        
                                                                                
           PERFORM 3000-REWRITE-BP13K600 THRU 3000-EXIT.                        
                                                                                
           IF F865-CDE-STATUS = 'R'                                             
              PERFORM 2100-READ-BP13K607 THRU 2100-EXIT                         
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       2100-READ-BP13K607.                                                      
      **********************                                                    
                                                                                
           MOVE SPACES                     TO   BP13K607-REC.                   
           INITIALIZE                           BP13K607-REC.                   
                                                                                
           IF K600-TAG-EHPS-APPLN = 'Y'                                         
              MOVE 'Y'                     TO   K607-TAG-EHPS-APPLN             
           ELSE                                                                 
              MOVE 'N'                     TO   K607-TAG-EHPS-APPLN             
           END-IF.                                                              
                                                                                
           MOVE F001-DTE-CURRENT           TO K607-DTE-REJ-RECV.                
           MOVE K600-NUM-ORIG-REGN         TO K607-NUM-REGN.                    
           MOVE K600-NUM-CPF               TO K607-NUM-CPF.                     
           MOVE K600-CDE-CPF               TO K607-CDE-CPF.                     
           MOVE K600-NUM-SNO               TO K607-NUM-SNO.                     
                                                                                
           READ BP13K607.                                                       
                                                                                
           EVALUATE K607-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 4200-REWRITE-BP13K607 THRU 4200-EXIT                
                                                                                
               WHEN 23                                                          
                    PERFORM 4100-WRITE-BP13K607   THRU 4100-EXIT                
                                                                                
               WHEN OTHER                                                       
                    MOVE K60F-STATUS                TO RETURN-CODE              
                    DISPLAY 'ERROR READING BP13K60F = ' K600-NUM-SNO            
                    DISPLAY 'STATUS = '                 K60F-STATUS             
                    PERFORM 9999-WRAP-UP          THRU 9999-EXIT                
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***********************                                                   
       3000-REWRITE-BP13K600.                                                   
      ***********************                                                   
                                                                                
           REWRITE BP13K600-REC.                                                
                                                                                
           IF K600-STATUS = ZEROS                                               
              ADD  1             TO WS-CNT-K600-WRT                             
           ELSE                                                                 
              MOVE K600-STATUS   TO RETURN-CODE                                 
              DISPLAY 'ERROR REWRITING BP13K600, STATUS = '                     
                      K600-STATUS                                               
              PERFORM 9999-WRAP-UP THRU 9999-EXIT                               
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *********************                                                     
       3100-SYNC-BP13K60F.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO   BP13K60F-REC.                   
                                                                                
           IF K600-TAG-EHPS-APPLN = 'Y'                                         
              MOVE 'Y'                     TO   K60F-TAG-EHPS-APPLN             
           ELSE                                                                 
              MOVE 'N'                     TO   K60F-TAG-EHPS-APPLN             
           END-IF.                                                              
                                                                                
           MOVE K600-DTE-SEND              TO   K60F-DTE-CREATE                 
           MOVE K600-NUM-ORIG-REGN         TO   K60F-NUM-REGN.                  
           MOVE K600-NUM-CPF               TO   K60F-NUM-CPF.                   
           MOVE K600-CDE-CPF               TO   K60F-CDE-CPF.                   
           MOVE K600-NUM-SNO               TO   K60F-NUM-SNO.                   
                                                                                
           PERFORM 3110-READ-BP13K60F      THRU 3110-EXIT.                      
                                                                                
           IF WS-K60F-FND = 'Y'                                                 
              PERFORM 3120-DELETE-BP13K60F THRU 3120-EXIT                       
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3110-READ-BP13K60F.                                                      
      *********************                                                     
                                                                                
           READ BP13K60F.                                                       
                                                                                
           EVALUATE K60F-STATUS                                                 
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K60F-FND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K60F-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE K60F-STATUS             TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K60F = ' K600-NUM-SNO                  
              DISPLAY 'STATUS = '               K60F-STATUS                     
              PERFORM 9999-WRAP-UP         THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***********************                                                   
       3120-DELETE-BP13K60F.                                                    
      ***********************                                                   
                                                                                
           DELETE BP13K60F.                                                     
                                                                                
           EVALUATE K60F-STATUS                                                 
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K60F-DEL                 
                                                                                
           WHEN OTHER                                                           
              MOVE K60F-STATUS             TO   RETURN-CODE                     
              DISPLAY 'ERROR DELETING BP13K60F = ' K600-NUM-SNO                 
              DISPLAY 'STATUS = '               K60F-STATUS                     
              PERFORM 9999-WRAP-UP         THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***********************                                                   
       4000-MOVE-TO-BP13K607.                                                   
      ***********************                                                   
                                                                                
           MOVE SPACES                     TO   BP13K607-REC.                   
                                                                                
           IF K600-TAG-EHPS-APPLN = 'Y'                                         
              MOVE 'Y'                     TO   K607-TAG-EHPS-APPLN             
           ELSE                                                                 
              MOVE 'N'                     TO   K607-TAG-EHPS-APPLN             
           END-IF.                                                              
                                                                                
           MOVE F001-DTE-CURRENT           TO   K607-DTE-REJ-RECV.              
           MOVE K600-NUM-ORIG-REGN         TO   K607-NUM-REGN.                  
           MOVE K600-NUM-CPF               TO   K607-NUM-CPF.                   
           MOVE K600-CDE-CPF               TO   K607-CDE-CPF.                   
           MOVE K600-NUM-SNO               TO   K607-NUM-SNO.                   
           MOVE BP13K600-REC               TO   K607-ORIGINAL-REC.              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *********************                                                     
       4100-WRITE-BP13K607.                                                     
      *********************                                                     
                                                                                
           PERFORM 4000-MOVE-TO-BP13K607   THRU 4000-EXIT.                      
                                                                                
           WRITE BP13K607-REC.                                                  
                                                                                
           IF K607-STATUS = ZEROS OR 02                                         
              ADD  1                       TO   WS-CNT-K607-WRT                 
           ELSE                                                                 
              MOVE K607-STATUS             TO   RETURN-CODE                     
              DISPLAY 'ERROR WRITING BP13K607, STATUS = ' K607-STATUS           
              PERFORM 9999-WRAP-UP         THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ************************                                                  
       4200-REWRITE-BP13K607.                                                   
      ************************                                                  
                                                                                
           PERFORM 4000-MOVE-TO-BP13K607   THRU 4000-EXIT.                      
                                                                                
           REWRITE BP13K607-REC.                                                
                                                                                
           IF K607-STATUS = ZEROS                                               
              ADD  1             TO WS-CNT-K607-UPD                             
           ELSE                                                                 
              MOVE K607-STATUS   TO RETURN-CODE                                 
              DISPLAY 'ERROR REWRITING BP13K607, STATUS = '                     
                      K607-STATUS                                               
              PERFORM 9999-WRAP-UP THRU 9999-EXIT                               
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **************                                                            
       9999-WRAP-UP.                                                            
      **************                                                            
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C472'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F865 RECS READ     = ' WS-CNT-F865-READ.          
           DISPLAY 'TOTAL BP13F865 RECS BYPASSED = ' WS-CNT-F865-BYPASS.        
           DISPLAY 'TOTAL BP13K600 RECS WRITTEN  = ' WS-CNT-K600-WRT.           
           DISPLAY 'TOTAL BP13K607 RECS WRITTEN  = ' WS-CNT-K607-WRT.           
           DISPLAY 'TOTAL BP13K607 RECS UPDATED  = ' WS-CNT-K607-UPD.           
           DISPLAY 'TOTAL BP13K60F RECS DELETED  = ' WS-CNT-K60F-DEL.           
                                                                                
           CLOSE BP13K600                                                       
                 BP13K607                                                       
                 BP13K60F                                                       
                 AB02F865                                                       
                 SY02F001.                                                      
                                                                                
           IF K600-STATUS NOT = 0                                               
              MOVE K600-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K600 ' K600-STATUS                  
           END-IF.                                                              
                                                                                
           IF K607-STATUS NOT = 0                                               
              MOVE K607-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K607 ' K607-STATUS                  
           END-IF.                                                              
                                                                                
           IF K60F-STATUS NOT = 0                                               
              MOVE K60F-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K60F ' K60F-STATUS                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
