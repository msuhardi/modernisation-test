       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45P.                                                 
      *AUTHOR.        FP6.                                                      
      *DATE-WRITTEN.  28/05/2020.                                               
      * ========================================================== *            
      * OBJECTIVE    : SELF BOOKING FOR POST CIRCUIT BREAKER       *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC30                               *            
      * I-O    FILES :  1.) BP13KC30                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP138330 FP6  28/05/2020 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC30  ASSIGN        TO BP13FC30.                          
                                                                                
           SELECT BP13KC30  ASSIGN        TO BP13KC30                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC30-KEY-FLD                       
                            FILE STATUS   IS WS-KC30-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC30                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200  CHARACTERS.                                     
           COPY BP13FC30.                                                       
                                                                                
       FD  BP13KC30                                                             
           RECORD CONTAINS 200  CHARACTERS.                                     
           COPY BP13KC30.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KC30-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC30-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC30-WRT           PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC30-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC30-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC30-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
           05  WS-DTE-NEXT-MO.                                                  
               10  WS-DTE-NEXT-MO-YR     PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-NEXT-MO-MM     PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-NEXT-MO-DD     PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-WORK.                                                     
               10  WS-DTE-WORK-YR        PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-WORK-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-WORK-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-ADD                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-ADD-INT            PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-SMS-SENT           PIC X(8)   VALUE ZEROES.               
           05  WS-DTE-TMP.                                                      
               10  WS-DTE-TMP-YR         PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-TMP-MM         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-TMP-DD         PIC 9(2)   VALUE ZEROES.               
           05  WS-TIME-RUN               PIC X(8)   VALUE ZEROES.               
           05  FILLER OCCURS 10 TIMES.                                          
               10  WS-DTE-APPT           PIC X(8)   VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-TMP-DTE-APPT           PIC X(8)   VALUE SPACES.               
           05  WS-TMP-TME-APPT           PIC X(4)   VALUE SPACES.               
           05  WS-NUM-BATCH              PIC X(10)  VALUE SPACES.               
           05  WS-NUM-APPT-TYPE          PIC X(2)   VALUE SPACES.               
           05  WS-NUM-DAYS               PIC 9(4)   VALUE ZEROES.               
           05  WS-QUOTIENT               PIC 9(4)   VALUE ZEROES.               
           05  WS-REMAINDER              PIC 9(4)   VALUE ZEROES.               
           05  WS-NUM-DAY-OF-WEEK        PIC 9(1)   VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13FC30-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC30-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC30                                                 
                I-O    BP13KC30.                                                
                                                                                
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
           MOVE WS-DTE-FMT8                TO   WS-DTE-CURR-8.                  
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC30-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC30                                                        
           AT END                                                               
              MOVE 'Y'                     TO   WS-FC30-EOF                     
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1                           TO   WS-CNT-FC30-READ.               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
                                                                                
           PERFORM 3300-PROC-TIME-APPT   THRU 3300-EXIT.                        
                                                                                
           PERFORM 2000-BP13FC30-READ    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3300-PROC-TIME-APPT.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                  TO   BP13KC30-REC                       
           MOVE FC30-DTE-SENT-SELF-BK   TO   KC30-DTE-SENT-SELF-BK              
           MOVE FC30-NUM-APPT-TYPE      TO   KC30-NUM-APPT-TYPE                 
           MOVE FC30-DTE-APPT           TO   KC30-DTE-APPT                      
           MOVE FC30-TME-APPT           TO   KC30-TME-APPT                      
           MOVE FC30-NUM-SLOT           TO   KC30-NUM-SLOT                      
           MOVE FC30-NUM-REGN           TO   KC30-NUM-REGN                      
                                                                                
           ACCEPT WS-TIME-RUN           FROM TIME                               
           MOVE WS-TIME-RUN             TO   KC30-TME-CREATE                    
           MOVE WS-DTE-CURR-8           TO   KC30-DTE-CREATE                    
                                                                                
           PERFORM 3310-BP13KC30-WRITE  THRU 3310-EXIT.                         
                                                                                
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3310-BP13KC30-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC30-REC.                                                  
                                                                                
           EVALUATE WS-KC30-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-KC30-WRT                 
                                                                                
           WHEN 22                                                              
              DISPLAY 'REC ALREADY EXISTS IN BP13KC30: '                        
                      KC30-DTE-APPT KC30-TME-APPT KC30-NUM-SLOT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13KC30 ERROR '   WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC30                                                       
                 BP13KC30.                                                      
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC30 ERROR ' WS-KC30-STATUS                  
              MOVE     WS-KC30-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45P CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13KC30 WRITTEN   : ' WS-CNT-KC30-WRT.           
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
