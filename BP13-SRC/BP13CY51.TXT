       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY51.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  11/06/2009.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   :                                       *                
      *                                                        *                
      *    INPUT FILE  : BP13F557                              *                
      *                  BP13F558                              *                
      *                                                        *                
      *   OUTPUT FILE  : P13F557A                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------- ---- -------- ------------------------------- *                
      * BP133653 JB8  11/06/09 NEW PROGRAM                     *                
      * BP135615 IMC1 23/12/14 EXPAND F557 TO X(400)           *                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558  ASSIGN       TO BP13F558.                           
           SELECT BP13F557  ASSIGN       TO BP13F557.                           
           SELECT P13F557A  ASSIGN       TO P13F557A.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F557                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F557.                                                           
                                                                                
       FD   P13F557A                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   P13F557A-REC            PIC X(400).                                 
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY             PIC X(04) .                                 
           05 WS-SYS-MM             PIC X(02) .                                 
           05 WS-SYS-DD             PIC X(02) .                                 
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DAY           PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-MONTH         PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-YEAR          PIC X(04) .                                 
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F557-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F557-MATCH              PIC 9(8)  VALUE ZEROES.                
           05 WS-F557-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F558-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F557A-WRITE             PIC 9(8)  VALUE ZEROES.                
           05 WS-F558-READ               PIC 9(8)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05 EOF-F558-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F558                     VALUE 'YES'.                      
           05 EOF-F557-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F557                     VALUE 'YES'.                      
           05 WS-F558-MATCH         PIC X(1)  VALUE SPACE.                      
              88 F558-MATCH                   VALUE 'Y'.                        
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           PERFORM 200-READ-BP13F557   THRU 200-EXIT.                           
           PERFORM 300-READ-BP13F558   THRU 300-EXIT.                           
           PERFORM 400-PROCESS-INDATA  THRU 400-EXIT                            
             UNTIL F557-NUM-REGN = HIGH-VALUE                                   
               AND F558-NUM-REGN = HIGH-VALUE.                                  
           PERFORM 900-CLOSE-FILES     THRU 900-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       100-OPEN-FILES.                                                          
      *------------------                                                       
                                                                                
           OPEN INPUT  BP13F558                                                 
                       BP13F557                                                 
                OUTPUT P13F557A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYS-DATE.                   
           MOVE   WS-SYS-DD       TO WS-EDIT-DAY                                
           MOVE   WS-SYS-MM       TO WS-EDIT-MONTH                              
           MOVE   WS-SYS-YY       TO WS-EDIT-YEAR.                              
                                                                                
                                                                                
           MOVE 'NO '     TO EOF-F558-IND.                                      
           MOVE 'NO '     TO EOF-F557-IND.                                      
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F557.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F557  AT END                                                
                MOVE 'YES'       TO EOF-F557-IND                                
                MOVE HIGH-VALUES TO F557-NUM-REGN                               
                GO TO 200-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F557-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BP13F558.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F558  AT END                                                
                MOVE 'YES'       TO EOF-F558-IND                                
                MOVE HIGH-VALUES TO F558-NUM-REGN                               
                GO TO 300-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F558-READ.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       400-PROCESS-INDATA.                                                      
      *------------------*                                                      
                                                                                
                                                                                
           IF F557-NUM-REGN = F558-NUM-REGN                                     
              ADD 1                              TO WS-F557-MATCH               
              MOVE 'Y'                           TO WS-F558-MATCH               
              PERFORM 490-WRITE-P13F557A       THRU 490-EXIT                    
              PERFORM 200-READ-BP13F557        THRU 200-EXIT                    
              PERFORM 300-READ-BP13F558        THRU 300-EXIT                    
           ELSE                                                                 
              IF F557-NUM-REGN < F558-NUM-REGN                                  
                 ADD  1                          TO WS-F557-NOMATCH             
                 MOVE 'N'                        TO WS-F558-MATCH               
                 PERFORM 490-WRITE-P13F557A    THRU 490-EXIT                    
                 PERFORM 200-READ-BP13F557     THRU 200-EXIT                    
              ELSE                                                              
                 ADD  1                          TO WS-F558-NOMATCH             
                 PERFORM 300-READ-BP13F558     THRU 300-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       490-WRITE-P13F557A.                                                      
                                                                                
           IF F558-MATCH                                                        
              MOVE F558-DTE-APPR-RSL-LATEST                                     
                TO F557-DTE-SALE-EFF-RSL-HA1                                    
              MOVE F558-CDE-FT-RSL-LATEST                                       
                TO F557-NUM-FT-RSL-HA1                                          
              MOVE F558-CDE-SALES-TYPE-SMS                                      
                TO F557-NUM-SALE-TYPE-SMS-HA1                                   
           END-IF.                                                              
                                                                                
           WRITE P13F557A-REC FROM BP13F557-REC.                                
                                                                                
           ADD 1                                 TO WS-F557A-WRITE.             
                                                                                
       490-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------                                                         
       900-CLOSE-FILES.                                                         
      *----------------                                                         
                                                                                
           CLOSE BP13F557                                                       
                 P13F557A                                                       
                 BP13F558.                                                      
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CY51                 '.                    
           DISPLAY '                                      '.                    
           DISPLAY 'DATE : ' WS-EDIT-DATE.                                      
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS READ FROM F557   = ' WS-F557-READ.           
           DISPLAY 'NO. OF RECORDS READ FROM F558   = ' WS-F558-READ.           
           DISPLAY 'NO. OF RECORDS OF F557 MATCHD   = ' WS-F557-MATCH.          
           DISPLAY 'NO. OF RECORDS OF F557 NOT MATCHED ='                       
                   WS-F557-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS OF F558 NOT MATCHED ='                       
                   WS-F558-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS WRITTEN TO F557A = ' WS-F557A-WRITE.         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
