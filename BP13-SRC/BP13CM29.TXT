      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13CM29.                                                  
       AUTHOR.       SUJITHRA CHINNIAH.                                         
       DATE-WRITTEN. 18/08/2023.                                                
      ******************************************************************        
      *                                                                *        
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                          *        
      *    SYSTEM ID   : BP13                                          *        
      *    OBJECTIVES  : TO PERFORM FT(PMC) TAG UPDATE TO APPLICATION  *        
      *                  REQUEST VSAM FILE BP13K730.                   *        
      *                                                                *        
      *    INPUT  FILE : BP13FIN1 - ACCEPTED AND REJECTED FILE         *        
      *                  BP13K730 - APPLICATION REQUEST FILE           *        
      *                                                                *        
      *    OUTPUT FILE : BP13FOT1 - REJECTED FILE                      *        
      *                                                                *        
      ******************************************************************        
      * CHG REQ   BY      DATE     DESCRIPTION                         *        
      *----------------------------------------------------------------*        
      * BP139530  SC54  18/08/2023 NEW PROGRAM                         *        
      ******************************************************************        
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13FIN1 ASSIGN TO BP13FIN1                                  
            FILE STATUS  IS WS-FIN1-STA.                                        
                                                                                
            SELECT BP13K730 ASSIGN TO BP13K730                                  
            ACCESS MODE  IS DYNAMIC                                             
            ORGANIZATION IS INDEXED                                             
            RECORD KEY   IS K730-NUM-REGN                                       
            FILE STATUS  IS WS-K730-STA.                                        
                                                                                
            SELECT BP13FOT1 ASSIGN TO BP13FOT1.                                 
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD BP13FIN1                                                              
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01 FIN1-INPUT                         PIC X(100).                        
                                                                                
       FD BP13K730                                                              
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD BP13FOT1                                                              
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01 FOT1-OUTPUT                        PIC X(100).                        
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *-------------------------*                                               
      *---------------------------------------------------------*               
      *  FILE LAYOUTS                                           *               
      *---------------------------------------------------------*               
                                                                                
       01 WS-FIN1-INPUT.                                                        
          05 WS-FIN1-HEADER.                                                    
             10 FILLER                       PIC X(100).                        
          05 WS-FIN1-DETAIL REDEFINES WS-FIN1-HEADER.                           
             10 FILLER                       PIC X(06).                         
             10 WS-FIN1-NUM-REGN             PIC X(08).                         
             10 WS-FIN1-FILLER               PIC X(41).                         
             10 WS-FIN1-CDE-FTPMC            PIC X(01).                         
             10 FILLER                       PIC X(44).                         
          05 WS-FIN1-TRAILER REDEFINES WS-FIN1-HEADER.                          
             10 FILLER                       PIC X(100).                        
                                                                                
       01 WS-FOT1-OUTPUT.                                                       
          05 WS-FOT1-HEADER.                                                    
             10 FILLER                       PIC X(100) VALUE SPACES.           
          05 WS-FOT1-DETAIL REDEFINES WS-FOT1-HEADER.                           
             10 FILLER                       PIC X(56).                         
             10 WS-FOT1-REMARK               PIC X(30).                         
             10 FILLER                       PIC X(14).                         
          05 WS-FOT1-TRAILER REDEFINES WS-FOT1-HEADER.                          
             10 FILLER                       PIC X(14).                         
             10 FOT1-RCD-CNT                 PIC X(08).                         
             10 FILLER                       PIC X(78).                         
                                                                                
      *---------------------------------------------------------*               
      *  PROGRAM VARIABLES                                      *               
      *---------------------------------------------------------*               
         01 WS-FILE-STA.                                                        
            05 WS-FIN1-STA                   PIC 9(02) VALUE ZEROES.            
            05 WS-K730-STA                   PIC 9(02) VALUE ZEROES.            
         01 WS-FILE-CNT.                                                        
            05 WS-FIN1-CNT                   PIC 9(08) VALUE ZEROES.            
            05 WS-FOT1-CNT                   PIC 9(08) VALUE ZEROES.            
            05 WS-K730-UPDATE-CNT            PIC 9(08) VALUE ZEROES.            
            05 WS-M-CNT                      PIC 9(08) VALUE ZEROES.            
            05 WS-C-CNT                      PIC 9(08) VALUE ZEROES.            
            05 WS-O-CNT                      PIC 9(08) VALUE ZEROES.            
            05 WS-N-CNT                      PIC 9(08) VALUE ZEROES.            
            05 WS-OTHERS-CNT                 PIC 9(08) VALUE ZEROES.            
         01 WS-CHAR.                                                            
            05 WS-EOF-FIN1                   PIC X VALUE 'N'.                   
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
      ****************************************************************          
       0000-MAIN-PROCESS.                                                       
      ****************************************************************          
           PERFORM 1000-INIT-PROCESS         THRU 1000-EXIT.                    
           PERFORM 2000-APPL-PROCESS         THRU 2000-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INIT-PROCESS.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FIN1                                                 
                I-O    BP13K730                                                 
                OUTPUT BP13FOT1.                                                
                                                                                
           IF WS-K730-STA NOT = ZEROS AND '97'                                  
              DISPLAY 'BP13CM29-E02: BP13K730 FILE OPENING ERROR.'              
              DISPLAY 'K730-STATUS : ' WS-K730-STA                              
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
              PERFORM 9100-CANCEL-JOB        THRU 9100-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                       TO WS-FOT1-OUTPUT                  
           PERFORM 1100-INIT-READ            THRU 1100-EXIT.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1100-INIT-READ.                                                          
      ****************************************************************          
                                                                                
           READ BP13FIN1 INTO WS-FIN1-INPUT                                     
                AT END                                                          
                   DISPLAY 'BP13CM29-E01: BP13FIN1 IS EMPTY.'                   
                   PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                     
                   PERFORM 9100-CANCEL-JOB   THRU 9100-EXIT                     
                NOT AT END                                                      
                   MOVE WS-FIN1-HEADER       TO WS-FOT1-HEADER                  
                   WRITE FOT1-OUTPUT         FROM WS-FOT1-HEADER                
                   PERFORM 5000-READ-BP13FIN1                                   
                                             THRU 5000-EXIT                     
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-APPL-PROCESS.                                                       
      ****************************************************************          
                                                                                
           PERFORM UNTIL WS-FIN1-NUM-REGN = HIGH-VALUES                         
                      OR WS-EOF-FIN1 = 'Y'                                      
                 MOVE WS-FIN1-DETAIL         TO WS-FOT1-DETAIL                  
                 PERFORM 3000-COUNT-FTPMC    THRU 3000-EXIT                     
                 PERFORM 4000-READ-BP13K730  THRU 4000-EXIT                     
                 PERFORM 5000-READ-BP13FIN1  THRU 5000-EXIT                     
           END-PERFORM.                                                         
           PERFORM 9200-PRINT-CTL-SUMMARY    THRU 9200-EXIT.                    
           PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-COUNT-FTPMC.                                                        
      ****************************************************************          
                                                                                
           EVALUATE WS-FIN1-CDE-FTPMC                                           
              WHEN 'M'                                                          
                 ADD 1                       TO WS-M-CNT                        
              WHEN 'C'                                                          
                 ADD 1                       TO WS-C-CNT                        
              WHEN 'O'                                                          
                 ADD 1                       TO WS-O-CNT                        
              WHEN 'N'                                                          
                 ADD 1                       TO WS-N-CNT                        
              WHEN OTHER                                                        
                 ADD 1                       TO WS-OTHERS-CNT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-READ-BP13K730.                                                      
      ****************************************************************          
                                                                                
           MOVE SPACES                       TO BP13K730-REC.                   
           INITIALIZE                           BP13K730-REC.                   
           MOVE WS-FIN1-NUM-REGN             TO K730-NUM-REGN.                  
                                                                                
           READ BP13K730                                                        
                                                                                
           EVALUATE WS-K730-STA                                                 
               WHEN ZEROES                                                      
                 IF K730-NUM-FTPMC = SPACES                                     
                    PERFORM 4100-UPDATE-BP13K730                                
                                             THRU 4100-EXIT                     
                 ELSE                                                           
                    MOVE 'FTPMC HAS VALUE'   TO WS-FOT1-REMARK                  
                    PERFORM 6000-OUTPUT-BP13FOT1                                
                                             THRU 6000-EXIT                     
                 END-IF                                                         
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'FTPMC UPDATE ERROR'                                   
                                             TO WS-FOT1-REMARK                  
                    PERFORM 6000-OUTPUT-BP13FOT1                                
                                             THRU 6000-EXIT                     
               WHEN OTHER                                                       
                 DISPLAY 'BP13CM29-E03: BP13K730 READING ERROR.'                
                 DISPLAY 'K730-STATUS : ' WS-K730-STA                           
                 DISPLAY 'REGN        : ' WS-FIN1-NUM-REGN                      
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
                 PERFORM 9100-CANCEL-JOB     THRU 9100-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4100-UPDATE-BP13K730.                                                    
      ****************************************************************          
                                                                                
           MOVE WS-FIN1-CDE-FTPMC            TO K730-NUM-FTPMC                  
           REWRITE BP13K730-REC.                                                
                                                                                
           EVALUATE WS-K730-STA                                                 
               WHEN ZEROES                                                      
                 ADD 1                       TO WS-K730-UPDATE-CNT              
               WHEN 10                                                          
               WHEN 23                                                          
                 MOVE 'FTPMC UPDATE ERROR'   TO WS-FOT1-REMARK                  
                 PERFORM 6000-OUTPUT-BP13FOT1                                   
                                             THRU 6000-EXIT                     
               WHEN OTHER                                                       
                 DISPLAY 'BP13CM29-E04: BP13K730 REWRITE ERROR.'                
                 DISPLAY 'K730-STATUS : ' WS-K730-STA                           
                 DISPLAY 'REGN        : ' WS-FIN1-NUM-REGN                      
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
                 PERFORM 9100-CANCEL-JOB     THRU 9100-EXIT                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-READ-BP13FIN1.                                                      
      ****************************************************************          
                                                                                
           READ BP13FIN1  INTO WS-FIN1-INPUT                                    
                AT END                                                          
                    MOVE 'Y'                 TO WS-EOF-FIN1                     
                 NOT AT END                                                     
                   IF WS-FIN1-NUM-REGN NOT = HIGH-VALUES                        
                      ADD 1                  TO WS-FIN1-CNT                     
                   ELSE                                                         
                      MOVE WS-FIN1-TRAILER   TO WS-FOT1-TRAILER                 
                      MOVE WS-FOT1-CNT          TO FOT1-RCD-CNT                 
                      WRITE FOT1-OUTPUT         FROM WS-FOT1-TRAILER            
                   END-IF                                                       
           END-READ.                                                            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6000-OUTPUT-BP13FOT1.                                                    
      ****************************************************************          
                                                                                
           ADD 1                             TO WS-FOT1-CNT                     
           WRITE FOT1-OUTPUT FROM WS-FOT1-OUTPUT.                               
                                                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13FIN1                                                       
                 BP13FOT1                                                       
                 BP13K730.                                                      
                                                                                
           IF WS-K730-STA NOT = ZEROS AND 97                                    
              DISPLAY 'BP13CM29-E05: BP13K730 FILE CLOSING ERROR.'              
              DISPLAY 'K730-STATUS : ' WS-K730-STA                              
              PERFORM 9100-CANCEL-JOB        THRU 9100-EXIT                     
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9100-CANCEL-JOB.                                                         
      ****************************************************************          
                                                                                
           MOVE 111                          TO RETURN-CODE                     
           STOP RUN.                                                            
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9200-PRINT-CTL-SUMMARY.                                                  
      ****************************************************************          
                                                                                
           DISPLAY 'BP13RM29-I01: CONTROL SUMMARY'                              
           DISPLAY 'BP13FIN1 RCD CNT      : ' WS-FIN1-CNT                       
           DISPLAY 'UPDATED CNT           : ' WS-K730-UPDATE-CNT                
           DISPLAY 'NO. OF FT(PMC)-M      : ' WS-M-CNT                          
           DISPLAY 'NO. OF FT(PMC)-C      : ' WS-C-CNT                          
           DISPLAY 'NO. OF FT(PMC)-O      : ' WS-O-CNT                          
           DISPLAY 'NO. OF FT(PMC)-N      : ' WS-N-CNT                          
           DISPLAY 'NO. OF FT(PMC)-OTHERS : ' WS-OTHERS-CNT                     
           DISPLAY 'NO. OF UPDATE ERROR   : ' WS-FOT1-CNT.                      
                                                                                
                                                                                
                                                                                
       9200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
           STOP RUN.                                                            
      ****************************************************************          
