       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C03F.                                                 
       AUTHOR.        RALSTON JAMES ELIEZER B MEDRANO.                          
      *DATE-WRITTEN.  17/01/2013.                                               
                                                                                
      *****************************************************************         
      *                SYSTEM NAME (PROJECT CODE)                     *         
      *===============================================================*         
      *  OBJECTIVES : 1. PROGRAM TO OUTPUT SPR NRIS FOR CDB SYSTEM    *         
      *               2. CDB WILL SEND DATA TO MHA FOR NRO SEARCH     *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ #  DATE      AUTHOR    REMARKS                       *         
      * ----------  ----      ------    -------                       *         
      * BP134194    17012013  RJE1      NEW PROGRAM                   *         
      * BP139280    06112023  EAA2      EXPAND BP13K757 FROM 3000 TO  *         
      *                                 4000                          *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN        TO BP13F310.                           
                                                                                
           SELECT BP13F820 ASSIGN        TO BP13F820.                           
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K022 ASSIGN        TO BP13K022                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K022-KEY-FLD                        
                           ALTERNATE KEY IS K022-NUM-REGN                       
                           FILE STATUS   IS WS-K022-STATUS.                     
                                                                                
           SELECT BP13K757 ASSIGN        TO BP13K757                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K757-KEY-FLD                        
                           FILE STATUS   IS WS-K757-STATUS.                     
                                                                                
           SELECT BP13F219 ASSIGN        TO BP13F219.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F310                                                             
           RECORD    CONTAINS 150 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD    CONTAINS 2000 CHARACTERS.                                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F219                                                             
           RECORD CONTAINS 70 CHARACTERS                                        
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F219.                                                           
                                                                                
       FD  BP13F820                                                             
           RECORD    CONTAINS 400 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F820.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K757                                                            
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13K757.                                                           
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  WS-K022-STATUS      PIC 9(02)     VALUE ZEROES.                  
           05  WS-K800-STATUS      PIC 9(02)     VALUE ZEROES.                  
           05  WS-K757-STATUS      PIC 9(02)     VALUE ZEROES.                  
       01  WS-COUNTERS.                                                         
           05  WS-F310-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-F820-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-F310-NMR         PIC 9(8)      VALUE ZEROS.                   
           05  WS-F820-NMR         PIC 9(8)      VALUE ZEROS.                   
           05  WS-K800-NTFND       PIC 9(8)      VALUE ZEROS.                   
           05  WS-MATCH-REC        PIC 9(8)      VALUE ZEROS.                   
           05  WS-F219-WRITE       PIC 9(8)      VALUE ZEROS.                   
       01  WS-K800-FND             PIC X         VALUE SPACES.                  
       01  WS-WRITE                PIC X         VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-F310-READ        THRU 2000-EXIT.                        
           PERFORM 3000-F820-READ        THRU 3000-EXIT.                        
           PERFORM 4000-PROCESS-ROUTINE  THRU 4000-EXIT                         
             UNTIL F310-REGN-NO        = HIGH-VALUE                             
             AND   F820-NUM-REGN       = HIGH-VALUE.                            
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13F820                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP13K757                                                 
                OUTPUT BP13F219.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K800 STATUS IS ' WS-K800-STATUS            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                        
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K022 STATUS IS ' WS-K022-STATUS            
              MOVE WS-K022-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                        
                                                                                
           IF WS-K757-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K757 STATUS IS ' WS-K757-STATUS            
              MOVE WS-K757-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                        
                                                                                
           INITIALIZE WS-F310-READ                                              
                      WS-F820-READ                                              
                      WS-K800-FND                                               
                      WS-F310-NMR                                               
                      WS-F820-NMR                                               
                      WS-F219-WRITE                                             
                      WS-K800-NTFND                                             
                      WS-MATCH-REC.                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       2000-F310-READ.                                                          
      *****************************************************************         
           READ BP13F310 AT END                                                 
                MOVE HIGH-VALUE          TO F310-REGN-NO                        
                GO                       TO 2000-EXIT.                          
                                                                                
           ADD 1                         TO WS-F310-READ.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3000-F820-READ.                                                          
      *****************************************************************         
           READ BP13F820 AT END                                                 
                MOVE HIGH-VALUE          TO F820-NUM-REGN                       
                GO                       TO 3000-EXIT.                          
                                                                                
           ADD 1                         TO WS-F820-READ.                       
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4000-PROCESS-ROUTINE.                                                    
      *****************************************************************         
                                                                                
           IF F310-REGN-NO = F820-NUM-REGN                                      
               ADD 1                     TO WS-MATCH-REC                        
               PERFORM 5000-WRITE-RECS   THRU 5000-EXIT                         
               UNTIL F820-NUM-REGN NOT = F310-REGN-NO                           
                  OR F820-NUM-REGN     = HIGH-VALUE                             
               PERFORM 2000-F310-READ    THRU 2000-EXIT                         
           ELSE                                                                 
              IF F310-REGN-NO > F820-NUM-REGN                                   
                 ADD 1                   TO WS-F820-NMR                         
                 PERFORM 3000-F820-READ  THRU 3000-EXIT                         
              ELSE                                                              
                 ADD 1                   TO WS-F310-NMR                         
                PERFORM 2000-F310-READ   THRU 2000-EXIT                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-WRITE-RECS.                                                         
      *****************************************************************         
           MOVE SPACES                   TO K800-NUM-REGN                       
                                            K022-KEY-FLD                        
                                            K757-KEY-FLD                        
                                            BP13F219-REC.                       
                                                                                
           INITIALIZE                       K800-NUM-REGN                       
                                            K022-KEY-FLD                        
                                            K757-KEY-FLD                        
                                            BP13F219-REC                        
                                            WS-K800-FND.                        
                                                                                
           PERFORM 5100-READ-K800        THRU 5100-EXIT.                        
                                                                                
           IF WS-K800-FND = 'Y'                                                 
              PERFORM 5200-READ-K022     THRU 5200-EXIT                         
              PERFORM 5300-READ-K757     THRU 5300-EXIT                         
              IF WS-WRITE = 'Y'                                                 
                 PERFORM 5400-MOVE-RECS  THRU 5400-EXIT                         
                                                                                
                 WRITE BP13F219-REC                                             
                 ADD 1                   TO WS-F219-WRITE                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-F820-READ        THRU 3000-EXIT.                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5100-READ-K800.                                                          
      *****************************************************************         
                                                                                
           MOVE F310-REGN-NO             TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
             WHEN 00                                                            
               IF K800-NUM-STATUS = 'C'                                         
                  MOVE 'N'               TO WS-K800-FND                         
               ELSE                                                             
                  MOVE 'Y'               TO WS-K800-FND                         
               END-IF                                                           
             WHEN 23                                                            
               DISPLAY 'REGN NOT FOUND IN K800 '                                
                                            F310-REGN-NO                        
               MOVE 'N'                  TO WS-K800-FND                         
               ADD 1                     TO WS-K800-NTFND                       
             WHEN OTHER                                                         
               DISPLAY 'BP13K800 READ ERROR, STATUS ' WS-K800-STATUS            
                       '  REGN : ' K800-NUM-REGN                                
               MOVE WS-K800-STATUS       TO RETURN-CODE                         
               PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5200-READ-K022.                                                          
      *****************************************************************         
           MOVE F310-REGN-NO             TO K022-NUM-REGN.                      
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
                                                                                
             WHEN 00                                                            
                  CONTINUE                                                      
             WHEN 23                                                            
                  DISPLAY 'REGN NOT FOUND IN BP13K022 ' F310-REGN-NO            
             WHEN OTHER                                                         
                  DISPLAY 'ERROR IN READING BP13K022 ' WS-K022-STATUS           
                  MOVE WS-K022-STATUS    TO RETURN-CODE                         
                  PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5300-READ-K757.                                                          
      *****************************************************************         
           MOVE F310-REGN-NO             TO K757-NUM-REGN.                      
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE WS-K757-STATUS                                              
           WHEN ZEROES                                                          
              IF K757-NUM-OWN-PTE-PRTY = 'Y' OR                                 
                 K757-NUM-DISPOSED-PTE-PRTY = 'Y'                               
                 MOVE 'N'                TO WS-WRITE                            
              ELSE                                                              
                 MOVE 'Y'                TO WS-WRITE                            
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'REGN NOT FOUND IN K757 : ' F310-REGN-NO                  
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K757   : ' WS-K757-STATUS              
              MOVE WS-K757-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5400-MOVE-RECS.                                                          
      *****************************************************************         
           MOVE '*'                      TO F219-FILLER-01                      
                                            F219-FILLER-02                      
                                            F219-FILLER-03                      
                                            F219-FILLER-04                      
                                            F219-FILLER-05                      
                                            F219-FILLER-06                      
                                            F219-FILLER-07                      
                                            F219-FILLER-08                      
                                            F219-FILLER-09.                     
                                                                                
           MOVE F820-NUM-NRIC            TO F219-NUM-UIN.                       
           MOVE 'S'                      TO F219-NUM-TAG.                       
           MOVE K800-NUM-REGN            TO F219-NUM-REGN.                      
           MOVE K800-NUM-ELIG-SCHEME     TO F219-NUM-ELIG-SCH.                  
           MOVE K800-NUM-ALLO-SCHEME     TO F219-NUM-ALLOC-SCH.                 
           MOVE K800-NUM-ALLO-CAT        TO F219-ALLOC-MODE.                    
           MOVE K800-DTE-REGN            TO F219-DTE-REGN.                      
           MOVE K022-NUM-OIC             TO F219-NUM-OIC.                       
           MOVE K757-NUM-OWN-PTE-PRTY    TO F219-NUM-PPO(1:1).                  
           MOVE K757-NUM-DISPOSED-PTE-PRTY  TO F219-NUM-PPO(2:1).               
           MOVE FUNCTION CURRENT-DATE(1:8)  TO F219-DTE-UPDATE.                 
                                                                                
       5400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
           DISPLAY '  CONTROL LIST - BP13C03F   '                               
           DISPLAY '  '.                                                        
           DISPLAY '  ** INPUT COUNT **         '.                              
           DISPLAY 'A) TOTAL F310 REC READ      = ' WS-F310-READ.               
           DISPLAY 'B) TOTAL F820 REC READ      = ' WS-F820-READ.               
           DISPLAY '  '.                                                        
           DISPLAY ' **  OUTPUT COUNT **        '.                              
           DISPLAY '  '.                                                        
           DISPLAY 'C) TOTAL RECORDS WRITTEN   = ' WS-F219-WRITE.               
           DISPLAY 'D) REGN MATCHED F310&F820  = ' WS-MATCH-REC.                
           DISPLAY 'E) REGN NOT MATCH F820     = ' WS-F820-NMR.                 
           DISPLAY 'F) REGN NOT MATCH F310     = ' WS-F310-NMR.                 
           DISPLAY 'G) REGN NOT FOUND K800     = ' WS-K800-NTFND.               
                                                                                
           CLOSE      BP13F310                                                  
                      BP13F820                                                  
                      BP13K800                                                  
                      BP13K022                                                  
                      BP13K757                                                  
                      BP13F219.                                                 
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR K800 STATUS IS ' WS-K800-STATUS            
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K022-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR K022 STATUS IS ' WS-K022-STATUS            
              MOVE WS-K022-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K757-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR K757 STATUS IS ' WS-K757-STATUS            
              MOVE WS-K757-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
         9000-EXIT.                                                             
           EXIT.                                                                
      *====================  END OF PROGRAM  ====================*              
