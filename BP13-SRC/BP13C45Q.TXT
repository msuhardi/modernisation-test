       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45Q.                                                 
      *AUTHOR.        FP6.                                                      
      *DATE-WRITTEN.  29/05/2020.                                               
      * ========================================================== *            
      * OBJECTIVE    : PROGRAM TO POPULATE BP13KC70 BASED ON       *            
      *                SELFBOOK SLOTS BP13F454 FOR CB END MATTERS  *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F454                               *            
      *                 2.) BP13K800                               *            
      *                 3.) BP13K820                               *            
      *                 4.) BP13KC20                               *            
      *                 5.) BP13KC25                               *            
      * I-O    FILES :  1.) BP13KC70                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP138334 FP6  29/05/2020 NEW PROGRAM                       *            
      * BP138334 KAC1 30/05/2020 ADDED READING OF K110 TO GET KEY  *            
      * BP138334 KAC1 05/06/2020 TO CATER FOR SA-TP                *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F454  ASSIGN        TO BP13F454.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13KC20  ASSIGN        TO BP13KC20                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC20-KEY-FLD                       
                            ALTERNATE KEY IS KC20-ALT-KEY                       
                            FILE STATUS   IS WS-KC20-STATUS.                    
                                                                                
           SELECT BP13KC25  ASSIGN        TO BP13KC25                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC25-KEY-FLD                       
                            ALTERNATE KEY IS KC25-ALT-KEY1                      
                            FILE STATUS   IS WS-KC25-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-AIX1                          
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BP13KC70  ASSIGN        TO BP13KC70                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC70-KEY-FLD                       
                            ALTERNATE KEY IS KC70-ALT-KEY1                      
                            FILE STATUS   IS WS-KC70-STATUS.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F454                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13F454.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
           COPY BP13K820.                                                       
                                                                                
       FD  BP13KC20                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC20.                                                       
                                                                                
       FD  BP13KC25                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC25.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K110.                                                       
                                                                                
       FD  BP13KC70                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       01  BP13KC70-REC.                                                        
           05  KC70-KEY-FLD.                                                    
               10  KC70-ESTATE                PIC X(3).                         
               10  KC70-NEIGHBOURHOOD         PIC X(4).                         
               10  KC70-CONTRACT-NO           PIC X(4).                         
               10  KC70-BLK-NO                PIC X(5).                         
               10  KC70-ALT-KEY1.                                               
                   15  KC70-NUM-REGN          PIC X(8).                         
           05  KC70-NUM-APPT-TYPE             PIC X(4).                         
           05  KC70-NUM-STATUS                PIC X(1).                         
           05  KC70-CDE-APPT                  PIC X(1).                         
           05  KC70-NUM-CATEGORY              PIC X(1).                         
           05  KC70-NUM-COVID19               PIC X(1).                         
           05  FILLER                         PIC X(68).                        
           05  KC70-NUM-USERID-DEL            PIC X(8).                         
           05  KC70-DTE-UPDATE-DEL            PIC X(8).                         
           05  KC70-TME-UPDATE-DEL            PIC X(9).                         
           05  KC70-NUM-USERID                PIC X(8).                         
           05  KC70-DTE-UPDATE                PIC X(8).                         
           05  KC70-TME-UPDATE                PIC X(9).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC20-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC25-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K110-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC70-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-IDX                    PIC 9(2)   VALUE ZEROES.               
           05  WS-CTR                    PIC 9(2)   VALUE ZEROES.               
           05  WS-CNT-F454-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F454-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC60-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC70-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-SATP                   PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F454-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K820-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC20-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC25-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8.                                                   
               10  WS-DTE-CURR8-CCYY     PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-CURR8-MM       PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-CURR8-DD       PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
           05  WS-DTE-BIRTH.                                                    
               10  WS-DTE-BIRTH-CCYY     PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-BIRTH-MM       PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-BIRTH-DD       PIC 9(2)   VALUE ZEROES.               
           05  WS-TIME-RUN               PIC X(8)   VALUE ZEROES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-AVG-AGE            PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-REM                PIC 9(2)   VALUE ZEROES.               
           05  WS-TMP-DIV                PIC 9(1)   VALUE ZEROES.               
           05  WS-TMP-AGE-TOT            PIC 9(3)   VALUE ZEROES.               
           05  FILLER OCCURS 4 TIMES.                                           
               10  WS-NUM-O-UIN       PIC X(9)    VALUE SPACES.                 
               10  WS-NUM-O-AGE       PIC 9(2)    VALUE ZEROES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F454-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F454-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F454                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13KC20                                                 
                       BP13KC25                                                 
                       BM06K110                                                 
                I-O    BP13KC70.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC25-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC25 ERROR ' WS-KC25-STATUS                  
              MOVE     WS-KC25-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC70-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC70 ERROR ' WS-KC70-STATUS                  
              MOVE     WS-KC70-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F454-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F454                                                        
              AT END MOVE 'Y'              TO   WS-F454-EOF.                    
                                                                                
           IF WS-F454-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F454-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF F454-DTE-APPT NOT = SPACES AND LOW-VALUES AND ZEROES              
              PERFORM 3100-PROCESS-REGNS  THRU 3100-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F454-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3100-PROCESS-REGNS.                                                      
      *=============================================================            
      *-------------------------------------------------------------            
      *    SKIP RECORDS WITH NO REGNS                                           
      *-------------------------------------------------------------            
           IF F454-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES              
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET SOC DETAILS                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE F454-NUM-REGN              TO   K800-NUM-REGN.                  
                                                                                
           PERFORM 3110-BP13K800-READ      THRU 3110-EXIT.                      
                                                                                
           IF WS-K800-FND NOT = 'Y'                                             
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    SKIP RECORDS WITH EXISTING APPOINTMENT DATES                         
      *-------------------------------------------------------------            
           IF K800-DTE-SO-APPT NOT = SPACES AND LOW-VALUES AND ZEROES           
              DISPLAY 'REGN  ' F454-NUM-REGN                                    
              DISPLAY 'OLD APPT    '   K800-DTE-SO-APPT                         
      *       GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    SKIP SATP CASES                                                      
      *    DON'T CHECK FOR TP OR SATP (COVID19 CB EXIT)                         
      *-------------------------------------------------------------            
      *    PERFORM 3140-CHECK-SATP         THRU 3140-EXIT.                      
      *    IF WS-CTR = 0                                                        
      *       ADD 1 TO WS-SATP                                                  
      *       GO TO 3100-EXIT                                                   
      *    END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET OCCUP DETAILS AND COMPUTE AVG AGE                                
      *-------------------------------------------------------------            
           PERFORM 3200-GET-OCCUP          THRU 3200-EXIT.                      
           PERFORM 3300-COMP-AVG-AGE       THRU 3300-EXIT.                      
                                                                                
      *-------------------------------------------------------------            
      *    CHECK RECORD IN SELF BOOKING LIST                                    
      *-------------------------------------------------------------            
           DISPLAY 'F454-KEY: ' F454-KEY-FLD                                    
                                                                                
           MOVE SPACES                     TO   BP13KC20-REC.                   
           MOVE F454-NUM-REGN              TO   KC20-NUM-REGN.                  
                                                                                
           PERFORM 3120-BP13KC20-READ      THRU 3120-EXIT.                      
                                                                                
           IF WS-KC20-FND = 'Y'                                                 
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CHECK RECORD IN SELF BOOKING QUEUE                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13KC25-REC.                   
           MOVE F454-NUM-REGN              TO   KC25-NUM-REGN.                  
                                                                                
           PERFORM 3130-BP13KC25-READ      THRU 3130-EXIT.                      
                                                                                
           IF WS-KC25-FND = 'Y'                                                 
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-SCH-ACC             TO K110-KEY-FLD.                   
                                                                                
           PERFORM 3400-READ-BM06K110      THRU 3400-EXIT.                      
                                                                                
           IF WS-K110-FND NOT = 'Y'                                             
              GO TO 3100-EXIT                                                   
           ELSE                                                                 
           IF K110-DTE-KEY-ISSUED > '00000000'                                  
              DISPLAY 'KEY ISSUED REGN '   K110-REGN-NO                         
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 4000-CREATE-SELFREGN    THRU 4000-EXIT.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3110-BP13K800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K800-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K800-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K800 ERROR '    WS-K800-STATUS                  
              MOVE    WS-K800-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3120-BP13KC20-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC20 KEY IS KC20-ALT-KEY.                                   
                                                                                
           EVALUATE WS-KC20-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC20-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC20-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC20 ERROR '    WS-KC20-STATUS                  
              MOVE    WS-KC20-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3130-BP13KC25-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC25 KEY IS KC25-ALT-KEY1.                                  
                                                                                
           EVALUATE WS-KC25-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC25-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC25-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC25 ERROR '    WS-KC25-STATUS                  
              MOVE    WS-KC25-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3130-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3140-CHECK-SATP.                                                         
      *=============================================================            
      * FOR SATP CASES, SOC-DEBTOR/CASH/CPF MUST BE EQUAL TO ZEROES.            
                                                                                
           MOVE 0    TO WS-CTR.                                                 
                                                                                
           IF K800-AMT-SOC-DEBTOR > ZEROES                                      
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-CASH > ZEROES                                        
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF1-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF2-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF3-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF4-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
       3140-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3200-GET-OCCUP.                                                          
      *=============================================================            
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > 4                  
              MOVE SPACES                  TO   WS-NUM-O-UIN(WS-IDX)            
              MOVE ZEROES                  TO   WS-NUM-O-AGE(WS-IDX)            
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES                     TO   WS-IDX.                         
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC1          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC2          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC3          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC4          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
                     UNTIL WS-IDX > 4                                           
                        OR WS-NUM-O-UIN(WS-IDX) = SPACES                        
              MOVE SPACES                  TO   BP13K820-REC                    
              MOVE K800-NUM-REGN           TO   K820-NUM-REGN                   
              MOVE WS-NUM-O-UIN(WS-IDX)    TO   K820-NUM-NRIC                   
                                                                                
              PERFORM 3210-BP13K820-READ   THRU 3210-EXIT                       
                                                                                
              IF WS-K820-FND = 'Y'                                              
                 IF K820-DTE-BIRTH NOT NUMERIC                                  
                    MOVE ZEROES            TO   WS-NUM-O-AGE(WS-IDX)            
                 ELSE                                                           
                    MOVE K820-DTE-BIRTH    TO   WS-DTE-BIRTH                    
                    COMPUTE WS-NUM-O-AGE(WS-IDX) =                              
                            WS-DTE-CURR8-CCYY - WS-DTE-BIRTH-CCYY               
                                                                                
                    IF WS-DTE-CURR8-MM = WS-DTE-BIRTH-MM                        
                       IF WS-DTE-CURR8-DD > WS-DTE-BIRTH-DD                     
                          SUBTRACT 1       FROM WS-NUM-O-AGE(WS-IDX)            
                       END-IF                                                   
                    ELSE                                                        
                       IF WS-DTE-CURR8-MM > WS-DTE-BIRTH-MM                     
                          SUBTRACT 1       FROM WS-NUM-O-AGE(WS-IDX)            
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3210-BP13K820-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K820-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K820-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K820 ERROR '    WS-K820-STATUS                  
              MOVE    WS-K820-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-COMP-AVG-AGE.                                                       
      *=============================================================            
                                                                                
           MOVE ZEROES                     TO   WS-TMP-DIV                      
                                                WS-TMP-AGE-TOT.                 
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > 4                  
              IF (WS-NUM-O-AGE(WS-IDX) IS NUMERIC)  AND                         
                 (WS-NUM-O-AGE(WS-IDX) NOT = SPACES AND LOW-VALUES AND          
                                              ZEROES)                           
                 ADD 1                     TO   WS-TMP-DIV                      
                 ADD WS-NUM-O-AGE(WS-IDX)  TO   WS-TMP-AGE-TOT                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-TMP-AGE-TOT = 0 OR WS-TMP-DIV = 0                              
              MOVE 0                       TO   WS-NUM-AVG-AGE                  
           ELSE                                                                 
              DIVIDE WS-TMP-AGE-TOT BY WS-TMP-DIV GIVING WS-NUM-AVG-AGE         
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-READ-BM06K110.                                                      
      *=============================================================            
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                     TO   WS-K110-FND               
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'N'                     TO   WS-K110-FND               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'READ BM06K110 ERROR '    WS-K110-STATUS            
                    MOVE    WS-K110-STATUS       TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-CREATE-SELFREGN.                                                    
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13KC70-REC.                   
      *    DISPLAY 'F454-DTE ' F454-DTE-APPT                                    
      *            ' REGN ' F454-NUM-REGN                                       
      *    STRING F454-DTE-APPT 'COVID19 '                                      
      *         DELIMITED BY SIZE          INTO KC70-KEY-FLD(1:16)              
                                                                                
           DISPLAY 'K110-AIX1 : ' K110-AIX1.                                    
                                                                                
           MOVE K110-ESTATE                TO   KC70-ESTATE.                    
           MOVE K110-NEIGHBOURHOOD         TO   KC70-NEIGHBOURHOOD.             
           MOVE K110-CONTRACT-NO           TO   KC70-CONTRACT-NO.               
           MOVE K110-BLK-NO                TO   KC70-BLK-NO.                    
           MOVE F454-NUM-REGN              TO   KC70-NUM-REGN.                  
                                                                                
           DISPLAY 'KC70-KEY-FLD : ' KC70-KEY-FLD.                              
           DISPLAY ' '.                                                         
                                                                                
           MOVE 'TP'                       TO   KC70-NUM-APPT-TYPE.             
           MOVE 'W'                        TO   KC70-CDE-APPT.                  
           MOVE 'A'                        TO   KC70-NUM-STATUS.                
           MOVE 'BP13C45Q'                 TO   KC70-NUM-USERID.                
           MOVE WS-DTE-CURR-8              TO   KC70-DTE-UPDATE.                
           MOVE FUNCTION CURRENT-DATE(9:9) TO   KC70-TME-UPDATE.                
                                                                                
           IF (K800-NUM-HOUSEHOLD = 'H' OR 'T') AND                             
              (WS-NUM-AVG-AGE < 30)                                             
              MOVE '1'                     TO   KC70-NUM-CATEGORY               
           ELSE                                                                 
              IF K800-NUM-PPS-ELIG = 'Y' OR 'E'                                 
                 MOVE '2'                  TO   KC70-NUM-CATEGORY               
              ELSE                                                              
                 MOVE '0'                  TO   KC70-NUM-CATEGORY               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'Y'                        TO   KC70-NUM-COVID19.               
                                                                                
           PERFORM 4010-BP13KC70-WRITE     THRU 4010-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-BP13KC70-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC70-REC.                                                  
                                                                                
           EVALUATE WS-KC70-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-KC70-WRITE               
                                                                                
           WHEN 22                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13KC70 ERROR '   WS-KC70-STATUS                  
              MOVE     WS-KC70-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F454                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13KC20                                                       
                 BP13KC25                                                       
                 BM06K110                                                       
                 BP13KC70.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC25-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC25 ERROR ' WS-KC25-STATUS                  
              MOVE     WS-KC25-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC70-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC70 ERROR ' WS-KC70-STATUS                  
              MOVE     WS-KC70-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45D CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F454 READ      : ' WS-CNT-F454-READ.          
           DISPLAY ' 2. NO OF BP13KC70 WRITTEN   : ' WS-CNT-KC70-WRITE.         
           DISPLAY ' 3. NO OF SATP CASES         : ' WS-SATP.                   
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
