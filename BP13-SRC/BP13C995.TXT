       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C995.                                                  
      *DATE-WRITTEN. 13 JUN 2016.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO EXTRACT EXEMPT AND NONEXEMPT CASES WITH  *            
      *                INCOMPLETE DETAILS FOR THE GENERATION OF    *            
      *                3 PPO EMAIL ALERTS.                         *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136326 20160613 KVL1  NEW PROGRAM                        *            
      * BP136910 20170721 KVL1  TO SEPARATE CASES THAT ARE NON EX- *            
      *                         EMPT AND ALREADY TP.               *            
      * BP139793 20240205 KAC1  TO DISPLAY K73E KEY AND TO CONTINUE*            
      *                         WHEN ERROR NOT FOUND ENCOUTERED IN *            
      *                         BM06K110.                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F73A ASSIGN TO BP13F73A.                                  
           SELECT BP13F73B ASSIGN TO BP13F73B.                                  
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS BP13K024-STATUS.                   
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K410-KEY-FLD                       
                            FILE STATUS   IS BP13K410-STATUS.                   
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BP13L73A ASSIGN TO BP13L73A.                                  
           SELECT BP13L73B ASSIGN TO BP13L73B.                                  
           SELECT BP13L73C ASSIGN TO BP13L73C.                                  
           SELECT BP13L73D ASSIGN TO BP13L73D.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F73A                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BP13K73E REPLACING ==BP13K73E-REC== BY ==BP13F73A-REC==.            
                                                                                
       FD  BP13F73B                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BP13K73E REPLACING ==BP13K73E-REC== BY ==BP13F73B-REC==.            
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100  CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  BP13K024                                                             
           RECORD CONTAINS 100  CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K410.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L73A                                                             
           RECORD CONTAINS 250   CHARACTERS                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE  IS  F.                                               
       01  BP13L73A-REC.                                                        
           05 L73A-NUM-REGN          PIC X(08).                                 
           05 L73A-NUM-NRIC          PIC X(09).                                 
           05 L73A-NUM-HDB-REF       PIC X(11).                                 
           05 L73A-PPO-ADDRESS       PIC X(100).                                
           05 L73A-DTE-DISPOSE-DUE   PIC X(08).                                 
           05 L73A-OIC-EMAIL-POSB    PIC X(60).                                 
           05 L73A-OIC-EMAIL-PREB    PIC X(50).                                 
           05 FILLER                 PIC X(04).                                 
                                                                                
       FD  BP13L73B                                                             
           RECORD CONTAINS 250   CHARACTERS                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE  IS  F.                                               
       01  BP13L73B-REC              PIC X(250).                                
                                                                                
       FD  BP13L73C                                                             
           RECORD CONTAINS 250   CHARACTERS                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE  IS  F.                                               
       01  BP13L73C-REC              PIC X(250).                                
                                                                                
       FD  BP13L73D                                                             
           RECORD CONTAINS 250   CHARACTERS                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE  IS  F.                                               
       01  BP13L73D-REC              PIC X(250).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K800-STATUS          PIC 9(02)  VALUE ZEROES.                    
       01  BP13K022-STATUS          PIC 9(02)  VALUE ZEROES.                    
       01  BP13K024-STATUS          PIC 9(02)  VALUE ZEROES.                    
       01  BP13K410-STATUS          PIC 9(02)  VALUE ZEROES.                    
       01  BM06K110-STATUS          PIC 9(02)  VALUE ZEROES.                    
       01  WS-F73A-READ             PIC 9(6)   VALUE ZERO.                      
       01  WS-F73B-READ             PIC 9(6)   VALUE ZERO.                      
       01  WS-L73A-WRITE            PIC 9(6)   VALUE ZERO.                      
       01  WS-L73B-WRITE            PIC 9(6)   VALUE ZERO.                      
       01  WS-L73C-WRITE            PIC 9(6)   VALUE ZERO.                      
       01  WS-L73D-WRITE            PIC 9(6)   VALUE ZERO.                      
       01  WS-F73A-EOF              PIC X      VALUE 'N'.                       
       01  WS-F73B-EOF              PIC X      VALUE 'N'.                       
       01  WS-BP13K022-FND          PIC X      VALUE 'N'.                       
       01  WS-BP13CCNT-FND          PIC X      VALUE 'N'.                       
       01  WS-BP13K800-FND          PIC X      VALUE 'N'.                       
       01  WS-BP13K410-FND          PIC X      VALUE 'N'.                       
       01  WS-LISTING-TAG           PIC X(1)   VALUE SPACE.                     
       01  WS-NUM-REGN              PIC X(8)   VALUE SPACES.                    
       01  WS-SAVE-OIC              PIC X(3)   VALUE SPACES.                    
       01  WS-CUR-DATE              PIC 9(08)  VALUE ZEROES.                    
       01  WS-PREV-MTH-DATE.                                                    
           05 WS-CCYY               PIC 9(04)  VALUE ZEROES.                    
           05 WS-MM                 PIC 9(02)  VALUE ZEROES.                    
           05 WS-DD                 PIC 9(02)  VALUE ZEROES.                    
       01  WS-TXT-ADDR1             PIC X(40)  VALUE SPACES.                    
       01  WS-TXT-ADDR1-PTE.                                                    
           05 WS-TXT-ADDR1-PART1    PIC X(50)  VALUE SPACES.                    
           05 WS-TXT-ADDR1-PART2    PIC X(50)  VALUE SPACES.                    
           05 WS-TXT-ADDR1-PART3    PIC X(50)  VALUE SPACES.                    
           05 WS-TXT-ADDR1-PART4    PIC X(50)  VALUE SPACES.                    
       01  WS-TXT-ADDR1-OUT         PIC X(40)  VALUE SPACES.                    
       01  WS-WRITE-DETAILS.                                                    
           05 WS-WRITE-NUM-REGN     PIC X(08)  VALUE SPACES.                    
           05 WS-WRITE-NUM-NRIC     PIC X(09)  VALUE SPACES.                    
           05 WS-WRITE-NUM-HDB-REF  PIC X(11)  VALUE SPACES.                    
           05 WS-WRITE-PPO-ADDRESS  PIC X(100) VALUE SPACES.                    
           05 WS-WRITE-DTE-DISPOSE  PIC X(08)  VALUE SPACES.                    
           05 WS-WRITE-OIC-EMAIL    PIC X(60)  VALUE SPACES.                    
           05 WS-WRITE-OIC          PIC X(05)  VALUE SPACES.                    
           05 WS-WRITE-NAME         PIC X(40)  VALUE SPACES.                    
           05 WS-WRITE-DTE-TP       PIC X(08)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      *----------------------------------------------------------------*        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)        TO WS-CUR-DATE                
                                                     WS-PREV-MTH-DATE.          
           SUBTRACT 1                             FROM WS-MM                    
           MOVE     1                             TO   WS-DD                    
                                                                                
           PERFORM 1000-OPEN-FILES                THRU 1000-EXIT.               
                                                                                
           PERFORM 2000-READ-BP13F73A             THRU 2000-EXIT.               
           IF WS-F73A-READ  =  0                                                
              DISPLAY 'FILE BP13F73A IS EMPTY'                                  
           ELSE                                                                 
              PERFORM 4000-WRITE-EXEMPT-LISING    THRU 4000-EXIT                
                UNTIL WS-F73A-EOF = 'Y'                                         
           END-IF                                                               
                                                                                
           PERFORM 3000-READ-BP13F73B             THRU 3000-EXIT.               
           IF WS-F73B-READ  =  0                                                
              DISPLAY 'FILE BP13F73B IS EMPTY'                                  
           ELSE                                                                 
              PERFORM 5000-WRITE-NOEXEMPT-LISING  THRU 5000-EXIT                
                UNTIL WS-F73B-EOF = 'Y'                                         
           END-IF                                                               
                                                                                
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F73A                                                 
                       BP13F73B                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K410                                                 
                       BM06K110                                                 
                OUTPUT BP13L73A                                                 
                       BP13L73B                                                 
                       BP13L73C                                                 
                       BP13L73D.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 OPEN ERROR, STATUS : ' BP13K800-STATUS          
              MOVE     BP13K800-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 OPEN ERROR, STATUS : ' BP13K022-STATUS          
              MOVE     BP13K022-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K024 OPEN ERROR, STATUS : ' BP13K024-STATUS          
              MOVE     BP13K024-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K410-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K410 OPEN ERROR, STATUS : ' BP13K410-STATUS          
              MOVE     BP13K410-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BM06K110 OPEN ERROR, STATUS : ' BM06K110-STATUS          
              MOVE     BM06K110-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F73A.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F73A AT END                                                 
                MOVE 'Y' TO WS-F73A-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F73A-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN                                   
                                                                                
           EVALUATE BP13K022-STATUS                                             
           WHEN ZEROES                                                          
              MOVE 'Y'                       TO   WS-BP13K022-FND               
           WHEN 23                                                              
              MOVE 'N'                       TO   WS-BP13K022-FND               
           WHEN OTHER                                                           
              DISPLAY 'BP13K022 READ ERROR, STATUS IS '                         
                       BP13K022-STATUS                                          
              MOVE     BP13K022-STATUS       TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-CALL-BP13CCNT.                                                      
      *-------------------------------------------------------------            
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROES                                                          
              MOVE    'Y'                TO WS-BP13CCNT-FND                     
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                       K022-NUM-OIC    ' REGN : ' WS-NUM-REGN                   
              MOVE    'N'                TO WS-BP13CCNT-FND                     
           WHEN OTHER                                                           
              MOVE '98'                  TO RETURN-CODE                         
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN                                   
                                                                                
           EVALUATE BP13K800-STATUS                                             
           WHEN ZEROES                                                          
              MOVE 'Y'                       TO   WS-BP13K800-FND               
           WHEN 23                                                              
              MOVE 'N'                       TO   WS-BP13K800-FND               
           WHEN OTHER                                                           
              DISPLAY 'BP13K800 READ ERROR, STATUS IS '                         
                       BP13K800-STATUS                                          
              MOVE     BP13K800-STATUS       TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2400-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD                                    
                                                                                
           EVALUATE BM06K110-STATUS                                             
           WHEN ZEROES                                                          
              CONTINUE                                                          
           WHEN 23                                                              
              DISPLAY 'HDB REF NOT FOUND IN BM06K110, KEY IS '                  
                       BP13F73B-REC(1:30)                                       
      *CR9793                                                                   
      *       MOVE     BM06K110-STATUS       TO   RETURN-CODE                   
      *       PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           WHEN OTHER                                                           
              DISPLAY 'BM06K110 READ ERROR, STATUS IS '                         
                       BM06K110-STATUS                                          
              MOVE     BM06K110-STATUS       TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2500-READ-BP13K410.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE BP13K410-STATUS                                             
           WHEN ZEROES                                                          
              MOVE 'Y'                       TO   WS-BP13K410-FND               
           WHEN 23                                                              
              MOVE 'N'                       TO   WS-BP13K410-FND               
           WHEN OTHER                                                           
              DISPLAY 'BP13K410 READ ERROR, STATUS IS '                         
                       BP13K410-STATUS                                          
              MOVE     BP13K410-STATUS       TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F73B.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F73B AT END                                                 
                MOVE 'Y' TO WS-F73B-EOF                                         
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F73B-READ.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-EXEMPT-LISING.                                                
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4100-INITIALIZE       THRU 4100-EXIT.                        
           MOVE K73E-NUM-REGN            OF BP13F73A-REC                        
             TO L73A-NUM-REGN                                                   
                WS-NUM-REGN.                                                    
           MOVE K73E-NUM-NRIC            OF BP13F73A-REC                        
             TO L73A-NUM-NRIC.                                                  
           MOVE K73E-NUM-HDB-REF         OF BP13F73A-REC                        
             TO L73A-NUM-HDB-REF.                                               
                                                                                
           MOVE K73E-TXT-ADDR1-PTE       OF BP13F73A-REC                        
             TO WS-TXT-ADDR1.                                                   
           MOVE SPACES                   TO WS-TXT-ADDR1-PTE                    
                                            WS-TXT-ADDR1-OUT                    
           PERFORM 4200-UNSTRING-ADD1    THRU 4200-EXIT.                        
                                                                                
           IF WS-TXT-ADDR1-PART4 NOT = SPACE                                    
              STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE                     
                     ' '                 DELIMITED BY SIZE                      
                     WS-TXT-ADDR1-PART2  DELIMITED BY SPACE                     
                     ' '                 DELIMITED BY SIZE                      
                     WS-TXT-ADDR1-PART3  DELIMITED BY SPACE                     
                     ' '                 DELIMITED BY SIZE                      
                     WS-TXT-ADDR1-PART4  DELIMITED BY SPACE                     
                     '*'                 DELIMITED BY SIZE                      
                INTO WS-TXT-ADDR1-OUT                                           
              END-STRING                                                        
           ELSE                                                                 
              IF WS-TXT-ADDR1-PART3 NOT = SPACE                                 
                 STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE                  
                        ' '                 DELIMITED BY SIZE                   
                        WS-TXT-ADDR1-PART2  DELIMITED BY SPACE                  
                        ' '                 DELIMITED BY SIZE                   
                        WS-TXT-ADDR1-PART3  DELIMITED BY SPACE                  
                        '*'                 DELIMITED BY SIZE                   
                   INTO WS-TXT-ADDR1-OUT                                        
                 END-STRING                                                     
              ELSE                                                              
                 IF WS-TXT-ADDR1-PART2 NOT = SPACE                              
                    STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE               
                           ' '                 DELIMITED BY SIZE                
                           WS-TXT-ADDR1-PART2  DELIMITED BY SPACE               
                           '*'                 DELIMITED BY SIZE                
                      INTO WS-TXT-ADDR1-OUT                                     
                    END-STRING                                                  
                 ELSE                                                           
                    STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE               
                           '*'                 DELIMITED BY SIZE                
                      INTO WS-TXT-ADDR1-OUT                                     
                    END-STRING                                                  
                 END-IF                                                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF K73E-OVERSEAS-TAG OF BP13F73A-REC     = 'N'                       
             IF K73E-NUM-UNIT-PTE OF BP13F73A-REC                               
                                  = SPACES OR LOW-VALUES                        
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     WS-TXT-ADDR1-OUT       DELIMITED BY '*'                    
                     ' '                    DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' S('                  DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73A-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             ELSE                                                               
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     WS-TXT-ADDR1-OUT       DELIMITED BY '*'                    
                     ' '                    DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     '-'                    DELIMITED BY SIZE                   
                     K73E-NUM-UNIT-PTE      OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' S('                  DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73A-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             END-IF                                                             
           ELSE                                                                 
             IF K73E-NUM-UNIT-PTE OF BP13F73A-REC                               
                                  = SPACES OR LOW-VALUES                        
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     K73E-TXT-ADDR1-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-TXT-ADDR2-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     '('                    DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73A-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             ELSE                                                               
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     K73E-TXT-ADDR1-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-TXT-ADDR2-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     '-'                    DELIMITED BY SIZE                   
                     K73E-NUM-UNIT-PTE      OF BP13F73A-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     '('                    DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73A-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE WS-WRITE-PPO-ADDRESS     TO L73A-PPO-ADDRESS                    
           MOVE K73E-DTE-DISPOSE-DUE     OF BP13F73A-REC                        
             TO L73A-DTE-DISPOSE-DUE.                                           
                                                                                
           MOVE K73E-NUM-REGN            OF BP13F73A-REC                        
             TO K022-NUM-REGN                                                   
           PERFORM 2100-READ-BP13K022    THRU 2100-EXIT                         
           IF WS-BP13K022-FND = 'Y'                                             
              MOVE 'EML'                  TO   LINK-CNTCT-TYPE                  
              IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                       
                 MOVE K022-NUM-OIC        TO   LINK-STAFF-NICK                  
              ELSE                                                              
                 MOVE K022-NUM-OIC-APPL   TO   LINK-STAFF-NICK                  
              END-IF                                                            
              MOVE SPACES                 TO   LINK-STAFF-NUM                   
              PERFORM 2200-CALL-BP13CCNT  THRU 2200-EXIT                        
              IF WS-BP13CCNT-FND = 'Y'                                          
                 IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                    
                    MOVE LINK-STAFF-CNTCT TO   L73A-OIC-EMAIL-PREB              
                    MOVE SPACES           TO   L73A-OIC-EMAIL-POSB              
                 ELSE                                                           
                    MOVE LINK-STAFF-CNTCT TO   L73A-OIC-EMAIL-PREB              
                    MOVE 'EML'            TO   LINK-CNTCT-TYPE                  
                    MOVE K022-NUM-OIC     TO   LINK-STAFF-NICK                  
                    MOVE SPACES           TO   LINK-STAFF-NUM                   
                    PERFORM 2200-CALL-BP13CCNT THRU 2200-EXIT                   
                    IF WS-BP13CCNT-FND = 'Y'                                    
                       MOVE LINK-STAFF-CNTCT TO L73A-OIC-EMAIL-POSB             
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           WRITE BP13L73A-REC.                                                  
           ADD 1                          TO   WS-L73A-WRITE.                   
                                                                                
           PERFORM 2000-READ-BP13F73A    THRU 2000-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-INITIALIZE.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO   BP13L73A-REC                      
                                              BP13K022-REC                      
                                              BP13K800-MASTER                   
                                              K110-REC                          
                                              WS-WRITE-DETAILS                  
                                              WS-TXT-ADDR1                      
                                              WS-BP13CCNT-LINK                  
                                              WS-TXT-ADDR1-PTE.                 
           MOVE ' '                      TO   WS-BP13CCNT-FND                   
                                              WS-BP13K022-FND.                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-UNSTRING-ADD1.                                                      
      *-------------------------------------------------------------            
                                                                                
           UNSTRING WS-TXT-ADDR1       DELIMITED BY SPACE                       
               INTO WS-TXT-ADDR1-PART1 WS-TXT-ADDR1-PART2                       
                    WS-TXT-ADDR1-PART3 WS-TXT-ADDR1-PART4                       
           END-UNSTRING.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-NOEXEMPT-LISING.                                              
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACE                       TO   WS-LISTING-TAG.                
           PERFORM 4100-INITIALIZE          THRU 4100-EXIT.                     
           MOVE K73E-NUM-REGN               OF BP13F73B-REC                     
             TO K800-NUM-REGN.                                                  
           PERFORM 2300-READ-BP13K800       THRU 2300-EXIT.                     
                                                                                
           IF WS-BP13K800-FND = 'Y'                                             
              MOVE K800-NUM-SCH-ACC            TO   K110-KEY-FLD                
              PERFORM 2400-READ-BM06K110       THRU 2400-EXIT                   
                                                                                
              IF K800-DTE-SALE-AGMT = SPACES   OR   LOW-VALUES                  
                 IF K110-DTE-ACCEPTANCE        >=  '20160601' AND               
                    K110-DTE-ACCEPTANCE        <    WS-PREV-MTH-DATE            
                    MOVE 'B'                   TO   WS-LISTING-TAG              
                    PERFORM 5100-OUTPUT        THRU 5100-EXIT                   
                    WRITE BP13L73B-REC         FROM WS-WRITE-DETAILS            
                    ADD 1                      TO   WS-L73B-WRITE               
                 END-IF                                                         
              ELSE                                                              
                 IF K800-DTE-SALE-AGMT         <    WS-CUR-DATE                 
                    IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES               
                       MOVE 'C'                TO   WS-LISTING-TAG              
                       PERFORM 5100-OUTPUT     THRU 5100-EXIT                   
                       WRITE BP13L73C-REC      FROM WS-WRITE-DETAILS            
                       ADD 1                   TO   WS-L73C-WRITE               
                    ELSE                                                        
                       MOVE 'D'                TO   WS-LISTING-TAG              
                       MOVE K110-DTE-KEY-ISSUED                                 
                                               TO   WS-WRITE-DTE-TP             
                       PERFORM 5200-OUTPUT     THRU 5200-EXIT                   
                       WRITE BP13L73D-REC      FROM WS-WRITE-DETAILS            
                       ADD 1                   TO   WS-L73D-WRITE               
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE K73E-NUM-REGN               OF   BP13F73B-REC                
                TO K410-NUM-REGN                                                
              PERFORM 2500-READ-BP13K410       THRU 2500-EXIT                   
              IF WS-BP13K410-FND = 'Y'                                          
                 MOVE 'D'                      TO   WS-LISTING-TAG              
                 MOVE K410-DTE-KEY-ISSUE       TO   WS-WRITE-DTE-TP             
                 PERFORM 5200-OUTPUT           THRU 5200-EXIT                   
                 WRITE BP13L73D-REC            FROM WS-WRITE-DETAILS            
                 ADD 1                         TO   WS-L73D-WRITE               
              END-IF                                                            
           END-IF.                                                              
           PERFORM 3000-READ-BP13F73B          THRU 3000-EXIT.                  
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-OUTPUT.                                                             
      *-------------------------------------------------------------            
                                                                                
           MOVE K73E-NUM-REGN            OF BP13F73B-REC                        
             TO WS-WRITE-NUM-REGN                                               
                WS-NUM-REGN.                                                    
           MOVE K73E-NUM-NRIC            OF BP13F73B-REC                        
             TO WS-WRITE-NUM-NRIC.                                              
           MOVE K73E-NUM-HDB-REF         OF BP13F73B-REC                        
             TO WS-WRITE-NUM-HDB-REF.                                           
           MOVE K73E-TXT-ADDR1-PTE       OF BP13F73B-REC                        
             TO WS-TXT-ADDR1.                                                   
           MOVE SPACES                   TO WS-TXT-ADDR1-PTE                    
                                            WS-TXT-ADDR1-OUT                    
           PERFORM 4200-UNSTRING-ADD1    THRU 4200-EXIT.                        
                                                                                
           IF WS-TXT-ADDR1-PART4 NOT = SPACE                                    
              STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE                     
                     ' '                 DELIMITED BY SIZE                      
                     WS-TXT-ADDR1-PART2  DELIMITED BY SPACE                     
                     ' '                 DELIMITED BY SIZE                      
                     WS-TXT-ADDR1-PART3  DELIMITED BY SPACE                     
                     ' '                 DELIMITED BY SIZE                      
                     WS-TXT-ADDR1-PART4  DELIMITED BY SPACE                     
                     '*'                 DELIMITED BY SIZE                      
                INTO WS-TXT-ADDR1-OUT                                           
              END-STRING                                                        
           ELSE                                                                 
              IF WS-TXT-ADDR1-PART3 NOT = SPACE                                 
                 STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE                  
                        ' '                 DELIMITED BY SIZE                   
                        WS-TXT-ADDR1-PART2  DELIMITED BY SPACE                  
                        ' '                 DELIMITED BY SIZE                   
                        WS-TXT-ADDR1-PART3  DELIMITED BY SPACE                  
                        '*'                 DELIMITED BY SIZE                   
                   INTO WS-TXT-ADDR1-OUT                                        
                 END-STRING                                                     
              ELSE                                                              
                 IF WS-TXT-ADDR1-PART2 NOT = SPACE                              
                    STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE               
                           ' '                 DELIMITED BY SIZE                
                           WS-TXT-ADDR1-PART2  DELIMITED BY SPACE               
                           '*'                 DELIMITED BY SIZE                
                      INTO WS-TXT-ADDR1-OUT                                     
                    END-STRING                                                  
                 ELSE                                                           
                    STRING WS-TXT-ADDR1-PART1  DELIMITED BY SPACE               
                           '*'                 DELIMITED BY SIZE                
                      INTO WS-TXT-ADDR1-OUT                                     
                    END-STRING                                                  
                 END-IF                                                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF K73E-OVERSEAS-TAG OF BP13F73B-REC     = 'N'                       
             IF K73E-NUM-UNIT-PTE OF BP13F73B-REC                               
                                  = SPACES OR LOW-VALUES                        
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     WS-TXT-ADDR1-OUT       DELIMITED BY '*'                    
                     ' '                    DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' S('                  DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73B-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             ELSE                                                               
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     WS-TXT-ADDR1-OUT       DELIMITED BY '*'                    
                     ' '                    DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     '-'                    DELIMITED BY SIZE                   
                     K73E-NUM-UNIT-PTE      OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' S('                  DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73B-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             END-IF                                                             
           ELSE                                                                 
             IF K73E-NUM-UNIT-PTE OF BP13F73B-REC                               
                                  = SPACES OR LOW-VALUES                        
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     K73E-TXT-ADDR1-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-TXT-ADDR2-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' ('                   DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73B-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             ELSE                                                               
              STRING K73E-NUM-BLDNG-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' '                    DELIMITED BY SIZE                   
                     K73E-TXT-ADDR1-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-TXT-ADDR2-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SIZE                   
                     K73E-NUM-LEVEL-PTE     OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     '-'                    DELIMITED BY SIZE                   
                     K73E-NUM-UNIT-PTE      OF BP13F73B-REC                     
                                            DELIMITED BY SPACE                  
                     ' ('                   DELIMITED BY SIZE                   
                     K73E-NUM-POSTAL-PTE    OF BP13F73B-REC                     
                     ')'                    DELIMITED BY SIZE                   
                INTO WS-WRITE-PPO-ADDRESS                                       
              END-STRING                                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE K73E-DTE-DISPOSE-DUE     OF  BP13F73B-REC                       
             TO WS-WRITE-DTE-DISPOSE.                                           
                                                                                
           MOVE K73E-NUM-REGN            OF  BP13F73B-REC                       
             TO K022-NUM-REGN                                                   
           PERFORM 2100-READ-BP13K022        THRU 2100-EXIT                     
           IF WS-BP13K022-FND = 'Y'                                             
              MOVE 'EML'                     TO   LINK-CNTCT-TYPE               
              MOVE SPACES                    TO   LINK-STAFF-NUM                
              IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                       
                 MOVE K022-NUM-OIC           TO   LINK-STAFF-NICK               
              ELSE                                                              
                 MOVE K022-NUM-OIC-APPL      TO   LINK-STAFF-NICK               
              END-IF                                                            
              PERFORM 2200-CALL-BP13CCNT     THRU 2200-EXIT                     
              IF WS-BP13CCNT-FND   = 'Y'                                        
                 IF WS-LISTING-TAG = 'B'                                        
                    MOVE LINK-STAFF-CNTCT    TO   WS-WRITE-OIC-EMAIL            
                 ELSE                                                           
                    IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                 
                       MOVE SPACES           TO   WS-WRITE-OIC-EMAIL            
                    ELSE                                                        
                       MOVE 'EML'            TO   LINK-CNTCT-TYPE               
                       MOVE SPACES           TO   LINK-STAFF-NUM                
                       MOVE K022-NUM-OIC     TO   LINK-STAFF-NICK               
                       PERFORM 2200-CALL-BP13CCNT THRU 2200-EXIT                
                       IF WS-BP13CCNT-FND = 'Y'                                 
                          MOVE LINK-STAFF-CNTCT TO WS-WRITE-OIC-EMAIL           
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5200-OUTPUT.                                                             
      *-------------------------------------------------------------            
                                                                                
           MOVE K73E-NUM-REGN            OF BP13F73B-REC                        
             TO WS-WRITE-NUM-REGN                                               
                WS-NUM-REGN.                                                    
           MOVE K73E-NUM-NRIC            OF BP13F73B-REC                        
             TO WS-WRITE-NUM-NRIC.                                              
           MOVE K73E-TXT-NME-PPO         OF BP13F73B-REC                        
             TO WS-WRITE-NAME.                                                  
                                                                                
           MOVE K73E-NUM-REGN            OF BP13F73B-REC                        
             TO K022-NUM-REGN                                                   
           PERFORM 2100-READ-BP13K022        THRU 2100-EXIT                     
           IF WS-BP13K022-FND = 'Y'                                             
              MOVE 'EML'                     TO   LINK-CNTCT-TYPE               
              MOVE SPACES                    TO   LINK-STAFF-NUM                
              IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                       
                 MOVE K022-NUM-OIC           TO   LINK-STAFF-NICK               
                                                  WS-WRITE-OIC                  
              ELSE                                                              
                 MOVE K022-NUM-OIC-APPL      TO   LINK-STAFF-NICK               
                                                  WS-WRITE-OIC                  
              END-IF                                                            
              PERFORM 2200-CALL-BP13CCNT     THRU 2200-EXIT                     
              IF WS-BP13CCNT-FND   = 'Y'                                        
                 MOVE LINK-STAFF-CNTCT       TO   WS-WRITE-OIC-EMAIL            
              ELSE                                                              
                 IF K022-NUM-OIC   = SPACES OR LOW-VALUES                       
                    MOVE SPACES              TO   WS-WRITE-OIC-EMAIL            
                 ELSE                                                           
                    MOVE 'EML'               TO   LINK-CNTCT-TYPE               
                    MOVE SPACES              TO   LINK-STAFF-NUM                
                    MOVE K022-NUM-OIC        TO   LINK-STAFF-NICK               
                    PERFORM 2200-CALL-BP13CCNT THRU 2200-EXIT                   
                    IF WS-BP13CCNT-FND = 'Y'                                    
                       MOVE LINK-STAFF-CNTCT TO WS-WRITE-OIC-EMAIL              
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '---- BP13C995 CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BP13F73A   = '                         
                     WS-F73A-READ.                                              
           DISPLAY ' NO OF REC READ FROM BP13F73B   = '                         
                     WS-F73B-READ.                                              
           DISPLAY ' NO OF REC WRITTEN TO LISTING A = '                         
                     WS-L73A-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN TO LISTING B = '                         
                     WS-L73B-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN TO LISTING C = '                         
                     WS-L73C-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN TO LISTING D = '                         
                     WS-L73D-WRITE.                                             
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F73A                                                 
                       BP13F73B                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BM06K110                                                 
                       BP13L73A                                                 
                       BP13L73B                                                 
                       BP13L73C                                                 
                       BP13L73D.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
