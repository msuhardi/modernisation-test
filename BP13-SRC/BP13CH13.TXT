       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH13.                                                 
      *AUTHOR.        MICHAEL ARRIOLA.                                          
      *DATE-WRITTEN.  22/09/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                             
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: WRITE FH15 W/IN LIMIT CASES INTO F580          *            
      *             STARTING FROM MAY BTO, SO LONG AS INVITED, EVEN*            
      *             THOUGH NOT BOOK, SYSTEM WILL STILL DELETE ALL  *            
      *             PREVIOUS UNSUCC ATTEMPTS, SIMILAR TO BOOK CASE.*            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133432  MSD1  20080922  NEW PGM                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FH15  ASSIGN        TO BP13FH15.                          
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
           SELECT BP13F580  ASSIGN        TO BP13F580.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FH15                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH15.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F580                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F580.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-FILE-STATUS.                                                       
          05 BP13K800-STATUS       PIC 9(02) VALUE ZEROES.                      
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-FH15-READ          PIC 9(08) VALUE ZEROES.                      
          05 WS-K800-READ          PIC 9(08) VALUE ZEROES.                      
          05 WS-F580-WRITE         PIC 9(08) VALUE ZEROES.                      
          05 WS-LIMIT-CTR          PIC 9(08) VALUE ZEROES.                      
          05 WS-NLIMIT-CTR         PIC 9(08) VALUE ZEROES.                      
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-CURRENT-DATE       PIC X(08) VALUE SPACES.                      
          05 WS-CURRENT-TIME       PIC 9(08) VALUE ZEROES.                      
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-FH15-EOF           PIC X     VALUE 'N'.                         
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13FH15  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL WS-FH15-EOF  = 'Y'.                                         
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FH15                                                 
                       BP13K800                                                 
               OUTPUT  BP13F580.                                                
                                                                                
           IF BP13K800-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE     BP13K800-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-CURRENT-DATE.                 
           ACCEPT        WS-CURRENT-TIME  FROM TIME.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FH15.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FH15 AT END                                                 
                MOVE 'Y'         TO WS-FH15-EOF                                 
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-FH15-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF FH15-NUM-WITHIN-LIMIT = 'Y'                                       
              ADD                      1 TO WS-LIMIT-CTR                        
              PERFORM 4000-READ-BP13K800 THRU 4000-EXIT                         
              PERFORM 5000-POPULATE-F580 THRU 5000-EXIT                         
                                                                                
              WRITE BP13F580-REC                                                
              ADD                      1 TO WS-F580-WRITE                       
           ELSE                                                                 
              ADD                      1 TO WS-NLIMIT-CTR                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FH15    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES             TO BP13K800-MASTER.                          
           INITIALIZE                 BP13K800-MASTER.                          
                                                                                
           MOVE FH15-NUM-REGN      TO K800-NUM-REGN.                            
                                                                                
           READ BP13K800.                                                       
           EVALUATE BP13K800-STATUS                                             
             WHEN 00                                                            
                ADD 1 TO WS-K800-READ                                           
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K800'                             
                      ', K800-NUM-REGN : ' K800-NUM-REGN                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K800 READ ERROR,STATUS : ' BP13K800-STATUS         
                      ', K800-NUM-REGN : ' K800-NUM-REGN                        
                MOVE BP13K800-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-POPULATE-F580.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                  TO BP13F580-REC.                        
           INITIALIZE                      BP13F580-REC.                        
                                                                                
           MOVE FH15-NUM-REGN           TO F580-NUM-REGN.                       
           MOVE K800-NUM-SCH-ACC        TO F580-SCH-ACC.                        
           MOVE FH15-DTE-BK-APPMT       TO F580-DTE-ACC-REJ.                    
           MOVE 'AI'                    TO F580-NUM-ACC-REJ.                    
                                                                                
           MOVE K800-DTE-REGN           TO F580-DTE-REGN                        
           MOVE K800-DTE-BALLOT         TO F580-DTE-BALLOT                      
           MOVE K800-NUM-ALLO-CAT       TO F580-NUM-ALLO-CAT                    
           MOVE K800-NUM-FLAT-TYPE      TO F580-NUM-FLAT-TYPE                   
           MOVE K800-NUM-HOUSEHOLD      TO F580-NUM-HOUSEHOLD                   
           MOVE FH15-NUM-NT             TO F580-NUM-NT                          
           MOVE K800-NUM-NRIC1          TO F580-NUM-NRIC1                       
           MOVE K800-NUM-NRIC2          TO F580-NUM-NRIC2                       
           MOVE K800-NUM-NRIC3          TO F580-NUM-NRIC3                       
           MOVE K800-NUM-NRIC4          TO F580-NUM-NRIC4                       
           MOVE K800-NUM-ALLO-SCHEME    TO F580-NUM-ALLOC-SCHEME                
           MOVE K800-NUM-ALLOC-TAG      TO F580-NUM-ALLOC-TAG                   
                                                                                
           MOVE WS-CURRENT-DATE         TO F580-DTE-UPDATE                      
           MOVE 'BP13CH13'              TO F580-NUM-USERID.                     
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH13 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13FH15 : ' WS-FH15-READ.            
           DISPLAY 'NO OF RECS W/IN LIMIT = "N"    : ' WS-NLIMIT-CTR.           
           DISPLAY 'NO OF RECS W/IN LIMIT = "Y"    : ' WS-LIMIT-CTR.            
           DISPLAY 'NO OF RECS READ  FROM BP13K800 : ' WS-K800-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13F580 : ' WS-F580-WRITE.           
                                                                                
           CLOSE BP13FH15                                                       
                 BP13K800                                                       
                 BP13F580.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
