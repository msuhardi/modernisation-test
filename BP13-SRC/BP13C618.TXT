       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C618                                                     
      *AUTHOR. LSB.                                                             
      *DATE WRITTEN : 15/08/97                                                  
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT                          *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  PROGRAM TO MATCH THIS QTR TME-QUEUE (PIN)     *        
      *                  WITH PREVIOUS QTR TME-QUEUE, OUTPUT THE PIN   *        
      *                  BT MATCHING TWO FILES BY REGN-NO.             *        
      *                                                                *        
      *   INPUT  FILE :  1.  BP13F500   -  CURRENT QTR FILE            *        
      *                  2.  BP13F520   -  PREVIOUS QTR FILE           *        
      *                                                                *        
      *   OUTPUT FILE :  1. BP13F510                                   *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      * BP130348   LSB   970815  - NEW PGM                             *        
      * BP130652   NOR   990219  - EXPAND F500, F510 & F520 RECL FROM  *        
      *                            300 TO 500 CHARACTERS               *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13F510 ASSIGN TO BP13F510.                                  
                                                                                
           EJECT                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F520                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F500                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F510                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F510.                                                           
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
       01  WS-F520-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F510-OUTPUT          PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F520-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-UPD-NHH              PIC 9(8)    VALUE ZERO.                      
       01  WS-UPD-UPG              PIC 9(8)    VALUE ZERO.                      
       01  WS-NEWTEN-CNT           PIC 9(8)    VALUE ZERO.                      
       01  WS-PAGE                 PIC 9(3)    VALUE ZERO.                      
       01  WS-CNT                  PIC 9(2)    VALUE 99.                        
       01  WS-SERIAL               PIC 9(4)    VALUE ZERO.                      
       01  WS-CUR-DATE.                                                         
           05  WS-YY               PIC X(4)    VALUE SPACES.                    
           05  WS-MM               PIC X(2)    VALUE SPACES.                    
           05  WS-DD               PIC X(2)    VALUE SPACES.                    
                                                                                
       01  WS-QUEUE-TIME.                                                       
           05  WS-TIME             PIC 9(2)    VALUE ZEROS.                     
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD        PIC X(2)    VALUE SPACES.                    
           05  FILLER              PIC X       VALUE '/'.                       
           05  WS-EDITED-MM        PIC X(2)    VALUE SPACES.                    
           05  FILLER              PIC X       VALUE '/'.                       
           05  WS-EDITED-YY        PIC X(4)    VALUE SPACES.                    
                                                                                
       77  WS-EOF-F500             PIC X(3).                                    
           88  EOF-OF-F500                     VALUE 'YES'.                     
       77  WS-EOF-F520             PIC X(3).                                    
           88  EOF-OF-F520                     VALUE 'YES'.                     
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION.                                           
           PERFORM 200-READ-F500 THRU 200-EXIT.                                 
           PERFORM 300-READ-F520 THRU 300-EXIT.                                 
           PERFORM 100-PROCESS   THRU 100-EXIT                                  
              UNTIL F500-NUM-REGN = HIGH-VALUE                                  
               AND  F520-NUM-REGN = HIGH-VALUE.                                 
           PERFORM 900-FINALIZATION.                                            
       10-FINISH.                                                               
           STOP RUN.                                                            
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13F500                                                 
                       BP13F520                                                 
                OUTPUT BP13F510.                                                
           MOVE 'NO' TO WS-EOF-F500.                                            
           MOVE 'NO' TO WS-EOF-F520.                                            
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                    
           MOVE WS-YY TO WS-EDITED-YY.                                          
           MOVE WS-MM TO WS-EDITED-MM.                                          
           MOVE WS-DD TO WS-EDITED-DD.                                          
                                                                                
      *****************************************************************         
      *  MATCH REF                                                    *         
      *****************************************************************         
       100-PROCESS.                                                             
           IF F500-NUM-REGN   =   F520-NUM-REGN                                 
              PERFORM 110-MATCH-REF THRU 110-EXIT                               
              ADD 1                 TO   WS-MATCH                               
              PERFORM 200-READ-F500 THRU 200-EXIT                               
              PERFORM 300-READ-F520 THRU 300-EXIT                               
           ELSE                                                                 
           IF (F500-NUM-REGN   < F520-NUM-REGN)                                 
              ADD 1 TO WS-F500-NOTMATCH                                         
              ADD 1 TO WS-F510-OUTPUT                                           
              WRITE BP13F510-REC FROM BP13F500-REC                              
              PERFORM 200-READ-F500 THRU 200-EXIT                               
           ELSE                                                                 
              ADD 1 TO WS-F520-NOTMATCH                                         
              PERFORM 300-READ-F520 THRU 300-EXIT.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
      *  F500-TME-QUEUE-PREVQTR RE-DEFINE CDE-CHORT (COL 80)                    
      *                                                                         
       110-MATCH-REF.                                                           
                                                                                
           MOVE F520-TME-QUEUE  TO  F500-TME-QUEUE-PREVQTR.                     
           ADD 1 TO WS-F510-OUTPUT.                                             
           WRITE BP13F510-REC FROM BP13F500-REC.                                
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F500.                                                           
           READ BP13F500 AT END MOVE 'YES' TO WS-EOF-F500                       
                                MOVE HIGH-VALUE TO F500-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F500-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F520.                                                           
           READ BP13F520 AT END MOVE 'YES' TO WS-EOF-F520                       
                                MOVE HIGH-VALUE TO F520-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F520-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*-----------  BP13C618 ------------*'.                    
             DISPLAY ' F500-READ      ' WS-F500-READ.                           
             DISPLAY ' F520-READ      ' WS-F520-READ.                           
             DISPLAY ' MATCH          ' WS-MATCH.                               
             DISPLAY ' F500-NOT-MT    ' WS-F500-NOTMATCH.                       
             DISPLAY ' F520-NOT-MT    ' WS-F520-NOTMATCH.                       
             DISPLAY ' F510-OUTPUT    ' WS-F510-OUTPUT.                         
           CLOSE      BP13F500                                                  
                      BP13F520                                                  
                      BP13F510.                                                 
                                                                                
