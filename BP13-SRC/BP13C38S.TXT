      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38S.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  FEBRUARY 15, 2016                                         
      *===============================================================*         
      * OBJECTIVE  :  PRINT EXCEPTION REPORT FOR 'Y' ELIGIBLE RS CODES*         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F454                                     *         
      *               2. BP13K800                                     *         
      *               3. BP13K820                                     *         
      *               4. AB03K080                                     *         
      *               5. AB03K030                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L38S                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP135880 15/02/2016 PCL4 NEW PROGRAM.                         *         
      * BP136477 18/10/2016 RJB1 TO REPLACE KIM HUAT TO YEE MIEN      *         
      * BP138757 23/07/2021 KV8  TO REPLACE KATHIJAH TO SUZANNE       *         
      * BP139030 07/01/2022 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN          
      * BP139276 05/04/2024 KAC1 REPLACE LIM KOK CHUN BY              *         
      *                                  KATHIJAH_BEE_ALI_MOHAMED     *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F454  ASSIGN        TO BP13F454.                         
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
           SELECT  AB03K080  ASSIGN        TO AB03K080                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K080-KEY                          
                             FILE STATUS   IS WS-K080-STATUS.                   
                                                                                
           SELECT  AB03K030  ASSIGN        TO AB03K030                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K030-NUM-HDB-REF                  
                             FILE STATUS   IS WS-K030-STATUS.                   
                                                                                
           SELECT  BP13L38S  ASSIGN        TO BP13L38S.                         
           SELECT  MAILC38S  ASSIGN        TO MAILC38S.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F454                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F454.                                                           
                                                                                
       FD  AB03K080                                                             
           RECORD CONTAINS 50 CHARACTERS.                                       
       COPY AB03K080.                                                           
                                                                                
       FD  AB03K030                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
       COPY AB03K030.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13L38S                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 110 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L38S-REC               PIC X(110).                              
                                                                                
       FD   MAILC38S                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  110 CHARACTERS.                                    
       01   MAILC38S-REC               PIC X(110).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F454             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K820             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K080             PIC X(1)  VALUE 'N'.                     
           05  WS-K030-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K080-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-HA                   PIC 9(1)  VALUE ZEROES.                  
           05  WS-F454-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38S-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-L38X-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-F454-NUM-REGN         PIC X(8)  VALUE SPACES.                  
           05 WS-NUM-NRIC              PIC X(9)  VALUE SPACES.                  
           05 WS-NUM-RELN              PIC X(20) VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01  WS-FIX-VARS.                                                         
           05  WS-DOB-DATE.                                                     
               10  WS-DOB-YR                 PIC  9(04).                        
               10  WS-DOB-MMDD               PIC  9(04).                        
           05  WS-SYS-DATE2.                                                    
               10  WS-SYS-YR                 PIC  9(04).                        
               10  WS-SYS-MMDD               PIC  9(04).                        
           05  WS-SALES-TYPE                 PIC  X(02).                        
           05  WS-SMS-ADDR                   PIC  X(50).                        
           05  WS-HA-DOB                     PIC  X(08).                        
           05  WS-HA-AGE                     PIC  9(04).                        
                                                                                
       01  WS-ADDRESS.                                                          
           05 WS-BLK-NO                PIC X(05) VALUE SPACES.                  
           05 FILLER                   PIC X     VALUE SPACES.                  
           05 WS-FLAT-NO.                                                       
              10 FILLER                PIC X     VALUE '#'.                     
              10 WS-LVL-NO             PIC X(2)  VALUE SPACES.                  
              10 FILLER                PIC X     VALUE '-'.                     
              10 WS-UNIT-NO            PIC X(4)  VALUE SPACES.                  
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-STREET                PIC X(25) VALUE SPACES.                  
           05 WS-SINGAPORE             PIC X(01) VALUE 'S'.                     
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-OPEN                  PIC X(01) VALUE '('.                     
           05 WS-POSTAL-CODE           PIC X(6)  VALUE SPACES.                  
           05 WS-CLOSE                 PIC X(01) VALUE ')'.                     
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  L38S-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38S'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38S-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38S-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  L38S-HEADER02.                                                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(80)     VALUE                          
               'EXCEPTION REPORT FOR RSL STATUS VERIFICATION CHECK'.            
                                                                                
       01  L38S-HEADER03.                                                       
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE '  S/N'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'REGN NO '.                 
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'RSL STAT'.                 
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(50)  VALUE 'FLAT ADDRESS'.             
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'SALES TYPE'.               
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(16)  VALUE 'APPOINTMENT DATE'.         
           05  FILLER              PIC X(01)  VALUE SPACES.                     
                                                                                
       01  L38S-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38S-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38S-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  L38S-RSL-CODE       PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(04)        VALUE SPACES.               
           05  L38S-ADDRESS        PIC X(50)        VALUE SPACES.               
           05  FILLER              PIC X(05)        VALUE SPACES.               
           05  L38S-SALES-TYP      PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(05)        VALUE SPACES.               
           05  L38S-APPT-DATE      PIC X(16)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT4             PIC X(50)                               
      *        VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.           
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<TENG_SIEW_HONG@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT7             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT8             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT9             PIC X(50)                               
      *        VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                
               VALUE 'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT10            PIC X(50)                               
               VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT11            PIC X(50)                               
               VALUE 'TO:<TENG_SIEW_HONG@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT13            PIC X(80)                               
               VALUE 'SUBJECT: EXCEPTION REPORT FOR RSL STATUS'.                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01  WS-LINK-BP13C913.                                                    
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH        PIC X(4).                                
               10  LINK-NUM-ACC        PIC X(5).                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F454      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F454 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F454                                                 
                       BP13K820                                                 
                       BP13K800                                                 
                       AB03K080                                                 
                       AB03K030                                                 
              OUTPUT   BP13L38S                                                 
                       MAILC38S.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = 00 AND 97                                    
              DISPLAY 'AB03K030 - ERROR OPENING : ' WS-K030-STATUS              
              MOVE WS-K030-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K080-STATUS NOT = 00 AND 97                                    
              DISPLAY 'AB03K080 - ERROR OPENING : ' WS-K080-STATUS              
              MOVE WS-K080-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F454.                                                      
      ******************************************************************        
                                                                                
           READ BP13F454           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F454                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F454-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F454-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO BP13K800-MASTER                              
                    INITIALIZE     BP13K800-MASTER                              
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F454-DTE-APPT >= WS-SYSTEM-DATE                                   
              PERFORM 2100-READ-BP13K800        THRU 2100-EXIT                  
              PERFORM 3100-START-BP13K820       THRU 3100-EXIT                  
              PERFORM 4000-PROCESS-RECORDS      THRU 4000-EXIT                  
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F454           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-START-BP13K820.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13K820-REC.          
           INITIALIZE                                    BP13K820-REC.          
                                                                                
           MOVE SPACES                                TO WS-SMS-ADDR            
                                                         WS-SALES-TYPE.         
                                                                                
           MOVE 'N'                                   TO WS-EOF-K820.           
                                                                                
           MOVE F454-NUM-REGN                         TO K820-NUM-REGN.         
                                                                                
           START BP13K820 KEY = K820-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3200-READNEXT-BP13K820  THRU 3200-EXIT              
                    PERFORM 3300-PROCESS-BP13K820   THRU 3300-EXIT              
                            UNTIL WS-EOF-K820 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE SPACES               TO BP13K820-REC                   
                    INITIALIZE                   BP13K820-REC                   
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K820 : ' WS-K820-STATUS              
                          ' REGN : ' K820-NUM-REGN                              
                  PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READNEXT-BP13K820.                                                  
      ******************************************************************        
                                                                                
           READ BP13K820 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K820.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-PROCESS-BP13K820.                                                   
      ******************************************************************        
                                                                                
           IF K820-NUM-REGN = F454-NUM-REGN                                     
              PERFORM 3400-START-AB03K080    THRU 3400-EXIT                     
              PERFORM 3200-READNEXT-BP13K820 THRU 3200-EXIT                     
           ELSE                                                                 
              MOVE 'Y'                         TO WS-EOF-K820                   
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-START-AB03K080.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO AB03K080-REC.          
           INITIALIZE                                    AB03K080-REC.          
                                                                                
           MOVE 'N'                                   TO WS-EOF-K080.           
           MOVE K820-NUM-NRIC                         TO K080-NUM-UIN.          
                                                                                
           START AB03K080 KEY = K080-NUM-UIN.                                   
                                                                                
           EVALUATE WS-K080-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3500-READNEXT-AB03K080  THRU 3500-EXIT              
                    PERFORM 3600-PROCESS-AB03K080   THRU 3600-EXIT              
                            UNTIL WS-EOF-K080 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR AB03K080 : ' WS-K080-STATUS              
                          ' UIN : ' K080-NUM-UIN                                
                  PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-READNEXT-AB03K080.                                                  
      ******************************************************************        
                                                                                
           READ AB03K080 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K080.                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-PROCESS-AB03K080.                                                   
      ******************************************************************        
                                                                                
           IF K080-NUM-UIN = K820-NUM-NRIC                                      
              PERFORM 3700-READ-AB03K030     THRU 3700-EXIT                     
              PERFORM 3500-READNEXT-AB03K080 THRU 3500-EXIT                     
           ELSE                                                                 
             MOVE 'Y'                          TO WS-EOF-K080                   
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3700-READ-AB03K030.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO AB03K030-REC.                       
           INITIALIZE                       AB03K030-REC.                       
                                                                                
           MOVE K080-NUM-HDB-REF         TO K030-NUM-HDB-REF.                   
                                                                                
           READ AB03K030.                                                       
                                                                                
           EVALUATE WS-K030-STATUS                                              
               WHEN 00                                                          
                    PERFORM 7300-CALL-BP13C913 THRU 7300-EXIT                   
                    MOVE K030-CDE-SALES-TYP      TO WS-SALES-TYPE               
                                                                                
                    MOVE COMM8-NUM-BLK           TO WS-BLK-NO                   
                    MOVE COMM8-NUM-LEVEL         TO WS-LVL-NO                   
                    MOVE COMM8-NUM-UNIT-MAIN     TO WS-UNIT-NO                  
                    MOVE COMM8-NME-STREET        TO WS-STREET                   
                    MOVE COMM8-NUM-POSTAL-CODE   TO WS-POSTAL-CODE              
                    MOVE WS-ADDRESS              TO WS-SMS-ADDR                 
                                                                                
                    MOVE 'Y'                     TO WS-EOF-K080                 
                                                    WS-EOF-K820                 
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES TO AB03K030-REC                                 
                    INITIALIZE     AB03K030-REC                                 
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO L38S-DETAIL01                   
           INITIALIZE                           L38S-DETAIL01.                  
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L38S-SN.                           
           MOVE F454-NUM-REGN             TO L38S-REGN.                         
           MOVE F454-NUM-RSL-CODE         TO L38S-RSL-CODE.                     
           MOVE WS-SMS-ADDR               TO L38S-ADDRESS.                      
           MOVE WS-SALES-TYPE             TO L38S-SALES-TYP.                    
                                                                                
           STRING F454-DTE-APPT(7:2) '/'                                        
                  F454-DTE-APPT(5:2) '/'                                        
                  F454-DTE-APPT(1:4) ' '                                        
                  F454-TME-SLOT-HH   ':'                                        
                  F454-TME-SLOT-MM   ':'                                        
              DELIMITED BY SIZE INTO L38S-APPT-DATE                             
                                                                                
           PERFORM 6000-PRINT-REPORT            THRU 6000-EXIT.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC38S-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT7.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT8.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT9.                               
            WRITE MAILC38S-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILC38S-REC FROM WS-MAIL-RPT11.                              
                                                                                
            STRING 'SUBJECT: EXCEPTION REPORT FOR RSL STATUS - '                
                    WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                  
              DELIMITED BY SIZE INTO WS-MAIL-RPT13.                             
                                                                                
            WRITE MAILC38S-REC FROM WS-MAIL-RPT13.                              
                                                                                
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                    DELIMITED BY SIZE INTO WS-DATE-RPT.                         
                                                                                
            WRITE MAILC38S-REC FROM WS-DATE-RPT-DTL                             
                                                                                
            MOVE SPACES TO MAILC38S-REC.                                        
            WRITE MAILC38S-REC.                                                 
                                                                                
            WRITE MAILC38S-REC FROM L38S-HEADER01.                              
            WRITE MAILC38S-REC FROM L38S-HEADER02.                              
                                                                                
            MOVE SPACES TO MAILC38S-REC.                                        
            WRITE MAILC38S-REC.                                                 
                                                                                
            WRITE MAILC38S-REC FROM L38S-HEADER03.                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           WRITE BP13L38S-REC               FROM L38S-DETAIL01.                 
           WRITE MAILC38S-REC               FROM L38S-DETAIL01.                 
           ADD 1                              TO WS-CNT-L38S-WRITE.             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L38S-DATE-HDR.                             
                                                                                
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38S-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38S-PAGE-HDR.                
                                                                                
           WRITE BP13L38S-REC    FROM L38S-HEADER01 AFTER PAGE.                 
           WRITE BP13L38S-REC    FROM L38S-HEADER02.                            
           WRITE BP13L38S-REC    FROM L38S-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
           PERFORM 5000-HEADER-EMAIL           THRU 5000-EXIT.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       7300-CALL-BP13C913.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO BP13COMM8-REC.                  
           INITIALIZE                           BP13COMM8-REC.                  
                                                                                
           MOVE K030-NUM-HDB-REF             TO LINK-NUM-SCH-ACC                
                                                                                
           CALL 'BP13C913' USING WS-LINK-BP13C913,                              
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       7300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38S'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F454 RECORDS READ............. : '            
                    WS-F454-READ.                                               
           DISPLAY '(2) NO OF BP13L38S RECORDS WRITTEN.......... : '            
                    WS-CNT-L38S-WRITE.                                          
                                                                                
           CLOSE    BP13F454                                                    
                    BP13K800                                                    
                    AB03K080                                                    
                    AB03K030                                                    
                    BP13K820                                                    
                    BP13L38S                                                    
                    MAILC38S.                                                   
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : ' WS-K820-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K080-STATUS NOT = 00 AND 97                                    
              DISPLAY 'AB03K080 - ERROR CLOSING : ' WS-K080-STATUS              
              MOVE WS-K080-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = 00 AND 97                                    
              DISPLAY 'AB03K030 - ERROR CLOSING : ' WS-K030-STATUS              
              MOVE WS-K030-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
