      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C391.                                                 
       AUTHOR.        M.PRINCE.                                                 
       DATE-WRITTEN.  07/12/98.                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  PROGRAM MATCH FIS WITH SPC OCCUPIER BY    *        
      *                      REGN TO EXTRACT BOOKED FOR THE YEAR THE   *        
      *                      DATE TO BE SENT TO  MOE                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      * BP130604    MPA   071298   NEW PROGRAM                         *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820  ASSIGN        TO BP13F820.                          
           SELECT BP13F216  ASSIGN        TO BP13F216.                          
           SELECT P13F216A  ASSIGN        TO P13F216A.                          
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F820                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13F820.                                                           
                                                                                
       FD   BP13F216                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13F216.                                                           
                                                                                
                                                                                
       FD   P13F216A                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       01  P13F216A-REC.                                                        
           05  FILLER                    PIC X(100).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F820-READ-CNT             PIC 9(6)  VALUE 0.                      
       01  WS-F216-READ-CNT             PIC 9(6)  VALUE 0.                      
       01  WS-MATCH-CNT                 PIC 9(6)  VALUE 0.                      
       01  WS-WRITE-CNT                 PIC 9(6)  VALUE 0.                      
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM  100-OPEN-FILES        THRU    100-EXIT.                     
           PERFORM  200-READ-BP13F820     THRU    200-EXIT.                     
           PERFORM  300-READ-BP13F216     THRU    300-EXIT.                     
           PERFORM  400-PROCESS           THRU    400-EXIT                      
            UNTIL   F820-NUM-REGN = HIGH-VALUES                                 
             AND    F216-NUM-REGN = HIGH-VALUES.                                
           PERFORM  1000-CLOSE-ROUTINE    THRU    1000-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT  BP13F820                                                 
                       BP13F216                                                 
                OUTPUT P13F216A.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *============================================================*            
      *                                                            *            
      *    1. READ FILE BP13F820.                                  *            
      *    2. READ FILE BP13F216.                                  *            
      *    3. MATCH BY REGN NO.                                    *            
      *    4. OUTPUT NRIC, POSTAL CODE, ECD                        *            
      *                                                            *            
      *============================================================*            
                                                                                
      ******************************************************************        
       200-READ-BP13F820.                                                       
      ******************************************************************        
      *--------------------------------------------------------*                
      *    READ BP13F820 UNTIL EOF                             *                
      *--------------------------------------------------------*                
           READ BP13F820                                                        
                AT END MOVE HIGH-VALUES TO F820-NUM-REGN                        
                       GO TO 200-EXIT.                                          
           ADD  1                   TO WS-F820-READ-CNT.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-READ-BP13F216.                                                       
      ******************************************************************        
      *--------------------------------------------------------*                
      *    READ BP13F216 UNTIL EOF                             *                
      *--------------------------------------------------------*                
           READ BP13F216                                                        
                AT END MOVE HIGH-VALUES TO F216-NUM-REGN                        
                       GO TO 300-EXIT.                                          
           ADD  1                   TO WS-F216-READ-CNT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-PROCESS.                                                             
      ******************************************************************        
           IF F820-NUM-REGN = F216-NUM-REGN                                     
              PERFORM 500-OUTPUT-F820 THRU 500-EXIT                             
               UNTIL F820-NUM-REGN NOT = F216-NUM-REGN                          
           ELSE                                                                 
             IF F820-NUM-REGN < F216-NUM-REGN                                   
                PERFORM  200-READ-BP13F820     THRU    200-EXIT                 
             ELSE                                                               
                IF F820-NUM-REGN > F216-NUM-REGN                                
                   PERFORM  300-READ-BP13F216  THRU    300-EXIT.                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-OUTPUT-F820.                                                         
      ******************************************************************        
                                                                                
           MOVE F820-NUM-NRIC            TO F216-NUM-NRIC.                      
           MOVE SPACES                   TO P13F216A-REC.                       
           MOVE BP13F216-REC             TO P13F216A-REC.                       
           WRITE P13F216A-REC.                                                  
           ADD   1                       TO WS-WRITE-CNT.                       
           PERFORM  200-READ-BP13F820    THRU    200-EXIT.                      
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           CLOSE BP13F820                                                       
                 BP13F216                                                       
                 P13F216A.                                                      
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C391 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM               (BP13F820) = '        
                    WS-F820-READ-CNT.                                           
           DISPLAY 'NO OF RECORDS READ FROM               (BP13F216) = '        
                    WS-F216-READ-CNT.                                           
           DISPLAY 'NO OF RECORD  WRITTEN                 (P13F216A) = '        
                    WS-WRITE-CNT.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
