      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C418.                                                 
      *AUTHOR.        TMK.                                                      
      *DATE-WRITTEN.  27/10/89.                                                 
                                                                                
      **********************************************************                
      *    OBJECTIVE  :                                        *                
      *        1.  TO ACCUMULATE THE DAILY VALID SALES         *                
      *            ORDER TO ACCUMULATIVE SALES ORDER FILE      *                
      *            AND TO PRINT THE LISTINGS IF THE RECORD     *                
      *            FOUND IN ACCUM SALE ORDER FILE WHICH MEANS  *                
      *            THE LESSEE BOUGHT ANOTHER NEW FLAT.         *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13F410  :  VALIDATED SALES ORDER            *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *       1. BP13K410  :  ACCUMULATED SALES ORDER          *                
      *                                                        *                
      *     LISTING:                                           *                
      *       1. LIST OF SALES ORDER RAISED FOR THE DAY BUT THE*                
      *          REGN-NO FOUND IN CUMMULATIVE SALE ORDER FILE. *                
      *                                                        *                
      *CHG-NO  BY   ON      DESCRIPTION                        *                
      *--------------------------------                        *                
      *C910736 LSB 910730 - TO COMPUTE 20% PREMIUM             *                
      *C930993 LJL 940216 - TO CATER FOR GST FOR SURVEY FEE.   *                
      *BP130181 LJL 960910  COLLECTION OF 15%, 20%, 25%,       *                
      *                         30% & 40% PREMIUM              *                
      *BP130418 RHB 980609  Y2K                                *                
      *BP132660 JF4 050315  TO GET PREMIUM FROM BP13K410       *                
      **********************************************************                
                                                                                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ACCESS MODE  IS DYNAMIC                                       
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K410-KEY-FLD                                  
                  FILE STATUS  IS K410-STATUS.                                  
           SELECT BP13L418 ASSIGN TO BP13L418.                                  
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD  BP13F410         BLOCK  CONTAINS   0 RECORDS                         
                            RECORD CONTAINS 2000 CHARACTERS                     
                            LABEL  RECORDS  ARE STANDARD.                       
       COPY BP13F410.                                                           
                                                                                
       FD  BP13K410         BLOCK  CONTAINS   0 RECORDS                         
                            RECORD CONTAINS 2000 CHARACTERS                     
                            LABEL  RECORDS  ARE STANDARD.                       
       COPY BP13K410.                                                           
                                                                                
       FD  BP13L418                                                             
           RECORD CONTAINS 133 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L418-PRT-REC                PIC X(133).                              
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-FILE-STATUS.                                                      
           05  K410-STATUS         PIC 99      VALUE ZEROS.                     
               88  K410-REC-FOUND  VALUE ZEROS.                                 
               88  K410-REC-NOTFND VALUE 23.                                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-WRITE        PIC 9(5)    VALUE ZERO.                      
           05  WS-CNT-REWRITE      PIC 9(5)    VALUE ZERO.                      
           05  WS-CNT-READ         PIC 9(5)    VALUE ZERO.                      
           05  WS-CNT-M-ORD        PIC 9(5)    VALUE ZERO.                      
           05  WS-CNT-PAGE         PIC 9(5)    VALUE ZERO.                      
           05  WS-TOT-STP-FL-PR    PIC 9(7)V99 VALUE ZEROS.                     
           05  WS-TOT-MTHLY-PAY    PIC 9(5)V99 VALUE ZEROS.                     
           05  WS-SELLING-PRICE    PIC 9(8)    VALUE ZEROS.                     
                                                                                
       01  WS-SWITCHES.                                                         
           05  EOF-F410-IND        PIC X       VALUE SPACES.                    
               88 EOF-F410         VALUE 'Y'.                                   
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-DD               PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-MM               PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-CC               PIC 99.                                      
           05  WS-YY               PIC 99.                                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CC               PIC 99.                                      
           05  WS-YY               PIC 99.                                      
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  L418-PR-HDG-A.                                                       
           03  FILLER              PIC X(35)   VALUE SPACES.                    
           03  FILLER              PIC X(60)   VALUE                            
               'SALES ORDER RAISED FOR ADVANCE ALLOCATION FLAT'.                
                                                                                
       01  L418-PR-HDG-B.                                                       
           03  FILLER              PIC X(45)   VALUE SPACES.                    
           03  FILLER              PIC X(60)   VALUE                            
               'TAKE POSSESSION FOR SOC FLAT'.                                  
                                                                                
       01  L418-PR-HDG-1.                                                       
           05  PR-LIST-ID          PIC X(8).                                    
           05  FILLER              PIC X(6)    VALUE SPACES.                    
           05  FILLER              PIC X(25)   VALUE 'HDB3'.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(25)   VALUE SPACES.                    
           05  FILLER              PIC X(6)    VALUE 'DATE: '.                  
           05  PR-CURR-DATE        PIC X(10).                                   
           05  FILLER              PIC X(08)   VALUE '  PAGE: '.                
           05  PR-PAGE-NO          PIC ZZZZ9    VALUE ZEROS.                    
                                                                                
       01  L418-PR-HDG-2.                                                       
           05  FILLER              PIC X(41)   VALUE SPACES.                    
           05  FILLER              PIC X(35)   VALUE                            
               'LIST OF MANUALLY RAISED SALES ORDER'.                           
           05  FILLER              PIC X(16)   VALUE ' FOR SALES TYPE '.        
           05  L418-HD3-SALES-TYPE PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(09)   VALUE SPACES.                    
           05  FILLER              PIC X(28)   VALUE                            
               'USER: AGREEMENT TEAM (SALES)'.                                  
                                                                                
       01  L418-PR-DTL-1.                                                       
           05  FILLER              PIC X(22)   VALUE                            
               'LOAN SCHEME         : '.                                        
           05  PR-LOAN-SCH         PIC X.                                       
           05  FILLER              PIC X(9)    VALUE '   MTF : '.               
           05  PR-MTF-JB-CD        PIC X.                                       
           05  FILLER              PIC X(14)   VALUE '   INT-RATE : '.          
           05  PR-INT-RATE         PIC X.                                       
           05  FILLER              PIC X(10)   VALUE '   BOND : '.              
           05  PR-PD-BOND          PIC 99.                                      
           05  FILLER              PIC X(18)   VALUE                            
               'YRS      REGN   : '.                                            
           05  PR-REGN-NO          PIC X(8).                                    
           05  FILLER              PIC X(21)  VALUE                             
               '   NT              : '.                                         
           05  PR-ALLOC-NT         PIC X(3).                                    
                                                                                
       01  L418-PR-DTL-2.                                                       
           05  FILLER              PIC X(22)   VALUE                            
               'EFF-DATE-SALE       : '.                                        
           05  PR-CC-DAY           PIC X(10).                                   
           05  FILLER              PIC X(15)   VALUE                            
               ' SCH-ACCT-NO : '.                                               
           05  PR-SCHEME           PIC 9(4).                                    
           05  FILLER              PIC X       VALUE '-'.                       
           05  PR-ACC              PIC 9(4).                                    
           05  FILLER              PIC X       VALUE '-'.                       
           05  PR-CHK              PIC 9.                                       
           05  FILLER              PIC X       VALUE '-'.                       
           05  PR-ACC-TYPE         PIC 99.                                      
           05  FILLER              PIC X(11)   VALUE '  PERIOD : '.             
           05  PR-REPAYMENT-PERIOD PIC 9(2).                                    
           05  FILLER              PIC X(12)   VALUE 'YRS         '.            
                                                                                
       01  L418-PR-DTL-3.                                                       
           05  FILLER              PIC X(22)   VALUE                            
               'NAME-APPLT-1        : '.                                        
           05  PR-NAME1            PIC X(60).                                   
           05  FILLER              PIC X(15)   VALUE '     NRIC-1  : '.         
           05  PR-NRIC1            PIC X(9).                                    
           05  FILLER              PIC X(13)   VALUE '    CPF-1  : '.           
           05  PR-CPF1             PIC X(9).                                    
                                                                                
       01  L418-PR-DTL-4.                                                       
           05  FILLER              PIC X(22)   VALUE                            
               'NAME-APPLT-2        : '.                                        
           05  PR-NAME2            PIC X(60).                                   
           05  FILLER              PIC X(15)   VALUE '     NRIC-2  : '.         
           05  PR-NRIC2            PIC X(9).                                    
           05  FILLER              PIC X(13)   VALUE '    CPF-2  : '.           
           05  PR-CPF2             PIC X(9).                                    
                                                                                
       01  L418-PR-DTL-5.                                                       
           05  FILLER              PIC X(22)   VALUE                            
               'NAME-APPLT-3        : '.                                        
           05  PR-NAME3            PIC X(60).                                   
           05  FILLER              PIC X(15)   VALUE '     NRIC-3  : '.         
           05  PR-NRIC3            PIC X(9).                                    
           05  FILLER              PIC X(13)   VALUE '    CPF-3  : '.           
           05  PR-CPF3             PIC X(9).                                    
                                                                                
       01  L418-PR-DTL-6.                                                       
           05  FILLER              PIC X(22)   VALUE                            
               'NAME-APPLT-4        : '.                                        
           05  PR-NAME4            PIC X(60).                                   
           05  FILLER              PIC X(15)   VALUE '     NRIC-4  : '.         
           05  PR-NRIC4            PIC X(9).                                    
           05  FILLER              PIC X(13)   VALUE '    CPF-4  : '.           
           05  PR-CPF4             PIC X(9).                                    
                                                                                
       01  L418-PR-DTL-7.                                                       
           05  FILLER              PIC X(22)   VALUE                            
               'SELLING-PRICE       : '.                                        
           05  PR-SELLING-PRICE    PIC $Z,ZZZ,ZZ9.99.                           
           05  FILLER              PIC X(15)   VALUE                            
              '     CD-CASH : '.                                                
           05  PR-SOC-CASH         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '    MTHLY-INSTAL    : '.                                        
           05  PR-MTHLY-LOAN-INST  PIC $Z,ZZ9.99.                               
           05  FILLER              PIC X(21)   VALUE                            
               '     1ST-INST-CASH : '.                                         
           05  PR-FIRST-INST-CASH  PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-8.                                                       
           05  FILLER              PIC X(25)   VALUE                            
               'PREMIUM-STP-FEE     :    '.                                     
           05  PR-PREM-STP-FEE     PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(15)   VALUE                            
               '        CPF1 : '.                                               
           05  PR-SOC-CPF1         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '    SC-CC-CHARGE    : '.                                        
           05  PR-SC-CC            PIC $Z,ZZ9.99.                               
           05  FILLER              PIC X(21)   VALUE                            
               '              CPF1 : '.                                         
           05  PR-1ST-INST-CPF1    PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-9.                                                       
           05  FILLER              PIC X(25)   VALUE                            
               'LEASE-IN-ESCROW-FEE :    '.                                     
           05  PR-ESCROW-FEE       PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(15)   VALUE                            
               '        CPF2 : '.                                               
           05  PR-SOC-CPF2         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '    TOT-MTHLY-PAYMT : '.                                        
           05  PR-TOT-MTHLY-PAYMT  PIC $Z,ZZ9.99.                               
           05  FILLER              PIC X(21)   VALUE                            
               '              CPF2 : '.                                         
           05  PR-1ST-INST-CPF2    PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-10.                                                      
           05  FILLER              PIC X(25)   VALUE                            
               'LEASE-REG/MRO-FEE   :    '.                                     
           05  PR-LEASE-REGN-FEE   PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(15)   VALUE                            
               '        CPF3 : '.                                               
           05  PR-SOC-CPF3         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '    MTHLY-DED-CASH  : '.                                        
           05  PR-MTHLY-DED-CASH   PIC $Z,ZZ9.99.                               
           05  FILLER              PIC X(21)   VALUE                            
               '              CPF3 : '.                                         
           05  PR-1ST-INST-CPF3    PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-11.                                                      
           05  FILLER              PIC X(25)   VALUE                            
               'MORTGAGE-STP-FEE    :    '.                                     
           05  PR-MORT-STAMP-FEE   PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(15)   VALUE                            
               '        CPF4 : '.                                               
           05  PR-SOC-CPF4         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '              CPF1  : '.                                        
           05  PR-MTHLY-DED-CPF1   PIC $Z,ZZ9.99.                               
           05  FILLER              PIC X(21)   VALUE                            
               '              CPF4 : '.                                         
           05  PR-1ST-INST-CPF4    PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-12.                                                      
           05  FILLER              PIC X(25)   VALUE                            
               'MORT-REG/MRO FEE    :    '.                                     
           05  PR-MORT-REGN-FEE    PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(15)   VALUE                            
               '    ICP-CASH : '.                                               
           05  PR-ICP-CASH         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '              CPF2  : '.                                        
           05  PR-MTHLY-DED-CPF2   PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-13.                                                      
           05  FILLER              PIC X(13)   VALUE                            
               'SURVEY-FEE : '.                                                 
           05  PR-SURVEY-FEE-GST   PIC $ZZ9.99.                                 
           05  FILLER              PIC X(6)   VALUE 'GST + '.                   
           05  PR-SURVEY-FEE       PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(14)   VALUE                            
               '       CPF1 : '.                                                
           05  PR-ICP-CPF1         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '              CPF3  : '.                                        
           05  PR-MTHLY-DED-CPF3   PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-14.                                                      
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'CPF2 : '.                 
           05  PR-ICP-CPF2         PIC $ZZ,ZZ9.99.                              
           05  FILLER              PIC X(22)   VALUE                            
               '              CPF4  : '.                                        
           05  PR-MTHLY-DED-CPF4   PIC $Z,ZZ9.99.                               
                                                                                
       01  L418-PR-DTL-15.                                                      
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'CPF3 : '.                 
           05  PR-ICP-CPF3         PIC $ZZ,ZZ9.99.                              
                                                                                
       01  L418-PR-DTL-16.                                                      
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'CPF4 : '.                 
           05  PR-ICP-CPF4         PIC $ZZ,ZZ9.99.                              
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
      *-----------------*                                                       
       010-OPEN-ROUTINE.                                                        
      *-----------------*                                                       
           OPEN INPUT BP13F410                                                  
                I-O   BP13K410                                                  
               OUTPUT BP13L418.                                                 
                                                                                
           IF K410-STATUS NOT = 0 AND 97                                        
              DISPLAY ' OPENING ERROR, K410-STATUS '                            
                        K410-STATUS                                             
              MOVE K410-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE.                                        
                                                                                
                                                                                
      *-------------------*                                                     
       020-PROCESS-ROUTINE.                                                     
      *-------------------*                                                     
           PERFORM 100-PROCESS-INDATA THRU 100-EXIT                             
                    UNTIL EOF-F410.                                             
                                                                                
      *-----------------*                                                       
       999-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
           CLOSE BP13F410                                                       
                 BP13K410                                                       
                 BP13L418.                                                      
                                                                                
           IF K410-STATUS NOT = 0 AND 97                                        
              DISPLAY ' CLOSING ERROR, K410-STATUS ' K410-STATUS                
              MOVE K410-STATUS TO RETURN-CODE.                                  
                                                                                
           DISPLAY ' ** BP13C418 ** '.                                          
           DISPLAY ' NO. OF RECORDS READ FROM F410  = '                         
                     WS-CNT-READ.                                               
           DISPLAY ' NO. OF RECORDS WRITTEN TO K410 = '                         
                     WS-CNT-WRITE.                                              
           DISPLAY ' NO. OF RECORDS UPDATED TO K410 = '                         
                     WS-CNT-REWRITE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ***********************************************************               
      *  1.  READ F410 FILE AT END EXIT                         *               
      *  2.  READ K410 FILE USING CUR-REG                       *               
      *  3.  IF RECORD FOUND                                    *               
      *         PRINT OUT K410 DETAILS BEFORE UPDATE            *               
      *         OVERWRITE K410 WITH DETAILS FROM F410           *               
      *      ELSE                                               *               
      *      IF RECORD NOT FOUND                                *               
      *         WRITE TO K410 WITH DETAILS FROM F410            *               
      *      ELSE                                               *               
      *         ERROR                                           *               
      ***********************************************************               
      *------------------*                                                      
       100-PROCESS-INDATA.                                                      
      *------------------*                                                      
           READ BP13F410 AT END MOVE 'Y' TO EOF-F410-IND                        
                GO TO 100-EXIT.                                                 
                                                                                
           ADD 1 TO WS-CNT-READ.                                                
                                                                                
           MOVE F410-NUM-REGN TO K410-NUM-REGN.                                 
           READ BP13K410.                                                       
           IF K410-REC-FOUND                                                    
              PERFORM 300-PRINT-REWRITE-BP13K410 THRU 300-EXIT                  
              MOVE BP13F410-REC TO BP13K410-REC                                 
              REWRITE BP13K410-REC                                              
              ADD 1 TO WS-CNT-REWRITE                                           
           ELSE                                                                 
           IF K410-REC-NOTFND                                                   
              MOVE BP13F410-REC TO BP13K410-REC                                 
              WRITE BP13K410-REC                                                
              ADD 1 TO WS-CNT-WRITE                                             
              IF K410-STATUS = ZEROES OR 02                                     
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 DISPLAY ' ERROR WRITING, BP13K410 ' K410-STATUS                
                 MOVE K410-STATUS TO RETURN-CODE                                
                 PERFORM 999-CLOSE-ROUTINE                                      
           ELSE                                                                 
              DISPLAY ' ERROR READING, BP13K410 ' K410-STATUS                   
              MOVE K410-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE.                                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *          K410-DTE-KEY-ISSUE NOT = SPACES AND ZEROES    *                
      *          PRINT LISTING                                 *                
      *                                                        *                
      **********************************************************                
      *--------------------------*                                              
       300-PRINT-REWRITE-BP13K410.                                              
      *--------------------------*                                              
           IF K410-DTE-KEY-ISSUE = ZEROS OR SPACES OR LOW-VALUES                
              GO TO 300-EXIT                                                    
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           MOVE K410-CDE-SALES-TYPE  TO   L418-HD3-SALES-TYPE.                  
           PERFORM 400-PRINT-HEADING THRU 400-EXIT.                             
           PERFORM 500-PRINT-DETAIL  THRU 500-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *       PRINT LIST OF MANUALLY RAISED SALES ORDERS       *                
      *                                                        *                
      **********************************************************                
      *------------------*                                                      
       400-PRINT-HEADING.                                                       
      *------------------*                                                      
            ADD 1                      TO WS-CNT-PAGE.                          
            MOVE 'BP13L418'            TO PR-LIST-ID.                           
            MOVE WS-CNT-PAGE           TO PR-PAGE-NO.                           
            MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                          
            MOVE CORR WS-CUR-DATE      TO WS-EDITED-DATE.                       
            MOVE WS-EDITED-DATE        TO PR-CURR-DATE.                         
            WRITE L418-PRT-REC FROM L418-PR-HDG-1 AFTER PAGE.                   
            WRITE L418-PRT-REC FROM L418-PR-HDG-2 AFTER 2.                      
            WRITE L418-PRT-REC FROM L418-PR-HDG-B AFTER 2.                      
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-PRINT-DETAIL.                                                        
      *------------------*                                                      
            MOVE  ZEROS             TO WS-SELLING-PRICE.                        
            MOVE  SPACES            TO L418-PRT-REC.                            
            WRITE L418-PRT-REC      AFTER ADVANCING 3.                          
            ADD   1                 TO WS-CNT-M-ORD.                            
            MOVE  K410-CDE-LOAN-SCH TO PR-LOAN-SCH.                             
                                                                                
            IF K410-CDE-ALLOC-SCH = 'MT ' OR 'MTS'                              
                MOVE 'Y' TO PR-MTF-JB-CD                                        
            ELSE                                                                
                MOVE 'N' TO PR-MTF-JB-CD.                                       
                                                                                
            MOVE K410-CDE-INTEREST-RATE TO PR-INT-RATE.                         
            MOVE K410-NUM-BOND-PERIOD   TO PR-PD-BOND.                          
            MOVE K410-NUM-REGN          TO PR-REGN-NO.                          
            MOVE K410-CDE-NT            TO PR-ALLOC-NT.                         
                                                                                
            MOVE SPACES                 TO L418-PRT-REC.                        
            WRITE L418-PRT-REC FROM L418-PR-DTL-1 AFTER ADVANCING 3.            
                                                                                
            MOVE K410-DTE-SO-EFFECTIVE  TO PR-CC-DAY.                           
            MOVE K410-SCHEME            TO PR-SCHEME.                           
            MOVE K410-ACC               TO PR-ACC.                              
            MOVE K410-CHK               TO PR-CHK.                              
            MOVE K410-LESSEE-CODE       TO PR-ACC-TYPE.                         
            MOVE K410-NUM-REPAY-PERIOD  TO PR-REPAYMENT-PERIOD.                 
                                                                                
            MOVE SPACES                 TO L418-PRT-REC.                        
            WRITE L418-PRT-REC FROM L418-PR-DTL-2 AFTER ADVANCING 2.            
                                                                                
            MOVE K410-NAME1             TO PR-NAME1.                            
            MOVE K410-NRIC1             TO PR-NRIC1.                            
            MOVE K410-CPF1              TO PR-CPF1.                             
            MOVE SPACES                 TO L418-PRT-REC.                        
            WRITE L418-PRT-REC FROM L418-PR-DTL-3 AFTER ADVANCING 2.            
                                                                                
            MOVE K410-NAME2             TO PR-NAME2.                            
            MOVE K410-NRIC2             TO PR-NRIC2.                            
            MOVE K410-CPF2              TO PR-CPF2.                             
            MOVE SPACES                 TO L418-PRT-REC.                        
            WRITE L418-PRT-REC FROM L418-PR-DTL-4 AFTER ADVANCING 2.            
                                                                                
            MOVE K410-NAME3               TO PR-NAME3.                          
            MOVE K410-NRIC3               TO PR-NRIC3.                          
            MOVE K410-CPF3                TO PR-CPF3.                           
            MOVE SPACES                   TO L418-PRT-REC.                      
            WRITE L418-PRT-REC FROM L418-PR-DTL-5 AFTER ADVANCING 2.            
                                                                                
            MOVE K410-NAME4               TO PR-NAME4.                          
            MOVE K410-NRIC4               TO PR-NRIC4.                          
            MOVE K410-CPF4                TO PR-CPF4.                           
            MOVE SPACES                   TO L418-PRT-REC.                      
            WRITE L418-PRT-REC FROM L418-PR-DTL-6 AFTER ADVANCING 2.            
                                                                                
            IF K410-AMT-PREMIUM-NEW NOT NUMERIC                                 
               MOVE ZEROES    TO K410-AMT-PREMIUM-NEW                           
            END-IF.                                                             
                                                                                
            COMPUTE WS-SELLING-PRICE = K410-AMT-SELLING-PRICE +                 
                                       K410-AMT-PREMIUM-NEW.                    
                                                                                
            MOVE WS-SELLING-PRICE           TO  PR-SELLING-PRICE.               
                                                                                
            MOVE K410-AMT-CD-CASH           TO  PR-SOC-CASH.                    
            MOVE K410-AMT-LOAN-INSTAL       TO  PR-MTHLY-LOAN-INST.             
            MOVE K410-AMT-FIRST-CASH        TO  PR-FIRST-INST-CASH.             
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-7 AFTER ADVANCING 2.            
                                                                                
            MOVE K410-AMT-PREMIUM-STAMP-FEE TO  PR-PREM-STP-FEE.                
            MOVE K410-AMT-CD-CPF1           TO  PR-SOC-CPF1.                    
            MOVE K410-AMT-SC-CC             TO  PR-SC-CC.                       
            MOVE K410-AMT-FIRST-CPF1        TO  PR-1ST-INST-CPF1.               
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-8 AFTER ADVANCING 2.            
                                                                                
            MOVE K410-AMT-LEASE-ESCROW-FEE  TO  PR-ESCROW-FEE.                  
            MOVE K410-AMT-CD-CPF2           TO  PR-SOC-CPF2.                    
                                                                                
            COMPUTE WS-TOT-MTHLY-PAY = K410-AMT-LOAN-INSTAL +                   
                                       K410-AMT-SC-CC.                          
                                                                                
            MOVE WS-TOT-MTHLY-PAY           TO  PR-TOT-MTHLY-PAYMT.             
            MOVE K410-AMT-FIRST-CPF2        TO  PR-1ST-INST-CPF2.               
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-9 AFTER ADVANCING 2.            
                                                                                
            MOVE K410-AMT-LEASE-REGN-FEE    TO  PR-LEASE-REGN-FEE.              
            MOVE K410-AMT-CD-CPF3           TO  PR-SOC-CPF3.                    
            MOVE K410-AMT-MTHLY-CASH        TO  PR-MTHLY-DED-CASH.              
            MOVE K410-AMT-FIRST-CPF3        TO  PR-1ST-INST-CPF3.               
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-10 AFTER ADVANCING 2.           
                                                                                
            MOVE K410-AMT-MORT-STAMP-FEE    TO  PR-MORT-STAMP-FEE.              
            MOVE K410-AMT-CD-CPF4           TO  PR-SOC-CPF4.                    
            MOVE K410-AMT-MTHLY-CPF1        TO  PR-MTHLY-DED-CPF1.              
            MOVE K410-AMT-FIRST-CPF4        TO  PR-1ST-INST-CPF4.               
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-11 AFTER ADVANCING 2.           
                                                                                
            MOVE K410-AMT-MORT-REGN-FEE     TO  PR-MORT-REGN-FEE.               
            MOVE K410-AMT-ICP-CASH          TO  PR-ICP-CASH.                    
            MOVE K410-AMT-MTHLY-CPF2        TO  PR-MTHLY-DED-CPF2.              
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-12 AFTER ADVANCING 2.           
                                                                                
            MOVE K410-AMT-SURVEY-FEE        TO  PR-SURVEY-FEE.                  
            IF   K410-AMT-SURVEY-FEE IS NOT NUMERIC                             
                 MOVE ZEROS                 TO  PR-SURVEY-FEE-GST               
                 ELSE                                                           
               MOVE K410-AMT-SURVEY-FEE-GST TO  PR-SURVEY-FEE-GST.              
            MOVE K410-AMT-ICP-CPF1          TO  PR-ICP-CPF1.                    
            MOVE K410-AMT-MTHLY-CPF3        TO  PR-MTHLY-DED-CPF3.              
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-13 AFTER ADVANCING 2.           
                                                                                
            MOVE K410-AMT-ICP-CPF2          TO  PR-ICP-CPF2.                    
            MOVE K410-AMT-MTHLY-CPF4        TO  PR-MTHLY-DED-CPF4.              
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-14 AFTER ADVANCING 2.           
                                                                                
            MOVE K410-AMT-ICP-CPF3          TO  PR-ICP-CPF3.                    
            MOVE SPACES                     TO  L418-PRT-REC.                   
            WRITE L418-PRT-REC FROM L418-PR-DTL-15 AFTER ADVANCING 2.           
                                                                                
            MOVE K410-AMT-ICP-CPF4          TO  PR-ICP-CPF4.                    
            WRITE L418-PRT-REC FROM L418-PR-DTL-16 AFTER ADVANCING 2.           
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
