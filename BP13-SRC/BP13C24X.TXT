       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24X.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  23/09/14                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  MATCHING PROGRAM TO FILTER THE BP13F205     *            
      *                                                            *            
      *  INPUT FILES :  1)  BP13F205                               *            
      *                 2)  BM06K100                               *            
      *                 3)  BB14K500                               *            
      *                                                            *            
      *  OUTPUT FILE :  1)  P13F205B                               *            
      * ---------------------------------------------------------- *            
      * CHGE NO   OIC   DATE    DESCRIPTION                        *            
      * -------- ----- ------   ---------------------------------- *            
      * BP135377 KAM4  160914   NEW PROGRAM.                       *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN        TO  BP13F205.                         
                                                                                
           SELECT BM06K100  ASSIGN        TO  BM06K100                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K100-KEY-FLD                      
                            FILE STATUS   IS  WS-K100-STATUS.                   
                                                                                
           SELECT BB14K500  ASSIGN        TO  BB14K500                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K500-KEY-FLD                      
                            FILE STATUS   IS  WS-K500-STATUS.                   
                                                                                
           SELECT P13F205B  ASSIGN        TO  P13F205B.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       01 WS-F205TEMP-REC.                                                      
          05 WS-F205-EST                  PIC X(03).                            
          05 WS-F205-BLK-NO               PIC X(05).                            
          05 FILLER                       PIC X(05).                            
          05 WS-F205-OPT                  PIC X(02).                            
          05 WS-F205-NEIGH                PIC X(04).                            
          05 WS-F205-CONTR                PIC X(04).                            
          05 FILLER                       PIC X(07).                            
          05 WS-F205-DTE-BLDG-CMPLT       PIC X(08).                            
          05 FILLER                       PIC X(42).                            
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       FD   P13F205B                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       01  P13F205B-REC                   PIC X(80).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F205                   PIC X     VALUE 'N'.               
           05  WS-K100-STATUS                PIC 99    VALUE  00.               
           05  WS-K500-STATUS                PIC 99    VALUE  00.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-TOTAL-CNT             PIC 9(5)  VALUE ZEROS.             
           05  WS-WRITE-TOT-CNT              PIC 9(5)  VALUE ZEROS.             
                                                                                
       01 OTHER-VARIABLES.                                                      
           05 WS-TEMP                        PIC X(08) VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      **************************************************************            
       000-MAIN-ROUTINE.                                                        
      **************************************************************            
           PERFORM 100-OPEN-FILES            THRU 100-EXIT.                     
           PERFORM 200-READ-BP13F205         THRU 200-EXIT.                     
           PERFORM 300-PROCESS-RECORD        THRU 300-EXIT                      
                   UNTIL WS-EOF-F205 = 'Y'.                                     
                                                                                
           PERFORM  9999-CLOSE-ROUTINE       THRU 9999-EXIT.                    
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *------------------------------------------------------------*            
           OPEN INPUT  BP13F205                                                 
                       BM06K100                                                 
                       BB14K500                                                 
               OUTPUT  P13F205B.                                                
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - OPEN ERROR :' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS           TO RETURN-CODE                  
              PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-K500-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BB14K500 - OPEN ERROR :' WS-K500-STATUS                  
              MOVE     WS-K500-STATUS           TO RETURN-CODE                  
              PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT                  
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y'                 TO WS-EOF-F205                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1                                TO WS-READ-TOTAL-CNT.           
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *------------------------------------------------------------*            
           MOVE SPACES                         TO WS-F205-DTE-BLDG-CMPLT        
                                                   K100-KEY-FLD.                
           MOVE WS-F205-EST                     TO K100-ESTATE.                 
           MOVE WS-F205-NEIGH                   TO K100-NEIGHBOURHOOD.          
           MOVE WS-F205-CONTR                   TO K100-CONTRACT-NO.            
           MOVE WS-F205-BLK-NO                  TO K100-BLK-NO.                 
                                                                                
           PERFORM 400-READ-K100-K500         THRU 400-EXIT.                    
           IF (K100-DTE-KEY-AVAIL = ZERO OR SPACES OR LOW-VALUES) AND           
              (K500-DTE-BLDG-COMPLETE  NOT = ZERO AND SPACES                    
                                              AND LOW-VALUES)                   
      *        MOVE K500-DTE-BLDG-COMPLETE    TO WS-F205-DTE-BLDG-CMPLT         
               MOVE SPACES                    TO P13F205B-REC                   
               MOVE WS-F205TEMP-REC           TO P13F205B-REC                   
               WRITE P13F205B-REC                                               
               ADD   1                        TO WS-WRITE-TOT-CNT               
               DISPLAY 'WRITE REC TO FILTERED F205:' K100-KEY-FLD               
              DISPLAY ' '                                                       
           ELSE                                                                 
              DISPLAY ' '                                                       
              DISPLAY 'KEY NOT PROCESS (' K100-KEY-FLD ') DUE TO :'             
              DISPLAY 'K100-DTE-KEY-AVAIL IS NOT ZEROES/BLANK'                  
                      '(' K100-DTE-KEY-AVAIL ')'                                
              DISPLAY '             OR                       '                  
              DISPLAY 'K500-DTE-BLDG-COMPLETE IS ZEROES/BLANK'                  
                      '(' K500-DTE-BLDG-COMPLETE ')'                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F205          THRU 200-EXIT.                    
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-READ-K100-K500.                                                      
      *------------------------------------------------------------*            
           READ BM06K100.                                                       
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
                    DISPLAY '+++ RECORD FOUND IN BM06K100 +++'                  
                    PERFORM 500-READ-BB14K500  THRU 500-EXIT                    
               WHEN 23                                                          
                    MOVE SPACES                  TO K100-DTE-KEY-AVAIL          
                    DISPLAY 'K100 KEY NTFND IN K100 (' K100-KEY-FLD ')'         
               WHEN OTHER                                                       
                    DISPLAY 'BM06K100 - ERROR ERROR : ' WS-K100-STATUS          
                    PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                  
           END-EVALUATE.                                                        
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-READ-BB14K500.                                                       
      *------------------------------------------------------------*            
           MOVE LOW-VALUES                    TO K500-KEY-FLD.                  
           MOVE K100-CDE-BLK                  TO K500-CDE-BLK.                  
                                                                                
           READ BB14K500.                                                       
           EVALUATE WS-K500-STATUS                                              
               WHEN 00                                                          
                  DISPLAY '+++ RECORD FOUND IN BB14K500 +++'                    
                                                                                
               WHEN 23                                                          
                  MOVE SPACES               TO K500-DTE-BLDG-COMPLETE           
                  DISPLAY 'CDE-BLK NOTFND IN K500 (' K100-CDE-BLK ')'           
               WHEN OTHER                                                       
                    DISPLAY 'BB14K500 - READ ERROR   : ' WS-K500-STATUS         
                    PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                  
           END-EVALUATE.                                                        
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C24X SUMMARY --------------*'.             
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F205 READ        = ' WS-READ-TOTAL-CNT.           
           DISPLAY 'NO OF FILTERED F205        = ' WS-WRITE-TOT-CNT.            
                                                                                
           CLOSE BP13F205                                                       
                 BM06K100                                                       
                 BB14K500                                                       
                 P13F205B.                                                      
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BM06K100 CLOSE ERROR: ' WS-K100-STATUS                    
             MOVE  WS-K100-STATUS      TO       RETURN-CODE.                    
                                                                                
           IF WS-K500-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BB14K500 CLOSE ERROR: ' WS-K500-STATUS                    
             MOVE  WS-K500-STATUS      TO       RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
