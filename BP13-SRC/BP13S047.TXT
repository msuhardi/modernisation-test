  /****************************************************************/            
  /* SERS SYSTEM (BP13)                                           */            
  /*==============================================================*/            
  /* OBJECTIVES :                                                 */            
  /*     TO PRINT REPORT ON REASON FOR CANCELLATION OF BTO        */            
  /*     PINNACLE@DUXTON                                          */            
  /*--------------------------------------------------------------*/            
  /* CHG NO   DATE     OIC  DESCRIPTION                           */            
  /* -------- -------- ---- ------------------------------------- */            
  /* BP132558 06072004 PGS1 NEW PROGRAM                           */            
  /* BP133340 17072008 JB8  DISPLAY NT,FT,HH                      */            
  /****************************************************************/            
  OPTIONS  NODATE;                                                              
   %GLOBAL DATE;                                                                
                                                                                
   PROC FORMAT;                                                                 
                                                                                
   VALUE $HHFMT  "1"="FT"                                                       
                 "2"="2T"                                                       
                 ;                                                              
                                                                                
   VALUE $RMFMT  "1 ","1A"="1R"                                                 
                 "2 ","2A"="2R"                                                 
                 "3 "="3R"                                                      
                 "4 ","4D","4S"="4R"                                            
                 "5 ","5D"="5R"                                                 
                 "6 ","E ","ED"="EF";                                           
                                                                                
                                                                                
   DATA  SY02F001;                                                              
      INFILE SY02F001;                                                          
      INPUT @01  CURRENT  YYMMDD8.;                                             
                                                                                
   CALL SYMPUT ('DATE',PUT(CURRENT,DDMMYY10.));                                 
                                                                                
   DATA BP13F595;                                                               
      INFILE BP13F595;                                                          
      INPUT @1   RANDOM   $CHAR28.                                              
            @116 HH       $CHAR1.                                               
            @187 NT       $CHAR3.                                               
            @211 FT       $CHAR2.                                               
            @258 CODE_TAG $CHAR2.;                                              
                 CODE='36';                                                     
                 CODE1=CODE || CODE_TAG;                                        
                                                                                
     /* READING BP13K816 TO DECODE NT-CODE */                                   
           LENGTH K816_KEY $3;                                                  
           K816_KEY = NT;                                                       
           INFILE BP13K816 VSAM KEY = K816_KEY;                                 
           INPUT @;                                                             
           RC = _IORC_;                                                         
           IF RC = 0 THEN                                                       
              DO;                                                               
                 INPUT @41 DEC_FT  $CHAR1.                                      
                       @90 DEC_NT  $CHAR3.;                                     
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                 DEC_FT = " ";                                                  
                 DEC_NT = NT;                                                   
              END;                                                              
                                                                                
   LENGTH NTFT $4.;                                                             
   NTFT = DEC_NT||DEC_FT;                                                       
                                                                                
   IF HH="H" OR HH="T" THEN CDE_HH="1";                                         
   IF HH="G" OR HH="3" THEN CDE_HH="2";                                         
                                                                                
   IF FT="1 " THEN CDE_FT="1";                                                  
   IF FT="2 " THEN CDE_FT="2";                                                  
   IF FT="3 " THEN CDE_FT="3";                                                  
   IF FT="4 " THEN CDE_FT="4";                                                  
   IF FT="5 " THEN CDE_FT="5";                                                  
   IF FT="6 " THEN CDE_FT="6";                                                  
                                                                                
   PROC SORT;                                                                   
      BY CODE1;                                                                 
                                                                                
   DATA BP13F060;                                                               
      INFILE BP13F060;                                                          
      INPUT @01  CODE1    $CHAR4.                                               
            @06  DESP     $CHAR20.;                                             
                                                                                
   PROC SORT;                                                                   
      BY CODE1;                                                                 
                                                                                
   DATA F595F060;                                                               
   MERGE BP13F595(IN=A) BP13F060(IN=B);                                         
      BY CODE1;                                                                 
                                                                                
      IF A;                                                                     
      IF DESP='     '  THEN DESP=CODE_TAG;                                      
                                                                                
   PROC SORT; BY DESP;                                                          
                                                                                
   /*---------------------------------------------------------------*/          
   /*TABULATE ON CANCELATION CASES FOR BTO CASES FOR PINNACLE@DUXTON*/          
   /*---------------------------------------------------------------*/          
   OPTIONS MISSING=0 PAGENO=1;                                                  
   PROC PRINTTO FILE=BP13L047 NEW;                                              
   PROC TABULATE DATA=F595F060 MISSING FORMAT=6.;                               
TITLE1 "BP13L047   HDB3                            SYSTEM OF COMMITEMENT        
                DATE :   &DATE      PAGE : ";                                   
TITLE2 'REASON FOR CANCELATION OF BTO FOR PINNACLE@DUXTON ';                    
                                                                                
   CLASS  DESP NTFT CDE_HH CDE_FT;                                              
                                                                                
   FORMAT CDE_HH   $HHFMT.                                                      
          CDE_FT   $RMFMT.;                                                     
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
  TABLE  NTFT="NEW TOWN :  " ,                                                  
         DESP=" " ALL,                                                          
         (CDE_HH=" " ALL) *                                                     
         (CDE_FT=" " ALL)                                                       
         /RTS=30 BOX="       CANCEL REASON";                                    
                                                                                
