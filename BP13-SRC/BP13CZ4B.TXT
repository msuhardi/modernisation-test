       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ4B.                                                 
      *AUTHOR.        SMP3.                                                     
      *DATE-WRITTEN.  11/02/2021.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE   :  UPDATE THE RANDOM NUMBER OF CCA RECORDS     *            
      *                                                            *            
      * INPUT FILE  :  BP13LZ4A  RECORDS TO BE PROCESSED           *            
      * I-O FILE    :  BP13K740  BP13.K740.MATUREBA                *            
      * OUTPUT FILE :  BP13LZ4B  REPORT ON THE UPDATED RECORDS     *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP138600  SMP3  11022021  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13LZ4A ASSIGN        TO BP13LZ4A.                           
                                                                                
           SELECT BP13K740 ASSIGN        TO BP13K740                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS DYNAMIC                             
                           RECORD KEY    IS K740-KEY-FLD                        
                           ALTERNATE KEY IS K740-ALT1-KEY                       
                           FILE STATUS   IS WS-K740-STATUS.                     
                                                                                
           SELECT BP13LZ4B ASSIGN        TO BP13LZ4B.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13LZ4A                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS  50 CHARACTERS.                                     
       01   BP13LZ4A-REC.                                                       
            05  LZ4A-NUM-REGN             PIC X(08).                            
            05  LZ4A-NUM-RANDOM           PIC X(28).                            
            05  FILLER                    PIC X(14).                            
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       FD   BP13LZ4B                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13LZ4B-REC                  PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-LZ4A-READ               PIC 9(08) VALUE ZEROES.               
           05  WS-K741-FND                PIC 9(08) VALUE ZEROES.               
           05  WS-K741-NTFND              PIC 9(08) VALUE ZEROES.               
           05  WS-K740-FND                PIC 9(08) VALUE ZEROES.               
           05  WS-K740-NTFND              PIC 9(08) VALUE ZEROES.               
           05  WS-K740-WRITE              PIC 9(08) VALUE ZEROES.               
           05  WS-K740-DELETE             PIC 9(08) VALUE ZEROES.               
           05  WS-LZ4B-WRITE              PIC 9(08) VALUE ZEROES.               
           05  WS-LZ4B-SNO                PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-LZ4A-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-LZ4B-HDR                PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY            PIC 9(4).                             
               10  WS-SYS-MM              PIC 9(2).                             
               10  WS-SYS-DD              PIC 9(2).                             
           05  WS-EDITED-DATE             PIC X(10).                            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-JB-REGNO                PIC X(08) VALUE SPACES.               
           05  WS-RANDNO-ORIG             PIC X(28) VALUE SPACES.               
           05  WS-DTE-UPDATE              PIC X(10) VALUE SPACES.               
                                                                                
      *============================================================*            
      *         BP13LZ4B  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  LZ4B-HEADER-01.                                                      
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'BP13LZ4B'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(43)     VALUE                          
              'UPDATE ON RANDOM NUMBER OF CCA APPLICATIONS'.                    
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LZ4B-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
                                                                                
       01  LZ4B-HEADER-02.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE '  S/N '.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGN NO '.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'PAIR REGN NO'.          
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(28)     VALUE                          
              'ORIGINAL RANDOM NUMBER      '.                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(28)     VALUE                          
              'UPDATED RANDOM NUMBER       '.                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE 'DATE OF UPDATE'.        
                                                                                
       01  LZ4B-BLANK.                                                          
           05  FILLER              PIC X(132)    VALUE ALL SPACES.              
                                                                                
       01  LZ4B-DETAIL.                                                         
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LZ4B-SNO            PIC ZZZZZ9.                                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ4B-REGNO          PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ4B-JB-REGNO       PIC X(08).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  LZ4B-RANDNO-ORIG    PIC X(28).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ4B-RANDNO-UPD     PIC X(28).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LZ4B-DTE-UPDATE     PIC X(10).                                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13LZ4A       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-BP13LZ4A    THRU 3000-EXIT                      
             UNTIL WS-LZ4A-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13LZ4A                                                 
                  I-O  BP13K740                                                 
                OUTPUT BP13LZ4B.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K740, STATUS: ' WS-K740-STATUS         
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-SYSTEM-DATE.                  
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE INTO WS-EDITED-DATE.                        
           MOVE WS-EDITED-DATE              TO LZ4B-RUN-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13LZ4A.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13LZ4A AT END                                                 
                MOVE 'Y'                    TO WS-LZ4A-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-LZ4A-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-BP13LZ4A.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-READ-BP13K741       THRU 4000-EXIT.                     
           PERFORM 2000-READ-BP13LZ4A       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-READ-BP13K741.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                      TO BP13K740-REC.                    
           INITIALIZE                          BP13K740-REC.                    
                                                                                
           MOVE LZ4A-NUM-REGN               TO K740-ALT1-KEY.                   
                                                                                
           READ BP13K740 KEY IS K740-ALT1-KEY.                                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K741-FND                      
                MOVE K740-NUM-RANDOM        TO WS-RANDNO-ORIG                   
                MOVE K740-NUM-JB-REGN       TO WS-JB-REGNO                      
                STRING K740-DTE-UPDATE(7:2) '/'                                 
                       K740-DTE-UPDATE(5:2) '/'                                 
                       K740-DTE-UPDATE(1:4)                                     
                       DELIMITED BY SIZE INTO WS-DTE-UPDATE                     
                PERFORM 5000-WRITE-BP13K740 THRU 5000-EXIT                      
                                                                                
           WHEN 23                                                              
                ADD 1                       TO WS-K741-NTFND                    
                DISPLAY 'RECORD NOT FOUND IN BP13K741: ' K740-ALT1-KEY          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K741, STATUS: '                      
                         WS-K740-STATUS                                         
                MOVE WS-K740-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-BP13K740.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE LZ4A-NUM-RANDOM             TO K740-NUM-RANDOM                  
           MOVE 'C'                         TO K740-NUM-RANDOM(28:1)            
                                                                                
           WRITE BP13K740-REC.                                                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
                ADD 1                       TO WS-K740-WRITE                    
                PERFORM 6000-READ-BP13K740  THRU 6000-EXIT                      
                                                                                
           WHEN 22                                                              
                DISPLAY 'DUPLICATE RECORD IN BP13K740: ' K740-KEY-FLD           
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR WRITING TO BP13K740, STATUS: '                   
                         WS-K740-STATUS                                         
                MOVE WS-K740-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       6000-READ-BP13K740.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                      TO BP13K740-REC.                    
           INITIALIZE                          BP13K740-REC.                    
                                                                                
           MOVE WS-RANDNO-ORIG              TO K740-KEY-FLD.                    
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-K740-FND                      
                PERFORM 7000-DEL-BP13K740   THRU 7000-EXIT                      
                PERFORM 8000-WRITE-BP13LZ4B THRU 8000-EXIT                      
                                                                                
           WHEN 23                                                              
                ADD 1                       TO WS-K740-NTFND                    
                DISPLAY 'RECORD NOT FOUND IN BP13K740' K740-KEY-FLD             
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K740, STATUS: '                      
                         WS-K740-STATUS                                         
                MOVE WS-K740-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-DEL-BP13K740.                                                       
      *-------------------------------------------------------------            
                                                                                
           DELETE BP13K740.                                                     
                                                                                
           IF WS-K740-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR DELETING BP13K740, STATUS: ' WS-K740-STATUS        
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           ADD 1                            TO WS-K740-DELETE.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-WRITE-BP13LZ4B.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO  BP13LZ4B-REC.                   
                                                                                
           IF WS-LZ4B-HDR = 'N'                                                 
              MOVE 'Y'                      TO WS-LZ4B-HDR                      
              WRITE BP13LZ4B-REC            FROM LZ4B-HEADER-01                 
              WRITE BP13LZ4B-REC            FROM LZ4B-BLANK                     
              WRITE BP13LZ4B-REC            FROM LZ4B-BLANK                     
              WRITE BP13LZ4B-REC            FROM LZ4B-HEADER-02                 
           END-IF.                                                              
                                                                                
           MOVE SPACES                      TO LZ4B-DETAIL.                     
           INITIALIZE                          LZ4B-DETAIL.                     
                                                                                
           ADD 1                            TO WS-LZ4B-SNO.                     
           MOVE WS-LZ4B-SNO                 TO LZ4B-SNO.                        
           MOVE LZ4A-NUM-REGN               TO LZ4B-REGNO.                      
           MOVE WS-JB-REGNO                 TO LZ4B-JB-REGNO.                   
           MOVE WS-RANDNO-ORIG              TO LZ4B-RANDNO-ORIG.                
           MOVE LZ4A-NUM-RANDOM             TO LZ4B-RANDNO-UPD.                 
           MOVE WS-DTE-UPDATE               TO LZ4B-DTE-UPDATE.                 
                                                                                
           WRITE BP13LZ4B-REC               FROM LZ4B-DETAIL.                   
           ADD 1                            TO WS-LZ4B-WRITE.                   
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '========================================'.                  
           DISPLAY '     PROGRAM : BP13CZ4B  ' WS-EDITED-DATE.                  
           DISPLAY '========================================'.                  
           DISPLAY ' '.                                                         
           DISPLAY ' 1. BP13LZ4A RECORDS READ    : ' WS-LZ4A-READ.              
           DISPLAY ' 2. BP13K741 RECORDS FOUND   : ' WS-K741-FND.               
           DISPLAY ' 3. BP13K741 RECORDS NOT FND : ' WS-K741-NTFND.             
           DISPLAY ' 4. BP13K740 RECORDS WRITTEN : ' WS-K740-WRITE.             
           DISPLAY ' 5. BP13K740 RECORDS FOUND   : ' WS-K740-FND.               
           DISPLAY ' 6. BP13K740 RECORDS NOT FND : ' WS-K740-NTFND.             
           DISPLAY ' 7. BP13K740 RECORDS DELETED : ' WS-K740-DELETE.            
           DISPLAY ' 8. BP13LZ4B RECORDS WRITTEN : ' WS-LZ4B-WRITE.             
           DISPLAY ' '.                                                         
           DISPLAY ' CHECK : '.                                                 
           DISPLAY '    1 = 2 + 3 '.                                            
           DISPLAY '    2 = 4 '.                                                
           DISPLAY '    4 = 5 + 6 '.                                            
           DISPLAY '    5 = 7 = 8'.                                             
           DISPLAY ' '.                                                         
           DISPLAY '========================================'.                  
                                                                                
           CLOSE BP13LZ4A                                                       
                 BP13K740.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740 CLOSE ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
