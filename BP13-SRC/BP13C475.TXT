       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C475.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  23/03/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : TO EXTRACT SENT BP13K600 RECORDS (FR C470)  *            
      *                AND UPDATE DATES IN BP13K60A                *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F600                               *            
      * I-O    FILES :  1.) BP13K60A                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136692 KSJ3 23/03/2017 NEW PROGRAM                       *            
      * BP136711 KSJ3 16/05/2017 TO MOVE VALUES TO K60A ALT IDX 4  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F600  ASSIGN        TO BP13F600.                          
                                                                                
           SELECT BP13K60A  ASSIGN        TO BP13K60A                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K60A-KEY-FLD                       
                            FILE STATUS   IS WS-K60A-STATUS.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F600                                                             
           RECORD CONTAINS 1800 CHARACTERS.                                     
           COPY BP13F600.                                                       
                                                                                
       FD  BP13K60A.                                                            
           COPY BP13K60A.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K60A-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F600-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K60A-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-I                      PIC 9(2)   VALUE ZEROES.               
           05  WS-I-MAX                  PIC 9(2)   VALUE 50.                   
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F600-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K60A-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F600-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F600-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F600                                                 
                I-O    BP13K60A.                                                
                                                                                
           IF WS-K60A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K60A ERROR ' WS-K60A-STATUS                  
              MOVE     WS-K60A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F600-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F600 NEXT                                                   
              AT END MOVE 'Y'              TO   WS-F600-EOF.                    
                                                                                
           IF WS-F600-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F600-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF F600-DATE-EHPS-SUB = SPACES OR LOW-VALUES OR ZEROES               
              GO TO 3000-CONTINUE-NEXT-REC.                                     
                                                                                
           MOVE SPACES                     TO   BP13K60A-REC.                   
           MOVE F600-KEY-FLD               TO   K60A-KEY-FLD                    
                                                                                
           PERFORM 3010-BP13K60A-READ      THRU 3010-EXIT.                      
                                                                                
           IF WS-K60A-FND NOT = 'Y'                                             
              GO TO 3000-CONTINUE-NEXT-REC.                                     
                                                                                
           MOVE BP13F600-REC               TO   BP13K60A-REC.                   
           MOVE F600-NUM-CPF               TO   K60A-NUM-CPF4.                  
           MOVE F600-NUM-SNO               TO   K60A-NUM-SNO4.                  
                                                                                
           PERFORM 3020-BP13K60A-REWRITE   THRU 3020-EXIT.                      
                                                                                
       3000-CONTINUE-NEXT-REC.                                                  
                                                                                
           PERFORM 2000-BP13F600-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13K60A-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K60A.                                                       
                                                                                
           EVALUATE WS-K60A-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K60A-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K60A-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K60A ERROR '    WS-K60A-STATUS                  
                      ' / '                     K60A-KEY-FLD                    
              MOVE     WS-K60A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3020-BP13K60A-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K60A-REC.                                                
                                                                                
           EVALUATE WS-K60A-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K60A-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'UPDATE BP13K60A ERROR '  WS-K60A-STATUS                  
                      ' / '                     K60A-KEY-FLD                    
              MOVE     WS-K60A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F600                                                       
                 BP13K60A.                                                      
                                                                                
           IF WS-K60A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K60A ERROR ' WS-K60A-STATUS                  
              MOVE     WS-K60A-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C475 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F600 READ      : ' WS-CNT-F600-READ.          
           DISPLAY ' 2. NO OF BP13K60A UPDATED   : ' WS-CNT-K60A-UPD.           
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
