       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C752.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
       DATE-WRITTEN.  30/09/92.                                                 
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO PRINT REPORT ON FLAT ALLOCATED UNDER       *                
      *          RESIDENT SCHEME AFTER EACH PIGEONHOLING.      *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *      1. BP13K720                                       *                
      *      2. BP13F713                                       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1. BP13L752                                       *                
      *                                                        *                
      * CHG-NO  BY   ON        DESCRIPTION                     *                
      * ----------------------------------                     *                
      * C9209D3 RAZ 30/09/92 TO CATER FOR RESIDENT SCHEME.     *                
      * C930335 LSB 17/03/93 EXPAND BK13K720, BP13K010 FOR RDC *                
      * I920318 LJL 01/04/93 CONVERT TO COBOLII.               *                
      * C950137 GD  17/01/95 ADD PROCESSING FOR TCP            *                
      * C9506F9 LSB 07/07/95 ADD PROCESSING FOR CC1/CC2/CC3    *                
      * BP134861 EAA1 22/04/13 EXPAND BP13K010 TO 600          *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K720 ASSIGN TO BP13K720                                   
                ACCESS MODE  IS SEQUENTIAL                                      
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K720-KEY-FLD                                    
                FILE STATUS  IS BP13K720-STATUS.                                
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K010-KEY-FLD                                    
                FILE STATUS  IS BP13K010-STATUS.                                
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13L752 ASSIGN TO BP13L752.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K720                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K720.                                                           
                                                                                
       FD   BP13K010                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K010.                                                           
                                                                                
       FD   BP13F713                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F713.                                                           
                                                                                
       FD   BP13L752                                                            
            RECORD CONTAINS 133 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01   L752-PRINT-REC               PIC X(133).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  BP13K720-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  BP13K010-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  WS-TIME.                                                         
               10  WS-TIME-HH            PIC 9(02) VALUE 0.                     
               10  WS-TIME-MM            PIC 9(02) VALUE 0.                     
               10  WS-TIME-SS            PIC 9(02) VALUE 0.                     
           05  WS-CNT-K720               PIC 9(05) VALUE 0.                     
           05  WS-CNT-K010               PIC 9(05) VALUE 0.                     
           05  WS-CNT-PAGE               PIC 9(04) VALUE 0.                     
           05  WS-CNT-SNO                PIC 9(05) VALUE 0.                     
           05  WS-MAX-LINE               PIC 9(02) VALUE 54.                    
           05  WS-CNT-LINE               PIC 9(02) VALUE 54.                    
           05  WS-CDE-FT                 PIC X(02).                             
           05  WS-CDE-NT                 PIC X(03).                             
           05  WS-K720-EOF               PIC X     VALUE SPACES.                
               88  K720-EOF                        VALUE 'Y'.                   
           05  WS-K010-FND               PIC X     VALUE SPACES.                
               88  K010-FOUND                      VALUE 'Y'.                   
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY                     PIC X(02).                             
           05  WS-MM                     PIC X(02).                             
           05  WS-DD                     PIC X(02).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(02).                             
                                                                                
      ******************* REPORT LAYOUT *****************                       
                                                                                
       01  L752-HEAD1.                                                          
           05  FILLER                    PIC X(08) VALUE 'BP13L752'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'HDBCAT3'.             
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'DATE : '.             
           05  L752-DTE-CURRENT          PIC X(08).                             
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  L752-TME-HH               PIC 9(02).                             
           05  FILLER                    PIC X(01) VALUE ':'.                   
           05  L752-TME-MM               PIC 9(02).                             
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           05  L752-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L752-HEAD2.                                                          
           05  FILLER                    PIC X(100) VALUE SPACES.               
           05  FILLER                    PIC X(23) VALUE                        
               'USER (APPLICATION TEAM)'.                                       
                                                                                
       01  L752-HEAD3.                                                          
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(44) VALUE                        
               'REPORT ON FLAT ALLOTED UNDER RESIDENT SCHEME'.                  
           05  FILLER                    PIC X(26) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE                        
               'PROCESS : '.                                                    
           05  L752-HEAD-PROCESS         PIC X(15).                             
                                                                                
       01  L752-HEAD4.                                                          
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(06) VALUE 'S/NO.'.               
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE 'NT'.                  
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE 'FT'.                  
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'ALC-SCH'.             
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'TOT-UNIT'.            
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'APP-RES'.             
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'APP-NRES'.            
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'FLAT-RES'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'FLAT-NRES'.           
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'REG-RES'.             
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'REG-NRES'.            
                                                                                
       01  L752-DETAIL1.                                                        
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L752-SNO                  PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L752-NT                   PIC X(03).                             
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  L752-FT                   PIC X(02).                             
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  L752-ALLOC-SCH            PIC X(03).                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-TOT-UNIT             PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-APP-RES              PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-APP-NRES             PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-FLAT-RES             PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-FLAT-NRES            PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-REG-RES              PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-REG-NRES             PIC ZZZZ9.                             
                                                                                
       01  L752-DETAIL2.                                                        
           05  FILLER                    PIC X(26) VALUE SPACES.                
           05  L752-ALLOC-SCH1           PIC X(03).                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-TOT-UNIT1            PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-APP-RES1             PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-APP-NRES1            PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-FLAT-RES1            PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-FLAT-NRES1           PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-REG-RES1             PIC ZZZZ9.                             
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L752-REG-NRES1            PIC ZZZZ9.                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 1000-OPEN-FILES                                              
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 2000-READ-BP13F713                                           
              THRU 2000-EXIT.                                                   
                                                                                
           PERFORM 3000-READ-BP13K720                                           
              THRU 3000-EXIT.                                                   
                                                                                
           PERFORM 4000-MAIN-ROUTINE                                            
              THRU 4000-EXIT                                                    
             UNTIL K720-EOF.                                                    
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  BP13K720                                                 
                       BP13K010                                                 
                       BP13F713                                                 
                OUTPUT BP13L752.                                                
                                                                                
           IF BP13K720-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K720,STATUS : '                        
                        BP13K720-STATUS                                         
              MOVE BP13K720-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
           IF BP13K010-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010,STATUS : '                        
                        BP13K010-STATUS                                         
              MOVE BP13K010-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
           ACCEPT WS-DTE-CURRENT         FROM DATE.                             
           MOVE CORR WS-DTE-CURRENT        TO WS-DTE-TODATE.                    
           MOVE WS-DTE-TODATE              TO L752-DTE-CURRENT.                 
                                                                                
           ACCEPT  WS-TIME                 FROM TIME.                           
           MOVE WS-TIME-HH                 TO L752-TME-HH.                      
           MOVE WS-TIME-MM                 TO L752-TME-MM.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F713.                                                      
                                                                                
           READ BP13F713                AT END                                  
           GO TO 2000-EXIT.                                                     
                                                                                
            EVALUATE  F713-CDE-COHORT                                           
            WHEN '01'   MOVE 'COHORT 1       '    TO L752-HEAD-PROCESS          
            WHEN '02'   MOVE 'COHORT 2       '    TO L752-HEAD-PROCESS          
            WHEN '03'   MOVE 'COHORT 3       '    TO L752-HEAD-PROCESS          
            WHEN '04'   MOVE 'COHORT 4       '    TO L752-HEAD-PROCESS          
            WHEN '05'   MOVE 'COHORT 5       '    TO L752-HEAD-PROCESS          
            WHEN '06'   MOVE 'COHORT 6       '    TO L752-HEAD-PROCESS          
            WHEN '07'   MOVE 'COHORT 7       '    TO L752-HEAD-PROCESS          
            WHEN '08'   MOVE 'COHORT 8       '    TO L752-HEAD-PROCESS          
            WHEN 'MT'   MOVE 'MTF/SFF/RH     '    TO L752-HEAD-PROCESS          
            WHEN 'CC'   MOVE 'GRO            '    TO L752-HEAD-PROCESS          
            WHEN 'C1'   MOVE 'GRO RESIDENT   '    TO L752-HEAD-PROCESS          
            WHEN 'C2'   MOVE 'GRO HPS-NONRES '    TO L752-HEAD-PROCESS          
            WHEN 'C3'   MOVE 'GRO RES/NONRES '    TO L752-HEAD-PROCESS          
            WHEN 'PA'   MOVE 'MATURE NT - PA '    TO L752-HEAD-PROCESS          
            WHEN 'NH'   MOVE 'NHH/UPG        '    TO L752-HEAD-PROCESS          
            WHEN 'TR'   MOVE 'TRANSITIONAL   '    TO L752-HEAD-PROCESS          
            WHEN OTHER  MOVE '               '    TO L752-HEAD-PROCESS          
            END-EVALUATE.                                                       
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K720.                                                      
                                                                                
            READ BP13K720 NEXT           AT END                                 
            MOVE 'Y'                     TO WS-K720-EOF                         
            GO TO 3000-EXIT.                                                    
                                                                                
            ADD 1                        TO WS-CNT-K720.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MAIN-ROUTINE.                                                       
                                                                                
           MOVE K720-KEY-FLD             TO K010-KEY-FLD.                       
                                                                                
           READ BP13K010.                                                       
                                                                                
           IF BP13K010-STATUS = 00                                              
              ADD 1                      TO WS-CNT-K010                         
              MOVE 'Y'                   TO WS-K010-FND                         
           ELSE                                                                 
              IF BP13K010-STATUS = 23                                           
                 MOVE 'N'                TO WS-K010-FND                         
              ELSE                                                              
                 MOVE 'N'                TO WS-K010-FND                         
                 DISPLAY '*** ERROR READING , BP13K010-STATUS '                 
                 BP13K010-STATUS.                                               
                                                                                
           IF  WS-CNT-LINE  =  WS-MAX-LINE                                      
               ADD 1                     TO WS-CNT-PAGE                         
               PERFORM 5000-PRINT-HEADING                                       
                  THRU 5000-EXIT                                                
               MOVE 6                    TO WS-CNT-LINE.                        
                                                                                
           ADD 1                         TO WS-CNT-SNO.                         
                                                                                
           MOVE WS-CNT-SNO               TO L752-SNO.                           
           MOVE K720-CDE-NT              TO L752-NT.                            
           MOVE K720-CDE-FLAT-TY         TO L752-FT.                            
           MOVE 'MTF'                    TO L752-ALLOC-SCH.                     
           IF K010-FOUND                                                        
              MOVE K010-NUM-TOT-MTF      TO L752-TOT-UNIT                       
           ELSE                                                                 
              MOVE ZEROS                 TO L752-TOT-UNIT.                      
           MOVE K720-NUM-APP-RES-MTF     TO L752-APP-RES.                       
           MOVE K720-NUM-APP-NONRES-MTF  TO L752-APP-NRES.                      
           MOVE K720-NUM-FLAT-RES-MTF    TO L752-FLAT-RES.                      
           MOVE K720-NUM-FLAT-NONRES-MTF TO L752-FLAT-NRES.                     
           MOVE K720-NUM-REG-RES-MTF     TO L752-REG-RES.                       
           MOVE K720-NUM-REG-NONRES-MTF  TO L752-REG-NRES.                      
           WRITE L752-PRINT-REC        FROM L752-DETAIL1 AFTER 2.               
                                                                                
           MOVE SPACES                   TO L752-DETAIL2.                       
           MOVE 'CCC'                    TO L752-ALLOC-SCH1.                    
           IF K010-FOUND                                                        
              MOVE K010-NUM-TOT-CCC      TO L752-TOT-UNIT1                      
           ELSE                                                                 
              MOVE ZEROS                 TO L752-TOT-UNIT1.                     
           MOVE K720-NUM-APP-RES-CCC     TO L752-APP-RES1.                      
           MOVE K720-NUM-APP-NONRES-CCC  TO L752-APP-NRES1.                     
           MOVE K720-NUM-FLAT-RES-CCC    TO L752-FLAT-RES1.                     
           MOVE K720-NUM-FLAT-NONRES-CCC TO L752-FLAT-NRES1.                    
           MOVE K720-NUM-REG-RES-CCC     TO L752-REG-RES1.                      
           MOVE K720-NUM-REG-NONRES-CCC  TO L752-REG-NRES1.                     
           WRITE L752-PRINT-REC        FROM L752-DETAIL2.                       
                                                                                
           MOVE SPACES                   TO L752-DETAIL2.                       
           MOVE 'RH '                    TO L752-ALLOC-SCH1.                    
           IF K010-FOUND                                                        
              MOVE K010-NUM-TOT-RH       TO L752-TOT-UNIT1                      
           ELSE                                                                 
              MOVE ZEROS                 TO L752-TOT-UNIT1.                     
           MOVE K720-NUM-APP-RES-RH      TO L752-APP-RES1.                      
           MOVE K720-NUM-APP-NONRES-RH   TO L752-APP-NRES1.                     
           MOVE K720-NUM-FLAT-RES-RH     TO L752-FLAT-RES1.                     
           MOVE K720-NUM-FLAT-NONRES-RH  TO L752-FLAT-NRES1.                    
           MOVE K720-NUM-REG-RES-RH      TO L752-REG-RES1.                      
           MOVE K720-NUM-REG-NONRES-RH   TO L752-REG-NRES1.                     
           WRITE L752-PRINT-REC        FROM L752-DETAIL2.                       
                                                                                
           MOVE SPACES                   TO L752-DETAIL2.                       
           MOVE 'REN'                    TO L752-ALLOC-SCH1.                    
           IF K010-FOUND                                                        
              MOVE K010-NUM-TOT-REN      TO L752-TOT-UNIT1                      
           ELSE                                                                 
              MOVE ZEROS                 TO L752-TOT-UNIT1.                     
           MOVE K720-NUM-APP-RES-REN     TO L752-APP-RES1.                      
           MOVE K720-NUM-APP-NONRES-REN  TO L752-APP-NRES1.                     
           MOVE K720-NUM-FLAT-RES-REN    TO L752-FLAT-RES1.                     
           MOVE K720-NUM-FLAT-NONRES-REN TO L752-FLAT-NRES1.                    
           MOVE K720-NUM-REG-RES-REN     TO L752-REG-RES1.                      
           MOVE K720-NUM-REG-NONRES-REN  TO L752-REG-NRES1.                     
           WRITE L752-PRINT-REC        FROM L752-DETAIL2.                       
                                                                                
           MOVE SPACES                   TO L752-DETAIL2.                       
           MOVE 'RDC'                    TO L752-ALLOC-SCH1.                    
           IF K010-FOUND                                                        
              MOVE K010-NUM-TOT-RDC      TO L752-TOT-UNIT1                      
           ELSE                                                                 
              MOVE ZEROS                 TO L752-TOT-UNIT1.                     
           MOVE K720-NUM-APP-RES-RDC     TO L752-APP-RES1.                      
           MOVE K720-NUM-APP-NONRES-RDC  TO L752-APP-NRES1.                     
           MOVE K720-NUM-FLAT-RES-RDC    TO L752-FLAT-RES1.                     
           MOVE K720-NUM-FLAT-NONRES-RDC TO L752-FLAT-NRES1.                    
           MOVE K720-NUM-REG-RES-RDC     TO L752-REG-RES1.                      
           MOVE K720-NUM-REG-NONRES-RDC  TO L752-REG-NRES1.                     
           WRITE L752-PRINT-REC        FROM L752-DETAIL2.                       
                                                                                
           MOVE SPACES                   TO L752-DETAIL2.                       
           MOVE 'PA '                    TO L752-ALLOC-SCH1.                    
           IF K010-FOUND                                                        
              MOVE K010-NUM-TOT-PA       TO L752-TOT-UNIT1                      
           ELSE                                                                 
              MOVE ZEROS                 TO L752-TOT-UNIT1.                     
           MOVE K720-NUM-APP-RES-PA      TO L752-APP-RES1.                      
           MOVE K720-NUM-APP-NONRES-PA   TO L752-APP-NRES1.                     
           MOVE K720-NUM-FLAT-RES-PA     TO L752-FLAT-RES1.                     
           MOVE K720-NUM-FLAT-NONRES-PA  TO L752-FLAT-NRES1.                    
           MOVE K720-NUM-REG-RES-PA      TO L752-REG-RES1.                      
           MOVE K720-NUM-REG-NONRES-PA   TO L752-REG-NRES1.                     
           WRITE L752-PRINT-REC        FROM L752-DETAIL2.                       
                                                                                
           MOVE SPACES                   TO L752-DETAIL2.                       
           MOVE 'TCP'                    TO L752-ALLOC-SCH1.                    
           IF K010-FOUND                                                        
              MOVE K010-NUM-TOT-TCP      TO L752-TOT-UNIT1                      
           ELSE                                                                 
              MOVE ZEROS                 TO L752-TOT-UNIT1.                     
           MOVE K720-NUM-APP-RES-TCP     TO L752-APP-RES1.                      
           MOVE K720-NUM-APP-NONRES-TCP  TO L752-APP-NRES1.                     
           MOVE K720-NUM-FLAT-RES-TCP    TO L752-FLAT-RES1.                     
           MOVE K720-NUM-FLAT-NONRES-TCP TO L752-FLAT-NRES1.                    
           MOVE K720-NUM-REG-RES-TCP     TO L752-REG-RES1.                      
           MOVE K720-NUM-REG-NONRES-TCP  TO L752-REG-NRES1.                     
           WRITE L752-PRINT-REC        FROM L752-DETAIL2.                       
                                                                                
           ADD 7                         TO WS-CNT-LINE.                        
                                                                                
           PERFORM 3000-READ-BP13K720                                           
              THRU 3000-EXIT.                                                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PRINT-HEADING.                                                      
                                                                                
           MOVE WS-CNT-PAGE              TO L752-CNT-PAGE.                      
                                                                                
           WRITE L752-PRINT-REC        FROM L752-HEAD1 AFTER PAGE.              
           WRITE L752-PRINT-REC        FROM L752-HEAD2.                         
           WRITE L752-PRINT-REC        FROM L752-HEAD3.                         
           WRITE L752-PRINT-REC        FROM L752-HEAD4 AFTER 1.                 
           MOVE  SPACES                  TO L752-PRINT-REC.                     
           WRITE L752-PRINT-REC.                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
                                                                                
           CLOSE BP13K720                                                       
                 BP13K010                                                       
                 BP13F713                                                       
                 BP13L752.                                                      
                                                                                
           DISPLAY '*************** BP13C752 ***************'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF RECORDS READ FROM BP13K720 : ' WS-CNT-K720.          
           DISPLAY 'NO. OF RECORDS READ FROM BP13K010 : ' WS-CNT-K010.          
           DISPLAY ' '.                                                         
                                                                                
           IF BP13K720-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K720-STATUS '                    
              BP13K720-STATUS.                                                  
                                                                                
           IF BP13K010-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K010-STATUS '                    
              BP13K010-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
