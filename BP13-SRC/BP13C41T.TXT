      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C41T.                                                 
      *AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
      *DATE-WRITTEN.  10/05/22.                                                 
                                                                                
      * ========================================================= *             
      * SYSTEM OF COMMITMENT  (BP13)                              *             
      * ========================================================= *             
      *    OBJECTIVE :                                            *             
      *      1. THE PROGRAM IS TO CALL AB05C902 TO EARMARK THE    *             
      *         STAMP FEES REQUIRED INTO AB05 FILES.              *             
      *                                                           *             
      *                                                           *             
      *     I/O FILES:                                            *             
      *       INPUT  :  AG05F255 -                                *             
      *                                                           *             
      *      OUTPUT  :  B05K006                                   *             
      *                 AB05K009                                  *             
      *                 AB05K902                                  *             
      *                                                           *             
      * --------------------------------------------------------- *             
      * CHG REF  DATE   BY   DESCRIPTIONS                         *             
      * -------- ------ ---  ------------                         *             
      * BP139197 100522 KAC1 REVISED COPY OF BP13C416             *             
      * ========================================================= *             
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F225  ASSIGN       TO AG07F225.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
      *============*                                                            
      *------------------                                                       
      * VOUCHER FILE                                                            
      *------------------                                                       
       FD   AG07F225                                                            
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 600  CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD                                       
            RECORDING MODE  IS   F.                                             
       COPY AG07F225.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       COPY SERSCH.                                                             
                                                                                
      *-----------------------------------------------------------*             
      *    DEFINE ERROR MESSAGE FORMAT                            *             
      *-----------------------------------------------------------*             
       01  WS-ERROR.                                                            
           05  FILLER                       PIC X(24) VALUE                     
               ' ERROR ENCOUNTERED WHEN '.                                      
           05  WS-CUR-PROC                  PIC X(21).                          
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  FILLER                       PIC X(15) VALUE                     
               ' STATUS CODE = '.                                               
           05  WS-CUR-STATUS                PIC X(2).                           
                                                                                
      *-----------------------------------------------------------*             
      *    DEFINE SUMMARY FORMAT                                  *             
      *-----------------------------------------------------------*             
       01  WS-SUMMARY-HEADER.                                                   
           05  FILLER                       PIC X     VALUE SPACES.             
           05  FILLER                       PIC X(16) VALUE ALL '*'.            
           05  FILLER                       PIC X(20) VALUE                     
               'BP13C41T SUMMARY RPT'.                                          
           05  FILLER                       PIC X(19) VALUE ALL '*'.            
                                                                                
       01  WS-READ-CASES.                                                       
           05  FILLER                       PIC X(52) VALUE                     
               ' NO OF RECORDS READ FROM (BP13F315) = '.                        
           05  WS-EDIT-AG07F225-READ        PIC ZZZZZZ9.                        
                                                                                
       01  WS-SUMMARY-TRAILER.                                                  
           05  FILLER                       PIC X     VALUE SPACES.             
           05  FILLER                       PIC X(55) VALUE ALL '*'.            
                                                                                
      *-----------------------------------------------------------*             
      *    LEVEL-88 VARIABLES                                     *             
      *-----------------------------------------------------------*             
       01  WS-EOF                           PIC X     VALUE 'N'.                
           88  END-OF-AG07F225                        VALUE 'Y'.                
                                                                                
      *-----------------------------------------------------------*             
      *    OTHER VARIABLES                                        *             
      *-----------------------------------------------------------*             
       01  WS-COUNTERS.                                                         
           05  WS-AG07F225-READ-CNT         PIC 9(7)    VALUE ZEROS.            
           05  WS-SUBR-SUCCESS-CNT          PIC 9(7)    VALUE ZEROES.           
           05  WS-UNSUCC-SUBR-CALL-CNT      PIC 9(7)    VALUE ZEROS.            
                                                                                
       01  WS-BLANK-LINE                    PIC X     VALUE SPACES.             
       01  WS-F225-STATUS                   PIC 9(2)  VALUE ZEROS.              
       01  WS-AMT-EARMARK                   PIC 9(9)V99 VALUE ZEROES.           
                                                                                
      *----------------------------------------------------------*              
      *    WS VARIABLES PASSED FROM SUB-PROGRAM "BP13C906"       *              
      *----------------------------------------------------------*              
       COPY P13C906C.                                                           
                                                                                
      *----------------------------------------------------------*              
      *    WS VARIABLES PASSED FROM SUB-PROGRAM "AB05C902"       *              
      *----------------------------------------------------------*              
       COPY AB05902C.                                                           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
            PERFORM  0200-OPEN-FILES             THRU  0200-EXIT.               
            PERFORM  1100-READ-AG07F225          THRU  1100-EXIT.               
            PERFORM  1000-READ-AND-PRINT-LOOP    THRU  1000-EXIT                
                          UNTIL END-OF-AG07F225.                                
            PERFORM  8000-DISPLAY-STATISTICS     THRU  8000-EXIT.               
            PERFORM  9000-CLOSE-FILES.                                          
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0200-OPEN-FILES.                                                         
      ******************************************************************        
            OPEN INPUT  AG07F225.                                               
      *          OUTPUT B05K006                                                 
      *                 AB05K902.                                               
                                                                                
      * VOUCHER NUMBER                                                          
      *     MOVE 'AG07K960' TO    C907-NUM-FILENME.                             
      *     CALL 'K960OPEN' USING AG07C907-AREA.                                
      *                                                                         
      *     IF C907-NUM-FILE-STATUS NOT = ('00' AND '97')                       
      *        DISPLAY 'ERROR OPENING AG07K960, STATUS = '                      
      *                 C907-NUM-FILE-STATUS                                    
      *        MOVE     C907-NUM-FILE-STATUS    TO     RETURN-CODE              
      *        PERFORM  9000-CLOSE-FILES        THRU   9000-EXIT                
      *     END-IF.                                                             
                                                                                
      **CALL AB05C902 TO OPEN B05K006 AND AB05K009 FILES(CODE='O')              
            MOVE SPACES              TO   WS-B902-AREA.                         
            INITIALIZE                    WS-B902-AREA.                         
            MOVE 'O'                 TO   B902-CDE-TYPE.                        
            CALL 'AB05C902'  USING WS-B902-AREA.                                
                                                                                
            IF B902-CDE-ERROR NOT = ZEROES                                      
               MOVE B902-CDE-SYSERR  TO   RETURN-CODE                           
               DISPLAY 'ERROR CALLING AB05C902 SUB-ROUTINE!'                    
               PERFORM  9000-CLOSE-FILES                                        
            END-IF.                                                             
                                                                                
       0200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       1000-READ-AND-PRINT-LOOP.                                                
      ******************************************************************        
      *     DISPLAY '-----------------------'                                   
      *     DISPLAY 'F225-NUM-REC-TYP       ' F225-NUM-REC-TYP                  
      *     DISPLAY 'F225-NUM-TRANS-SERIAL  ' F225-NUM-TRANS-SERIAL             
      *     DISPLAY 'F225-NUM-TRAILER-SERIAL' F225-NUM-TRAILER-SERIAL           
      *     DISPLAY '-----------------------'                                   
                                                                                
            IF (F225-NUM-TRANS-SERIAL NOT = 00 AND 99)                          
               DISPLAY 'CALL 1055-EARMARK-ACCOUNT '                             
               PERFORM  1055-EARMARK-ACCOUNT  THRU 1055-EXIT                    
                                                                                
               IF B902-CDE-SYSERR NOT = ZEROES                                  
                  DISPLAY 'ERROR CALLING AB05C902, STATUS = '                   
                           B902-CDE-SYSERR                                      
                  MOVE     '99'                 TO RETURN-CODE                  
                  PERFORM  9000-CLOSE-FILES   THRU 9000-EXIT                    
               END-IF                                                           
            END-IF.                                                             
                                                                                
      *     DISPLAY '*****READ INPUT AGAIN*****'.                               
            PERFORM 1100-READ-AG07F225        THRU 1100-EXIT.                   
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       1055-EARMARK-ACCOUNT.                                                    
      ******************************************************************        
            MOVE SPACES              TO   WS-B902-AREA.                         
            INITIALIZE                    WS-B902-AREA.                         
                                                                                
            STRING F225-NUM-DDD  F225-NUM-FIN-YEAR                              
                   F225-NUM-VR-SERIAL                                           
                   DELIMITED BY SIZE INTO B902-VR-NO                            
            END-STRING.                                                         
                                                                                
            MOVE 'E'                     TO   B902-CDE-TYPE.                    
            MOVE 'BP13'                  TO   B902-CDE-SYSTEMID.                
            MOVE FUNCTION CURRENT-DATE   TO   B902-DTE-TRANS.                   
            MOVE 'P13C41T'               TO   B902-NUM-USERID-UPDATE.           
            MOVE 'CV'                    TO   B902-CDE-EARMARK-REASON.          
            MOVE 01                      TO   B902-NUM-HDB-REF-CNT.             
            MOVE F225-NUM-HDB-REF        TO   B902-NUM-HDB-REF(1).              
            MOVE F225-AMT-TRANS          TO   WS-AMT-EARMARK.                   
                                                                                
            MOVE WS-AMT-EARMARK          TO   B902-AMT-EARMARK(1).              
                                                                                
      *     DISPLAY '----------------'.                                         
      *     DISPLAY 'B902-VR-NO       = ' B902-VR-NO.                           
      *     DISPLAY 'B902-CDE-TYPE    = ' B902-CDE-TYPE.                        
      *     DISPLAY 'CDE-SYSTEMID     = ' B902-CDE-SYSTEMID.                    
      *     DISPLAY 'B902-DTE-TRANS   = ' B902-DTE-TRANS.                       
      *     DISPLAY 'NUM-USERID       = ' B902-NUM-USERID-UPDATE.               
      *     DISPLAY 'HDB-REF-CNT      = ' B902-NUM-HDB-REF-CNT.                 
      *     DISPLAY 'NUM-HDB-REF      = ' B902-NUM-HDB-REF(1).                  
      *     DISPLAY 'B902-AMT-EARMARK = ' B902-AMT-EARMARK(1).                  
                                                                                
            CALL 'AB05C902'  USING WS-B902-AREA.                                
                                                                                
      *     DISPLAY 'CDE-ERROR-ACCT   = ' B902-CDE-ERROR-ACCT(1).               
      *     DISPLAY '----------------'.                                         
                                                                                
           EVALUATE B902-CDE-ERROR-ACCT(1)                                      
               WHEN SPACES                                                      
                    ADD 1  TO WS-SUBR-SUCCESS-CNT                               
               WHEN 'S0045'                                                     
                    DISPLAY 'B05K006 FILE ERROR     ' F225-NUM-HDB-REF          
               WHEN 'S0052'                                                     
                    DISPLAY 'INVALID INPUT          ' F225-NUM-HDB-REF          
               WHEN 'S0053'                                                     
                    DISPLAY 'AB05K009 FILE ERROR    ' F225-NUM-HDB-REF          
               WHEN 'S0054'                                                     
                    DISPLAY 'B05K006 REC NOT FND    ' F225-NUM-HDB-REF          
               WHEN 'S0055'                                                     
                    DISPLAY 'NOT ENOUGH BALANCE     ' F225-NUM-HDB-REF          
               WHEN OTHER                                                       
                    DISPLAY 'AB05C902 SUBRTINE ERROR' F225-NUM-HDB-REF          
           END-EVALUATE.                                                        
                                                                                
           IF B902-CDE-ERROR-ACCT(1) NOT = SPACES AND LOW-VALUES                
              ADD 1   TO  WS-UNSUCC-SUBR-CALL-CNT                               
           END-IF.                                                              
                                                                                
       1055-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       1100-READ-AG07F225.                                                      
      ******************************************************************        
            READ AG07F225                                                       
              AT END                                                            
                 MOVE    'Y'  TO WS-EOF                                         
                 GO TO   1100-EXIT.                                             
                                                                                
            ADD     1                 TO  WS-AG07F225-READ-CNT.                 
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       2000-DISPLAY-ERROR-MESSAGE.                                              
      ******************************************************************        
            DISPLAY ' *************   ERROR MESSAGE   *************'.           
            DISPLAY 'ERROR ENCOUNTERED WHEN ' WS-CUR-PROC.                      
            DISPLAY 'STATUS CODE = ' WS-CUR-STATUS.                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       8000-DISPLAY-STATISTICS.                                                 
      ******************************************************************        
            MOVE    WS-AG07F225-READ-CNT  TO  WS-EDIT-AG07F225-READ.            
                                                                                
            DISPLAY WS-BLANK-LINE.                                              
            DISPLAY WS-SUMMARY-HEADER.                                          
            DISPLAY WS-BLANK-LINE.                                              
            DISPLAY WS-READ-CASES.                                              
            DISPLAY WS-BLANK-LINE.                                              
            DISPLAY WS-SUMMARY-TRAILER.                                         
            DISPLAY 'SUCCESS SUBROUTINE CALL (AB05C902) = '                     
                    WS-SUBR-SUCCESS-CNT.                                        
            DISPLAY 'NO OF RECS WITH ERROR IN EARMARK                = '        
                    WS-UNSUCC-SUBR-CALL-CNT.                                    
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
            CLOSE  AG07F225.                                                    
                                                                                
      **CALL AB05C902 TO CLOSE B05K006 AND AB05K009 FILES(CODE='C')             
            MOVE SPACES              TO   WS-B902-AREA.                         
            INITIALIZE                    WS-B902-AREA.                         
            MOVE 'C'                 TO   B902-CDE-TYPE.                        
            CALL 'AB05C902'  USING WS-B902-AREA.                                
                                                                                
            IF B902-CDE-ERROR NOT = ZEROES                                      
               MOVE B902-CDE-SYSERR  TO   RETURN-CODE                           
               DISPLAY 'ERROR CALLING AB05C902 SUB-ROUTINE!'                    
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
