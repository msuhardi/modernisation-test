       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C231.                                                 
       AUTHOR.        ROSALYN M. LUMANGLAS.                                     
       DATE-WRITTEN.  21/09/93.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : ALLOCATION OF FLATS.                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *                                                        *                
      *        THIS IS THE PROGRAM TO PRINT ATTENDANCE LIST    *                
      *                                                        *                
      *    INPUT FILES : BM06CTL                               *                
      *                  BM06K010                              *                
      *                  BP13K800                              *                
      *                  BP13K820                              *                
      *                  BP13K060                              *                
      *                  BP13K76D                              *                
      *                                                        *                
      *    OUTPUT      : BP13L231                              *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  --   ----     -------------------------------- *                
      *         BAL  940325   REPLACE BM06CTL FILE W/ BP13F205 *                
      *                       & BM06K010 W/ BP13K200.          *                
      *         BAL  940627   CHG BP13K200 WITH BP13F200.      *                
      * SOC-PH9 GD   941108   CATER FOR NT AND ZONE IN LETTER  *                
      * C9506F9 LMS  950825   TO INCLUDE DECODING OF HHTY FOR  *                
      *                       CGO.                             *                
      *BP130297 LMS  970519   TO OUTPUT CUR-REGN.              *                
      *BP130297 LMS  970524   TO OUTPUT SEQ/REGN/JB-PARENT-REGN*                
      *                       FOR JB CASES, AND SEQ/REGN FOR   *                
      *                       OTHER CASES.                     *                
      *BP130330 LMS  970623   TO CATER FOR JB IN SERS.         *                
      *BP130452 LMS  980617   TO AMEND CONDITIONS FOR PRINTING *                
      *                       JB IN SERS.                      *                
      *BP130418 LMS  980917   CHG IN BP13K800 AND BP13K820.    *                
      *BP130768 GBR  991213   TO CATER FOR ONE-TIME BALLOT.    *                
      *BP130778 GBR  991213   TO INCLUDE 3 ROOM TYPE UNDER FLAT*                
      *                       TYPE 'SE'.                       *                
      *BP130949 ANC  001106   INCLUDE NUM-JB-QUEUE-SEQUENCE IN *                
      *                       BP13L231 REPORT FILE             *                
      *BP131059 LMS  010821   TO CATER FOR BTO.                *                
      *BP132095 MTD  220402   EXPAND LENGTH OF BP13F200        *                
      *                       (FROM 100 TO 300)                *                
      *BP132501 MTD  170404   ADD JB AND LF FOR SERS APPLN     *                
      *BP132500 MTD  050504   ADD QUEUE NUMBER IN LINE 2 FOR   *                
      *                       FOR NT-ZONE-SW = 'T'             *                
      *BP133349 LMS  180608   TO CATER FOR SER 2-ROOM NEW FLATS*                
      *BP133482 ESA1 161208   TO ADJUST ALIGNMENT OF THE REPORT*                
      *BP133610 ESA1 270408   TO REPLACE BP13K767 W/ BP13K816  *                
      *BP133841 ESA1 180310   TO CATER FOR PAIR UNIT           *                
      *BP134320 ESA1 020811   TO ADD READING OF BP13K76D       *                
      *BP134383 ESA1 091111   TO DISPLAY DTE-BALLOT & PROJ NAME*                
      *BP134681 ESA1 190912   TO REPLACE BP13K816 W/ BP13K813  *                
      *BP135103 ESA1 121113   TO ADD DISPLAY OF SER,AST,PP,SAP & 3GEN           
      *BP135190 SMR2 041213   EXPAND BP13K813 TO 1000          *                
      *BP135451 ESA1 150814   CATER FOR SBF 3-ROOM MATURE &    *                
      *                       NON-MATURE ESTATES               *                
      *BP135285 ESA1 010914   TO REMOVE CALLING OF BP13C910    *                
      *BP135561 ESA1 280115   TO INCLUDE DJDE IN THE PROGRAM   *                
      *BP135591 ESA1 180215   TO BYPASS PRINTING FOR MGPS CASES*                
      *BP136066 ESA1 210116   TO CATER FOR 2-ROOM FLEXI PRINTING                
      *BP136122 ESA1 080416   CATER FOR '2F' IN READING BP13K246                
      *BP138051 EL27 201119   FIX 2F PRINTING OF FLAT TYPE     *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS WS-K060-STATUS.                               
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS WS-K800-STATUS.                               
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K820-KEY-FLD                                  
                  FILE STATUS  IS WS-K820-STATUS.                               
           SELECT BP13K76D ASSIGN TO BP13K76D                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K76D-KEY-FLD                                  
                  FILE STATUS  IS WS-K76D-STATUS.                               
           SELECT BP13K246 ASSIGN TO BP13K246                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K246-KEY-FLD                                  
                  FILE STATUS  IS WS-K246-STATUS.                               
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K813-KEY-FLD                                  
                  FILE STATUS  IS WS-K813-STATUS.                               
           SELECT BP13L231 ASSIGN TO BP13L231.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F205                                                             
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F200                                                             
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F200.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25 CHARACTERS.                                       
       COPY BP13K060.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K76D                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K76D.                                                           
                                                                                
       FD  BP13K246                                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY BP13K246.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13L231                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC                        PIC X(80).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-K060-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K800-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K820-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K76D-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K246-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K813-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-F205-READ-CNT               PIC 9(4)   VALUE ZEROS.               
       01  WS-F200-READ-CNT               PIC 9(4)   VALUE ZEROS.               
       01  WS-K060-CNT                    PIC 9(4)   VALUE ZEROS.               
       01  WS-LINE-CNT                    PIC 99     VALUE ZEROS.               
       01  EOF-F205                       PIC X      VALUE 'N'.                 
       01  EOF-F200                       PIC X      VALUE 'N'.                 
       01  WS-EOF-K813                    PIC X      VALUE 'N'.                 
       01  WS-K76D-FND                    PIC X      VALUE 'N'.                 
       01  WS-PREV-RM-TYP                 PIC X(2)   VALUE SPACES.              
       01  WS-PREV-NT-ZONE                PIC X(3)   VALUE SPACES.              
       01  WS-PREV-APPT-DATE              PIC 9(8)   VALUE ZEROS.               
       01  WS-PREV-APPT-TIME              PIC 9(4)   VALUE ZEROS.               
       01  WS-NAME                        PIC X(22)  VALUE SPACES.              
       01  WS-PAGE-CNT                    PIC 9999   VALUE ZEROS.               
       01  WS-PRINT-TIME                  PIC X      VALUE 'N'.                 
       01  WS-NT-ZONE-DESC                PIC X(20)  VALUE SPACES.              
       01  WS-JB                          PIC X(02)  VALUE SPACES.              
       01  WS-LF                          PIC X(02)  VALUE SPACES.              
       01  WS-PROJECT-NAME                PIC X(50)  VALUE SPACES.              
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(50).                          
           05 WS-PARSED-STRING              PIC X(50).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  WS-DTE-ALLOC.                                                        
           05 WS-DTE-DD                   PIC 99.                               
           05 FILLER                      PIC X   VALUE '/'.                    
           05 WS-DTE-MM                   PIC 99.                               
           05 FILLER                      PIC X   VALUE '/'.                    
           05 WS-DTE-CC                   PIC 99.                               
           05 WS-DTE-YY                   PIC 99.                               
                                                                                
       01  WS-CURRENT-DATE                PIC X(8).                             
                                                                                
       01  WS-TIME.                                                             
           05 WS-HH                       PIC 99.                               
           05 WS-MM                       PIC 99.                               
                                                                                
       01  WS-DTE-APPT.                                                         
           05 WS-DTE-CC                   PIC 99.                               
           05 WS-DTE-YY                   PIC 99.                               
           05 WS-DTE-MM                   PIC 99.                               
           05 WS-DTE-DD                   PIC 99.                               
                                                                                
       01  WS-ALLOC-NT-ZONE.                                                    
           05 WS-ALLOC-DATE               PIC 9(8).                             
           05 WS-NT-ZONE                  PIC X(3).                             
           05 WS-FT                       PIC X(2).                             
                                                                                
      *============================================================*            
      *            P R I N T    L A Y O U T                        *            
      *============================================================*            
                                                                                
       01  L231A-HEADER-1.                                                      
           05  FILLER                     PIC X(05) VALUE                       
               'PAGE '.                                                         
           05  L231A-PAGE                 PIC ZZZ9.                             
           05  FILLER                     PIC X(40) VALUE SPACES.               
           05  FILLER                     PIC X(20) VALUE                       
               '             DATE : '.                                          
           05  FILLER.                                                          
               10  L231A-RUN-DD           PIC X(2).                             
               10  FILLER                 PIC X(1)  VALUE '/'.                  
               10  L231A-RUN-MM           PIC X(2).                             
               10  FILLER                 PIC X(1)  VALUE '/'.                  
               10  L231A-RUN-CY           PIC X(4).                             
                                                                                
       01  L231A-HEADER-2.                                                      
           05  FILLER                     PIC X(13) VALUE SPACES.               
           05  FILLER                     PIC X(49) VALUE                       
               'ATTENDANCE LIST FOR PRIORITY AND PUBLIC SELECTION'.             
           05  FILLER                     PIC X(11) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE 'CAT3'.               
                                                                                
       01  L231A-HEADER-3.                                                      
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  FILLER                     PIC X(49)  VALUE ALL '-'.             
                                                                                
       01  L231A-HEADER-3A.                                                     
           05  FILLER                     PIC X(19)  VALUE                      
               'BALLOT QUARTER   : '.                                           
           05  L231A-BALLOT-QTR           PIC X(07).                            
                                                                                
       01  L231A-HEADER-4.                                                      
           05  L231A-NT-ZONE-TXT          PIC X(8)  VALUE SPACES.               
           05  FILLER                     PIC X(11) VALUE                       
               '         : '.                                                   
           05  L231A-NEWTOWN-ZONE         PIC X(25).                            
           05  L231A-PROJECT-NAME         PIC X(32).                            
                                                                                
       01  L231A-HEADER-5.                                                      
           05  FILLER                     PIC X(19)  VALUE                      
               'ROOM-TYPE        : '.                                           
           05  L231A-ROOMTYPE             PIC X(21).                            
                                                                                
       01  L231A-HEADER-6.                                                      
           05  FILLER                     PIC X(19)  VALUE                      
               'APPOINTMENT DATE : '.                                           
           05  L231A-APPTDATE             PIC X(10).                            
                                                                                
       01  L231A-HEADER-7.                                                      
           05  FILLER                     PIC X(19)  VALUE                      
               'APPOINTMENT TIME : '.                                           
           05  L231A-APPT-HH              PIC Z9.                               
           05  FILLER                     PIC X      VALUE '.'.                 
           05  L231A-APPT-MM              PIC 99.                               
           05  FILLER                     PIC X      VALUE SPACE.               
           05  L231A-AM-PM                PIC X(02).                            
                                                                                
       01  L231A-HEADER-8.                                                      
           05  FILLER                     PIC X(48)  VALUE                      
               'REGN NO                 ALSCH NAME'.                            
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  FILLER                     PIC X(27)  VALUE                      
               'CAT  TIME             TIME'.                                    
                                                                                
       01  L231A-HEADER-8-ZONE.                                                 
           05  FILLER                     PIC X(48)  VALUE                      
               'QUEUE NO/HT/REG NO/ ALSCH NAME'.                                
           05  FILLER                     PIC X(5)   VALUE SPACES.              
           05  FILLER                     PIC X(27)  VALUE                      
               'CAT  TIME             TIME'.                                    
                                                                                
       01  L231A-HEADER-9.                                                      
           05  FILLER                     PIC X(08)  VALUE                      
               'QUEUE NO'.                                                      
           05  FILLER                     PIC X(50)  VALUE SPACES.              
           05  FILLER                     PIC X(4)   VALUE                      
               ' IN '.                                                          
           05  FILLER                     PIC X(2)   VALUE SPACES.              
           05  FILLER                     PIC X(9)   VALUE                      
               ' REMARKS '.                                                     
           05  FILLER                     PIC X(3)   VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE                      
               'OUT '.                                                          
                                                                                
       01  L231A-HEADER-10.                                                     
           05  L231A-ULINE1               PIC X(23)  VALUE                      
               '--------               '.                                       
           05  FILLER                     PIC X(30)  VALUE                      
               ' ----- ----'.                                                   
           05  FILLER                     PIC X(27)  VALUE                      
               '---  ----  ---------  ----'.                                    
                                                                                
       01  L231A-SUB-HEADER-1.                                                  
           05  FILLER                     PIC X(19)  VALUE                      
               'APPOINTMENT TIME : '.                                           
           05  L231A-APPT-HH2             PIC Z9.                               
           05  FILLER                     PIC X      VALUE '.'.                 
           05  L231A-APPT-MM2             PIC 99.                               
           05  FILLER                     PIC X      VALUE SPACE.               
           05  L231A-AM-PM2               PIC X(02).                            
                                                                                
       01  L231A-DETAIL-LINE-1.                                                 
           05  L231A-NT-FORMAT.                                                 
               10  L231A-REGN-NO-NT       PIC X(8).                             
               10  FILLER                 PIC X(1)   VALUE SPACE.               
               10  L231A-FT               PIC X(7).                             
               10  L231A-JB-LF            PIC X(6).                             
               10  FILLER                 PIC X(1)   VALUE SPACE.               
           05  L231A-ZONE-FORMAT REDEFINES L231A-NT-FORMAT.                     
               10  L231A-QUEUE-NO         PIC X(5).                             
               10  L231A-SLASH-1          PIC X(1).                             
               10  L231A-HT               PIC X(1).                             
               10  L231A-SLASH-2          PIC X(1).                             
               10  L231A-SEQ-NO           PIC X(7).                             
               10  L231A-SLASH-3          PIC X(1).                             
               10  FILLER-1               PIC X(7).                             
           05  L231A-ZONE-FORMAT2 REDEFINES L231A-ZONE-FORMAT.                  
               10  L231A-QUEUE-NO-A       PIC X(5).                             
               10  L231A-SLASH-1A         PIC X(1).                             
               10  L231A-HT-A             PIC X(1).                             
               10  L231A-SLASH-2A         PIC X(1).                             
               10  L231A-JB-QUEUE-SEQ     PIC X(7).                             
               10  L231A-SLASH-3A         PIC X(1).                             
               10  L231A-SEQ-NO-A         PIC X(7).                             
           05  FILLER                     PIC X      VALUE SPACES.              
           05  L231A-LEFT-PAR             PIC X      VALUE SPACES.              
           05  L231A-ALLOC-SCH            PIC X(3)   VALUE SPACES.              
           05  L231A-RIGHT-PAR            PIC X      VALUE SPACES.              
           05  FILLER                     PIC X(1)   VALUE SPACES.              
           05  L231A-APPL-NAME            PIC X(22)  VALUE SPACES.              
           05  FILLER                     PIC X(2)   VALUE SPACES.              
           05  L231A-CAT                  PIC X      VALUE SPACES.              
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  L231A-REMARKS              PIC X      VALUE SPACES.              
           05  FILLER                     PIC X(05)  VALUE SPACES.              
                                                                                
       01  L231A-DETAIL-LINE-2.                                                 
           05  L231A-QUEUE-NO-2           PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L231A-REGN-NO-ZONE         PIC X(08)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L231A-SERS-TAG             PIC X(04)  VALUE SPACES.              
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L231A-SAPS-TAG             PIC X(04)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L231A-COAPPL-NAME          PIC X(22)  VALUE SPACES.              
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  L231A-MGPS-REMARKS         PIC X(09)  VALUE SPACES.              
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L231A-AST-TAG              PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L231A-PPS-TAG              PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L231A-GEN3-TAG             PIC X(04)  VALUE SPACES.              
                                                                                
       01  L231A-UNDERLINE.                                                     
           05  FILLER                     PIC X(80)  VALUE ALL '-'.             
                                                                                
       01  DJDE-LINE1                      PIC X(80) VALUE                      
           '$DJDE$ JDE=13C230,JDL=BP13,NUMBER=(0000,63,80),END;'.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F205 THRU 200-EXIT                                  
             UNTIL EOF-F205 = 'Y'.                                              
           PERFORM 999-CLOSE-FILES.                                             
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BP13F205                                                 
                       BP13K060                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K76D                                                 
                       BP13K246                                                 
                       BP13K813                                                 
                OUTPUT BP13L231.                                                
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K060 ' WS-K060-STATUS                
              MOVE WS-K060-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K800 ' WS-K800-STATUS                
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K820 ' WS-K820-STATUS                
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K76D-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K76D ' WS-K76D-STATUS                
              MOVE WS-K76D-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K246-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K246 ' WS-K246-STATUS                
              MOVE WS-K246-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K813 ' WS-K813-STATUS                
              MOVE WS-K813-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           MOVE   FUNCTION CURRENT-DATE   TO WS-CURRENT-DATE.                   
           MOVE   WS-CURRENT-DATE (7:2)   TO L231A-RUN-DD.                      
           MOVE   WS-CURRENT-DATE (5:2)   TO L231A-RUN-MM.                      
           MOVE   WS-CURRENT-DATE (1:4)   TO L231A-RUN-CY.                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F205.                                                           
           READ BP13F205 AT END MOVE 'Y' TO EOF-F205                            
                            GO  TO 200-EXIT.                                    
                                                                                
           IF F205-NUM-GRO = 'P'                                                
              MOVE 'Y'    TO  EOF-F205                                          
              GO TO 200-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              MOVE 'NEW TOWN'          TO L231A-NT-ZONE-TXT                     
           ELSE                                                                 
              MOVE 'ZONE'              TO L231A-NT-ZONE-TXT                     
              MOVE ALL '-'             TO L231A-ULINE1                          
              MOVE '/'                 TO L231A-SLASH-1                         
                                          L231A-SLASH-1A                        
                                          L231A-SLASH-2                         
                                          L231A-SLASH-2A                        
                                          L231A-SLASH-3                         
                                          L231A-SLASH-3A                        
           END-IF.                                                              
                                                                                
           MOVE F205-NUM-NT-ZONE   TO WS-NT-ZONE.                               
           MOVE F205-DTE-ALLOCN    TO WS-ALLOC-DATE.                            
           MOVE F205-NUM-FLAT-TYPE TO WS-FT.                                    
           MOVE 'N'                TO EOF-F205.                                 
           ADD  1                  TO WS-F205-READ-CNT.                         
           PERFORM 300-PROCESS-F200 THRU 300-EXIT.                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READ-F200.                                                           
           READ BP13F200 AT END MOVE 'Y' TO EOF-F200                            
                             GO TO 210-EXIT.                                    
                                                                                
           ADD  1               TO WS-F200-READ-CNT.                            
           MOVE 'N'             TO EOF-F200.                                    
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-F200.                                                        
           MOVE 'N' TO EOF-F200.                                                
           OPEN INPUT BP13F200.                                                 
           PERFORM 210-READ-F200 THRU 210-EXIT.                                 
           IF EOF-F200 = 'Y'                                                    
              GO TO 300-EXIT                                                    
           END-IF.                                                              
           PERFORM 400-CHECK-F200 THRU 400-EXIT                                 
             UNTIL EOF-F200 = 'Y'.                                              
                                                                                
           CLOSE BP13F200.                                                      
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CHECK-F200.                                                          
           IF (BP13F200-REC(1:13) = WS-ALLOC-NT-ZONE) AND                       
              (F200-DTE-BKAPPMT-DATE >= WS-CURRENT-DATE)                        
              PERFORM 500-READ-K800     THRU 500-EXIT                           
              PERFORM 600-READ-K060     THRU 600-EXIT                           
              PERFORM 800-PROCESS-PRINT THRU 800-EXIT                           
              PERFORM 900-PRINT-DETAIL  THRU 900-EXIT                           
              MOVE F200-DTE-BKAPPMT-DATE  TO WS-PREV-APPT-DATE                  
              MOVE F200-TME-BKAPPMT       TO WS-PREV-APPT-TIME                  
              MOVE F200-NUM-FLAT-TYPE     TO WS-PREV-RM-TYP                     
              MOVE F200-NUM-NT-ZONE       TO WS-PREV-NT-ZONE.                   
                                                                                
           PERFORM 210-READ-F200 THRU 210-EXIT.                                 
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-READ-K800.                                                           
                                                                                
           MOVE SPACES                TO BP13K800-MASTER.                       
                                                                                
           MOVE F200-NUM-REGN         TO K800-NUM-REGN.                         
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              IF K800-NUM-PORT-ELIG-TAG = 'Y' OR 'P'                            
                 MOVE 'SERS'   TO L231A-SERS-TAG                                
              ELSE                                                              
                 MOVE SPACES   TO L231A-SERS-TAG                                
              END-IF                                                            
              IF K800-NUM-APPS-ELIG-TAG = 'Y'                                   
                 MOVE 'SAPS'   TO L231A-SAPS-TAG                                
              ELSE                                                              
                 MOVE SPACES   TO L231A-SAPS-TAG                                
              END-IF                                                            
              IF K800-NUM-AST-ELIG = 'Y'                                        
                 IF K800-NUM-AST-QUOTA = 'S' OR 'R' OR 'W'                      
                    MOVE 'AST'  TO L231A-AST-TAG                                
                 ELSE                                                           
                    MOVE SPACES TO L231A-AST-TAG                                
                 END-IF                                                         
              ELSE                                                              
                 MOVE SPACES TO L231A-AST-TAG                                   
              END-IF                                                            
              IF K800-NUM-PPS-ELIG = 'Y' OR 'E'                                 
                 IF K800-NUM-PPS-QUOTA = 'S' OR 'R' OR 'W' OR 'A'               
                    MOVE 'PPS'  TO L231A-PPS-TAG                                
                 ELSE                                                           
                    MOVE SPACES TO L231A-PPS-TAG                                
                 END-IF                                                         
              ELSE                                                              
                 MOVE SPACES TO L231A-PPS-TAG                                   
              END-IF                                                            
              IF K800-NUM-GEN3 = 'Y'                                            
                 MOVE '3GEN'    TO L231A-GEN3-TAG                               
              ELSE                                                              
                 MOVE SPACES    TO L231A-GEN3-TAG                               
              END-IF                                                            
              PERFORM 550-READ-K820 THRU 550-EXIT                               
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              MOVE SPACES TO L231A-APPL-NAME                                    
                             L231A-COAPPL-NAME                                  
                             L231A-SERS-TAG  L231A-SAPS-TAG                     
                             L231A-AST-TAG   L231A-PPS-TAG                      
                             L231A-GEN3-TAG                                     
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' WS-K800-STATUS            
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              GO TO 500-EXIT                                                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800 ' WS-K800-STATUS                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
        500-EXIT.                                                               
            EXIT.                                                               
                                                                                
        550-READ-K820.                                                          
                                                                                
            MOVE K800-NUM-REGN     TO K820-NUM-REGN.                            
            MOVE K800-NUM-NRIC1    TO K820-NUM-NRIC.                            
            PERFORM 560-ACCESS-K820 THRU 560-EXIT.                              
            MOVE WS-NAME           TO L231A-APPL-NAME                           
                                                                                
            IF (K800-NUM-NRIC2(2:1) NOT = '#') AND                              
               (K800-NUM-NRIC2      NOT = SPACES AND LOW-VALUES)                
               MOVE K800-NUM-REGN  TO K820-NUM-REGN                             
               MOVE K800-NUM-NRIC2 TO K820-NUM-NRIC                             
               PERFORM 560-ACCESS-K820 THRU 560-EXIT                            
               MOVE WS-NAME        TO L231A-COAPPL-NAME                         
            ELSE                                                                
               MOVE SPACES         TO L231A-COAPPL-NAME.                        
                                                                                
        550-EXIT.                                                               
            EXIT.                                                               
                                                                                
        560-ACCESS-K820.                                                        
                                                                                
            READ BP13K820 KEY IS K820-KEY-FLD.                                  
                                                                                
            IF WS-K820-STATUS = 00                                              
               MOVE K820-NME-OCCP   TO WS-NAME                                  
            ELSE                                                                
            IF WS-K820-STATUS = 23                                              
               MOVE SPACES          TO WS-NAME                                  
               DISPLAY 'RECORD NOT FOUND IN K820 ' WS-K820-STATUS               
               DISPLAY 'K820-KEY-FLD = ' K820-KEY-FLD                           
           ELSE                                                                 
               DISPLAY 'ERROR READING BP13K820 ' WS-K820-STATUS                 
               DISPLAY 'K820-KEY-FLD = ' K820-KEY-FLD                           
               GO TO 999-CLOSE-FILES.                                           
                                                                                
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-READ-K060.                                                           
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              MOVE 02                  TO K060-SERIAL-NO                        
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              MOVE 45                  TO K060-SERIAL-NO                        
           END-IF.                                                              
                                                                                
           IF F200-NUM-SELTYPE = 'BTO'                                          
              PERFORM 700-READ-BP13K813 THRU 700-EXIT                           
              MOVE K813-NME-SITE        TO   WS-NT-ZONE-DESC                    
              STRING '(' K813-NME-PROJECT-LTR ')'                               
                     DELIMITED BY SIZE INTO  WS-STRING-TO-PARSE                 
              MOVE LENGTH OF WS-STRING-TO-PARSE                                 
                                       TO WS-STRING-LENGTH                      
              MOVE 'Y'                 TO WS-INSERT-SPACE-FLAG                  
              PERFORM 930-PARSE-STRING THRU 930-EXIT                            
              MOVE WS-PARSED-STRING    TO  WS-PROJECT-NAME                      
           ELSE                                                                 
              MOVE SPACES               TO   WS-PROJECT-NAME                    
              MOVE F200-NUM-NT-ZONE    TO K060-CODE                             
              READ BP13K060                                                     
              EVALUATE WS-K060-STATUS                                           
                 WHEN 00                                                        
                   MOVE K060-DESC        TO WS-NT-ZONE-DESC                     
                   ADD 1                 TO WS-K060-CNT                         
                 WHEN 23                                                        
                   MOVE SPACES           TO WS-NT-ZONE-DESC                     
                   DISPLAY 'REC NOT FOUND IN BP13K060 ' WS-K060-STATUS          
                   DISPLAY 'K060-KEY-FLD = ' K060-KEY-FLD                       
                 WHEN OTHER                                                     
                   DISPLAY 'ERROR READING BP13K060 ' WS-K060-STATUS             
                   DISPLAY 'K060-KEY-FLD = ' K060-KEY-FLD                       
                   GO TO 999-CLOSE-FILES                                        
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-READ-BP13K813.                                                       
      *-------------------------------------------------------------*           
                                                                                
           MOVE 'N'              TO WS-EOF-K813.                                
           MOVE SPACES           TO K813-KEY-FLD.                               
           MOVE F200-NUM-NT-ZONE TO K813-NUM-ZONE.                              
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD.                                  
           IF WS-K813-STATUS = 00 OR 02                                         
              PERFORM 701-READNEXT-BP13K813 THRU 701-EXIT                       
                   UNTIL WS-EOF-K813 = 'Y'                                      
           ELSE                                                                 
              MOVE SPACES          TO BP13K813-REC                              
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       701-READNEXT-BP13K813.                                                   
      *-------------------------------------------------------------*           
                                                                                
           READ BP13K813 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF-K813.                                 
                                                                                
           IF WS-K813-STATUS = 00 OR 02                                         
              IF K813-NUM-ZONE   = F200-NUM-NT-ZONE                             
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE SPACES       TO BP13K813-REC                              
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES          TO BP13K813-REC                              
           END-IF.                                                              
           MOVE 'Y'                TO WS-EOF-K813.                              
                                                                                
       701-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PROCESS-PRINT.                                                       
           IF (WS-LINE-CNT               = 0)              OR                   
              (F200-NUM-FLAT-TYPE    NOT = WS-PREV-RM-TYP) OR                   
              (F200-NUM-NT-ZONE      NOT = WS-PREV-NT-ZONE) OR                  
              (F200-DTE-BKAPPMT-DATE NOT = WS-PREV-APPT-DATE)                   
              PERFORM 850-PRINT-HEADING  THRU 850-EXIT                          
              MOVE 10                      TO WS-LINE-CNT.                      
                                                                                
           IF F200-TME-BKAPPMT NOT = WS-PREV-APPT-TIME                          
              MOVE 'Y' TO WS-PRINT-TIME.                                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-PRINT-HEADING.                                                       
                                                                                
           ADD  1            TO WS-PAGE-CNT.                                    
           MOVE WS-PAGE-CNT  TO L231A-PAGE.                                     
           MOVE 'Y'          TO WS-PRINT-TIME.                                  
           MOVE L231A-PAGE   TO DJDE-LINE1(36:4).                               
           WRITE PRINT-REC FROM DJDE-LINE1 AFTER PAGE.                          
           WRITE PRINT-REC FROM L231A-HEADER-1.                                 
           WRITE PRINT-REC FROM L231A-HEADER-2  AFTER 1.                        
           WRITE PRINT-REC FROM L231A-HEADER-3 AFTER 1.                         
           MOVE SPACES       TO PRINT-REC.                                      
           WRITE PRINT-REC AFTER 1.                                             
           MOVE F200-DTE-BKAPPMT-DATE      TO WS-DTE-APPT.                      
           STRING F200-DTE-BALLOT(5:2) '/' F200-DTE-BALLOT(1:4)                 
                  DELIMITED BY SIZE INTO L231A-BALLOT-QTR.                      
           WRITE PRINT-REC  FROM L231A-HEADER-3A AFTER 2.                       
           MOVE CORR WS-DTE-APPT           TO WS-DTE-ALLOC.                     
           MOVE WS-DTE-ALLOC               TO L231A-APPTDATE.                   
           MOVE WS-NT-ZONE-DESC            TO L231A-NEWTOWN-ZONE.               
           MOVE WS-PROJECT-NAME            TO L231A-PROJECT-NAME.               
           IF F200-NUM-SELTYPE = 'BTO'                                          
              IF K813-NUM-FLAT-TYPE = '4P' OR '5P'                              
                 STRING K813-NUM-FLAT-TYPE (1:1) '-ROOM'                        
                        ' (PAIRED UNITS)'                                       
                        DELIMITED BY SIZE                                       
                        INTO L231A-ROOMTYPE                                     
              ELSE                                                              
                  IF K813-NUM-FLAT-TYPE = '2F'                                  
                     MOVE '2-ROOM FLEXI FLAT' TO L231A-ROOMTYPE                 
                  ELSE                                                          
                     STRING K813-NUM-FLAT-TYPE (1:1) '-ROOM'                    
                            DELIMITED BY SIZE                                   
                           INTO L231A-ROOMTYPE                                  
                  END-IF                                                        
              END-IF                                                            
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = 'SE'                                         
      **      MOVE F200-NUM-ACTUAL-FT TO L231A-ROOMTYPE                         
              MOVE 'SE'     TO L231A-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = '4D'                                         
              MOVE 'TYPE-A' TO L231A-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = '5D'                                         
              MOVE 'TYPE-B' TO L231A-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = 'ED'                                         
              MOVE 'TYPE-C' TO L231A-ROOMTYPE                                   
           ELSE                                                                 
           IF F200-NUM-FLAT-TYPE = 'E '                                         
              MOVE ' EXEC ' TO L231A-ROOMTYPE                                   
           ELSE                                                                 
              MOVE F200-NUM-FLAT-TYPE  TO L231A-ROOMTYPE.                       
                                                                                
           IF F200-NUM-SELTYPE = 'SBF'                                          
              PERFORM 970-READ-K813    THRU 970-EXIT                            
              IF K813-NUM-MATURE-EST-TAG = 'N'                                  
                 IF F200-NUM-FLAT-TYPE = '3P'                                   
                    MOVE '3-ROOM PREMIUM' TO L231A-ROOMTYPE                     
                 ELSE                                                           
                    IF F200-NUM-FLAT-TYPE = '3 '                                
                       MOVE '3-ROOM STANDARD' TO L231A-ROOMTYPE                 
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F200-NUM-SELTYPE = 'SBF'                                          
              PERFORM 920-READ-K246    THRU 920-EXIT                            
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC  FROM L231A-HEADER-4 AFTER 1.                        
           WRITE PRINT-REC  FROM L231A-HEADER-5 AFTER 1.                        
           WRITE PRINT-REC  FROM L231A-HEADER-6 AFTER 1.                        
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              WRITE PRINT-REC  FROM L231A-HEADER-8  AFTER 2                     
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              WRITE PRINT-REC  FROM L231A-HEADER-8-ZONE AFTER 2                 
           END-IF.                                                              
           WRITE PRINT-REC  FROM L231A-HEADER-9 AFTER 1.                        
           WRITE PRINT-REC  FROM L231A-HEADER-10 AFTER 1.                       
           MOVE SPACES      TO   PRINT-REC.                                     
           WRITE PRINT-REC                       AFTER 1.                       
           MOVE SPACES      TO   PRINT-REC.                                     
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-PRINT-DETAIL.                                                        
                                                                                
           IF WS-PRINT-TIME = 'Y'                                               
              MOVE F200-TME-BKAPPMT  TO WS-TIME                                 
              PERFORM 905-TIME-CHECK THRU 905-EXIT                              
              MOVE WS-HH             TO L231A-APPT-HH2                          
              MOVE WS-MM             TO L231A-APPT-MM2                          
              ADD  1                 TO WS-LINE-CNT                             
              WRITE PRINT-REC FROM L231A-SUB-HEADER-1 AFTER 1                   
              MOVE SPACES            TO PRINT-REC                               
              WRITE PRINT-REC AFTER 1                                           
              MOVE 'N'               TO WS-PRINT-TIME.                          
                                                                                
           MOVE K800-NUM-CAT         TO L231A-CAT.                              
                                                                                
           PERFORM 910-READ-BP13K76D THRU 910-EXIT.                             
           IF WS-K76D-FND = 'Y'                                                 
              MOVE 'B'               TO L231A-REMARKS                           
           ELSE                                                                 
              MOVE SPACE             TO L231A-REMARKS                           
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME = 'PA'                                       
              MOVE SPACES               TO L231A-LEFT-PAR                       
              MOVE SPACES               TO L231A-ALLOC-SCH                      
              MOVE SPACES               TO L231A-RIGHT-PAR                      
           ELSE                                                                 
              MOVE '('                  TO L231A-LEFT-PAR                       
              MOVE K800-NUM-ALLO-SCHEME TO L231A-ALLOC-SCH                      
              MOVE ')'                  TO L231A-RIGHT-PAR.             .       
                                                                                
           IF K800-NUM-MGPS = 'P' OR 'C'                                        
              MOVE '(MGPS PA)'          TO L231A-MGPS-REMARKS                   
           ELSE                                                                 
              MOVE SPACES               TO L231A-MGPS-REMARKS                   
           END-IF.                                                              
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              MOVE F200-NUM-REGN           TO L231A-REGN-NO-NT                  
              MOVE F200-NUM-HHTY-QUEUE-SERIAL TO L231A-QUEUE-NO-2               
              IF F200-NUM-FLAT-TYPE = 'SE'                                      
                 EVALUATE K800-NUM-FLAT-TYPE                                    
                   WHEN '2 '                                                    
                     MOVE '(2-RM)' TO L231A-FT                                  
                   WHEN '2F'                                                    
                     MOVE '(2F-RM)' TO L231A-FT                                 
                   WHEN '3 '                                                    
                     MOVE '(3-RM)' TO L231A-FT                                  
                   WHEN '3P'                                                    
                     MOVE '(3P-RM)' TO L231A-FT                                 
                   WHEN '4 '                                                    
                     MOVE '(4-RM)' TO L231A-FT                                  
                   WHEN '5 '                                                    
                     MOVE '(5-RM)' TO L231A-FT                                  
                 END-EVALUATE                                                   
                 PERFORM 901-CHECK-JB-LF          THRU 901-EXIT                 
              ELSE                                                              
                 MOVE SPACES TO L231A-FT                                        
              END-IF                                                            
              WRITE PRINT-REC FROM L231A-DETAIL-LINE-1 AFTER 1                  
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              IF F200-NUM-JB-QUEUE-SEQUENCE = SPACES OR LOW-VALUES              
                 MOVE F200-NUM-HHTY-QUEUE-SERIAL TO L231A-QUEUE-NO              
                 EVALUATE F200-NUM-HHTY-QUEUE-ALPHA                             
                   WHEN 'H'                                                     
                        MOVE 'F' TO L231A-HT                                    
                   WHEN 'G'                                                     
                        MOVE 'U' TO L231A-HT                                    
                   WHEN 'C'                                                     
                        MOVE 'G' TO L231A-HT                                    
                   WHEN OTHER                                                   
                        MOVE F200-NUM-HHTY-QUEUE-ALPHA TO L231A-HT              
                 END-EVALUATE                                                   
                 MOVE F200-NUM-SEQUENCE          TO L231A-SEQ-NO                
                 MOVE '/'                        TO L231A-SLASH-3               
                 MOVE SPACES                     TO FILLER-1                    
              ELSE                                                              
                 MOVE F200-NUM-HHTY-QUEUE-SERIAL TO L231A-QUEUE-NO-A            
                 EVALUATE F200-NUM-HHTY-QUEUE-ALPHA                             
                   WHEN 'H'                                                     
                        MOVE 'F' TO L231A-HT-A                                  
                   WHEN 'G'                                                     
                        MOVE 'U' TO L231A-HT-A                                  
                   WHEN 'C'                                                     
                        MOVE 'G' TO L231A-HT-A                                  
                   WHEN OTHER                                                   
                        MOVE F200-NUM-HHTY-QUEUE-ALPHA TO L231A-HT-A            
                 END-EVALUATE                                                   
                 MOVE F200-NUM-SEQUENCE          TO L231A-SEQ-NO-A              
                 MOVE '/'                        TO L231A-SLASH-3A              
                 MOVE F200-NUM-JB-QUEUE-SEQUENCE TO L231A-JB-QUEUE-SEQ          
              END-IF                                                            
              MOVE F200-NUM-REGN              TO L231A-REGN-NO-ZONE             
              WRITE PRINT-REC FROM L231A-DETAIL-LINE-1 AFTER 1                  
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC FROM L231A-DETAIL-LINE-2 AFTER 1.                    
           WRITE PRINT-REC FROM L231A-UNDERLINE     AFTER 1.                    
                                                                                
           ADD 3                        TO WS-LINE-CNT.                         
           IF WS-LINE-CNT > 42                                                  
              MOVE 0                    TO WS-LINE-CNT.                         
                                                                                
        900-EXIT.                                                               
            EXIT.                                                               
                                                                                
        901-CHECK-JB-LF.                                                        
                                                                                
            MOVE SPACES       TO WS-JB                                          
                                 WS-LF                                          
                                 L231A-JB-LF.                                   
                                                                                
            IF K800-NUM-JT-BALLOT-MTF = 8                                       
               MOVE 'JB'      TO WS-JB                                          
            END-IF.                                                             
                                                                                
            IF K800-NUM-LOW-FLOOR = 'G' OR 'Y'                                  
               MOVE K800-NUM-LOW-FLOOR   TO WS-LF                               
            END-IF.                                                             
                                                                                
            IF WS-JB NOT = SPACES AND                                           
               WS-LF NOT = SPACES                                               
               STRING '/'  WS-JB                                                
                      '/'  WS-LF                                                
                      DELIMITED BY SIZE INTO L231A-JB-LF                        
               END-STRING                                                       
            END-IF.                                                             
                                                                                
            IF WS-JB NOT = SPACES AND                                           
               WS-LF = SPACES                                                   
               STRING '/'  WS-JB                                                
                      DELIMITED BY SIZE INTO L231A-JB-LF                        
               END-STRING                                                       
            END-IF.                                                             
                                                                                
            IF WS-JB = SPACES AND                                               
               WS-LF NOT = SPACES                                               
               STRING '/'  WS-LF                                                
                      DELIMITED BY SIZE INTO L231A-JB-LF                        
               END-STRING                                                       
            END-IF.                                                             
                                                                                
        901-EXIT.                                                               
            EXIT.                                                               
                                                                                
        905-TIME-CHECK.                                                         
                                                                                
           IF WS-HH = 12                                                        
              MOVE 'PM'           TO L231A-AM-PM2                               
           ELSE                                                                 
           IF WS-HH < 12                                                        
              MOVE 'AM'           TO L231A-AM-PM2                               
           ELSE                                                                 
              MOVE 'PM'           TO L231A-AM-PM2                               
              SUBTRACT 12 FROM WS-HH.                                           
                                                                                
        905-EXIT.                                                               
            EXIT.                                                               
                                                                                
        910-READ-BP13K76D.                                                      
                                                                                
           MOVE F200-NUM-SELTYPE       TO K76D-NUM-SALES-MODE.                  
           MOVE F200-DTE-BALLOT        TO K76D-NUM-DTE-BALLOT.                  
           MOVE F200-NUM-REGN          TO K76D-NUM-REGN.                        
                                                                                
           READ BP13K76D.                                                       
           EVALUATE WS-K76D-STATUS                                              
             WHEN   00                                                          
                    MOVE 'Y'      TO WS-K76D-FND                                
             WHEN   23                                                          
                    MOVE 'N'      TO WS-K76D-FND                                
             WHEN   OTHER                                                       
                    DISPLAY 'READ BP13K76D ERROR ' K76D-NUM-REGN                
                    DISPLAY 'STATUS ' WS-K800-STATUS                            
                    MOVE WS-K76D-STATUS TO RETURN-CODE                          
                    PERFORM 999-CLOSE-FILES                                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
        910-EXIT.                                                               
           EXIT.                                                                
                                                                                
        920-READ-K246.                                                          
                                                                                
           EVALUATE F200-NUM-FLAT-TYPE                                          
             WHEN '1A'                                                          
             WHEN '2A'                                                          
              MOVE 'SA       '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '2 '                                                          
             WHEN '2F'                                                          
              MOVE '2-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '3 '                                                          
              MOVE '3-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '3P'                                                          
              MOVE '3P-ROOM  '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '4 '                                                          
              MOVE '4-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN '5 '                                                          
              MOVE '5-ROOM   '        TO  K246-NUM-FLAT-TYPE                    
             WHEN 'E '                                                          
              MOVE 'EXECUTIVE'        TO  K246-NUM-FLAT-TYPE                    
           END-EVALUATE.                                                        
           MOVE K060-DESC             TO  K246-NUM-NEW-TOWN.                    
           MOVE F200-DTE-BALLOT       TO  K246-DTE-BALLOT.                      
                                                                                
           READ BP13K246.                                                       
                                                                                
           IF WS-K246-STATUS = 00                                               
              IF K246-COMBINED-NT NOT = SPACES AND LOW-VALUES                   
                 IF K246-NUM-NT-TAG = 'Y'                                       
                    STRING K246-NUM-NEW-TOWN '/ '                               
                           K246-COMBINED-NT                                     
                           DELIMITED BY SIZE INTO WS-STRING-TO-PARSE            
                    END-STRING                                                  
                    MOVE LENGTH OF WS-STRING-TO-PARSE                           
                                      TO WS-STRING-LENGTH                       
                    MOVE 'Y'          TO WS-INSERT-SPACE-FLAG                   
                    PERFORM 930-PARSE-STRING    THRU 930-EXIT                   
                    MOVE WS-PARSED-STRING   TO  L231A-NEWTOWN-ZONE              
                 ELSE                                                           
                    STRING K246-COMBINED-NT '/ '                                
                           K246-NUM-NEW-TOWN                                    
                           DELIMITED BY SIZE INTO WS-STRING-TO-PARSE            
                    END-STRING                                                  
                    MOVE LENGTH OF WS-STRING-TO-PARSE                           
                                      TO WS-STRING-LENGTH                       
                    MOVE 'Y'          TO WS-INSERT-SPACE-FLAG                   
                    PERFORM 930-PARSE-STRING    THRU 930-EXIT                   
                    MOVE WS-PARSED-STRING   TO  L231A-NEWTOWN-ZONE              
                 END-IF                                                         
              END-IF                                                            
              IF K246-COMBINED-FT NOT = SPACES AND LOW-VALUES                   
                 IF F200-NUM-NT-ZONE = 'CT '                                    
                    IF F200-NUM-FLAT-TYPE = '4 '                                
                       MOVE 'S1 APT   '        TO K246-NUM-FLAT-TYPE            
                    ELSE                                                        
                       IF F200-NUM-FLAT-TYPE = '5 '                             
                          MOVE 'S2 APT   '        TO K246-NUM-FLAT-TYPE         
                       END-IF                                                   
                    END-IF                                                      
                    STRING K246-COMBINED-FT(1:1) '/'                            
                           K246-NUM-FLAT-TYPE(1:2)                              
                           DELIMITED BY SIZE                                    
                           INTO  L231A-ROOMTYPE                                 
                    END-STRING                                                  
                 ELSE                                                           
                    IF K246-NUM-FT-TAG = 'N'                                    
                       STRING K246-COMBINED-FT(1:1) '/'                         
                              K246-NUM-FLAT-TYPE(1:1)                           
                              DELIMITED BY SIZE                                 
                              INTO  L231A-ROOMTYPE                              
                       END-STRING                                               
                    ELSE                                                        
                       STRING K246-NUM-FLAT-TYPE(1:1) '/'                       
                              K246-COMBINED-FT(1:1)                             
                              DELIMITED BY SIZE INTO L231A-ROOMTYPE             
                       END-STRING                                               
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF WS-K246-STATUS = 23                                            
                 DISPLAY 'REC NOT FOUND IN BP13K246 ' K246-KEY-FLD              
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K246 ' WS-K246-STATUS               
                 DISPLAY 'K246-KEY-FLD = ' K246-KEY-FLD                         
                 PERFORM 999-CLOSE-FILES                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
        920-EXIT.                                                               
            EXIT.                                                               
                                                                                
        930-PARSE-STRING.                                                       
                                                                                
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 940-GET-CHAR           THRU 940-EXIT                         
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
        930-EXIT.                                                               
            EXIT.                                                               
                                                                                
        940-GET-CHAR.                                                           
                                                                                
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
        940-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------          
       970-READ-K813.                                                           
      *---------------------------------------------------------------          
           MOVE F200-NUM-NT-ZONE     TO  K813-NUM-ZONE.                         
           MOVE F200-NUM-FLAT-TYPE   TO  K813-NUM-FLAT-TYPE.                    
           MOVE F200-DTE-BALLOT      TO  K813-DTE-BALLOT.                       
           READ BP13K813.                                                       
           IF WS-K813-STATUS = ZERO                                             
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACES        TO BP13K813-REC                                
           END-IF.                                                              
                                                                                
       970-EXIT.                                                                
           EXIT.                                                                
                                                                                
        999-CLOSE-FILES.                                                        
           CLOSE BP13F205                                                       
                 BP13K060                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K76D                                                       
                 BP13K246                                                       
                 BP13K813                                                       
                 BP13L231.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========END OF BP13C231============*'.                  
           DISPLAY SPACES.                                                      
           DISPLAY '   NO. OF F205 RECORDS READ = ' WS-F205-READ-CNT.           
           DISPLAY '   NO. OF F200 RECORDS READ = ' WS-F200-READ-CNT.           
           DISPLAY '   NO. OF K060 RECORDS READ = ' WS-K060-CNT.                
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           IF WS-K060-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K060  CLOSE ERROR ' WS-K060-STATUS.             
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K800  CLOSE ERROR ' WS-K800-STATUS.             
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K820  CLOSE ERROR ' WS-K820-STATUS.             
           IF WS-K76D-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K76D  CLOSE ERROR ' WS-K76D-STATUS.             
           IF WS-K246-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K246  CLOSE ERROR ' WS-K246-STATUS.             
           IF WS-K813-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K813  CLOSE ERROR ' WS-K813-STATUS.             
                                                                                
           STOP RUN.                                                            
