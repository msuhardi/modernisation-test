      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID. BP13C838.                                                    
      *AUTHOR.     TEREZA SALONGA.                                              
                                                                                
      * ========================================================== *            
      *               SYSTEM OF COMMITMENT (BP13)                  *            
      * ========================================================== *            
      *  OBJECTIVES :                                              *            
      *    1.  TO UPDATE RESULT TO BP13K752.                       *            
      *                                                            *            
      *    INPUT  FILES: BP13F200 - BP13.F200.BKAPPMT              *            
      *    I-O    FILES: BP13K752 - WIS/3R STATUS AND RESULT FILE  *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE        DESCRIPTION                     *            
      * -------- ----  ----------  ----------------------------    *            
      * BP133058 MTSR1 03/04/07    NEW PROGRAM                     *            
      * ========================================================== *            
                                                                                
      *---------------------------*                                             
       ENVIRONMENT DIVISION.                                                    
      *---------------------------*                                             
       CONFIGURATION SECTION.                                                   
      *---------------------------*                                             
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
      *---------------------------*                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *---------------------------*                                             
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13K752 ASSIGN TO BP13K752                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K752-KEY-FLD                               
                  FILE STATUS     IS WS-K752-STATUS.                            
                                                                                
                                                                                
      *---------------------------*                                             
       DATA DIVISION.                                                           
      *---------------------------*                                             
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K752                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K752.                                                           
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CURR-DATE              PIC X(8).                              
           05  WS-NUM-UIN                PIC X(9)    VALUE SPACES.              
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F200-EOF               PIC X     VALUE 'N'.                   
           05  WS-K752-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-PAGE-CNT               PIC 9(4)  VALUE ZEROES.                
           05  WS-LINE-CNT               PIC 9(5)  VALUE 56.                    
           05  WS-F200-READ              PIC 9(5)  VALUE ZEROES.                
           05  WS-F200-PROCESSED         PIC 9(5)  VALUE ZEROES.                
           05  WS-K752-REWRITE           PIC 9(5)  VALUE ZEROES.                
           05  WS-K752-ERROR             PIC 9(5)  VALUE ZEROES.                
           05  WS-K752-NOTFND            PIC 9(5)  VALUE ZEROES.                
           05  WS-K752-READ-ERR          PIC 9(5)  VALUE ZEROES.                
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
                   UNTIL WS-F200-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F200                                                 
                I-O    BP13K752.                                                
                                                                                
           IF WS-K752-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING ERROR, K752-STATUS ' WS-K752-STATUS              
              MOVE WS-K752-STATUS          TO   RETURN-CODE.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-CURR-DATE.                   
                                                                                
           PERFORM 3000-READ-BP13F200      THRU 3000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-PROCESS.                                                            
      ******************************************************************        
           IF (F200-NUM-NRIC1 NOT = SPACES AND LOW-VALUES) AND                  
              (F200-NUM-NRIC1(1:1) NOT = '#')                                   
              MOVE    SPACES               TO   WS-NUM-UIN                      
              MOVE    F200-NUM-NRIC1       TO   WS-NUM-UIN                      
              ADD     1                    TO   WS-F200-PROCESSED               
              PERFORM 4000-READ-BP13K752   THRU 4000-EXIT.                      
                                                                                
           IF (F200-NUM-NRIC2 NOT = SPACES AND LOW-VALUES) AND                  
              (F200-NUM-NRIC2(1:1) NOT = '#')                                   
              MOVE    SPACES               TO   WS-NUM-UIN                      
              MOVE    F200-NUM-NRIC2       TO   WS-NUM-UIN                      
              ADD     1                    TO   WS-F200-PROCESSED               
              PERFORM 4000-READ-BP13K752   THRU 4000-EXIT.                      
                                                                                
           IF (F200-NUM-NRIC3 NOT = SPACES AND LOW-VALUES) AND                  
              (F200-NUM-NRIC3(1:1) NOT = '#')                                   
              MOVE    SPACES               TO   WS-NUM-UIN                      
              MOVE    F200-NUM-NRIC3       TO   WS-NUM-UIN                      
              ADD     1                    TO   WS-F200-PROCESSED               
              PERFORM 4000-READ-BP13K752   THRU 4000-EXIT.                      
                                                                                
           IF (F200-NUM-NRIC4 NOT = SPACES AND LOW-VALUES) AND                  
              (F200-NUM-NRIC4(1:1) NOT = '#')                                   
              MOVE    SPACES               TO   WS-NUM-UIN                      
              MOVE    F200-NUM-NRIC4       TO   WS-NUM-UIN                      
              ADD     1                    TO   WS-F200-PROCESSED               
              PERFORM 4000-READ-BP13K752   THRU 4000-EXIT.                      
                                                                                
           PERFORM 3000-READ-BP13F200      THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13F200.                                                      
      ******************************************************************        
           READ BP13F200                                                        
                AT END MOVE 'Y'            TO   WS-F200-EOF                     
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F200-READ.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K752.                                                      
      ******************************************************************        
           MOVE SPACES                     TO   BP13K752-REC.                   
           INITIALIZE                           BP13K752-REC.                   
                                                                                
           MOVE WS-NUM-UIN                 TO   K752-NUM-UIN.                   
      *    MOVE '3R '                      TO   K752-NUM-SALES-MODE.            
           MOVE F200-NUM-NT-ZONE           TO   K752-NUM-SALES-MODE.            
                                                                                
           READ BP13K752.                                                       
                                                                                
           EVALUATE WS-K752-STATUS                                              
              WHEN 00                                                           
                   PERFORM 5000-REWRITE-BP13K752 THRU 5000-EXIT                 
              WHEN 23                                                           
                   DISPLAY WS-NUM-UIN '3R - NOT FOUND IN BP13K752'              
                   ADD   1                 TO   WS-K752-NOTFND                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K752, STATUS '                    
                   WS-K752-STATUS ' KEY ' WS-NUM-UIN ' 3R'                      
                   MOVE WS-K752-STATUS     TO   RETURN-CODE                     
                   ADD   1                 TO   WS-K752-READ-ERR                
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-REWRITE-BP13K752.                                                   
      ******************************************************************        
           PERFORM 7000-MOVE-DETAILS       THRU 7000-EXIT.                      
                                                                                
           REWRITE BP13K752-REC.                                                
                                                                                
           EVALUATE WS-K752-STATUS                                              
           WHEN 00                                                              
              ADD   1                      TO   WS-K752-REWRITE                 
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING BP13K752, STATUS ' WS-K752-STATUS        
                 ' KEY ' K752-KEY-FLD                                           
              MOVE WS-K752-STATUS          TO   RETURN-CODE                     
              ADD   1                      TO   WS-K752-ERROR                   
              GO TO 9000-CLOSE-FILES                                            
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       7000-MOVE-DETAILS.                                                       
      ******************************************************************        
           MOVE 'S'                        TO K752-NUM-RESULT.                  
           MOVE F200-DTE-BKAPPMT-DATE      TO K752-DTE-APPT.                    
           MOVE F200-TME-BKAPPMT           TO K752-TME-APPT.                    
           MOVE F200-NUM-HHTY-QUEUE-SERIAL TO K752-NUM-QUEUE.                   
           MOVE F200-NUM-QUEUE-FT          TO K752-NUM-QUEUE-BY-FT.             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO K752-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO K752-TME-UPDATE.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY ' '.                                                         
           DISPLAY ' *****  BP13C838  ***** '.                                  
           DISPLAY ' NO. OF REC READ FROM F200   = ' WS-F200-READ.              
           DISPLAY ' ------------------------------'.                           
           DISPLAY ' NO. OF UIN PROCESSED (F200) = ' WS-F200-PROCESSED.         
           DISPLAY ' NO. OF REC REWRITTEN        = ' WS-K752-REWRITE.           
           DISPLAY ' NO. OF REC REWR ERROR       = ' WS-K752-ERROR.             
           DISPLAY ' NO. OF REC NOTFND IN K752   = ' WS-K752-NOTFND.            
           DISPLAY ' NO. OF REC WITH READ ERROR  = ' WS-K752-READ-ERR.          
                                                                                
           CLOSE BP13F200                                                       
                 BP13K752.                                                      
                                                                                
           IF WS-K752-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING ERROR, K752-STATUS ' WS-K752-STATUS              
              MOVE WS-K752-STATUS          TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
      * >>>>>>>>>>>>>>>>>>>>>>> END OF BP13AC30 <<<<<<<<<<<<<<<<<<<<<< *        
      *----------------------------------------------------------------*        
