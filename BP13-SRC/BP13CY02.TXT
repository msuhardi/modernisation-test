      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY02.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  14/04/08.                                                 
      *****************************************************************         
      * OBJECTIVE - EXTRACT APPLICATION UNDER LAUNCH-DATE AND NT                
      *                                                                         
      *     INPUT FILE :                                                        
      *                  1. BP13F730                                            
      *                  2. BP13F813                                            
      *                  3. BP13F205                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. P13F730A                                            
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP133340  14/04/08  JB8  NEW PROGRAM.                                   
      *           14/07/08  JB8  CHANGE BP13K767(150) TO BP13K813(400)          
      * BP133580  01/12/08  JB8  CATER FOR EMPTY NT                             
      * BP133891  19/05/10  PCL3 TO HANDLE F205-DTE-RESELECTION FOR             
      *                          BP13AY0D JOB                                   
      * BP134304  04082011  PCL3 TO INITIALIZE FIELDS FOR INVITED AND *         
      *                          NOT INVITED CASES.                   *         
      * BP134471  120312    ESA1 TO USE F730-NUM-SPRQUOTA-TAG         *         
      * BP135340  12/05/14  IMC1 TO REPLACE BP13K816 WITH BP13K813    *         
      * BP135485  19/09/14  SMR2 REMOVE SPRQUOTA-TAG                  *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F730  ASSIGN        TO BP13F730.                         
           SELECT  BP13F813  ASSIGN        TO BP13F813.                         
           SELECT  P13F730A  ASSIGN        TO P13F730A.                         
           SELECT  P13F730B  ASSIGN        TO P13F730B.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F730                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F813                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 1000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13K813.                                                           
                                                                                
       FD   P13F730A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F730A-REC                PIC X(500).                              
                                                                                
       FD   P13F730B                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F730B-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-EOF-BP13F813         PIC X     VALUE 'N'.                     
           05  WS-EOF-BP13F730         PIC X     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-BP13F730-READ         PIC 9(7)  VALUE ZEROES.                  
           05 WS-BP13F813-READ         PIC 9(7)  VALUE ZEROES.                  
           05 WS-P13F730A-WRITEN       PIC 9(7)  VALUE ZEROES.                  
           05 WS-P13F730B-WRITEN       PIC 9(7)  VALUE ZEROES.                  
           05 WS-P13F730A-AY0D         PIC 9(7)  VALUE ZEROES.                  
           05 WS-P13F730B-AY0D         PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1500-READ-BP13F205      THRU 1500-EXIT.                      
                                                                                
           PERFORM 2100-READ-BP13F813      THRU 2100-EXIT                       
           PERFORM 2200-READ-BP13F730      THRU 2200-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
                   UNTIL K813-NUM-ZONE = HIGH-VALUES AND                        
                         F730-CDE-NT1  = HIGH-VALUES.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F813                                                 
                       BP13F730                                                 
                       BP13F205                                                 
                OUTPUT P13F730A                                                 
                       P13F730B.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1500-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       1500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F813.                                                      
      ******************************************************************        
                                                                                
           READ BP13F813           AT   END                                     
                MOVE HIGH-VALUES   TO   K813-NUM-ZONE                           
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F813-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2200-READ-BP13F730.                                                      
      ******************************************************************        
                                                                                
           READ BP13F730           AT   END                                     
                MOVE HIGH-VALUES   TO   F730-CDE-NT1                            
                MOVE 'Y'           TO   WS-EOF-BP13F730                         
                GO                 TO   2200-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F730-READ.                       
                                                                                
       2200-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF K813-NUM-ZONE = '###'                                             
              PERFORM UNTIL WS-EOF-BP13F730 = 'Y'                               
                 PERFORM 4000-WRITE-P13F730A  THRU 4000-EXIT                    
                 PERFORM 2200-READ-BP13F730   THRU 2200-EXIT                    
              END-PERFORM                                                       
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF K813-NUM-ZONE = F730-CDE-NT1                                      
              PERFORM 4000-WRITE-P13F730A     THRU 4000-EXIT                    
              PERFORM 2200-READ-BP13F730      THRU 2200-EXIT                    
           ELSE                                                                 
              IF K813-NUM-ZONE > F730-CDE-NT1                                   
                 PERFORM 5000-WRITE-P13F730B  THRU 5000-EXIT                    
                 PERFORM 2200-READ-BP13F730   THRU 2200-EXIT                    
              ELSE                                                              
                 PERFORM 2100-READ-BP13F813   THRU 2100-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      *********************                                                     
       4000-WRITE-P13F730A.                                                     
      *********************                                                     
                                                                                
           IF F205-NUM-OPTION = 'YD'                                            
              IF F730-DTE-REQUEST = F205-DTE-ALLOCN                             
                 IF F730-TME-SYSTEM(1:4) > F205-DTE-RESELECTION(1:4)            
                    MOVE SPACES      TO F730-CDE-CITIZENSHIP-HOUSEHOLD          
                                        F730-KEY-FLD                            
                                                                                
                    WRITE P13F730A-REC          FROM BP13F730-REC               
                    ADD 1                         TO WS-P13F730A-WRITEN         
                 ELSE                                                           
                    ADD 1                         TO WS-P13F730A-AY0D           
                 END-IF                                                         
              ELSE                                                              
                 IF F730-DTE-REQUEST = F205-DTE-END                             
                    IF F730-TME-SYSTEM(1:4) <= F205-DTE-RESELECTION(5:4)        
                       MOVE SPACES   TO F730-CDE-CITIZENSHIP-HOUSEHOLD          
                                        F730-KEY-FLD                            
                                                                                
                       WRITE P13F730A-REC      FROM BP13F730-REC                
                       ADD 1                     TO WS-P13F730A-WRITEN          
                    ELSE                                                        
                       ADD 1                     TO WS-P13F730A-AY0D            
                    END-IF                                                      
                 ELSE                                                           
                    IF F730-DTE-REQUEST >= F205-DTE-ALLOCN AND                  
                       F730-DTE-REQUEST <= F205-DTE-END                         
                       MOVE SPACES   TO F730-CDE-CITIZENSHIP-HOUSEHOLD          
                                        F730-KEY-FLD                            
                                                                                
                       WRITE P13F730A-REC      FROM BP13F730-REC                
                       ADD 1                     TO WS-P13F730A-WRITEN          
                    ELSE                                                        
                       ADD 1                     TO WS-P13F730A-AY0D            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES           TO F730-CDE-CITIZENSHIP-HOUSEHOLD           
                                       F730-KEY-FLD                             
                                                                                
              WRITE P13F730A-REC              FROM BP13F730-REC                 
              ADD 1                             TO WS-P13F730A-WRITEN           
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-WRITE-P13F730B.                                                     
      *********************                                                     
                                                                                
           IF F205-NUM-OPTION = 'YD'                                            
              IF F730-DTE-REQUEST = F205-DTE-ALLOCN                             
                 IF F730-TME-SYSTEM(1:4) > F205-DTE-RESELECTION(1:4)            
                    MOVE SPACES   TO F730-CDE-CITIZENSHIP-HOUSEHOLD             
                                     F730-KEY-FLD                               
                                                                                
                    WRITE P13F730B-REC          FROM BP13F730-REC               
                    ADD 1                         TO WS-P13F730B-WRITEN         
                 ELSE                                                           
                    ADD 1                         TO WS-P13F730B-AY0D           
                 END-IF                                                         
              ELSE                                                              
                 IF F730-DTE-REQUEST = F205-DTE-END                             
                    IF F730-TME-SYSTEM(1:4) <= F205-DTE-RESELECTION(5:4)        
                       MOVE SPACES TO F730-CDE-CITIZENSHIP-HOUSEHOLD            
                                      F730-KEY-FLD                              
                                                                                
                       WRITE P13F730B-REC      FROM BP13F730-REC                
                       ADD 1                     TO WS-P13F730B-WRITEN          
                    ELSE                                                        
                       ADD 1                     TO WS-P13F730B-AY0D            
                    END-IF                                                      
                 ELSE                                                           
                    IF F730-DTE-REQUEST >= F205-DTE-ALLOCN AND                  
                       F730-DTE-REQUEST <= F205-DTE-END                         
                       MOVE SPACES TO F730-CDE-CITIZENSHIP-HOUSEHOLD            
                                      F730-KEY-FLD                              
                                                                                
                       WRITE P13F730B-REC      FROM BP13F730-REC                
                       ADD 1                     TO WS-P13F730B-WRITEN          
                    ELSE                                                        
                       ADD 1                     TO WS-P13F730B-AY0D            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES           TO F730-CDE-CITIZENSHIP-HOUSEHOLD           
                                       F730-KEY-FLD                             
                                                                                
              WRITE P13F730B-REC              FROM BP13F730-REC                 
              ADD 1                             TO WS-P13F730B-WRITEN           
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CY02'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F813 RECORDS READ..........: '                    
                    WS-BP13F813-READ.                                           
           DISPLAY 'NO OF BP13F730 RECORDS READ..........: '                    
                    WS-BP13F730-READ.                                           
           DISPLAY 'NO OF P13F730A RECORDS WRITTEN.......: '                    
                    WS-P13F730A-WRITEN.                                         
           DISPLAY 'NO OF P13F730B RECORDS WRITTEN.......: '                    
                    WS-P13F730B-WRITEN.                                         
           DISPLAY 'NO OF P13F730A RECORDS BYPASS - AY0D : '                    
                    WS-P13F730A-AY0D.                                           
           DISPLAY 'NO OF P13F730B RECORDS BYPASS - AY0D : '                    
                    WS-P13F730B-AY0D.                                           
                                                                                
           CLOSE BP13F813                                                       
                 BP13F730                                                       
                 BP13F205                                                       
                 P13F730A                                                       
                 P13F730B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
