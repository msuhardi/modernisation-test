      *===*===*===*===*===*====*                                                
       IDENTIFICATION DIVISION.                                                 
      *===*===*===*===*===*====*                                                
       PROGRAM-ID.    BP13C290.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  20/08/02.                                                 
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *    TO PRINT SUBMISSION REPORT FOR ALLOCATION EXERCISE,     *            
      *    WRITE THE OUTPUT TO P13F800A.                           *            
      *                                                            *            
      *  INPUT FILES : BP13F205                                    *            
      *                BP13F800                                    *            
      *                BM06K010                                    *            
      *                                                            *            
      *  OUTPUT/S    : P13F800A                                    *            
      * ---------------------------------------------------------- *            
      * BP132201 AS1  210802  NEW PROGRAM                          *            
      * ========================================================== *            
                                                                                
      *===*===*===*===*====*                                                    
       ENVIRONMENT DIVISION.                                                    
      *===*===*===*===*====*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *=====================*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *=====================*                                                   
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT P13F800A ASSIGN TO P13F800A.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE KEY   IS K110-REGN-NO                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
      *===*===*===*==*                                                          
       DATA DIVISION.                                                           
      *===*===*===*==*                                                          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           BLOCK     CONTAINS 0    RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORD    CONTAINS 80   CHARACTERS                                   
           RECORDING MODE F.                                                    
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK     CONTAINS 0    RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORD    CONTAINS 2000 CHARACTERS                                   
           RECORDING MODE F.                                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD   CONTAINS 500  CHARACTERS.                                  
       COPY BM06K110.                                                           
                                                                                
       FD  P13F800A                                                             
           BLOCK     CONTAINS 0    RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORD    CONTAINS 2000 CHARACTERS                                   
           RECORDING MODE F.                                                    
       01  WS-F800A-REC.                                                        
           05 WS-F800A-REC1                PIC X(943) VALUE SPACES.             
           05 WS-F800A-NUM-MAIN            PIC X(04)  VALUE SPACES.             
           05 WS-F800A-NUM-SUB             PIC X(03)  VALUE SPACES.             
           05 WS-F800A-NUM-LEVEL           PIC X(02)  VALUE SPACES.             
           05 WS-F800A-BLK                 PIC X(05)  VALUE SPACES.             
           05 WS-F800A-STREET-NAME         PIC X(20)  VALUE SPACES.             
           05 WS-F800A-CDE-TYP-ACTLUSE     PIC X(02)  VALUE SPACES.             
           05 WS-F800A-CDE-MODL-DESG       PIC X(02)  VALUE SPACES.             
           05 WS-F800A-F205-NUM-NT-ZONE    PIC X(03)  VALUE SPACES.             
           05 WS-F800A-F205-NUM-NT-ZONE-SW PIC X(01)  VALUE SPACES.             
           05 WS-F800A-K110-ALLOC-SCH      PIC X(03)  VALUE SPACES.             
           05 FILLER                       PIC X(988) VALUE SPACES.             
           05 WS-F800A-REC2                PIC X(24)  VALUE SPACES.             
                                                                                
      *========================*                                                
       WORKING-STORAGE SECTION.                                                 
      *========================*                                                
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K110-STATUS             PIC 9(02) VALUE ZEROS.                
                                                                                
       01  WS-CNT.                                                              
           05  WS-BP13F800-READ           PIC 9(04) VALUE ZEROS.                
           05  WS-K110-READ               PIC 9(04) VALUE ZEROS.                
           05  WS-F800A-WRT               PIC 9(04) VALUE ZEROS.                
           05  WS-F205-READ-CNT           PIC 9(04) VALUE ZEROS.                
           05  WS-LINE-CNT                PIC 9(02) VALUE ZEROS.                
                                                                                
       01  WS-TEMP.                                                             
           05  EOF-F205                   PIC X(01) VALUE 'N'.                  
           05  WS-F800-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-SQL-CODE                PIC 9(04) VALUE ZEROS.                
           05  WS-NEXT-F205-REC           PIC X(1)  VALUE 'N'.                  
           05  WS-F800-PRINTED            PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-F205-KEY.                                                         
           05 WS-F205-NT-ZONE             PIC X(03) VALUE SPACES.               
           05 WS-F205-FLAT-TYPE           PIC X(01) VALUE SPACES.               
           05 WS-F205-DTE-BALLOT          PIC X(06) VALUE SPACES.               
                                                                                
       01  WS-F800-KEY.                                                         
           05 WS-F800-NUM-NEW-TOWN        PIC X(03) VALUE SPACES.               
           05 WS-F800-NUM-FLAT-TYPE       PIC X(01) VALUE SPACES.               
           05 WS-F800-DTE-BALLOT          PIC X(06) VALUE SPACES.               
                                                                                
      *============================================================*            
      *              LINKAGE FOR SUBPROGRAM 'BP13C913'                          
      *============================================================*            
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(04).                            
            05 WS-LINK-NUM-ACC            PIC X(04).                            
            05 WS-LINK-NUM-CHK            PIC X(01).                            
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *==*===*==*==*==*===*                                                     
       PROCEDURE DIVISION.                                                      
      *==*===*==*==*==*===*                                                     
                                                                                
      *=================*                                                       
       000-MAIN-ROUTINE.                                                        
      *=================*                                                       
                                                                                
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F205      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-F205   THRU 300-EXIT                             
             UNTIL EOF-F205 = 'Y'.                                              
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==================*                                                      
       100-INITIALIZATION.                                                      
      *==================*                                                      
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F800                                                 
                       BM06K110                                                 
                OUTPUT P13F800A.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BM06K110  OPEN ERROR ' WS-K110-STATUS               
              MOVE WS-K110-STATUS     TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE 'N'                TO WS-F800-EOF                               
                                      EOF-F205.                                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *=============*                                                           
       200-READ-F205.                                                           
      *=============*                                                           
                                                                                
           READ BP13F205 AT END                                                 
                MOVE 'Y'        TO EOF-F205                                     
                MOVE HIGH-VALUE TO WS-F205-KEY                                  
                GO  TO 200-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE  F205-NUM-NT-ZONE        TO  WS-F205-NT-ZONE.                   
           MOVE  F205-NUM-FLAT-TYPE(1:1) TO  WS-F205-FLAT-TYPE.                 
           MOVE  F205-DTE-BALLOT         TO  WS-F205-DTE-BALLOT.                
           ADD   1                       TO  WS-F205-READ-CNT.                  
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===============*                                                         
       300-PROCESS-F205.                                                        
      *===============*                                                         
                                                                                
           MOVE SPACES TO WS-F800-KEY.                                          
                                                                                
           PERFORM 400-READ-F800 THRU 400-EXIT                                  
             UNTIL (WS-F800-KEY  > WS-F205-KEY)                                 
                OR (WS-F800-EOF  = 'Y').                                        
                                                                                
           MOVE 'N'    TO WS-F800-EOF.                                          
                                                                                
           PERFORM 200-READ-F205 THRU 200-EXIT.                                 
           MOVE 'Y' TO WS-NEXT-F205-REC.                                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *============*                                                            
       400-READ-F800.                                                           
      *============*                                                            
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'        TO WS-F800-EOF                                  
                MOVE HIGH-VALUE TO WS-F800-KEY                                  
                GO TO 400-EXIT                                                  
           END-READ.                                                            
                                                                                
           MOVE  F800-NUM-NEW-TOWN       TO  WS-F800-NUM-NEW-TOWN.              
           MOVE  F800-NUM-FLAT-TYPE(1:1) TO  WS-F800-NUM-FLAT-TYPE.             
           MOVE  F800-DTE-BALLOT         TO  WS-F800-DTE-BALLOT.                
                                                                                
           IF WS-F800-KEY = WS-F205-KEY                                         
              PERFORM 420-PROCESS-DETAILS THRU 420-EXIT.                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===================*                                                     
       420-PROCESS-DETAILS.                                                     
      *===================*                                                     
                                                                                
           ADD   1                 TO WS-BP13F800-READ.                         
                                                                                
           MOVE SPACES             TO K110-REC.                                 
           MOVE F800-NUM-REGN      TO K110-REGN-NO.                             
                                                                                
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
                ADD  1                           TO   WS-K110-READ              
                PERFORM 450-CALL-BP13C913        THRU 450-EXIT                  
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN K110 ' WS-K110-STATUS              
                DISPLAY 'K110-NUM-REGN = ' K110-REGN-NO                         
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING K110 FILE  ' WS-K110-STATUS              
                MOVE WS-K110-STATUS  TO RETURN-CODE                             
                DISPLAY 'K110-REGN-NO = ' K110-REGN-NO                          
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *=================*                                                       
       450-CALL-BP13C913.                                                       
      *=================*                                                       
                                                                                
           MOVE SPACES           TO BP13COMM8-REC.                              
           INITIALIZE               BP13COMM8-REC.                              
                                                                                
           MOVE K110-SCHEME      TO WS-LINK-NUM-SCH.                            
           MOVE K110-ACC         TO WS-LINK-NUM-ACC.                            
           MOVE K110-CHK         TO WS-LINK-NUM-CHK.                            
                                                                                
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR = 0                                              
              PERFORM 455-MOVE-TO-REPORT THRU  455-EXIT                         
           ELSE                                                                 
              MOVE COMM8-CDE-SYSERR  TO WS-SQL-CODE                             
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE: ' WS-LINK-REC         
                 PERFORM 455-MOVE-TO-REPORT THRU  455-EXIT                      
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' WS-SQL-CODE                  
                 MOVE WS-SQL-CODE   TO RETURN-CODE                              
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==================*                                                      
       455-MOVE-TO-REPORT.                                                      
      *==================*                                                      
                                                                                
           MOVE SPACES                   TO WS-F800A-REC.                       
           INITIALIZE  WS-F800A-REC.                                            
                                                                                
           MOVE BP13F800-MASTER(1:943)   TO WS-F800A-REC1.                      
                                                                                
           MOVE COMM8-NUM-UNIT-MAIN      TO WS-F800A-NUM-MAIN.                  
           MOVE COMM8-NUM-UNIT-SUB       TO WS-F800A-NUM-SUB.                   
           MOVE COMM8-NUM-LEVEL          TO WS-F800A-NUM-LEVEL.                 
           MOVE COMM8-NUM-BLK            TO WS-F800A-BLK.                       
           MOVE COMM8-NME-STREET         TO WS-F800A-STREET-NAME.               
           MOVE COMM8-CDE-TYP-ACTLUSE    TO WS-F800A-CDE-TYP-ACTLUSE.           
           MOVE COMM8-CDE-MODL-DESG      TO WS-F800A-CDE-MODL-DESG.             
                                                                                
           MOVE F205-NUM-NT-ZONE         TO WS-F800A-F205-NUM-NT-ZONE.          
           MOVE F205-NUM-NT-ZONE-SW      TO WS-F800A-F205-NUM-NT-ZONE-SW        
           MOVE K110-ALLOC-SCH           TO WS-F800A-K110-ALLOC-SCH.            
                                                                                
           MOVE BP13F800-MASTER(1977:23) TO  WS-F800A-REC2.                     
                                                                                
           WRITE WS-F800A-REC.                                                  
           ADD   1   TO WS-F800A-WRT.                                           
                                                                                
       455-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===============*                                                         
       999-CLOSE-FILES.                                                         
      *===============*                                                         
                                                                                
           CLOSE BP13F205                                                       
                 BM06K110                                                       
                 BP13F800                                                       
                 P13F800A.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BM06K110 CLOSE ERROR ' WS-K110-STATUS               
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY ' BP13C290 CONTROL TOTAL'.                                   
           DISPLAY '----------------------------------------'.                  
           DISPLAY ' NO. OF F205 RECORDS READ = ' WS-F205-READ-CNT.             
           DISPLAY ' NO. OF F800 RECORDS READ = ' WS-BP13F800-READ.             
           DISPLAY ' NO. OF K110 RECORDS READ = ' WS-K110-READ.                 
           DISPLAY ' NO. OF F800A WRITTEN INTO= ' WS-F800A-WRT.                 
           DISPLAY SPACES.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
