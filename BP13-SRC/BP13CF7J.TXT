       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF7J.                                                  
       AUTHOR.       ALJOHN MONCADA.                                            
       DATE-WRITTEN. 04/07/2018.                                                
                                                                                
      *****************************************************************         
      *                SYSTEM NAME (BP13)                             *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *  -TO PROCESS NUM-RESULT-CODE IN INPUT BP17F872.               *         
      *      MATCH NUM-REGN FROM F872 AND F872A FILES                 *         
      *      IF MATCHED                                               *         
      *         READ BP13K730 USING NUM-REGN                          *         
      *         IF FOUND, CHECK RESULT CODE                           *         
      *            WHEN 'P'                                           *         
      *               UPDATE K730 PORT TAG TO 'P' IF VALUE NOT = 'P'  *         
      *            WHEN 'S'                                           *         
      *               UPDATE K730 PORT TAG TO 'S' IF VALUE NOT = 'S'  *         
      *            WHEN 'N'                                           *         
      *               UPDATE K730 PORT TAG TO ' ' IF VALUE NOT = ' '  *         
      *                                                               *         
      *      WRITE ALL RECORDS FROM INPUT P17F872A FILE TO OUTPUT FILE*         
      *      P17F872B WITH NUM-RESULT-CODE AND UPDATED PORT TAG.      *         
      *                                                               *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP17F872 I     SEQUENTIAL 200                                 *         
      * P17F872A I     SEQUENTIAL 500                                 *         
      * BP13K730 I/O   RANDOM     500  K730-KEY-NEW                   *         
      * P17F872B O     SEQUENTIAL 500                                 *         
      *                                                               *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHGE REQ # DATE     AUTHOR REMARKS                            *         
      * ---------- -------- ------ ---------------------------------- *         
      * BP137102   04/07/18 AM25   NEW PROGRAM                        *         
      *                                                               *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP17F872 ASSIGN      TO BP17F872.                             
           SELECT P17F872A ASSIGN      TO P17F872A.                             
                                                                                
           SELECT BP13K730 ASSIGN      TO BP13K730                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K730-KEY-NEW                                     
              FILE STATUS   IS WS-K730-STATUS.                                  
                                                                                
           SELECT P17F872B ASSIGN      TO P17F872B.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP17F872                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP17F872.                                                           
                                                                                
       FD  P17F872A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P17F872A-REC.                                                        
           05  F872A-NUM-REF           PIC X(8).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-STATUS        PIC X.                                   
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-NRIC          PIC X(9).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NME-OCCP          PIC X(66).                               
           05  FILLER                  PIC X.                                   
           05  F872A-DTE-SERS          PIC X(8).                                
           05  FILLER                  PIC X.                                   
           05  F872A-CDE-DEBAR         PIC X(2).                                
           05  FILLER                  PIC X.                                   
           05  F872A-SER-SCH-ACC       PIC X(9).                                
           05  FILLER                  PIC X.                                   
           05  F872A-SER-ADDR          PIC X(99).                               
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-REGN          PIC X(8).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-HHTY          PIC X.                                   
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-ALLO-SCH      PIC X(3).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-PORT-TAG      PIC X.                                   
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-FT            PIC X(2).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-NT            PIC X(3).                                
           05  FILLER                  PIC X.                                   
           05  F872A-DTE-APPT          PIC X(8).                                
           05  FILLER                  PIC X.                                   
           05  F872A-TME-APPT          PIC X(4).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-BLOCK         PIC X(5).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-HOUSE-NO      PIC X(11).                               
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-STREETNAME    PIC X(65).                               
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-POSTCODE      PIC X(6).                                
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-APPS          PIC X.                                   
           05  FILLER                  PIC X.                                   
           05  F872A-NUM-MGPS          PIC X.                                   
           05  FILLER                  PIC X.                                   
           05  FILLER                  PIC X(157).                              
                                                                                
       FD  BP13K730                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K730.                                                           
                                                                                
       FD  P17F872B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P17F872B-REC.                                                        
           05  F872B-NUM-REF           PIC X(8).                                
           05  F872B-FIL1              PIC X.                                   
           05  F872B-NUM-STATUS        PIC X.                                   
           05  F872B-FIL2              PIC X.                                   
           05  F872B-NUM-NRIC          PIC X(9).                                
           05  F872B-FIL3              PIC X.                                   
           05  F872B-NME-OCCP          PIC X(66).                               
           05  F872B-FIL4              PIC X.                                   
           05  F872B-DTE-SERS          PIC X(8).                                
           05  F872B-FIL5              PIC X.                                   
           05  F872B-CDE-DEBAR         PIC X(2).                                
           05  F872B-FIL6              PIC X.                                   
           05  F872B-SER-SCH-ACC       PIC X(9).                                
           05  F872B-FIL7              PIC X.                                   
           05  F872B-SER-ADDR          PIC X(99).                               
           05  F872B-FIL8              PIC X.                                   
           05  F872B-NUM-REGN          PIC X(8).                                
           05  F872B-FIL9              PIC X.                                   
           05  F872B-NUM-HHTY          PIC X.                                   
           05  F872B-FIL10             PIC X.                                   
           05  F872B-NUM-ALLO-SCH      PIC X(3).                                
           05  F872B-FIL11             PIC X.                                   
           05  F872B-NUM-PORT-TAG      PIC X.                                   
           05  F872B-FIL12             PIC X.                                   
           05  F872B-NUM-FT            PIC X(2).                                
           05  F872B-FIL13             PIC X.                                   
           05  F872B-NUM-NT            PIC X(3).                                
           05  F872B-FIL14             PIC X.                                   
           05  F872B-DTE-APPT          PIC X(8).                                
           05  F872B-FIL15             PIC X.                                   
           05  F872B-TME-APPT          PIC X(4).                                
           05  F872B-FIL16             PIC X.                                   
           05  F872B-NUM-BLOCK         PIC X(5).                                
           05  F872B-FIL17             PIC X.                                   
           05  F872B-NUM-HOUSE-NO      PIC X(11).                               
           05  F872B-FIL18             PIC X.                                   
           05  F872B-NUM-STREETNAME    PIC X(65).                               
           05  F872B-FIL19             PIC X.                                   
           05  F872B-NUM-POSTCODE      PIC X(6).                                
           05  F872B-FIL20             PIC X.                                   
           05  F872B-NUM-APPS          PIC X.                                   
           05  F872B-FIL21             PIC X.                                   
           05  F872B-NUM-MGPS          PIC X.                                   
           05  F872B-FIL22             PIC X.                                   
           05  F872B-NUM-RESULT-CODE   PIC X.                                   
           05  F872B-FIL23             PIC X.                                   
           05  F872B-UPD-PORT-TAG      PIC X(9).                                
           05  F872B-FIL24             PIC X.                                   
           05  FILLER                  PIC X(145).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K730-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F872-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-F872A-READ           PIC 9(5)        VALUE ZEROES.            
           05  WS-K730-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K730-UPDATE          PIC 9(5)        VALUE ZEROES.            
           05  WS-F872B-WRITE          PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F872-EOF             PIC X           VALUE 'N'.               
           05  WS-F872A-EOF            PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP17F872   THRU 2000-EXIT.                         
           PERFORM 2500-READ-P17F872A   THRU 2500-EXIT.                         
           PERFORM 3000-MAIN-PROCESSING THRU 3000-EXIT                          
              UNTIL WS-F872-EOF = 'Y' AND WS-F872A-EOF = 'Y'.                   
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP17F872                                                 
                       P17F872A                                                 
                I-O    BP13K730                                                 
                OUTPUT P17F872B.                                                
                                                                                
           IF (WS-K730-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K730 FAIL STATUS=' WS-K730-STATUS               
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP17F872.                                                      
      *****************************************************************         
                                                                                
           READ BP17F872                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F872-NUM-REGNO                        
                 MOVE 'Y'              TO WS-F872-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F872-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2500-READ-P17F872A.                                                      
      *****************************************************************         
                                                                                
           READ P17F872A                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F872A-NUM-REGN                        
                 MOVE 'Y'              TO WS-F872A-EOF                          
              NOT AT END                                                        
                 ADD 1                 TO WS-F872A-READ                         
           END-READ.                                                            
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           EVALUATE TRUE                                                        
              WHEN F872-NUM-REGNO = F872A-NUM-REGN                              
                 PERFORM 3500-READ-BP13K730  THRU 3500-EXIT                     
              WHEN F872-NUM-REGNO < F872A-NUM-REGN                              
                 PERFORM 2000-READ-BP17F872  THRU 2000-EXIT                     
              WHEN F872-NUM-REGNO > F872A-NUM-REGN                              
                 PERFORM 4000-WRITE-P17F872B THRU 4000-EXIT                     
                    UNTIL F872-NUM-REGNO <= F872A-NUM-REGN                      
                       OR WS-F872A-EOF = 'Y'                                    
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3500-READ-BP13K730.                                                      
      *****************************************************************         
                                                                                
           MOVE F872-NUM-REGNO         TO K730-NUM-REGN.                        
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K730-READ                          
                                                                                
                 PERFORM 3600-CHECK-RESULT-CODE THRU 3600-EXIT                  
                 PERFORM 4000-WRITE-P17F872B    THRU 4000-EXIT                  
                    UNTIL F872-NUM-REGNO NOT = F872A-NUM-REGN                   
                       OR WS-F872A-EOF = 'Y'                                    
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K730: '                       
                                                  F872-NUM-REGNO                
                 PERFORM 4000-WRITE-P17F872B    THRU 4000-EXIT                  
                    UNTIL F872-NUM-REGNO NOT = F872A-NUM-REGN                   
                       OR WS-F872A-EOF = 'Y'                                    
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K730 FAIL STATUS=' WS-K730-STATUS            
                 MOVE WS-K730-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP17F872           THRU 2000-EXIT.                 
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3600-CHECK-RESULT-CODE.                                                  
      *****************************************************************         
                                                                                
           EVALUATE F872-NUM-RESULT-CODE                                        
              WHEN 'P'                                                          
                 IF K730-NUM-PORT-ELIG-TAG NOT = 'P'                            
                    MOVE F872-NUM-RESULT-CODE                                   
                                       TO K730-NUM-PORT-ELIG-TAG                
                                                                                
                    PERFORM 3650-UPDATE-BP13K730 THRU 3650-EXIT                 
                 END-IF                                                         
              WHEN 'S'                                                          
                 IF K730-NUM-PORT-ELIG-TAG NOT = 'S'                            
                    MOVE F872-NUM-RESULT-CODE                                   
                                       TO K730-NUM-PORT-ELIG-TAG                
                                                                                
                    PERFORM 3650-UPDATE-BP13K730 THRU 3650-EXIT                 
                 END-IF                                                         
              WHEN 'N'                                                          
                 IF K730-NUM-PORT-ELIG-TAG NOT = SPACES                         
                    MOVE SPACES        TO K730-NUM-PORT-ELIG-TAG                
                                                                                
                    PERFORM 3650-UPDATE-BP13K730 THRU 3650-EXIT                 
                 END-IF                                                         
              WHEN OTHER                                                        
                 CONTINUE                                                       
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3650-UPDATE-BP13K730.                                                    
      *****************************************************************         
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           IF WS-K730-STATUS = ZEROES                                           
              ADD 1                    TO WS-K730-UPDATE                        
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K730 FAIL STATUS=' WS-K730-STATUS            
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-WRITE-P17F872B.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO P17F872B-REC.                         
           INITIALIZE P17F872B-REC.                                             
                                                                                
           MOVE ';'                    TO F872B-FIL1  F872B-FIL2                
                              F872B-FIL3  F872B-FIL4  F872B-FIL5                
                              F872B-FIL6  F872B-FIL7  F872B-FIL8                
                              F872B-FIL9  F872B-FIL10 F872B-FIL11               
                              F872B-FIL12 F872B-FIL13 F872B-FIL14               
                              F872B-FIL15 F872B-FIL16 F872B-FIL17               
                              F872B-FIL18 F872B-FIL19 F872B-FIL20               
                              F872B-FIL21 F872B-FIL22 F872B-FIL23               
                              F872B-FIL24.                                      
                                                                                
           MOVE F872A-NUM-REF          TO F872B-NUM-REF.                        
           MOVE F872A-NUM-STATUS       TO F872B-NUM-STATUS.                     
           MOVE F872A-NUM-NRIC         TO F872B-NUM-NRIC.                       
           MOVE F872A-NME-OCCP         TO F872B-NME-OCCP.                       
           MOVE F872A-DTE-SERS         TO F872B-DTE-SERS.                       
           MOVE F872A-CDE-DEBAR        TO F872B-CDE-DEBAR.                      
           MOVE F872A-SER-SCH-ACC      TO F872B-SER-SCH-ACC.                    
           MOVE F872A-SER-ADDR         TO F872B-SER-ADDR.                       
           MOVE F872A-NUM-REGN         TO F872B-NUM-REGN.                       
           MOVE F872A-NUM-HHTY         TO F872B-NUM-HHTY.                       
           MOVE F872A-NUM-ALLO-SCH     TO F872B-NUM-ALLO-SCH.                   
           MOVE F872A-NUM-PORT-TAG     TO F872B-NUM-PORT-TAG.                   
           MOVE F872A-NUM-FT           TO F872B-NUM-FT.                         
           MOVE F872A-NUM-NT           TO F872B-NUM-NT.                         
           MOVE F872A-DTE-APPT         TO F872B-DTE-APPT.                       
           MOVE F872A-TME-APPT         TO F872B-TME-APPT.                       
           MOVE F872A-NUM-BLOCK        TO F872B-NUM-BLOCK.                      
           MOVE F872A-NUM-HOUSE-NO     TO F872B-NUM-HOUSE-NO.                   
           MOVE F872A-NUM-STREETNAME   TO F872B-NUM-STREETNAME.                 
           MOVE F872A-NUM-POSTCODE     TO F872B-NUM-POSTCODE.                   
           MOVE F872A-NUM-APPS         TO F872B-NUM-APPS.                       
           MOVE F872A-NUM-MGPS         TO F872B-NUM-MGPS.                       
                                                                                
           IF F872-NUM-REGNO = F872A-NUM-REGN                                   
              MOVE F872-NUM-RESULT-CODE                                         
                                       TO F872B-NUM-RESULT-CODE                 
                                                                                
              IF WS-K730-STATUS = ZEROES                                        
                 MOVE K730-NUM-PORT-ELIG-TAG                                    
                                       TO F872B-UPD-PORT-TAG                    
              ELSE                                                              
                 MOVE 'NOT FOUND'      TO F872B-UPD-PORT-TAG                    
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES              TO F872B-NUM-RESULT-CODE                 
                                          F872B-UPD-PORT-TAG                    
           END-IF.                                                              
                                                                                
           WRITE P17F872B-REC.                                                  
           ADD 1                       TO WS-F872B-WRITE.                       
                                                                                
           PERFORM 2500-READ-P17F872A THRU 2500-EXIT.                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP17F872                                                       
                 P17F872A                                                       
                 BP13K730                                                       
                 P17F872B.                                                      
                                                                                
           IF WS-K730-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K730 FAIL STATUS=' WS-K730-STATUS              
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*****************************'.                             
           DISPLAY '* PROGRAM BP13CF7J          *'.                             
           DISPLAY '* CONTROL COUNTS            *'.                             
           DISPLAY '*---------------------------*'.                             
           DISPLAY '* REC READ F872     : ' WS-F872-READ ' *'.                  
           DISPLAY '* REC READ F872A    : ' WS-F872A-READ ' *'.                 
           DISPLAY '* REC READ K730     : ' WS-K730-READ ' *'.                  
           DISPLAY '* REC UPDATE K730   : ' WS-K730-UPDATE ' *'.                
           DISPLAY '* REC WRITTEN F872B : ' WS-F872B-WRITE ' *'.                
           DISPLAY '*****************************'.                             
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
