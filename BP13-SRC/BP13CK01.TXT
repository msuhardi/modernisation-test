       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK01.                                                 
      *AUTHOR.        YCH2.                                                     
      *DATE-WRITTEN.  03/03/2006.                                               
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  USE NRIC IN REQUEST FILE TO CHECK RESALE DP   *                
      *          FLATS                                         *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP132883 YCH2 03032005 NEW PROGRAM                     *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558   ASSIGN TO BP13F558.                                
                                                                                
           SELECT BB24K510  ASSIGN TO BB24K510                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K510-KEY-FLD                              
                  FILE STATUS      IS BB24K510-STATUS.                          
                                                                                
           SELECT BB24K500  ASSIGN TO BB24K500                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K500-KEY-FLD                              
                  FILE STATUS      IS BB24K500-STATUS.                          
                                                                                
           SELECT P13F558O  ASSIGN TO P13F558O.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BB24K510                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
                                                                                
       COPY BB24K510.                                                           
                                                                                
       FD   BB24K500                                                            
            RECORD CONTAINS 1700 CHARACTERS.                                    
                                                                                
       COPY BB24K500.                                                           
                                                                                
       FD   P13F558O                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F558O-REC            PIC X(600).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BB24K500-STATUS          PIC 9(2)    VALUE ZEROES.                   
       01  BB24K510-STATUS          PIC 9(2)    VALUE ZEROES.                   
                                                                                
       01  WS-K510-READ             PIC 9(8)    VALUE 0.                        
       01  WS-K500-READ             PIC 9(8)    VALUE 0.                        
       01  WS-F558-READ             PIC 9(8)    VALUE 0.                        
       01  WS-OUTPUT-CNT            PIC 9(8)    VALUE 0.                        
       01  WS-BYPASS                PIC 9(8)    VALUE 0.                        
       01  WS-MR-NRIC               PIC 9(8)    VALUE 0.                        
       01  WS-NMR-NRIC              PIC 9(8)    VALUE 0.                        
       01  WS-K510-NOTFD            PIC X       VALUE 'N'.                      
                                                                                
       01  WS-FLAT-TYPE-TABLE.                                                  
           05  WS-FLAT-TYPE-TAB1    PIC X(8)    VALUE '12345EHG'.               
           05  FILLER REDEFINES WS-FLAT-TYPE-TAB1 OCCURS 8.                     
               10 WS-CDE-FLAT-TYPE-TAB  PIC X.                                  
       01  WS-NUMBER                PIC 9(2)    VALUE 0.                        
                                                                                
       01  WS-RESALE-OM             PIC X(01)   VALUE SPACES.                   
       01  WS-HDB-REF               PIC X(11)   VALUE SPACES.                   
       01  WS-FLAT-TYPE             PIC X(02)   VALUE SPACES.                   
       01  WS-FLAT-TYPE-2           PIC X(01)   VALUE SPACES.                   
       01  WS-FILE-STATUS           PIC X(03)   VALUE SPACES.                   
       01  WS-DTE-APPROVAL          PIC X(08)   VALUE SPACES.                   
       01  WS-DTE-APPROVAL2         PIC X(08)   VALUE SPACES.                   
       01  WS-DTE-RESALE-EFF        PIC X(08)   VALUE SPACES.                   
       01  WS-DTE-SOLD-SELLER       PIC X(08)   VALUE SPACES.                   
       01  WS-K510-CDE-STATUS       PIC X(01)   VALUE SPACES.                   
       01  WS-NUM-MTH-OWNED-RSL     PIC 9(03)   VALUE 0.                        
                                                                                
       01  EOF-BB24K510            PIC X        VALUE 'N'.                      
       01  EOF-BB24K500            PIC X        VALUE 'N'.                      
       01  EOF-BP13F558            PIC X        VALUE 'N'.                      
                                                                                
       01  WS-RSL-ADDRESS.                                                      
           05  WS-RSL-NUM-BLK      PIC X(5)   VALUE SPACES.                     
           05  WS-RSL-NUM-LEVEL    PIC X(2)   VALUE SPACES.                     
           05  WS-RSL-NUM-UNIT     PIC X(5)   VALUE SPACES.                     
           05  WS-RSL-NME-STREET   PIC X(32)  VALUE SPACES.                     
           05  WS-RSL-NUM-POSTAL   PIC X(6)   VALUE SPACES.                     
                                                                                
      *-----------------------------------------------------------*             
      *        LINKAGE FOR SUB-PROGRAM BP13C913                   *             
      *-----------------------------------------------------------*             
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CC               PIC X(2).                                    
           05  WS-YY               PIC X(2).                                    
           05  WS-MM               PIC X(2).                                    
           05  WS-DD               PIC X(2).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN.                                                                
                                                                                
           OPEN INPUT  BB24K510                                                 
                       BB24K500                                                 
                       BP13F558                                                 
                OUTPUT P13F558O.                                                
                                                                                
           IF BB24K510-STATUS NOT = ZEROS AND 97                                
               DISPLAY 'FILE BB24K510 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BB24K510-STATUS                        
               MOVE BB24K510-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE.                                       
                                                                                
           IF BB24K500-STATUS NOT = ZEROS AND 97                                
               DISPLAY 'FILE BB24K500 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BB24K500-STATUS                        
               MOVE BB24K500-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE.                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-CURR-DATE.                  
                                                                                
           PERFORM 100-READ-F558-REC THRU 100-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL EOF-BP13F558 = 'Y'                                     
                     OR  F558-NUM-NRIC = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F558-REC.                                                       
                                                                                
           READ BP13F558 AT END MOVE 'Y' TO EOF-BP13F558                        
                MOVE HIGH-VALUE TO F558-NUM-NRIC                                
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       200-PROCESS-REC.                                                         
      *                                                                         
           MOVE 0             TO WS-NUM-MTH-OWNED-RSL.                          
           MOVE 'N'           TO WS-K510-NOTFD.                                 
           MOVE SPACES        TO K510-KEY-FLD.                                  
           MOVE F558-NUM-NRIC TO K510-NUM-UIN.                                  
           MOVE SPACES        TO WS-HDB-REF.                                    
           MOVE SPACES        TO WS-FLAT-TYPE.                                  
           MOVE SPACES        TO WS-FLAT-TYPE-2.                                
           MOVE SPACES        TO WS-FILE-STATUS.                                
           MOVE SPACES        TO WS-DTE-APPROVAL.                               
           MOVE SPACES        TO WS-DTE-APPROVAL2.                              
           MOVE SPACES        TO WS-DTE-RESALE-EFF.                             
           MOVE SPACES        TO WS-DTE-SOLD-SELLER.                            
                                                                                
           START BB24K510 KEY >=  K510-KEY-FLD.                                 
                                                                                
           IF  BB24K510-STATUS = ZEROES                                         
               PERFORM 210-READ-BB24K510 THRU 210-EXIT                          
                  UNTIL K510-NUM-UIN NOT = F558-NUM-NRIC                        
                    OR  WS-K510-NOTFD = 'Y'                                     
           ELSE                                                                 
               ADD 1 TO WS-NMR-NRIC                                             
           END-IF.                                                              
                                                                                
       200-READ-NEXT.                                                           
            PERFORM 100-READ-F558-REC THRU 100-EXIT.                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READ-BB24K510.                                                       
                                                                                
           READ BB24K510 NEXT AT END MOVE HIGH-VALUE TO K510-KEY-FLD            
                                     GO TO 210-EXIT.                            
                                                                                
           ADD 1 TO WS-K510-READ.                                               
                                                                                
           IF  (F558-NUM-NRIC = K510-NUM-UIN) AND                               
               (K510-CDE-STATUS = 'P' OR                                        
                K510-CDE-STATUS = 'S')                                          
                MOVE K510-CDE-STATUS TO WS-K510-CDE-STATUS                      
                PERFORM 211-READ-BB24K500 THRU 211-EXIT                         
                PERFORM 212-STORE-RESALE-INFOR THRU 212-EXIT                    
           ELSE                                                                 
                ADD 1 TO WS-BYPASS                                              
           END-IF.                                                              
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
       211-READ-BB24K500.                                                       
                                                                                
           MOVE SPACES             TO WS-RESALE-OM.                             
           MOVE K510-NUM-HDB-REF   TO K500-NUM-HDB-REF.                         
           MOVE K510-DTE-APPROVAL  TO K500-DTE-APPROVAL.                        
                                                                                
           READ BB24K500.                                                       
                                                                                
           ADD 1 TO WS-K500-READ.                                               
           IF  BB24K500-STATUS = ZEROES                                         
              IF K500-CDE-RESALE-STATUS2 = LOW-VALUES OR SPACES                 
                 MOVE K500-CDE-RESALE-STATUS TO WS-FILE-STATUS                  
                 IF K500-CDE-RESALE-STATUS = 'OM ' OR 'OMS' OR 'SAM' OR         
                                             'SNA' OR 'MOM' OR 'MNA' OR         
                                             'GSR' OR 'GOM' OR 'BNA' OR         
                                             'BOM' OR 'BSA' OR 'BN ' OR         
                                             'GS '                              
                    MOVE 'Y' TO WS-RESALE-OM                                    
                 ELSE                                                           
                    IF K500-CDE-RESALE-STATUS = 'NA '                           
                        PERFORM 215-CHK-PASIR-RIS-ACCT THRU 215-EXIT            
                     END-IF                                                     
                 END-IF                                                         
              ELSE                                                              
                IF K500-NUM-UIN-SELLER2 = F558-NUM-NRIC OR                      
                   K500-NUM-UIN-SPOUSE2 = F558-NUM-NRIC                         
                   MOVE K500-CDE-RESALE-STATUS2 TO WS-FILE-STATUS               
                   IF K500-CDE-RESALE-STATUS = 'OM ' OR 'OMS' OR 'SAM'          
                                            OR 'SNA' OR 'MOM' OR 'MNA'          
                                            OR 'GSR' OR 'GOM' OR 'BNA'          
                                            OR 'BOM' OR 'BSA' OR 'BN '          
                                            OR 'GS '                            
                      MOVE 'Y' TO WS-RESALE-OM                                  
                   END-IF                                                       
                END-IF                                                          
              END-IF                                                            
           ELSE                                                                 
               MOVE 'Y' TO WS-RESALE-OM                                         
               MOVE 'Y' TO WS-K510-NOTFD                                        
               DISPLAY 'RECORD NOT FOUND IN BB24K500 ' F558-NUM-NRIC            
                       '--> ' K510-DTE-APPROVAL                                 
                       '--> ' K510-NUM-HDB-REF                                  
           END-IF.                                                              
                                                                                
       211-EXIT.                                                                
           EXIT.                                                                
                                                                                
       212-STORE-RESALE-INFOR.                                                  
                                                                                
           IF WS-RESALE-OM = 'Y'                                                
              GO TO 212-EXIT.                                                   
                                                                                
           PERFORM 214-MOVE-DIRECT-PURCHASE THRU 214-EXIT                       
                                                                                
           MOVE 'R'                    TO F558-CDE-FILE.                        
           MOVE WS-FLAT-TYPE           TO F558-CDE-FLAT-TYPE-RESALE.            
           MOVE WS-FLAT-TYPE-2         TO F558-CDE-FT-RSL-LATEST.               
           MOVE WS-HDB-REF             TO F558-NUM-HDB-REF-RSL.                 
           MOVE WS-DTE-APPROVAL        TO F558-DTE-APPROVAL-RSL.                
           MOVE WS-DTE-APPROVAL2       TO F558-DTE-APPR-RSL-LATEST.             
           MOVE WS-DTE-SOLD-SELLER     TO F558-DTE-SOLD-SELLER-RSL.             
           MOVE WS-FILE-STATUS         TO F558-CDE-FILE-STATUS-RSL.             
           MOVE WS-K510-CDE-STATUS     TO F558-NUM-SELLER-RSL.                  
           MOVE WS-NUM-MTH-OWNED-RSL   TO F558-NUM-MTH-OWNED-RSL.               
                                                                                
           IF  WS-DTE-RESALE-EFF = SPACES                                       
               MOVE WS-DTE-APPROVAL   TO F558-DTE-RESALE-EFF                    
           ELSE                                                                 
               MOVE WS-DTE-RESALE-EFF TO F558-DTE-RESALE-EFF                    
           END-IF.                                                              
                                                                                
           MOVE WS-HDB-REF(1:9)        TO WS-LINK-NUM-SCH-ACC.                  
                                                                                
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
              WHEN 00                                                           
                   MOVE COMM8-NUM-BLK         TO WS-RSL-NUM-BLK                 
                   MOVE COMM8-NME-STREET      TO WS-RSL-NME-STREET              
                   MOVE COMM8-NUM-POSTAL-CODE TO WS-RSL-NUM-POSTAL              
                   MOVE COMM8-NUM-LEVEL       TO WS-RSL-NUM-LEVEL               
                   MOVE COMM8-NUM-UNIT-MAIN   TO WS-RSL-NUM-UNIT(1:4)           
                   MOVE COMM8-NUM-UNIT-SUB    TO WS-RSL-NUM-UNIT(5:1)           
              WHEN 100                                                          
                   MOVE SPACES                TO WS-RSL-ADDRESS                 
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING EX ADDRESS ' COMM8-CDE-SYSERR         
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-RSL-ADDRESS  TO  BP13F558-REC(550:50)                        
           WRITE P13F558O-REC  FROM BP13F558-REC                                
           ADD 1   TO WS-OUTPUT-CNT                                             
           ADD 1 TO WS-MR-NRIC.                                                 
                                                                                
       212-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       214-MOVE-DIRECT-PURCHASE.                                                
                                                                                
           MOVE K500-CDE-TYP-ACTLUSE TO WS-NUMBER.                              
           IF WS-NUMBER > 0 AND < 9                                             
                MOVE WS-CDE-FLAT-TYPE-TAB (WS-NUMBER) TO WS-FLAT-TYPE           
           ELSE                                                                 
               MOVE SPACES  TO  WS-FLAT-TYPE                                    
           END-IF.                                                              
                                                                                
           MOVE K500-NUM-HDB-REF        TO WS-HDB-REF.                          
           MOVE K500-DTE-APPROVAL       TO WS-DTE-APPROVAL.                     
           MOVE K500-DTE-RESL-EFF       TO WS-DTE-RESALE-EFF.                   
           MOVE K500-DTE-SOLD-TO-SELLER TO WS-DTE-SOLD-SELLER.                  
                                                                                
           IF  K500-NUM-MTHS-OWNED IS NOT NUMERIC                               
               MOVE ZEROS TO K500-NUM-MTHS-OWNED                                
           END-IF.                                                              
                                                                                
           MOVE K500-NUM-MTHS-OWNED    TO WS-NUM-MTH-OWNED-RSL.                 
                                                                                
           IF K500-DTE-APPROVAL > WS-DTE-APPROVAL2                              
              MOVE K500-DTE-APPROVAL         TO WS-DTE-APPROVAL2                
              MOVE K500-CDE-TYP-ACTLUSE(2:1) TO WS-FLAT-TYPE-2                  
           END-IF.                                                              
                                                                                
       214-EXIT.                                                                
           EXIT.                                                                
                                                                                
       215-CHK-PASIR-RIS-ACCT.                                                  
                                                                                
           IF (K500-NUM-HDB-REF >= '85750245901' AND                            
                                <= '85750375015')                               
              IF K500-CDE-ALLOC-SCH-SELLER = 'OM '                              
                 MOVE 'Y' TO WS-RESALE-OM                                       
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y' TO WS-RESALE-OM                                          
           END-IF.                                                              
                                                                                
       215-EXIT.                                                                
           EXIT.                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'BP13CK01        CURR-DATE : ' WS-CURR-DATE.                 
           DISPLAY 'MATCH NRIC IN REQUEST FILE WITH RESALE FILE'.               
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F558   ' WS-F558-READ.              
           DISPLAY 'NO OF RECORDS READ FROM K500   ' WS-K500-READ.              
           DISPLAY 'NO OF RECORDS READ FROM K510   ' WS-K510-READ.              
           DISPLAY 'NO OF RECORDS OUTPUT BP13F558  ' WS-OUTPUT-CNT.             
           DISPLAY 'NO OF RECORDS BYPASS RESALE    ' WS-BYPASS.                 
           DISPLAY 'NO OF RECORDS NOT MATCH        ' WS-NMR-NRIC.               
           DISPLAY 'NO OF RECORDS MATCH            ' WS-MR-NRIC.                
                                                                                
           CLOSE P13F558O                                                       
                 BP13F558                                                       
                 BB24K500                                                       
                 BB24K510.                                                      
                                                                                
           IF  BB24K510-STATUS NOT = ZEROS AND 97                               
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'BB24K510-STATUS ' BB24K510-STATUS.                      
           IF  BB24K500-STATUS NOT = ZEROS AND 97                               
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'BB24K500-STATUS ' BB24K500-STATUS.                      
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
