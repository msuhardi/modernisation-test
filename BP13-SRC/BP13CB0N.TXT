      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB0N.                                                 
      *AUTHOR.        MARGE LAM KO.                                             
      *DATE-WRITTEN.  12 MAY 2017.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : GET SCP AND TOP UP CASES                    *           
      *                 (EC CASES), OUTPUT TO USE IN REPORT         *           
      *                                                             *           
      *   INPUT:                                                    *           
      *   1.  GRANT_ASMNT                                           *           
      *   2.  GRANT_ASMNT_PERSON - TO CHECK AMOUNT                  *           
      *   3.  BP13K130 - USED TO GET POST DATE (TO CHECK FNCL YR)   *           
      *                                                             *           
      *   OUTPUT FILES: REGN NO, OTP DATE, NRIC1, NRIC2, NRIC3, NRIC4,          
      *                 APRVL DATE                                  *           
      *                                                             *           
      *   1.  BP13RPT1                                              *           
      *   2.  BP13RPT2                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136810 12/05/2017 MRN1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13K730  ASSIGN        TO BP13K730                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K730-KEY-NEW                      
                             FILE STATUS   IS WS-K730-STATUS.                   
                                                                                
           SELECT  BP18K300  ASSIGN        TO BP18K300                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K300-NUM-REGN                     
                             FILE STATUS   IS WS-K300-STATUS.                   
                                                                                
           SELECT BP13KB20 ASSIGN       TO BP13KB20                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KB20-KEY-FLD                         
                           FILE STATUS  IS WS-KB20-STATUS.                      
                                                                                
           SELECT BP13K130 ASSIGN TO BP13K130                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K130-KEY-FLD                               
                  FILE STATUS     IS WS-K130-STATUS.                            
                                                                                
           SELECT BP13RPT1   ASSIGN       TO BP13RPT1.                          
           SELECT BP13RPT2   ASSIGN       TO BP13RPT2.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP18K300                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP18K300.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
       FD   BP13K130                                                            
            RECORD    CONTAINS 150 CHARACTERS.                                  
       COPY BP13K130.                                                           
                                                                                
       FD   BP13RPT1                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13RPT1-REC          PIC X(300).                                 
                                                                                
       FD   BP13RPT2                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13RPT2-REC          PIC X(300).                                 
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-SCP-CNT               PIC 9(05) VALUE ZEROES.                     
       01  WS-TUG-CNT               PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT1-CNT              PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT2-CNT              PIC 9(05) VALUE ZEROES.                     
       01  WS-CTR                   PIC 9(02) VALUE ZEROES.                     
       01  WS-FINCL-YEAR            PIC X(01) VALUE 'N'.                        
       01  WS-KB20-FND              PIC X(01) VALUE 'N'.                        
       01  WS-K730-FND              PIC X(01) VALUE 'N'.                        
       01  WS-K300-FND              PIC X(01) VALUE 'N'.                        
       01  WS-K130-FND              PIC X(01) VALUE 'N'.                        
       01  WS-EOF-K130              PIC X(01) VALUE 'N'.                        
       01  WS-END-CURSOR1           PIC X(01) VALUE 'N'.                        
       01  WS-END-CURSOR2           PIC X(01) VALUE 'N'.                        
       01  WS-HHTYPE                PIC X(01) VALUE SPACES.                     
       01  WS-NUM-REGN              PIC X(08).                                  
       01  WS-NUM-GRANT-TYPE        PIC X(06).                                  
       01  WS-DTE-PYMNT             PIC X(10).                                  
       01  WS-DTE-POST              PIC X(08).                                  
       01  WS-REPORT-TYPE           PIC X(03).                                  
       01  WS-RETURN-CODE           PIC 9(02) VALUE ZEROES.                     
       01  WS-KB20-STATUS           PIC 9(2)    VALUE  0.                       
       01  WS-K730-STATUS           PIC 9(2)    VALUE  0.                       
       01  WS-K300-STATUS           PIC 9(2)    VALUE  0.                       
       01  WS-K130-STATUS           PIC 9(2)    VALUE  0.                       
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05 WS-DTE-CCYY           PIC 9(04).                                  
           05 WS-DTE-MM             PIC 9(02).                                  
           05 WS-DTE-DD             PIC 9(02).                                  
       01  WS-TIME-HHMMSS.                                                      
           05 WS-TIME-HH            PIC X(02) VALUE SPACES.                     
           05 WS-TIME-MM            PIC X(02) VALUE SPACES.                     
           05 WS-TIME-SS            PIC X(02) VALUE SPACES.                     
           05 WS-TIME-MS            PIC X(02) VALUE SPACES.                     
                                                                                
       01  WS-DTE-FINCL-CCYYMM      PIC X(06).                                  
       01  WS-YEAR                  PIC 9(04).                                  
       01  WS-YEAR-PREV             PIC 9(04).                                  
       01  WS-YEAR-NEXT             PIC 9(04).                                  
                                                                                
       01  WS-LAST-MONTH.                                                       
           05 WS-LAST-CCYY          PIC 9(04).                                  
           05 WS-LAST-MM            PIC 9(02).                                  
                                                                                
       01  WS-BP13RPT-HDR.                                                      
           05  FILLER               PIC X(08) VALUE 'REGN NUM'.                 
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  FILLER               PIC X(03) VALUE 'TYP'.                      
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  FILLER               PIC X(09) VALUE '  NRIC1  '.                
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  FILLER               PIC X(09) VALUE '  NRIC2  '.                
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  FILLER               PIC X(09) VALUE '  NRIC3  '.                
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  FILLER               PIC X(09) VALUE '  NRIC4  '.                
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  FILLER               PIC X(08) VALUE 'OTP DATE'.                 
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  FILLER               PIC X(08) VALUE 'POST DTE'.                 
           05  FILLER               PIC X(230) VALUE SPACES.                    
                                                                                
       01  WS-BP13RPT1-DTL.                                                     
           05  WS-RPT1-REGN-NO      PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT1-GRANT-TYPE   PIC X(03) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT1-NRIC1        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT1-NRIC2        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT1-NRIC3        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT1-NRIC4        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT1-DTE-OTP      PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT1-DTE-POST     PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(230) VALUE SPACES.                    
                                                                                
       01  WS-BP13RPT2-DTL.                                                     
           05  WS-RPT2-REGN-NO      PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT2-GRANT-TYPE   PIC X(03) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT2-NRIC1        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT2-NRIC2        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT2-NRIC3        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT2-NRIC4        PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT2-DTE-OTP      PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE ';'.                        
           05  WS-RPT2-DTE-POST     PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(230) VALUE SPACES.                    
                                                                                
       01  WS-SQLCODE             PIC ---9.                                     
       01  WS-SQL-STATUS.                                                       
           05 WS-SQL-OK                PIC S9(03) VALUE +000.                   
           05 WS-SQL-NFND              PIC S9(03) VALUE +100.                   
           05 WS-SQL-DUPREC            PIC S9(03) VALUE -803.                   
                                                                                
      *--------------------------------------------------------------*          
      *                          SQLCA                               *          
      *--------------------------------------------------------------*          
           EXEC SQL INCLUDE SQLCA          END-EXEC.                            
           EXEC SQL INCLUDE P13VGASM       END-EXEC.                            
           EXEC SQL INCLUDE P13VGAPN       END-EXEC.                            
                                                                                
      *===============================================================          
      *  CURSOR TO EXTRACT NON-CANCELLED EC SCP CASES (1)                       
      *===============================================================          
           EXEC SQL                                                             
              DECLARE CURSOR1 CURSOR FOR                                        
               SELECT DISTINCT                                                  
                      T1.NUM_RGSTRN,                                            
                      T1.NUM_GRANT_TYPE,                                        
                      T1.DTE_PYMNT                                              
                FROM  GRANT_ASMNT T1,                                           
                      GRANT_ASMNT_PERSON T2                                     
                WHERE                                                           
                  T1.NUM_RGSTRN = T2.NUM_RGSTRN AND                             
                  T1.NUM_GRANT_TYPE = T2.NUM_GRANT_TYPE AND                     
                  T1.NUM_HSHLD_CTZNSP = 'SCP' AND                               
                  T1.NUM_GRANT_TYPE NOT IN('TUG') AND                           
                  T1.NUM_RGSTRN LIKE '6%'                                       
           END-EXEC.                                                            
                                                                                
      *===============================================================          
      *  CURSOR TO EXTRACT NON-CANCELLED EC TUG CASES (1)                       
      *===============================================================          
           EXEC SQL                                                             
              DECLARE CURSOR2 CURSOR FOR                                        
               SELECT DISTINCT                                                  
                      T1.NUM_RGSTRN,                                            
                      T1.NUM_GRANT_TYPE,                                        
                      T1.DTE_PYMNT                                              
                FROM  GRANT_ASMNT T1,                                           
                      GRANT_ASMNT_PERSON T2                                     
                WHERE                                                           
                  T1.NUM_RGSTRN = T2.NUM_RGSTRN AND                             
                  T1.NUM_GRANT_TYPE = T2.NUM_GRANT_TYPE AND                     
                  T2.AMT_GRANT = 10000 AND                                      
                  T1.NUM_GRANT_TYPE = 'TUG' AND                                 
                  T1.NUM_RGSTRN LIKE '6%'                                       
           END-EXEC.                                                            
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-REC  THRU 2000-EXIT.                            
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13K730                                                 
                       BP18K300                                                 
                       BP13KB20                                                 
                       BP13K130                                                 
                OUTPUT BP13RPT1                                                 
                       BP13RPT2.                                                
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KB20 : ' WS-KB20-STATUS              
              MOVE     WS-KB20-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP18K300 - ERROR OPENING : ' WS-K300-STATUS              
              MOVE     WS-K300-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 - ERROR OPENING : ' WS-K730-STATUS              
              MOVE     WS-K730-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K130 - ERROR OPENING : ' WS-K130-STATUS              
              MOVE     WS-K130-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           ACCEPT WS-DTE-CCYYMMDD   FROM   DATE YYYYMMDD.                       
           ACCEPT WS-TIME-HHMMSS    FROM   TIME.                                
                                                                                
           WRITE WS-BP13RPT1-REC FROM WS-BP13RPT-HDR.                           
           WRITE WS-BP13RPT2-REC FROM WS-BP13RPT-HDR.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
      *** SCP CASES (1) ***                                                     
           PERFORM 2200-OPEN-CURSOR1  THRU 2200-EXIT.                           
           MOVE 'N'                   TO WS-END-CURSOR1.                        
           PERFORM 2300-FETCH-CURSOR1 THRU 2300-EXIT                            
                   UNTIL WS-END-CURSOR1 = 'Y'                                   
           PERFORM 2500-CLOSE-CURSOR1 THRU 2500-EXIT.                           
                                                                                
      *** TUG CASES (2) ***                                                     
           PERFORM 3200-OPEN-CURSOR2  THRU 3200-EXIT.                           
           MOVE 'N'                   TO WS-END-CURSOR2.                        
           PERFORM 3300-FETCH-CURSOR2 THRU 3300-EXIT                            
                   UNTIL WS-END-CURSOR2 = 'Y'                                   
           PERFORM 3500-CLOSE-CURSOR2 THRU 3500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       2200-OPEN-CURSOR1.                                                       
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                OPEN CURSOR1                                                    
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       2300-FETCH-CURSOR1.                                                      
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                FETCH CURSOR1                                                   
                INTO  :WS-NUM-REGN,                                             
                      :WS-NUM-GRANT-TYPE,                                       
                      :WS-DTE-PYMNT                                             
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                PERFORM 2400-PROCESS-SCP         THRU 2400-EXIT                 
           WHEN 100                                                             
                MOVE 'Y'  TO WS-END-CURSOR1                                     
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       2400-PROCESS-SCP.                                                        
      ***************************************************************           
                                                                                
           MOVE 'HGT' TO WS-REPORT-TYPE.                                        
                                                                                
           PERFORM 3550-GET-POST-DATE   THRU 3550-EXIT.                         
           PERFORM 2410-GET-HHTYPE      THRU 2410-EXIT.                         
                                                                                
           IF WS-HHTYPE = 'H'                                                   
              PERFORM 2450-READ-BP13KB20 THRU 2450-EXIT                         
      *       IF WS-KB20-FND = 'Y'                                              
                 ADD 1 TO WS-SCP-CNT                                            
                 MOVE WS-NUM-REGN             TO WS-RPT1-REGN-NO                
                 MOVE WS-NUM-GRANT-TYPE(1:3)  TO WS-RPT1-GRANT-TYPE             
                 MOVE KB20-NUM-APPLT1-UIN     TO WS-RPT1-NRIC1                  
                 MOVE KB20-NUM-APPLT2-UIN     TO WS-RPT1-NRIC2                  
                 MOVE KB20-NUM-APPLT3-UIN     TO WS-RPT1-NRIC3                  
                 MOVE KB20-NUM-APPLT4-UIN     TO WS-RPT1-NRIC4                  
                 MOVE KB20-DTE-ACCEPT         TO WS-RPT1-DTE-OTP                
                 MOVE WS-DTE-POST             TO WS-RPT1-DTE-POST               
                 WRITE WS-BP13RPT1-REC FROM WS-BP13RPT1-DTL                     
                 ADD 1 TO WS-RPT1-CNT                                           
                 INITIALIZE WS-BP13RPT1-DTL                                     
      *       END-IF                                                            
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2410-GET-HHTYPE.                                                         
      ******************************************************************        
                                                                                
            MOVE SPACES                       TO WS-HHTYPE.                     
                                                                                
            MOVE WS-NUM-REGN                TO K730-KEY-NEW                     
            PERFORM 2420-READ-BP13K730      THRU 2420-EXIT                      
            IF WS-K730-FND = 'Y'                                                
               MOVE K730-CDE-HOUSEHOLD      TO WS-HHTYPE                        
            ELSE                                                                
               MOVE WS-NUM-REGN             TO K300-NUM-REGN                    
               PERFORM 2430-READ-BP18K300   THRU 2430-EXIT                      
               IF WS-K300-FND = 'Y'                                             
                  MOVE K300-NUM-HSHLD       TO WS-HHTYPE                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       2410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2420-READ-BP13K730.                                                      
      ******************************************************************        
                                                                                
            READ BP13K730.                                                      
                                                                                
            EVALUATE WS-K730-STATUS                                             
                WHEN 00                                                         
                     MOVE 'Y'                 TO WS-K730-FND                    
                WHEN 23                                                         
                     MOVE 'N'                 TO WS-K730-FND                    
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K730 : ' WS-K730-STATUS         
                             ' REGN NO = ' K730-KEY-NEW                         
                     PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT            
            END-EVALUATE.                                                       
                                                                                
       2420-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2430-READ-BP18K300.                                                      
      ******************************************************************        
                                                                                
            READ BP18K300.                                                      
                                                                                
            EVALUATE WS-K300-STATUS                                             
                WHEN 00                                                         
                     MOVE 'Y'                 TO WS-K300-FND                    
                WHEN 23                                                         
                     MOVE 'N'                 TO WS-K300-FND                    
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP18K300 : ' WS-K300-STATUS         
                             ' REGN NO = ' K300-NUM-REGN                        
                     PERFORM  9000-CLOSE-FILES      THRU 9000-EXIT              
            END-EVALUATE.                                                       
                                                                                
       2430-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       2450-READ-BP13KB20.                                                      
      **********************************************************                
           MOVE SPACES TO BP13KB20-REC.                                         
                                                                                
           MOVE WS-NUM-REGN TO KB20-NUM-REGN.                                   
                                                                                
           READ BP13KB20.                                                       
                                                                                
           IF WS-KB20-STATUS = 00 OR 97 OR 02                                   
              MOVE 'Y'   TO WS-KB20-FND                                         
           ELSE                                                                 
              IF WS-KB20-STATUS = 23                                            
                 DISPLAY '-REC NOT FND IN KB20 ' KB20-NUM-REGN                  
                 MOVE 'N'   TO WS-KB20-FND                                      
              ELSE                                                              
                 DISPLAY '-- ERROR READING BP13KB20, REGN '                     
                          WS-NUM-REGN ', STATUS ' WS-KB20-STATUS                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       2500-CLOSE-CURSOR1.                                                      
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                CLOSE CURSOR1                                                   
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       3200-OPEN-CURSOR2.                                                       
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                OPEN CURSOR2                                                    
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       3300-FETCH-CURSOR2.                                                      
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                FETCH CURSOR2                                                   
                INTO  :WS-NUM-REGN,                                             
                      :WS-NUM-GRANT-TYPE,                                       
                      :WS-DTE-PYMNT                                             
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                PERFORM 3400-PROCESS-TUG         THRU 3400-EXIT                 
           WHEN 100                                                             
                MOVE 'Y'  TO WS-END-CURSOR2                                     
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       3400-PROCESS-TUG.                                                        
      ***************************************************************           
                                                                                
           MOVE 'TUG' TO WS-REPORT-TYPE.                                        
                                                                                
           PERFORM 3550-GET-POST-DATE   THRU 3550-EXIT.                         
           PERFORM 2410-GET-HHTYPE THRU 2410-EXIT.                              
                                                                                
           IF WS-HHTYPE = 'H'                                                   
              PERFORM 2450-READ-BP13KB20 THRU 2450-EXIT                         
      *       IF WS-KB20-FND = 'Y'                                              
                 ADD 1 TO WS-TUG-CNT                                            
                 MOVE WS-NUM-REGN             TO WS-RPT1-REGN-NO                
                 MOVE WS-NUM-GRANT-TYPE(1:3)  TO WS-RPT1-GRANT-TYPE             
                 MOVE KB20-NUM-APPLT1-UIN     TO WS-RPT1-NRIC1                  
                 MOVE KB20-NUM-APPLT2-UIN     TO WS-RPT1-NRIC2                  
                 MOVE KB20-NUM-APPLT3-UIN     TO WS-RPT1-NRIC3                  
                 MOVE KB20-NUM-APPLT4-UIN     TO WS-RPT1-NRIC4                  
                 MOVE KB20-DTE-ACCEPT         TO WS-RPT1-DTE-OTP                
                 MOVE WS-DTE-POST             TO WS-RPT1-DTE-POST               
                 WRITE WS-BP13RPT1-REC FROM WS-BP13RPT1-DTL                     
                 ADD 1 TO WS-RPT1-CNT                                           
                 INITIALIZE WS-BP13RPT1-DTL                                     
                 MOVE WS-NUM-REGN             TO WS-RPT2-REGN-NO                
                 MOVE WS-NUM-GRANT-TYPE(1:3)  TO WS-RPT2-GRANT-TYPE             
                 MOVE KB20-NUM-APPLT1-UIN     TO WS-RPT2-NRIC1                  
                 MOVE KB20-NUM-APPLT2-UIN     TO WS-RPT2-NRIC2                  
                 MOVE KB20-NUM-APPLT3-UIN     TO WS-RPT2-NRIC3                  
                 MOVE KB20-NUM-APPLT4-UIN     TO WS-RPT2-NRIC4                  
                 MOVE KB20-DTE-ACCEPT         TO WS-RPT2-DTE-OTP                
                 MOVE WS-DTE-POST             TO WS-RPT2-DTE-POST               
                 WRITE WS-BP13RPT2-REC FROM WS-BP13RPT2-DTL                     
                 ADD 1 TO WS-RPT2-CNT                                           
                 INITIALIZE WS-BP13RPT2-DTL                                     
      *       END-IF                                                            
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       3500-CLOSE-CURSOR2.                                                      
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
                CLOSE CURSOR2                                                   
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                PERFORM 9999-SQL-ERROR    THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3550-GET-POST-DATE.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                TO BP13K130-CDHIST.                       
           INITIALIZE                    BP13K130-CDHIST.                       
           MOVE 'N'                   TO WS-EOF-K130.                           
           MOVE SPACES                TO WS-DTE-POST.                           
           MOVE WS-NUM-REGN           TO K130-NUM-ORIG-REGN.                    
                                                                                
           START BP13K130 KEY = K130-NUM-ORIG-REGN.                             
                                                                                
           EVALUATE WS-K130-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3600-READNEXT-BP13K130 THRU 3600-EXIT               
                    PERFORM 3700-PROCESS-BP13K130  THRU 3700-EXIT               
                            UNTIL WS-EOF-K130 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE SPACES                      TO WS-DTE-POST             
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K130 : ' WS-K130-STATUS              
                          ' REGN : ' K130-NUM-ORIG-REGN                         
                  PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-READNEXT-BP13K130.                                                  
      ******************************************************************        
                                                                                
           READ BP13K130 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K130.                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3700-PROCESS-BP13K130.                                                   
      ******************************************************************        
                                                                                
           IF WS-NUM-REGN = K130-NUM-ORIG-REGN                                  
              IF ((K130-PAYMENT-TYPE = 'HGT') OR                                
                  (WS-REPORT-TYPE = K130-PAYMENT-TYPE)) AND                     
                 (K130-TRANS-TYPE = '60')                                       
                  MOVE K130-DTE-POST              TO WS-DTE-POST                
                  MOVE 'Y'                        TO WS-EOF-K130                
              ELSE                                                              
                  PERFORM 3600-READNEXT-BP13K130  THRU 3600-EXIT                
              END-IF                                                            
           ELSE                                                                 
               MOVE 'Y'                        TO WS-EOF-K130                   
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB0N *************'.                            
           DISPLAY 'NO OF SCP CASES                 : ' WS-SCP-CNT.             
           DISPLAY 'NO OF TUG CASES                 : ' WS-TUG-CNT.             
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 1 : ' WS-RPT1-CNT.            
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 2 : ' WS-RPT2-CNT.            
                                                                                
           CLOSE BP13K730                                                       
                 BP18K300                                                       
                 BP13KB20                                                       
                 BP13K130                                                       
                 BP13RPT1                                                       
                 BP13RPT2.                                                      
                                                                                
           IF WS-K730-STATUS NOT = 0 AND 97                                     
              DISPLAY ' CLOSING ERROR, WS-K730-STATUS ' WS-K730-STATUS          
              MOVE WS-K730-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K300-STATUS NOT = 0 AND 97                                     
              DISPLAY ' CLOSING ERROR, WS-K300-STATUS ' WS-K300-STATUS          
              MOVE WS-K300-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-KB20-STATUS NOT = 0 AND 97                                     
              DISPLAY ' CLOSING ERROR, WS-KB20-STATUS ' WS-KB20-STATUS          
              MOVE WS-KB20-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       9999-SQL-ERROR.                                                          
      *----------------------------------------------------------------         
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL ROLLBACK               END-EXEC.                            
           MOVE 99                         TO    WS-RETURN-CODE.                
           PERFORM 9000-CLOSE-FILES        THRU  9000-EXIT.                     
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
