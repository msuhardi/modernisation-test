       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB57.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  30/10/2017.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      * OBJECTIVE   : COUNTING RECORDS FOR PRE-BALLOT REPORTS       *           
      *                                                             *           
      * INPUT FILES : BP13F730 - DTE-BALLOT, ALLO-CAT, NT, FT       *           
      *               BP13F500 - NO. OF RANDOM NOS. TO BE ASSIGNED  *           
      *               BP13L54D - TOTAL NO. OF APPLICATIONS RECEIVED *           
      *                          APPLNS RECEIVED AFTER CLOSING      *           
      *               BP13L54G - NO. OF CANCELLED APPLICATIONS      *           
      *               BP13K813 - NO. OF FLATS OFFERED               *           
      *               BP13K060 - CODE TABLE FILE                    *           
      *        (BTO)  BP13K762 - APPLNS RECEIVED AT CLOSING DATE    *           
      *        (SBF)  BP13K769 - APPLNS RECEIVED AT CLOSING DATE    *           
      *        (ROF)  BP13K766 - APPLNS RECEIVED AT CLOSING DATE    *           
      * OUTPUT FILE : BP13LB57 - SUMMARY COUNT PER NT/FT            *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ  DATE       BY   DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP137052 30/10/2017 SMR2 NEW PROGRAM                        *           
      * BP137052 04/12/2017 SMR2 CATER FOR GRAND TOTAL              *           
      * BP137706 14/03/2019 AM25 INCLUDE CHECKING OF FLAT TY FOR ROF*           
      * BP138214 17/02/2020 AM25 CATER FOR BP13K762 COPYCOB CHANGES *           
      * BP138730 11/06/2021 AM25 CORRECT CALCULATE OF DTE-CLOSE-BTO *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13F500 ASSIGN       TO BP13F500.                            
                                                                                
           SELECT BP13L54D ASSIGN       TO BP13L54D.                            
                                                                                
           SELECT BP13L54G ASSIGN       TO BP13L54G.                            
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS WS-K060-STATUS.                      
                                                                                
           SELECT BP13K762 ASSIGN       TO BP13K762                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K762-KEY-FLD                         
                           FILE STATUS  IS WS-K762-STATUS.                      
                                                                                
           SELECT BP13K769 ASSIGN       TO BP13K769                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K769-KEY-FLD                         
                           FILE STATUS  IS WS-K769-STATUS.                      
                                                                                
           SELECT BP13K766 ASSIGN       TO BP13K766                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K766-KEY-FLD                         
                           FILE STATUS  IS WS-K766-STATUS.                      
                                                                                
           SELECT BP13LB57 ASSIGN       TO BP13LB57.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F500                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13F500.                                                           
                                                                                
       FD   BP13L54D                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 133 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       01   BP13L54D-REC.                                                       
            05  FILLER                 PIC X(10)  VALUE SPACES.                 
            05  L54D-REGNO             PIC X(08)  VALUE SPACES.                 
            05  FILLER                 PIC X(14)  VALUE SPACES.                 
            05  L54D-NT                PIC X(03)  VALUE SPACES.                 
            05  FILLER                 PIC X(07)  VALUE SPACES.                 
            05  L54D-FT                PIC X(02)  VALUE SPACES.                 
            05  FILLER                 PIC X(04)  VALUE SPACES.                 
            05  L54D-DTE-REQ           PIC X(08)  VALUE SPACES.                 
            05  FILLER                 PIC X(02)  VALUE SPACES.                 
            05  L54D-TME-REQ           PIC X(08)  VALUE SPACES.                 
            05  FILLER                 PIC X(67)  VALUE SPACES.                 
                                                                                
       FD   BP13L54G                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 133 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       01   BP13L54G-REC.                                                       
            05  FILLER                 PIC X(10)  VALUE SPACES.                 
            05  L54G-REGNO             PIC X(08)  VALUE SPACES.                 
            05  FILLER                 PIC X(14)  VALUE SPACES.                 
            05  L54G-NT                PIC X(03)  VALUE SPACES.                 
            05  FILLER                 PIC X(07)  VALUE SPACES.                 
            05  L54G-FT                PIC X(02)  VALUE SPACES.                 
            05  FILLER                 PIC X(89)  VALUE SPACES.                 
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25 CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K762                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K762.                                                           
                                                                                
       FD   BP13K769                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K769.                                                           
                                                                                
       FD   BP13K766                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K766.                                                           
                                                                                
       FD   BP13LB57                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       01   BP13LB57-REC.                                                       
            05  LB57-NUM-ZONE               PIC X(03).                          
            05  LB57-NUM-FLAT-TYPE          PIC X(02).                          
            05  LB57-DTE-BALLOT             PIC X(06).                          
            05  LB57-NUM-SALES-MODE         PIC X(03).                          
            05  LB57-NUM-UNIT-OFFER         PIC 9(04).                          
            05  LB57-NUM-APPLN-RECV         PIC 9(05).                          
            05  LB57-NUM-APPLN-RECV-FTS     PIC 9(05).                          
            05  LB57-NUM-APPLN-RECV-TOT     PIC 9(05).                          
            05  LB57-NUM-APPLN-CAN          PIC 9(05).                          
            05  LB57-NUM-APPLN-RECV-AF-CL   PIC 9(05).                          
            05  LB57-NUM-APPLN-RAND-NO-TOT  PIC 9(05).                          
            05  LB57-NUM-RAND-NO-ASSIGN     PIC 9(05).                          
            05  LB57-NME-NEW-TOWN           PIC X(20).                          
            05  LB57-NUM-GEN3-TAG           PIC X(01).                          
            05  LB57-TOT-APPLN-RAND-NO-TOT  PIC 9(06).                          
            05  LB57-TOT-RAND-NO-ASSIGN     PIC 9(06).                          
            05  FILLER                      PIC X(114).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K813-STATUS               PIC 9(02)   VALUE ZEROES.           
           05  WS-K060-STATUS               PIC 9(02)   VALUE ZEROES.           
           05  WS-K762-STATUS               PIC 9(02)   VALUE ZEROES.           
           05  WS-K769-STATUS               PIC 9(02)   VALUE ZEROES.           
           05  WS-K766-STATUS               PIC 9(02)   VALUE ZEROES.           
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-F500-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-L54D-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-L54G-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-LB57-WRITE                PIC 9(06)   VALUE ZEROES.           
                                                                                
       01  WS-F730-COUNTERS.                                                    
           05  WS-NUM-APPLN-RECV            PIC 9(05)   VALUE ZEROES.           
           05  WS-NUM-APPLN-RECV-TOT        PIC 9(05)   VALUE ZEROES.           
           05  WS-NUM-APPLN-CAN             PIC 9(05)   VALUE ZEROES.           
           05  WS-NUM-APPLN-RECV-AF-CL      PIC 9(05)   VALUE ZEROES.           
           05  WS-NUM-RAND-NO-ASSIGN        PIC 9(05)   VALUE ZEROES.           
                                                                                
       01  WS-GRAND-TOTAL.                                                      
           05  WS-TOT-APPLN-RAND-NO-TOT     PIC 9(06)   VALUE ZEROES.           
           05  WS-TOT-RAND-NO-ASSIGN        PIC 9(06)   VALUE ZEROES.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURR-DATE                 PIC X(08)   VALUE SPACES.           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F730-KEY.                                                     
               10  WS-F730-NT               PIC X(03)   VALUE SPACES.           
               10  WS-F730-FT               PIC X(02)   VALUE SPACES.           
           05  WS-F730-DTE-BAL              PIC X(06)   VALUE SPACES.           
           05  WS-F730-SMODE                PIC X(03)   VALUE SPACES.           
           05  WS-F500-KEY.                                                     
               10  WS-F500-NT               PIC X(03)   VALUE SPACES.           
               10  WS-F500-FT               PIC X(02)   VALUE SPACES.           
           05  WS-L54D-KEY.                                                     
               10  WS-L54D-NT               PIC X(03)   VALUE SPACES.           
               10  WS-L54D-FT               PIC X(02)   VALUE SPACES.           
           05  WS-L54G-KEY.                                                     
               10  WS-L54G-NT               PIC X(03)   VALUE SPACES.           
               10  WS-L54G-FT               PIC X(02)   VALUE SPACES.           
           05  WS-NME-NEW-TOWN              PIC X(20)   VALUE SPACES.           
           05  WS-DTE-CLOSE-BTO             PIC 9(08)   VALUE ZEROES.           
           05  WS-DTE-CLOSE-BTO-R REDEFINES WS-DTE-CLOSE-BTO.                   
               10  WS-DTE-CLOSE-BTO-C       PIC X(08).                          
           05  WS-DTE-NEXT-INT              PIC S9(9)   VALUE ZEROES.           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
           IF WS-F730-READ > ZEROES                                             
              PERFORM 2200-READ-BP13F500    THRU 2200-EXIT                      
              PERFORM 2400-READ-BP13L54D    THRU 2400-EXIT                      
              PERFORM 2600-READ-BP13L54G    THRU 2600-EXIT                      
           END-IF.                                                              
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-F730-KEY = HIGH-VALUES.                                   
           IF WS-F730-READ > ZEROES                                             
              PERFORM 7100-WRITE-BP13LB57-TOT THRU 7100-EXIT                    
           END-IF.                                                              
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F500                                                 
                       BP13L54D                                                 
                       BP13L54G                                                 
                       BP13K813                                                 
                       BP13K060                                                 
                       BP13K762                                                 
                       BP13K769                                                 
                       BP13K766                                                 
                OUTPUT BP13LB57.                                                
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13K813, STATUS : ' WS-K813-STATUS           
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13K060, STATUS : ' WS-K060-STATUS           
              MOVE WS-K060-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K762-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13K762, STATUS : ' WS-K762-STATUS           
              MOVE WS-K762-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K769-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13K769, STATUS : ' WS-K769-STATUS           
              MOVE WS-K769-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K766-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR BP13K766, STATUS : ' WS-K766-STATUS           
              MOVE WS-K766-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE     TO WS-CURR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13F730.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F730-KEY                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F730-READ.                    
                                                                                
           MOVE F730-CDE-NT1                TO WS-F730-NT.                      
           MOVE F730-CDE-FLAT-TYPE          TO WS-F730-FT.                      
           MOVE F730-DTE-BALLOT             TO WS-F730-DTE-BAL.                 
           MOVE F730-NUM-ALLO-CAT           TO WS-F730-SMODE.                   
                                                                                
           MOVE ZEROES                      TO WS-F730-COUNTERS.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2200-READ-BP13F500.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F500 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F500-KEY                      
                GO TO 2200-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F500-READ.                    
                                                                                
           MOVE F500-CDE-NT1                TO WS-F500-NT.                      
           MOVE F500-CDE-FLAT-TYPE          TO WS-F500-FT.                      
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2400-READ-BP13L54D.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13L54D AT END                                                 
                MOVE HIGH-VALUES            TO WS-L54D-KEY                      
                GO TO 2400-EXIT.                                                
                                                                                
           ADD 1                            TO WS-L54D-READ.                    
                                                                                
           MOVE L54D-NT                     TO WS-L54D-NT.                      
           MOVE L54D-FT                     TO WS-L54D-FT.                      
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2600-READ-BP13L54G.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13L54G AT END                                                 
                MOVE HIGH-VALUES            TO WS-L54G-KEY                      
                GO TO 2600-EXIT.                                                
                                                                                
           ADD 1                            TO WS-L54G-READ.                    
                                                                                
           MOVE L54G-NT                     TO WS-L54G-NT.                      
           MOVE L54G-FT                     TO WS-L54G-FT.                      
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3000-PROCESS-RECS.                                                       
      *------------------*                                                      
                                                                                
           IF WS-F730-KEY = WS-F500-KEY                                         
              PERFORM 3500-READ-BP13K813    THRU 3500-EXIT                      
                                                                                
              PERFORM 4000-PROCESS-F500     THRU 4000-EXIT                      
                UNTIL WS-F500-KEY > WS-F730-KEY                                 
                   OR WS-F500-KEY = HIGH-VALUES                                 
                                                                                
              PERFORM 4200-PROCESS-L54D     THRU 4200-EXIT                      
                UNTIL WS-L54D-KEY > WS-F730-KEY                                 
                   OR WS-L54D-KEY = HIGH-VALUES                                 
                                                                                
              PERFORM 4400-PROCESS-L54G     THRU 4400-EXIT                      
                UNTIL WS-L54G-KEY > WS-F730-KEY                                 
                   OR WS-L54G-KEY = HIGH-VALUES                                 
                                                                                
              IF WS-F730-SMODE = 'BTO'                                          
                 PERFORM 5000-READ-BP13K762 THRU 5000-EXIT                      
              ELSE                                                              
                 IF WS-F730-SMODE = 'SBF'                                       
                    PERFORM 5200-READ-BP13K769 THRU 5200-EXIT                   
                 ELSE                                                           
                    IF WS-F730-SMODE = 'ROF'                                    
                       PERFORM 5400-READ-BP13K766 THRU 5400-EXIT                
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-F730-SMODE = 'BTO' OR 'SBF'                                 
                 PERFORM 6000-DECODE-NEW-TOWN  THRU 6000-EXIT                   
              ELSE                                                              
                 IF WS-F730-SMODE = 'ROF'                                       
                    MOVE 'RE-OFFER OF BAL FLAT' TO WS-NME-NEW-TOWN              
                 END-IF                                                         
              END-IF                                                            
                                                                                
              PERFORM 7000-WRITE-BP13LB57      THRU 7000-EXIT                   
                                                                                
              PERFORM 2000-READ-BP13F730       THRU 2000-EXIT                   
           ELSE                                                                 
              IF WS-F730-KEY < WS-F500-KEY                                      
                 PERFORM 2000-READ-BP13F730    THRU 2000-EXIT                   
              ELSE                                                              
                 IF WS-F730-KEY > WS-F500-KEY                                   
                    PERFORM 2200-READ-BP13F500 THRU 2200-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3500-READ-BP13K813.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
                                                                                
           MOVE ZEROES                      TO WS-DTE-CLOSE-BTO.                
                                                                                
           MOVE WS-F730-NT                  TO K813-NUM-ZONE.                   
           MOVE WS-F730-FT                  TO K813-NUM-FLAT-TYPE.              
           MOVE WS-F730-DTE-BAL             TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K813-DTE-CLOSE-BTO     TO WS-DTE-CLOSE-BTO                 
                                                                                
                COMPUTE WS-DTE-NEXT-INT =                                       
                     FUNCTION INTEGER-OF-DATE(WS-DTE-CLOSE-BTO) + 1             
                COMPUTE WS-DTE-CLOSE-BTO =                                      
                     FUNCTION DATE-OF-INTEGER(WS-DTE-NEXT-INT)                  
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K813 : ' K813-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS              
                MOVE WS-K813-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-PROCESS-F500.                                                       
      *------------------*                                                      
                                                                                
           ADD 1                            TO WS-NUM-RAND-NO-ASSIGN.           
                                                                                
           PERFORM 2200-READ-BP13F500       THRU 2200-EXIT.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4200-PROCESS-L54D.                                                       
      *------------------*                                                      
                                                                                
           IF WS-L54D-KEY = WS-F730-KEY                                         
              IF (L54D-DTE-REQ < WS-DTE-CLOSE-BTO-C) OR                         
                 (L54D-DTE-REQ = WS-DTE-CLOSE-BTO-C AND                         
                  L54D-TME-REQ <= '13000000')                                   
                 ADD 1                      TO WS-NUM-APPLN-RECV-TOT            
              ELSE                                                              
                 ADD 1                      TO WS-NUM-APPLN-RECV-AF-CL          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2400-READ-BP13L54D       THRU 2400-EXIT.                     
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4400-PROCESS-L54G.                                                       
      *------------------*                                                      
                                                                                
           IF WS-L54G-KEY = WS-F730-KEY                                         
              ADD 1                         TO WS-NUM-APPLN-CAN                 
           END-IF.                                                              
                                                                                
           PERFORM 2600-READ-BP13L54G       THRU 2600-EXIT.                     
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5000-READ-BP13K762.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K762-REC.                    
           INITIALIZE                          BP13K762-REC.                    
                                                                                
           MOVE WS-F730-NT                  TO K762-NUM-CHOICE.                 
           MOVE WS-F730-FT                  TO K762-NUM-FT.                     
                                                                                
           READ BP13K762.                                                       
                                                                                
           EVALUATE WS-K762-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K762-NUM-DEMAND        TO WS-NUM-APPLN-RECV                
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K762 : ' K762-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K762 : ' WS-K762-STATUS              
                MOVE WS-K762-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5200-READ-BP13K769.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K769-REC.                    
           INITIALIZE                          BP13K769-REC.                    
                                                                                
           MOVE WS-F730-NT                  TO K769-NUM-CHOICE.                 
           MOVE WS-F730-FT                  TO K769-NUM-FT.                     
                                                                                
           READ BP13K769.                                                       
                                                                                
           EVALUATE WS-K769-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K769-NUM-DEMAND        TO WS-NUM-APPLN-RECV                
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K769 : ' K769-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K769 : ' WS-K769-STATUS              
                MOVE WS-K769-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5400-READ-BP13K766.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13K766-REC.                    
           INITIALIZE                          BP13K766-REC.                    
                                                                                
           MOVE WS-F730-NT                  TO K766-NUM-CHOICE.                 
           MOVE WS-F730-FT                  TO K766-NUM-FT.                     
                                                                                
           READ BP13K766.                                                       
                                                                                
           EVALUATE WS-K766-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K766-NUM-DEMAND        TO WS-NUM-APPLN-RECV                
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K766 : ' K766-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K766 : ' WS-K766-STATUS              
                MOVE WS-K766-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       6000-DECODE-NEW-TOWN.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES                      TO BP13K060-REC.                    
           INITIALIZE                          BP13K060-REC.                    
                                                                                
           MOVE SPACES                      TO WS-NME-NEW-TOWN.                 
                                                                                
           MOVE 02                          TO K060-SERIAL-NO.                  
           MOVE WS-F730-NT                  TO K060-CODE.                       
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K060-DESC              TO WS-NME-NEW-TOWN                  
                                                                                
           WHEN 23                                                              
                DISPLAY 'KEY-FLD NOT FOUND IN BP13K060 : ' K060-KEY-FLD         
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K060 : ' WS-K060-STATUS              
                MOVE WS-K060-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       7000-WRITE-BP13LB57.                                                     
      *--------------------*                                                    
                                                                                
           MOVE SPACES                      TO BP13LB57-REC.                    
           INITIALIZE                          BP13LB57-REC.                    
                                                                                
           MOVE WS-F730-NT                  TO LB57-NUM-ZONE.                   
           MOVE WS-F730-FT                  TO LB57-NUM-FLAT-TYPE.              
           MOVE WS-F730-DTE-BAL             TO LB57-DTE-BALLOT.                 
           MOVE WS-F730-SMODE               TO LB57-NUM-SALES-MODE.             
           MOVE K813-NUM-UNIT-OFFER         TO LB57-NUM-UNIT-OFFER.             
           MOVE WS-NUM-APPLN-RECV           TO LB57-NUM-APPLN-RECV.             
           MOVE WS-NUM-APPLN-RECV-TOT       TO LB57-NUM-APPLN-RECV-TOT.         
           COMPUTE LB57-NUM-APPLN-RECV-FTS = WS-NUM-APPLN-RECV-TOT -            
                                             WS-NUM-APPLN-RECV.                 
           MOVE WS-NUM-APPLN-CAN            TO LB57-NUM-APPLN-CAN.              
           MOVE WS-NUM-APPLN-RECV-AF-CL     TO LB57-NUM-APPLN-RECV-AF-CL        
           IF WS-F730-FT = '2F' OR 'RF'                                         
              COMPUTE LB57-NUM-APPLN-RAND-NO-TOT =                              
                      WS-NUM-APPLN-RECV-TOT - WS-NUM-APPLN-CAN +                
                      WS-NUM-APPLN-RECV-AF-CL                                   
           ELSE                                                                 
              COMPUTE LB57-NUM-APPLN-RAND-NO-TOT =                              
                      WS-NUM-APPLN-RECV - WS-NUM-APPLN-CAN +                    
                      WS-NUM-APPLN-RECV-AF-CL                                   
           END-IF.                                                              
           MOVE WS-NUM-RAND-NO-ASSIGN       TO LB57-NUM-RAND-NO-ASSIGN.         
           MOVE WS-NME-NEW-TOWN             TO LB57-NME-NEW-TOWN.               
           MOVE K813-NUM-GEN3-TAG           TO LB57-NUM-GEN3-TAG.               
           ADD LB57-NUM-APPLN-RAND-NO-TOT   TO WS-TOT-APPLN-RAND-NO-TOT.        
           ADD LB57-NUM-RAND-NO-ASSIGN      TO WS-TOT-RAND-NO-ASSIGN.           
                                                                                
           WRITE BP13LB57-REC.                                                  
           ADD 1                            TO WS-LB57-WRITE.                   
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       7100-WRITE-BP13LB57-TOT.                                                 
      *------------------------*                                                
                                                                                
           MOVE SPACES                   TO BP13LB57-REC.                       
           INITIALIZE                       BP13LB57-REC.                       
                                                                                
           MOVE '999'                    TO LB57-NUM-ZONE.                      
           MOVE '99'                     TO LB57-NUM-FLAT-TYPE.                 
           MOVE WS-TOT-APPLN-RAND-NO-TOT TO LB57-TOT-APPLN-RAND-NO-TOT.         
           MOVE WS-TOT-RAND-NO-ASSIGN    TO LB57-TOT-RAND-NO-ASSIGN.            
           MOVE 'GRAND TOTAL'            TO LB57-NME-NEW-TOWN.                  
                                                                                
           WRITE BP13LB57-REC.                                                  
           ADD 1                         TO WS-LB57-WRITE.                      
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*===========================================*'.             
           DISPLAY '  BP13CB57 CONTROL TOTAL     : ' WS-CURR-DATE.              
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  RECS READ FROM BP13F730    : ' WS-F730-READ.              
           DISPLAY '  RECS READ FROM BP13F500    : ' WS-F500-READ.              
           DISPLAY '  RECS READ FROM BP13L54D    : ' WS-L54D-READ.              
           DISPLAY '  RECS READ FROM BP13L54G    : ' WS-L54G-READ.              
           DISPLAY '  RECS WRITTEN IN BP13LB57   : ' WS-LB57-WRITE.             
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13F730                                                       
                 BP13F500                                                       
                 BP13L54D                                                       
                 BP13L54G                                                       
                 BP13K813                                                       
                 BP13K060                                                       
                 BP13K762                                                       
                 BP13K769                                                       
                 BP13K766                                                       
                 BP13LB57.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K813, STATUS : ' WS-K813-STATUS          
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K060, STATUS : ' WS-K060-STATUS          
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K762-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K762, STATUS : ' WS-K762-STATUS          
              MOVE WS-K762-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K769-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K769, STATUS : ' WS-K769-STATUS          
              MOVE WS-K769-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K766-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K766, STATUS : ' WS-K766-STATUS          
              MOVE WS-K766-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
