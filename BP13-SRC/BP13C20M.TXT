      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C20M.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  10 AUGUST 2011.                                           
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  MONTHLY CANCELLATION OF DBSS APPLICATION    *          
      *                                                              *          
      *    INPUT FILES  : P13F800C                                   *          
      *    I-O          : BP13K800                                   *          
      *                 : BP13K848                                   *          
      *    OUTPUT       : P13F800D                                   *          
      *                 : BP13L20M                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP134210 ESA1 10082011 NEW PGM(MODELLED AFTER BP13C20L)       *          
      *BP135494 ESA1 07102014 TO CATER FOR EXTENSION OF SDS APPLN    *          
      *BP137200 FNP1 08032018 ADD WRITE TO BP13F915                  *          
      *BP137326 FNP1 06122018 BLANK OFF OTHER DETAILS WHEN CREATING  *          
      *                       'TE' RECORD FOR F915                   *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13F800C ASSIGN TO P13F800C.                                  
           SELECT P13F800D ASSIGN TO P13F800D.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BP13K848 ASSIGN TO BP13K848                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K848-KEY-FLD                               
                  FILE STATUS     IS K848-STATUS.                               
                                                                                
           SELECT BP13L20M ASSIGN TO BP13L20M.                                  
           SELECT BP13F915 ASSIGN TO BP13F915.                                  
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   P13F800C                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE  IS F.                                               
       COPY BP13F800.                                                           
                                                                                
       FD   P13F800D                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE  IS F.                                               
       01   P13F800D-REC.                                                       
            05 FILLER               PIC X(508).                                 
            05 F800D-DTE-BK-APPT    PIC X(08).                                  
            05 FILLER               PIC X(1484).                                
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K848                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K848.                                                           
                                                                                
       FD   BP13L20M                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   BP13L20M-REC               PIC X(132).                              
                                                                                
       FD   BP13F915                                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F915.                                                           
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F800C-EOF               PIC X(1)  VALUE 'N'.                  
           05  WS-F800C-COUNTER           PIC 9(2)  VALUE 0.                    
                                                                                
           05  WS-K820-FOUND              PIC X(1)  VALUE 'N'.                  
           05  K800-STATUS                PIC 9(2)  VALUE 0.                    
           05  K820-STATUS                PIC 9(2)  VALUE 0.                    
           05  K848-STATUS                PIC 9(2)  VALUE 0.                    
           05  WS-F80A-COUNTER            PIC 9(2)  VALUE 0.                    
                                                                                
           05  WS-CNT-K800-UPD            PIC 9(8)  VALUE 0.                    
           05  WS-CNT-K848-WRITE          PIC 9(8)  VALUE 0.                    
           05  WS-F800D-WRITE             PIC 9(8)  VALUE 0.                    
           05  WS-F915-WRITE              PIC 9(8)  VALUE 0.                    
           05  WS-CNT-CUR-LINE            PIC 9(5)  VALUE 60.                   
           05  WS-CNT-LINE-L20M           PIC 9(5)  VALUE 0.                    
           05  WS-CNT-PAGE-L20M           PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-NRIC                    PIC X(9)  VALUE SPACES.               
           05  WS-NUM-REGN                PIC X(8)  VALUE SPACES.               
           05  WS-CNT-F800C-READ          PIC 9(8)  VALUE 0.                    
           05  WS-K820-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K820-NTFND              PIC X(01) VALUE 'N'.                  
           05  WS-REC-PROCESS             PIC X(01) VALUE 'N'.                  
           05  WS-K820-FND                PIC 9(06) VALUE ZEROES.               
           05  WS-K820-NOTFND             PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
       01  WS-SYSTEM-TIME.                                                      
           05 WS-HH                       PIC 9(2).                             
           05 WS-MM                       PIC 9(2).                             
           05 WS-SS                       PIC 9(2).                             
           05 WS-MI                       PIC 9(2).                             
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  L20M-HDG1.                                                           
           05  L20M-LISTING             PIC X(14) VALUE 'BP13L20M'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L20M-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  L20M-PAGE                PIC ZZZZ9.                              
                                                                                
       01  L20M-HDG2.                                                           
           05  FILLER                   PIC X(41) VALUE SPACES.                 
           05  FILLER                   PIC X(50) VALUE                         
               'CONTROL LIST FOR CANCELLATION OF APPLICATION WHICH'.            
                                                                                
       01  L20M-HDG2A.                                                          
           05  FILLER                   PIC X(41) VALUE SPACES.                 
           05  FILLER                   PIC X(50) VALUE                         
               'WHICH WERE INVITED BUT DID NOT SELECT A FLAT'.                  
                                                                                
       01  L20M-HDG3.                                                           
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S/N REGN      ALLO NT  FT  CAT  HH/B-HH'.                       
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(40) VALUE                         
               'DTE_BK    QUEUE  MOD    NRIC1      NRIC2'.                      
           05  FILLER                   PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(36) VALUE                         
               'CANCEL REASON    DTE-CANCEL'.                                   
                                                                                
       01  L20M-DTL.                                                            
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-SN                  PIC ZZZZ9 VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  L20M-REGN                PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-ALLO-SCH            PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-NT                  PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-FT                  PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-CAT                 PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  L20M-HH                  PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE '/'.                    
           05  L20M-BALLOT-HH           PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  L20M-DTE-BK              PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-QUEUE               PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-ALLO-CAT            PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  L20M-NRIC1               PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  L20M-NRIC2               PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  L20M-CANCEL-REASON       PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(9)  VALUE SPACES.                 
           05  L20M-DTE-CANCEL          PIC X(10) VALUE SPACES.                 
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 1200-READ-F800C        THRU 1200-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                UNTIL WS-F800C-EOF = 'Y'.                                       
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  P13F800C                                                 
                       BP13K820                                                 
                I-O    BP13K800                                                 
                       BP13K848                                                 
                OUTPUT P13F800D                                                 
                       BP13F915                                                 
                       BP13L20M.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K800, STATUS = ' K800-STATUS           
              MOVE     K800-STATUS TO RETURN-CODE                               
              PERFORM  9999-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K820, STATUS = ' K820-STATUS           
              MOVE     K820-STATUS TO RETURN-CODE                               
              PERFORM  9999-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF K848-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K848, STATUS = ' K848-STATUS           
              MOVE     K848-STATUS TO RETURN-CODE                               
              PERFORM  9999-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F800C.                                                         
      *--------------------------------------------------------------*          
                                                                                
           READ P13F800C                                                        
                AT END  MOVE 'Y' TO WS-F800C-EOF                                
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F800C-READ.                                          
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           PERFORM 3000-PROCESS1          THRU 3000-EXIT.                       
                                                                                
           PERFORM 1200-READ-F800C        THRU 1200-EXIT.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-PROCESS1.                                                           
      *--------------------------------------------------------------*          
                                                                                
           MOVE F800-NUM-REGN         TO WS-NUM-REGN.                           
           PERFORM 4000-READ-K800     THRU 4000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       4000-READ-K800.                                                          
      *--------------------------------------------------------------*          
           MOVE LOW-VALUES            TO K800-NUM-REGN.                         
           MOVE WS-NUM-REGN           TO K800-NUM-REGN.                         
                                                                                
           READ BP13K800.                                                       
           EVALUATE K800-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                   IF (K800-NUM-STATUS NOT = 'C') OR                            
                      (K800-NUM-STATUS = 'C' AND                                
                       K800-NUM-ALLOC-TAG = 'RQ')                               
                       PERFORM 5000-WRITE-K800-IMAGE THRU 5000-EXIT             
                       PERFORM 7000-REWRITE-K800     THRU 7000-EXIT             
                   END-IF                                                       
              WHEN 23                                                           
                   DISPLAY 'BP13K800 RECORD NOT FOUND: ' K800-NUM-REGN          
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K800. STATUS IS '                 
                           K800-STATUS                                          
                   PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-K800-IMAGE.                                                   
      *--------------------------------------------------------------*          
                                                                                
           MOVE BP13K800-MASTER TO BP13K848-MASTER.                             
           MOVE 'C20M'          TO K848-NUM-TRANS-BKIMAGE.                      
                                                                                
           MOVE F800-NUM-USERID TO K848-NUM-USERID.                             
           MOVE WS-SYSTEM-DATE  TO K848-DTE-BKIMAGE.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
                                                                                
           MOVE WS-SYSTEM-TIME  TO K848-TME-BKIMAGE.                            
           MOVE K848-NUM-REGN   TO K848-NUM-REGN-BKIMAGE.                       
                                                                                
           WRITE BP13K848-MASTER.                                               
                                                                                
           EVALUATE K848-STATUS                                                 
               WHEN 00                                                          
                    ADD 1 TO WS-CNT-K848-WRITE                                  
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K848. STATUS IS '                
                            K848-STATUS                                         
                    PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       7000-REWRITE-K800.                                                       
      *--------------------------------------------------------------*          
           MOVE 'C'                     TO K800-NUM-STATUS.                     
                                                                                
           IF K800-NUM-ALLOC-TAG = 'RQ'                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE WS-SYSTEM-DATE TO K800-DTE-CANCEL                            
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-ALLOC-TAG TO K800-NUM-CANCEL-REASON.                   
                                                                                
           MOVE F800-NUM-USERID         TO K800-NUM-USERID.                     
                                                                                
           MOVE WS-SYSTEM-DATE    TO K800-DTE-UPDATE.                           
           MOVE SPACES            TO K800-DTE-LETTER-RECV-CAN.                  
                                                                                
           IF F800-NUM-HALF-IDP = 'A' OR 'Q'                                    
              MOVE SPACES    TO K800-NUM-HALF-IDP                               
           END-IF.                                                              
                                                                                
           MOVE 'N'               TO K800-NUM-REGN-REFUND.                      
           MOVE WS-SYSTEM-DATE    TO K800-DTE-REQ-CANCEL.                       
           MOVE SPACES            TO K800-TME-QUEUE                             
                                     K800-TME-QUEUE-PRIO-IFPA                   
                                     K800-NUM-SHORTLIST-PA-HHTY                 
                                     K800-NUM-SHORTLIST-PA-CAT                  
                                     K800-NUM-SHORTLIST-PRIO-HHTY               
                                     K800-NUM-SHORTLIST-PRIO-CAT                
                                     K800-NUM-SLIST-PA-ZNFT                     
                                     K800-NUM-SLIST-PA-ZNFT-CAT                 
                                     K800-NUM-SLIST-PRIO-ZNFT                   
                                     K800-NUM-SLIST-PRIO-ZNFT-CAT.              
                                                                                
            REWRITE BP13K800-MASTER.                                            
                                                                                
            EVALUATE K800-STATUS                                                
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1    TO WS-CNT-K800-UPD                                 
                    MOVE BP13K800-MASTER    TO P13F800D-REC                     
                    MOVE 'NOINVITE'         TO F800D-DTE-BK-APPT                
                    WRITE P13F800D-REC                                          
                    ADD 1   TO WS-F800D-WRITE                                   
                    PERFORM 8000-PROCESS-CDB   THRU 8000-EXIT                   
                    PERFORM 9000-PRINT-REPORT  THRU 9000-EXIT                   
               WHEN OTHER                                                       
                    DISPLAY 'ERROR UPDATING BP13K800. STATUS IS '               
                            K800-STATUS                                         
                    PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       8000-PROCESS-CDB.                                                        
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE 'N'                      TO WS-K820-EOF                         
                                            WS-K820-NTFND                       
                                            WS-REC-PROCESS.                     
                                                                                
           MOVE K800-NUM-REGN            TO K820-NUM-REGN.                      
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 8100-READNEXT-BP13K820 THRU 8100-EXIT               
                      UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                   
                         OR WS-K820-EOF = 'Y'                                   
                         OR WS-K820-NTFND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 START BROWSE ERROR : '                    
                            K820-STATUS                                         
                    MOVE K820-STATUS  TO RETURN-CODE                            
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       8100-READNEXT-BP13K820.                                                  
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
                    IF K820-NUM-REGN = K800-NUM-REGN                            
                       ADD 1             TO WS-K820-FND                         
                       MOVE 'Y'          TO WS-REC-PROCESS                      
                                                                                
                       PERFORM 8200-WRITE-BP13F915 THRU 8200-EXIT               
                    ELSE                                                        
                       IF WS-REC-PROCESS = 'N'                                  
                          ADD 1          TO WS-K820-NOTFND                      
                       END-IF                                                   
                                                                                
                       MOVE 'Y'          TO WS-K820-NTFND                       
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
                    MOVE 'Y'             TO WS-K820-NTFND                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ NEXT ERROR : ' K820-STATUS           
                    MOVE K820-STATUS  TO RETURN-CODE                            
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       8200-WRITE-BP13F915.                                                     
      *************************************************************             
           IF K820-NUM-CSTMR-SOURCE NOT = SPACES AND LOW-VALUES                 
              MOVE SPACES                   TO BP13F915-REC                     
              INITIALIZE                       BP13F915-REC                     
                                                                                
              MOVE  'TE'                    TO F915-TXN-TYPE                    
              MOVE  'P'                     TO F915-CSTMR-TYPE                  
              MOVE  'BP13'                  TO F915-NUM-UPDATE-SOURCE           
              MOVE  'BP13C20M'              TO F915-NUM-UPDATE-USERID           
              MOVE  K820-NUM-CSTMR-SOURCE   TO F915-NUM-CSTMR-SOURCE            
                                                                                
              IF K820-NUM-NRIC = K800-NUM-NRIC1                                 
                 MOVE '011'                 TO F915-NUM-ROLE                    
              ELSE                                                              
                 IF (K820-NUM-NRIC = K800-NUM-NRIC2) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC3) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC4)                            
                     MOVE '012'             TO F915-NUM-ROLE                    
                 ELSE                                                           
                     MOVE '013'             TO F915-NUM-ROLE                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE  K820-NUM-REGN           TO F915-NUM-BSNS-REF                
                                                                                
              STRING K800-DTE-CANCEL(1:4) '-'                                   
                     K800-DTE-CANCEL(5:2) '-'                                   
                     K800-DTE-CANCEL(7:2)                                       
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-ROLE-END                                          
                                                                                
              STRING WS-SYSTEM-DATE(1:4) '-'                                    
                     WS-SYSTEM-DATE(5:2) '-'                                    
                     WS-SYSTEM-DATE(7:2)                                        
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-UPDATE-SOURCE                                     
                                                                                
              WRITE BP13F915-REC                                                
                                                                                
              ADD 1                         TO WS-F915-WRITE                    
                                                                                
           END-IF.                                                              
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9000-PRINT-REPORT.                                                       
      *--------------------------------------------------------------*          
                                                                                
           ADD 1                          TO WS-CNT-LINE-L20M.                  
                                                                                
           IF WS-CNT-CUR-LINE > 58                                              
              PERFORM 9100-WRITE-HEADER THRU 9100-EXIT                          
              ADD 1                       TO WS-CNT-CUR-LINE                    
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-LINE-L20M          TO L20M-SN.                           
           MOVE F800-NUM-REGN             TO L20M-REGN.                         
           MOVE F800-NUM-ALLO-SCHEME      TO L20M-ALLO-SCH.                     
           MOVE F800-NUM-NEW-TOWN         TO L20M-NT.                           
           MOVE F800-NUM-FLAT-TYPE        TO L20M-FT.                           
           MOVE F800-NUM-CAT              TO L20M-CAT.                          
           MOVE F800-NUM-HOUSEHOLD        TO L20M-HH.                           
           MOVE F800-CDE-BALLOT-HOUSEHOLD TO L20M-BALLOT-HH.                    
           IF F800-DTE-BK-APPT = 'NOINVITE'                                     
              MOVE SPACES                 TO L20M-DTE-BK                        
           ELSE                                                                 
              MOVE F800-DTE-BK-APPT       TO L20M-DTE-BK                        
           END-IF.                                                              
           MOVE F800-NUM-NT-FT-QUEUE      TO L20M-QUEUE.                        
           MOVE F800-NUM-ALLO-CAT         TO L20M-ALLO-CAT.                     
           MOVE F800-NUM-NRIC1            TO L20M-NRIC1.                        
           MOVE F800-NUM-NRIC2            TO L20M-NRIC2.                        
           MOVE K800-NUM-CANCEL-REASON    TO L20M-CANCEL-REASON.                
           STRING K800-DTE-CANCEL(7:2) '/' K800-DTE-CANCEL(5:2)                 
                  '/' K800-DTE-CANCEL(1:4) DELIMITED BY SIZE                    
                  INTO L20M-DTE-CANCEL                                          
           END-STRING.                                                          
                                                                                
           WRITE BP13L20M-REC FROM L20M-DTL.                                    
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9100-WRITE-HEADER.                                                       
      **************************************************************            
                                                                                
           ADD 1                          TO WS-CNT-PAGE-L20M.                  
           MOVE WS-CNT-PAGE-L20M          TO L20M-PAGE.                         
           MOVE WS-EDITED-DATE            TO L20M-DATE.                         
           WRITE BP13L20M-REC FROM L20M-HDG1 AFTER PAGE.                        
           WRITE BP13L20M-REC FROM L20M-HDG2 AFTER 2.                           
           WRITE BP13L20M-REC FROM L20M-HDG2A.                                  
           WRITE BP13L20M-REC FROM L20M-HDG3 AFTER 3.                           
           MOVE 7                         TO WS-CNT-CUR-LINE.                   
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BP13K848                                                       
                 P13F800C                                                       
                 P13F800D                                                       
                 BP13F915                                                       
                 BP13L20M.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K800, STATUS = ' K800-STATUS           
              MOVE     K800-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K820, STATUS = ' K820-STATUS           
              MOVE     K820-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF K848-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K848, STATUS = ' K848-STATUS           
              MOVE     K848-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C20M CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (P13F800C) = ' WS-CNT-F800C-READ.               
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECS UPDATED    = '.                                 
           DISPLAY '    (BP13K800)         = ' WS-CNT-K800-UPD.                 
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECS WRITE      = '.                                 
           DISPLAY '    (BP13K848)         = ' WS-CNT-K848-WRITE.               
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECS WRITE      = ' .                                
           DISPLAY '    (P13F800D)         = ' WS-F800D-WRITE.                  
           DISPLAY 'NO. OF RECS WRITE      = ' .                                
           DISPLAY '    (BP13F915)         = ' WS-F915-WRITE.                   
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
