       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C609.                                                    
       AUTHOR. SONNY I. ALEJANDR.                                               
      *================================================================*        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  INPUT FILES :                                                 *        
      *    1.  BP13K454                                                *        
      *  OUTPUT:                                                                
      *    2.  BP13F454                                                *        
      *                                                                *        
      * CHG-NO   BY   ON       DESCRIPTION                             *        
      * ------   --- --------  -----------                             *        
      * BP130154 SIA 19960919  NEW PROGRAM                             *        
      * BP130418 CLT 19980827  Y2K CHANGES                             *        
      * BP135959 KSJ 20151120  CHANGE END DATE TO 1 DAY LATER (4 DAYS  *        
      *                        FROM CURRENT DATE)                      *        
      *================================================================*        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F454  ASSIGN TO BP13F454.                                 
                                                                                
           SELECT BP13K454  ASSIGN TO BP13K454                                  
                  ACCESS MODE IS SEQUENTIAL                                     
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K454-KEY-FLD                                    
                  FILE STATUS IS WS-K454-STATUS.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K454                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORD IS STANDARD.                                            
       COPY BP13K454.                                                           
                                                                                
       FD  BP13F454                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORD IS STANDARD.                                            
       COPY BP13F454.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K454-STATUS          PIC 9(2)    VALUE ZEROES.                    
       01  WS-K454-EOF             PIC X(3).                                    
           88  K454-EOF                        VALUE 'YES'.                     
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-SYS-CCYY.                                                     
               10  WS-SYS-CC       PIC 9(2)    VALUE ZEROES.                    
               10  WS-SYS-YY       PIC 9(2)    VALUE ZEROES.                    
           05  WS-SYS-CCYY1 REDEFINES WS-SYS-CCYY  PIC 9(4).                    
           05  WS-SYS-MM           PIC 9(2)    VALUE ZEROES.                    
           05  WS-SYS-DD           PIC 9(2)    VALUE ZEROES.                    
                                                                                
       01  WS-START-DATE.                                                       
           05  WS-START-CC         PIC X(2)    VALUE SPACES.                    
           05  WS-START-YY         PIC X(2)    VALUE SPACES.                    
           05  WS-START-MM         PIC X(2)    VALUE SPACES.                    
           05  WS-START-DD         PIC X(2)    VALUE SPACES.                    
                                                                                
       01  WS-END-DATE.                                                         
           05  WS-END-CC           PIC X(2)    VALUE SPACES.                    
           05  WS-END-YY           PIC X(2)    VALUE SPACES.                    
           05  WS-END-MM           PIC X(2)    VALUE SPACES.                    
           05  WS-END-DD           PIC X(2)    VALUE SPACES.                    
                                                                                
       01  WS-TMP-DATE.                                                         
           05  WS-TMP-CCYY         PIC 9(4)    VALUE ZEROES.                    
           05  WS-TMP-MM           PIC 9(2)    VALUE ZEROES.                    
           05  WS-TMP-DD           PIC 9(2)    VALUE ZEROES.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K454-READ        PIC 9(5)    VALUE ZERO.                      
           05  WS-F454-WRITE       PIC 9(5)    VALUE ZERO.                      
                                                                                
       01  WS-MM-31D                        PIC 9(2).                           
           88   31-DAY-MONTH                VALUE 01, 03, 05, 07, 08,           
                                                  10, 12.                       
           88   30-DAY-MONTH                VALUE 04, 06, 09, 11.               
           88   28-29-DAY-MONTH             VALUE 02.                           
           88   VALID-MONTH                 VALUE 01 THRU 12.                   
                                                                                
       01  WS-REMAINDER                     PIC 9           VALUE ZERO.         
       01  WS-QUOTIENT                      PIC 99          VALUE ZERO. .       
       01  WS-YEAR-TYPE                     PIC X           VALUE '0'.          
           88  WS-LEAP-YEAR                                 VALUE '1'.          
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *===============================================================*         
      *  MAIN CONTROL                                                 *         
      *===============================================================*         
       000-MAIN-CTRL.                                                           
           PERFORM 100-OPEN-FILES      THRU  100-EXIT.                          
           PERFORM 400-COMPUTE-DATE    THRU  400-EXIT.                          
           MOVE    LOW-VALUES          TO    K454-KEY-FLD.                      
           START   BP13K454 KEY IS     NOT < K454-KEY-FLD.                      
           PERFORM 200-READ-K454       THRU  200-EXIT.                          
           PERFORM 300-PROCESS         THRU  300-EXIT                           
                                       UNTIL K454-EOF.                          
           PERFORM 999-CLOSE-FILES     THRU  999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *===============================================================*         
       100-OPEN-FILES.                                                          
      *===============================================================*         
           OPEN INPUT  BP13K454                                                 
                OUTPUT BP13F454.                                                
                                                                                
           IF WS-K454-STATUS NOT = 0 AND 97                                     
              DISPLAY 'ERROR IN OPENING BP13K454 STATUS IS '                    
              WS-K454-STATUS                                                    
              MOVE WS-K454-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-FILES.                                          
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-CURRENT-DATE.                      
                                                                                
           MOVE 'NO'                TO WS-K454-EOF.                             
           MOVE ZEROES              TO WS-COUNTERS.                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *===============================================================*         
       200-READ-K454.                                                           
      *===============================================================*         
           READ BP13K454 NEXT RECORD.                                           
           EVALUATE WS-K454-STATUS                                              
           WHEN 00                                                              
                ADD 1                   TO WS-K454-READ                         
           WHEN 10                                                              
                MOVE 'YES' TO WS-K454-EOF                                       
           WHEN OTHER                                                           
              DISPLAY 'BP13K454 FILE READ ERROR.'                               
              PERFORM 999-CLOSE-FILES     THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *===============================================================*         
       300-PROCESS.                                                             
      *===============================================================*         
                                                                                
           IF (K454-DTE-APPT >= WS-START-DATE) AND                              
              (K454-DTE-APPT <= WS-END-DATE)                                    
               MOVE  BP13K454-REC       TO   BP13F454-REC                       
               WRITE BP13F454-REC                                               
               ADD   1                  TO   WS-F454-WRITE.                     
                                                                                
           PERFORM 200-READ-K454       THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *===============================================================*         
       400-COMPUTE-DATE.                                                        
      *===============================================================*         
      *--> ADD 2 DAY TO START DATE                                              
           MOVE    WS-CURRENT-DATE     TO   WS-TMP-DATE                         
           ADD     2                   TO   WS-TMP-DD.                          
           PERFORM 401-VALIDATE-DATE   THRU 401-EXIT.                           
           MOVE    WS-TMP-DATE         TO   WS-START-DATE                       
                                                                                
      *--> FROM START DATE ADD 2 MORE DAY TO GET 4 DAYS FROM CURR DAY.          
           ADD     2                   TO   WS-TMP-DD.                          
           PERFORM 401-VALIDATE-DATE   THRU 401-EXIT.                           
           MOVE    WS-TMP-DATE         TO   WS-END-DATE.                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===============================================================*         
       401-VALIDATE-DATE.                                                       
      *===============================================================*         
                                                                                
      *--> CHECK FOR LEAP YEAR                                                  
           DIVIDE WS-SYS-CCYY1 BY 4 GIVING    WS-QUOTIENT                       
                                 REMAINDER WS-REMAINDER.                        
           IF WS-REMAINDER = 0                                                  
              MOVE '1' TO WS-YEAR-TYPE.                                         
                                                                                
           MOVE WS-TMP-MM  TO WS-MM-31D.                                        
           EVALUATE TRUE                                                        
           WHEN 31-DAY-MONTH AND WS-TMP-DD > 31                                 
                ADD       1 TO   WS-TMP-MM                                      
                SUBTRACT 31 FROM WS-TMP-DD                                      
                                                                                
           WHEN 30-DAY-MONTH AND WS-TMP-DD > 30                                 
                ADD       1 TO   WS-TMP-MM                                      
                SUBTRACT 30 FROM WS-TMP-DD                                      
                                                                                
           WHEN (28-29-DAY-MONTH) AND (WS-LEAP-YEAR) AND                        
                (WS-TMP-DD > 29)                                                
                ADD       1 TO   WS-TMP-MM                                      
                SUBTRACT 29 FROM WS-TMP-DD                                      
                                                                                
           WHEN (28-29-DAY-MONTH) AND (NOT WS-LEAP-YEAR) AND                    
                (WS-TMP-DD > 28)                                                
                ADD       1 TO   WS-TMP-MM                                      
                SUBTRACT 28 FROM WS-TMP-DD                                      
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-TMP-MM  TO WS-MM-31D.                                        
                                                                                
           IF NOT VALID-MONTH                                                   
              ADD 1                   TO   WS-TMP-CCYY                          
              SUBTRACT 12             FROM WS-TMP-MM                            
           END-IF.                                                              
                                                                                
       401-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===============================================================*         
      *  CLOSE ALL FILES                                              *         
      *===============================================================*         
       999-CLOSE-FILES.                                                         
           CLOSE      BP13F454                                                  
                      BP13K454.                                                 
                                                                                
           IF WS-K454-STATUS NOT = 00                                           
              DISPLAY 'BP13K454 CLOSE ERROR, STATUS IS ' WS-K454-STATUS         
              MOVE WS-K454-STATUS TO RETURN-CODE.                               
                                                                                
           DISPLAY '*-----------------------------------*'.                     
           DISPLAY '* PROGRAM  : BP13C609'.                                     
           DISPLAY '* DATE     : ' WS-CURRENT-DATE.                             
           DISPLAY '*------------- COUNTERS ------------*'.                     
           DISPLAY 'K454-READ   ' WS-K454-READ.                                 
           DISPLAY 'F454-WRITE   ' WS-F454-WRITE.                               
                                                                                
       999-EXIT.                                                                
           STOP RUN.                                                            
                                                                                
