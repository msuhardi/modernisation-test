      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID. BP13C87A.                                                    
       AUTHOR. RJB1.                                                            
      *DATE-WRITTEN. 02/02/2016.                                                
                                                                                
      ******************************************************************        
      *                    SYSTEM OF COMMITMENT (BP13)                 *        
      *================================================================*        
      * OBJECTIVE: REPORT ON ENQUIRY OF SOC MASTER BY USERS FROM OTHER *        
      *            DEPARTMENTS WHO ARE DOING OVERTIME (OT) WORK AT SALES        
      *            DEPARTMENTS.                                        *        
      ******************************************************************        
      *  MODIFICATIONS :                                               *        
      *  CHGE REQ #  DATE          AUTHOR         REMARKS              *        
      *  ----------  ----          ------         -------              *        
      *  BP136030    02/02/2016    RJB1           NEW PROGRAM          *        
      ******************************************************************        
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800   ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT BP13K820   ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-NUM-NRIC                     
                             FILE STATUS   IS WS-K820-STATUS.                   
                                                                                
           SELECT SY08K010   ASSIGN        TO SY08K010                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K010-NUM-USERID                   
                             FILE STATUS   IS WS-K010-STATUS.                   
                                                                                
           SELECT BP13F860 ASSIGN TO BP13F860.                                  
           SELECT BP13F879 ASSIGN TO BP13F879.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13L87A ASSIGN TO BP13L87A.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD   SY08K010                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY SY08K010.                                                           
                                                                                
       FD   BP13F879                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE  IS   F.                                             
       COPY BP13F879.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13F860                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS   F.                                             
       COPY BP13F860.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13L87A                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL  RECORD   IS  OMITTED                                         
            DATA   RECORD   IS  BP13L87A-REC                                    
            RECORDING MODE IS F.                                                
       01   BP13L87A-REC         PIC X(132).                                    
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05  WS-DTE-CC                 PIC X(2).                              
           05  WS-DTE-CURRENT.                                                  
               10  WS-DTE-YY             PIC X(2).                              
               10  WS-DTE-MM             PIC X(2).                              
               10  WS-DTE-DD             PIC X(2).                              
                                                                                
       01  WS-DTE-LOG.                                                          
           05  WS-YYMM-LOG               PIC X(6).                              
           05  WS-DD-LOG                 PIC X(2).                              
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-PRINT-SW               PIC X     VALUE 'N'.                   
           05  WS-F860-EOF               PIC X     VALUE 'N'.                   
           05  WS-K820-EOF               PIC X     VALUE 'N'.                   
           05  WS-F879-EOF               PIC X     VALUE 'N'.                   
           05  WS-ERROR-SW               PIC X     VALUE 'N'.                   
           05  WS-F879-FOUND-TAG         PIC X     VALUE 'N'.                   
           05  WS-K800-FOUND-TAG         PIC X     VALUE 'N'.                   
           05  WS-K820-FOUND-TAG         PIC X     VALUE 'N'.                   
           05  WS-K010-FOUND-TAG         PIC X     VALUE 'N'.                   
           05  WS-K800-STATUS            PIC 9(2)  VALUE ZEROS.                 
           05  WS-K820-STATUS            PIC 9(2)  VALUE ZEROS.                 
           05  WS-K010-STATUS            PIC 9(2)  VALUE ZEROS.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-L87A-CNT-PAGE          PIC 9(5)  VALUE ZEROS.                 
           05  WS-L87A-CNT-LINE          PIC 9(5)  VALUE ZEROS.                 
           05  WS-L87A-CNT-REC           PIC 9(8)  VALUE ZEROS.                 
           05  WS-F860-CNT-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-F879-CNT-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-F879-NFND              PIC 9(8)  VALUE ZEROS.                 
           05  WS-K800-NFND              PIC 9(8)  VALUE ZEROS.                 
           05  WS-K820-NFND              PIC 9(8)  VALUE ZEROS.                 
           05  WS-K010-NFND              PIC 9(8)  VALUE ZEROS.                 
           05  WS-F879-FND               PIC 9(8)  VALUE ZEROS.                 
           05  WS-K800-FND               PIC 9(8)  VALUE ZEROS.                 
           05  WS-K820-FND               PIC 9(8)  VALUE ZEROS.                 
           05  WS-K010-FND               PIC 9(8)  VALUE ZEROS.                 
                                                                                
       01 WS-SAVE.                                                              
           05  WS-F860-COMP.                                                    
               10  WS-SVE-ID-F860        PIC X(05) VALUE SPACES.                
               10  WS-SVE-DTE-F860       PIC X(06) VALUE SPACES.                
           05  WS-F879-COMP.                                                    
               10  WS-SVE-ID-F879        PIC X(05) VALUE SPACES.                
               10  WS-SVE-DTE-F879       PIC X(06) VALUE SPACES.                
                                                                                
       01 WS-NUM-ACCESS-KEY.                                                    
           05  WS-F879-REGNO             PIC X(08) VALUE SPACES.                
           05  WS-FILLER                 PIC X(01) VALUE SPACES.                
                                                                                
      *---------------------------------------------------------------          
      *   LAY-OUT FOR BP13L87A REPORT                                           
      *---------------------------------------------------------------          
       01  WS-PAGE-HEADING1.                                                    
           05  FILLER                    PIC X(14) VALUE 'BP13L87A'.            
           05  FILLER                    PIC X(42) VALUE 'HDB3'.                
           05  FILLER                    PIC X(44)                              
               VALUE 'SYSTEM OF COMMITMENT'.                                    
           05  FILLER                    PIC X(7)  VALUE 'DATE : '.             
           05  L87A-DTE-HDR-DD           PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  L87A-DTE-HDR-MM           PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  L87A-DTE-HDR-CC           PIC X(2).                              
           05  L87A-DTE-HDR-YY           PIC X(5).                              
           05  FILLER                    PIC X(7)  VALUE 'PAGE : '.             
           05  L87A-PAGE-NUM             PIC ZZZ9.                              
           05  FILLER                    PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-PAGE-HEADING2.                                                    
           05  FILLER                    PIC X(35) VALUE SPACES.                
           05  FILLER                    PIC X(63) VALUE                        
               'REPORT ON ENQUIRY OF SOC MASTER BY USERS FROM OTHER DEPA        
      -        'RTMENTS'.                                                       
                                                                                
       01  WS-PAGE-HEADING3.                                                    
           05  FILLER                    PIC X(40) VALUE SPACES.                
           05  FILLER                    PIC X(53) VALUE                        
               'WHO ARE DOING OVERTIME (OT) WORK AT SALES DEPARTMENTS'.         
                                                                                
       01  WS-PAGE-COL-HEADING1.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(09)                              
               VALUE 'OT USERID'.                                               
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(20)                              
               VALUE 'OT USER NAME'.                                            
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(08)                              
               VALUE 'DEPT'.                                                    
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(08)                              
               VALUE 'DATE ACC'.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(08)                              
               VALUE 'TIME ACC'.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(09)                              
               VALUE 'NRIC/REGN'.                                               
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(07)                              
               VALUE 'DATE AO'.                                                 
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(07)                              
               VALUE 'DATE SO'.                                                 
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(07)                              
               VALUE 'DATE BK'.                                                 
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(10)                              
               VALUE '1ST LETTER'.                                              
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(13)                              
               VALUE '1ST TP LETTER'.                                           
                                                                                
       01  WS-HORIZONTAL-LINE.                                                  
           05  FILLER                    PIC X(132)                             
               VALUE ALL '-'.                                                   
                                                                                
       01  WS-PAGE-DATA-LINE.                                                   
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L87A-USERID               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L87A-USERNAME             PIC X(20) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L87A-DEPT                 PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L87A-DTE-ACCESS           PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L87A-TME-ACCESS           PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L87A-ACCESS-KEY           PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L87A-DTE-AO               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L87A-DTE-SO               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L87A-DTE-BK               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L87A-DTE-1ST              PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L87A-DTE-1ST-TP           PIC X(08) VALUE SPACES.                
                                                                                
       01  WS-END-OF-REPORT.                                                    
           05  FILLER                    PIC X(55) VALUE SPACES.                
           05  FILLER                    PIC X(21)                              
               VALUE '*** END OF REPORT ***'.                                   
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
       01  WS-EMPTY-REPORT.                                                     
           05  FILLER                    PIC X(56) VALUE SPACES.                
           05  FILLER                    PIC X(20)                              
               VALUE '*** EMPTY REPORT ***'.                                    
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F860 THRU 2000-EXIT.                           
           PERFORM 2500-READ-BP13F879 THRU 2500-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F860-EOF = 'Y'                                      
                      OR WS-F879-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F860                                                 
                       BP13F879                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       SY08K010                                                 
                       SY02F001                                                 
                OUTPUT BP13L87A.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'SY08K010 - ERROR OPENING : ' WS-K010-STATUS              
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT       TO WS-DTE-CCYYMMDD.                      
           MOVE WS-DTE-DD TO L87A-DTE-HDR-DD.                                   
           MOVE WS-DTE-MM TO L87A-DTE-HDR-MM.                                   
           MOVE WS-DTE-CC TO L87A-DTE-HDR-CC.                                   
           MOVE WS-DTE-YY TO L87A-DTE-HDR-YY.                                   
                                                                                
           PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F860.                                                      
      ******************************************************************        
           READ BP13F860 AT END                                                 
                         MOVE 'Y' TO WS-F860-EOF                                
                         GO TO 2000-EXIT                                        
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F860-CNT-READ.                                           
                                                                                
           MOVE BP13F860-REC(13:5)          TO WS-SVE-ID-F860.                  
           MOVE F001-DTE-LAST-DAY-PREV(1:6) TO WS-SVE-DTE-F860.                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2500-READ-BP13F879.                                                      
      ******************************************************************        
           READ BP13F879 AT END                                                 
                         MOVE 'Y' TO WS-F879-EOF                                
                         GO TO 2500-EXIT                                        
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F879-CNT-READ.                                           
           MOVE BP13F879-REC(96:5)         TO WS-SVE-ID-F879.                   
           MOVE F879-DTE-LOG(1:6)          TO WS-SVE-DTE-F879.                  
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS.                                                            
      ******************************************************************        
                                                                                
           IF WS-F879-COMP = WS-F860-COMP                                       
              ADD 1    TO WS-F879-FND                                           
              MOVE F879-NUM-ACCESS-KEY(1:9) TO WS-NUM-ACCESS-KEY                
              PERFORM 5000-PROCESS-REPORT                                       
                 THRU 5000-EXIT                                                 
              PERFORM 2500-READ-BP13F879                                        
                 THRU 2500-EXIT                                                 
           ELSE                                                                 
              IF WS-F879-COMP > WS-F860-COMP                                    
                 ADD 1 TO WS-F879-NFND                                          
                 PERFORM 2000-READ-BP13F860                                     
                    THRU 2000-EXIT                                              
              ELSE                                                              
                 PERFORM 2500-READ-BP13F879                                     
                    THRU 2500-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-PROCESS-REPORT.                                                     
      ******************************************************************        
           MOVE 'N'                        TO WS-PRINT-SW.                      
                                                                                
           IF WS-FILLER = SPACES OR LOW-VALUES                                  
              MOVE WS-F879-REGNO           TO K800-NUM-REGN                     
              PERFORM 6000-READ-BP13K800                                        
                 THRU 6000-EXIT                                                 
              PERFORM 5200-CHECK-DATES                                          
                 THRU 5200-EXIT                                                 
           ELSE                                                                 
              MOVE WS-NUM-ACCESS-KEY       TO K820-NUM-NRIC                     
              PERFORM 6600-STARTBR-K820                                         
                 THRU 6600-EXIT                                                 
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-CHECK-DATES.                                                        
      ******************************************************************        
                                                                                
           IF WS-K800-FOUND-TAG = 'Y'                                           
              IF K800-DTE-AO-APPT NOT = SPACES AND LOW-VALUES                   
                 IF F001-DTE-CURRENT NOT = K800-DTE-AO-APPT                     
                    MOVE 'Y'               TO WS-PRINT-SW                       
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF K800-DTE-SO-APPT NOT = SPACES AND LOW-VALUES                   
                 IF F001-DTE-CURRENT NOT = K800-DTE-SO-APPT                     
                    MOVE 'Y'               TO WS-PRINT-SW                       
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF K800-DTE-BK-APPT NOT = SPACES AND LOW-VALUES                   
                 IF F001-DTE-CURRENT NOT = K800-DTE-BK-APPT                     
                    MOVE 'Y'               TO WS-PRINT-SW                       
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF K800-DTE-1ST-LETTER NOT = SPACES                               
                 IF F001-DTE-CURRENT NOT = K800-DTE-1ST-LETTER                  
                    MOVE 'Y'               TO WS-PRINT-SW                       
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF K800-DTE-1ST-TP-LETTER NOT = SPACES                            
                 IF F001-DTE-CURRENT NOT = K800-DTE-1ST-TP-LETTER               
                    MOVE 'Y'               TO WS-PRINT-SW                       
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-PRINT-SW = 'Y'                                              
                 PERFORM 5500-WRITE-REPORT                                      
                    THRU 5500-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5500-WRITE-REPORT.                                                       
      ******************************************************************        
                                                                                
           IF WS-L87A-CNT-LINE > 60                                             
              PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 6500-READ-SY08K010                                           
              THRU 6500-EXIT.                                                   
                                                                                
           MOVE SPACES                        TO WS-PAGE-DATA-LINE.             
           ADD  1                             TO WS-L87A-CNT-REC.               
           MOVE F860-NUM-USERID               TO L87A-USERID.                   
           MOVE K010-NME-USER                 TO L87A-USERNAME.                 
           MOVE K010-CDE-DEFGRP               TO L87A-DEPT.                     
           MOVE F879-DTE-LOG                  TO L87A-DTE-ACCESS.               
           MOVE F879-TME-LOG                  TO L87A-TME-ACCESS.               
           MOVE WS-NUM-ACCESS-KEY             TO L87A-ACCESS-KEY.               
           MOVE K800-DTE-AO-APPT              TO L87A-DTE-AO.                   
           MOVE K800-DTE-SO-APPT              TO L87A-DTE-SO.                   
           MOVE K800-DTE-BK-APPT              TO L87A-DTE-BK.                   
           MOVE K800-DTE-1ST-LETTER           TO L87A-DTE-1ST.                  
           MOVE K800-DTE-1ST-TP-LETTER        TO L87A-DTE-1ST-TP.               
                                                                                
                                                                                
           WRITE BP13L87A-REC FROM WS-PAGE-DATA-LINE                            
                 AFTER ADVANCING 1 LINE.                                        
                                                                                
           ADD 1                   TO WS-L87A-CNT-LINE.                         
                                                                                
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y' TO WS-K800-FOUND-TAG                                   
                ADD 1                      TO WS-K800-FND                       
           WHEN 23                                                              
                MOVE 'N' TO WS-K800-FOUND-TAG                                   
                ADD 1                      TO WS-K800-NFND                      
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS              
                        ' REGN = ' K800-NUM-REGN                                
                PERFORM 9000-CLOSE-FILES                                        
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6500-READ-SY08K010.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO K010-NUM-USERID.                  
           MOVE F860-NUM-USERID            TO K010-NUM-USERID.                  
                                                                                
           READ SY08K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y'                   TO WS-K010-FOUND-TAG                 
                ADD 1                      TO WS-K010-FND                       
           WHEN 23                                                              
                MOVE SPACES                TO K010-NME-USER                     
                                              K010-CDE-DEFGRP                   
                MOVE 'N'                   TO WS-K010-FOUND-TAG                 
                ADD 1                      TO WS-K010-NFND                      
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING SY08K010 : ' WS-K010-STATUS              
                        ' KEY  = ' K010-NUM-USERID                              
                PERFORM 9000-CLOSE-FILES                                        
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6600-STARTBR-K820.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO WS-K820-EOF.                      
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC.                                 
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
                MOVE 'N' TO WS-K820-FOUND-TAG                                   
                PERFORM 6700-READNEXT-K820 THRU 6700-EXIT                       
                   UNTIL K820-NUM-NRIC NOT = WS-NUM-ACCESS-KEY                  
                      OR WS-K820-EOF = 'Y'                                      
                IF WS-K820-FOUND-TAG = 'N'                                      
                   ADD 1 TO WS-K820-NFND                                        
                END-IF                                                          
           WHEN 23                                                              
           WHEN 10                                                              
                MOVE 'N' TO WS-K820-FOUND-TAG                                   
                ADD 1                      TO WS-K820-NFND                      
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K820 : ' WS-K820-STATUS              
                        ' NRIC = ' K820-NUM-NRIC                                
                PERFORM 9000-CLOSE-FILES                                        
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6700-READNEXT-K820.                                                      
      ******************************************************************        
                                                                                
           READ BP13K820 NEXT RECORD                                            
              AT END MOVE 'Y' TO WS-K820-EOF                                    
              GO TO 6700-EXIT.                                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF K820-NUM-NRIC = WS-NUM-ACCESS-KEY                            
                   MOVE K820-NUM-REGN      TO K800-NUM-REGN                     
                   ADD 1                   TO WS-K820-FND                       
                   MOVE 'Y'                TO WS-K820-FOUND-TAG                 
                   PERFORM 6000-READ-BP13K800                                   
                      THRU 6000-EXIT                                            
                   PERFORM 5200-CHECK-DATES                                     
                      THRU 5200-EXIT                                            
                ELSE                                                            
                   MOVE 'Y'                TO WS-K820-EOF                       
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                   TO WS-K820-EOF                       
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K820, STATUS  '                      
                        WS-K820-STATUS                                          
                        ',  KEY ' K820-NUM-NRIC                                 
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADERS.                                                      
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  PRINTS THE PAGE HEADER.                                       *        
      *----------------------------------------------------------------*        
           ADD  1                TO WS-L87A-CNT-PAGE.                           
           MOVE WS-L87A-CNT-PAGE TO L87A-PAGE-NUM.                              
                                                                                
           WRITE BP13L87A-REC    FROM WS-PAGE-HEADING1                          
                 AFTER ADVANCING PAGE.                                          
           WRITE BP13L87A-REC    FROM WS-PAGE-HEADING2                          
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L87A-REC    FROM WS-PAGE-HEADING3                          
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L87A-REC    FROM WS-PAGE-COL-HEADING1                      
                 AFTER ADVANCING 2 LINES.                                       
           WRITE BP13L87A-REC    FROM WS-HORIZONTAL-LINE                        
                 AFTER ADVANCING 1 LINE.                                        
                                                                                
           MOVE 5                TO WS-L87A-CNT-LINE.                           
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY '                                  '.                        
           DISPLAY ' *****  BP13C87A  *****           '.                        
           DISPLAY '                                  '.                        
           DISPLAY ' NO. OF REC READ FROM F860  = ' WS-F860-CNT-READ.           
           DISPLAY ' NO. OF REC PRINTED         = ' WS-L87A-CNT-REC.            
           DISPLAY ' NO. OF REC READ FROM F879  = ' WS-F879-CNT-READ.           
           DISPLAY ' NO. OF REC FOUND F879      = ' WS-F879-FND.                
           DISPLAY ' NO. OF REC NOT FOUND F879  = ' WS-F879-NFND.               
           DISPLAY ' NO. OF REC FOUND K800      = ' WS-K800-FND.                
           DISPLAY ' NO. OF REC NOT FOUND K800  = ' WS-K800-NFND.               
           DISPLAY ' NO. OF REC FOUND K820      = ' WS-K820-FND.                
           DISPLAY ' NO. OF REC NOT FOUND K820  = ' WS-K820-NFND.               
           DISPLAY ' NO. OF REC FOUND SY08      = ' WS-K010-FND.                
           DISPLAY ' NO. OF REC NOT FOUND SY08  = ' WS-K010-NFND.               
                                                                                
           CLOSE BP13F860                                                       
                 BP13F879                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 SY08K010                                                       
                 SY02F001.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, SY08K010-STATUS ' WS-K010-STATUS          
              MOVE 'Y' TO WS-ERROR-SW                                           
              MOVE WS-K010-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13K800-STATUS ' WS-K800-STATUS          
              MOVE 'Y' TO WS-ERROR-SW                                           
              MOVE WS-K800-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13K820-STATUS ' WS-K820-STATUS          
              MOVE 'Y' TO WS-ERROR-SW                                           
              MOVE WS-K820-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           PERFORM 9100-PRINT-END-EMPTY THRU 9100-EXIT.                         
                                                                                
           CLOSE BP13L87A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9100-PRINT-END-EMPTY.                                                    
      ******************************************************************        
           IF WS-L87A-CNT-REC = ZEROES                                          
              IF WS-ERROR-SW = 'Y'                                              
                 PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT                      
              END-IF                                                            
              MOVE  SPACES TO BP13L87A-REC                                      
              WRITE BP13L87A-REC AFTER ADVANCING 1 LINE                         
              MOVE  SPACES TO BP13L87A-REC                                      
              IF WS-ERROR-SW = 'Y'                                              
                 MOVE 'FILE ERROR !!! ' TO   WS-EMPTY-REPORT                    
                 WRITE BP13L87A-REC FROM WS-EMPTY-REPORT                        
                       AFTER ADVANCING 1 LINE                                   
              ELSE                                                              
                 WRITE BP13L87A-REC FROM WS-EMPTY-REPORT                        
                       AFTER ADVANCING 1 LINE                                   
              END-IF                                                            
           ELSE                                                                 
              MOVE  SPACES TO BP13L87A-REC                                      
              WRITE BP13L87A-REC AFTER ADVANCING 1 LINE                         
              MOVE  SPACES TO BP13L87A-REC                                      
              WRITE BP13L87A-REC FROM WS-END-OF-REPORT                          
                    AFTER ADVANCING 1 LINE                                      
           END-IF.                                                              
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
      * >>>>>>>>>>>>>>>>>>>>>>> END OF BP13C87A <<<<<<<<<<<<<<<<<<<<<< *        
      *----------------------------------------------------------------*        
