       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C549.                                                 
      *AUTHOR.        K.P.V.SANTOS.                                             
      *DATE-WRITTEN.  24/05/93.                                                 
      ***************************************************************           
      * OBJECTIVE  - TO BACK-UP OCCUPIER INFO (LATEST) FROM         *           
      *              BP13K558 TO BP13K552.                          *           
      *                                                             *           
      * SUBROUTINE    - NIL                                         *           
      *                                                             *           
      * IDENTICAL PGM - NIL                                         *           
      *                                                             *           
      *-------------------------------------------------------------*           
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS               *           
      * -------  ----    ---  -----------------------               *           
      * N930012  930607  JK   TO BLANK OUT REF NO FOR K552 IF HIST  *           
      *                       REC IS BEING WRITTEN                  *           
      * SOC-PH9  941110  GD   TO CHANGE THE FF. FILES TO NEW FORMAT:*           
      *                         - BP13K552  TO BP13F828             *           
      *                         - BP13K558  TO BP13F825             *           
      ***************************************************************           
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT  BP13F825  ASSIGN  TO   BP13F825.                             
                                                                                
           SELECT  BP13F828  ASSIGN  TO   BP13F828.                             
                                                                                
           SELECT  P13F825O  ASSIGN  TO   P13F825O.                             
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F825                     RECORDING MODE IS F                     
                                        BLOCK CONTAINS 0 RECORDS                
                                        LABEL RECORDS ARE STANDARD              
                                        RECORD CONTAINS 200 CHARACTERS.         
                                                                                
       COPY BP13F825.                                                           
                                                                                
       FD  BP13F828                     RECORDING MODE F                        
                                        BLOCK CONTAINS 0 RECORDS                
                                        LABEL RECORDS ARE STANDARD              
                                        RECORD CONTAINS 200 CHARACTERS.         
                                                                                
       COPY BP13F828.                                                           
                                                                                
       FD  P13F825O                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 200 CHARACTERS          
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
                                                                                
       01  P13F825O-REC                 PIC X(200).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F825-EOF                  PIC X     VALUE 'N'.                
           05  WS-F828-EOF                  PIC X     VALUE 'N'.                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F825                  PIC 9(7)  VALUE 0.                  
           05  WS-CNT-OUT                   PIC 9(7)  VALUE 0.                  
           05  WS-CNT-F828                  PIC 9(7)  VALUE 0.                  
           05  WS-CNT-MTCH                  PIC 9(7)  VALUE 0.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       0000-MAIN-PARA.                                                          
                                                                                
           PERFORM 1000-START-UP            THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F825       THRU 2000-EXIT.                     
           PERFORM 3000-READ-BP13F828       THRU 3000-EXIT.                     
           PERFORM 4000-PROCESS-MATCHING    THRU 4000-EXIT                      
             UNTIL WS-F825-EOF = 'Y' AND WS-F828-EOF = 'Y'.                     
           PERFORM 9999-WRAP-UP             THRU 9999-EXIT.                     
                                                                                
                                                                                
       1000-START-UP.                                                           
                                                                                
           OPEN INPUT  BP13F825                                                 
                       BP13F828                                                 
               OUTPUT  P13F825O.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       2000-READ-BP13F825.                                                      
                                                                                
           READ BP13F825 AT END                                                 
                MOVE 'Y'         TO WS-F825-EOF                                 
                MOVE HIGH-VALUES TO F825-NUM-NRIC                               
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-CNT-F825.                                                
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       3000-READ-BP13F828.                                                      
                                                                                
           READ BP13F828 AT END                                                 
                MOVE 'Y'         TO WS-F828-EOF                                 
                MOVE HIGH-VALUES TO F828-NUM-NRIC                               
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-CNT-F828.                                                
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       4000-PROCESS-MATCHING.                                                   
                                                                                
           IF F825-NUM-NRIC = F828-NUM-NRIC                                     
              MOVE BP13F825-REC TO P13F825O-REC                                 
              WRITE P13F825O-REC                                                
              ADD 1 TO WS-CNT-MTCH                                              
              ADD 1 TO WS-CNT-OUT                                               
              PERFORM 2000-READ-BP13F825 THRU 2000-EXIT                         
              PERFORM 3000-READ-BP13F828 THRU 3000-EXIT                         
           ELSE                                                                 
           IF F825-NUM-NRIC < F828-NUM-NRIC                                     
              MOVE BP13F825-REC TO P13F825O-REC                                 
              WRITE P13F825O-REC                                                
              ADD 1 TO WS-CNT-OUT                                               
              PERFORM 2000-READ-BP13F825 THRU 2000-EXIT                         
           ELSE                                                                 
           IF F825-NUM-NRIC > F828-NUM-NRIC                                     
              MOVE BP13F828-REC TO P13F825O-REC                                 
              WRITE P13F825O-REC                                                
              ADD 1 TO WS-CNT-OUT                                               
              PERFORM 3000-READ-BP13F828 THRU 3000-EXIT.                        
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
       9999-WRAP-UP.                                                            
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS *******'.                          
           DISPLAY 'PROGRAM-ID : BP13C549'.                                     
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY 'NUMBER OF BP13F825  RECORDS READ    : ' WS-CNT-F825.        
           DISPLAY 'NUMBER OF BP13F828  RECORDS READ    : ' WS-CNT-F828.        
           DISPLAY 'MATCHED CASES                       : ' WS-CNT-MTCH.        
           DISPLAY 'NUMBER OF P13F825O RECORDS WRITTEN  : ' WS-CNT-OUT.         
                                                                                
           CLOSE BP13F825                                                       
                 BP13F828                                                       
                 P13F825O.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
                                                                                
