      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CC06.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  16 SEPTEMBER 2005.                                        
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM VALIDATES TRANSACTIONS PASSED  *           
      *                 FROM PAYMENT SYSTEM ON PAYMENT TO DEVELOPER.*           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  AG07F630 -- VOUCHER TRANSACTION FILE                  *           
      *   2.  P13FD10A -- SORTED SOC/DBSS VOUCHER OIC FILE          *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KD10 -- SOC/DBSS VOUCHER OIC FILE                 *           
      *   2.  BP13KD00 -- PROGRESS PAYMENT MASTER                   *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13F125 -- EDITED FIRST TRANSACTION FILE             *           
      *   2.  P13FD10B -- EDITED SOC/DBSS VOUCHER OIC FILE          *           
      *   3.  BP13LC06 -- CONTROL LISTING                           *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132801 16/09/2005 SD10 NEW PROGRAM                        *           
      * BP132772 06/04/2006 BA3  INCLUDED BP13KD30. CHANGED BP13K633*           
      *                          TO BP13KD10 AND BP13F633 TO        *           
      *                          BP13F633.                          *           
      *          12/04/2006 BA3  INCLUDED UPDATE TO BP13KD00.       *           
      *          12/10/2006 BA3  ADD KD00-SCHACC TO BP13F125.       *           
      *          16/10/2006 BA3  ADD BATCH-NO TO FD10               *           
      *          24/11/2006 SD10 CHANGE REPORT LAYOUT               *           
      *          18/01/2007 BA3  INCLUDED 2 SPACES BETWEEN RECORDS  *           
      *                          WHEN PRINTED ON THE REPORT AND     *           
      *                          REMOVED CONDITON:                  *           
      *                          F630-NUM-HDB-REF(9:1) = SPACES     *           
      * BP132973 08/02/2007 SD10 INITIALIZE BP13F125 REC            *           
      *          23/02/2007 BA3  UPDATE BP13KD30 DTE-FIN-PROCESS AND*           
      *                          DTE-CHEQUE-SENT FOR 'DEV' CASES.   *           
      *                          DTE-CHEQUE-SENT SHOULD BE SPACES.  *           
      * BP132888 09/03/2007 SD10 ADD KD10-AMT-OPTION-REIMBURSE TO   *           
      *                          KD00-AMT-OPTION-REIMBURSE AND ADD  *           
      *                          KD10-AMT-CPF-GRANT-PAYEE1-4 TO     *           
      *                          KD00-AMT-OPTION-REFUND1-4.         *           
      * BP133175 16/11/2007 BA3  INCLUDE CDE-LOAN-SCH AND CDE-CRDR  *           
      *                          FOR PAYMENT TYPE EQUAL TO 'PP' AND *           
      *                          'INS' AND TRANS TYPE EQUAL TO '63'.*           
      * BP133091 17/10/2007 RB12 EXPAND KD00 LENGTH FR 1200 TO 1600 *           
      * BP133209 30/01/2008 ED1  CATER FOR KD30 PP-SCHED SWAPPING,  *           
      *                          CHK IF NUM-PP = KD30 PP SCHED      *           
      *                          BEFORE UPDATING CORRECT FIELDS     *           
      * BP133330 15/05/2008 ED1  REMOVE UPDATE OF KD00-AMT-OPTION-  *           
      *                          REIMBURSE                          *           
      * BP133535 11/02/2009 JB8  MOVE 'Y' TO DBSS-TOP FOR SCHDL 09  *           
      * BP133545 24/02/2009 JB8  OUTPUT SYS-CDE TO KD10             *           
      * BP133398 18/05/2009 JB8  EXPAND KD10 LENGTH FR 450  TO 1400 *           
      * BP133662 14/08/2009 JB8  TO CHECK GL-ACCOUNT WHEN OUTPUT    *           
      *                          DBSS-TOP                           *           
      * BP133612 16/09/2009 JB8  OUTPUT NEW REPORT FOR CASES TRANSFR*           
      *                          TO SMS BUT GL AC IS 540834         *           
      * BP134154 19/04/2011 JB8  TO REMOVE UPDATE OF BP13KD30       *           
      * BP135161 11/06/2014 JB8  TO CATER FOR DEFER PAYMENT 02, 03  *           
      *                          IF NOTFD IN BP13KD00 TAG 'Y' TOP   *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD10 ASSIGN TO BP13KD10                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD10-KEY-FLD                               
                  FILE STATUS     IS BP13KD10-STATUS.                           
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD00-KEY-FLD                               
                  FILE STATUS     IS BP13KD00-STATUS.                           
                                                                                
           SELECT BP13KD03 ASSIGN TO BP13KD03                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD03-KEY-FLD                               
                  FILE STATUS     IS BP13KD03-STATUS.                           
                                                                                
           SELECT AG07F630   ASSIGN       TO AG07F630.                          
                                                                                
           SELECT P13FD10A   ASSIGN       TO P13FD10A.                          
                                                                                
           SELECT P13FD10B   ASSIGN       TO P13FD10B.                          
                                                                                
           SELECT BP13F125   ASSIGN       TO BP13F125.                          
                                                                                
           SELECT BP13LC06   ASSIGN       TO BP13LC06.                          
                                                                                
           SELECT P13LC06B   ASSIGN       TO P13LC06B.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD10                                                            
            RECORD CONTAINS  1400 CHARACTERS.                                   
       COPY BP13KD10.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD03                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD03.                                                           
                                                                                
       FD   AG07F630                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  130 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AG07F630.                                                           
                                                                                
       FD   P13FD10A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  1400 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FD10.                                                           
                                                                                
       FD   P13FD10B                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  1400 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   WS-BP13FD10B-REC       PIC X(1400).                         00055007
                                                                                
       FD   BP13F125                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F125.                                                           
                                                                                
       FD   BP13LC06                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(132).                                   
                                                                                
       FD   P13LC06B                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   LC06B-REC             PIC X(132).                                   
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-NRIC                PIC X(09) VALUE SPACES.                       
       01  WS-TOT-AMT-PAID        PIC 9(06)V99.                                 
       01  WS-BP13F125-CASHTRAN   PIC X(150).                                   
       01  WS-DTE-IBG-VALUE       PIC X(08).                                    
       01  WS-TXT-IBG-SERIAL      PIC X(08).                                    
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-F125-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-F630-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-FD10-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD10-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD10-DELETE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD00-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD00-UPDATE        PIC 9(05) VALUE ZEROES.                       
       01  WS-KD00-FND            PIC X(01) VALUE SPACE.                        
           88  KD00-FND                     VALUE 'Y'.                          
       01  CNT-PRT-CASES          PIC 9(06) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
       01  CNT-FD10-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-LC06B-SNO          PIC 9(05) VALUE ZEROES.                       
       01  CNT-LC06B-LINE         PIC 9(02) VALUE 60.                           
       01  CNT-LC06B-PAGE         PIC 9(03) VALUE ZEROES.                       
       01  WS-MATCH-REC           PIC 9(08) VALUE ZEROES.                       
       01  WS-F630-EOF            PIC X     VALUE SPACE.                        
       01  WS-FD10-EOF            PIC X     VALUE SPACE.                        
       01  BP13KD10-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD00-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD03-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-KD10-REGN-FND       PIC X     VALUE SPACE.                        
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-NUM-PP              PIC X(2)  VALUE SPACES.                       
       01  WS-NUM-PP-NO REDEFINES WS-NUM-PP PIC 9(2).                           
       01  WS-KD00-SCHACC         PIC X(11) VALUE SPACE.                        
       01  WS-KD00-CDE-LOAN-SCH        PIC X(01) VALUE SPACE.                   
                                                                                
       01  WS-SCHEDULE-NO.                                                      
           05  WS-SCHEDULE-NUM    PIC X(08).                                    
           05  WS-DOC-NO          PIC X(06).                                    
                                                                                
       01  LC06-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LC06'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LC06-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LC06-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LC06-PR-HEAD-02.                                                     
           05  FILLER             PIC X(40) VALUE SPACES.                       
           05  FILLER             PIC X(47) VALUE                               
               '  LIST OF TRANSACTIONS PASSED FROM VOUCHER FILE'.               
           05  FILLER             PIC X(33) VALUE SPACES.                       
                                                                                
       01  LC06-PR-HEAD-03.                                                     
           05  FILLER             PIC X(37)                                     
                  VALUE '  S/NO   REGN NO.   SCH ACCT         '.                
           05  FILLER             PIC X(36)                                     
                  VALUE 'AMOUNT    TRANS-TY  VOUCHER NO      '.                 
           05  FILLER             PIC X(25)                                     
                  VALUE 'BATCH NO     DATE POST   '.                            
           05  FILLER             PIC X(23)                                     
                  VALUE 'PAYMT TYPE      REMARKS'.                              
                                                                                
       01  LC06-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LC06-SNO           PIC ZZZZZ9 .                                  
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LC06-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LC06-SCH-ACCT      PIC X(11).                                    
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LC06-AMT           PIC $$,$$$,$$9.99DB.                          
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC06-TRANS-TY      PIC X(02).                                    
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  LC06-VR-NO         PIC X(11).                                    
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC06-NUM-RECEIPT   PIC X(08).                                    
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC06-DTE-POST      PIC X(08).                                    
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  LC06-PAYMENT-TYPE  PIC X(08).                                    
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LC06-REMARK        PIC X(15).                                    
                                                                                
       01  LC06-CONTROL-LINE.                                                   
           05  FILLER             PIC X(20) VALUE  SPACES.                      
           05  CNTL-ITEM          PIC X(30) VALUE  SPACES.                      
           05  CNTL-CNT           PIC ZZZZ9 .                                   
           05  FILLER             PIC X(58) VALUE  SPACES.                      
      ***                                                                       
      **REPORT LAYOUT FOR P13LC06B                                              
      ***                                                                       
       01  LC06B-HEAD-01.                                                       
           05  FILLER             PIC X(107) VALUE                              
               'P13LC06B     HDBCAT 3                        S Y S T E M        
      -        '   O F   C O M M I T M E N T                 DATE: '.           
           05  LC06B-DATE         PIC X(10).                                    
           05  FILLER             PIC X(10) VALUE '   PAGE : '.                 
           05  LC06B-PAGE         PIC ZZ9 .                                     
                                                                                
       01  LC06B-HEAD-02.                                                       
           05  FILLER             PIC X(32) VALUE SPACES.                       
           05  FILLER             PIC X(65) VALUE                               
               'RECORDS TRANSFERRED TO SMS AND PV RAISED UNDER GL ACCOUN        
      -        'T 54-0834'.                                                     
           05  FILLER             PIC X(35) VALUE SPACES.                       
                                                                                
       01  LC06B-TITLE.                                                         
           05  FILLER             PIC X(108) VALUE                              
               '   S/NO   REGN NO    SCH-ACC         PP SCH    DUE DATE         
      -        '    VOUCHER NO      DATE TRANS   LOAN      ($)      '.          
           05  FILLER             PIC X(24)  VALUE SPACES.                      
                                                                                
       01  LC06B-DETAIL.                                                        
           05  FILLER             PIC X(02)  VALUE SPACES.                      
           05  LC06B-SNO          PIC ZZZZ9  VALUE ZEROES.                      
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  LC06B-REGN         PIC X(08)  VALUE SPACES.                      
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  LC06B-SCH-ACC      PIC X(14)  VALUE SPACES.                      
           05  FILLER             PIC X(04)  VALUE SPACES.                      
           05  LC06B-PP           PIC X(02)  VALUE SPACES.                      
           05  FILLER             PIC X(06)  VALUE SPACES.                      
           05  LC06B-DTE-DUE      PIC X(10)  VALUE SPACES.                      
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  LC06B-VR-NO        PIC X(13)  VALUE SPACES.                      
           05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  LC06B-DTE-TRANS    PIC X(10)  VALUE SPACES.                      
           05  FILLER             PIC X(03)  VALUE SPACES.                      
      **   05  LC06B-CPF-NO       PIC X(09)  VALUE SPACES.                      
      **   05  FILLER             PIC X(03)  VALUE SPACES.                      
           05  LC06B-AMT-LOAN     PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.                
           05  FILLER             PIC X(30)  VALUE SPACES.                      
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F630    THRU 2000-EXIT.                            
           PERFORM 2100-READ-FD10    THRU 2100-EXIT.                            
           MOVE ZEROES               TO CNT-PRT-CASES.                          
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F630-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  AG07F630                                                 
                       P13FD10A                                                 
                       BP13KD03                                                 
                I-O    BP13KD10                                                 
                       BP13KD00                                                 
                OUTPUT BP13F125                                                 
                       P13FD10B                                                 
                       BP13LC06                                                 
                       P13LC06B.                                                
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD10 : ' BP13KD10-STATUS             
              MOVE BP13KD10-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD00-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD03-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD03 : ' BP13KD03-STATUS             
              MOVE BP13KD03-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO LC06-DATE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F630.                                                          
      *---------------*                                                         
                                                                                
           READ AG07F630 AT END                                                 
                MOVE HIGH-VALUES TO AG07F630-REC                                
                MOVE 'Y' TO WS-F630-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F630-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-FD10.                                                          
      *---------------*                                                         
                                                                                
           READ P13FD10A AT END                                                 
                MOVE HIGH-VALUES TO BP13FD10-REC                                
                MOVE 'Y' TO WS-FD10-EOF                                         
                GO TO 2100-EXIT.                                                
           ADD 1 TO CNT-FD10-READ.                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           IF F630-NUM-VR = FD10-NUM-VR                                         
              ADD  1                    TO WS-MATCH-REC                         
              PERFORM 3100-PROCESS-REC  THRU 3100-EXIT                          
              MOVE SPACES               TO LC06-PR-DETAILS                      
              MOVE SPACES               TO LC06-REMARK                          
              PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                          
              PERFORM 2000-READ-F630    THRU 2000-EXIT                          
              PERFORM 2100-READ-FD10    THRU 2100-EXIT                          
           ELSE                                                                 
              IF F630-NUM-VR > FD10-NUM-VR                                      
                 PERFORM 2100-READ-FD10    THRU 2100-EXIT                       
              ELSE                                                              
                 MOVE SPACES                    TO LC06-PR-DETAILS              
                 MOVE SPACES                    TO LC06-REMARK                  
                 MOVE 'ORIG VOUCHER NO. ERROR'  TO LC06-REMARK                  
                 MOVE ZEROES    TO KD10-AMT-CPF-GRANT-PAYEE(1)                  
                                   KD10-AMT-CPF-GRANT-PAYEE(2)                  
                                   KD10-AMT-CPF-GRANT-PAYEE(3)                  
                                   KD10-AMT-CPF-GRANT-PAYEE(4)                  
                 PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                       
                 PERFORM 2000-READ-F630    THRU 2000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES                TO WS-KD00-SCHACC                         
                                         WS-KD00-CDE-LOAN-SCH.                  
           MOVE SPACES                TO KD10-KEY-FLD.                          
           MOVE FD10-NUM-TRANS        TO KD10-NUM-TRANS.                        
           MOVE FD10-NUM-REGN         TO KD10-NUM-REGN.                         
           MOVE FD10-NUM-PP           TO KD10-NUM-PP.                           
           PERFORM 3200-READ-BP13KD10 THRU 3200-EXIT.                           
           MOVE SPACES                TO WS-BP13FD10B-REC.                      
                                                                                
           IF FD10-NUM-TRANS = 'OPT'                                            
              IF FD10-AMT-OPTION-REIMBURSE IS NOT NUMERIC                       
                 MOVE ZEROS TO FD10-AMT-OPTION-REIMBURSE                        
                               KD10-AMT-OPTION-REIMBURSE                        
              END-IF                                                            
              IF FD10-AMT-CPF-GRANT-PAYEE(1) IS NOT NUMERIC                     
                 MOVE ZEROS TO FD10-AMT-CPF-GRANT-PAYEE(1)                      
                               KD10-AMT-CPF-GRANT-PAYEE(1)                      
              END-IF                                                            
              IF FD10-AMT-CPF-GRANT-PAYEE(2) IS NOT NUMERIC                     
                 MOVE ZEROS TO FD10-AMT-CPF-GRANT-PAYEE(2)                      
                               KD10-AMT-CPF-GRANT-PAYEE(2)                      
              END-IF                                                            
              IF FD10-AMT-CPF-GRANT-PAYEE(3) IS NOT NUMERIC                     
                 MOVE ZEROS TO FD10-AMT-CPF-GRANT-PAYEE(3)                      
                               KD10-AMT-CPF-GRANT-PAYEE(3)                      
              END-IF                                                            
              IF FD10-AMT-CPF-GRANT-PAYEE(4) IS NOT NUMERIC                     
                 MOVE ZEROS TO FD10-AMT-CPF-GRANT-PAYEE(4)                      
                               KD10-AMT-CPF-GRANT-PAYEE(4)                      
              END-IF                                                            
                                                                                
              COMPUTE WS-TOT-AMT-PAID = FD10-AMT-CPF-GRANT-PAYEE(1) +           
                                        FD10-AMT-CPF-GRANT-PAYEE(2) +           
                                        FD10-AMT-CPF-GRANT-PAYEE(3) +           
                                        FD10-AMT-CPF-GRANT-PAYEE(4)             
              ADD WS-TOT-AMT-PAID       TO KD10-AMT-OPTION-REIMBURSE            
           ELSE                                                                 
              IF FD10-AMT-PP-LUMPSUM NOT NUMERIC                                
                 MOVE ZEROES             TO KD10-AMT-PP-LUMPSUM                 
              END-IF                                                            
              IF FD10-AMT-LOAN NOT NUMERIC                                      
                 MOVE ZEROES             TO KD10-AMT-LOAN                       
              END-IF                                                            
              MOVE ZEROES                TO WS-TOT-AMT-PAID                     
              IF FD10-AMT-PP-LUMPSUM NOT NUMERIC                                
                 MOVE ZEROES             TO KD10-AMT-PP-LUMPSUM                 
              END-IF                                                            
              IF FD10-AMT-LOAN NOT NUMERIC                                      
                 MOVE ZEROES             TO KD10-AMT-LOAN                       
              END-IF                                                            
                                                                                
              COMPUTE WS-TOT-AMT-PAID = KD10-AMT-PP-LUMPSUM +                   
                                        KD10-AMT-LOAN                           
              MOVE WS-TOT-AMT-PAID       TO KD10-AMT-PAYMENT-DEVELOPER          
           END-IF.                                                              
                                                                                
           MOVE F630-DTE-TRANS        TO KD10-DTE-FIN-RPOCESS.                  
           MOVE F630-NUM-BATCH        TO KD10-NUM-BATCH.                        
           MOVE AG07F630-REC(111:8)   TO KD10-DTE-IBG-VALUE.                    
           MOVE AG07F630-REC(119:12)  TO KD10-TXT-IBG-SERIAL.                   
           MOVE F630-NUM-SYS          TO KD10-NUM-SYSTEM                        
           IF FD10-NUM-PAYMENT-MODE = 'G'                                       
              MOVE KD10-DTE-IBG-VALUE TO KD10-DTE-SEND-VOUCHER                  
           ELSE                                                                 
              MOVE FD10-DTE-DUE-PP    TO KD10-DTE-SEND-VOUCHER                  
           END-IF.                                                              
           MOVE BP13KD10-REC          TO WS-BP13FD10B-REC.                      
           WRITE WS-BP13FD10B-REC.                                              
           ADD 1                      TO CNT-FD10-WRITE.                        
                                                                                
           PERFORM 3920-UPDATE-BP13KD00     THRU 3920-EXIT.                     
           IF KD00-FND                                                          
              PERFORM 3950-REWRITE-BP13KD00 THRU 3950-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 3300-MOVE-DATA-BP13F125  THRU 3300-EXIT.                     
           PERFORM 3700-DELETE-BP13KD10     THRU 3700-EXIT.                     
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13KD10.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13KD10.                                                       
           IF BP13KD10-STATUS = 0                                               
              MOVE 'Y'    TO WS-KD10-REGN-FND                                   
           ELSE                                                                 
              IF BP13KD10-STATUS = 23                                           
                 MOVE 'N' TO WS-KD10-REGN-FND                                   
                 DISPLAY 'REGNO NOT FOUND IN BP13KD10 : ' KD10-NUM-REGN         
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD10 : ' BP13KD10-STATUS          
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3300-MOVE-DATA-BP13F125.                                                 
      *------------------------*                                                
                                                                                
           MOVE SPACES       TO BP13F125-CASHTRAN.                              
                                                                                
           IF FD10-NUM-TRANS = 'OPT'                                            
              MOVE '13'         TO F125-CDE-TRANS-TYPE                          
                                                                                
              MOVE FD10-NUM-REGN      TO F125-NUM-REGN                          
              MOVE KD10-AMT-OPTION-REIMBURSE TO F125-AMT-RECEIPT                
                                                                                
              MOVE WS-NRIC            TO F125-NUM-NRIC                          
              MOVE SPACES             TO F125-NME-NAME                          
                                                                                
              MOVE F630-NUM-CPF       TO F125-CDE-CPF-APPL                      
              MOVE F630-NUM-VR        TO F125-NUM-RECPT-JRNO                    
              MOVE F630-NUM-VR-DDD    TO F125-NUM-VR-DDD                        
              MOVE F630-NUM-VR-YY     TO F125-NUM-VR-YY                         
              MOVE F630-DTE-TRANS     TO F125-DTE-TRANS                         
              MOVE WS-DATE            TO F125-DTE-POST                          
              MOVE SPACES             TO F125-CDE-APPLT-CPF                     
              MOVE ZERO               TO F125-COUNT-HISTORY                     
              MOVE '01'               TO F125-NUM-PP-SCHEDULE                   
              MOVE FD10-DTE-DUE-PP    TO F125-DTE-PP-DUE                        
              MOVE WS-KD00-SCHACC     TO F125-NUM-SCH-ACCT                      
                                                                                
              MOVE 'OPT'               TO F125-CDE-PAYMENT-TYPE                 
              MOVE BP13F125-CASHTRAN   TO WS-BP13F125-CASHTRAN                  
              WRITE BP13F125-CASHTRAN                                           
              ADD 1       TO CNT-F125-WRITE                                     
              MOVE WS-BP13F125-CASHTRAN   TO BP13F125-CASHTRAN                  
              GO TO 3300-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE '63'         TO F125-CDE-TRANS-TYPE.                            
                                                                                
           MOVE FD10-NUM-REGN      TO F125-NUM-REGN.                            
           MOVE FD10-AMT-PAYMENT-DEVELOPER TO F125-AMT-RECEIPT.                 
                                                                                
           MOVE WS-NRIC            TO F125-NUM-NRIC.                            
           MOVE SPACES             TO F125-NME-NAME.                            
                                                                                
           MOVE F630-NUM-CPF       TO F125-CDE-CPF-APPL.                        
           MOVE F630-NUM-VR        TO F125-NUM-RECPT-JRNO.                      
           MOVE F630-NUM-VR-DDD    TO F125-NUM-VR-DDD.                          
           MOVE F630-NUM-VR-YY     TO F125-NUM-VR-YY.                           
           MOVE F630-DTE-TRANS     TO F125-DTE-TRANS.                           
           MOVE WS-DATE            TO F125-DTE-POST.                            
           MOVE SPACES             TO F125-CDE-APPLT-CPF.                       
           MOVE ZERO               TO F125-COUNT-HISTORY.                       
           MOVE FD10-NUM-PP        TO F125-NUM-PP-SCHEDULE.                     
           MOVE FD10-DTE-DUE-PP    TO F125-DTE-PP-DUE.                          
           MOVE WS-KD00-SCHACC     TO F125-NUM-SCH-ACCT.                        
           MOVE WS-KD00-CDE-LOAN-SCH  TO F125-NUM-LOAN-SCH.                     
           MOVE 'C'                TO F125-CDE-CRDR.                            
                                                                                
           IF FD10-AMT-PP-LUMPSUM > ZEROES                                      
              IF FD10-NUM-PP > '08' OR WS-KD00-FND = 'N'                        
                 MOVE 'Y'              TO F125-NUM-DBSS-TOP                     
              END-IF                                                            
                                                                                
              MOVE FD10-AMT-PP-LUMPSUM TO F125-AMT-RECEIPT                      
              MOVE 'PP '               TO F125-CDE-PAYMENT-TYPE                 
              MOVE BP13F125-CASHTRAN   TO WS-BP13F125-CASHTRAN                  
              WRITE BP13F125-CASHTRAN                                           
              ADD 1       TO CNT-F125-WRITE                                     
              MOVE WS-BP13F125-CASHTRAN   TO BP13F125-CASHTRAN                  
           END-IF.                                                              
                                                                                
           IF FD10-AMT-LOAN > ZEROES                                            
              IF FD10-NUM-PP > '08' OR WS-KD00-FND = 'N'                        
                 IF F630-NUM-GL-ACCT(8:6) = '540810'                            
                    MOVE 'Y'           TO F125-NUM-DBSS-TOP                     
                 ELSE                                                           
                    IF F630-NUM-GL-ACCT(8:6) = '540834' AND NOT KD00-FND        
                       MOVE 'Z'                    TO F125-NUM-DBSS-TOP         
                       PERFORM 5000-PRINT-P13LC06B THRU 5000-EXIT               
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE FD10-AMT-LOAN       TO F125-AMT-RECEIPT                      
              MOVE 'INS'               TO F125-CDE-PAYMENT-TYPE                 
              WRITE BP13F125-CASHTRAN                                           
              ADD 1       TO CNT-F125-WRITE                                     
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3700-DELETE-BP13KD10.                                                    
      *---------------------*                                                   
                                                                                
           DELETE BP13KD10.                                                     
                                                                                
           EVALUATE BP13KD10-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-KD10-DELETE                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13KD10-REC ...'                   
                    MOVE BP13KD10-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3920-UPDATE-BP13KD00.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13KD00-REC.                               
           MOVE KD10-NUM-REGN    TO KD00-NUM-REGN.                              
           MOVE 'N'              TO WS-KD00-FND                                 
                                                                                
           READ BP13KD00.                                                       
           IF BP13KD00-STATUS = 0                                               
              MOVE 'Y' TO WS-KD00-FND                                           
              ADD 1    TO CNT-KD00-READ                                         
           ELSE                                                                 
           IF BP13KD00-STATUS = 23                                              
              MOVE SPACES                    TO LC06-REMARK                     
              MOVE 'REC NOT FOUND IN KD00'   TO LC06-REMARK                     
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF                                                               
           END-IF.                                                              
                                                                                
       3920-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3950-REWRITE-BP13KD00.                                                   
      *----------------------*                                                  
                                                                                
           MOVE KD00-NUM-SCH-ACCNT   TO WS-KD00-SCHACC.                         
           MOVE KD00-CDE-LOAN-SCH    TO WS-KD00-CDE-LOAN-SCH.                   
           IF FD10-NUM-TRANS = 'OPT'                                            
      ****    ADD KD10-AMT-OPTION-REIMBURSE                                     
      ****                              TO KD00-AMT-OPTION-REIMBURSE            
              IF KD10-AMT-CPF-GRANT-PAYEE(1) > ZEROES                           
                 ADD  KD10-AMT-CPF-GRANT-PAYEE(1)                               
                                        TO KD00-AMT-OPTION-REFUND1              
              END-IF                                                            
              IF KD10-AMT-CPF-GRANT-PAYEE(2) > ZEROES                           
                 ADD  KD10-AMT-CPF-GRANT-PAYEE(2)                               
                                        TO KD00-AMT-OPTION-REFUND2              
              END-IF                                                            
              IF KD10-AMT-CPF-GRANT-PAYEE(3) > ZEROES                           
                 ADD  KD10-AMT-CPF-GRANT-PAYEE(3)                               
                                        TO KD00-AMT-OPTION-REFUND3              
              END-IF                                                            
              IF KD10-AMT-CPF-GRANT-PAYEE(4) > ZEROES                           
                 ADD  KD10-AMT-CPF-GRANT-PAYEE(4)                               
                                        TO KD00-AMT-OPTION-REFUND4              
              END-IF                                                            
           ELSE                                                                 
              ADD  FD10-AMT-PP-LUMPSUM  TO KD00-AMT-PP-PD-DEVLP-TOT             
              ADD  FD10-AMT-LOAN        TO KD00-AMT-LOAN-PD-DEVLP-TOT           
              MOVE F630-DTE-TRANS       TO KD00-DTE-PP-PAID-DEVLP               
           END-IF                                                               
           MOVE 'BP13CC06'           TO KD00-NUM-USERID.                        
           MOVE WS-DATE              TO KD00-DTE-UPDATE.                        
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF BP13KD00-STATUS = 0                                               
              ADD 1    TO CNT-KD00-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3950-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-PRINT-REPORT.                                                       
      *------------------*                                                      
                                                                                
           IF CNT-LINE-ON-PAGE > 46                                             
              MOVE 1 TO CNT-LINE-ON-PAGE                                        
              PERFORM 4100-REPORT-HEADING THRU 4100-EXIT                        
           ELSE                                                                 
              ADD 1 TO CNT-LINE-ON-PAGE                                         
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-PRT-CASES.                                              
                                                                                
           MOVE CNT-PRT-CASES       TO LC06-SNO.                                
           IF F630-NUM-VR = FD10-NUM-VR                                         
              MOVE FD10-NUM-REGN    TO LC06-REG-NO                              
           ELSE                                                                 
              MOVE SPACES           TO LC06-REG-NO                              
           END-IF.                                                              
                                                                                
           IF F630-NUM-VR NOT = SPACES AND LOW-VALUES AND ZEROES                
              MOVE F630-NUM-VR      TO LC06-VR-NO                               
           ELSE                                                                 
              MOVE SPACES           TO LC06-VR-NO                               
           END-IF.                                                              
                                                                                
           MOVE F630-NUM-HDB-REF    TO LC06-SCH-ACCT.                           
                                                                                
           IF FD10-AMT-PP-LUMPSUM NOT NUMERIC                                   
              MOVE ZEROES           TO FD10-AMT-PP-LUMPSUM.                     
                                                                                
           IF FD10-AMT-LOAN NOT NUMERIC                                         
              MOVE ZEROES           TO FD10-AMT-LOAN.                           
                                                                                
           IF FD10-NUM-TRANS = 'OPT'                                            
              MOVE 'OPT'               TO LC06-PAYMENT-TYPE                     
           ELSE                                                                 
              IF FD10-AMT-PP-LUMPSUM > ZEROES AND                               
                 FD10-AMT-LOAN > ZEROES                                         
                 MOVE 'PP + INS'       TO LC06-PAYMENT-TYPE                     
              ELSE                                                              
                 IF FD10-AMT-PP-LUMPSUM > ZEROES                                
                    MOVE 'PP '         TO LC06-PAYMENT-TYPE                     
                 ELSE                                                           
                    IF FD10-AMT-LOAN > ZEROES                                   
                       MOVE 'INS'      TO LC06-PAYMENT-TYPE                     
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF FD10-NUM-TRANS = 'OPT'                                            
              MOVE '13'             TO LC06-TRANS-TY                            
              IF KD10-AMT-OPTION-REIMBURSE NOT NUMERIC                          
                 MOVE ZEROES        TO KD10-AMT-OPTION-REIMBURSE                
              END-IF                                                            
              MOVE KD10-AMT-OPTION-REIMBURSE  TO LC06-AMT                       
           ELSE                                                                 
              MOVE '63'             TO LC06-TRANS-TY                            
              IF FD10-AMT-PAYMENT-DEVELOPER NOT NUMERIC                         
                 MOVE ZEROES        TO FD10-AMT-PAYMENT-DEVELOPER               
              END-IF                                                            
              MOVE FD10-AMT-PAYMENT-DEVELOPER TO LC06-AMT                       
           END-IF.                                                              
                                                                                
           MOVE KD10-DTE-FIN-RPOCESS   TO LC06-DTE-POST.                        
           MOVE F630-NUM-BATCH      TO LC06-NUM-RECEIPT.                        
                                                                                
           WRITE PRINT-REC          FROM LC06-PR-DETAILS.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4100-REPORT-HEADING.                                                     
      *--------------------*                                                    
                                                                                
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LC06-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM LC06-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LC06-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM LC06-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'    TO PRINT-REC.                                       
            WRITE PRINT-REC.                                                    
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5000-PRINT-P13LC06B.                                                     
      *-------------------*                                                     
                                                                                
           ADD 2                                   TO CNT-LC06B-LINE            
           IF CNT-LC06B-LINE > 60                                               
              PERFORM 5100-WRITE-LC06B-HEADER    THRU 5100-EXIT                 
              ADD 2                                TO CNT-LC06B-LINE            
           END-IF.                                                              
                                                                                
           PERFORM 5200-READ-BP13KD03     THRU 5200-EXIT                        
                                                                                
           ADD 1                            TO CNT-LC06B-SNO                    
           MOVE CNT-LC06B-SNO               TO LC06B-SNO                        
           MOVE FD10-NUM-REGN               TO LC06B-REGN                       
           MOVE FD10-NUM-PP                 TO LC06B-PP                         
           IF KD03-NUM-SCH-ACCNT NOT = SPACES AND LOW-VALUES                    
              STRING KD03-NUM-SCH-ACCNT(1:4) '-'                                
                     KD03-NUM-SCH-ACCNT(5:4) '-'                                
                     KD03-NUM-SCH-ACCNT(9:1) '-'                                
                     KD03-NUM-SCH-ACCNT(10:2)                                   
                     DELIMITED BY SIZE INTO LC06B-SCH-ACC                       
           END-IF                                                               
           STRING FD10-DTE-DUE-PP(7:2) '/'                                      
                  FD10-DTE-DUE-PP(5:2) '/'                                      
                  FD10-DTE-DUE-PP(1:4)                                          
                  DELIMITED BY SIZE INTO LC06B-DTE-DUE                          
           STRING F630-NUM-VR(1:3) '-'                                          
                  F630-NUM-VR(4:2) '-'                                          
                  F630-NUM-VR(6:6)                                              
                  DELIMITED BY SIZE INTO LC06B-VR-NO                            
           STRING F630-DTE-TRANS(7:2) '/'                                       
                  F630-DTE-TRANS(5:2) '/'                                       
                  F630-DTE-TRANS(1:4)                                           
                  DELIMITED BY SIZE INTO LC06B-DTE-TRANS                        
      *    MOVE F630-NUM-CPF                TO LC06B-CPF-NO.                    
           MOVE FD10-AMT-LOAN               TO LC06B-AMT-LOAN.                  
                                                                                
           MOVE LC06B-DETAIL                TO LC06B-REC.                       
           WRITE LC06B-REC AFTER 2.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       5100-WRITE-LC06B-HEADER.                                                 
      *------------------------*                                                
                                                                                
           ADD 1                           TO CNT-LC06B-PAGE.                   
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO LC06B-DATE                             
           MOVE CNT-LC06B-PAGE             TO LC06B-PAGE                        
                                                                                
           WRITE LC06B-REC AFTER PAGE.                                          
           WRITE LC06B-REC FROM LC06B-HEAD-01 AFTER 3.                          
           WRITE LC06B-REC FROM LC06B-HEAD-02.                                  
           WRITE LC06B-REC FROM LC06B-TITLE AFTER 2.                            
           MOVE ALL '='                    TO LC06B-REC                         
           WRITE LC06B-REC.                                                     
                                                                                
           MOVE 8                          TO CNT-LC06B-LINE.                   
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5200-READ-BP13KD03.                                                      
      *-------------------*                                                     
                                                                                
           MOVE FD10-NUM-REGN              TO KD03-NUM-REGN                     
                                                                                
           READ BP13KD03.                                                       
                                                                                
           IF BP13KD03-STATUS = 0                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13KD10-STATUS = 23                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13KD03 , REGN : '               
                         FD10-NUM-REGN                                          
                 MOVE SPACES               TO KD03-NUM-SCH-ACCNT                
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD03 : ' BP13KD03-STATUS          
                 MOVE BP13KD03-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CC06 *************'.                            
           DISPLAY 'NO OF RECS MATCHED F630/FD10    : ' WS-MATCH-REC.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13F125  : ' CNT-F125-WRITE.         
           DISPLAY 'NO OF RECS READ FROM  AG07F630  : ' CNT-F630-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13FD10  : ' CNT-FD10-READ.          
           DISPLAY 'NO OF FD10 WRITTEN TO BP13FD10  : ' CNT-FD10-WRITE.         
           DISPLAY 'NO OF RECS DELETED FR BP13KD10  : ' CNT-KD10-DELETE.        
           DISPLAY 'NO OF RECS READ FROM  BP13KD00  : ' CNT-KD00-READ.          
           DISPLAY 'NO OF RECS UPDATED IN BP13KD00  : ' CNT-KD00-UPDATE.        
           DISPLAY 'NO OF RECS WRITTEN TO BP13LC06  : ' CNT-PRT-CASES.          
           DISPLAY 'NO OF RECS WRITTEN TO P13LC06B  : ' CNT-LC06B-SNO.          
                                                                                
           CLOSE BP13KD10                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 AG07F630                                                       
                 P13FD10A                                                       
                 P13FD10B                                                       
                 BP13F125                                                       
                 BP13LC06                                                       
                 P13LC06B.                                                      
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD10-STATUS ' BP13KD10-STATUS          
            MOVE BP13KD10-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD00-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD00-STATUS ' BP13KD00-STATUS          
            MOVE BP13KD00-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD03-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD03-STATUS ' BP13KD03-STATUS          
            MOVE BP13KD03-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
