      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C215.                                                 
      *AUTHOR.        EDWARD S CRUZ.                                            
      *DATE-WRITTEN.  26/02/2001.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : 1.  PRINT REPORT ON APPLICANTS WHO HAVE     *            
      *                    FAILED TO SELECT FLAT ON BOOKING-DTE    *            
      *                    UNDER BE AND BTO.                       *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F800  -  SOC MASTER FILE            *            
      *                2.  BP13K060  -  CODE TRANSCRIPTION FILE    *            
      *                3.  BP13K820  -  OCCUPIER FILE              *            
      *                4.  BP13K830  -  ADDRESS FILE               *            
      *                                                            *            
      *  LISTINGS    : 1.  BP13P215  - LETTER (FORM: BP134U)       *            
      *                2.  BP13L215  - REPORT FOR BTO              *            
      *                3.  P13L215E  - REPORT FOR BE               *            
      *                                                            *            
      * SUBROUTINES  : AV02C015 - TO GET THE BAR CODE.             *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---  ------   -----------                         *            
      * BP130970 ED   20010226 NEW PROGRAM.                        *            
      * BP130970 ED   20010518 ADD FT TO REPORT, READ K830 BY ALT-K*            
      * BP131049 AS1  20010721 ADDING NEW FORM BP13J5              *            
      * BP131059 JAM  20010825 ADDING NEW FORM BP13B1              *            
      * BP132045 CT2  20011109 PRINT DESIGNATION OF OIC IN BP1347  *            
      * BP132160 SN   20020826 REMOVE PRINTING OF BP13J5           *            
      * BP132285 JF4  20030121 NEW LETTER BP134T FOR BE APPLICANTS *            
      *                        AND NEW LISTING                     *            
      * BP132293 JF4  20030206 ADDING NEW FORM BP134U FOR BTO      *            
      * BP132545 SSS2 20041102 USE COMMON LETTER                   *            
      * BP132545 SSS2 20041108 REMOVE DJDE FROM PGM                *            
      * ========================================================== *            
                                                                                
      *-------------------------------------------------------------            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BP13K060  ASSIGN       TO BP13K060                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K060-KEY-FLD                        
                            FILE STATUS  IS BP13K060-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            FILE STATUS  IS WS-K830-STATUS                      
                            ALTERNATE KEY IS K830-NUM-REGN.                     
                                                                                
           SELECT BP13P215  ASSIGN       TO BP13P215.                           
                                                                                
           SELECT BP13L215  ASSIGN       TO BP13L215.                           
           SELECT P13L215E  ASSIGN       TO P13L215E.                           
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS   25 CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD  BP13P215                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  P215-PRT-REC                     PIC X(132).                         
                                                                                
       FD  BP13L215                                                             
           RECORDING MODE  IS  F                                                
           LABEL RECORD ARE OMITTED                                             
           RECORD CONTAINS 132  CHARACTERS.                                     
       01  L215-PRT-REC                     PIC X(132).                         
                                                                                
       FD  P13L215E                                                             
           RECORDING MODE  IS  F                                                
           LABEL RECORD ARE OMITTED                                             
           RECORD CONTAINS 132  CHARACTERS.                                     
       01  L215E-PRT-REC                     PIC X(132).                        
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       COPY AV02COMM.                                                           
                                                                                
       01  WS-STATUS.                                                           
           05  BP13K060-STATUS              PIC 99    VALUE ZEROS.              
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K830-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-NOT-FND              PIC 9(06)  VALUE ZEROS.             
           05  WS-K060-NOT-FND              PIC 9(06)  VALUE ZEROS.             
           05  WS-SKIPPED-RECORDS           PIC 9(06)  VALUE ZEROS.             
           05  WS-L215-PAGE-CTR             PIC 9(04)  VALUE ZEROS.             
           05  WS-L215-LINE-CTR             PIC 9(02)  VALUE 55.                
           05  WS-L215E-PAGE-CTR            PIC 9(04)  VALUE ZEROS.             
           05  WS-L215E-LINE-CTR            PIC 9(02)  VALUE 55.                
           05  WS-F800-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-L215-SNO-CTR              PIC 9(06)  VALUE ZEROS.             
           05  WS-L215E-SNO-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-P215-WRT-CTR              PIC 9(06)  VALUE ZEROS.             
           05  WS-P215E-WRT-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-SUBJECT                   PIC X(38)  VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F800-EOF                  PIC X(01) VALUE 'N'.                
           05  WS-K820-NME-OCCP             PIC X(66) VALUE SPACES.             
                                                                                
       01  WS-DATE                          PIC X(8)  VALUE SPACES.             
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER TO APPLICANT                     *            
      *------------------------------------------------------------*            
       01  P215-LINE-01.                                                        
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  P215-REGN-NO             PIC X(08).                              
           05  FILLER                   PIC X(72) VALUE SPACES.                 
                                                                                
       01  P215-LINE-02.                                                        
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  P215-QUEUE-NO            PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(51) VALUE SPACES.                 
                                                                                
       01  P215-LINE-03.                                                        
           05  FILLER                   PIC X(71) VALUE SPACES.                 
           05  P215-DATE                PIC X(10) VALUE SPACES.                 
                                                                                
       01  P215-LINE-04.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  P215-NAME01              PIC X(66) VALUE SPACES.                 
                                                                                
       01  P215-LINE-05.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  P215-NAME02              PIC X(66) VALUE SPACES.                 
                                                                                
       01  P215-LINE-06.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  P215-BLK                 PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X     VALUE SPACES.                 
           05  P215-UNIT                PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(48) VALUE SPACES.                 
                                                                                
       01  P215-LINE-07.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  P215-ADDR-L2             PIC X(47) VALUE SPACES.                 
           05  FILLER                   PIC X(23) VALUE SPACES.                 
                                                                                
       01  P215-LINE-08.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  P215-ADDR-L3             PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(50) VALUE SPACES.                 
                                                                                
       01  P215-LINE-POSTAL.                                                    
           05 FILLER                    PIC X(10) VALUE SPACES.                 
           05 P215-BAR-CODE             PIC X(9)  VALUE SPACES.                 
                                                                                
       01  P215-LINE-09.                                                        
           05  FILLER                   PIC X(47) VALUE SPACES.                 
           05  P215-SUBJECT             PIC X(38) VALUE SPACES.                 
                                                                                
       01  P215-LINE-10.                                                        
           05  FILLER                   PIC X(53) VALUE SPACES.                 
           05  P215-PARA1               PIC X(38) VALUE SPACES.                 
           05  FILLER                   PIC X     VALUE '.'.                    
                                                                                
       01  P215-LINE-11.                                                        
           05  FILLER                   PIC X(21) VALUE SPACES.                 
           05  P215-PARA2               PIC X(38) VALUE SPACES.                 
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  L215-HDG1.                                                           
           05  FILLER                   PIC X(14) VALUE 'BP13L215'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(15) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(15) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE : '.              
           05  L215-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE '  PAGE: '.             
           05  L215-PAGE                PIC ZZZZ9 VALUE ZEROS.                  
                                                                                
       01  L215-HDG2.                                                           
           05  FILLER                   PIC X(30) VALUE SPACES.                 
           05  L215-REP-TITLE           PIC X(60) VALUE SPACES.                 
           05  FILLER                   PIC X(42) VALUE SPACES.                 
                                                                                
       01  L215-HDG3.                                                           
           05  FILLER               PIC X(49) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE 'FLAT'.                     
           05  FILLER               PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE 'NT-FT'.                    
           05  FILLER               PIC X(11) VALUE SPACES.                     
           05  FILLER               PIC X(06) VALUE 'BALLOT'.                   
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'BOOKING'.                  
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'BOOKING'.                  
           05  FILLER               PIC X(23) VALUE SPACES.                     
                                                                                
       01  L215-HDG4.                                                           
           05  FILLER               PIC X(06) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE 'S/NO'.                     
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'REGN NO'.                  
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  L215-ZONE-NT         PIC X(04) VALUE 'ZONE'.                     
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE 'TYPE'.                     
           05  FILLER               PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE 'QUEUE'.                    
           05  FILLER               PIC X(13) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE 'DATE'.                     
           05  FILLER               PIC X(14) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE 'DATE'.                     
           05  FILLER               PIC X(15) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE 'TIME'.                     
                                                                                
       01  L215-HDG5.                                                           
           05  FILLER               PIC X(132) VALUE ALL '-'.                   
                                                                                
       01  PR-DETAIL.                                                           
           05  FILLER               PIC X(03) VALUE SPACES.                     
           05  PR-SNO               PIC ZZZ,ZZ9.                                
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PR-REGN-NO           PIC X(08).                                  
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PR-ZONE              PIC X(05).                                  
           05  FILLER               PIC X(07) VALUE SPACES.                     
           05  PR-FLAT-TYPE         PIC X(02).                                  
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PR-QUEUE-NO          PIC X(05).                                  
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PR-DTE-BALLOT        PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  PR-DTE-BK            PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE SPACES.                     
           05  PR-TME-BK            PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(25) VALUE SPACES.                     
                                                                                
       01  PR-TRAILER.                                                          
           05  FILLER               PIC X(42) VALUE SPACES.                     
           05  FILLER               PIC X(40) VALUE                             
                 '* * *   E N D  O F  R E P O R T   * * *'.                     
                                                                                
      *------------------------*                                                
      *  LINKAGE FOR AV02C015  *                                                
      *------------------------*                                                
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE         PIC X(6).                          
            05  AV02C015-OUT-POSTCODE-CHKSUM PIC X(9).                          
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3500-READ-BP13F800          THRU 3500-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                       UNTIL WS-F800-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT BP13F800                                                  
                      BP13K060                                                  
                      BP13K820                                                  
                      BP13K830                                                  
               OUTPUT BP13P215                                                  
                      BP13L215                                                  
                      P13L215E.                                                 
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR-BP13K060-STATUS ' BP13K060-STATUS          
              MOVE     BP13K060-STATUS TO RETURN-CODE                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS IS ' WS-K820-STATUS            
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K830 STATUS IS ' WS-K830-STATUS            
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L215-DATE.                             
           MOVE L215-DATE                  TO P215-DATE.                        
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4500-MOVE-DETAILS      THRU 4500-EXIT.                       
           PERFORM 6000-PRINT-REPORT      THRU 6000-EXIT.                       
           PERFORM 3500-READ-BP13F800     THRU 3500-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800                                                        
                AT END MOVE 'Y' TO WS-F800-EOF                                  
                       GO TO 3500-EXIT.                                         
                                                                                
           ADD  1  TO  WS-F800-READ-CTR.                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4500-MOVE-DETAILS.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                TO P215-REGN-NO                           
                                         P215-QUEUE-NO                          
                                         P215-NAME01                            
                                         P215-NAME02                            
                                         P215-BLK                               
                                         P215-UNIT                              
                                         P215-ADDR-L2                           
                                         P215-ADDR-L3                           
                                         P215-BAR-CODE                          
                                         P215-SUBJECT                           
                                         P215-PARA1                             
                                         P215-PARA2.                            
                                                                                
           MOVE WS-L215-SNO-CTR          TO PR-SNO.                             
           MOVE F800-NUM-REGN            TO PR-REGN-NO                          
                                            P215-REGN-NO.                       
           MOVE F800-NUM-NT-FT-QUEUE     TO PR-QUEUE-NO                         
                                            P215-QUEUE-NO.                      
                                                                                
           MOVE F800-NUM-ZONE            TO PR-ZONE.                            
                                                                                
           MOVE F800-NUM-FLAT-TYPE       TO PR-FLAT-TYPE.                       
                                                                                
           STRING F800-DTE-BALLOT(1:4) '/' F800-DTE-BALLOT(5:2)                 
                  DELIMITED BY SIZE INTO PR-DTE-BALLOT.                         
                                                                                
           STRING F800-DTE-BK-APPT(7:2) '/' F800-DTE-BK-APPT(5:2) '/'           
                  F800-DTE-BK-APPT(1:4) DELIMITED BY SIZE                       
                  INTO PR-DTE-BK.                                               
           STRING F800-TME-BK-APPT(1:2) ':' F800-TME-BK-APPT(3:2)               
                  DELIMITED BY SIZE INTO PR-TME-BK.                             
                                                                                
      *   <<< GET NAME FROM K820 >>>                                            
           MOVE SPACES               TO    K820-KEY-FLD.                        
           MOVE F800-NUM-REGN        TO    K820-NUM-REGN.                       
           MOVE F800-NUM-NRIC1       TO    K820-NUM-NRIC.                       
                                                                                
           PERFORM  4600-READ-K820   THRU  4600-EXIT.                           
           MOVE     WS-K820-NME-OCCP TO    P215-NAME01.                         
                                                                                
           IF F800-NUM-NRIC2 (2:1) = '#' OR SPACES OR LOW-VALUES                
              MOVE SPACES            TO    P215-NAME02                          
           ELSE                                                                 
              MOVE SPACES            TO    K820-KEY-FLD                         
              MOVE F800-NUM-REGN     TO    K820-NUM-REGN                        
              MOVE F800-NUM-NRIC2    TO    K820-NUM-NRIC                        
                                                                                
              PERFORM  4600-READ-K820   THRU  4600-EXIT                         
              MOVE     WS-K820-NME-OCCP TO    P215-NAME02                       
           END-IF.                                                              
                                                                                
           PERFORM  4800-GET-ADDR    THRU  4800-EXIT.                           
                                                                                
           MOVE SPACES TO WS-SUBJECT.                                           
                                                                                
           EVALUATE F800-NUM-ALLO-CAT                                           
           WHEN 'BE '                                                           
                PERFORM  5000-GET-SUBJECT-BE  THRU  5000-EXIT                   
                MOVE F800-NUM-NEW-TOWN  TO PR-ZONE                              
           WHEN 'BTO'                                                           
                PERFORM  5500-GET-SUBJECT-BTO THRU  5500-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-SUBJECT            TO P215-SUBJECT                           
                                         P215-PARA1                             
                                         P215-PARA2.                            
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4600-READ-K820.                                                          
      ******************************************************************        
           MOVE    SPACES                TO  WS-K820-NME-OCCP                   
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE    K820-NME-OCCP      TO  WS-K820-NME-OCCP                   
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'K820 REC NOT FOUND : '   K820-KEY-FLD                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = '   WS-K820-STATUS              
              DISPLAY '      K820-KEY-FLD     = '   K820-KEY-FLD                
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4800-GET-ADDR.                                                           
      ******************************************************************        
                                                                                
           MOVE SPACES            TO    K830-KEY-FLD                            
                                        WS-LINK-AV02C015                        
           MOVE F800-NUM-REGN     TO    K830-NUM-REGN.                          
           READ BP13K830                                                        
                KEY IS K830-NUM-REGN.                                           
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO P215-BLK                          
                 MOVE K830-NUM-FLAT        TO P215-UNIT                         
                 MOVE K830-NME-STREET-BLDG(1:47) TO P215-ADDR-L2                
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO P215-ADDR-L3                      
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE INTO P215-ADDR-L3                      
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 7000-CALL-AV02C015 THRU 7000-EXIT                   
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO P215-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO P215-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO P215-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG(1:47) TO P215-ADDR-L2              
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO P215-ADDR-L3                  
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO P215-ADDR-L3                  
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 7000-CALL-AV02C015 THRU 7000-EXIT                   
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO P215-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
           IF WS-K830-STATUS = 23                                               
              MOVE SPACES TO P215-BLK  P215-UNIT  P215-ADDR-L2                  
                             P215-ADDR-L3                                       
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ',  K830-KEY-FLD IS ' K830-KEY-FLD                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
                      ',  K830-KEY-FLD IS ' K830-KEY-FLD                        
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       5000-GET-SUBJECT-BE.                                                     
      ******************************************************************        
                                                                                
           EVALUATE F800-DTE-BALLOT(5:2)                                        
           WHEN '01'                                                            
              STRING 'JANUARY ' F800-DTE-BALLOT(1:4)                            
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '02'                                                            
              STRING 'FEBRUARY ' F800-DTE-BALLOT(1:4)                           
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '03'                                                            
              STRING 'MARCH ' F800-DTE-BALLOT(1:4)                              
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '04'                                                            
              STRING 'APRIL ' F800-DTE-BALLOT(1:4)                              
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '05'                                                            
              STRING 'MAY ' F800-DTE-BALLOT(1:4)                                
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '06'                                                            
              STRING 'JUNE ' F800-DTE-BALLOT(1:4)                               
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '07'                                                            
              STRING 'JULY ' F800-DTE-BALLOT(1:4)                               
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '08'                                                            
              STRING 'AUGUST ' F800-DTE-BALLOT(1:4)                             
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '09'                                                            
              STRING 'SEPTEMBER ' F800-DTE-BALLOT(1:4)                          
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '10'                                                            
              STRING 'OCTOBER ' F800-DTE-BALLOT(1:4)                            
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '11'                                                            
              STRING 'NOVEMBER ' F800-DTE-BALLOT(1:4)                           
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '12'                                                            
              STRING 'DECEMBER ' F800-DTE-BALLOT(1:4)                           
                     ' BALLOTING EXERCISE'                                      
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       5500-GET-SUBJECT-BTO.                                                    
      ******************************************************************        
                                                                                
           EVALUATE F800-DTE-BALLOT(5:2)                                        
           WHEN '01'                                                            
              STRING 'JANUARY ' F800-DTE-BALLOT(1:4)                            
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '02'                                                            
              STRING 'FEBRUARY ' F800-DTE-BALLOT(1:4)                           
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '03'                                                            
              STRING 'MARCH ' F800-DTE-BALLOT(1:4)                              
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '04'                                                            
              STRING 'APRIL ' F800-DTE-BALLOT(1:4)                              
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '05'                                                            
              STRING 'MAY ' F800-DTE-BALLOT(1:4)                                
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '06'                                                            
              STRING 'JUNE ' F800-DTE-BALLOT(1:4)                               
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '07'                                                            
              STRING 'JULY ' F800-DTE-BALLOT(1:4)                               
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '08'                                                            
              STRING 'AUGUST ' F800-DTE-BALLOT(1:4)                             
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '09'                                                            
              STRING 'SEPTEMBER ' F800-DTE-BALLOT(1:4)                          
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '10'                                                            
              STRING 'OCTOBER ' F800-DTE-BALLOT(1:4)                            
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '11'                                                            
              STRING 'NOVEMBER ' F800-DTE-BALLOT(1:4)                           
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           WHEN '12'                                                            
              STRING 'DECEMBER ' F800-DTE-BALLOT(1:4)                           
                     ' BUILD-TO-ORDER EXERCISE'                                 
                     DELIMITED BY SIZE                                          
                     INTO WS-SUBJECT                                            
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
           EVALUATE F800-NUM-ALLO-CAT                                           
           WHEN 'BE '                                                           
               ADD   1                  TO WS-L215E-SNO-CTR                     
               MOVE WS-L215E-SNO-CTR    TO PR-SNO                               
               IF WS-L215E-LINE-CTR = 55                                        
                 ADD   1                TO WS-L215E-PAGE-CTR                    
                 MOVE WS-L215E-PAGE-CTR TO L215-PAGE                            
                 MOVE ' NT '            TO L215-ZONE-NT                         
                 STRING 'BE APPLICANTS WHO HAVE NOT SELECTED '                  
                        'FLAT ON BOOKING DATE'  DELIMITED BY SIZE               
                         INTO L215-REP-TITLE                                    
                 WRITE L215E-PRT-REC  FROM L215-HDG1 AFTER PAGE                 
                 WRITE L215E-PRT-REC  FROM L215-HDG2 AFTER 1                    
                 WRITE L215E-PRT-REC  FROM L215-HDG3 AFTER 1                    
                 WRITE L215E-PRT-REC  FROM L215-HDG4 AFTER 1                    
                 WRITE L215E-PRT-REC  FROM L215-HDG5 AFTER 1                    
                 WRITE L215E-PRT-REC  FROM PR-DETAIL AFTER 1                    
                 MOVE   7               TO WS-L215E-LINE-CTR                    
               ELSE                                                             
                 ADD    1               TO WS-L215E-LINE-CTR                    
                 WRITE L215E-PRT-REC  FROM PR-DETAIL AFTER 1                    
               END-IF                                                           
                                                                                
           WHEN 'BTO'                                                           
               ADD  1  TO WS-L215-SNO-CTR                                       
               MOVE WS-L215-SNO-CTR TO PR-SNO                                   
               IF WS-L215-LINE-CTR = 55                                         
                 STRING 'BTO APPLICANTS WHO HAVE NOT SELECTED '                 
                        'FLAT ON BOOKING DATE'  DELIMITED BY SIZE               
                         INTO L215-REP-TITLE                                    
                 ADD    1                  TO WS-L215-PAGE-CTR                  
                 MOVE WS-L215-PAGE-CTR     TO L215-PAGE                         
                 MOVE 'ZONE'               TO L215-ZONE-NT                      
                 WRITE L215-PRT-REC      FROM L215-HDG1 AFTER PAGE              
                 WRITE L215-PRT-REC      FROM L215-HDG2 AFTER 1                 
                 WRITE L215-PRT-REC      FROM L215-HDG3 AFTER 2                 
                 WRITE L215-PRT-REC      FROM L215-HDG4 AFTER 1                 
                 WRITE L215-PRT-REC      FROM L215-HDG5 AFTER 1                 
                 WRITE L215-PRT-REC      FROM PR-DETAIL AFTER 1                 
                 MOVE    7                 TO WS-L215-LINE-CTR                  
               ELSE                                                             
                 ADD     1                 TO WS-L215-LINE-CTR                  
                 WRITE L215-PRT-REC      FROM PR-DETAIL AFTER 1                 
               END-IF                                                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----- PRINT LETTER ---------                                             
                                                                                
           MOVE SPACES            TO  P215-PRT-REC.                             
                                                                                
           WRITE P215-PRT-REC   FROM  P215-LINE-03  AFTER PAGE.                 
           WRITE P215-PRT-REC   FROM  P215-LINE-01  AFTER 3.                    
           WRITE P215-PRT-REC   FROM  P215-LINE-02.                             
           WRITE P215-PRT-REC   FROM  P215-LINE-04  AFTER 3.                    
           WRITE P215-PRT-REC   FROM  P215-LINE-05.                             
           WRITE P215-PRT-REC   FROM  P215-LINE-06.                             
           WRITE P215-PRT-REC   FROM  P215-LINE-07.                             
           WRITE P215-PRT-REC   FROM  P215-LINE-08.                             
           WRITE P215-PRT-REC   FROM  P215-LINE-POSTAL AFTER 2.                 
                                                                                
           WRITE P215-PRT-REC   FROM  P215-LINE-09     AFTER 5.                 
           WRITE P215-PRT-REC   FROM  P215-LINE-10     AFTER 3.                 
           WRITE P215-PRT-REC   FROM  P215-LINE-11     AFTER 4.                 
                                                                                
           ADD   1              TO    WS-P215-WRT-CTR.                          
                                                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY ' '.                                                        
            DISPLAY ' ===== BP13C215 CONTROL TOTALS =============='.            
            DISPLAY ' '.                                                        
            DISPLAY ' F800 RECORDS READ     : ' WS-F800-READ-CTR.               
            DISPLAY ' '.                                                        
            DISPLAY ' BTO DETAILS WRITTEN   : ' WS-L215-SNO-CTR.                
            DISPLAY ' BE  DETAILS WRITTEN   : ' WS-L215E-SNO-CTR.               
            DISPLAY ' LETTERS PRINTED       : ' WS-P215-WRT-CTR.                
            DISPLAY ' '.                                                        
            DISPLAY ' ============================================'.            
                                                                                
            IF WS-L215-LINE-CTR <= 55                                           
               WRITE L215-PRT-REC FROM PR-TRAILER AFTER 1                       
            ELSE                                                                
               WRITE L215-PRT-REC FROM PR-TRAILER AFTER PAGE                    
            END-IF.                                                             
                                                                                
            IF WS-L215E-LINE-CTR <= 55                                          
               WRITE L215E-PRT-REC FROM PR-TRAILER AFTER 1                      
            ELSE                                                                
               WRITE L215E-PRT-REC FROM PR-TRAILER AFTER PAGE                   
            END-IF.                                                             
                                                                                
            CLOSE BP13F800                                                      
                  BP13K060                                                      
                  BP13K820                                                      
                  BP13K830                                                      
                  BP13P215                                                      
                  BP13L215                                                      
                  P13L215E.                                                     
                                                                                
            IF BP13K060-STATUS NOT = 00                                         
               DISPLAY 'CLOSING ERROR-BP13K060-STAT ' BP13K060-STATUS.          
                                                                                
            IF WS-K820-STATUS NOT = 00                                          
               DISPLAY 'CLOSING ERROR-BP13K820-STAT ' WS-K820-STATUS.           
                                                                                
            IF WS-K830-STATUS NOT = 00                                          
               DISPLAY 'CLOSING ERROR-BP13K830-STAT ' WS-K830-STATUS.           
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
