       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC98.                                                 
       AUTHOR.        EDEN DINGAL.                                              
       DATE-WRITTEN.  18/03/2008.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO CREATE DBS MASTER HISTORY FILE     *                
      *                  FOR REPUDIATION CASES                 *                
      *                : TO CREATE DBS MASTER HISTORY FILE     *                
      *                  AFTER EFF-DATE-TRANSFER AND LOAN-BAL  *                
      *                  = ZEROS.                              *                
      *                                                        *                
      *    HANDLE CASES WITH DTE-REPUDIATE :                   *                
      *    1. IF LN-BAL = 0,   DELETE KD00 AND CREATE KD03     *                
      *                        CDE-ACCT-STAT : Z               *                
      *    2. IF LN-BAL NOT 0, UPDATE KD00                     *                
      *                        CDE-ACCT-STAT : X               *                
      *                                                        *                
      *    INPUT FILES : BP13FD00                              *                
      *    I-O FILES   : BP13KD00                              *                
      *                  BP13KD03                              *                
      *                  BP13KD63                              *                
      *                  BP13KD64                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * --------  --     ----      -----------                 *                
      * BP133040  ED1   20080318   NEW PGM                     *                
      * BP133450  JB8   20080924   CATER FOR DTE-EFF-TRANS     *                
      * BP133450  JB8   20081006   MOVE KD63 TO KD64           *                
      * BP133489  JB8   20090317   MOVE KD60 TO KD61           *                
      * BP133588  JB8   20090506   TO OUTPUT F136              *                
      * BP133591  JB8   20091009   TO PRINT NEW REPORT         *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
           SELECT BP13FD00 ASSIGN       TO BP13FD00.                            
           SELECT BP13F136 ASSIGN       TO BP13F136.                            
                                                                                
           SELECT BP13KD00 ASSIGN       TO BP13KD00                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD00-NUM-REGN                        
                           FILE STATUS  IS KD00-STATUS.                         
                                                                                
           SELECT BP13KD03 ASSIGN       TO BP13KD03                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD03-NUM-REGN                        
                           FILE STATUS  IS KD03-STATUS.                         
                                                                                
           SELECT BP13KD60 ASSIGN       TO BP13KD60                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD60-NUM-REGN                        
                           FILE STATUS  IS KD60-STATUS.                         
                                                                                
           SELECT BP13KD61 ASSIGN       TO BP13KD61                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD61-KEY-FLD                         
                           FILE STATUS  IS KD61-STATUS.                         
                                                                                
           SELECT BP13KD63 ASSIGN       TO BP13KD63                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS KD63-KEY-FLD                         
                           FILE STATUS  IS KD63-STATUS.                         
                                                                                
           SELECT BP13KD64 ASSIGN       TO BP13KD64                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS KD64-KEY-FLD                         
                           FILE STATUS  IS KD64-STATUS.                         
                                                                                
           SELECT BP13LC98 ASSIGN       TO BP13LC98.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13FD00                                                             
           RECORD CONTAINS 1600 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13FD00.                                                           
                                                                                
       FD  BP13F136                                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F136.                                                           
                                                                                
       FD  BP13KD03                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD03.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD61                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13KD61.                                                           
                                                                                
       FD  BP13KD60                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13KD60.                                                           
                                                                                
       FD  BP13KD63                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13KD63.                                                           
                                                                                
       FD  BP13KD64                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13KD64.                                                           
                                                                                
       FD  BP13LC98                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13LC98-REC               PIC X(132).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-PROCESS-DATE                PIC X(8)  VALUE SPACES.               
       01  WS-CURR-DATE.                                                        
           05  WS-DATE.                                                         
                10  WS-DATE-CYM.                                                
                    15  WS-DATE-CCYY      PIC X(4).                             
                    15  WS-DATE-MM        PIC X(2).                             
                10  WS-DATE-DD            PIC X(2).                             
           05  WS-CURR-TIME               PIC X(9).                             
           05  WS-TIME REDEFINES WS-CURR-TIME.                                  
                10  WS-HH                 PIC 9(2).                             
                10  WS-MM                 PIC 9(2).                             
                10  WS-SS                 PIC 9(2).                             
                10  WS-MS                 PIC 9(2).                             
                10  WS-GM                 PIC 9(1).                             
           05  FILLER                     PIC X(4).                             
                                                                                
       01  KD00-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  KD03-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  KD63-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  KD64-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  KD60-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  KD61-STATUS                    PIC 9(2)  VALUE ZERO.                 
                                                                                
       01  WS-FD00-READ                   PIC 9(7)  VALUE ZERO.                 
       01  WS-KD00-READ                   PIC 9(7)  VALUE ZERO.                 
       01  WS-KD03-READ                   PIC 9(7)  VALUE ZERO.                 
       01  WS-KD60-READ                   PIC 9(7)  VALUE ZERO.                 
       01  WS-KD60-DELETE                 PIC 9(7)  VALUE ZERO.                 
       01  WS-KD61-WRITE                  PIC 9(7)  VALUE ZERO.                 
       01  WS-KD03-WRITE                  PIC 9(7)  VALUE ZERO.                 
       01  WS-KD63-DELETE                 PIC 9(7)  VALUE ZERO.                 
       01  WS-KD63-READ                   PIC 9(7)  VALUE ZERO.                 
       01  WS-KD64-WRITE                  PIC 9(7)  VALUE ZERO.                 
       01  WS-KD00-UPDATE                 PIC 9(7)  VALUE ZERO.                 
       01  WS-KD00-DELETE                 PIC 9(7)  VALUE ZERO.                 
       01  WS-F136-WRITE-PP               PIC 9(7)  VALUE ZERO.                 
       01  WS-F136-WRITE-INS              PIC 9(7)  VALUE ZERO.                 
       01  WS-WRITE-KD61                  PIC X(01) VALUE 'N'.                  
       01  WS-SER-VAR.                                                          
           05  WS-SER-CHR.                                                      
               10 WS-SER-CHR-1          PIC X(01).                              
               10 WS-SER-CHR-2          PIC X(01).                              
               10 WS-SER-CHR-3          PIC X(01).                              
           05  WS-NUM-SER REDEFINES WS-SER-CHR                                  
                                       PIC 9(03).                               
                                                                                
       01  WS-EOF-FD00                    PIC X     VALUE SPACE.                
           88 EOF-FD00                    VALUE 'Y'.                            
       01  WS-EOF-KD63                    PIC X     VALUE SPACE.                
           88 EOF-KD63                    VALUE 'Y'.                            
       01  WS-KD00-FND                    PIC X     VALUE 'N'.                  
       01  WS-KD60-FND                    PIC X     VALUE SPACE.                
           88 KD60-FND                    VALUE 'Y'.                            
                                                                                
      **************************************************************            
      *                        REPORT LAYOUT                       *            
      **************************************************************            
       01  WS-LC98-PAGE-CNT               PIC 9(05) VALUE ZEROES.               
       01  WS-LC98-SNO                    PIC 9(05) VALUE ZEROES.               
       01  WS-LC98-REC-CNT                PIC 9(02) VALUE 60.                   
                                                                                
       01  LC98-HEADER-1.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC98'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC98-DTE-CURRENT    PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC98-PAGE-NO        PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  LC98-HEADER-2.                                                       
           05  FILLER                    PIC X(38) VALUE SPACES.                
           05  FILLER                    PIC X(55) VALUE                        
              'DELETE REPUDIATION OR SUB TRANSFER CASES FROM PP-MASTER'.        
           05  FILLER                    PIC X(39) VALUE SPACES.                
                                                                                
       01  LC98-HEADER-3.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '     SNO   REGN        SCH ACCT          REPUDIATE DATE         
      -        '   EFF TRANSFER DATE    LOAN COMM DATE'.                        
                                                                                
       01  LC98-HEADER-4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '   -----   --------    --------------    --------------         
      -        '   -----------------    --------------'.                        
                                                                                
       01  LC98-DETAIL.                                                         
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  LC98-SNO                  PIC ZZZZ9 VALUE ZEROES.                
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  LC98-NUM-REGN             PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  LC98-SCH-ACC              PIC X(14) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  LC98-DTE-REPUDIATE        PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  LC98-DTE-EFF-TRANSFER     PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  LC98-DTE-LOAN-COMM        PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(41) VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           MOVE FUNCTION CURRENT-DATE    TO  WS-CURR-DATE.                      
           PERFORM 2000-READ-BP13FD00    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-BP13KD00 THRU 3000-EXIT                         
             UNTIL EOF-FD00.                                                    
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13FD00                                                 
                       SY02F001                                                 
                I-O    BP13KD03                                                 
                       BP13KD60                                                 
                       BP13KD61                                                 
                       BP13KD63                                                 
                       BP13KD64                                                 
                       BP13KD00                                                 
                OUTPUT BP13F136                                                 
                       BP13LC98.                                                
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, KD00-STATUS ' KD00-STATUS                    
              MOVE KD00-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF KD03-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, KD03-STATUS ' KD03-STATUS                    
              MOVE KD03-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF KD60-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, KD60-STATUS ' KD60-STATUS                    
              MOVE KD60-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF KD61-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, KD61-STATUS ' KD61-STATUS                    
              MOVE KD61-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF KD63-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, KD63-STATUS ' KD63-STATUS                    
              MOVE KD63-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF KD64-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, KD64-STATUS ' KD64-STATUS                    
              MOVE KD64-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT   TO WS-PROCESS-DATE.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13FD00.                                                      
      *---------------------------------------------------------------*         
           READ BP13FD00 AT END                                                 
                MOVE 'Y' TO WS-EOF-FD00                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-FD00-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-BP13KD00.                                                   
      *---------------------------------------------------------------*         
            MOVE FD00-NUM-REGN TO KD00-NUM-REGN.                                
                                                                                
            READ BP13KD00.                                                      
            EVALUATE KD00-STATUS                                                
               WHEN 00                                                          
                  ADD 1 TO WS-KD00-READ                                         
                  PERFORM 4000-PROCESS-REPUD THRU 4000-EXIT                     
               WHEN 23                                                          
                  DISPLAY 'BP13KD00 - REC NOTFD ' KD00-NUM-REGN                 
               WHEN OTHER                                                       
                  DISPLAY 'ERROR IN READING BP13KD00 - STATUS '                 
                              KD00-STATUS                                       
            END-EVALUATE.                                                       
                                                                                
            PERFORM 2000-READ-BP13FD00    THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-REPUD.                                                      
      *---------------------------------------------------------------*         
           IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE ZEROS TO KD00-AMT-LOAN-BAL                                   
           END-IF.                                                              
                                                                                
           IF (KD00-DTE-REPUDIATE NOT = SPACES AND                              
                                        LOW-VALUES AND ZEROS) AND               
              (KD00-DTE-REPUDIATE(1:6) < WS-PROCESS-DATE(1:6))                  
              IF KD00-AMT-LOAN-BAL  = ZEROS                                     
                 MOVE SPACES       TO BP13KD03-REC                              
                 INITIALIZE           BP13KD03-REC                              
                 MOVE BP13KD00-REC TO BP13KD03-REC                              
                 MOVE 'Z'          TO KD03-CDE-ACCT-STAT                        
                 PERFORM 8000-PRINT-REPORT     THRU 8000-EXIT                   
                 PERFORM 7000-WRITE-BP13F136   THRU 7000-EXIT                   
                 PERFORM 5000-WRITE-BP13KD03   THRU 5000-EXIT                   
                 PERFORM 4200-START-BP13KD63   THRU 4200-EXIT                   
                 PERFORM 5100-DELETE-BP13KD00  THRU 5100-EXIT                   
                 PERFORM 5200-WRITE-BP13KD61   THRU 5200-EXIT                   
              ELSE                                                              
                 MOVE 'X'          TO KD00-CDE-ACCT-STAT                        
                 PERFORM 6000-REWRITE-BP13KD00 THRU 6000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (KD00-DTE-EFF-TRANSFER NOT = SPACES AND                           
                                        LOW-VALUES AND ZEROS) AND               
              KD00-DTE-EFF-TRANSFER(1:6) < WS-PROCESS-DATE(1:6) AND             
              KD00-AMT-LOAN-BAL = ZEROES AND                                    
              (KD00-DTE-LOAN-COMM NOT = SPACES AND LOW-VALUES)                  
                                                                                
                 MOVE SPACES       TO BP13KD03-REC                              
                 INITIALIZE           BP13KD03-REC                              
                 MOVE BP13KD00-REC TO BP13KD03-REC                              
                 MOVE 'Z'          TO KD03-CDE-ACCT-STAT                        
                 PERFORM 8000-PRINT-REPORT     THRU 8000-EXIT                   
                 PERFORM 7000-WRITE-BP13F136   THRU 7000-EXIT                   
                 PERFORM 5000-WRITE-BP13KD03   THRU 5000-EXIT                   
                 PERFORM 4200-START-BP13KD63   THRU 4200-EXIT                   
                 PERFORM 5100-DELETE-BP13KD00  THRU 5100-EXIT                   
                 PERFORM 5200-WRITE-BP13KD61   THRU 5200-EXIT                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-START-BP13KD63.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13KD63-REC.                       
           INITIALIZE                       BP13KD63-REC.                       
           MOVE 'N'                      TO WS-EOF-KD63                         
                                                                                
           MOVE KD00-NUM-REGN            TO KD63-NUM-REGN.                      
                                                                                
           START BP13KD63 KEY >= KD63-KEY-FLD.                                  
                                                                                
           EVALUATE KD63-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 4240-READNEXT-BP13KD63 THRU 4240-EXIT                  
                   UNTIL KD63-NUM-REGN NOT = KD00-NUM-REGN                      
                      OR WS-EOF-KD63 = 'Y'                                      
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD63 - ERROR READING : '                          
                         KD63-STATUS                                            
                 MOVE KD63-STATUS        TO RETURN-CODE                         
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4240-READNEXT-BP13KD63.                                                  
      ******************************************************************        
                                                                                
           READ BP13KD63 NEXT                                                   
             AT END MOVE 'Y' TO WS-EOF-KD63.                                    
                                                                                
           EVALUATE KD63-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                 IF KD63-NUM-REGN = KD00-NUM-REGN                               
                    ADD 1                            TO WS-KD63-READ            
                    MOVE BP13KD63-REC                TO BP13KD64-REC            
                    PERFORM 4260-WRITE-BP13KD64    THRU 4260-EXIT               
                    PERFORM 4280-DELETE-BP13KD63   THRU 4280-EXIT               
                 ELSE                                                           
                    MOVE 'Y'                      TO WS-EOF-KD63                
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'                         TO WS-EOF-KD63                
              WHEN OTHER                                                        
                 DISPLAY '**BP13KD63 - ERROR READING : '                        
                         KD63-STATUS                                            
                 MOVE KD63-STATUS                 TO RETURN-CODE                
           END-EVALUATE.                                                        
                                                                                
       4240-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4260-WRITE-BP13KD64.                                                     
      ******************************************************************        
                                                                                
           WRITE BP13KD64-REC.                                                  
                                                                                
           IF KD64-STATUS = 00 OR 22                                            
              ADD 1                         TO WS-KD64-WRITE                    
           ELSE                                                                 
              DISPLAY 'BP13KD64 - ERROR WRITING : '                             
                      KD64-STATUS                                               
              MOVE KD64-STATUS              TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
       4260-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4280-DELETE-BP13KD63.                                                    
      ******************************************************************        
                                                                                
           DELETE BP13KD63.                                                     
                                                                                
           IF KD63-STATUS = 00                                                  
              ADD 1                         TO WS-KD63-DELETE                   
           ELSE                                                                 
              DISPLAY 'BP13KD63 - ERROR WRITING : '                             
                      KD63-STATUS                                               
              MOVE KD63-STATUS              TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
       4280-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-WRITE-BP13KD03.                                                     
      *---------------------------------------------------------------*         
           WRITE BP13KD03-REC.                                                  
                                                                                
           EVALUATE KD03-STATUS                                                 
              WHEN ZERO                                                         
                ADD 1   TO WS-KD03-WRITE                                        
              WHEN 22                                                           
                DISPLAY 'DUPLICATE REC IN BP13KD03 ' KD03-NUM-REGN              
              WHEN OTHER                                                        
                DISPLAY 'WRITE ERROR, KD03-STATUS ' KD03-STATUS                 
                DISPLAY ',KEY IS' KD03-NUM-REGN                                 
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       5100-DELETE-BP13KD00.                                                    
      *---------------------------------------------------------------*         
           DELETE BP13KD00.                                                     
                                                                                
           EVALUATE KD00-STATUS                                                 
              WHEN ZERO                                                         
                ADD 1   TO WS-KD00-DELETE                                       
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN BP13KD00 ' KD00-NUM-REGN           
              WHEN OTHER                                                        
                DISPLAY 'DELETE ERROR, KD00-STATUS ' KD00-STATUS                
                DISPLAY ',KEY IS ' KD00-NUM-REGN                                
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5200-WRITE-BP13KD61.                                                     
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 5210-READ-BP13KD60        THRU 5210-EXIT.                    
           IF KD60-FND                                                          
              PERFORM 5220-MOVE-VALUES-KD61  THRU 5220-EXIT                     
              MOVE 'N'    TO WS-WRITE-KD61                                      
              PERFORM 5230-WRITE-KD61        THRU 5230-EXIT                     
                UNTIL WS-WRITE-KD61 = 'Y'                                       
              PERFORM 5250-DELETE-BP13KD60   THRU 5250-EXIT                     
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5210-READ-BP13KD60.                                                      
      *---------------------------------------------------------------*         
                                                                                
           MOVE FD00-NUM-REGN            TO KD60-NUM-REGN.                      
           MOVE 'N'                      TO WS-KD60-FND.                        
                                                                                
           READ BP13KD60.                                                       
                                                                                
           EVALUATE KD60-STATUS                                                 
              WHEN ZERO                                                         
                ADD 1    TO WS-KD60-READ                                        
                MOVE 'Y' TO WS-KD60-FND                                         
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN BP13KD60 ' KD60-NUM-REGN           
              WHEN OTHER                                                        
                DISPLAY 'DELETE ERROR, KD60-STATUS ' KD60-STATUS                
                DISPLAY ',KEY IS ' KD60-NUM-REGN                                
           END-EVALUATE.                                                        
                                                                                
       5210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5220-MOVE-VALUES-KD61.                                                   
      ******************************************************************        
           MOVE SPACES                 TO BP13KD61-REC.                         
           INITIALIZE                     BP13KD61-REC.                         
           MOVE BP13KD60-REC           TO BP13KD61-REC.                         
                                                                                
           MOVE WS-TIME                TO KD61-TME-UPDATE.                      
           MOVE WS-CURR-DATE           TO KD61-DTE-UPDATE.                      
           MOVE 'BP13CC98'             TO KD61-NUM-USERID.                      
                                                                                
       5220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5230-WRITE-KD61.                                                         
      ******************************************************************        
                                                                                
           WRITE BP13KD61-REC.                                                  
           IF KD61-STATUS = 00 OR 02                                            
              ADD 1    TO WS-KD61-WRITE                                         
              MOVE 'Y' TO WS-WRITE-KD61                                         
           ELSE                                                                 
              IF KD61-STATUS = 22                                               
                 PERFORM 5240-SERIAL-PROCESS  THRU 5240-EXIT                    
              ELSE                                                              
                 DISPLAY ' BP13KD61-WRITING ERROR, STATUS: '                    
                         KD61-STATUS                                            
                 MOVE KD61-STATUS           TO   RETURN-CODE                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5230-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5240-SERIAL-PROCESS.                                                     
      ******************************************************************        
                                                                                
           MOVE KD61-NUM-SEQ           TO   WS-SER-CHR.                         
           IF WS-NUM-SER IS NOT NUMERIC                                         
              MOVE ZEROES              TO   WS-NUM-SER                          
           END-IF.                                                              
                                                                                
           ADD +1                      TO   WS-NUM-SER.                         
           MOVE WS-NUM-SER             TO   KD61-NUM-SEQ.                       
                                                                                
       5240-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5250-DELETE-BP13KD60.                                                    
      *---------------------------------------------------------------*         
           DELETE BP13KD60.                                                     
                                                                                
           EVALUATE KD60-STATUS                                                 
              WHEN ZERO                                                         
                ADD 1   TO WS-KD60-DELETE                                       
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN BP13KD60 ' KD60-NUM-REGN           
              WHEN OTHER                                                        
                DISPLAY 'DELETE ERROR, KD60-STATUS ' KD60-STATUS                
                DISPLAY ',KEY IS ' KD60-NUM-REGN                                
           END-EVALUATE.                                                        
                                                                                
       5250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-REWRITE-BP13KD00.                                                   
      *---------------------------------------------------------------*         
           REWRITE BP13KD00-REC.                                                
                                                                                
           EVALUATE KD00-STATUS                                                 
              WHEN ZERO                                                         
                ADD 1   TO WS-KD00-UPDATE                                       
              WHEN 23                                                           
                DISPLAY 'RECORD NOT FOUND IN BP13KD00 ' KD00-NUM-REGN           
              WHEN OTHER                                                        
                DISPLAY 'REWRITE ERROR, KD00-STATUS ' KD00-STATUS               
                DISPLAY ',KEY IS ' KD00-NUM-REGN                                
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7000-WRITE-BP13F136.                                                     
      *---------------------------------------------------------------*         
                                                                                
           IF KD00-AMT-LOAN-PAID IS NOT NUMERIC                                 
              MOVE ZEROS TO KD00-AMT-LOAN-PAID.                                 
                                                                                
           IF KD00-AMT-LOAN-PD-DEVLP-TOT IS NOT NUMERIC                         
              MOVE ZEROS TO KD00-AMT-LOAN-PD-DEVLP-TOT.                         
                                                                                
           IF KD00-AMT-PP-LUMPSUM-PAID IS NOT NUMERIC                           
              MOVE ZEROS TO KD00-AMT-PP-LUMPSUM-PAID.                           
                                                                                
           IF KD00-AMT-PP-PD-DEVLP-TOT IS NOT NUMERIC                           
              MOVE ZEROS TO KD00-AMT-PP-PD-DEVLP-TOT.                           
                                                                                
           IF (KD00-AMT-LOAN-PD-DEVLP-TOT > ZEROES OR                           
               KD00-AMT-LOAN-PAID > ZEROES) AND                                 
              (KD00-AMT-LOAN-PD-DEVLP-TOT NOT =                                 
               KD00-AMT-LOAN-PAID)                                              
              PERFORM 7100-WRITE-F136-INS71 THRU 7100-EXIT                      
           END-IF.                                                              
                                                                                
           IF (KD00-AMT-PP-PD-DEVLP-TOT > ZEROES OR                             
               KD00-AMT-PP-LUMPSUM-PAID > ZEROES) AND                           
              (KD00-AMT-PP-PD-DEVLP-TOT NOT =                                   
               KD00-AMT-PP-LUMPSUM-PAID)                                        
              PERFORM 7200-WRITE-F136-PP71  THRU 7200-EXIT                      
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       7100-WRITE-F136-INS71.                                                   
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES                   TO BP13F136-CASHTRAN.                  
           INITIALIZE                       BP13F136-CASHTRAN.                  
                                                                                
           MOVE '71'                     TO F136-CDE-TRANS-TYPE.                
           MOVE 'INS'                    TO F136-CDE-PAYMENT-TYPE.              
           MOVE WS-PROCESS-DATE          TO F136-DTE-POST                       
                                            F136-DTE-TRANS.                     
           MOVE 'D'                      TO F136-CDE-CRDR.                      
                                                                                
           MOVE KD00-CDE-LOAN-SCH        TO F136-NUM-LOAN-SCH.                  
           MOVE KD00-NUM-REGN            TO F136-NUM-REGN.                      
           MOVE KD00-NUM-SCH-ACCNT       TO F136-NUM-SCH-ACCT.                  
           COMPUTE F136-AMT-RECEIPT = KD00-AMT-LOAN-PD-DEVLP-TOT -              
                                      KD00-AMT-LOAN-PAID.                       
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1                         TO WS-F136-WRITE-INS.                  
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       7200-WRITE-F136-PP71.                                                    
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES                   TO BP13F136-CASHTRAN.                  
           INITIALIZE                       BP13F136-CASHTRAN.                  
                                                                                
           MOVE '71'                     TO F136-CDE-TRANS-TYPE.                
           MOVE 'PP '                    TO F136-CDE-PAYMENT-TYPE.              
           MOVE WS-PROCESS-DATE          TO F136-DTE-POST                       
                                            F136-DTE-TRANS.                     
           MOVE 'D'                      TO F136-CDE-CRDR.                      
                                                                                
           MOVE KD00-CDE-LOAN-SCH        TO F136-NUM-LOAN-SCH.                  
           MOVE KD00-NUM-REGN            TO F136-NUM-REGN.                      
           MOVE KD00-NUM-SCH-ACCNT       TO F136-NUM-SCH-ACCT.                  
           COMPUTE F136-AMT-RECEIPT = KD00-AMT-PP-PD-DEVLP-TOT -                
                                      KD00-AMT-PP-LUMPSUM-PAID.                 
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1                         TO WS-F136-WRITE-PP.                   
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       8000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
                                                                                
           ADD 2                              TO WS-LC98-REC-CNT                
           IF WS-LC98-REC-CNT > 60                                              
              PERFORM 8100-PRINT-HEADER     THRU 8100-EXIT                      
              ADD 2                           TO WS-LC98-REC-CNT                
           END-IF.                                                              
                                                                                
           MOVE SPACES                        TO LC98-DETAIL.                   
           INITIALIZE                            LC98-DETAIL.                   
                                                                                
           ADD 1                              TO WS-LC98-SNO                    
           MOVE WS-LC98-SNO                   TO LC98-SNO                       
           MOVE KD00-NUM-REGN                 TO LC98-NUM-REGN                  
           STRING KD00-NUM-SCH-ACCNT(1:4) '-'                                   
                  KD00-NUM-SCH-ACCNT(5:4) '-'                                   
                  KD00-NUM-SCH-ACCNT(9:1) '-'                                   
                  KD00-NUM-SCH-ACCNT(10:2)                                      
                  DELIMITED BY SIZE INTO LC98-SCH-ACC                           
           END-STRING.                                                          
           IF KD00-DTE-REPUDIATE NOT = SPACES AND LOW-VALUES AND ZEROES         
              STRING KD00-DTE-REPUDIATE(7:2) '/'                                
                     KD00-DTE-REPUDIATE(5:2) '/'                                
                     KD00-DTE-REPUDIATE(1:4)                                    
                     DELIMITED BY SIZE INTO LC98-DTE-REPUDIATE                  
              END-STRING                                                        
           END-IF                                                               
           IF KD00-DTE-EFF-TRANSFER NOT = SPACES AND LOW-VALUES                 
              STRING KD00-DTE-EFF-TRANSFER(7:2) '/'                             
                     KD00-DTE-EFF-TRANSFER(5:2) '/'                             
                     KD00-DTE-EFF-TRANSFER(1:4)                                 
                     DELIMITED BY SIZE INTO LC98-DTE-EFF-TRANSFER               
              END-STRING                                                        
           END-IF.                                                              
           IF KD00-DTE-LOAN-COMM NOT = SPACES AND LOW-VALUES                    
              STRING KD00-DTE-LOAN-COMM(7:2) '/'                                
                     KD00-DTE-LOAN-COMM(5:2) '/'                                
                     KD00-DTE-LOAN-COMM(1:4)                                    
                     DELIMITED BY SIZE INTO LC98-DTE-LOAN-COMM                  
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           WRITE BP13LC98-REC FROM LC98-DETAIL AFTER 2.                         
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       8100-PRINT-HEADER.                                                       
      *---------------------------------------------------------------*         
                                                                                
           ADD 1                              TO WS-LC98-PAGE-CNT               
           MOVE WS-LC98-PAGE-CNT              TO LC98-PAGE-NO                   
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LC98-DTE-CURRENT                       
           END-STRING                                                           
                                                                                
           MOVE SPACES                        TO BP13LC98-REC                   
           WRITE BP13LC98-REC AFTER PAGE.                                       
           WRITE BP13LC98-REC FROM LC98-HEADER-1 AFTER 3.                       
           WRITE BP13LC98-REC FROM LC98-HEADER-2.                               
           WRITE BP13LC98-REC FROM LC98-HEADER-3 AFTER 2.                       
           WRITE BP13LC98-REC FROM LC98-HEADER-4.                               
           MOVE 8                             TO WS-LC98-REC-CNT.               
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
           CLOSE BP13FD00                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 BP13KD60                                                       
                 BP13KD61                                                       
                 BP13KD63                                                       
                 BP13KD64                                                       
                 BP13F136                                                       
                 BP13LC98.                                                      
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, KD00-STATUS ' KD00-STATUS                   
           END-IF.                                                              
                                                                                
           IF KD03-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, KD03-STATUS ' KD03-STATUS                   
           END-IF.                                                              
                                                                                
           IF KD60-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, KD60-STATUS ' KD60-STATUS                   
           END-IF.                                                              
                                                                                
           IF KD61-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, KD61-STATUS ' KD61-STATUS                   
           END-IF.                                                              
                                                                                
           IF KD63-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, KD63-STATUS ' KD63-STATUS                   
           END-IF.                                                              
                                                                                
           IF KD64-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, KD64-STATUS ' KD64-STATUS                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CC98 ================*'.              
           DISPLAY '   NO. OF FD00 RECORDS READ    = ' WS-FD00-READ.            
           DISPLAY '   NO. OF KD00 RECORDS READ    = ' WS-KD00-READ.            
           DISPLAY '   NO. OF KD03 RECORDS WRITTEN = ' WS-KD03-WRITE.           
           DISPLAY '   NO. OF KD00 RECORDS UPDATED = ' WS-KD00-UPDATE.          
           DISPLAY '   NO. OF KD00 RECORDS DELETED = ' WS-KD00-DELETE.          
           DISPLAY '   NO. OF KD63 RECORDS READ    = ' WS-KD63-READ.            
           DISPLAY '   NO. OF KD64 RECORDS WRIITEN = ' WS-KD64-WRITE.           
           DISPLAY '   NO. OF KD60 RECORDS READ    = ' WS-KD60-READ.            
           DISPLAY '   NO. OF KD61 RECORDS WRITTEN = ' WS-KD61-WRITE.           
           DISPLAY '   NO. OF KD60 RECORDS DELETED = ' WS-KD60-DELETE.          
           DISPLAY '   NO. OF F136 RECORDS WRITTEN (INS) = '                    
                                                   WS-F136-WRITE-INS.           
           DISPLAY '   NO. OF F136 RECORDS WRITTEN (PP ) = '                    
                                                   WS-F136-WRITE-PP.            
           DISPLAY '   NO. OF RECORDS PRINTED IN REPORT  = '                    
                                                   WS-LC98-SNO.                 
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
