      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C261.                                                 
       AUTHOR.        GD.                                                       
       DATE-WRITTEN.  20/06/95.                                                 
      *================================================================*        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT   (SOC)                  *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS THE PROGRAM TO PRINT REPORT ON    *        
      *                      K110 RECORDS WITH DIFFERENT PREVIOUS      *        
      *                      AND CURRENT SELLING PRICES.               *        
      *                                                                *        
      *   INPUT FILES :  1.  BM06K110                                  *        
      *                  2.  BM06F190                                  *        
      *                  3.  BM06K510                                  *        
      *                                                                *        
      *   LISTINGS    :  1.  P13L261A                                  *        
      *                                                                *        
      *   CHGE-NO   DATE   BY   DESCRIPTION                            *        
      *   -------  ------  ---  -------------------------------------  *        
      *   C9504B6  200695  GD   OCS (CREATED).                         *        
      *   C9504B6  070995  LMS  TO CHANGE PREV-SELLING-PRICE TO        *        
      *                         1ST-OFFER-SELLING-PRICE.               *        
      *   BP130418 030398  SIA  CHANGED BM06F900 TO BM06F190  &        *        
      *                         CHANGED BM06K211 TO BM06K110  &        *        
      *================================================================*        
                                                                                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F190  ASSIGN        TO BM06F190.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                  ORGANIZATION   INDEXED                                        
                  ACCESS         DYNAMIC                                        
                  RECORD KEY     K110-KEY-FLD                                   
                  ALTERNATE KEY  K110-AIX1 WITH DUPLICATES                      
                  FILE STATUS    WS-K110-STATUS.                                
           SELECT BM06K510  ASSIGN        TO BM06K510                           
                  ORGANIZATION   INDEXED                                        
                  ACCESS         RANDOM                                         
                  RECORD KEY     K510-ESTCDE                                    
                  FILE STATUS    WS-K510-STATUS.                                
           SELECT P13L261A  ASSIGN        TO P13L261A.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BM06F190                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 650 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD.                                         
       COPY BM06F190.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS 60 CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD   P13L261A                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   OMITTED                                             
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS.                                     
                                                                                
       01   PRINT-REC                  PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-F190                      PIC X(1)  VALUE 'N'.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-PAGE-CNT                      PIC 9(4)  VALUE ZEROES.         
           05  WS-REC-CNT                       PIC 9(8)  VALUE ZEROES.         
           05  WS-READ-F190-CNT                 PIC 9(5)  VALUE ZEROES.         
                                                                                
       01  WS-CURR-DATE                         PIC X(8)  VALUE SPACES.         
                                                                                
       01  WS-MISC.                                                             
           05  WS-K110-STATUS                   PIC X(2).                       
           05  WS-K510-STATUS                   PIC X(2).                       
           05  WS-F190-READ-CTR                 PIC 9(8)  VALUE 0.              
           05  WS-K110-READ-CTR                 PIC 9(8)  VALUE 0.              
           05  WS-SERNO                         PIC 9(4)  VALUE 0.              
           05  WS-SPACING                       PIC 9(4)  VALUE 2.              
           05  WS-FIRST-DETAIL-SW               PIC X(1).                       
           05  WS-F190-EST-NEIGH-CONT-BLK       PIC X(16).                      
                                                                                
      *----------------------------------------------------------------*        
      *    P13L261A HEADER                                             *        
      *----------------------------------------------------------------*        
       01  L261-HDG1.                                                           
           05  PRT-REPORT-TITLE         PIC X(9)  VALUE 'BP13L261'.             
           05  FILLER                   PIC X(37) VALUE SPACES.                 
           05  FILLER                   PIC X(40) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(29)  VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'DATE :'.              
           05  PRT-DATE                 PIC X(10).                              
                                                                                
       01  L261-HDG2.                                                           
           05  FILLER                   PIC X(30)  VALUE SPACES.                
           05  FILLER                   PIC X(36)  VALUE                        
               'REPORT ON FIS RECORDS WITH DIFFERENT'.                          
           05  FILLER                   PIC X(1)   VALUE SPACE.                 
           05  FILLER                   PIC X(35)  VALUE                        
               'PREVIOUS AND CURRENT SELLING PRICES'.                           
           05  FILLER                   PIC X(15)  VALUE SPACES.                
           05  FILLER                   PIC X(7)   VALUE 'PAGE : '.             
           05  PRT-PAGE                 PIC ZZZ9.                               
                                                                                
       01  L261-HDG3.                                                           
           05  FILLER                   PIC X(105) VALUE SPACES.                
           05  FILLER                   PIC X(13)  VALUE                        
               'SELLING PRICE'.                                                 
                                                                                
       01  L261-HDG4.                                                           
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(27) VALUE 'ESTATE'.                
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'NEIGH'.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'CONT'.                  
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE ' BLK '.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE 'SCHEME ACCT NO'.        
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE ' PREVIOUS '.            
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE ' CURRENT  '.            
                                                                                
       01  L261-HDG5.                                                           
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE ALL '-'.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(27) VALUE ALL '-'.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE ALL '-'.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE ALL '-'.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE ALL '-'.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE ALL '-'.                 
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(23) VALUE ALL '-'.                 
                                                                                
      *----------------------------------------------------------------*        
      *   DETAIL RECORD                                                *        
      *----------------------------------------------------------------*        
       01  L261-DTL1.                                                           
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-S-NO                 PIC ZZZZ.                               
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-ESTATE               PIC X(27).                              
           05  FILLER                   PIC X(7)  VALUE SPACES.                 
           05  PRT-NEIGH                PIC X(4).                               
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-CONT                 PIC X(4).                               
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-BLK                  PIC X(5).                               
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  FILLER.                                                          
               10  PRT-SCHEME           PIC X(4).                               
               10  FILLER               PIC X(1)  VALUE '-'.                    
               10  PRT-ACC              PIC X(4).                               
               10  FILLER               PIC X(1)  VALUE '-'.                    
               10  PRT-CHK              PIC X(1).                               
               10  FILLER               PIC X(1)  VALUE '-'.                    
               10  PRT-ACC-TYPE         PIC X(2).                               
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-1ST-OFFER-SELLING-PRICE PIC ZZ,ZZZ,ZZ9.                      
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-CURR-SELLING-PRICE   PIC ZZ,ZZZ,ZZ9.                         
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ******************************************************************        
       000-MAIN.                                                                
      ******************************************************************        
                                                                                
           PERFORM   100-OPEN-ROUTINE      THRU    100-EXIT.                    
                                                                                
           PERFORM   200-READ-F190         THRU    200-EXIT.                    
                                                                                
           PERFORM   300-PROCESS-F190      THRU    300-EXIT                     
                         UNTIL WS-EOF-F190 = 'Y'.                               
                                                                                
           PERFORM   999-CLOSE-ROUTINE     THRU    999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           OPEN INPUT BM06F190                                                  
                      BM06K110                                                  
                      BM06K510                                                  
               OUTPUT P13L261A.                                                 
                                                                                
           IF WS-K110-STATUS NOT = '00' AND '97'                                
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              DISPLAY 'BM06K110 OPEN ERROR : ' WS-K110-STATUS                   
              PERFORM 999-CLOSE-ROUTINE   THRU  999-EXIT.                       
                                                                                
           IF WS-K510-STATUS NOT = '00' AND '97'                                
              MOVE WS-K510-STATUS TO RETURN-CODE                                
              DISPLAY 'BM06K510 OPEN ERROR : ' WS-K510-STATUS                   
              PERFORM 999-CLOSE-ROUTINE   THRU  999-EXIT.                       
                                                                                
           MOVE ZEROES TO  WS-COUNTERS.                                         
           MOVE 50     TO  WS-REC-CNT                                           
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING  WS-CURR-DATE (7:2) '/'                                       
                   WS-CURR-DATE (5:2) '/'                                       
                   WS-CURR-DATE (1:4)                                           
                   DELIMITED BY SIZE   INTO   PRT-DATE.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F190.                                                           
      ******************************************************************        
                                                                                
           READ BM06F190                                                        
                AT END MOVE  'Y'  TO  WS-EOF-F190                               
                       GO TO 200-EXIT                                           
           END-READ.                                                            
                                                                                
           ADD  +1               TO  WS-F190-READ-CTR.                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F190.                                                        
      ******************************************************************        
                                                                                
           MOVE    'Y'                     TO   WS-FIRST-DETAIL-SW.             
                                                                                
           PERFORM 310-STARTBR-K110        THRU 310-EXIT.                       
                                                                                
           PERFORM 320-READNEXT-K110       THRU 320-EXIT                        
             UNTIL K110-AIX1 NOT = WS-F190-EST-NEIGH-CONT-BLK.                  
                                                                                
           PERFORM 200-READ-F190          THRU  200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       310-STARTBR-K110.                                                        
      ******************************************************************        
                                                                                
           MOVE SPACES                  TO   K110-REC.                          
                                                                                
           MOVE F190-2-ESTATE           TO   K110-ESTATE.                       
           MOVE F190-2-NEIGHBOURHOOD    TO   K110-NEIGHBOURHOOD.                
           MOVE F190-2-CONTRACT-NO      TO   K110-CONTRACT-NO.                  
           MOVE F190-2-BLK-NO           TO   K110-BLK-NO.                       
           MOVE K110-AIX1               TO   WS-F190-EST-NEIGH-CONT-BLK.        
                                                                                
           START BM06K110    KEY IS >= K110-AIX1.                               
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN ZEROS                                                           
           WHEN 02                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE    HIGH-VALUES      TO  WS-F190-EST-NEIGH-CONT-BLK           
              DISPLAY 'BM06K110 RECORD NOT FOUND, KEY IS ' K110-AIX1            
           WHEN OTHER                                                           
              DISPLAY 'BM06K110 READ ERROR, STATUS IS ' WS-K110-STATUS          
              DISPLAY 'BM06K110 RECORD KEY IS ' K110-AIX1                       
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       320-READNEXT-K110.                                                       
      ******************************************************************        
                                                                                
           READ BM06K110 NEXT RECORD.                                           
           EVALUATE WS-K110-STATUS                                              
           WHEN ZEROS                                                           
           WHEN 02                                                              
              CONTINUE                                                          
           WHEN 10                                                              
              MOVE    HIGH-VALUES      TO   WS-F190-EST-NEIGH-CONT-BLK          
           WHEN OTHER                                                           
              DISPLAY 'BM06K110 READ ERROR, STATUS IS ' WS-K110-STATUS          
              DISPLAY 'BM06K110 RECORD KEY IS ' K110-AIX1                       
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           IF K110-AIX1 NOT = WS-F190-EST-NEIGH-CONT-BLK                        
              GO TO 320-EXIT                                                    
           END-IF.                                                              
                                                                                
           ADD   +1                    TO   WS-K110-READ-CTR.                   
                                                                                
           IF K110-1ST-OFFER-SELLING-PRICE NOT = K110-CURR-SELLING-PRICE        
              PERFORM 400-PRINT-L261   THRU 400-EXIT                            
           END-IF.                                                              
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-PRINT-L261.                                                          
      ******************************************************************        
                                                                                
           IF WS-REC-CNT > 40                                                   
              PERFORM 500-PRINT-HEADER  THRU  500-EXIT.                         
                                                                                
                                                                                
           IF WS-FIRST-DETAIL-SW = 'Y'                                          
              PERFORM 700-READ-BM06K510 THRU  700-EXIT                          
              ADD    +1                 TO    WS-SERNO                          
              MOVE   WS-SERNO           TO    PRT-S-NO                          
              MOVE   K510-ESTATE-NAME   TO    PRT-ESTATE                        
              MOVE   K110-NEIGHBOURHOOD TO    PRT-NEIGH                         
              MOVE   K110-CONTRACT-NO   TO    PRT-CONT                          
              MOVE   K110-BLK-NO        TO    PRT-BLK                           
              MOVE   SPACES             TO    WS-FIRST-DETAIL-SW                
              MOVE   2                  TO    WS-SPACING                        
           ELSE                                                                 
              MOVE   ZEROS              TO    PRT-S-NO                          
              MOVE   SPACES             TO    PRT-ESTATE                        
                                              PRT-NEIGH                         
                                              PRT-CONT                          
                                              PRT-BLK                           
              MOVE   1                  TO    WS-SPACING                        
              IF WS-REC-CNT = 0                                                 
                 MOVE   K510-ESTATE-NAME   TO    PRT-ESTATE                     
                 MOVE   K110-NEIGHBOURHOOD TO    PRT-NEIGH                      
                 MOVE   K110-CONTRACT-NO   TO    PRT-CONT                       
                 MOVE   K110-BLK-NO        TO    PRT-BLK                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE   K110-SCHEME           TO     PRT-SCHEME.                      
           MOVE   K110-ACC              TO     PRT-ACC.                         
           MOVE   K110-CHK              TO     PRT-CHK.                         
           MOVE   K110-ACC-TYPE         TO     PRT-ACC-TYPE.                    
                                                                                
           MOVE   K110-1ST-OFFER-SELLING-PRICE                                  
             TO   PRT-1ST-OFFER-SELLING-PRICE.                                  
           MOVE   K110-CURR-SELLING-PRICE TO   PRT-CURR-SELLING-PRICE.          
                                                                                
           WRITE  PRINT-REC        FROM   L261-DTL1 AFTER WS-SPACING.           
           ADD    WS-SPACING              TO   WS-REC-CNT.                      
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-PRINT-HEADER.                                                        
      ******************************************************************        
                                                                                
           ADD    1                   TO     WS-PAGE-CNT.                       
           MOVE   WS-PAGE-CNT         TO     PRT-PAGE.                          
           WRITE  PRINT-REC   FROM L261-HDG1   AFTER PAGE.                      
           WRITE  PRINT-REC   FROM L261-HDG2   AFTER 1.                         
           WRITE  PRINT-REC   FROM L261-HDG3   AFTER 2.                         
           WRITE  PRINT-REC   FROM L261-HDG4   AFTER 1.                         
           WRITE  PRINT-REC   FROM L261-HDG5   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC.                                                    
           MOVE   ZEROES      TO   WS-REC-CNT.                                  
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-READ-BM06K510.                                                       
      ******************************************************************        
                                                                                
           MOVE   SPACES              TO     K510-REC.                          
           MOVE   K110-ESTATE         TO     K510-ESTCDE.                       
                                                                                
           READ   BM06K510        KEY IS     K510-ESTCDE.                       
           EVALUATE WS-K510-STATUS                                              
           WHEN ZEROS                                                           
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE 'NO RECORD FOUND ' TO     K510-ESTATE-NAME                   
           WHEN OTHER                                                           
              DISPLAY 'BM06K510 READ ERROR, STATUS IS ' WS-K510-STATUS          
              DISPLAY 'BM06K510 RECORD KEY IS ' K510-ESTCDE                     
              MOVE WS-K510-STATUS TO RETURN-CODE                                
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           CLOSE BM06K110                                                       
                 BM06K510                                                       
                 BM06F190                                                       
                 P13L261A.                                                      
                                                                                
           IF WS-K110-STATUS NOT = '00' AND '97'                                
              DISPLAY 'BM06K110 CLOSE ERROR : ' WS-K110-STATUS                  
              MOVE WS-K110-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K510-STATUS NOT = '00' AND '97'                                
              DISPLAY 'BM06K510 CLOSE ERROR : ' WS-K510-STATUS                  
              MOVE WS-K510-STATUS TO RETURN-CODE.                               
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*********  BP13C261 CONTROL TOTALS  **********'             
           DISPLAY SPACES.                                                      
           DISPLAY 'BM06F190 RECORDS READ      = ' WS-F190-READ-CTR.            
           DISPLAY 'BM06K110 RECORDS READ      = ' WS-K110-READ-CTR.            
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
