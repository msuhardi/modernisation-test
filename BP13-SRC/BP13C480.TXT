       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C480.                                                 
      *AUTHOR.        S B LIAN.                                                 
      *DATE-WRITTEN.  15/07/85.                                                 
      * ========================================================= *             
      *                                                           *             
      *    SYSTEM NAME : SYSTEM OF COMMITMENT.                    *             
      *                                                           *             
      *    SYSTEM ID : BP13                                       *             
      *                                                           *             
      *    OBJECTIVE  :                                           *             
      *    1.   THIS IS THE PROGRAM TO PROVIDE REPORT ON RESETTLE *             
      *         -MENT CASES FROM FIS ON A MONTH TO MONTH BASIS :  *             
      *         A. ACCEPTED FLATS IN SELECTION BY ORDER OF REGI-  *             
      *            STRATION NO AND GIVE DATE OF ACCEPTANCE        *             
      *         B. SIGNED SALES AGREEMENT AND ISSUED KEYS BY ORDER*             
      *            OF REGISTRATION NO AND GIVE KEY ISSUED DATE    *             
      *         C. REJECTED AFTER ACCEPTING FLAT.                 *             
      *                                                           *             
      *     INPUT FILES:                                          *             
      *     1. BM06F320 (RESETTLEMENT FILE)                       *             
      *                                                           *             
      *     LISTING:                                              *             
      *     1. DETAIL REPORT (M6L480)                             *             
      *                                                           *             
      * CHG REQ  OIC  DATE   DESC                                 *             
      * =======  === ======  ====                                 *             
      *          LMS 120593  TO CHANGE F320 TO F220.              *             
      * BP130015 SSS 130995  CONVERT TO LASER PRINT REPORT        *             
      *                      CHG REPORT-ID POSITION TO COL 1      *             
      * BP130418 RHB 250898  TO CATER FOR Y2000.                  *             
      * ========================================================= *             
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F220 ASSIGN TO BP13F220.                                  
           SELECT BP13L480 ASSIGN TO BP13L480.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F220                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F220.                                                           
                                                                                
       FD  BP13L480                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC-1                 PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  NO-F220-READ            PIC 9(6)    VALUE ZEROS.                     
       01  WS-NUMBER               PIC 9(2)    VALUE 99.                        
       01  NO-OF-PAGE              PIC 9(5)    VALUE 0.                         
       01  WS-MONTH                PIC X(10)   VALUE SPACES.                    
       01  PRE-REGN-NO             PIC X(8)    VALUE SPACES.                    
       01  NO-ACCEPT-SELECTION     PIC 9(6)    VALUE 0.                         
       01  NO-REJECT-SELECTION     PIC 9(6)    VALUE 0.                         
       01  NO-KEY-ISSUE            PIC 9(6)    VALUE 0.                         
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF              PIC X(2)    VALUE 'N'.                       
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDIT-DD          PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDIT-MM          PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDIT-CC          PIC 99.                                      
           05  WS-EDIT-YY          PIC 99.                                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-MM           PIC 99.                                      
           05  FILLER              PIC X.                                       
           05  WS-CUR-DD           PIC 99.                                      
           05  FILLER              PIC X.                                       
           05  WS-CUR-CC           PIC 99.                                      
           05  WS-CUR-YY           PIC 99.                                      
                                                                                
       01  WS-CURR-CCYYMMDD.                                                    
           05  WS-CURR-CC          PIC 99.                                      
           05  WS-CURR-YY          PIC 99.                                      
           05  WS-CURR-MM          PIC 99.                                      
           05  WS-CURR-DD          PIC 99.                                      
                                                                                
       01  WS-F220-DATE.                                                        
           05  WS-CC               PIC 99.                                      
           05  WS-YY               PIC 99.                                      
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  L480-PR-HEAD-1.                                                      
           05  FILLER              PIC X(8)      VALUE 'BP13L480'.              
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(23)     VALUE SPACES.                  
           05  FILLER              PIC X(99)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE : '.               
           05  120A1-DATE          PIC X(10).                                   
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  120A1-PAGE          PIC ZZZZ9.                                   
                                                                                
       01  L480-PR-HEAD-1A.                                                     
           05  FILLER              PIC X(59)     VALUE SPACES.                  
           05  FILLER              PIC X(15)     VALUE                          
               'ALLOCATION TEAM'.                                               
           05  FILLER              PIC X(58)     VALUE SPACES.                  
                                                                                
       01  L480-PR-HEAD-2.                                                      
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(57)     VALUE                          
           'APPLICATIONS UNDER RESETTLEMENT SCHEME ACCEPTED/REJECTED'.          
           05  FILLER              PIC X(11)     VALUE                          
           'FLATS'.                                                             
           05  FILLER              PIC X(25)     VALUE 'SPACES'.                
                                                                                
       01  L480-PR-HEAD-3.                                                      
           05  FILLER              PIC X(47)     VALUE SPACES.                  
           05  FILLER              PIC X(13)     VALUE                          
           'FOR MONTH OF '.                                                     
           05  120A1-MONTH         PIC X(10).                                   
           05  FILLER              PIC X(90)     VALUE SPACES.                  
                                                                                
       01  L480-PR-HEAD-4.                                                      
           05  FILLER              PIC X(48)     VALUE                          
           ' CURRENT  APPLICANT/                       BLOCK'.                  
           05  FILLER              PIC X(51)     VALUE                          
           '  STREET NAME          HOUSE-NO    ROOM DATE ACCEPT'.               
           05  FILLER              PIC X(33)     VALUE                          
           ' FIS       STATUS       KEY ISSUE'.                                 
                                                                                
       01  L480-PR-HEAD-5.                                                      
           05  FILLER              PIC X(83)     VALUE                          
           ' REGN-NO  JOINT-APPLICANT'.                                         
           05  FILLER              PIC X(27)     VALUE                          
           'TYPE   /REJECT   AL-SCH '.                                          
           05  FILLER              PIC X(19)     VALUE                          
           '               DATE'.                                               
                                                                                
       01  L480-PR-HEAD-6.                                                      
           05  FILLER              PIC X         VALUE SPACES.                  
           05  120A1-REGN-NO       PIC X(8).                                    
           05  FILLER              PIC X         VALUE SPACE.                   
           05  120A1-APPLN-NAME    PIC X(32).                                   
           05  FILLER              PIC X         VALUE SPACE.                   
           05  120A1-BLK-NO        PIC X(5).                                    
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  120A1-STREET-NAME   PIC X(20).                                   
           05  FILLER              PIC X(1)      VALUE SPACES.                  
           05  120A1-LEVEL-NO      PIC X(2).                                    
           05  FILLER              PIC X(1)      VALUE '-'.                     
           05  120A1-MAIN-UNIT     PIC X(4).                                    
           05  FILLER              PIC X(1)      VALUE SPACES.                  
           05  120A1-SUB-UNIT      PIC X(3).                                    
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  120A1-RM-TYPE       PIC X(2).                                    
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  120A1-ACCEPT-DATE   PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  120A1-ALLOC-SCH     PIC X(3).                                    
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  120A1-STATUS        PIC X(16).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  120A1-KEY-ISSUED-D  PIC X(10).                                   
                                                                                
       01  L480-PR-HEAD-7.                                                      
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  120A1-COAPPLN-NAME  PIC X(32).                                   
           05  FILLER              PIC X(88)     VALUE SPACES.                  
                                                                                
       01  L480-PR-HEAD-9.                                                      
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(28)     VALUE                          
               'NUMBER OF F220 READ       = '.                                  
           05  120A1-F220-READ     PIC ZZZZZ9.                                  
           05  FILLER              PIC X(90)     VALUE SPACES.                  
                                                                                
       01  L480-PR-HEAD-10.                                                     
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(28)     VALUE                          
               'NUMBER OF KEY ISSUED      = '.                                  
           05  120A1-KEY-ISSUE     PIC ZZZZZ9.                                  
           05  FILLER              PIC X(90)     VALUE SPACES.                  
                                                                                
       01  L480-PR-HEAD-11.                                                     
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(28)     VALUE                          
               'NO ACCEPTED IN SELECTION  = '.                                  
           05  120A1-ACC-SELECTION PIC ZZZZZ9.                                  
           05  FILLER              PIC X(90)     VALUE SPACES.                  
                                                                                
       01  L480-PR-HEAD-13.                                                     
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(28)     VALUE                          
               'NO REJECTED IN SELECTION  = '.                                  
           05  120A1-REJ-SELECTION PIC ZZZZZ9.                                  
           05  FILLER              PIC X(90)     VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       MAIN.                                                                    
           OPEN INPUT BP13F220                                                  
               OUTPUT BP13L480.                                                 
           PERFORM 200-READ-DATA THRU 200-EXIT.                                 
           PERFORM 300-PROCESS-REPORT THRU 300-EXIT                             
                                    UNTIL WS-EOF EQUAL TO 'Y'.                  
           PERFORM 800-PRINT-CONTROL-LIST.                                      
           PERFORM 1400-CLOSE-ROUTINE.                                          
                                                                                
       200-READ-DATA.                                                           
            READ BP13F220 AT END MOVE 'Y' TO WS-EOF                             
                        GO TO 200-EXIT.                                         
            ADD 1 TO NO-F220-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *   PRINT REPORT FOR THOSE APPLICANTS UNDER RESETTLEMENT *                
      *   SCHEME ACCEPTED OR REJECTED FLATS FOR THAT MONTH.    *                
      *                                                        *                
      **********************************************************                
                                                                                
       300-PROCESS-REPORT.                                                      
            PERFORM 400-PROCESS-PRINT.                                          
            PERFORM 200-READ-DATA THRU 200-EXIT.                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PROCESS-PRINT.                                                       
            IF WS-NUMBER > 46                                                   
                PERFORM 500-PRINT-HEADING.                                      
            PERFORM 700-PRINT-DETAIL.                                           
                                                                                
       500-PRINT-HEADING.                                                       
            ADD 1 TO NO-OF-PAGE.                                                
            MOVE NO-OF-PAGE TO 120A1-PAGE.                                      
            MOVE FUNCTION CURRENT-DATE TO WS-CURR-CCYYMMDD.                     
            MOVE WS-CURR-CC          TO WS-CUR-CC  WS-EDIT-CC.                  
            MOVE WS-CURR-YY          TO WS-CUR-YY  WS-EDIT-YY.                  
            MOVE WS-CURR-MM          TO WS-CUR-MM  WS-EDIT-MM.                  
            MOVE WS-CURR-DD          TO WS-CUR-DD  WS-EDIT-DD.                  
            MOVE WS-EDITED-DATE      TO 120A1-DATE.                             
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-1 AFTER PAGE.                   
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-1A AFTER 1.                     
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-2 AFTER 1.                      
            PERFORM 600-PRINT-MONTH.                                            
            MOVE WS-MONTH TO 120A1-MONTH.                                       
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-3 AFTER 1.                      
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-4 AFTER 3.                      
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-5 AFTER 1.                      
            MOVE 0 TO WS-NUMBER.                                                
                                                                                
       600-PRINT-MONTH.                                                         
            IF WS-CUR-MM       = 01                                             
                MOVE 'JANUARY ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 02                                             
                MOVE 'FEBRUARY ' TO WS-MONTH                                    
            ELSE IF WS-CUR-MM  = 03                                             
                MOVE 'MARCH   ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 04                                             
                MOVE 'APRIL   ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 05                                             
                MOVE 'MAY     ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 06                                             
                MOVE 'JUNE    ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 07                                             
                MOVE 'JULY    ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 08                                             
                MOVE 'AUGUST  ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 09                                             
                MOVE 'SEPTEMBER' TO WS-MONTH                                    
            ELSE IF WS-CUR-MM  = 10                                             
                MOVE 'OCTOBER ' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 11                                             
                MOVE 'NOVEMBER' TO WS-MONTH                                     
            ELSE IF WS-CUR-MM  = 12                                             
                MOVE 'DECEMBER' TO WS-MONTH.                                    
                                                                                
       700-PRINT-DETAIL.                                                        
            IF F220-NUM-REGN NOT = PRE-REGN-NO                                  
               MOVE SPACES TO PRINT-REC-1                                       
               WRITE PRINT-REC-1 AFTER 1                                        
               ADD 1 TO WS-NUMBER.                                              
            IF F220-ALLOC-STATUS = 'AS ' OR 'AW ' OR 'AI ' OR 'AA '             
               MOVE 'ACC IN SELECTION' TO 120A1-STATUS                          
               ADD 1 TO NO-ACCEPT-SELECTION                                     
            ELSE                                                                
            IF F220-ALLOC-STATUS = 'RS ' OR 'RA ' OR 'RW ' OR 'RI '             
               MOVE 'REJ IN SELECTION' TO 120A1-STATUS                          
               ADD 1 TO NO-REJECT-SELECTION                                     
            ELSE                                                                
            IF F220-ALLOC-STATUS = 'KI '                                        
               MOVE 'KEY ISSUED   ' TO 120A1-STATUS                             
               ADD 1 TO NO-KEY-ISSUE                                            
            ELSE                                                                
               MOVE SPACES TO 120A1-STATUS.                                     
                                                                                
            IF F220-ACCEPT-REJECT-DATE NOT NUMERIC                              
               MOVE ZEROES TO F220-ACCEPT-REJECT-DATE.                          
                                                                                
            IF F220-KEY-ISSUE-DATE     NOT NUMERIC                              
               MOVE ZEROES TO F220-KEY-ISSUE-DATE.                              
                                                                                
            IF F220-ACCEPT-REJECT-DATE NOT = 0                                  
               MOVE F220-ACCEPT-REJECT-DATE TO WS-F220-DATE                     
               MOVE WS-CC OF WS-F220-DATE TO WS-CUR-CC WS-EDIT-CC               
               MOVE WS-YY OF WS-F220-DATE TO WS-CUR-YY WS-EDIT-YY               
               MOVE WS-MM OF WS-F220-DATE TO WS-CUR-MM WS-EDIT-MM               
               MOVE WS-DD OF WS-F220-DATE TO WS-CUR-DD WS-EDIT-DD               
               MOVE WS-EDITED-DATE        TO 120A1-ACCEPT-DATE                  
            ELSE                                                                
               MOVE SPACES TO 120A1-ACCEPT-DATE.                                
                                                                                
            IF F220-KEY-ISSUE-DATE NOT = 0                                      
               MOVE F220-KEY-ISSUE-DATE TO WS-F220-DATE                         
               MOVE WS-CC OF WS-F220-DATE TO WS-CUR-CC WS-EDIT-CC               
               MOVE WS-YY OF WS-F220-DATE TO WS-CUR-YY WS-EDIT-YY               
               MOVE WS-MM OF WS-F220-DATE TO WS-CUR-MM WS-EDIT-MM               
               MOVE WS-DD OF WS-F220-DATE TO WS-CUR-DD WS-EDIT-DD               
               MOVE WS-EDITED-DATE        TO 120A1-KEY-ISSUED-D                 
            ELSE                                                                
               MOVE SPACES TO 120A1-KEY-ISSUED-D.                               
                                                                                
            ADD 1 TO WS-NUMBER.                                                 
            MOVE F220-NUM-REGN TO 120A1-REGN-NO.                                
                                                                                
            IF F220-CDE-TYP-ACTUSE NOT NUMERIC                                  
               MOVE F220-CDE-TYP-ACTUSE TO 120A1-RM-TYPE                        
            ELSE                                                                
               IF F220-CDE-TYP-ACTUSE = 06                                      
                  MOVE 'E ' TO 120A1-RM-TYPE                                    
               ELSE                                                             
               IF F220-CDE-TYP-ACTUSE = 08                                      
                  MOVE 'MG' TO 120A1-RM-TYPE                                    
               ELSE                                                             
                  MOVE F220-CDE-TYP-ACTUSE TO 120A1-RM-TYPE.                    
                                                                                
            MOVE F220-BLK-NO      TO 120A1-BLK-NO.                              
            MOVE F220-STREET-NAME TO 120A1-STREET-NAME.                         
            MOVE F220-LEVEL-NO    TO 120A1-LEVEL-NO.                            
            MOVE F220-MAIN-UNIT   TO 120A1-MAIN-UNIT.                           
            MOVE F220-SUB-UNIT    TO 120A1-SUB-UNIT.                            
            MOVE F220-NME-APPL1   TO 120A1-APPLN-NAME.                          
            MOVE F220-ALLOC-SCH   TO 120A1-ALLOC-SCH.                           
            WRITE PRINT-REC-1     FROM L480-PR-HEAD-6 AFTER 1.                  
            IF F220-NME-APPL2 NOT = SPACES                                      
                ADD 1 TO WS-NUMBER                                              
                MOVE F220-NME-APPL2 TO 120A1-COAPPLN-NAME                       
                WRITE PRINT-REC-1 FROM L480-PR-HEAD-7 AFTER 1.                  
            MOVE F220-NUM-REGN TO PRE-REGN-NO.                                  
                                                                                
      **********************************************************                
      *                                                        *                
      *    PRINT CONTROL LIST                                  *                
      *                                                        *                
      **********************************************************                
                                                                                
       800-PRINT-CONTROL-LIST.                                                  
            MOVE SPACES TO PRINT-REC-1                                          
            WRITE PRINT-REC-1.                                                  
            MOVE NO-F220-READ TO 120A1-F220-READ.                               
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-9.                              
            MOVE NO-KEY-ISSUE TO 120A1-KEY-ISSUE.                               
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-10 AFTER 3.                     
            MOVE NO-ACCEPT-SELECTION TO 120A1-ACC-SELECTION.                    
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-11 AFTER 3.                     
            MOVE NO-REJECT-SELECTION TO 120A1-REJ-SELECTION.                    
            WRITE PRINT-REC-1 FROM L480-PR-HEAD-13 AFTER 3.                     
                                                                                
       1400-CLOSE-ROUTINE.                                                      
           CLOSE BP13F220                                                       
                 BP13L480.                                                      
           STOP RUN.                                                            
                                                                                
