      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C39H.                                                 
       AUTHOR.        LIZ PATIAG.                                               
       DATE-WRITTEN.  20 JUN 2016.                                              
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO PRINT ACCUMULATED MONTHLY*                
      *          AMORTISATION REPORT (FROM BP13S324)           *                
      *                                                        *                
      *    INPUT FILES:                                        *                
      *      1.  BP13F690                                      *                
      *                                                        *                
      *    LISTING:                                            *                
      *      1.  BP13L39H                                      *                
      *                                                        *                
      * CHG-NO   BY    ON      DESCRIPTION                     *                
      * -------  ---  -------- --------------------------      *                
      * BP136398 FNP1 20/06/16 NEW PGM                         *                
      * BP136535 FNP1 31/01/17 CHANGE GL ACCT FOR REG TO 419200*                
      * BP137219 FNP1 19/02/18 CATER FOR FSG GRANT/SALES PRICE *                
      * BP137539 FNP1 22/10/2018 CHANGE PAY-TYP FOR TOPUP-LEASE                 
      * BP138083 KR13 18/01/2020 ADDED EHG                     *                
      * BP138560 FP6  28/12/2020 ADDED LHG                     *                
      * BP138663 FP6  26/08/2021 CATER FOR CCA                 *                
      * BP138944 FP6  14/10/2021 EXPAND F690 FROM 600 TO 800   *                
      *                          ADDED SUG                     *                
      * BP139942 ZAR7 21/03/2024 REPLACE 410101 WITH 41CZ01    *                
      **********************************************************                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F690  ASSIGN       TO BP13F690.                           
           SELECT BP13L39H  ASSIGN       TO BP13L39H.                           
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F690                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13F690.                                                           
                                                                        00630000
       FD  BP13L39H                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 250 CHARACTERS.                                      
       01  BP13L39H-REC                PIC X(250).                              
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F690-READ       PIC 9(5)     VALUE ZEROES.                
           05  WS-CNT-PRINT-L39HA     PIC 9(5)     VALUE ZEROES.                
      *                                                                         
       01  WS-ACCUM-FIELDS.                                                     
           05  WS-SUB-CASE            PIC 9(5)    VALUE ZEROES.                 
           05  WS-SUB-AMT             PIC 9(9)V99 VALUE ZEROES.                 
           05  WS-SUB-AMT-CURR        PIC 9(9)V99 VALUE ZEROES.                 
           05  WS-SUB-AMT-YTD         PIC 9(9)V99 VALUE ZEROES.                 
           05  WS-SUB-AMT-BAL         PIC 9(9)V99 VALUE ZEROES.                 
           05  WS-TOT-CASE            PIC 9(6)    VALUE ZEROES.                 
           05  WS-TOT-AMT             PIC 9(9)V99 VALUE ZEROES.                 
           05  WS-TOT-AMT-CURR        PIC 9(9)V99 VALUE ZEROES.                 
           05  WS-TOT-AMT-YTD         PIC 9(9)V99 VALUE ZEROES.                 
           05  WS-TOT-AMT-BAL         PIC 9(9)V99 VALUE ZEROES.                 
      *                                                                         
       01  WS-SWITCHES-N-RANGE.                                                 
           05  WS-F690-EOF            PIC X(01)    VALUE SPACES.                
           05  WS-1ST-TIME-IND        PIC X(01)    VALUE 'Y'.                   
               88  WS-1ST-TIME                     VALUE 'Y'.                   
           05  WS-SUBTOT-PRTED-IND    PIC X(01)    VALUE SPACES.                
               88  WS-SUBTOT-PRTED                 VALUE 'Y'.                   
      *                                                                         
       01  WS-DATES-VARIABLES.                                                  
           05  WS-CURRENT-DATE.                                                 
               10  WS-CUR-CC           PIC 99.                                  
               10  WS-CUR-YY           PIC 99.                                  
               10  WS-CUR-MM           PIC 99.                                  
               10  WS-CUR-DD           PIC 99.                                  
           05  WS-PRT-DATE.                                                     
               10  WS-PRT-DD           PIC XX.                                  
               10  FILLER              PIC X        VALUE '/'.                  
               10  WS-PRT-MM           PIC XX.                                  
               10  FILLER              PIC X        VALUE '/'.                  
               10  WS-PRT-CC           PIC XX.                                  
               10  WS-PRT-YY           PIC XX.                                  
           05  WS-TP-DATE              PIC X(8).                                
           05  WS-TP-NUM  REDEFINES   WS-TP-DATE.                               
               10  WS-TP-CCYY          PIC 9(4).                                
               10  WS-TP-MM            PIC 9(2).                                
               10  WS-TP-DD            PIC 9(2).                                
           05  WS-CREATE-DATE          PIC X(8).                                
           05  WS-CREATE-NUM  REDEFINES   WS-CREATE-DATE.                       
               10  WS-CREATE-CCYY      PIC 9(4).                                
               10  WS-CREATE-MM        PIC 9(2).                                
               10  WS-CREATE-DD        PIC 9(2).                                
                                                                                
       01  WS-MISCLLANEOUS.                                                     
           05  WS-PREV-FLAT-TYPE     PIC X(02)    VALUE SPACES.                 
           05  WS-SNOA                 PIC 9(5)   VALUE ZEROES.                 
           05  WS-SNOB                 PIC 9(5)   VALUE ZEROES.                 
                                                                                
       01  WS-AMORTISATION-TABLE.                                               
           05  WS-AMORT-DETAILS OCCURS 9 TIMES.                                 
               10  WS-SIGN             PIC X(1) VALUE SPACES.                   
               10  WS-AMOUNT           PIC 9(8)V99.                             
               10  WS-AMT-MTH          PIC 9(8)V99.                             
               10  WS-AMT-ACCUM        PIC 9(8)V99.                             
               10  WS-AMT-BAL          PIC 9(8)V99.                             
               10  WS-INT-ORDER        PIC X(6) VALUE SPACES.                   
               10  WS-TYPE             PIC X(07) VALUE SPACES.                  
           05  WS-CNT                  PIC 99 VALUE 0.                          
           05  WS-CTR                  PIC 99 VALUE 0.                          
           05  WS-BACKDATE             PIC 9(3) VALUE 0.                        
           05  WS-AMT-BACKDATE         PIC 9(8)V99 VALUE 0.                     
           05  WS-DEFER-TYPE           PIC X(03) VALUE SPACES.                  
                                                                                
       01  L39HA-PRT-HEADER.                                                    
           05  FILLER                  PIC X(56)    VALUE                       
             'BP13C39H - ACCUMULATED AMORTISATION REPORT FOR ALL CASES'.        
                                                                                
       01  L39HA-PRT-HEAD-1.                                                    
           05  FILLER                  PIC X(06)    VALUE 'S/N'.                
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(11)    VALUE 'SCHEME AC'.          
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(08)    VALUE 'REGN NO'.            
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(02)    VALUE 'FT'.                 
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(08)    VALUE 'TP DATE'.            
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(13)    VALUE 'SELL PRICE'.         
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(06)    VALUE 'TENURE'.             
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(09)    VALUE 'INT ORDER'.          
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(07)    VALUE 'TYP'.                
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(01)    VALUE ' '.                  
           05  FILLER                  PIC X(13)    VALUE 'AMOUNT'.             
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(09)    VALUE 'NUM AMORT'.          
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(01)    VALUE ' '.                  
           05  FILLER                  PIC X(13)    VALUE 'AMT CUR MTH'.        
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(01)    VALUE ' '.                  
           05  FILLER                  PIC X(13)    VALUE 'AMT BACKDTE'.        
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(01)    VALUE ' '.                  
           05  FILLER                  PIC X(13)    VALUE 'YTD AMOUNT'.         
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(01)    VALUE ' '.                  
           05  FILLER                  PIC X(13)    VALUE 'BAL AMOUNT'.         
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(08)    VALUE 'SUR DATE'.           
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(01)    VALUE ' '.                  
           05  FILLER                  PIC X(13)    VALUE 'SURR AMT'.           
           05  FILLER                  PIC X(01)    VALUE ';'.                  
           05  FILLER                  PIC X(70)    VALUE 'ADDRESS'.            
           05  FILLER                  PIC X(01)    VALUE ';'.                  
                                                                                
      *========================================================*                
      *           FORMAT  FOR DETAIL LINE                      *                
      *========================================================*                
       01  L39HA-PRT-DETAIL-1.                                                  
           05  L39HA-SNO               PIC ZZ,ZZ9.                              
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SCHEME-ACC        PIC X(11)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-REGN-NO           PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-NUM-FT            PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-TP-DATE           PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SELL-PRICE        PIC ZZ,ZZZ,ZZ9.99.                       
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-TENURE            PIC X(06)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-ORDER             PIC X(09)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-TYP               PIC X(07)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SIGN1             PIC X(1)     VALUE SPACES.               
           05  L39HA-AMOUNT            PIC ZZ,ZZZ,ZZ9.99.                       
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-NUM-AMORT         PIC X(09)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SIGN2             PIC X(1)     VALUE SPACES.               
           05  L39HA-AMT-CUR-MTH       PIC ZZ,ZZZ,ZZ9.99.                       
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SIGN3             PIC X(1)     VALUE SPACES.               
           05  L39HA-AMT-BACKDATE      PIC ZZ,ZZZ,ZZ9.99.                       
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SIGN4             PIC X(1)     VALUE SPACES.               
           05  L39HA-AMT-YTD           PIC ZZ,ZZZ,ZZ9.99.                       
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SIGN5             PIC X(1)     VALUE SPACES.               
           05  L39HA-AMT-BAL           PIC ZZ,ZZZ,ZZ9.99.                       
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SURR-DATE         PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-SIGN6             PIC X(1)     VALUE SPACES.               
           05  L39HA-SURR-AMT         PIC ZZ,ZZZ,ZZ9.99 BLANK WHEN ZERO.        
           05  FILLER                  PIC X(1)     VALUE ';'.                  
           05  L39HA-ADDRESS           PIC X(70)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE ';'.                  
                                                                                
                                                                                
      **********************************************************                
       PROCEDURE DIVISION.                                                      
      **********************************************************                
                                                                                
      *                                                                         
       MAIN-ROUTINE SECTION.                                                    
      *====================*                                                    
            PERFORM 100-OPEN-FILES     THRU  100-EXIT.                          
            PERFORM 150-READ-BP13F690  THRU  150-EXIT.                          
            PERFORM 300-PROCESS-RECORD THRU  300-EXIT                           
                 UNTIL WS-F690-EOF = 'Y'.                                       
            PERFORM 900-CLOSE-ROUTINE  THRU  900-EXIT.                          
                                                                                
       MAIN-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *                                                                         
       100-OPEN-FILES.                                                          
      *========================*                                                
           OPEN INPUT  BP13F690                                                 
                OUTPUT BP13L39H.                                                
      *                                                                         
           WRITE BP13L39H-REC   FROM L39HA-PRT-HEADER.                          
           WRITE BP13L39H-REC   FROM L39HA-PRT-HEAD-1.                          
      *                                                                         
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       150-READ-BP13F690.                                                       
      *===========================*                                             
           READ BP13F690                                                        
                AT END MOVE 'Y' TO WS-F690-EOF                                  
                GO TO 150-EXIT.                                                 
                                                                                
           ADD  1 TO WS-CNT-F690-READ.                                          
                                                                                
      *                                                                         
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *===========================*                                             
           MOVE ZEROES   TO WS-CNT                                              
                            WS-BACKDATE.                                        
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 9                  
               MOVE ZEROES TO    WS-AMOUNT(WS-CTR)                              
                                 WS-AMT-MTH(WS-CTR)                             
                                 WS-AMT-ACCUM (WS-CTR)                          
                                 WS-AMT-BAL(WS-CTR)                             
               MOVE SPACES TO    WS-INT-ORDER(WS-CTR)                           
                                  WS-TYPE(WS-CTR)                               
                                  WS-SIGN(WS-CTR)                               
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES              TO WS-CREATE-DATE                           
                                       WS-TP-DATE.                              
                                                                                
           MOVE F690-DTE-CREATE     TO WS-CREATE-DATE.                          
           MOVE F690-DTE-KEY-ISSUE  TO WS-TP-DATE.                              
                                                                                
           PERFORM 310-GET-INTORDER-AMT     THRU 310-EXIT.                      
                                                                                
           IF (WS-TP-DATE NOT = ZEROES AND LOW-VALUES AND                       
                                          SPACES) AND                           
              (WS-CREATE-DATE(1:6) > WS-TP-DATE(1:6))                           
              COMPUTE WS-BACKDATE = ((WS-CREATE-CCYY * 12) +                    
                                      WS-CREATE-MM) -                           
                                    ((WS-TP-CCYY * 12) +                        
                                      WS-TP-MM)                                 
           ELSE                                                                 
              COMPUTE WS-BACKDATE = 0                                           
           END-IF.                                                              
                                                                                
           PERFORM 500-WRITE-BP13L39H    THRU 500-EXIT                          
               VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > WS-CNT                 
               OR WS-AMOUNT(WS-CTR) = 0.                                        
                                                                                
           PERFORM 150-READ-BP13F690   THRU 150-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       310-GET-INTORDER-AMT.                                                    
      *===========================*                                             
           IF F690-AMT-DEFER-INCOME    NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              IF F690-NUM-AMORTISE-TRANS = 'H'                                  
                 MOVE '419200'   TO WS-INT-ORDER(WS-CNT)                        
                 MOVE 'FSH'      TO WS-TYPE(WS-CNT)                             
              ELSE                                                              
                 IF F690-NUM-AMORTISE-TRANS = 'S'                               
                    IF F690-NUM-TOPUP-LEASE = 'Y'                               
                       MOVE 'SA-LTU'   TO WS-TYPE(WS-CNT)                       
                    ELSE                                                        
                       MOVE 'SA '      TO WS-TYPE(WS-CNT)                       
                                       WS-DEFER-TYPE                            
                    END-IF                                                      
                    MOVE '000000'   TO WS-INT-ORDER(WS-CNT)                     
                 ELSE                                                           
                    MOVE '407600'   TO WS-INT-ORDER(WS-CNT)                     
                    IF F690-CDE-TYP-ACTUSE = '01' AND                           
                       F690-CDE-MODL-DESG  = '74'                               
                       MOVE 'CCA'   TO WS-TYPE(WS-CNT)                          
                                       WS-DEFER-TYPE                            
                    ELSE                                                        
                       MOVE 'FLX'   TO WS-TYPE(WS-CNT)                          
                                       WS-DEFER-TYPE                            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
              MOVE F690-AMT-DEFER-INCOME       TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-INCOME-MTH   TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-INCOME-ACCUM TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-DEFER-INCOME -              
                                           F690-AMT-AMORT-INCOME-ACCUM          
              MOVE ' '                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-SINGLES-PREMIUM NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '41CZ01'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'SGP'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-SINGLES-PREMIUM    TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-SGP-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-SGP-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-SINGLES-PREMIUM -           
                                           F690-AMT-AMORT-SGP-ACCUM             
              MOVE ' '                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-SC-SPR-PREMIUM  NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '41CZ01'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'SPR'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-SC-SPR-PREMIUM     TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-SPR-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-SPR-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-SC-SPR-PREMIUM  -           
                                           F690-AMT-AMORT-SPR-ACCUM             
              MOVE ' '                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-ELDERLY-PREMIUM NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '407400'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'ELD'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-ELDERLY-PREMIUM    TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-ELD-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-ELD-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-ELDERLY-PREMIUM  -          
                                           F690-AMT-AMORT-ELD-ACCUM             
              MOVE ' '                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-AHG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '407000'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'AHG'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-AHG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-AHG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-AHG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-AHG-GRANT        -          
                                           F690-AMT-AMORT-AHG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-EHG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '407900'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'EHG'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-EHG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-EHG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-EHG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-EHG-GRANT        -          
                                           F690-AMT-AMORT-EHG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-LHG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '524300'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'LHG'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-LHG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-LHG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-LHG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-LHG-GRANT        -          
                                           F690-AMT-AMORT-LHG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
                                                                                
           IF F690-AMT-SHG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '407800'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'SHG'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-SHG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-SHG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-SHG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-SHG-GRANT        -          
                                           F690-AMT-AMORT-SHG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-SRG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '407300'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'SRG'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-SRG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-SRG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-SRG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-SRG-GRANT        -          
                                           F690-AMT-AMORT-SRG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-REG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '419200'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'REG'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-REG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-REG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-REG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-REG-GRANT        -          
                                           F690-AMT-AMORT-REG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-FSG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '41FS00'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'FSH UF'   TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-FSG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-FSG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-FSG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-FSG-GRANT        -          
                                           F690-AMT-AMORT-FSG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
           IF F690-AMT-SUG-GRANT       NOT = ZEROES                             
              ADD 1 TO WS-CNT                                                   
              MOVE '418200'   TO WS-INT-ORDER(WS-CNT)                           
              MOVE 'SUG'      TO WS-TYPE(WS-CNT)                                
              MOVE F690-AMT-SUG-GRANT          TO WS-AMOUNT(WS-CNT)             
              MOVE F690-AMT-AMORT-SUG-MTH      TO WS-AMT-MTH(WS-CNT)            
              MOVE F690-AMT-AMORT-SUG-ACCUM    TO WS-AMT-ACCUM(WS-CNT)          
              COMPUTE WS-AMT-BAL(WS-CNT) = F690-AMT-SUG-GRANT        -          
                                           F690-AMT-AMORT-SUG-ACCUM             
              MOVE '-'                         TO WS-SIGN(WS-CNT)               
           END-IF.                                                              
                                                                                
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       500-WRITE-BP13L39H.                                                      
      *===========================*                                             
                                                                                
           MOVE SPACES                  TO BP13L39H-REC.                        
           INITIALIZE                      BP13L39H-REC                         
                                           L39HA-PRT-DETAIL-1.                  
           MOVE ZEROES                  TO WS-AMT-BACKDATE.                     
                                                                                
           ADD 1                         TO WS-SNOA                             
           MOVE WS-SNOA                  TO L39HA-SNO                           
           MOVE F690-NUM-SCH-ACC         TO L39HA-SCHEME-ACC                    
           MOVE F690-NUM-REGN            TO L39HA-REGN-NO                       
           MOVE F690-CDE-TYP-ACTUSE      TO L39HA-NUM-FT                        
           MOVE F690-DTE-KEY-ISSUE       TO L39HA-TP-DATE                       
           MOVE F690-NUM-AMORT           TO L39HA-NUM-AMORT                     
           MOVE F690-AMT-SELLING-PRICE   TO L39HA-SELL-PRICE                    
           MOVE F690-NUM-LEASE-TENURE    TO L39HA-TENURE                        
           STRING 'BLK '  F690-NUM-BLOCK                                        
                  ' # '   F690-NUM-HSELVL                                       
                                                                                
                  ' '     F690-NME-STREET                                       
                  ' S('   F690-NUM-POSTAL                                       
                  ')'  DELIMITED BY SIZE INTO L39HA-ADDRESS                     
           END-STRING.                                                          
                                                                                
           MOVE WS-AMOUNT(WS-CTR)        TO L39HA-AMOUNT.                       
           MOVE WS-AMT-MTH(WS-CTR)       TO L39HA-AMT-CUR-MTH.                  
           MOVE WS-AMT-ACCUM(WS-CTR)     TO L39HA-AMT-YTD.                      
           MOVE WS-AMT-BAL(WS-CTR)       TO L39HA-AMT-BAL.                      
           MOVE WS-INT-ORDER(WS-CTR)     TO L39HA-ORDER.                        
           IF WS-TYPE(WS-CTR) NOT = 'SA ' AND 'FLX' AND 'SA-LTU' AND            
                                    'CCA'                                       
              STRING WS-DEFER-TYPE '-' WS-TYPE(WS-CTR)                          
                 DELIMITED BY SIZE INTO L39HA-TYP                               
           ELSE                                                                 
              MOVE WS-TYPE(WS-CTR)       TO L39HA-TYP                           
           END-IF.                                                              
                                                                                
           IF WS-TYPE(WS-CTR)(1:3) = 'FSH'                                      
              MOVE WS-TYPE(WS-CTR)       TO L39HA-TYP                           
           END-IF.                                                              
                                                                                
      *    MOVE WS-SIGN(WS-CTR)          TO L39HA-SIGN1                         
      *                                     L39HA-SIGN2                         
      *                                     L39HA-SIGN3                         
      *                                     L39HA-SIGN4                         
      *                                     L39HA-SIGN5                         
      *                                     L39HA-SIGN6.                        
                                                                                
           COMPUTE WS-AMT-BACKDATE = WS-BACKDATE * WS-AMT-MTH(WS-CTR).          
           MOVE WS-AMT-BACKDATE          TO L39HA-AMT-BACKDATE.                 
                                                                                
           IF F690-DTE-SURRENDER-REQUEST = SPACES OR LOW-VALUES                 
              MOVE ZEROES                       TO L39HA-SURR-AMT               
              MOVE SPACES                       TO L39HA-SURR-DATE              
           ELSE                                                                 
              MOVE F690-AMT-SURRENDER-COMPUTED  TO L39HA-SURR-AMT               
              MOVE F690-DTE-SURRENDER-REQUEST   TO L39HA-SURR-DATE              
           END-IF.                                                              
                                                                                
           WRITE BP13L39H-REC   FROM L39HA-PRT-DETAIL-1.                        
           ADD 1    TO               WS-CNT-PRINT-L39HA.                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       900-CLOSE-ROUTINE.                                                       
      *===========================*                                             
           IF WS-CNT-PRINT-L39HA = 0                                            
              MOVE 'NO RECORDS PRINTED ' TO BP13L39H-REC                        
              WRITE BP13L39H-REC                                                
           END-IF.                                                              
                                                                                
           CLOSE BP13F690                                                       
                 BP13L39H.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY ' ** BP13C39H ** '.                                          
           DISPLAY 'NO OF RECORDS READ FROM FILE (BP13F690) = '                 
                    WS-CNT-F690-READ.                                           
           DISPLAY 'NO OF L39HA RECORDS PRINTED             = '                 
                    WS-CNT-PRINT-L39HA.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
