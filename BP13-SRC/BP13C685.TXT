       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C685.                                                 
      *AUTHOR.        RHB.                                                      
      *DATE-WRITTEN . 17/03/1997                                                
      * ======================================================== *              
      * SYSTEM OF COMMITMENT (BP13)                              *              
      * ======================================================== *              
      *    OBJECTIVE :                                           *              
      *      1. THIS IS A PROGRAM TO PRINT APPLICATION AND       *              
      *         COOUPIERS WHO ARE NOT ELIGIBLE FOR 5 YEAR OCCP   *              
      *         CHECK.                                           *              
      *                                                          *              
      *    INPUT FILES:                                          *              
      *      1. BP13F716 - RDB RECORD                            *              
      *      2. BP13F558 - OCCP RECORD                           *              
      *      3. BP13F500 - APPLN RECORD                          *              
      *                                                          *              
      *    LISTINGS:                                             *              
      *      1. BP13L685 - REPORT                                *              
      *                                                          *              
      * ======================================================== *              
      * CHG-NO   BY  DATE    DESCRIPTION                         *              
      * -------- --- ------  -----------                         *              
      * BP130248 RHB 970317  NEW PGM                             *              
      * BP130323 LSB 970722  CHG 5YR TO 10YR                     *              
      * BP130410 MPA 971128 CHG REPORT LAYOUT, ADD PAGE-NO       *              
      * BP130445 SSS 990206 EXPAND F558 RECL FROM 150 TO 400     *              
      * BP130630 NOR 990114 CHGE F715 TO F716 EXPAND TO 350      *              
      * BP130652 NOR 990219 EXPAND F520 RECL FROM 300 TO 500     *              
      * BP130897 AKT 000803 EXPAND F558 RECL FROM 400 TO 600     *              
      * BP132603 JF4 281004 CHANGE 10-YR TIMEBAR TO 5-YR         *              
      * ======================================================== *              
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT BP13L685 ASSIGN TO BP13L685.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F520                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F558                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 600 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F558.                                                           
                                                                                
       FD  BP13F716                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 350 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F716.                                                           
                                                                                
       FD  BP13L685                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL  RECORDS  ARE OMITTED.                                         
       01  BP13L685-REC                 PIC X(132).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F520-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F520-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-UPD-NHH              PIC 9(8)    VALUE ZERO.                      
       01  WS-UPD-UPG              PIC 9(8)    VALUE ZERO.                      
       01  WS-NEWTEN-CNT           PIC 9(8)    VALUE ZERO.                      
       01  WS-PAGE                 PIC 9(3)    VALUE ZERO.                      
       01  WS-CNT                  PIC 9(2)    VALUE 99.                        
       01  WS-SERIAL               PIC 9(4)    VALUE ZERO.                      
       01  WS-MTH-RESIDE           PIC 9(4)    VALUE ZEROES.                    
       01  WS-DATE.                                                             
           05  WS-CC               PIC X(2)    VALUE SPACES.                    
           05  WS-YY               PIC X(2)    VALUE SPACES.                    
           05  WS-MM               PIC X(2)    VALUE SPACES.                    
           05  WS-DD               PIC X(2)    VALUE SPACES.                    
                                                                                
       01  WS-QUEUE-TIME.                                                       
           05  WS-TIME             PIC 9(2)    VALUE ZEROS.                     
                                                                                
       77  WS-EOF-F558             PIC X(1).                                    
           88  EOF-OF-F558                     VALUE 'Y'.                       
       77  WS-EOF-F520             PIC X(1).                                    
           88  EOF-OF-F520                     VALUE 'Y'.                       
                                                                                
       01  EOF-F716                PIC X  VALUE 'N'.                            
                                                                                
       01  L685-PR-HEAD-01.                                                     
           05  FILLER              PIC X(14)   VALUE 'BP13L685      '.          
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(20)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE :'.                  
           05  L685-DATE           PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE :'.                  
           05  L685-PAGE           PIC ZZZ9.                                    
                                                                                
       01  L685-PR-HEAD-02.                                                     
           05  FILLER              PIC X(33)   VALUE SPACES.                    
           05  FILLER              PIC X(60)   VALUE                            
           'RFS APPLICATION FALL SHORT OF 5-YEAR REQUIREMENT     '.             
           05  FILLER              PIC X(18)   VALUE SPACES.                    
                                                                                
       01  L685-PR-HEAD-03.                                                     
           05  FILLER              PIC X(57)  VALUE                             
           '  S/N ZN/FT ALC REGN     DATE     NRIC      RN LS 5  OCCP'.         
           05  FILLER              PIC X(55)   VALUE                            
           ' SCH       SALE EFF      TRANS    MOVE-IN  RES         '.           
                                                                                
           05  FILLER              PIC X(32)   VALUE                            
           'RES RES      FILE'.                                                 
                                                                                
       01  L685-PR-HEAD-04.                                                     
           05  FILLER              PIC X(57)  VALUE                             
           '            SCH NO       APPLN    NO           CH YR PD  '.         
           05  FILLER              PIC X(55)   VALUE                            
           ' ACC       TYP  DATE     DATE     DATE     SCH ACC     '.           
                                                                                
           05  FILLER              PIC X(32)   VALUE                            
           'FLT DTE SOLD STAT'.                                                 
                                                                                
       01  L685-PR-HEAD-05.                                                     
           05  FILLER              PIC X(57)  VALUE                             
           '----- ----- --- -------- -------- --------- -- -- -- ----'.         
           05  FILLER              PIC X(55)   VALUE                            
           ' --------- ---- -------- -------- -------- ----------- '.           
           05  FILLER              PIC X(32)   VALUE                            
           '--- -------- --- '.                                                 
                                                                                
       01  L685-PR-DETAIL-1.                                                    
           05  L685-NO             PIC ZZZZ9.                                   
           05  FILLER              PIC X(01).                                   
           05  L685-ZONE           PIC X(02).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-FT             PIC X(02).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-ALLOC          PIC X(03).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-REGN           PIC X(08).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-DTE-APPLN      PIC X(08).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-RELATIONSHIP   PIC X(02).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-LESSEE-CHK     PIC X(01).                                   
           05  FILLER              PIC X(02).                                   
           05  L685-5-YR-CHK       PIC X(01).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-MTH-RESIDE     PIC ZZZ9.                                    
           05  FILLER              PIC X(02).                                   
           05  L685-SCH-ACC        PIC X(09).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-SALES-TYPE     PIC X(02).                                   
           05  FILLER              PIC X(03).                                   
           05  L685-EFF-DATE       PIC X(08).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-TRANS-DATE     PIC X(08).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-MOVING-IN      PIC X(08).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-SCH-ACC-RSL    PIC X(11).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-FT-RSL         PIC X(01).                                   
           05  FILLER              PIC X(03).                                   
           05  L685-DTE-SOLD-RSL   PIC X(08).                                   
           05  FILLER              PIC X(01).                                   
           05  L685-CDE-FILE-STAT  PIC X(03).                                   
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
           PERFORM 010-INITIALIZATION.                                          
           PERFORM 200-READ-F558 THRU 200-EXIT.                                 
           PERFORM 300-READ-F520 THRU 300-EXIT.                                 
           PERFORM 100-PROCESS   THRU 100-EXIT                                  
              UNTIL F558-NUM-REGN = HIGH-VALUE                                  
               AND  F520-NUM-REGN = HIGH-VALUE.                                 
           PERFORM 900-FINALIZATION.                                            
       0000-FINISH.                                                             
           STOP RUN.                                                            
                                                                                
                                                                                
       010-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F558                                                 
                       BP13F520                                                 
                       BP13F716                                                 
                OUTPUT BP13L685.                                                
                                                                                
           MOVE 'N' TO WS-EOF-F558.                                             
           MOVE 'N' TO WS-EOF-F520.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-DATE.                         
           STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                               
                DELIMITED BY SIZE INTO L685-DATE.                               
                                                                                
       010-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-PROCESS.                                                             
      *-------------------------------------------------------------            
           IF F520-NUM-REGN = F558-NUM-REGN                                     
              ADD  1                        TO WS-MATCH                         
              MOVE F520-NUM-REGN            TO L685-REGN                        
              MOVE F520-CDE-ALLOC-SCH       TO L685-ALLOC                       
              MOVE F520-CDE-ZONE            TO L685-ZONE                        
              MOVE F520-CDE-FLAT-TYPE       TO L685-FT                          
              MOVE F520-DTE-REQUEST         TO L685-DTE-APPLN                   
                                                                                
              PERFORM 110-PROCESS-F558 THRU 110-EXIT                            
                      UNTIL F558-NUM-REGN NOT = F520-NUM-REGN OR                
                            F558-NUM-REGN = HIGH-VALUES                         
              MOVE SPACES TO BP13L685-REC                                       
              ADD  1      TO WS-CNT                                             
              WRITE BP13L685-REC                                                
              PERFORM 300-READ-F520 THRU 300-EXIT                               
           ELSE                                                                 
              IF F520-NUM-REGN < F558-NUM-REGN                                  
                 ADD 1 TO WS-F520-NOTMATCH                                      
                 PERFORM 300-READ-F520 THRU 300-EXIT                            
              ELSE                                                              
                 ADD 1 TO WS-F558-NOTMATCH                                      
                 PERFORM 200-READ-F558 THRU 200-EXIT                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       110-PROCESS-F558.                                                        
      *-------------------------------------------------------------            
           IF F558-NUM-REGN = F520-NUM-REGN                                     
                                                                                
              MOVE F558-NUM-NRIC           TO L685-NRIC                         
              MOVE F558-CDE-RELATIONSHIP   TO L685-RELATIONSHIP                 
              MOVE F558-CDE-LESSEE-CHECK   TO L685-LESSEE-CHK                   
              MOVE F558-NUM-HDB-REF-SMS    TO L685-SCH-ACC                      
              MOVE F558-CDE-SALES-TYPE-SMS TO L685-SALES-TYPE                   
              MOVE F558-DTE-FLAT-SOLD-SMS  TO L685-EFF-DATE                     
              MOVE F558-DTE-TRSF-SMS       TO L685-TRANS-DATE                   
      *                                                                         
              MOVE F558-NUM-HDB-REF-RSL      TO L685-SCH-ACC-RSL                
              MOVE F558-CDE-FLAT-TYPE-RESALE TO L685-FT-RSL                     
              MOVE F558-DTE-SOLD-SELLER-RSL  TO L685-DTE-SOLD-RSL               
              MOVE F558-CDE-FILE-STATUS-RSL  TO L685-CDE-FILE-STAT              
      *                                                                         
              MOVE 'N' TO EOF-F716                                              
              PERFORM 400-MATCH-F716 THRU 400-EXIT                              
                      UNTIL EOF-F716 = 'Y'                                      
              PERFORM 500-PRINT-RECORD THRU 500-EXIT                            
              MOVE SPACES                   TO L685-REGN                        
                                               L685-ALLOC                       
                                               L685-ZONE                        
                                               L685-FT                          
                                               L685-DTE-APPLN                   
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F558 THRU 200-EXIT.                                 
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END MOVE 'Y' TO WS-EOF-F558                         
                                MOVE HIGH-VALUE TO F558-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F558-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-F520.                                                           
      *-------------------------------------------------------------            
           READ BP13F520 AT END MOVE 'Y' TO WS-EOF-F520                         
                                MOVE HIGH-VALUE TO F520-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F520-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-MATCH-F716.                                                          
      *-------------------------------------------------------------            
           IF F558-NUM-REGN = F716-NUM-REGN                                     
              MOVE F716-DTE-MOVING-IN  TO L685-MOVING-IN                        
              MOVE F716-NUM-MTH-RESIDE TO WS-MTH-RESIDE                         
              MOVE WS-MTH-RESIDE       TO L685-MTH-RESIDE                       
              IF WS-MTH-RESIDE >= 60                                            
                 MOVE 'Y' TO L685-5-YR-CHK                                      
              ELSE                                                              
                 MOVE 'N' TO L685-5-YR-CHK                                      
              END-IF                                                            
              MOVE 'Y' TO EOF-F716                                              
           ELSE                                                                 
                                                                                
              IF F716-NUM-REGN < F558-NUM-REGN                                  
                 PERFORM 450-READ-F716 THRU 450-EXIT                            
              ELSE                                                              
                 MOVE 'Y' TO EOF-F716                                           
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-READ-F716.                                                           
      *-------------------------------------------------------------            
           READ BP13F716 AT END MOVE 'Y' TO EOF-F716                            
                                MOVE HIGH-VALUE TO F716-NUM-REGN                
               GO TO 450-EXIT.                                                  
            ADD 1 TO WS-F716-READ.                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-PRINT-RECORD.                                                        
      *-------------------------------------------------------------            
           ADD 1 TO WS-MATCH.                                                   
           IF WS-CNT > 50                                                       
              MOVE 0 TO WS-CNT                                                  
              ADD 1 TO  WS-PAGE                                                 
              MOVE  WS-PAGE TO L685-PAGE                                        
              WRITE BP13L685-REC FROM   L685-PR-HEAD-01 AFTER PAGE              
              WRITE BP13L685-REC FROM   L685-PR-HEAD-02                         
              WRITE BP13L685-REC FROM   L685-PR-HEAD-03 AFTER 4                 
              WRITE BP13L685-REC FROM   L685-PR-HEAD-04                         
              WRITE BP13L685-REC FROM   L685-PR-HEAD-05                         
              MOVE SPACES TO BP13L685-REC                                       
              WRITE BP13L685-REC.                                               
                                                                                
           ADD 1 TO WS-CNT.                                                     
           ADD 1 TO WS-SERIAL.                                                  
           MOVE WS-SERIAL                TO L685-NO.                            
           WRITE BP13L685-REC FROM          L685-PR-DETAIL-1.                   
           MOVE SPACES                   TO L685-PR-DETAIL-1.                   
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
      *-------------------------------------------------------------            
             DISPLAY '*----------- BP13C685  ------------*'.                    
             DISPLAY ' F558-READ      ' WS-F558-READ.                           
             DISPLAY ' F520-READ      ' WS-F520-READ.                           
             DISPLAY ' MATCH          ' WS-MATCH.                               
             DISPLAY ' F558-NOT-MT    ' WS-F558-NOTMATCH.                       
             DISPLAY ' F520-NOT-MT    ' WS-F520-NOTMATCH.                       
           CLOSE      BP13F558                                                  
                      BP13F520                                                  
                      BP13F716                                                  
                      BP13L685.                                                 
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
