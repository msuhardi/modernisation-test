       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF48.                                                 
      *AUTHOR.        zarita dane sarmiento                                     
      *DATE-WRITTEN.  20/03/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : EMAIL LIST OF cases with new hle extension    *            
      *              date                                          *            
      *                                                            *            
      * INPUT      : BP13F800                                      *            
      * OUTPUT     : BP13MAIL - OUTPUT FILE FOR SENDING EMAIL      *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTIONS                       *            
      * -------- -------- ----- ------------                       *            
      * BP134496 20120320 zdd1  NEW PROGRAM                        *            
      * BP135537 20150303 RJB1  CHANGE THE EMAIL RECIPIENT FROM    *            
      *                         NICKNAME INTO COMPLETE NAME        *            
      * BP138047 20191125 KAC1  RECOMPILE FOR BP27 COPYBOOK UPDATE *            
      * BP139564 20230603 KAC1 replace kong_Yoke_Pheng to David_Soh             
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K313  ASSIGN        TO BP13K313                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K313-KEY-FLD                       
                            FILE STATUS   IS BP13K313-STATUS.                   
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-HLA                       
                            FILE STATUS   IS BP27K800-STATUS.                   
                                                                                
                                                                                
           SELECT BP13MAIL  ASSIGN TO BP13MAIL.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K313                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K313.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS  1200 CHARACTERS.                                   
       COPY BP27K800.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                       PIC X(100).                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-F800-EOF                    PIC X     VALUE 'N'.                   
       01 WS-F800-READ                   PIC 9(6)  VALUE ZEROS.                 
       01 WS-SNO                         PIC 9(4)  VALUE ZEROS.                 
       01 WS-K313-FND                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K313-NFND                   PIC 9(4)  VALUE ZEROS.                 
       01 WS-K800-FND                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K800-NFND                   PIC 9(4)  VALUE ZEROS.                 
       01 WS-WRT-EMAIL                   PIC 9(4)  VALUE ZEROS.                 
                                                                                
       01 WS-DATE                        PIC X(8)  VALUE SPACES.                
       01 BP13K313-STATUS                PIC X(2)  VALUE SPACES.                
       01 BP27K800-STATUS                PIC X(2)  VALUE SPACES.                
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
      *                                                                         
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CSXXXXX'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
      *                                                                         
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-CCMAILID   PIC X(60)  VALUE                                 
               'David_FG_SOH@hdb.gov.sg>'.                                      
           05  FILLER          PIC X(11)  VALUE SPACES.                         
                                                                                
      *01  MAIL-DTL2B.                                                          
      *    05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  FILLER          PIC X(17)  VALUE                                 
      *        'lkc13@hdb.gov.sg>'.                                             
      *    05  FILLER          PIC X(54)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
               'TENG_SIEW_HONG@HDB.GOV.SG>'.                                    
           05  FILLER          PIC X(11)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-CCMAILID1  PIC X(60)  VALUE                                 
               'David_FG_SOH@hdb.gov.sg>'.                                      
           05  FILLER          PIC X(16)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6C.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  MAIL-CCMAILID1  PIC X(60)  VALUE                                 
               'TENG_SIEW_HONG@HDB.GOV.SG>'.                                    
           05  FILLER          PIC X(16)  VALUE SPACES.                         
                                                                                
       01  MAIL-SUBJ.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  MAIL-SUBJECT    PIC X(40)  VALUE                                 
               'REPORT ON HLE EXTN ( for ADHOC CASES )'.                        
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-TITLE.                                                          
           05 FILLER                   PIC X(30)  VALUE SPACES.                 
           05 FILLER                   PIC X(36)  VALUE                         
              'REPORT ON HLE Extn (for ADHOC CASES)'.                           
                                                                                
       01  MAIL-HEADER.                                                         
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(04)  VALUE 'S/NO'.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(08)  VALUE 'REGN NO.'.                  
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(04)  VALUE 'ALC'.                       
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(10)  VALUE ' HLE APPR '.                
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(10)  VALUE ' ORIG HLE '.                
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(10)  VALUE ' NEW HLE  '.                
                                                                                
       01  MAIL-HEADER2.                                                        
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(04)  VALUE SPACES.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(08)  VALUE SPACES.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(04)  VALUE 'MODE'.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(10)  VALUE '   DATE   '.                
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(10)  VALUE ' EXP DATE '.                
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 FILLER              PIC X(10)  VALUE ' EXP DATE '.                
                                                                                
       01  MAIL-DETAILS.                                                        
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 MAIL-SNO            PIC ZZZ9.                                     
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 MAIL-REGN           PIC X(08)  VALUE SPACES.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 MAIL-ALOC           PIC X(04)  VALUE SPACES.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 MAIL-APPR-DATE      PIC X(10)  VALUE SPACES.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 MAIL-ORIG-EXP-DATE  PIC X(10)  VALUE SPACES.                      
           05 FILLER              PIC X(05)  VALUE SPACES.                      
           05 MAIL-NEW-EXP-DATE   PIC X(10)  VALUE SPACES.                      
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2000-READ-BP13F800    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS-DETAIL   THRU 3000-EXIT                        
                    UNTIL WS-F800-EOF = 'Y'.                                    
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT  BP13F800                                               
                         BP13K313                                               
                         BP27K800                                               
                  OUTPUT BP13MAIL.                                              
                                                                                
           IF BP13K313-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K313 : ' BP13K313-STATUS             
              MOVE     BP13K313-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 5000-OUTPUT-EMAIL-HDR  THRU 5000-EXIT.                       
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
            READ BP13F800                                                       
                 AT END MOVE 'Y' TO WS-F800-EOF                                 
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1   TO WS-F800-READ.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-READ-BP13K313     THRU 4000-EXIT                        
           PERFORM 4100-READ-BP27K800     THRU 4100-EXIT                        
                                                                                
           ADD 1                       TO WS-SNO.                               
           MOVE WS-SNO                 TO MAIL-SNO.                             
           MOVE F800-NUM-REGN          TO MAIL-REGN.                            
           MOVE F800-NUM-ALLO-CAT      TO MAIL-ALOC.                            
           STRING K800-DTE-HLA-APPROVE(7:2) '/'                                 
                  K800-DTE-HLA-APPROVE(5:2) '/'                                 
                  K800-DTE-HLA-APPROVE(1:4)                                     
                  DELIMITED BY SIZE INTO MAIL-APPR-DATE                         
                                                                                
           STRING K800-DTE-HLA-EXPIRY(7:2) '/'                                  
                  K800-DTE-HLA-EXPIRY(5:2) '/'                                  
                  K800-DTE-HLA-EXPIRY(1:4)                                      
                  DELIMITED BY SIZE INTO MAIL-ORIG-EXP-DATE                     
                                                                                
           STRING K313-DTE-HLE-EXTENSION(7:2) '/'                               
                  K313-DTE-HLE-EXTENSION(5:2) '/'                               
                  K313-DTE-HLE-EXTENSION(1:4)                                   
                  DELIMITED BY SIZE INTO MAIL-NEW-EXP-DATE                      
                                                                                
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DETAILS.                                 
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           ADD   1 TO WS-WRT-EMAIL.                                             
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K313.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO  BP13K313-REC.                              
           MOVE '01'             TO  K313-NUM-TRAN-TYPE.                        
           MOVE F800-NUM-REGN    TO  K313-NUM-REGN.                             
                                                                                
           READ BP13K313.                                                       
                                                                                
           EVALUATE BP13K313-STATUS                                             
             WHEN 00                                                            
                 ADD 1 TO WS-K313-FND                                           
             WHEN 23                                                            
                 ADD 1 TO WS-K313-NFND                                          
             WHEN OTHER                                                         
              DISPLAY 'ERROR OPENING - BP13K313 : ' BP13K313-STATUS             
              MOVE     BP13K313-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-READ-BP27K800.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO  BP27K800-MASTER.                           
           MOVE F800-NUM-HLA     TO  K800-NUM-HLA.                              
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE BP27K800-STATUS                                             
             WHEN 00                                                            
                 ADD 1 TO WS-K800-FND                                           
             WHEN 23                                                            
                 ADD 1 TO WS-K800-NFND                                          
             WHEN OTHER                                                         
              DISPLAY 'ERROR OPENING - BP27K800 : ' BP27K800-STATUS             
              MOVE     BP27K800-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-OUTPUT-EMAIL-HDR.                                                   
      *-------------------------------------------------------------            
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6C.                                   
           WRITE MAIL-PRTREC FROM MAIL-SUBJ.                                    
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-TITLE.                                   
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-HEADER.                                  
           WRITE MAIL-PRTREC FROM MAIL-HEADER2.                                 
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY ' '.                                                        
            DISPLAY '- BP13CF48 CONTROL LIST --'                                
            DISPLAY '---------------------------------------------'.            
            DISPLAY ' 1) TOT BP13F800 READ     : ' WS-F800-READ.                
            DISPLAY ' 2) TOT BP13K313 FND      : ' WS-K313-FND.                 
            DISPLAY ' 2) TOT BP13K313 NFND     : ' WS-K313-NFND.                
            DISPLAY ' 3) TOT BP27K800 FND      : ' WS-K800-FND.                 
            DISPLAY ' 4) TOT BP27K800 NFND     : ' WS-K800-NFND.                
            DISPLAY ' 4) TOT RECORD WRITTEN    : ' WS-WRT-EMAIL.                
            DISPLAY ' '.                                                        
                                                                                
            CLOSE  BP13F800                                                     
                   BP13K313                                                     
                   BP27K800                                                     
                   BP13MAIL.                                                    
                                                                                
           IF BP13K313-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K313 : ' BP13K313-STATUS             
              MOVE     BP13K313-STATUS      TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF BP27K800-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP27K800 : ' BP27K800-STATUS             
              MOVE     BP27K800-STATUS      TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
