       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH50.                                                 
       AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  07/05/2015.                                               
      * ===============================================================*        
      * BP13 SYSTEM OF COMMITMENT                                      *        
      * ===============================================================*        
      * OBJECTIVE   : TO CREATE A REPORT FOR THOSE RUN DATE IS 6 MONTHS*        
      *               FROM PCD (K100-DTE-PACD) AND KEYS AVAILABLE DATE *        
      *               (K100-DTE-KEY-AVAIL) IS BLANK.                   *        
      *                                                                *        
      * CONDITIONS:                                                    *        
      *             - PPS TAG       =   E(S), E(R), E(W) or E(A)       *        
      *             - BC TAG        =   BLANK OR N                     *        
      *             - DELIVERY DATE <   REPORT RUN DATE                *        
      *                                                                *        
      *   INPUT FILES :  1. BM06F100                                   *        
      *                  2. BM06K110                                   *        
      *                  3. BP13K800                                   *        
      *                  4. BP13K022                                   *        
      *                  5. BP13K024                                   *        
      *                                                                *        
      *   OUTPUT      :  1. BP13LH50                                   *        
      *                  2. P13LH50A                                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *   BP135711  RJB1  07/05/15 NEW PROGRAM                         *        
      *   BP135711  RJB1  18/05/15 TO SEND EMAIL TO INDIVIDUAL OIC AND *        
      *                            ADDED THE EXPECTED DELIVERY DATE AND*        
      *                            EMAIL FOOTER IN THE REPORT          *        
      *   BP135711  RJB1  04/06/15 TO CALL BP13CCNT TO GET EMAIL ADDR  *        
      *   BP136082  KVL1  11/02/16 TO REPLACE TEEU_HUI@HDB.GOV.SG TO   *        
      *                            KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG *        
      *   BP136477  RJB1  18/10/16 TO REPLACE KIM HUAT TO YEE MIEN     *        
      *   BP138202  KV5   10/03/20 REPLACE MAIL FROM MOHAMED ALI BEE   *        
      *                            KATHIJAH TO HENG SC SUZANNE(HDB)    *        
      *   BP139030  ZAR7  07/01/22 REPLACE CHUA YEE MIEN TO LIM KOK CHUN        
      *   BP139276  KAC1  05/04/24 REPLACING LIM KOK CHUNBY            *        
      *                                      KATHIJAH_BEE_ALI_MOHAMED  *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-REGN                   
                            FILE STATUS      IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K022-KEY-FLD                    
                            ALTERNATE KEY    IS K022-NUM-REGN                   
                            FILE STATUS      IS WS-K022-STATUS.                 
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K024-KEY-FLD                    
                            FILE STATUS      IS WS-K024-STATUS.                 
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K110-KEY-FLD                    
                            ALTERNATE KEY    IS K110-AIX1                       
                                             WITH DUPLICATES                    
                            FILE STATUS      IS WS-K110-STATUS.                 
                                                                                
           SELECT BM06F100  ASSIGN        TO BM06F100.                          
           SELECT BP13LH50  ASSIGN        TO BP13LH50.                          
           SELECT P13LH50A  ASSIGN        TO P13LH50A.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LH50                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13LH50-REC        PIC X(132).                                      
                                                                                
       FD  P13LH50A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  P13LH50A-REC        PIC X(132).                                      
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06F100                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06F100.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K022.                                                           
                                                                                
       FD  BP13K024                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K024.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY             PIC 9(04).                               
           05  WS-CUR-MM               PIC 9(02).                               
           05  WS-CUR-DD               PIC 9(02).                               
                                                                                
       01  WS-REPORT-DATE.                                                      
           05  WS-REP-CCYY             PIC X(04).                               
           05  WS-REP-MM               PIC X(02).                               
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F100-INPUT           PIC 9(08) VALUE 0.                       
           05  WS-LH50-OUT             PIC 9(08) VALUE 0.                       
           05  WS-LH50A-OUT            PIC 9(08) VALUE 0.                       
           05  WS-F100-OUT             PIC 9(08) VALUE 0.                       
           05  WS-WRITTEN              PIC 9(08) VALUE 0.                       
                                                                                
       01  WS-TIME                     PIC 9(08) VALUE 0.                       
       01  WS-F100-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-K110-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-FOUND-K800               PIC X(01) VALUE 'N'.                     
       01  WS-FOUND-K110               PIC X(01) VALUE 'N'.                     
       01  WS-FOUND-K022               PIC X(01) VALUE 'N'.                     
       01  WS-PRINTED                  PIC X(01) VALUE 'N'.                     
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-K022-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-K024-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-K110-STATUS           PIC 99    VALUE ZEROS.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PPS-TAG.                                                       
              10 WS-PPS-ELIG           PIC X(01) VALUE SPACES.                  
              10 WS-PPS-QUOTA          PIC X(01) VALUE SPACES.                  
           05 WS-PPS-TAG-VALUE         PIC X(02).                               
              88 VALID-PPS-TAG         VALUE 'ES', 'ER', 'EW', 'EA'.            
           05 WS-BC-TAG                PIC X(01) VALUE SPACES.                  
           05 WS-BC-TAG-VALUE          PIC X(01).                               
              88 VALID-BC-TAG          VALUE ' ', 'N'.                          
           05 WS-DLVRY-DTE             PIC X(06) VALUE SPACES.                  
           05 WS-OIC                   PIC X(5)  VALUE SPACES.                  
           05 WS-OIC-NME               PIC X(30) VALUE SPACES.                  
           05 WS-EMAIL                 PIC X(50) VALUE SPACES.                  
           05 WS-SAVE-KEY              PIC X(16) VALUE SPACES.                  
           05  WS-DTE-SYS-VALID.                                                
              10 WS-YY-SYS-VALID       PIC 9(4)  VALUE ZEROES.                  
              10 WS-MM-SYS-VALID       PIC 9(2)  VALUE ZEROES.                  
              10 WS-DD-SYS-VALID       PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-DATE-TAB.                                                         
           05  XX-DATE.                                                         
               10  XXXX-YEAR           PIC 9(4).                                
                   88  VALID-YEAR                VALUE 1900 THRU 2200.          
               10  XX-MONTH            PIC 9(2).                                
                   88  31-DAY-MONTH              VALUE 01, 03, 05,              
                                                    07, 08, 10.                 
                   88  30-DAY-MONTH              VALUE 04, 06, 09, 11.          
                   88  28-29-DAY-MONTH           VALUE 02.                      
                   88  VALID-MONTH               VALUE 01 THRU 12.              
                   88  MONTH-DEC                 VALUE 12.                      
               10  XX-DAY              PIC 9(2).                                
                   88  VALID-DAY                 VALUE 01 THRU 31.              
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1A.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
      *        'TEEU_HUI@HDB.GOV.SG>'.                                          
      *        'KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                          
               'SUZANNE_SC_HENG@HDB.GOV.SG>'.                                   
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5A.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(60)  VALUE                                 
      *        'TEEU_HUI@HDB.GOV.SG>'.                                          
      *        'KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                          
               'SUZANNE_SC_HENG@HDB.GOV.SG>'.                                   
                                                                                
       01  MAIL-DTL5B.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(60)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(19)  VALUE 'Submission of Birth'.          
           05  FILLER          PIC X(20)  VALUE ' Certificate for PPS'.         
           05  FILLER          PIC X(20)  VALUE '(E) Cases (6 months '.         
           05  FILLER          PIC X(09)  VALUE 'from PCD)'.                    
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-FOOTER1.                                                        
           05 FILLER                   PIC X(50)  VALUE                         
              'These cases are balloted successfully under PPS(E)'.             
           05 FILLER                   PIC X(50)  VALUE                         
              ', please check whether birth certificate has been '.             
           05 FILLER                   PIC X(27)  VALUE                         
              'received and update system.'.                                    
                                                                                
       01  MAIL-FOOTER2.                                                        
           05 FILLER                   PIC X(60)  VALUE                         
              'Otherwise, please request birth certificate from applican        
      -       'ts.'.                                                            
                                                                                
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 LH50-HDG1.                                                        
              10 FILLER                   PIC X(55)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE                     
                'SYSTEM  OF  COMMITMENT'.                                       
              10 FILLER                   PIC X(55)   VALUE SPACES.             
                                                                                
           05 LH50-HDG2.                                                        
              10 FILLER                   PIC X(26)   VALUE 'BP13LH50'.         
              10 FILLER                   PIC X(42)   VALUE                     
                'SUBMISSION OF BIRTH CERTIFICATE FOR PPS(E)'.                   
              10 FILLER                   PIC X(26)   VALUE                     
                ' CASES (6 MONTHS FROM PCD)'.                                   
              10 FILLER                   PIC X(12)   VALUE SPACES.             
              10 FILLER                   PIC X(07)   VALUE 'DATE : '.          
              10 LH50-SYS-DATE            PIC X(10).                            
                                                                                
           05 LH50-HDG3.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                'S/NO. '.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                'REGISTRATION NO.    '.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(28)   VALUE                     
                'EXPECTED DELIVERY DATE (EDD)'.                                 
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 'OIC           '.                                              
                                                                                
           05 LH50-HDG4.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                '======'.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                '===================='.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(28)   VALUE                     
                '============================'.                                 
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(40)   VALUE                     
                '========================================'.                     
                                                                                
           05 LH50-DTL1.                                                        
              10 LH50-SNO                 PIC ZZZZZ9  VALUE ZEROES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 LH50-REGN                PIC X(08)   VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE SPACES.             
              10 LH50-EDD                 PIC X(06)   VALUE SPACES.             
              10 FILLER                   PIC X(26)   VALUE SPACES.             
              10 LH50-OIC                 PIC X(60)   VALUE SPACES.             
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC 9(4).                                
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F100-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BM06F100                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                OUTPUT BP13LH50                                                 
                       P13LH50A.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K024 ERROR ' WS-K024-STATUS                  
              MOVE     WS-K024-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           INITIALIZE  WS-COUNT.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
           MOVE WS-CUR-DATE(1:6)           TO WS-REPORT-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BM06F100                                                       
                 AT END MOVE 'Y'  TO WS-F100-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F100-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F100-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE SPACES                     TO BP13K800-MASTER                   
                                              WS-PPS-TAG WS-BC-TAG              
                                              WS-DLVRY-DTE.                     
           INITIALIZE WS-FOUND-K800 WS-FOUND-K022 WS-PRINTED.                   
                                                                                
           PERFORM 3100-COMPUTE-DATE                                            
              THRU 3100-EXIT.                                                   
                                                                                
           IF F100-DTE-PACD-QTR = WS-DTE-SYS-VALID(1:6)                         
              DISPLAY 'VALID 6 MONTHS = ' F100-DTE-PACD-QTR ' KEY IS '          
                      F100-KEY-FLD                                              
              PERFORM 3200-READ-BM06K110                                        
                 THRU 3200-EXIT                                                 
              IF WS-PRINTED NOT = 'Y'                                           
                 ADD 1 TO WS-F100-OUT                                           
                 DISPLAY 'NOT PRINTED = ' F100-KEY-FLD '/'                      
                    K110-SCH-ACC-NO '/' K110-REGN-NO                            
              ELSE                                                              
                 MOVE 'N' TO WS-PRINTED                                         
              END-IF                                                            
           ELSE                                                                 
              IF F100-DTE-PACD-QTR < WS-DTE-SYS-VALID(1:6)                      
                 IF F100-DTE-PACD-QTR > WS-CUR-DATE(1:6)                        
                    DISPLAY 'VALID 6 MONTHS = ' F100-DTE-PACD-QTR               
                      ' KEY IS ' F100-KEY-FLD                                   
                    PERFORM 3200-READ-BM06K110                                  
                       THRU 3200-EXIT                                           
                    IF WS-PRINTED NOT = 'Y'                                     
                       ADD 1 TO WS-F100-OUT                                     
                       DISPLAY 'NOT PRINTED = ' F100-KEY-FLD '/'                
                         K110-SCH-ACC-NO '/' K110-REGN-NO                       
                    ELSE                                                        
                       MOVE 'N' TO WS-PRINTED                                   
                    END-IF                                                      
                 ELSE                                                           
                    ADD 1 TO WS-F100-OUT                                        
                    DISPLAY 'NOT 6 MONTHS FROM PCD = ' F100-DTE-PACD            
                 END-IF                                                         
              ELSE                                                              
                 ADD 1 TO WS-F100-OUT                                           
                 DISPLAY 'NOT 6 MONTHS FROM PCD = ' F100-DTE-PACD               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       3100-COMPUTE-DATE.                                                       
      *=========================================================*               
                                                                                
            MOVE WS-CUR-DATE            TO WS-DTE-SYS-VALID                     
                                                                                
            COMPUTE WS-MM-SYS-VALID = WS-MM-SYS-VALID + 6                       
                                                                                
            IF WS-MM-SYS-VALID > 12                                             
               COMPUTE WS-MM-SYS-VALID = WS-MM-SYS-VALID - 12                   
               COMPUTE WS-YY-SYS-VALID = WS-YY-SYS-VALID + 1                    
            END-IF.                                                             
                                                                                
            MOVE WS-DTE-SYS-VALID    TO WS-DATE-TAB.                            
            IF VALID-YEAR AND VALID-MONTH AND VALID-DAY                         
               CONTINUE                                                         
            ELSE                                                                
               DISPLAY 'INVALID DATE = ' WS-DTE-SYS-VALID                       
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3200-READ-BM06K110.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES                TO K110-REC.                              
           INITIALIZE                    K110-REC.                              
           MOVE 'N'                   TO WS-K110-EOF.                           
                                                                                
           MOVE F100-KEY-FLD          TO K110-AIX1.                             
                                                                                
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3210-READNEXT-BM06K110                                 
                    THRU 3210-EXIT                                              
                   UNTIL WS-K110-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y' TO WS-K110-EOF                                        
                 DISPLAY 'BM06K110 REC NOT FND : ' WS-K110-STATUS               
                         ' F100-KEY-FLD = ' F100-KEY-FLD                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STARTBROWSE BM06K110. STATUS IS '               
                            WS-K110-STATUS                                      
                 MOVE WS-K110-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3210-READNEXT-BM06K110.                                                  
      *================================================================*        
           READ BM06K110 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K110-AIX1 = F100-KEY-FLD                                    
                    IF K110-REGN-NO NOT = SPACES AND LOW-VALUES AND             
                       K110-REGN-NO(1:2) NOT = '##'                             
                       MOVE SPACES         TO BP13K800-MASTER                   
                       MOVE K110-REGN-NO   TO K800-NUM-REGN                     
                       PERFORM 3500-READ-BP13K800                               
                          THRU 3500-EXIT                                        
                       PERFORM 3510-PROCEED                                     
                          THRU 3510-EXIT                                        
                    ELSE                                                        
                       DISPLAY 'INVALID K110-REGN-NO = ' K110-REGN-NO           
                    END-IF                                                      
                 ELSE                                                           
                    DISPLAY 'K110-AIX1 = ' K110-AIX1                            
                    DISPLAY 'F100-KEY-FLD = ' F100-KEY-FLD                      
                    MOVE 'Y'               TO WS-K110-EOF                       
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y'                  TO   WS-K110-EOF                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT BM06K110. STATUS IS '                 
                            WS-K110-STATUS                                      
                 MOVE WS-K110-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-READ-BP13K800.                                                      
      *================================================================*        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                MOVE 'Y'                      TO WS-FOUND-K800                  
                MOVE K800-NUM-PPS-ELIG        TO WS-PPS-ELIG                    
                MOVE K800-NUM-PPS-QUOTA       TO WS-PPS-QUOTA                   
                MOVE K800-NUM-PPS-BIRTH-CERT  TO WS-BC-TAG                      
                MOVE K800-DTE-PPS-EST-DLVRY   TO WS-DLVRY-DTE                   
              WHEN 23                                                           
                MOVE 'N'                      TO WS-FOUND-K800                  
                DISPLAY 'BP13K800 REC NOT FND : ' WS-K800-STATUS                
                        ' K800-NUM-REGN = ' K800-NUM-REGN                       
              WHEN OTHER                                                        
                MOVE  WS-K800-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K800 FILE : ' WS-K800-STATUS         
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3510-PROCEED.                                                            
      *================================================================*        
                                                                                
           IF WS-FOUND-K800 = 'Y'                                               
              MOVE WS-PPS-TAG           TO WS-PPS-TAG-VALUE                     
              MOVE WS-BC-TAG            TO WS-BC-TAG-VALUE                      
              IF VALID-PPS-TAG AND VALID-BC-TAG AND                             
                 WS-DLVRY-DTE < WS-REPORT-DATE                                  
                 PERFORM 4000-CREATE-REPORT                                     
                    THRU 4000-EXIT                                              
                 IF WS-OIC NOT = SPACES AND LOW-VALUES                          
                    PERFORM 6000-CALL-BP13CCNT                                  
                       THRU 6000-EXIT                                           
                    IF WS-EMAIL NOT = SPACES AND LOW-VALUES                     
                       PERFORM 4100-CREATE-REPORT2                              
                          THRU 4100-EXIT                                        
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'REGN/PPS/BC/DLVRY  = ' K800-NUM-REGN                  
                         '/ ' WS-PPS-TAG '/ ' WS-BC-TAG                         
                         '/ ' WS-DLVRY-DTE                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3510-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
           ADD  1                   TO WS-LH50-OUT.                             
                                                                                
           IF WS-LH50-OUT = 1                                                   
              STRING WS-CUR-DATE(7:2) '/'                                       
                     WS-CUR-DATE(5:2) '/'                                       
                     WS-CUR-DATE(1:4)                                           
                     DELIMITED BY SIZE                                          
                     INTO LH50-SYS-DATE                                         
              END-STRING                                                        
              MOVE LH50-SYS-DATE TO MAIL-DATE                                   
              WRITE BP13LH50-REC FROM MAIL-HDR1                                 
              WRITE BP13LH50-REC FROM MAIL-HDR2                                 
              WRITE BP13LH50-REC FROM MAIL-DTL1A                                
              WRITE BP13LH50-REC FROM MAIL-DTL1B                                
              WRITE BP13LH50-REC FROM MAIL-DTL3                                 
              WRITE BP13LH50-REC FROM MAIL-DTL4                                 
              WRITE BP13LH50-REC FROM MAIL-DTL5A                                
              WRITE BP13LH50-REC FROM MAIL-DTL5B                                
              WRITE BP13LH50-REC FROM MAIL-DTL7                                 
              WRITE BP13LH50-REC FROM MAIL-DTL8                                 
              WRITE BP13LH50-REC FROM MAIL-SPACES                               
              WRITE BP13LH50-REC FROM LH50-HDG1                                 
              WRITE BP13LH50-REC FROM LH50-HDG2                                 
              MOVE SPACES TO BP13LH50-REC                                       
              WRITE BP13LH50-REC                                                
              WRITE BP13LH50-REC FROM LH50-HDG3                                 
              WRITE BP13LH50-REC FROM LH50-HDG4                                 
              MOVE SPACES TO BP13LH50-REC                                       
              WRITE BP13LH50-REC                                                
           END-IF.                                                              
                                                                                
           MOVE WS-LH50-OUT               TO LH50-SNO.                          
           MOVE K800-NUM-REGN             TO LH50-REGN.                         
           MOVE K800-DTE-PPS-EST-DLVRY    TO LH50-EDD.                          
           PERFORM 4500-GET-OIC                                                 
              THRU 4500-EXIT.                                                   
           IF WS-OIC NOT = SPACES AND LOW-VALUES                                
              STRING '(' WS-OIC ')' ' ' WS-OIC-NME                              
                 DELIMITED BY SIZE       INTO LH50-OIC                          
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES                  TO LH50-OIC                          
           END-IF.                                                              
           WRITE BP13LH50-REC            FROM LH50-DTL1.                        
           IF F100-KEY-FLD = WS-SAVE-KEY                                        
              ADD 1                        TO WS-F100-INPUT                     
           END-IF                                                               
           ADD 1                           TO WS-WRITTEN.                       
           MOVE F100-KEY-FLD               TO WS-SAVE-KEY.                      
           MOVE 'Y'                        TO WS-PRINTED.                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4100-CREATE-REPORT2.                                                     
      *================================================================*        
                                                                                
           MOVE 1                   TO WS-LH50A-OUT.                            
                                                                                
           STRING WS-CUR-DATE(7:2) '/'                                          
                  WS-CUR-DATE(5:2) '/'                                          
                  WS-CUR-DATE(1:4)                                              
                  DELIMITED BY SIZE                                             
                  INTO LH50-SYS-DATE                                            
           END-STRING                                                           
           MOVE LH50-SYS-DATE TO MAIL-DATE                                      
           WRITE P13LH50A-REC FROM MAIL-HDR1                                    
           WRITE P13LH50A-REC FROM MAIL-HDR2                                    
                                                                                
           MOVE SPACES  TO P13LH50A-REC                                         
           STRING 'RCPT TO:<' DELIMITED BY SIZE                                 
                   WS-EMAIL '>'                                                 
                   DELIMITED BY SPACES                                          
                   INTO P13LH50A-REC                                            
           WRITE P13LH50A-REC                                                   
                                                                                
           WRITE P13LH50A-REC FROM MAIL-DTL3                                    
           WRITE P13LH50A-REC FROM MAIL-DTL4                                    
           MOVE SPACES  TO P13LH50A-REC                                         
           STRING 'TO:<' DELIMITED BY SIZE                                      
                  WS-EMAIL '>'                                                  
                  DELIMITED BY SPACES                                           
                  INTO P13LH50A-REC                                             
           WRITE P13LH50A-REC                                                   
                                                                                
           WRITE P13LH50A-REC FROM MAIL-DTL7                                    
           WRITE P13LH50A-REC FROM MAIL-DTL8                                    
           WRITE P13LH50A-REC FROM MAIL-SPACES                                  
           WRITE P13LH50A-REC FROM LH50-HDG1                                    
           WRITE P13LH50A-REC FROM LH50-HDG2                                    
           MOVE SPACES TO P13LH50A-REC                                          
           WRITE P13LH50A-REC                                                   
           WRITE P13LH50A-REC FROM LH50-HDG3                                    
           WRITE P13LH50A-REC FROM LH50-HDG4                                    
           MOVE SPACES TO P13LH50A-REC                                          
           WRITE P13LH50A-REC                                                   
                                                                                
           MOVE WS-LH50A-OUT              TO LH50-SNO.                          
           MOVE K800-NUM-REGN             TO LH50-REGN.                         
           MOVE K800-DTE-PPS-EST-DLVRY    TO LH50-EDD.                          
           STRING '(' WS-OIC ')' ' ' WS-OIC-NME                                 
                 DELIMITED BY SIZE       INTO LH50-OIC                          
           END-STRING                                                           
           WRITE P13LH50A-REC            FROM LH50-DTL1.                        
                                                                                
           MOVE  SPACES                  TO P13LH50A-REC.                       
           WRITE P13LH50A-REC.                                                  
           WRITE P13LH50A-REC          FROM MAIL-FOOTER1.                       
           WRITE P13LH50A-REC          FROM MAIL-FOOTER2.                       
           WRITE P13LH50A-REC          FROM MAIL-END.                           
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-GET-OIC.                                                            
      *================================================================*        
           MOVE SPACES                     TO K022-KEY-FLD.                     
           MOVE K800-NUM-REGN              TO K022-NUM-REGN.                    
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN ZEROS                                                        
                 MOVE 'Y'                     TO WS-FOUND-K022                  
                 MOVE K022-NUM-OIC            TO WS-OIC                         
                 PERFORM 4600-GET-OIC-NAME                                      
                    THRU 4600-EXIT                                              
              WHEN 23                                                           
                 MOVE SPACES                  TO WS-OIC                         
              WHEN OTHER                                                        
                MOVE  WS-K022-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K022 FILE : ' WS-K022-STATUS         
                DISPLAY 'K022-NUM-REGN = ' K022-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4600-GET-OIC-NAME.                                                       
      *================================================================*        
           MOVE SPACES        TO K024-KEY-FLD.                                  
           MOVE K022-NUM-OIC  TO K024-NUM-OIC.                                  
                                                                                
           READ BP13K024                                                        
                                                                                
           EVALUATE WS-K024-STATUS                                              
              WHEN ZEROS                                                        
                 MOVE K024-NME-OIC            TO WS-OIC-NME                     
              WHEN 23                                                           
                 MOVE SPACES                  TO WS-OIC-NME                     
              WHEN OTHER                                                        
                MOVE  WS-K024-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K024 FILE : ' WS-K024-STATUS         
                DISPLAY 'K024-NUM-OIC = ' K024-NUM-OIC                          
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-EMAIL-FOOTER.                                                       
      *================================================================*        
                                                                                
           MOVE  SPACES         TO BP13LH50-REC.                                
           WRITE BP13LH50-REC.                                                  
           WRITE BP13LH50-REC FROM MAIL-FOOTER1.                                
           WRITE BP13LH50-REC FROM MAIL-FOOTER2.                                
           WRITE BP13LH50-REC FROM MAIL-END.                                    
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6000-CALL-BP13CCNT.                                                      
      *================================================================*        
      ***NOTE: MOVE THE CONTACT TYPE ACCORDING TO THE DESIRED OUTPUT            
      ***CONTACT TYPES ARE: 'EML', 'NCK', 'DID'                                 
                                                                                
           MOVE 'EML'                      TO LINK-CNTCT-TYPE.                  
           MOVE WS-OIC                     TO LINK-STAFF-NICK.                  
           MOVE SPACES                     TO LINK-STAFF-NUM.                   
                                                                                
           CALL 'BP13CCNT' USING WS-LINK-REC.                                   
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROES                                                          
              MOVE LINK-STAFF-CNTCT        TO WS-EMAIL                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                         WS-OIC  ', REGN : ' K110-REGN-NO                       
              MOVE  SPACES                 TO WS-EMAIL                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              MOVE  SPACES                 TO WS-EMAIL                          
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           IF WS-WRITTEN > 0                                                    
              PERFORM 5000-EMAIL-FOOTER                                         
                 THRU 5000-EXIT                                                 
           END-IF.                                                              
                                                                                
           DISPLAY 'PROGRAM: BP13CH50'.                                         
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F100-INPUT.               
           DISPLAY 'TOTAL RECORD WRITTEN       : ' WS-WRITTEN.                  
           DISPLAY 'TOTAL RECORD NOT WRITTEN   : ' WS-F100-OUT.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BM06F100                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 BP13LH50                                                       
                 P13LH50A.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K024 ERROR ' WS-K024-STATUS                  
              MOVE     WS-K024-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
