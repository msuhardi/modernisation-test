      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY15.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  27/06/08.                                                 
      *****************************************************************         
      * OBJECTIVE -                                                   *         
      *   GET LARGEST DTE-BALLOT OF ONE NRIC                          *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F595                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  2. P13F595A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP133340  27/06/28  JB8  NEW PROGRAM.                         *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F595  ASSIGN        TO BP13F595.                         
           SELECT  P13F595A  ASSIGN        TO P13F595A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F595                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F595.                                                           
                                                                                
       FD   P13F595A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F595A-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-EOF-BP13F595         PIC X     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F595-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-P13F595A-WRITTEN     PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARS.                                                             
           05  WS-F595-REC             PIC X(500).                              
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2100-READ-BP13F595      THRU 2100-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
                   UNTIL WS-EOF-BP13F595   = 'Y'                                
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F595                                                 
                OUTPUT P13F595A.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
      ******************************************************************        
       2100-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-BP13F595                         
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F595-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE BP13F595-REC              TO WS-F595-REC.                       
                                                                                
           PERFORM 2100-READ-BP13F595   THRU 2100-EXIT.                         
                                                                                
           IF F595-NUM-NRIC1 NOT = WS-F595-REC(41:9) OR                         
              WS-EOF-BP13F595 = 'Y'                                             
              WRITE P13F595A-REC FROM WS-F595-REC                               
              ADD 1                       TO WS-P13F595A-WRITTEN                
           END-IF.                                                              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CY15'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F595 RECORDS READ..........: '                    
                    WS-BP13F595-READ.                                           
           DISPLAY 'NO OF P13F595A RECORDS WRITTEN.......: '                    
                    WS-P13F595A-WRITTEN.                                        
                                                                                
           CLOSE BP13F595                                                       
                 P13F595A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
