       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB19.                                                 
      *AUTHOR.        IRENE LENG.                                               
      *DATE-WRITTEN.  07/11/2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  UPDATE BALLOT CHANCES INTO NUIM-WAIT-TIME   *            
      *                IN BP13K730                                 *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F730                                 *            
      *  I-O FILE   :  1. BP13K730                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133177 IL5 20071107 NEW                                  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS RANDOM                                           
                RECORD KEY IS K730-KEY-NEW                                      
                FILE STATUS IS K730-STATUS.                                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13K730                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K730.                                                           
                                                                                
       FD  BP13F730                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F730.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F730-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-K730-REWRITE     PIC 9(5)    VALUE ZEROS.                     
           05  WS-BYPASS-CTR       PIC 9(5)    VALUE ZEROS.                     
           05  WS-LINE-CTR         PIC 9(2)    VALUE 99.                        
           05  WS-PAGE-CTR         PIC 9(3)    VALUE ZEROES.                    
                                                                                
       01  WS-DATE                  PIC X(8)  VALUE SPACES.                     
       01  WS-EOF                   PIC X     VALUE 'N'.                        
       01  WS-PREV-REGN             PIC X(8)  VALUE SPACES.                     
       01  K730-STATUS              PIC 99    VALUE 0.                          
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F730        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13F730                                                  
                I-O   BP13K730.                                                 
                                                                                
           IF K730-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K730 - OPEN ERROR, STATUS: ' K730-STATUS             
              MOVE K730-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F730.                                                          
      *-------------------------------------------------------------            
            READ BP13F730 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F730-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-UPDATE-K730-REC THRU 4200-EXIT.                         
           PERFORM 2000-READ-F730       THRU 2000-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-UPDATE-K730-REC.                                                    
      *-------------------------------------------------------------            
            MOVE F730-KEY-NEW  TO K730-KEY-NEW.                                 
            READ BP13K730                                                       
            EVALUATE K730-STATUS                                                
            WHEN 00                                                             
                 PERFORM 4300-UPDATE-K730-REC THRU 4300-EXIT                    
                                                                                
            WHEN 23                                                             
                 DISPLAY 'BP13K730 - RECORD NOT FOUND, KEY: '                   
                          F730-NUM-REGN                                         
            WHEN OTHER                                                          
                 DISPLAY 'BP13K730 - ERROR READ, STATUS: '                      
                          K730-STATUS                                           
                 PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-UPDATE-K730-REC.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE F730-NUM-WAIT-TIME TO K730-NUM-WAIT-TIME.                       
           MOVE F730-NUM-RANDOM-PRIORITY TO K730-NUM-RANDOM-PRIORITY.           
                                                                                
           REWRITE BP13K730-REC.                                                
           EVALUATE K730-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO NO-K730-REWRITE                                        
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP13K730 - REWRITE ERROR, STATUS: '                    
                         K730-STATUS                                            
                PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13K730                                                       
                 BP13F730.                                                      
                                                                                
           IF K730-STATUS NOT = '00'                                            
             DISPLAY 'CLOSING ERROR K730, STATUS ' K730-STATUS                  
             MOVE K730-STATUS TO RETURN-CODE.                                   
                                                                                
           DISPLAY ' BP13CB19 CONTROL TOTAL '.                                  
           DISPLAY '---------------------------------'.                         
           DISPLAY ' NO. OF F730 REC READ   > ' NO-F730-READ.                   
           DISPLAY ' NO. OF F730 REC UPDATE > ' NO-K730-REWRITE.                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
