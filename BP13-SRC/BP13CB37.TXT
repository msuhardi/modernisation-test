       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB37.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  24/04/2017.                                               
      * ============================================================ *          
      * SYSTEM OF COMMITMENT (BP13)                                  *          
      * ============================================================ *          
      *                                                              *          
      * OBJECTIVES   : SCREEN OUT WIDOWED APPLICANT / OCCUPIER WHO   *          
      *                ARE ESSENTIAL PERSON IN THE SALES APPLICATION *          
      *                AND WHOSE DEMISED SPOUSE HAD SOLD OFF A DP    *          
      *                FLAT AND THE APPLICATION IS TO BE CLASSIFIED  *          
      *                AS A SECOND-TIMER APPLICATION.                *          
      *                                                              *          
      * INPUT  FILES : BP13F730 - CASES PER REGN                     *          
      *                BP13K825 - BP13.K825.REQOCCP                  *          
      * OUTPUT FILE  : BP13LB37 - GENERATED REPORT                   *          
      *                                                              *          
      * ============================================================ *          
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                        *          
      * --------  ----  --------  ---------------------------------- *          
      * BP136629  SMR2  24042017  NEW PROGRAM                        *          
      * BP136852  SMR2  28072017  ADD BLANK LINE AFTER EACH RECORD   *          
      * BP137048  SMR2  15112017  PASS SQLCODE TO RETURN CODE        *          
      * ============================================================ *          
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS WS-K825-STATUS.                      
                                                                                
           SELECT BP13LB37 ASSIGN       TO BP13LB37.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13LB37                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  132 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       01   BP13LB37-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       COPY BE01W812.                                                           
       COPY BE01W902.                                                           
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K825-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(06)   VALUE ZEROES.            
           05  WS-K825-NOWIDOW             PIC 9(06)   VALUE ZEROES.            
           05  WS-LB37-WRITE               PIC 9(06)   VALUE ZEROES.            
           05  WS-LB37-NOWRITE             PIC 9(06)   VALUE ZEROES.            
           05  WS-LB37-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-LB37-PAGE                PIC 9(05)   VALUE ZEROES.            
           05  WS-LB37-LINE                PIC 9(02)   VALUE 60.                
           05  WS-BE01-SUCC                PIC 9(06)   VALUE ZEROES.            
           05  WS-BE01-UNSUCC              PIC 9(06)   VALUE ZEROES.            
           05  WS-CTR                      PIC 9(02)   VALUE ZEROES.            
           05  WS-I                        PIC 9(02)   VALUE ZEROES.            
           05  WS-J                        PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-TBL-FND                  PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                     PIC X(08)   VALUE SPACES.            
           05  WS-DATE-EDIT                PIC X(10)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-IDNTY-TYPE               PIC X(03)   VALUE SPACES.            
           05  WS-NUM-CSTMR                PIC X(09)   VALUE SPACES.            
           05  WS-NUM-NRIC                 PIC X(09)   VALUE SPACES.            
           05  WS-NME-OCCP                 PIC X(66)   VALUE SPACES.            
           05  WS-F730-KEY-FLD.                                                 
               10  WS-F730-ALLO-CAT        PIC X(03)   VALUE SPACES.            
               10  WS-F730-DTE-BAL         PIC X(06)   VALUE SPACES.            
           05  WS-PREV-KEY-FLD             PIC X(09)   VALUE SPACES.            
           05  WS-RETURN-CODE              PIC 9(03)   VALUE ZEROES.    00880000
                                                                                
       01  WS-ARRAY-VARIABLES.                                                  
           05  WS-ARRAY-VARS OCCURS 10 TIMES.                                   
               10  WS-SP-CSTMR             PIC X(09)   VALUE SPACES.            
               10  WS-SP-NRIC              PIC X(09)   VALUE SPACES.            
               10  WS-SP-NAME              PIC X(66)   VALUE SPACES.            
               10  WS-SP-LSTAT             PIC X(01)   VALUE SPACES.            
               10  WS-SP-DOD               PIC X(08)   VALUE SPACES.            
                                                                                
      **************************************************************            
      *            BP13LB37 - R E P O R T   L A Y O U T            *            
      **************************************************************            
       01  LB37-HEADING-01.                                                     
           05  FILLER              PIC X(08)     VALUE 'BP13LB37'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LB37-RUN-DATE       PIC X(10).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LB37-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  LB37-HEADING-02.                                                     
           05  FILLER              PIC X(31)     VALUE SPACES.                  
           05  FILLER              PIC X(53)     VALUE                          
               'SPOUSE OF A WIDOWED PERSON (ESSENTIAL PERSON) IN THE '.         
           05  FILLER              PIC X(17)     VALUE                          
               'SALES APPLICATION'.                                             
                                                                                
       01  LB37-HEADING-03.                                                     
           05  FILLER              PIC X(14)     VALUE                          
               ' SALES MODE : '.                                                
           05  LB37-ALLO-CAT       PIC X(03)     VALUE SPACES.                  
                                                                                
       01  LB37-HEADING-04.                                                     
           05  FILLER              PIC X(14)     VALUE                          
               ' BALLOT QTR : '.                                                
           05  LB37-DTE-BAL        PIC X(06)     VALUE SPACES.                  
                                                                                
       01  LB37-HEADING-05.                                                     
           05  FILLER              PIC X(111)    VALUE SPACES.                  
           05  FILLER              PIC X(15)     VALUE                          
               'LIVE    DATE OF'.                                               
                                                                                
       01  LB37-HEADING-06.                                                     
           05  FILLER              PIC X(53)     VALUE                          
               ' S/NO  REGN      NT   FT  NRIC/NAME OF WIDOWED PERSON'.         
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(19)     VALUE                          
               'NRIC/NAME OF SPOUSE'.                                           
           05  FILLER              PIC X(23)     VALUE SPACES.                  
           05  FILLER              PIC X(15)     VALUE                          
               'STATUS    DEATH'.                                               
                                                                                
       01  LB37-HEADING-DASH.                                                   
           05  FILLER              PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  LB37-HEADING-SPACE.                                                  
           05  FILLER              PIC X(132)    VALUE SPACES.                  
                                                                                
       01  LB37-DETAIL-01.                                                      
           05  LB37-S-NO           PIC ZZZZ9     VALUE ZEROES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LB37-REGN           PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LB37-NT             PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LB37-FT             PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LB37-WID-NRIC       PIC X(09)     VALUE SPACES.                  
           05  LB37-WID-SEP        PIC X(01)     VALUE SPACES.                  
           05  LB37-WID-NAME       PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LB37-SP-NRIC        PIC X(09)     VALUE SPACES.                  
           05  LB37-SP-SEP         PIC X(01)     VALUE SPACES.                  
           05  LB37-SP-NAME        PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LB37-SP-LSTAT       PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LB37-SP-DOD         PIC X(10)     VALUE SPACES.                  
                                                                                
       01  LB37-DETAIL-02.                                                      
           05  FILLER              PIC X(68)     VALUE SPACES.                  
           05  LB37-OTH-NRIC       PIC X(09)     VALUE SPACES.                  
           05  LB37-OTH-SEP        PIC X(01)     VALUE SPACES.                  
           05  LB37-OTH-NAME       PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LB37-OTH-LSTAT      PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LB37-OTH-DOD        PIC X(10)     VALUE SPACES.                  
                                                                                
           EXEC SQL INCLUDE SQLCA     END-EXEC.                                 
           EXEC SQL INCLUDE P13PERSN  END-EXEC.                                 
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECS       THRU 3000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                OUTPUT BP13LB37.                                                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BE01W812-INPUT-INFO.              
           MOVE 'OPENS'                    TO BE01W812-IN-RTN-FXN.              
           CALL 'BE01C812' USING BE01W812-COMMAREA.                             
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-DATE.                          
           STRING WS-DATE(1:4) '-' WS-DATE(5:2) '-' WS-DATE(7:2)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO LB37-RUN-DATE.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
                                                                                
           MOVE F730-NUM-ALLO-CAT          TO WS-F730-ALLO-CAT.                 
           MOVE F730-DTE-BALLOT            TO WS-F730-DTE-BAL.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           PERFORM 4000-STARTBR-K825       THRU 4000-EXIT.                      
           IF WS-K825-FND = 'N'                                                 
              ADD 1                        TO WS-K825-NOWIDOW                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4000-STARTBR-K825.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K825-EOF                       
                                              WS-K825-FND.                      
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
                PERFORM 5000-READNEXT-K825 THRU 5000-EXIT                       
                  UNTIL WS-K825-EOF = 'Y'                                       
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
                CONTINUE                                                        
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'START ERROR BP13K825 : ' WS-K825-STATUS                
                        ' REGN : ' K825-NUM-REGN                                
                MOVE WS-K825-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-READNEXT-K825.                                                      
      *********************                                                     
                                                                                
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
                IF K825-NUM-REGN = F730-NUM-REGN                                
                   IF (K825-NUM-MARITAL-STATUS = 'W' OR '3') AND                
                      (K825-NUM-LESSEE-CHECK = 'Y')                             
                      MOVE 'Y'             TO WS-K825-FND                       
                      PERFORM 5100-LINK-BE01C812 THRU 5100-EXIT                 
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'                TO WS-K825-EOF                       
                END-IF                                                          
                                                                                
           WHEN 10                                                              
                MOVE 'Y'                   TO WS-K825-EOF                       
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K825 : ' K825-KEY-FLD          
                MOVE 'Y'                   TO WS-K825-EOF                       
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING ON BP13K825'                             
                MOVE WS-K825-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5100-LINK-BE01C812.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                 TO WS-ARRAY-VARIABLES.                   
                                                                                
           MOVE SPACES                 TO BE01W812-COMMAREA.                    
           INITIALIZE                     BE01W812-COMMAREA.                    
                                                                                
           MOVE 'ENQRY'                TO BE01W812-IN-RTN-FXN.                  
           MOVE 'BP13'                 TO BE01W812-IN-NUM-UPDATE-SOURCE.        
           IF K825-NUM-MARITAL-STATUS = 'W' OR '3'                              
              MOVE 'ALL'               TO BE01W812-IN-ENQRY-TYPE                
              MOVE 'EXT'               TO BE01W812-IN-ENQRY-SOURCE              
           END-IF.                                                              
                                                                                
           PERFORM 5200-LINK-BE01C902  THRU 5200-EXIT.                          
           IF WS-IDNTY-TYPE NOT = 'UIN' AND 'FIN'                               
              MOVE K825-NME-OCCP       TO BE01W812-IN-NME-PERSON                
           END-IF.                                                              
           MOVE K825-NUM-NRIC          TO BE01W812-IN-IDNTY-NO.                 
           MOVE WS-IDNTY-TYPE          TO BE01W812-IN-IDNTY-NO-TYPE.            
                                                                                
           CALL 'BE01C812' USING BE01W812-COMMAREA.                             
                                                                                
           EVALUATE BE01W812-OUT-OUTPUT-TAG                                     
           WHEN 'F'                                                             
                ADD 1                  TO WS-BE01-SUCC                          
                MOVE ZEROES            TO WS-I                                  
                MOVE 'N'               TO WS-TBL-FND                            
                MOVE 1                 TO WS-CTR                                
                PERFORM 6000-PROCESS-PERSONS THRU 6000-EXIT                     
                  UNTIL WS-CTR > BE01W812-OUT-MRG-REC-CNT                       
                     OR WS-CTR > 10                                             
                IF WS-TBL-FND = 'Y'                                             
                   PERFORM 7000-WRITE-BP13LB37  THRU 7000-EXIT                  
                ELSE                                                            
                   ADD 1               TO WS-LB37-NOWRITE                       
                END-IF                                                          
                                                                                
           WHEN 'N'                                                             
                DISPLAY 'CUSTOMER NOT FOUND / NO MRG INFO FOUND - '             
                         K825-KEY-FLD                                           
                ADD 1                  TO WS-BE01-UNSUCC                        
                                                                                
           WHEN 'I'                                                             
                DISPLAY 'INPUT REQUIRED - ' K825-KEY-FLD                        
                ADD 1                  TO WS-BE01-UNSUCC                        
                                                                                
           WHEN 'V'                                                             
                DISPLAY 'INPUT INVALID - ' K825-KEY-FLD                         
                ADD 1                  TO WS-BE01-UNSUCC                        
                                                                                
           WHEN 'M'                                                             
                DISPLAY 'MRG INFO FOUND & EXCEEDED MAX INFO TO '                
                        'RETURN - ' K825-KEY-FLD                                
                ADD 1                  TO WS-BE01-UNSUCC                        
                                                                                
           WHEN OTHER                                                           
                MOVE BE01W812-OUT-RTN-STATCDE TO WS-RETURN-CODE                 
                DISPLAY BE01W812-OUT-RTN-PGMID ' ERROR : '                      
                        BE01W812-OUT-RTN-ACTION                                 
                        BE01W812-OUT-RTN-MSGNO ' / '                            
                        BE01W812-OUT-RTN-STATCDE                                
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5200-LINK-BE01C902.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BE01W902-COMMAREA.                
           INITIALIZE                         BE01W902-COMMAREA.                
                                                                                
           MOVE K825-NUM-NRIC              TO BE01W902-IN-NUM-IDNTY-NO.         
                                                                                
           CALL 'BE01C902' USING BE01W902-COMMAREA.                             
                                                                                
           IF BE01W902-OUT-RTN-MSGNO = SPACES OR LOW-VALUES                     
              MOVE BE01W902-OUT-NUM-IDNTY-NO-TYPE                               
                                           TO WS-IDNTY-TYPE                     
           ELSE                                                                 
              IF BE01W902-OUT-RTN-MSGNO = '0127'                                
                 MOVE 'XIN'                TO WS-IDNTY-TYPE                     
              ELSE                                                              
                 DISPLAY 'ERROR IN BE01C902 NRIC : ' K825-NUM-NRIC              
                         ' / ' BE01W902-OUT-RTN-MSGNO                           
                 PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       6000-PROCESS-PERSONS.                                                    
      ***********************                                                   
                                                                                
           IF K825-NUM-NRIC = BE01W812-OUT-MRG-ID-NO-HSBND(WS-CTR)              
              MOVE BE01W812-OUT-MRG-CSTMRID-WIFE(WS-CTR)                        
                                           TO WS-NUM-CSTMR                      
              MOVE BE01W812-OUT-MRG-ID-NO-WIFE(WS-CTR)                          
                                           TO WS-NUM-NRIC                       
              MOVE BE01W812-OUT-MRG-NME-WIFE(WS-CTR)                            
                                           TO WS-NME-OCCP                       
           ELSE                                                                 
              IF K825-NUM-NRIC = BE01W812-OUT-MRG-ID-NO-WIFE(WS-CTR)            
                 MOVE BE01W812-OUT-MRG-CSTMRID-HSBND(WS-CTR)                    
                                           TO WS-NUM-CSTMR                      
                 MOVE BE01W812-OUT-MRG-ID-NO-HSBND(WS-CTR)                      
                                           TO WS-NUM-NRIC                       
                 MOVE BE01W812-OUT-MRG-NME-HSBND(WS-CTR)                        
                                           TO WS-NME-OCCP                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1                           TO WS-CTR.                           
                                                                                
           EXEC SQL                                                             
                SELECT   NUM_CSTMR                                              
                        ,NUM_LIVE_STATUS                                        
                        ,DTE_DEATH                                              
                  INTO  :DCLPERSONS.NUM-CSTMR                                   
                       ,:DCLPERSONS.NUM-LIVE-STATUS                             
                       ,:DCLPERSONS.DTE-DEATH                                   
                  FROM   PERSONS                                                
                  WHERE  NUM_CSTMR = :WS-NUM-CSTMR                              
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                MOVE 'Y'                   TO WS-TBL-FND                        
                ADD 1                      TO WS-I                              
                MOVE WS-NUM-CSTMR          TO WS-SP-CSTMR(WS-I)                 
                MOVE WS-NUM-NRIC           TO WS-SP-NRIC(WS-I)                  
                MOVE WS-NME-OCCP           TO WS-SP-NAME(WS-I)                  
                MOVE NUM-LIVE-STATUS       TO WS-SP-LSTAT(WS-I)                 
                STRING DTE-DEATH(1:4) DTE-DEATH(6:2) DTE-DEATH(9:2)             
                       DELIMITED BY SIZE INTO WS-SP-DOD(WS-I)                   
                                                                                
           WHEN 100                                                             
                DISPLAY 'REC NOT IN PERSONS TABLE FOR ' WS-NUM-CSTMR            
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE               TO WS-RETURN-CODE                    
                DISPLAY 'SQL ERROR IN PERSONS TABLE '                           
                DISPLAY 'NRIC  : ' WS-NUM-NRIC                                  
                DISPLAY 'CSTMR : ' WS-NUM-CSTMR                                 
                DISPLAY 'SQL ERROR CODE : ' WS-RETURN-CODE                      
                PERFORM 9999-SQL-ERROR     THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7000-WRITE-BP13LB37.                                                     
      **********************                                                    
                                                                                
           IF (WS-LB37-LINE > 55) OR                                            
              (WS-F730-KEY-FLD NOT = WS-PREV-KEY-FLD)                           
              PERFORM 7100-WRITE-HEAD-LB37  THRU 7100-EXIT                      
              MOVE WS-F730-KEY-FLD         TO WS-PREV-KEY-FLD                   
           END-IF.                                                              
                                                                                
           PERFORM 7200-WRITE-DET-LB37      THRU 7200-EXIT.                     
                                                                                
           MOVE 2                           TO WS-J.                            
           PERFORM 7300-WRITE-DET-OTH       THRU 7300-EXIT                      
             UNTIL WS-J > 10.                                                   
                                                                                
           WRITE BP13LB37-REC          FROM LB37-HEADING-SPACE.                 
           ADD 1                       TO WS-LB37-LINE.                         
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       7100-WRITE-HEAD-LB37.                                                    
      ***********************                                                   
                                                                                
           ADD 1                       TO WS-LB37-PAGE.                         
           MOVE WS-LB37-PAGE           TO LB37-PAGE-NO.                         
                                                                                
           MOVE WS-F730-ALLO-CAT       TO LB37-ALLO-CAT.                        
           MOVE WS-F730-DTE-BAL        TO LB37-DTE-BAL.                         
                                                                                
           WRITE BP13LB37-REC          FROM LB37-HEADING-01 AFTER PAGE.         
           WRITE BP13LB37-REC          FROM LB37-HEADING-02.                    
           WRITE BP13LB37-REC          FROM LB37-HEADING-SPACE.                 
           WRITE BP13LB37-REC          FROM LB37-HEADING-03.                    
           WRITE BP13LB37-REC          FROM LB37-HEADING-04.                    
           WRITE BP13LB37-REC          FROM LB37-HEADING-05.                    
           WRITE BP13LB37-REC          FROM LB37-HEADING-06.                    
           WRITE BP13LB37-REC          FROM LB37-HEADING-DASH.                  
                                                                                
           MOVE 9                      TO WS-LB37-LINE.                         
                                                                                
           IF WS-F730-KEY-FLD NOT = WS-PREV-KEY-FLD                             
              MOVE ZEROES              TO WS-LB37-SNO                           
           END-IF.                                                              
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7200-WRITE-DET-LB37.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO LB37-DETAIL-01.                   
                                                                                
           ADD 1                           TO WS-LB37-SNO                       
                                              WS-LB37-WRITE                     
                                              WS-LB37-LINE.                     
                                                                                
           MOVE WS-LB37-SNO                TO LB37-S-NO.                        
           MOVE F730-NUM-REGN              TO LB37-REGN.                        
           MOVE F730-CDE-NT1               TO LB37-NT.                          
           MOVE F730-CDE-FLAT-TYPE         TO LB37-FT.                          
                                                                                
           MOVE K825-NUM-NRIC              TO LB37-WID-NRIC.                    
           MOVE '/'                        TO LB37-WID-SEP.                     
           MOVE K825-NME-OCCP              TO LB37-WID-NAME.                    
                                                                                
           MOVE WS-SP-NRIC(1)              TO LB37-SP-NRIC.                     
           MOVE '/'                        TO LB37-SP-SEP.                      
           MOVE WS-SP-NAME(1)              TO LB37-SP-NAME.                     
                                                                                
           MOVE WS-SP-LSTAT(1)             TO LB37-SP-LSTAT.                    
           IF WS-SP-DOD(1) NOT = '00010101'                                     
              STRING WS-SP-DOD(1)(7:2) '/' WS-SP-DOD(1)(5:2) '/'                
                     WS-SP-DOD(1)(1:4)                                          
                     DELIMITED BY SIZE INTO LB37-SP-DOD                         
           END-IF.                                                              
                                                                                
           WRITE BP13LB37-REC              FROM LB37-DETAIL-01.                 
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       7300-WRITE-DET-OTH.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO LB37-DETAIL-02.                   
                                                                                
           IF WS-SP-NRIC(WS-J) NOT = SPACES AND LOW-VALUES                      
              MOVE WS-SP-NRIC(WS-J)        TO LB37-OTH-NRIC                     
              MOVE '/'                     TO LB37-OTH-SEP                      
              MOVE WS-SP-NAME(WS-J)        TO LB37-OTH-NAME                     
                                                                                
              MOVE WS-SP-LSTAT(WS-J)       TO LB37-OTH-LSTAT                    
              IF WS-SP-DOD(WS-J) NOT = '00010101'                               
                 STRING WS-SP-DOD(WS-J)(7:2) '/'                                
                        WS-SP-DOD(WS-J)(5:2) '/'                                
                        WS-SP-DOD(WS-J)(1:4)                                    
                        DELIMITED BY SIZE INTO LB37-OTH-DOD                     
              END-IF                                                            
                                                                                
              WRITE BP13LB37-REC           FROM LB37-DETAIL-02                  
              ADD 1                        TO WS-LB37-LINE                      
           END-IF.                                                              
                                                                                
           ADD 1                           TO WS-J.                             
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY '*===========================================*'.             
           DISPLAY '  BP13CB37 CONTROL TOTAL       : ' WS-DATE-EDIT.            
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  NO OF F730 RECORDS READ      : ' WS-F730-READ.            
           DISPLAY '    NO OF F730 W/O WIDOW CASE  : ' WS-K825-NOWIDOW.         
           DISPLAY '    NO OF BE01 SUCC ENQUIRY    : ' WS-BE01-SUCC.            
           DISPLAY '      NO OF LB37 RECS WRITTEN  : ' WS-LB37-WRITE.           
           DISPLAY '      NOT FOUND IN PERSONS TAB : ' WS-LB37-NOWRITE.         
           DISPLAY '    NO OF BE01 UNSUCC ENQUIRY  : ' WS-BE01-UNSUCC.          
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 BP13LB37.                                                      
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BE01W812-INPUT-INFO.              
           MOVE 'CLOSE'                    TO BE01W812-IN-RTN-FXN.              
           CALL 'BE01C812' USING BE01W812-COMMAREA.                             
                                                                                
           IF WS-RETURN-CODE > ZEROES                                           
              MOVE WS-RETURN-CODE          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
      *****************                                                         
       9999-SQL-ERROR.                                                          
      *****************                                                         
                                                                                
           DISPLAY ' *****   BP13CB37 ABEND   *****'.                           
                                                                                
           CALL 'DBATIAR' USING SQLCA.                                          
           EXEC SQL ROLLBACK END-EXEC.                                          
           PERFORM SQL-INITIAL UNTIL SQL-INIT-DONE                              
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                CONTINUE                                                        
           WHEN OTHER                                                           
                MOVE SQLCODE               TO WS-RETURN-CODE                    
                DISPLAY 'ROLLBACK FAILED, SQLCODE IS : ' WS-RETURN-CODE         
           END-EVALUATE.                                                        
                                                                                
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
