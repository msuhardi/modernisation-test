      CBL XOPTS('EXCI,COBOL3')                                                  
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C9CJ.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  14/10/2015.                                               
                                                                                
      * ========================================================== *            
      * OBJECTIVE    :  EXCI program to call online program        *            
      *                 BP13I9CJ passing to collect info from CPF  *            
      *                 thru AB02B900.                             *            
      * ========================================================== *            
      * INPUT FILES  :  1.) AB02F870                               *            
      * LISTING      :  1.) BP13L9CJ                               *            
      * ========================================================== *            
      * CHG-NO    BY    DATE        DESCRIPTION                    *            
      * --------  ----  ------      -----------                    *            
      * BP136412  KSJ3  14/10/2015  NEW PROGRAM                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F870  ASSIGN       TO AB02F870.                           
           SELECT BP13L9CJ  ASSIGN       TO BP13L9CJ.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AB02F870                                                             
           RECORDING MODE  IS F                                                 
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
           COPY AB02F870.                                                       
                                                                                
       FD  BP13L9CJ                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L9CJ-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *==============================================================*          
                                                                                
       01  WS-COUNT.                                                            
           05  WS-CNT-F870-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F870-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K680-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-SUCCESS-CNT            PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F870-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-F870-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
      **-------------------------------------------------------------**         
      **      ab02b900 comm-area                                     **         
      **-------------------------------------------------------------**         
       COPY AB02900C.                                                           
                                                                                
      **-------------------------------------------------------------**         
      **      START OF WORKING STORAGE FOR INTEGRATION TO CICS       **         
      **-------------------------------------------------------------**         
      *==============================================================*          
      *   Declare Call level,DPL, and EXEC level Return Code areas.  *          
      *==============================================================*          
       COPY DFHXCPLO.                                                           
                                                                                
      *==============================================================*          
      *   Declare Areas to be used for outputting return codes.      *          
      *==============================================================*          
       01  OUTPUT-RETAREA.                                                      
           05  FILLER                    PIC X(20)  VALUE SPACES.               
           05  O-RESP                    PIC 9(8).                              
           05  FILLER                    PIC X(2)   VALUE SPACES.               
           05  O-RESP2                   PIC 9(8).                              
           05  FILLER                    PIC X(2)   VALUE SPACES.               
           05  OEXCI-SUB-REASON1         PIC 9(8).                              
           05  O-ABCODE-LINE REDEFINES OEXCI-SUB-REASON1.                       
               10  O-ABCODE              PIC X(4).                              
               10  OPAD-ABCODE           PIC X(4).                              
           05  FILLER                    PIC X(80)  VALUE SPACES.               
                                                                                
       01  SUB                           PIC S9(8) COMP.                        
       01  SUBX                          PIC S9(8) COMP.                        
       01  SUBY                          PIC S9(8) COMP.                        
       01  OUT-REC.                                                             
           05  OUT-REC-ELEM              PIC X(1) OCCURS 128 TIMES.             
                                                                                
      *==============================================================*          
      *   Define Server call-type codes/Return Codes.                *          
      *==============================================================*          
       01  SERVER-CALLTYPES.                                                    
           05  INIT-CALL           PIC S9(8)    COMP VALUE 1.                   
           05  READ-CALL           PIC S9(8)    COMP VALUE 2.                   
           05  CLOSE-CALL          PIC S9(8)    COMP VALUE 3.                   
                                                                                
       01  SERVER-RETCODES.                                                     
           05  RET-BROWSE          PIC S9(8)    COMP VALUE ZERO.                
           05  RET-NOBRWS          PIC S9(8)    COMP VALUE 4.                   
           05  RET-EOF             PIC S9(8)    COMP VALUE 8.                   
           05  RET-NOFILE          PIC S9(8)    COMP VALUE 12.                  
           05  RET-ERROR           PIC S9(8)    COMP VALUE 16.                  
                                                                                
      *==============================================================*          
      *   Initialise Target information variables.                   *          
      *==============================================================*          
       01  TARGET-PROGRAM          PIC X(8)     VALUE 'BP13I9CJ'.               
       01  TARGET-TRANSID          PIC X(4)     VALUE 'A9CI'.                   
       01  TARGET-SYSTEM.                                                       
           05  TARGET-SYS-ELEM     PIC X(1) OCCURS 8 TIMES.                     
       01  TARGET-USERID.                                                       
           05  TARGET-USER-ID      PIC X(1) OCCURS 8 TIMES.                     
                                                                                
      *==============================================================*          
      *   Initialise Call level specific variables.                  *          
      *==============================================================*          
       01  APPLICATION                   PIC X(8)   VALUE 'BATCHCLI'.           
       01  USER-TOKEN                    PIC S9(8) COMP VALUE ZERO.             
       01  PIPE-TOKEN                    PIC S9(8) COMP VALUE ZERO.             
                                                                                
      *==============================================================*          
      *   Define Commarea struct.                                    *          
      *==============================================================*          
       01  COMMAREA.                                                            
           05  FILLER                          PIC X(18)  VALUE SPACES.         
           05  WS-COMMAREA.                                                     
               10  WS-COMM-ERR-CDE             PIC X(4)   VALUE SPACES.         
               10  WS-COMM-ERR-MSG             PIC X(66)  VALUE SPACES.         
               10  WS-COMM-AB02B900.                                            
                   15  WS-COMM-NUM-TRANS       PIC X(2)   VALUE SPACES.         
                   15  WS-COMM-NUM-HDB-REF     PIC X(11)  VALUE SPACES.         
                   15  WS-COMM-NUM-REGN        PIC X(8)   VALUE SPACES.         
                   15  WS-COMM-NUM-CASE        PIC X(8)   VALUE SPACES.         
                   15  WS-COMM-NUM-OFFICE      PIC X(2)   VALUE SPACES.         
                   15  WS-COMM-NUM-TYPE        PIC X(3)   VALUE SPACES.         
                   15  WS-COMM-NUM-CPF1        PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-NUM-CPF2        PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-NUM-CPF3        PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-NUM-CPF4        PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-NUM-UIN-LSE1    PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-NUM-UIN-LSE2    PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-NUM-UIN-LSE3    PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-NUM-UIN-LSE4    PIC X(9)   VALUE SPACES.         
                   15  WS-COMM-AMT-LOAN-GRANT-X                                 
                                               PIC X(7)   VALUE SPACES.         
                   15  WS-COMM-AMT-LOAN-GRANT REDEFINES                         
                       WS-COMM-AMT-LOAN-GRANT-X                                 
                                               PIC 9(7).                        
                   15  WS-COMM-DTE-LOAN-COMM   PIC X(8)   VALUE SPACES.         
                   15  WS-COMM-NUM-LOAN-SCHEME PIC X(1)   VALUE SPACES.         
                   15  WS-COMM-NUM-USERID      PIC X(7)   VALUE SPACES.         
                   15  WS-COMM-NUM-CNT-X       PIC X(1)   VALUE SPACES.         
                   15  WS-COMM-NUM-CNT REDEFINES WS-COMM-NUM-CNT-X              
                                               PIC 9(1).                        
                   15  WS-COMM-NUM-SYSTEM      PIC X(3)   VALUE SPACES.         
           05  FILLER                          PIC X(29)  VALUE SPACES.         
                                                                                
      *==============================================================*          
      *   Initialise Commarea length and Data length(in bytes).      *          
      *==============================================================*          
       01  COMM-LENGTH               PIC S9(8)  COMP VALUE 250.                 
       01  DATA-LENGTH               PIC S9(8)  COMP VALUE 250.                 
       01  LINK-COM-LEN              PIC S9(4)  COMP VALUE 18.                  
       01  LINK-DAT-LEN              PIC S9(4)  COMP VALUE 18.                  
                                                                                
      *==============================================================*          
      *   Initialise program specific variables and flags.           *          
      *==============================================================*          
       01  SERVER-RETCODE                PIC S9(8) COMP VALUE ZERO.             
                                                                                
       01  PROGRAM-FLAGS.                                                       
           05  ABORT                     PIC X(3)   VALUE 'NO '.                
           05  YES                       PIC X(3)   VALUE 'YES'.                
           05  NOT-YET                   PIC X(3)   VALUE 'NO '.                
                                                                                
       01  SAVED-RESPONSE                PIC 9(8) COMP VALUE ZERO.              
                                                                                
       01  PROGRAM-MESSAGES.                                                    
           05  MSG00            PIC X(128)     VALUE SPACES.                    
           05  MSG01            PIC X(128)     VALUE                            
           '*========== EXCI Sample Batch Client program   ========= '.         
           05  MSG02            PIC X(128)     VALUE                            
           '*                                                        '.         
           05  MSG03            PIC X(128)     VALUE                            
           '*  EXEC Level Processor.                                 '.         
           05  MSG04            PIC X(128)     VALUE                            
           '*  Setting up the EXEC level call.                       '.         
           05  MSG05            PIC X(128)     VALUE                            
           '*  The Link Request has successfully completed.          '.         
           05  MSG06            PIC X(128)     VALUE                            
           '*  Server Response:                                      '.         
           05  MSG07            PIC X(128)     VALUE                            
           '*  The file is set to a browsable state.                 '.         
           05  MSG08            PIC X(128)     VALUE                            
           '*  The file could not be found                           '.         
           05  MSG09            PIC X(128)     VALUE                            
           '*  The file could not be successfully initialized.       '.         
           05  MSG10            PIC X(128)     VALUE                            
           '*  A serious error was detected.                         '.         
           05  MSG11            PIC X(128)     VALUE                            
           '*  The Link Request has failed. Return code are :        '.         
           05  MSG13            PIC X(128)     VALUE                            
           '*  A message was received from the target CICS System:   '.         
           05  MSG14            PIC X(128)     VALUE                            
           '*  >>>> Aborting further processing <<<<                 '.         
           05  MSG15            PIC X(128)     VALUE                            
           '*  CALL Level Processor.                                 '.         
           05  MSG16            PIC X(128)     VALUE                            
           '*  Initialise_User call complete.                        '.         
           05  MSG17            PIC X(128)     VALUE                            
           '*  Allocate_Pipe call complete.                          '.         
           05  MSG18            PIC X(128)     VALUE                            
           '*  Open_Pipe call complete.                              '.         
           05  MSG19            PIC X(128)     VALUE                            
           '*  The connection has been successful.                   '.         
           05  MSG20            PIC X(128)     VALUE                            
           '*  The target file follows:                              '.         
           05  MSG21            PIC X(128)     VALUE                            
           '*================== Top of File =======================  '.         
           05  MSG22            PIC X(128)     VALUE                            
           '*================== End of File =======================  '.         
           05  MSG23            PIC X(128)     VALUE                            
           '*  The connection unsuccessful. Return codes are :       '.         
           05  MSG25            PIC X(128)     VALUE                            
           '*  The DPL request has failed.                           '.         
           05  MSG26            PIC X(128)     VALUE                            
           '*  Link return codes are:                                '.         
           05  MSG27            PIC X(128)     VALUE                            
           '*  DPL return codes are:                                 '.         
           05  MSG28            PIC X(128)     VALUE                            
           '*  Closing DPL Request has been attempted.               '.         
           05  MSG29            PIC X(128)     VALUE                            
           '*  Close_Pipe call complete.                             '.         
           05  MSG30            PIC X(128)     VALUE                            
           '*  Deallocate_Pipe call complete                         '.         
           05  MSG31            PIC X(128)     VALUE                            
           '*===== End of EXCI Sample batch client program =====     '.         
           05  MSG32.                                                           
               07  FILLER       PIC X(17)    VALUE                              
               '*    Parameters: '.                                             
               07  FILL-APP     PIC X(7)     VALUE 'APPLID='.                   
               07  MSG-SYSTEM   PIC X(8)     VALUE SPACES.                      
               07  FILL-USER    PIC X(9)     VALUE '  USERID='.                 
               07  MSG-USERID   PIC X(8)     VALUE SPACES.                      
               07  FILLER       PIC X(30)    VALUE SPACES.                      
               07  FILLER       PIC X(1)     VALUE SPACES.                      
               07  FILLER       PIC X(48)    VALUE SPACES.                      
      * COUNTER                                                                 
           05  WS-CTR-LINE1.                                                    
               07  FILLER       PIC X(50)    VALUE ALL '='.                     
               07  FILLER       PIC X(78)    VALUE SPACES.                      
                                                                                
           05  WS-CTR-LINE2.                                                    
               07  FILLER       PIC X(15)    VALUE SPACES.                      
               07  FILLER       PIC X(50)    VALUE '  BP13C9CJ'.                
               07  FILLER       PIC X(78)    VALUE SPACES.                      
                                                                                
           05  WS-CTR-LINE3.                                                    
               07  FILLER       PIC X(50)    VALUE ALL '-'.                     
               07  FILLER       PIC X(78)    VALUE SPACES.                      
                                                                                
           05  WS-CTR-LINE4.                                                    
               07  FILLER       PIC X(25)    VALUE                              
                   ' NO. OF AB02F870 READ : '.                                  
               07  WS-CTR-READ  PIC 9(8)     VALUE ZEROES.                      
               07  FILLER       PIC X(95)    VALUE SPACES.                      
                                                                                
           05  WS-CTR-LINE5.                                                    
               07  FILLER       PIC X(25)    VALUE                              
                   ' NO. OF RECS PROCESSED: '.                                  
               07  WS-CTR-SUCC  PIC 9(8)     VALUE ZEROES.                      
               07  FILLER       PIC X(95)    VALUE SPACES.                      
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
      *==============================================================*          
                                                                                
       01  NULL-PTR USAGE IS POINTER.                                           
                                                                                
       01  CALL-LEVEL-MSG.                                                      
           05  CALL-LEVEL-MSG-LEN        PIC S9(4) COMP.                        
           05  FILLER                    PIC S9(4) COMP.                        
           05  CALL-LEVEL-MSG-TEXT       PIC X(1) OCCURS 128 TIMES.             
                                                                                
       01  EXEC-LEVEL-MSG.                                                      
           05  EXEC-LEVEL-MSG-TEXT       PIC X(1) OCCURS 128 TIMES.             
                                                                                
       01  PARM-DATA.                                                           
           05  PARM-STRING-LENGTH        PIC 9(4) COMP.                         
           05  PARM-STRING               PIC X(1) OCCURS 17 TIMES.              
                                                                                
                                                                                
       PROCEDURE DIVISION USING PARM-DATA.                                      
      *==============================================================*          
       0000-CONTROL.                                                            
      *==============================================================*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-AB02F870      THRU 2000-EXIT.                      
                                                                                
           IF WS-F870-EOF NOT = 'Y'                                             
              PERFORM 3000-MAIN-ROUTINE    THRU 3000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       1000-OPEN-ROUTINE.                                                       
      *==============================================================*          
                                                                                
           OPEN INPUT  AB02F870                                                 
                OUTPUT BP13L9CJ.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       2000-READ-AB02F870.                                                      
      *==============================================================*          
                                                                                
           READ AB02F870                                                        
                AT END MOVE 'Y'            TO   WS-F870-EOF.                    
                                                                                
           IF WS-F870-EOF NOT = 'Y'                                             
              ADD  1                       TO   WS-CNT-F870-READ                
              MOVE SPACES                  TO   COMMAREA                        
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       3000-MAIN-ROUTINE.                                                       
      *==============================================================*          
                                                                                
           PERFORM 3100-GET-TARGET         THRU 3100-EXIT.                      
           PERFORM 4000-SECTION-1          THRU 4000-EXIT.                      
           PERFORM 5000-SECTION-2          THRU 5000-EXIT.                      
                                                                                
      *--------------------------------------------------------------*          
      *    Perform main program processing                                      
      *--------------------------------------------------------------*          
           PERFORM 6000-DPL-SECTION        THRU 6000-EXIT                       
                   UNTIL ABORT IS EQUAL TO YES                                  
                      OR WS-F870-EOF = 'Y'.                                     
                                                                                
           PERFORM 7000-SECTION-3          THRU 7000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       3100-GET-TARGET.                                                         
      *==============================================================*          
                                                                                
      * TESTING                                                                 
      *    MOVE 'CI3TORT '                 TO   TARGET-SYSTEM.                  
      *    MOVE 'BP13SURT'                 TO   TARGET-USERID.                  
                                                                                
      * PRODUCTION                                                              
      *    MOVE 'CI1TORP '                 TO   TARGET-SYSTEM.                  
      *    MOVE 'BP13SURP'                 TO   TARGET-USERID.                  
                                                                                
      *--------------------------------------------------------------*          
      *   GET TARGET-SYSTEM AND TARGET-USERID FROM PARM DATA                    
      *--------------------------------------------------------------*          
           IF PARM-STRING-LENGTH > 0                                            
              IF PARM-STRING(1) = ','                                           
                 MOVE SPACES               TO   FILL-APP                        
                 MOVE SPACES               TO   MSG-SYSTEM                      
                 MOVE 1                    TO   SUB                             
              ELSE                                                              
                 MOVE SPACES               TO   TARGET-SYSTEM                   
                 PERFORM TEST BEFORE VARYING SUB FROM 1 BY 1                    
                   UNTIL SUB > PARM-STRING-LENGTH                               
                      OR SUB > 8                                                
                      OR PARM-STRING(SUB) = ','                                 
                    MOVE PARM-STRING(SUB)  TO   TARGET-SYS-ELEM(SUB)            
                 END-PERFORM                                                    
                 MOVE TARGET-SYSTEM        TO   MSG-SYSTEM                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF PARM-STRING(SUB) = ','                                            
              ADD  1                       TO   SUB                             
           ELSE                                                                 
              MOVE SPACES                  TO   FILL-USER                       
              MOVE SPACES                  TO   MSG-USERID                      
           END-IF.                                                              
                                                                                
           IF PARM-STRING-LENGTH >= SUB                                         
              MOVE 1                       TO   SUBY                            
              PERFORM TEST BEFORE VARYING SUBX FROM SUB BY 1                    
                UNTIL SUBX > PARM-STRING-LENGTH                                 
                   OR SUBX > 17                                                 
                 MOVE PARM-STRING(SUBX)    TO   TARGET-USER-ID(SUBY)            
                 ADD 1 TO SUBY                                                  
              END-PERFORM                                                       
                                                                                
              MOVE TARGET-USERID           TO   MSG-USERID                      
           END-IF.                                                              
                                                                                
           IF PARM-STRING-LENGTH = 0                                            
              MOVE ' None'                 TO   FILL-APP                        
           END-IF.                                                              
                                                                                
           IF TARGET-USERID = SPACES OR LOW-VALUES                              
              MOVE LOW-VALUES              TO   TARGET-USERID                   
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       4000-SECTION-1.                                                          
      *==============================================================*          
      *                                                              *          
      *   SECTION-1:   This section will use an EXEC level EXCI call *          
      *                to invoke the program BATCHSER on the target  *          
      *                CICS system to check the state of the target  *          
      *                file, and, if it exists, will set the file to *          
      *                a browsable state.  The CALL-TYPE (1st) slot  *          
      *                of the Commarea will be used to return        *          
      *                information as to how successful the call was.*          
      *                If the call or the server fails in any way,   *          
      *                all further processing is aborted.            *          
      *                                                              *          
      *==============================================================*          
                                                                                
           WRITE BP13L9CJ-REC              FROM MSG00.                          
           WRITE BP13L9CJ-REC              FROM MSG01.                          
           WRITE BP13L9CJ-REC              FROM MSG02.                          
           WRITE BP13L9CJ-REC              FROM MSG32.                          
           WRITE BP13L9CJ-REC              FROM MSG02.                          
           WRITE BP13L9CJ-REC              FROM MSG03.                          
                                                                                
      *--------------------------------------------------------------*          
      *  Set up the Commarea for transmission.                                  
      *--------------------------------------------------------------*          
           WRITE BP13L9CJ-REC              FROM MSG04.                          
                                                                                
      *--------------------------------------------------------------*          
      *  Perform the Link Request;                                              
      *--------------------------------------------------------------*          
           EXEC CICS LINK PROGRAM  (TARGET-PROGRAM)                             
                          TRANSID  (TARGET-TRANSID)                             
                          APPLID   (TARGET-SYSTEM)                              
                          COMMAREA (COMMAREA)                                   
                          LENGTH   (LENGTH OF COMMAREA)                         
                          RETCODE  (EXCI-EXEC-RETURN-CODE)                      
                          SYNCONRETURN                                          
           END-EXEC.                                                            
                                                                                
      *--------------------------------------------------------------*          
      *  Check on how well the request has performed.  We may have              
      *  to abort further processing if either the link, or the                 
      *  server program failed in any way.                                      
      *--------------------------------------------------------------*          
           IF EXEC-RESP IS EQUAL TO ZERO                                        
              PERFORM 4100-CHECK-SERVER    THRU 4100-EXIT                       
           ELSE                                                                 
              PERFORM 4200-LINK-NOT-OK     THRU 4200-EXIT                       
           END-IF.                                                              
                                                                                
           IF ABORT IS EQUAL TO YES                                             
              WRITE BP13L9CJ-REC           FROM MSG02                           
              WRITE BP13L9CJ-REC           FROM MSG31                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       4100-CHECK-SERVER.                                                       
      *==============================================================*          
      *  The following code performs the required response checking             
      *  and will decide whether an abort is neccessary.                        
      *--------------------------------------------------------------*          
                                                                                
           WRITE BP13L9CJ-REC              FROM MSG05.                          
           WRITE BP13L9CJ-REC              FROM MSG06.                          
                                                                                
           EVALUATE SERVER-RETCODE                                              
           WHEN RET-BROWSE                                                      
                WRITE BP13L9CJ-REC         FROM MSG07                           
                MOVE NOT-YET               TO   ABORT                           
                                                                                
           WHEN RET-NOBRWS                                                      
                WRITE BP13L9CJ-REC         FROM MSG09                           
                WRITE BP13L9CJ-REC         FROM MSG14                           
                MOVE YES                   TO   ABORT                           
                MOVE 16                    TO   SAVED-RESPONSE                  
                                                                                
           WHEN RET-NOFILE                                                      
                WRITE BP13L9CJ-REC         FROM MSG08                           
                WRITE BP13L9CJ-REC         FROM MSG14                           
                MOVE YES                   TO   ABORT                           
                MOVE 16                    TO   SAVED-RESPONSE                  
                                                                                
           WHEN RET-ERROR                                                       
                WRITE BP13L9CJ-REC         FROM MSG10                           
                WRITE BP13L9CJ-REC         FROM MSG14                           
                MOVE 16                    TO   SAVED-RESPONSE                  
                MOVE YES                   TO   ABORT                           
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       4200-LINK-NOT-OK.                                                        
      *==============================================================*          
                                                                                
           WRITE BP13L9CJ-REC              FROM MSG11.                          
           MOVE EXEC-RESP                  TO   O-RESP.                         
           MOVE EXEC-RESP2                 TO   O-RESP2.                        
           MOVE SPACES                     TO   OPAD-ABCODE.                    
           MOVE EXEC-ABCODE                TO   O-ABCODE.                       
           WRITE BP13L9CJ-REC              FROM OUTPUT-RETAREA.                 
                                                                                
           IF EXEC-MSG-PTR IS NOT EQUAL TO NULLS                                
              WRITE BP13L9CJ-REC           FROM MSG13                           
              WRITE BP13L9CJ-REC           FROM MSG02                           
              SET ADDRESS OF EXEC-LEVEL-MSG TO EXEC-MSG-PTR                     
              MOVE SPACES                  TO   OUT-REC                         
              PERFORM TEST BEFORE VARYING SUB FROM 1 BY 1                       
                UNTIL SUB > EXEC-MSG-LEN                                        
                 MOVE EXEC-LEVEL-MSG-TEXT(SUB) TO OUT-REC-ELEM(SUB)             
              END-PERFORM                                                       
              WRITE BP13L9CJ-REC           FROM OUT-REC                         
              WRITE BP13L9CJ-REC           FROM MSG02                           
           END-IF.                                                              
                                                                                
           WRITE BP13L9CJ-REC              FROM MSG14.                          
           MOVE YES                        TO   ABORT.                          
           MOVE EXEC-RESP                  TO   SAVED-RESPONSE.                 
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       5000-SECTION-2.                                                          
      *==============================================================*          
      *                                                              *          
      *   SECTION-2:   This routine will perform a Call level EXCI   *          
      *                request by setting up a Pipe to the target    *          
      *                CICS system with Initialise User, Allocate,   *          
      *                and Open Pipe calls.  It will then perform a  *          
      *                series of DPL calls in succession until the   *          
      *                whole of the target file has been retrieved.  *          
      *                Each DPL request will call the BATCHSER server*          
      *                program defined on the target CICS system with*          
      *                the call type of READ.  If an 'Abort Required'*          
      *                situation is detected or when the process is  *          
      *                complete, then control will pass to Section-3.*          
      *                                                              *          
      *==============================================================*          
                                                                                
           WRITE BP13L9CJ-REC              FROM MSG02.                          
           WRITE BP13L9CJ-REC              FROM MSG15.                          
                                                                                
      *--------------------------------------------------------------*          
      *  Perform the Initialise User Call.                                      
      *--------------------------------------------------------------*          
           CALL 'DFHXCIS' USING VERSION-1 EXCI-RETURN-CODE USER-TOKEN           
                          INIT-USER APPLICATION.                                
           WRITE BP13L9CJ-REC              FROM MSG16.                          
                                                                                
      *--------------------------------------------------------------*          
      *  Perform the Allocate Pipe Call.                                        
      *--------------------------------------------------------------*          
           CALL 'DFHXCIS' USING VERSION-1 EXCI-RETURN-CODE USER-TOKEN           
                          ALLOCATE-PIPE PIPE-TOKEN TARGET-SYSTEM                
                          GENERIC-PIPE.                                         
           WRITE BP13L9CJ-REC              FROM MSG17.                          
                                                                                
      *--------------------------------------------------------------*          
      *  Perform the Open Pipe Call.                                            
      *--------------------------------------------------------------*          
           CALL 'DFHXCIS' USING VERSION-1 EXCI-RETURN-CODE USER-TOKEN           
                          OPEN-PIPE PIPE-TOKEN.                                 
           WRITE BP13L9CJ-REC              FROM MSG18.                          
                                                                                
      *--------------------------------------------------------------*          
      *  Check to see if the Pipe has been successfully allocated               
      *  and opened.  If not, then the DPL request will not work,               
      *  so we might as well abort now, and attempt to back out                 
      *  the connection building.  We could attempt to perform an               
      *  EXEC link again to close the target file, but if a pipe                
      *  could not be successfully set up manually, there shoud be              
      *  no reason to believe that this would work, so don't try it.            
      *--------------------------------------------------------------*          
           IF EXCI-RESPONSE IS EQUAL TO ZERO                                    
              WRITE BP13L9CJ-REC           FROM MSG19                           
              WRITE BP13L9CJ-REC           FROM MSG20                           
              WRITE BP13L9CJ-REC           FROM MSG02                           
              WRITE BP13L9CJ-REC           FROM MSG21                           
           ELSE                                                                 
              WRITE BP13L9CJ-REC           FROM MSG23                           
              MOVE EXCI-RESPONSE           TO   O-RESP                          
              MOVE EXCI-REASON             TO   O-RESP2                         
              MOVE EXCI-SUB-REASON1        TO   OEXCI-SUB-REASON1               
              WRITE BP13L9CJ-REC           FROM OUTPUT-RETAREA                  
              WRITE BP13L9CJ-REC           FROM MSG14                           
              MOVE EXCI-RESPONSE           TO   SAVED-RESPONSE                  
              MOVE YES                     TO   ABORT                           
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       6000-DPL-SECTION.                                                        
      *==============================================================*          
      *                                                              *          
      *   DPL-SECTION:   This is the routine which will perform      *          
      *                  multiple sequential DPL calls to the target *          
      *                  CICS system, each time retrieving a single  *          
      *                  record from the target file in the Commarea *          
      *                  until the end of the file is reached.  This *          
      *                  process is performed on the CICS side by the*          
      *                  BATCHSER server with the call type of READ. *          
      *                                                              *          
      *==============================================================*          
                                                                                
           MOVE SPACES                     TO   WS-COMM-AB02B900.               
           MOVE ZEROES                     TO   WS-COMM-NUM-CNT.                
                                                                                
           MOVE F870-NUM-TRANS             TO   WS-COMM-NUM-TRANS.              
           MOVE F870-NUM-HDB-REF           TO   WS-COMM-NUM-HDB-REF.            
           MOVE F870-NUM-REGN              TO   WS-COMM-NUM-REGN.               
           MOVE F870-NUM-CASE              TO   WS-COMM-NUM-CASE.               
           MOVE F870-NUM-OFFICE            TO   WS-COMM-NUM-OFFICE.             
           MOVE F870-NUM-ORIGINATOR        TO   WS-COMM-NUM-TYPE.               
                                                                                
           IF F870-NUM-CPF1 NOT = SPACES AND LOW-VALUES AND ZEROES              
              MOVE F870-NUM-CPF1           TO   WS-COMM-NUM-CPF1                
              ADD  1                       TO   WS-COMM-NUM-CNT                 
           END-IF.                                                              
                                                                                
           IF F870-NUM-UIN-LSE1 NOT = SPACES AND LOW-VALUES AND ZEROES          
              MOVE F870-NUM-UIN-LSE1       TO   WS-COMM-NUM-UIN-LSE1            
           END-IF.                                                              
                                                                                
           IF F870-NUM-CPF2 NOT = SPACES AND LOW-VALUES AND ZEROES              
              MOVE F870-NUM-CPF2           TO   WS-COMM-NUM-CPF2                
              ADD  1                       TO   WS-COMM-NUM-CNT                 
           END-IF.                                                              
                                                                                
           IF F870-NUM-UIN-LSE2 NOT = SPACES AND LOW-VALUES AND ZEROES          
              MOVE F870-NUM-UIN-LSE2       TO   WS-COMM-NUM-UIN-LSE2            
           END-IF.                                                              
                                                                                
           IF F870-NUM-CPF3 NOT = SPACES AND LOW-VALUES AND ZEROES              
              MOVE F870-NUM-CPF3           TO   WS-COMM-NUM-CPF3                
              ADD  1                       TO   WS-COMM-NUM-CNT                 
           END-IF.                                                              
                                                                                
           IF F870-NUM-UIN-LSE3 NOT = SPACES AND LOW-VALUES AND ZEROES          
              MOVE F870-NUM-UIN-LSE3       TO   WS-COMM-NUM-UIN-LSE3            
           END-IF.                                                              
                                                                                
           IF F870-NUM-CPF4 NOT = SPACES AND LOW-VALUES AND ZEROES              
              MOVE F870-NUM-CPF4           TO   WS-COMM-NUM-CPF4                
              ADD  1                       TO   WS-COMM-NUM-CNT                 
           END-IF.                                                              
                                                                                
           IF F870-NUM-UIN-LSE4 NOT = SPACES AND LOW-VALUES AND ZEROES          
              MOVE F870-NUM-UIN-LSE4       TO   WS-COMM-NUM-UIN-LSE4            
           END-IF.                                                              
                                                                                
           IF F870-AMT-LOAN-GRANT IS NOT NUMERIC                                
              MOVE ZEROES                  TO   WS-COMM-AMT-LOAN-GRANT          
           ELSE                                                                 
              MOVE F870-AMT-LOAN-GRANT     TO   WS-COMM-AMT-LOAN-GRANT          
           END-IF.                                                              
                                                                                
           IF F870-DTE-LOAN-COMM NOT = SPACES AND LOW-VALUES AND ZEROES         
              MOVE F870-DTE-LOAN-COMM      TO   WS-COMM-DTE-LOAN-COMM           
           ELSE                                                                 
              MOVE ZEROES                  TO   WS-COMM-DTE-LOAN-COMM           
           END-IF.                                                              
                                                                                
           MOVE F870-NUM-LOAN-SCHEME       TO   WS-COMM-NUM-LOAN-SCHEME.        
           MOVE F870-NUM-USERID            TO   WS-COMM-NUM-USERID.             
           MOVE F870-NUM-SYSTEM            TO   WS-COMM-NUM-SYSTEM.             
                                                                                
      *--------------------------------------------------------------*          
      *  No Userid nor Uowid is passed. This shows how to leave out             
      *  a parameter from the list. A null pointer must replace the             
      *  pointer in the list which would normally point to the parameter        
      *  missing.                                                               
      *--------------------------------------------------------------*          
           SET ADDRESS OF NULL-PTR         TO   NULLS.                          
                                                                                
      *--------------------------------------------------------------*          
      *  Set up the call, and perform the DPL request.                          
      *--------------------------------------------------------------*          
           CALL 'DFHXCIS' USING VERSION-1 EXCI-RETURN-CODE USER-TOKEN           
                          DPL-REQUEST PIPE-TOKEN TARGET-PROGRAM                 
                          COMMAREA COMM-LENGTH DATA-LENGTH                      
                          TARGET-TRANSID NULL-PTR TARGET-USERID                 
                          EXCI-DPL-RETAREA SYNCONRETURN.                        
                                                                                
      *--------------------------------------------------------------*          
      *  Check how successful the call was.  If it has worked, then             
      *  we have a record on the Commarea to be output to the                   
      *  SYSPRINT log.                                                          
      *--------------------------------------------------------------*          
           IF WS-COMM-ERR-CDE NOT = '0000' AND SPACES AND LOW-VALUES            
              MOVE SPACES                  TO   BP13L9CJ-REC                    
              INITIALIZE                        BP13L9CJ-REC                    
              STRING '   ' WS-COMM-NUM-REGN ' '                                 
                     WS-COMM-ERR-CDE ' '                                        
                     WS-COMM-ERR-MSG                                            
                     DELIMITED BY SIZE INTO BP13L9CJ-REC                        
              WRITE BP13L9CJ-REC                                                
           ELSE                                                                 
              ADD 1                        TO   WS-SUCCESS-CNT                  
           END-IF                                                               
                                                                                
           IF EXCI-RESPONSE IS = ZERO                                           
              IF EXCI-DPL-RESP IS NOT = ZERO                                    
                 WRITE BP13L9CJ-REC        FROM MSG25                           
                 WRITE BP13L9CJ-REC        FROM MSG27                           
                 MOVE EXCI-DPL-RESP        TO   O-RESP                          
                 MOVE EXCI-DPL-RESP2       TO   O-RESP2                         
                 MOVE SPACES               TO   OPAD-ABCODE                     
                 MOVE EXCI-DPL-ABCODE      TO   O-ABCODE                        
                 WRITE BP13L9CJ-REC        FROM OUTPUT-RETAREA                  
                 WRITE BP13L9CJ-REC        FROM MSG14                           
                 MOVE YES                  TO   ABORT                           
                 MOVE EXCI-DPL-RESP        TO   SAVED-RESPONSE                  
              ELSE                                                              
                 IF SERVER-RETCODE IS = RET-ERROR                               
                    WRITE BP13L9CJ-REC     FROM MSG06                           
                    WRITE BP13L9CJ-REC     FROM MSG10                           
                    MOVE YES               TO   ABORT                           
                    MOVE 16                TO   SAVED-RESPONSE                  
                 ELSE                                                           
                    PERFORM 2000-READ-AB02F870  THRU 2000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              WRITE BP13L9CJ-REC           FROM MSG25                           
              WRITE BP13L9CJ-REC           FROM MSG26                           
              MOVE EXCI-RESPONSE           TO   O-RESP                          
              MOVE EXCI-REASON             TO   O-RESP2                         
              MOVE EXCI-SUB-REASON1        TO   OEXCI-SUB-REASON1               
              WRITE BP13L9CJ-REC           FROM OUTPUT-RETAREA                  
              IF EXCI-MSG-PTR IS NOT EQUAL TO NULLS                             
                 WRITE BP13L9CJ-REC        FROM MSG13                           
                 WRITE BP13L9CJ-REC        FROM MSG02                           
                 SET ADDRESS OF CALL-LEVEL-MSG TO EXCI-MSG-PTR                  
                 MOVE SPACES               TO   OUT-REC                         
                 SUBTRACT 4 FROM CALL-LEVEL-MSG-LEN                             
                 PERFORM TEST BEFORE VARYING SUB FROM 1 BY 1                    
                   UNTIL SUB > CALL-LEVEL-MSG-LEN                               
                    MOVE CALL-LEVEL-MSG-TEXT(SUB) TO OUT-REC-ELEM(SUB)          
                 END-PERFORM                                                    
                 WRITE BP13L9CJ-REC        FROM OUT-REC                         
                 WRITE BP13L9CJ-REC        FROM MSG02                           
              END-IF                                                            
              WRITE BP13L9CJ-REC           FROM MSG27                           
              MOVE EXCI-DPL-RESP           TO   O-RESP                          
              MOVE EXCI-DPL-RESP2          TO   O-RESP2                         
              MOVE SPACES                  TO   OPAD-ABCODE                     
              MOVE EXCI-DPL-ABCODE         TO   O-ABCODE                        
              WRITE BP13L9CJ-REC           FROM OUTPUT-RETAREA                  
              WRITE BP13L9CJ-REC           FROM MSG14                           
              MOVE YES                     TO   ABORT                           
           END-IF.                                                              
                                                                                
           MOVE EXCI-RESPONSE              TO   SAVED-RESPONSE.                 
                                                                                
           IF SERVER-RETCODE IS = RET-EOF                                       
              WRITE BP13L9CJ-REC           FROM MSG22                           
              WRITE BP13L9CJ-REC           FROM MSG02                           
              MOVE YES                     TO   ABORT                           
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       7000-SECTION-3.                                                          
      *==============================================================*          
      *                                                              *          
      *   SECTION-3:  Whether or not SECTION-II processing was a     *          
      *               success, we assume that at least all the calls *          
      *               were completed. Hence we perform a further DPL *          
      *               call to the server program to set the Target   *          
      *               file Closed, and continue by Closing and       *          
      *               Deallocating the Pipe to the Target CICS system*          
      *               to shut down the Link before exiting to MVS.   *          
      *                                                              *          
      *==============================================================*          
                                                                                
      *--------------------------------------------------------------*          
      *  Perform Close Pipe call.                                               
      *--------------------------------------------------------------*          
           CALL 'DFHXCIS' USING VERSION-1 EXCI-RETURN-CODE USER-TOKEN           
                          CLOSE-PIPE PIPE-TOKEN.                                
           WRITE BP13L9CJ-REC             FROM MSG29.                           
                                                                                
      *--------------------------------------------------------------*          
      *  Perform Deallocate Pipe call.                                          
      *--------------------------------------------------------------*          
           CALL 'DFHXCIS' USING VERSION-1 EXCI-RETURN-CODE USER-TOKEN           
                          DEALLOCATE-PIPE PIPE-TOKEN.                           
           WRITE BP13L9CJ-REC             FROM MSG30.                           
                                                                                
           WRITE BP13L9CJ-REC             FROM MSG02.                           
           WRITE BP13L9CJ-REC             FROM MSG31.                           
           MOVE WS-CNT-F870-READ          TO   WS-CTR-READ.                     
           MOVE WS-SUCCESS-CNT            TO   WS-CTR-SUCC.                     
           WRITE BP13L9CJ-REC             FROM WS-CTR-LINE1.                    
           WRITE BP13L9CJ-REC             FROM WS-CTR-LINE2.                    
           WRITE BP13L9CJ-REC             FROM WS-CTR-LINE3.                    
           WRITE BP13L9CJ-REC             FROM WS-CTR-LINE1.                    
           WRITE BP13L9CJ-REC             FROM WS-CTR-LINE4.                    
           WRITE BP13L9CJ-REC             FROM WS-CTR-LINE5.                    
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============================================================*          
       9000-CLOSE-ROUTINE.                                                      
      *==============================================================*          
                                                                                
           CLOSE AB02F870                                                       
                 BP13L9CJ.                                                      
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C9CJ CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF AB02F870 READ      : ' WS-CNT-F870-READ.          
           DISPLAY ' 2. NO OF AB02F870 SKIPPED   : ' WS-CNT-F870-SKIP.          
           DISPLAY ' 3. NO OF BP13K680 WRITTEN   : ' WS-CNT-K680-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
      *    IF WS-CNT-F870-SKIP NOT = WS-SUCCESS-CNT                             
      *       MOVE 99                     TO SAVED-RESPONSE                     
      *    END-IF.                                                              
                                                                                
           MOVE SAVED-RESPONSE            TO RETURN-CODE.                       
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
