       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB9K.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  26 JUL 2016.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  MATCH BP13F730 (8AM) WITH BP13F730(11AM)      *                
      *          OUTPUT CASES NOT FOUND IN 8AM FILE            *                
      *          OUTPUT CASES FOUND IN 8AM FILE WITH CHANGE    *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136363 FNP1 260716  NEW PROGRAM                      *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT BP13F55Y ASSIGN TO BP13F55Y.                                  
           SELECT P13F55YA ASSIGN TO P13F55YA.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F55Y                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F55Y.                                                           
                                                                                
       FD  P13F55YA                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  P13F55YA-REC               PIC X(250).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F730-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F55Y-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F55YA-WRITE        PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF         PIC X       VALUE 'N'.                       
           05  WS-F55Y-EOF          PIC X       VALUE 'N'.                      
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-F730-MATCHED      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F730-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
           05  WS-F55Y-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F730   THRU 1100-EXIT.                         
           PERFORM 1200-READ-BP13F55Y   THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL F730-NUM-REGN = HIGH-VALUES AND                              
                   F55Y-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F55Y                                                 
                       BP13F730                                                 
                OUTPUT P13F55YA.                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F730.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F730 AT END                                                 
              MOVE HIGH-VALUES TO F730-NUM-REGN                                 
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1200-READ-BP13F55Y.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F55Y AT END                                                 
              MOVE HIGH-VALUES TO F55Y-NUM-REGN                                 
                GO TO 1200-EXIT.                                                
                                                                                
           ADD 1 TO WS-F55Y-READ.                                               
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           IF F730-NUM-REGN = F55Y-NUM-REGN                                     
              ADD 1 TO WS-F730-MATCHED                                          
              IF F55Y-NUM-BAL-HH = SPACES OR LOW-VALUES                         
                 IF F55Y-NUM-FT2T = 'Y'                                         
                    MOVE 'F'   TO F55Y-NUM-BAL-HH                               
                 ELSE                                                           
                    MOVE F55Y-NUM-HH          TO F55Y-NUM-BAL-HH                
                 END-IF                                                         
              END-IF                                                            
              WRITE P13F55YA-REC FROM BP13F55Y-REC                              
              ADD 1 TO WS-F55YA-WRITE                                           
              PERFORM 1100-READ-BP13F730    THRU 1100-EXIT                      
              PERFORM 1200-READ-BP13F55Y    THRU 1200-EXIT                      
           ELSE                                                                 
              IF F730-NUM-REGN < F55Y-NUM-REGN                                  
                 ADD 1 TO WS-F730-NOMATCH                                       
                 PERFORM 1100-READ-BP13F730     THRU 1100-EXIT                  
              ELSE                                                              
                 ADD 1 TO WS-F55Y-NOMATCH                                       
                 PERFORM 1200-READ-BP13F55Y   THRU 1200-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13CB9K END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF REC READ FROM BP13F730    ' WS-F730-READ.             
           DISPLAY 'NO OF REC READ FROM BP13F55Y    ' WS-F55Y-READ.             
           DISPLAY 'NO OF REC MATCHED W/ F55Y       ' WS-F730-MATCHED.          
           DISPLAY 'FOUND F55YA   NOT MTCH W/ F55Y  ' WS-F730-NOMATCH.          
           DISPLAY 'FOUND F55Y    NOT MTCH W/ F55YA ' WS-F55Y-NOMATCH.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF OUPUT                     ' WS-F55YA-WRITE.           
                                                                                
                                                                                
           CLOSE BP13F55Y                                                       
                 BP13F730                                                       
                 P13F55YA.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
