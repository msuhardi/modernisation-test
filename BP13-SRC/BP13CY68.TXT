       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY68.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 02 MAY 2008                          *              
      *                                                          *              
      * 1. OBJECTIVE      : BTO APPLICATION, USE NRIC TO MATCH   *              
      *                   : RESALE, IF FOUND COUNT AS RESALE CASE*              
      *                   : IF NOT FOUND, CONTINUE TO MACTH SMS. *              
      *                                                          *              
      * 2. INPUT          : BP13F595 -                           *              
      *                   : BB24K020 - BB24.K020.OCCUPIER        *              
      *                                                          *              
      * 3. OUTPUT         : BP13F557                             *              
      *                   : P13F595N                             *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP133340 02/05/2008 JB8  NEW PROGRAM. (BP13C994)        *              
      *  BP135615 26/12/2014 IMC  INCREASE F557 LENGTH TO 400    *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT BB24K020     ASSIGN        TO BB24K020                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K020-KEY-FLD                    
                               ALTERNATE RECORD KEY IS K020-NUM-UIN             
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K020-STATUS.                 
                                                                                
           SELECT BB24K010     ASSIGN        TO BB24K010                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K010-KEY-FLD                    
                               FILE STATUS   IS WS-K010-STATUS.                 
                                                                                
           SELECT BP13F557     ASSIGN        TO BP13F557.                       
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
                                                                                
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  BB24K020                                                             
                               RECORD CONTAINS 200 CHARACTERS.                  
       COPY BB24K020.                                                           
                                                                                
       FD  BB24K010                                                             
                               RECORD CONTAINS 5000 CHARACTERS.                 
       COPY BB24K010.                                                           
                                                                                
       FD  BP13F557                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 400 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F557.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC                  PIC X(500).                            
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC                  PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K020-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K010-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K020-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-F557-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K020-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K020-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
       01  WS-ALLOC-SCHEME          PIC X(2)    VALUE SPACES.                   
           88 VALID-ALLOC-SCH  VALUE 'H1' 'H2' 'H3' 'H4' 'H5' 'H6' 'H7'         
                                     'A1' 'A2' 'A3' 'A4' 'A5' 'A6' 'A7'         
                                     'T1' 'T2' 'T3' 'G1' 'G2' 'G3'              
                                     'U1' 'U2' 'U3' 'F1' 'F2' 'F3'              
                                     'I1' 'I2' 'I3' 'J1' 'J2' 'J3'              
                                     'W1' 'W2' 'W3' 'V1' 'V2' 'V3'.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BB24K020                                                 
                       BB24K010                                                 
                OUTPUT BP13F557                                                 
                       P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 OPEN ERROR ' WS-K020-STATUS                     
              MOVE WS-K020-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K010 OPEN ERROR ' WS-K010-STATUS                     
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO BP13F557-REC                        
                                            P13F595N-REC                        
                                                                                
                   PERFORM 3000-START-BB24K020 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'Y'                                               
                                                                                
              MOVE SPACES                       TO BP13F557-REC                 
              MOVE F595-NUM-REGN                TO F557-NUM-REGN                
              MOVE F595-NUM-ALLO-CAT            TO F557-CDE-SALES-MODE          
              MOVE F595-DTE-BALLOT              TO F557-DTE-QUARTER             
              MOVE F595-CDE-HOUSEHOLD           TO F557-NUM-HH                  
              MOVE F595-CDE-FLAT-TYPE           TO F557-NUM-FLAT-TYPE           
              MOVE F595-NUM-NRIC1               TO F557-NUM-NRIC1               
              MOVE K010-DTE-UPDATE-APPLN TO F557-DTE-SALE-EFF-RSL-HA1           
              MOVE K010-CDE-TYP-ACTLUSE     TO F557-NUM-FT-RSL-HA1              
              MOVE K010-AMT-RESL-PRICE      TO F557-AMT-SELLING-PRICE           
                                                                                
              WRITE BP13F557-REC                                                
              ADD 1                             TO WS-F557-WRITE-CNT            
              MOVE BP13F595-REC          TO P13F595F-REC                        
                                                                                
              WRITE P13F595F-REC                                                
                                                                                
              ADD 1                      TO WS-595F-WRITE-CNT                   
           ELSE                                                                 
              MOVE BP13F595-REC          TO P13F595N-REC                        
                                                                                
              WRITE P13F595N-REC                                                
                                                                                
              ADD 1                      TO WS-595N-WRITE-CNT                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-BB24K020.                                                     
      *************************************************************             
           MOVE SPACES                   TO BB24K020-REC.                       
           INITIALIZE                       BB24K020-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K020-NUM-UIN                        
                                            WS-K020-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K020-EOF                         
                                            WS-K020-REC-PROCESS.                
                                                                                
           START BB24K020 KEY >= K020-NUM-UIN.                                  
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BB24K020 THRU 3100-EXIT               
                      UNTIL K020-NUM-UIN NOT = WS-K020-NRIC-PREV                
                         OR WS-K020-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
      *             DISPLAY 'NRIC NOT FOUND IN K020: ' K020-NUM-UIN             
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 START ERROR ' WS-K020-STATUS              
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-BB24K020.                                                  
      *************************************************************             
           READ BB24K020 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K020-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K020-NUM-UIN = WS-K020-NRIC-PREV                         
                       MOVE K020-NUM-CASE TO K010-KEY-FLD                       
                       PERFORM 3200-READ-BB24K010  THRU 3200-EXIT               
                       IF VALID-ALLOC-SCH                                       
                         MOVE 'Y'          TO WS-NRIC-FOUND                     
                                            WS-K020-REC-PROCESS                 
                       END-IF                                                   
                    ELSE                                                        
                       IF WS-K020-REC-PROCESS NOT = 'Y'                         
                          CONTINUE                                              
                       END-IF                                                   
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 READ NEXT ERROR ' WS-K020-STATUS          
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3200-READ-BB24K010.                                                      
      *************************************************************             
                                                                                
           READ BB24K010                                                        
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
                    MOVE K010-CDE-SCH-ALLOC-BYR  TO WS-ALLOC-SCHEME             
               WHEN 23                                                          
                    CONTINUE                                                    
                    DISPLAY 'CASE NOT FOUND IN K010: ' K010-NUM-CASE            
               WHEN OTHER                                                       
                    DISPLAY 'BP13K010 READ NEXT ERROR ' WS-K010-STATUS          
                    DISPLAY 'K010 KEY: ' K010-KEY-FLD                           
                    MOVE WS-K010-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CY68 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS WRITTEN TO BP13F557 : ' WS-F557-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F595F : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F595N : ' WS-595N-WRITE-CNT.             
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 BB24K020                                                       
                 BB24K010                                                       
                 BP13F557                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 CLOSE ERROR ' WS-K020-STATUS                    
              MOVE WS-K020-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K010 CLOSE ERROR ' WS-K010-STATUS                    
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
