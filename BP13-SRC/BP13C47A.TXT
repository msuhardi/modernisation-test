       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C47A.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  17/08/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : (1) CLEAN-UP CASES FOR MONITOR PENDING ECPF *            
      *                    FOR SOCA                                *            
      *                (2) GENERATE SMS FILE FOR BYPASSED CASES    *            
      *                    WITH NO ACTION AFTER 10 DAYS            *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F60K                               *            
      *                 2.) BP13K300                               *            
      *                 3.) BP13K600                               *            
      * I-O    FILES :  1.) BP13K60K                               *            
      * OUTPUT FILES :  1.) BP13F203                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136757 KSJ3 17/08/2017 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F60K  ASSIGN        TO BP13F60K.                          
                                                                                
           SELECT BP13K330  ASSIGN        TO BP13K330                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K330-KEY-FLD                       
                            FILE STATUS   IS WS-K330-STATUS.                    
                                                                                
           SELECT BP13K600  ASSIGN        TO BP13K600                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K600-KEY-FLD                       
                            ALTERNATE KEY IS K600-NUM-SNO                       
                            FILE STATUS   IS WS-K600-STATUS.                    
                                                                                
           SELECT BP13K60K  ASSIGN        TO BP13K60K                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K60K-KEY-FLD                       
                            FILE STATUS   IS WS-K60K-STATUS.                    
                                                                                
           SELECT BP13F203  ASSIGN        TO BP13F203.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F60K                                                             
           RECORD CONTAINS  200 CHARACTERS.                                     
           COPY BP13F60K.                                                       
                                                                                
       FD  BP13K330.                                                            
           COPY BP13K330.                                                       
                                                                                
       FD  BP13K600.                                                            
           COPY BP13K600.                                                       
                                                                                
       FD  BP13K60K.                                                            
           COPY BP13K60K.                                                       
                                                                                
       FD  BP13F203                                                             
           RECORD CONTAINS  200 CHARACTERS.                                     
           COPY BP13F203.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K330-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K600-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K60K-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F60K-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K330-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K600-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K60K-DEL           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K60K-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F203-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F60K-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K330-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K600-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K60K-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
           05  WS-DTE-UPD                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-UPD-INT            PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-BYPASS-TMP         PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-AUTH-DUE           PIC 9(8)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                      PIC 9(1)   VALUE ZEROES.               
           05  WS-CNT-NRIC               PIC 9(1)   VALUE ZEROES.               
           05  WS-CNT-AUTH               PIC 9(1)   VALUE ZEROES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  FILLER                    OCCURS 4 TIMES.                        
               10  WS-NUM-SNO            PIC X(09)  VALUE SPACES.               
               10  WS-NUM-HP             PIC X(08)  VALUE SPACES.               
               10  WS-NUM-AUTH           PIC X(01)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F60K-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F60K-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F60K                                                 
                       BP13K330                                                 
                       BP13K600                                                 
                I-O    BP13K60K                                                 
                OUTPUT BP13F203.                                                
                                                                                
           IF WS-K330-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K330 ERROR ' WS-K330-STATUS                  
              MOVE     WS-K330-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K600-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K600 ERROR ' WS-K600-STATUS                  
              MOVE     WS-K600-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K60K-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K60K ERROR ' WS-K60K-STATUS                  
              MOVE     WS-K60K-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
KSJ        MOVE '20170831'                 TO   WS-DTE-CURR-8                   
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
KSJ        DISPLAY 'WS-DTE-CURR-8     = ' WS-DTE-CURR-8.                        
                                                                                
      *-------------------------------------------------------------            
      *    COMPUTE FOR BYPASS DATE WHERE SMS MUST BE SENT                       
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-8              TO   WS-DTE-UPD.                     
           COMPUTE WS-DTE-UPD-INT = FUNCTION INTEGER-OF-DATE                    
                                    (WS-DTE-UPD).                               
           SUBTRACT 10                     FROM WS-DTE-UPD-INT.                 
           COMPUTE WS-DTE-UPD     = FUNCTION DATE-OF-INTEGER                    
                                    (WS-DTE-UPD-INT).                           
           MOVE WS-DTE-UPD                 TO   WS-DTE-BYPASS-TMP.              
KSJ        DISPLAY 'WS-DTE-BYPASS-TMP = ' WS-DTE-BYPASS-TMP.                    
                                                                                
      *-------------------------------------------------------------            
      *    COMPUTE FOR NEW DUE DATE (USED IN SMS)                               
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-8              TO   WS-DTE-UPD.                     
           COMPUTE WS-DTE-UPD-INT = FUNCTION INTEGER-OF-DATE                    
                                    (WS-DTE-UPD).                               
           ADD 3                           TO   WS-DTE-UPD-INT.                 
           COMPUTE WS-DTE-UPD     = FUNCTION DATE-OF-INTEGER                    
                                    (WS-DTE-UPD-INT).                           
           MOVE WS-DTE-UPD                 TO   WS-DTE-AUTH-DUE.                
KSJ        DISPLAY 'WS-DTE-AUTH-DUE   = ' WS-DTE-AUTH-DUE.                      
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F60K-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F60K NEXT                                                   
              AT END MOVE 'Y'              TO   WS-F60K-EOF.                    
                                                                                
           IF WS-F60K-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F60K-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
KSJ        DISPLAY '*******************************'                            
KSJ        DISPLAY 'F60K-NUM-REGN     = ' F60K-NUM-REGN.                        
           MOVE SPACES                     TO   BP13K330-REC.                   
           MOVE F60K-NUM-REGN(1:8)         TO   K330-CURR-REG.                  
           MOVE '1'                        TO   K330-SEQ-NO.                    
                                                                                
           PERFORM 3010-BP13K330-READ      THRU 3010-EXIT.                      
                                                                                
           IF WS-K330-FND NOT = 'Y'                                             
              GO TO 3000-CONTINUE-NEXT-REC                                      
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    INITIALIZE ARRAY                                                     
      *-------------------------------------------------------------            
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              MOVE SPACES                  TO   WS-NUM-SNO(WS-I)                
                                                WS-NUM-HP(WS-I)                 
                                                WS-NUM-AUTH(WS-I)               
           END-PERFORM.                                                         
                                                                                
      *-------------------------------------------------------------            
      *    GET SERIAL NOS FROM AGR ORDER FILE AND WRITE SNO TO ARRAY            
      *-------------------------------------------------------------            
           MOVE ZEROES                     TO   WS-I.                           
                                                                                
           IF K330-NUM-SERIAL-CPF1 NOT = SPACES AND LOW-VALUES AND              
                                         ZEROES                                 
              ADD 1                        TO   WS-I                            
              STRING 'SS' K330-NUM-SERIAL-CPF1                                  
                     DELIMITED BY SIZE     INTO WS-NUM-SNO(WS-I)                
           END-IF.                                                              
                                                                                
           IF K330-NUM-SERIAL-CPF2 NOT = SPACES AND LOW-VALUES AND              
                                         ZEROES                                 
              ADD 1                        TO   WS-I                            
              STRING 'SS' K330-NUM-SERIAL-CPF2                                  
                     DELIMITED BY SIZE     INTO WS-NUM-SNO(WS-I)                
           END-IF.                                                              
                                                                                
           IF K330-NUM-SERIAL-CPF3 NOT = SPACES AND LOW-VALUES AND              
                                         ZEROES                                 
              ADD 1                        TO   WS-I                            
              STRING 'SS' K330-NUM-SERIAL-CPF3                                  
                     DELIMITED BY SIZE     INTO WS-NUM-SNO(WS-I)                
           END-IF.                                                              
                                                                                
           IF K330-NUM-SERIAL-CPF4 NOT = SPACES AND LOW-VALUES AND              
                                         ZEROES                                 
              ADD 1                        TO   WS-I                            
              STRING 'SS' K330-NUM-SERIAL-CPF4                                  
                     DELIMITED BY SIZE     INTO WS-NUM-SNO(WS-I)                
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    INIT COUNT.                                                          
      *    WS-CNT-NRIC = TOTAL # OF APPLICANTS UNDER REGN                       
      *    WS-CNT-AUTH = # OF APPLICANTS WHO HAS AUTHORIZED ECPF FORM           
      *-------------------------------------------------------------            
           MOVE WS-I                       TO   WS-CNT-NRIC.                    
           MOVE ZEROES                     TO   WS-CNT-AUTH.                    
                                                                                
      *-------------------------------------------------------------            
      *    CHECK EACH SNO FOR EHPS STATUS. GET HP IN CASE SEND SMS              
      *-------------------------------------------------------------            
           PERFORM VARYING WS-I FROM 1 BY 1                                     
                     UNTIL (WS-NUM-SNO(WS-I) = SPACES)                          
                        OR (WS-I > 4)                                           
              MOVE SPACES                  TO   BP13K600-REC                    
              MOVE WS-NUM-SNO(WS-I)        TO   K600-NUM-SNO                    
KSJ           DISPLAY 'K600-NUM-SNO      = ' K600-NUM-SNO                       
                                                                                
              PERFORM 3020-BP13K600-READ   THRU 3020-EXIT                       
                                                                                
              IF WS-K600-FND = 'Y'                                              
                 MOVE K600-NUM-HP          TO   WS-NUM-HP(WS-I)                 
KSJ           DISPLAY 'K600-DATE-EHPS-SUB= ' K600-DATE-EHPS-SUB                 
                 IF K600-DATE-EHPS-SUB NOT = SPACES AND LOW-VALUES AND          
                                             ZEROES                             
                    ADD 1                  TO   WS-CNT-AUTH                     
                    MOVE 'Y'               TO   WS-NUM-AUTH(WS-I)               
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                     TO   WS-CNT-AUTH                     
                 MOVE 'Y'                  TO   WS-NUM-AUTH(WS-I)               
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
      *-------------------------------------------------------------            
      *    IF ALL HAS AUTHORIZED, DELETE FROM BP13K60K                          
      *    ELSE, CHECK IF SMS IS DUE AS REMINDER TO SIGN ECPF FORM              
      *-------------------------------------------------------------            
KSJ        DISPLAY 'WS-CNT-AUTH       = ' WS-CNT-AUTH                           
KSJ        DISPLAY 'WS-CNT-NRIC       = ' WS-CNT-NRIC                           
           IF WS-CNT-AUTH = WS-CNT-NRIC                                         
              MOVE SPACES                  TO   BP13K60K-REC                    
              MOVE F60K-KEY-FLD            TO   K60K-KEY-FLD                    
              PERFORM 3030-BP13K60K-DELETE THRU 3030-EXIT                       
           ELSE                                                                 
KSJ        DISPLAY 'F60K-DTE-BYPASS   = ' F60K-DTE-BYPASS                       
KSJ        DISPLAY 'F60K-DTE-SMS-SENT = ' F60K-DTE-SMS-SENT                     
              IF (F60K-DTE-BYPASS <= WS-DTE-BYPASS-TMP) AND                     
                 (F60K-DTE-SMS-SENT = SPACES OR LOW-VALUES OR ZEROES)           
                 PERFORM VARYING WS-I FROM 1 BY 1                               
                           UNTIL (WS-NUM-SNO(WS-I) = SPACES)                    
                              OR (WS-I > 4)                                     
                    IF (WS-NUM-HP(WS-I) NOT = SPACES AND LOW-VALUES AND         
                                              ZEROES) AND                       
                       (WS-NUM-AUTH(WS-I) NOT = 'Y')                            
KSJ        DISPLAY 'WS-NUM-SNO        = ' WS-NUM-SNO(WS-I)                      
KSJ        DISPLAY 'WS-NUM-HP         = ' WS-NUM-HP(WS-I)                       
                       PERFORM 3050-BP13F203-WRITE THRU 3050-EXIT               
                    END-IF                                                      
                 END-PERFORM                                                    
                                                                                
                 MOVE BP13F60K-REC         TO   BP13K60K-REC                    
                 MOVE WS-DTE-CURR-8        TO   K60K-DTE-SMS-SENT               
                                                K60K-DTE-UPDATE                 
                 MOVE FUNCTION CURRENT-DATE(9:9)                                
                                           TO   K60K-TME-UPDATE                 
                 MOVE 'BP13C47A'           TO   K60K-NUM-USERID                 
                 PERFORM 3040-BP13K60K-REWRITE  THRU 3040-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-CONTINUE-NEXT-REC.                                                  
                                                                                
           PERFORM 2000-BP13F60K-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13K330-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K330.                                                       
                                                                                
           EVALUATE WS-K330-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K330-FND                     
              ADD 1                        TO   WS-CNT-K330-READ                
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K330-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K330 ERROR '    WS-K330-STATUS                  
                      ' / '                     K330-KEY-FLD                    
              MOVE     WS-K330-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3020-BP13K600-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K600 KEY IS K600-NUM-SNO.                                   
                                                                                
           EVALUATE WS-K600-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K600-FND                     
              ADD 1                        TO   WS-CNT-K600-READ                
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K600-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K600 ERROR '    WS-K600-STATUS                  
                      ' / '                     K330-KEY-FLD                    
              MOVE     WS-K600-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3030-BP13K60K-DELETE.                                                    
      *=============================================================            
                                                                                
           DELETE BP13K60K.                                                     
                                                                                
           EVALUATE WS-K60K-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K60K-DEL                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'DELETE ERROR ' K60K-KEY-FLD '-' WS-K60K-STATUS           
           END-EVALUATE.                                                        
                                                                                
       3030-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3040-BP13K60K-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K60K-REC.                                                
                                                                                
           EVALUATE WS-K60K-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-K60K-UPD                 
                                                                                
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K60K ERROR '   WS-K60K-STATUS                
              MOVE     WS-K60K-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3040-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3050-BP13F203-WRITE.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13F203-REC.                   
           MOVE 'C1'                       TO   F203-TYPE-APPMT.                
           MOVE F60K-NUM-REGN              TO   F203-NUM-REGN.                  
           MOVE WS-DTE-AUTH-DUE            TO   F203-DTE-BOOK-PD-END.           
           MOVE WS-NUM-HP(WS-I)            TO   F203-NUM-HP.                    
                                                                                
           WRITE BP13F203-REC.                                                  
           ADD 1                           TO   WS-CNT-F203-WRITE.              
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F60K                                                       
                 BP13K330                                                       
                 BP13K600                                                       
                 BP13K60K                                                       
                 BP13F203.                                                      
                                                                                
           IF WS-K330-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K330 ERROR ' WS-K330-STATUS                  
              MOVE     WS-K330-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K600-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K600 ERROR ' WS-K600-STATUS                  
              MOVE     WS-K600-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K60K-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K60K ERROR ' WS-K60K-STATUS                  
              MOVE     WS-K60K-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C47A CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F60K READ      : ' WS-CNT-F60K-READ.          
           DISPLAY ' 2. NO OF BP13K330 READ      : ' WS-CNT-K330-READ.          
           DISPLAY ' 3. NO OF BP13K600 READ      : ' WS-CNT-K600-READ.          
           DISPLAY ' 4. NO OF BP13K60K DELETED   : ' WS-CNT-K60K-DEL.           
           DISPLAY ' 5. NO OF BP13K60K UPDATED   : ' WS-CNT-K60K-UPD.           
           DISPLAY ' 6. NO OF BP13F203 WRITTEN   : ' WS-CNT-F203-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
