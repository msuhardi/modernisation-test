       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C886.                                                 
      *AUTHOR.        EVM.                                                      
      *DATE-WRITTEN.  25/07/96.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   PROGRAM TO WRITE BP13F740 REGISTRATION NUMBER  *                
      *         TO BP17K000 REGISTRATION NUMBER.               *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13F740                                       *                
      *                                                        *                
      *    I-O FILES    :                                      *                
      *    1.   BP13K740                                       *                
      *    2.   BP17K000                                       *                
      *                                                        *                
      * CHG-NO.  BY  ON       DESCRIPTION                      *                
      * -------- --- -------- -----------                      *                
      * BP130118 EVM 19960725 NEW PROGRAM                      *                
      * BP130652 NOR 19990222 EXPAND F740 RECL FROM 300 TO 500 *                
      * BP132064 YCH 20041011 EXPAND BP17F000 FROM 80 TO 200   *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740   ASSIGN TO BP13F740.                                
                                                                                
           SELECT BP17K000   ASSIGN TO BP17K000                                 
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K000-KEY-FLD                                    
                  ALTERNATE RECORD KEY IS K000-NUM-REF                          
                  FILE STATUS IS WS-K000-STATUS.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F740.                                                           
                                                                                
       FD   BP17K000                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP17K000.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K000-STATUS              PIC 99.                                  
       01  WS-F740-EOF                 PIC X(1).                                
           88  F740-EOF                            VALUE 'Y'.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-YY              PIC 9999.                                
           05  WS-CURR-MM              PIC 99.                                  
           05  WS-CURR-DD              PIC 99.                                  
                                                                                
       01  WS-F740-READ                PIC 9(7)   VALUE ZEROES.                 
       01  WS-K000-READ                PIC 9(7)   VALUE ZEROES.                 
       01  WS-K000-REWRITE             PIC 9(7)   VALUE ZEROES.                 
       01  WS-K000-NOTFND              PIC 9(7)   VALUE ZEROES.                 
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F740   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-F740    THRU 3000-EXIT                          
                        UNTIL F740-EOF.                                         
                                                                                
           DISPLAY 'PROGRAM : BP13C886    '.                                    
           DISPLAY 'DATE    : ' WS-CURRENT-DATE.                                
           DISPLAY '*=========== COUNTERS ===========*'                         
           DISPLAY 'F740-REC-READ     : ' WS-F740-READ.                         
           DISPLAY 'K000-REC-READ     : ' WS-K000-READ.                         
           DISPLAY 'K000-REC-NOTFND   : ' WS-K000-NOTFND.                       
           DISPLAY 'K000-REC-UPDATED  : ' WS-K000-REWRITE.                      
                                                                                
           PERFORM 9999-CLOSE-RTN THRU 9999-EXIT.                               
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
                                                                                
           OPEN INPUT   BP13F740                                                
                I-O     BP17K000.                                               
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              MOVE WS-K000-STATUS      TO RETURN-CODE                           
              DISPLAY 'BP17K000 OPEN ERROR, STATUS : ' WS-K000-STATUS           
              PERFORM 9999-CLOSE-RTN.                                           
                                                                                
           MOVE SPACES              TO WS-F740-EOF.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F740.                                                      
      ******************************************************************        
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'          TO WS-F740-EOF                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-F740.                                                       
      ******************************************************************        
           MOVE F740-NUM-REF   TO K000-NUM-REF.                                 
           READ BP17K000 KEY IS K000-NUM-REF.                                   
           EVALUATE WS-K000-STATUS                                              
           WHEN 00                                                              
              ADD 1                TO WS-K000-READ                              
              PERFORM 3100-REWRITE-BP17K000  THRU 3100-EXIT                     
           WHEN 23                                                              
              ADD 1                TO WS-K000-NOTFND                            
              DISPLAY 'BP17K000 RECORD NOT FND, KEY : ' K000-NUM-REF            
           WHEN OTHER                                                           
              DISPLAY 'BP17K000 READ ERROR, STATUS : ' WS-K000-STATUS           
              MOVE WS-K000-STATUS    TO RETURN-CODE                             
              PERFORM 9999-CLOSE-RTN                                            
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F740   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-REWRITE-BP17K000.                                                   
      ******************************************************************        
           ACCEPT WS-CURRENT-DATE FROM DATE YYYYMMDD.                           
           MOVE WS-CURRENT-DATE  TO K000-DTE-UPDATE.                            
                                                                                
           MOVE F740-NUM-REGN    TO K000-NUM-REGNO.                             
                                                                                
           REWRITE BP17K000-REC.                                                
           IF WS-K000-STATUS = 00 OR 02                                         
              ADD 1              TO WS-K000-REWRITE                             
           ELSE                                                                 
              DISPLAY 'BP17K000 REWRITE ERROR, STATUS : '                       
                       WS-K000-STATUS                                           
              DISPLAY 'K000-KEY : ' K000-NUM-REF.                               
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-RTN.                                                          
      ******************************************************************        
           CLOSE BP13F740                                                       
                 BP17K000.                                                      
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              MOVE WS-K000-STATUS      TO RETURN-CODE                           
              DISPLAY 'BP17K000 CLOSE ERROR, STATUS : ' WS-K000-STATUS.         
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
