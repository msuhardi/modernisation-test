 /*================================================================*/           
 /* SAS PROGRAM TO PRINT CASES IN BALLOT FILE BEFORE PIGEON-HOLDING*/           
 /*----------------------------------------------------------------*/           
 /* BP133142  IL5  05092007 NEW                                    */           
 /* BP133364  IL5  09072008 ADD HH TO REPORT FOR QUARTERLY EX      */           
 /* BP133501  IL5  29122008 TO PRINT STATUS AND NEW TOTAL COUNT    */           
 /* BP135392  IL5  04072014 TO REDUCE NUMBER OF LINES PRINTED/PAGE */           
 /*                         FROM 60 TO 58                          */           
 /* BP136954  IL5  28082017 CHANGE PUT PAGE TO PUT _PAGE_          */           
  *================================================================*/           
  OPTIONS  NODATE NONUMBER;                                                     
  DATA BP13K740 ;                                                               
  INFILE BP13K740;                                                              
  INPUT  @01  RANDOM    $CHAR28.                                                
         @29  PRIORITY  $CHAR2.                                                 
         @41  NRIC1     $CHAR9.                                                 
         @78  ZONE      $CHAR2.                                                 
         @461 HH        $CHAR1.                                                 
         @123 REGN      $CHAR8.                                                 
         @187 NT1       $CHAR3.                                                 
         @211 FLAT      $CHAR2.                                                 
         @213 ALLO_SCH  $CHAR3.                                                 
         @225 CAT       $CHAR1.                                                 
         @423 BUYBACK   $CHAR1.                                                 
         @226 NEWT      $CHAR3.                                                 
         @229 UNSUCC    $CHAR1.                                                 
         @260 STATUS    $CHAR1.;                                                
                                                                                
   IF FLAT='3 ' AND BUYBACK='Y' THEN                                            
      FLAT='3B';                                                                
                                                                                
                                                                                
  IF REGN ^= '      ' THEN OUTPUT;                                              
  PROC SORT; BY REGN PRIORITY;                                                  
    DATA _NULLS_ ;                                                              
    SET BP13K740 END=FINAL;                                                     
    BY REGN PRIORITY;                                                           
    OPTIONS PAGESIZE=58 LS=132 NODATE;                                          
    FILE BP13L512   HEADER=HH NOTITLES;                                         
                                                                                
   IF FIRST.REGN THEN                                                           
       CNT + 1;                                                                 
                                                                                
   IF FIRST.REGN AND STATUS = 'C' THEN                                          
      CCNT + 1;                                                                 
                                                                                
   IF FIRST.REGN AND STATUS ^= 'C' THEN                                         
      LCNT + 1;                                                                 
                                                                                
    IF _N_ = 1 THEN                                                             
       PUT _PAGE_;                                                              
                                                                                
        N+1;                                                                    
        PUT  @1 N 7.                                                            
             @10 REGN   $8.   @20  PRIORITY    $2.                              
             @27 RANDOM $28.  @56  NRIC1       $9.                              
             @67 NT1 $3.                                                        
             @72 FLAT $2.     @78 ALLO_SCH $3.                                  
             @84 CAT $1.      @90 HH $1.                                        
             @98 STATUS $1./;                                                   
                                                                                
    IF FINAL THEN                                                               
       PUT  // @10 'TOTAL NUMBER OF CANCELED APPLICATIONS : ' CCNT              
             / @10 'TOTAL NUMBER OF LIVE APPLICATIONS     : ' LCNT              
             / @10 'TOTAL NUMBER OF APPLICATIONS          : ' CNT;              
                                                                                
        RETURN;                                                                 
                                                                                
  HH:                                                                           
     PAGE+1 ;                                                                   
     CURRENT=DATE();                                                            
     PUT @2 'BP13L512' @14 'HDB3'                                               
         @35 'S Y S T E M    O F    C O M M I T M E N T'                        
         @104 'DATE :' @110 CURRENT DDMMYY8.                                    
         @122 'PAGE :' @128 PAGE 5.;                                            
     PUT @104 'SALES UNIT    ';                                                 
     PUT @42  'LIST OF REQUESTS FOR COMPUTER BALLOTING ';                       
     PUT @45  'BEFORE PIGEON-HOLDING TO NEW TOWN' //;                           
     PUT @1 'SERIAL' @10 'REGN NO' @20 'REC'  @27 'RANDOM NO'                   
         @56 'NRIC1'                                                            
         @67 'NT1 '                                                             
         @72 'FLAT'                                                             
         @78 'ALLO'                                                             
         @84 'CAT'                                                              
         @90 'HH'                                                               
         @96 'STATUS';                                                          
     PUT @1  'NUMBER'   @20 'CNT '                                              
         @72 'TYPE'                                                             
         @78 'SCH' //;                                                          
        RETURN;                                                                 
