      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C323.                                                 
       AUTHOR.        LKH.                                                      
      *DATE-WRITTEN.  24/07/90.                                                 
                                                                                
      *================================================================*        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT   (SOC)                  *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS THE PROGRAM TO PRINT OUT REPORTS  *        
      *                      WHERE THE BLOCK IS DUE.                   *        
      *                                                                *        
      *   INPUT FILES :  1.  BM06K100  -  BLOCK FILE                   *        
      *                  2.  BM06K110  -  FLATINV FILE                 *        
      *                  3.  BP13K330  -  AGREEMENT ORDER FILE         *        
      *                                                                *        
      *   LISTINGS    :  1.  REPORT FOR KEYS NOT AVAILABLE - BP13L323  *        
      *                  2.  REPORT FOR OCS WITH TIMBER DOORS ONLY -   *        
      *                      P13L323A                                  *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   DATE   BY   DESCRIPTION                            *        
      *   -------  ------  ---  -------------------------------------  *        
      *   C900721  240790  LKH  TO PRINT OUT CASES WHERE BLOCK IS DUE  *        
      *   C920597  160792  LSB  TO PRINT OCS-TAG = N OR BLANK          *        
      *   SOCPH8B  260893  KPS  TO INCLUDE A CALL TO BP13C904          *        
      *   C9310A1  171193  JEP  TO ADD ADDITIONAL REPORT P13L323A      *        
      *                         (OCS WITH TIMBER DOORS ONLY)           *        
      *   C9506G2  180196  LJL  TO CATER FOR 6 BYTES POSTAL CODE.      *        
      *   BP130488 980306  SIA  OCS IMPLEMENTATION                     *        
      *   BP130418 980306  SIA  CHGE BM06K211 TO K110, K210 - K100     *        
      *   BP130488 990104  GV   USE INTEGER INSTEAD OF JULIAN.         *        
      *   BP130883 000807  RASM TO CHANGE BP13C904 TO BP13C913         *        
      *================================================================*        
                                                                                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS K100-STATUS.                       
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE RECORD KEY IS K110-AIX1                   
                            FILE STATUS   IS K110-STATUS.                       
           SELECT BP13K330  ASSIGN        TO BP13K330                           
                            ACCESS MODE   IS SEQUENTIAL                         
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K330-KEY-FLD                       
                            FILE STATUS   IS K330-STATUS.                       
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS K800-STATUS.                       
           SELECT BP13L323  ASSIGN        TO BP13L323.                          
           SELECT P13L323A  ASSIGN        TO P13L323A.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K330                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K330.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13L323                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC-1                PIC X(132).                              
                                                                                
       FD   P13L323A                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC-2                PIC X(132).                              
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K100-STATUS                      PIC 99    VALUE 00.             
           05  K110-STATUS                      PIC 99    VALUE 00.             
           05  K330-STATUS                      PIC 99    VALUE 00.             
           05  K800-STATUS                      PIC 99    VALUE 00.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-K100                      PIC X(1)  VALUE 'N'.            
           05  WS-EOF-K110                      PIC X(1)  VALUE 'N'.            
           05  WS-EOF-K330                      PIC X(1)  VALUE 'N'.            
                                                                                
       01  FOUND-FLAG.                                                          
           05  K100-REC-FOUND                   PIC X     VALUE 'Y'.            
           05  K110-REC-FOUND                   PIC X     VALUE 'Y'.            
           05  K330-REC-FOUND                   PIC X     VALUE 'Y'.            
           05  K800-REC-FOUND                   PIC X     VALUE 'Y'.            
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-PAGE                          PIC 9(4)  VALUE ZEROES.         
           05  WS-REC-CNT                       PIC 99    VALUE ZEROES.         
           05  WS-SEQ-CNT                       PIC 9(5)  VALUE ZEROES.         
           05  WS-PGE-CNT-L323A                 PIC 9(4)  VALUE ZEROES.         
           05  WS-REC-CNT-L323A                 PIC 99    VALUE ZEROES.         
           05  WS-SEQ-CNT-L323A                 PIC 9(5)  VALUE ZEROES.         
           05  WS-INDEX                         PIC 99    VALUE ZEROES.         
           05  WS-DIFF-DAY                      PIC S9(6) VALUE ZEROES.         
           05  WS-READ-K100-CNT                 PIC 9(5)  VALUE ZEROES.         
           05  WS-READ-K110-CNT                 PIC 9(5)  VALUE ZEROES.         
           05  WS-READ-K330-CNT                 PIC 9(5)  VALUE ZEROES.         
           05  WS-READ-K800-CNT                 PIC 9(5)  VALUE ZEROES.         
           05  WS-REM                           PIC 9(2)  VALUE ZEROES.         
           05  WS-PREV-MTH                      PIC 9(2)  VALUE ZEROES.         
           05  WS-CUR-MTH-DAY                   PIC 9(2)  VALUE ZEROES.         
           05  WS-ACC-DAY                       PIC 9(2)  VALUE ZEROES.         
           05  WS-NUM-LEAP-YR                   PIC 9(3)  VALUE ZEROES.         
           05  WS-NUM-NON-LEAP-YR               PIC 9(3)  VALUE ZEROES.         
           05  WS-NUM-DAY-CURR-YR               PIC 9(3)  VALUE ZEROES.         
           05  WS-TOT-NUM-DAY                   PIC 9(6)  VALUE ZEROES.         
           05  WS-OCS-DUE-DAY                   PIC 9(6)  VALUE ZEROES.         
           05  WS-SYS-DAY                       PIC 9(6)  VALUE ZEROES.         
                                                                                
       01  WS-K330-KEY-FLD.                                                     
           05  WS-K330-CURR-REGN                PIC X(8).                       
           05  WS-K330-SEQ-NO                   PIC X.                          
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CC                       PIC 99    VALUE 00.             
           05  WS-CURR-YY                       PIC 99    VALUE 00.             
           05  WS-CURR-MM                       PIC 99    VALUE 00.             
           05  WS-CURR-DD                       PIC 99    VALUE 00.             
       01  WS-CURR-DATE-9 REDEFINES WS-CURR-DATE PIC 9(8).                      
                                                                                
       01  WS-ED-COMM-DATE.                                                     
           05 WS-COMM-CCYY.                                                     
              10  WS-COMM-CC                    PIC 9(2)  VALUE 00.             
              10  WS-COMM-YY                    PIC 9(2)  VALUE 00.             
           05  WS-COMM-MM                       PIC 9(2)  VALUE 00.             
           05  WS-COMM-DD                       PIC 9(2)  VALUE 00.             
                                                                                
       01  WS-EDIT-DATE.                                                        
           05  WS-EDIT-DD                       PIC 99    VALUE 00.             
           05  FILLER                           PIC X     VALUE '/'.            
           05  WS-EDIT-MM                       PIC 99    VALUE 00.             
           05  FILLER                           PIC X     VALUE '/'.            
           05  WS-EDIT-CC                       PIC 99    VALUE 00.             
           05  WS-EDIT-YY                       PIC 99    VALUE 00.             
                                                                                
       01  WS-CURR-DATE-INTEGER                 PIC S9(9).                      
       01  WS-OCS-DATE-INTEGER                  PIC S9(9).                      
       01  WS-OCS-DATE                          PIC 9(8).                       
                                                                                
       01  WS-UNIT-NO.                                                          
           05  WS-LEVEL                         PIC X(2)  VALUE SPACES.         
           05  FILLER                           PIC X     VALUE '-'.            
           05  WS-MAIN-UNIT                     PIC X(4)  VALUE SPACES.         
           05  WS-SLASH1                        PIC X     VALUE '-'.            
           05  WS-SUB-UNIT                      PIC X(3)  VALUE SPACES.         
                                                                                
       01  WS-SCH-ACC-NO.                                                       
           05  WS-SCHEME                        PIC X(4)  VALUE SPACES.         
           05  WS-ACCOUNT                       PIC X(4)  VALUE SPACES.         
           05  WS-CHK                           PIC X     VALUE SPACE.          
           05  WS-LESSEE                        PIC X(2)  VALUE SPACES.         
                                                                                
       01  WS-PREV-VALUES.                                                      
           05  WS-PREV-BLOCK                    PIC X(5)  VALUE SPACES.         
           05  WS-PREV-BLOCK-L323A              PIC X(5)  VALUE SPACES.         
                                                                                
       01  WS-BP13K330-REC.                                                     
           05  WS-K330-CUR-REG                  PIC X(8).                       
           05  FILLER                           PIC X(8).                       
           05  WS-K330-SCH-ACC.                                                 
               10  WS-K330-SCH                  PIC X(4).                       
               10  WS-K330-ACC                  PIC X(5).                       
               10  WS-K330-LESSEE-CODE          PIC X(2).                       
           05  WS-K330-IDP-SCH                  PIC X(3).                       
           05  WS-K330-ELIG-SCH                 PIC X(3).                       
           05  WS-K330-ALLOC-SCH                PIC X(3).                       
           05  WS-K330-CPF1.                                                    
               10  WS-K330-CPF1-PREFIX          PIC X(1).                       
               10  WS-K330-CPF1-ACCT            PIC X(8).                       
           05  WS-K330-NRIC1                    PIC X(9).                       
           05  WS-K330-PR-TAG1                  PIC X.                          
           05  WS-K330-CPF2.                                                    
               10  WS-K330-CPF2-PREFIX          PIC X(1).                       
               10  WS-K330-CPF2-ACCT            PIC X(8).                       
           05  WS-K330-NRIC2                    PIC X(9).                       
           05  WS-K330-PR-TAG2                  PIC X.                          
           05  WS-K330-CPF3.                                                    
               10  WS-K330-CPF3-PREFIX          PIC X(1).                       
               10  WS-K330-CPF3-ACCT            PIC X(8).                       
           05  WS-K330-NRIC3                    PIC X(9).                       
           05  WS-K330-PR-TAG3                  PIC X.                          
           05  WS-K330-CPF4.                                                    
               10  WS-K330-CPF4-PREFIX          PIC X(1).                       
               10  WS-K330-CPF4-ACCT            PIC X(8).                       
           05  WS-K330-NRIC4                    PIC X(9).                       
           05  WS-K330-PR-TAG4                  PIC X.                          
           05  WS-K330-NAME1                    PIC X(66).                      
           05  WS-K330-NAME2                    PIC X(66).                      
           05  WS-K330-NAME3                    PIC X(66).                      
           05  WS-K330-NAME4                    PIC X(66).                      
           05  FILLER                           PIC X(6).                       
           05  FILLER                           PIC X(6).                       
           05  WS-K330-ALLOCN-TAG               PIC X(2).                       
           05  WS-K330-ELIG-TAG                 PIC X.                          
           05  WS-K330-AMT-PREV-SELLPR          PIC 9(8).                       
           05  FILLER                           PIC X(5).                       
           05  WS-K330-AMT-SOC-DEBTOR           PIC S9(7)V99.                   
           05  WS-K330-AMT-SOC-CASH             PIC S9(7)V99.                   
           05  WS-K330-AMT-SOC-CPF1             PIC S9(7)V99.                   
           05  WS-K330-AMT-SOC-CPF2             PIC S9(7)V99.                   
           05  WS-K330-AMT-SOC-CPF3             PIC S9(7)V99.                   
           05  WS-K330-AMT-SOC-CPF4             PIC S9(7)V99.                   
           05  WS-K330-AMT-DISCOUNT-SELLPR      PIC 9(4).                       
           05  WS-K330-NUM-BEDROOM              PIC X(2).                       
           05  WS-K330-CDE-COLOUR-SCHEME        PIC X(1).                       
           05  WS-K330-AMT-FLEXI-SELLPR         PIC 9(8).                       
           05  WS-K330-AMT-COMP-SELLPR          PIC 9(8).                       
           05  FILLER                           PIC X(4).                       
           05  WS-K330-AMT-CD-REQ               PIC S9(7)V99.                   
           05  WS-K330-AMT-BOOKING-FEE          PIC S9(4)V99.                   
           05  FILLER                           PIC X(6).                       
           05  WS-K330-AMT-BK-TRANSFER          PIC S9(4)V99.                   
           05  WS-K330-AMT-LEASE-STAMP          PIC S9(7)V99.                   
           05  WS-K330-AMT-CONVEY               PIC S9(7)V99.                   
           05  WS-K330-CDE-OCS-TAG              PIC X.                          
           05  WS-K330-CDE-OCS-TYPE             PIC X.                          
           05  WS-K330-AMT-TOPUP-CASH           PIC S9(7)V99.                   
           05  WS-K330-AMT-TOPUP-CPF1           PIC S9(7)V99.                   
           05  WS-K330-AMT-TOPUP-CPF2           PIC S9(7)V99.                   
           05  WS-K330-AMT-TOPUP-CPF3           PIC S9(7)V99.                   
           05  WS-K330-AMT-TOPUP-CPF4           PIC S9(7)V99.                   
           05  WS-K330-AMT-LEASE-STAMP-CASH     PIC S9(5)V99.                   
           05  WS-K330-AMT-LEASE-STAMP-CPF1     PIC S9(5)V99.                   
           05  WS-K330-AMT-LEASE-STAMP-CPF2     PIC S9(5)V99.                   
           05  WS-K330-AMT-LEASE-STAMP-CPF3     PIC S9(5)V99.                   
           05  WS-K330-AMT-LEASE-STAMP-CPF4     PIC S9(5)V99.                   
           05  WS-K330-AMT-CONV-CASH            PIC S9(5)V99.                   
           05  WS-K330-AMT-CONV-CPF1            PIC S9(5)V99.                   
           05  WS-K330-AMT-CONV-CPF2            PIC S9(5)V99.                   
           05  WS-K330-AMT-CONV-CPF3            PIC S9(5)V99.                   
           05  WS-K330-AMT-CONV-CPF4            PIC S9(5)V99.                   
           05  FILLER                           PIC X(06).                      
           05  WS-K330-DTE-SALE-AGRM.                                           
               10  WS-K330-SALE-AGRM-DTE-CC     PIC X(2).                       
               10  WS-K330-SALE-AGRM-DTE-YY     PIC X(2).                       
               10  WS-K330-SALE-AGRM-DTE-MM     PIC X(2).                       
               10  WS-K330-SALE-AGRM-DTE-DD     PIC X(2).                       
           05  WS-K330-DTE-AGRM-ORDER.                                          
               10  WS-K330-AGRORDER-DTE-CC      PIC X(2).                       
               10  WS-K330-AGRORDER-DTE-YY      PIC X(2).                       
               10  WS-K330-AGRORDER-DTE-MM      PIC X(2).                       
               10  WS-K330-AGRORDER-DTE-DD      PIC X(2).                       
           05  WS-K330-JB-NO                    PIC X(8).                       
           05  WS-K330-UPDATE-TAG               PIC X.                          
           05  WS-K330-CDE-TRANS                PIC X(2).                       
           05  WS-K330-CDE-LESSEE-CHG1          PIC X.                          
           05  WS-K330-CDE-LESSEE-CHG2          PIC X.                          
           05  WS-K330-CDE-LESSEE-CHG3          PIC X.                          
           05  WS-K330-CDE-LESSEE-CHG4          PIC X.                          
           05  WS-K330-CDE-SUB-TRANSFER         PIC X.                          
           05  FILLER                           PIC X.                          
           05  WS-K330-DTE-BOND-EXP             PIC X(8).                       
           05  WS-K330-PREV-CPF1                PIC X(9).                       
           05  WS-K330-PREV-CPF2                PIC X(9).                       
           05  WS-K330-PREV-CPF3                PIC X(9).                       
           05  WS-K330-PREV-CPF4                PIC X(9).                       
           05  WS-K330-PREV-NRIC1               PIC X(9).                       
           05  WS-K330-PREV-NRIC2               PIC X(9).                       
           05  WS-K330-PREV-NRIC3               PIC X(9).                       
           05  WS-K330-PREV-NRIC4               PIC X(9).                       
           05  WS-K330-CDE-MANUAL               PIC X.                          
           05  WS-K330-CDE-ASPF                 PIC X.                          
           05  WS-K330-CDE-PRIORITY-PUR         PIC X.                          
           05  WS-K330-NEW-TOWN                 PIC X(3).                       
           05  WS-K330-DTE-SO-APPT              PIC X(8).                       
           05  WS-K330-DTE-DELIVERY-POSSN       PIC X(8).                       
           05  FILLER                           PIC X(6).                       
           05  FILLER                           PIC X(2).                       
           05  FILLER                           PIC X(4).                       
           05  FILLER                           PIC X(4).                       
           05  FILLER                           PIC X(4).                       
           05  FILLER                           PIC X(4).                       
           05  WS-K330-QTY-DOORS                PIC X(2).                       
           05  FILLER                           PIC X(46).                      
           05  WS-K330-DTE-SUB-TRANSFER         PIC X(8).                       
           05  WS-K330-DTE-LAST-UPDATE          PIC X(8).                       
           05  WS-WS-K330-KEY-FLD.                                              
               10  WS-K330-CURR-REG             PIC X(8).                       
               10  WS-K330-SEQ-NUM              PIC X.                          
           05  WS-K330-SYSTIME                  PIC 9(7).                       
           05  WS-K330-USERID                   PIC X(8).                       
           05  WS-K330-DTE-APPLN                PIC X(8).                       
           05  WS-K330-DTE-BOOKING              PIC X(8).                       
           05  WS-K330-DTE-OCS                  PIC X(8).                       
           05  WS-K330-AMT-CONVEY-GST           PIC S9(6)V99.                   
           05  WS-K330-NUM-SERIAL-CPF1          PIC 9(7).                       
           05  WS-K330-NUM-SERIAL-CPF2          PIC 9(7).                       
           05  WS-K330-NUM-SERIAL-CPF3          PIC 9(7).                       
           05  WS-K330-NUM-SERIAL-CPF4          PIC 9(7).                       
           05  WS-K330-AMT-RETIRE-CPF1          PIC S9(6)V99.                   
           05  WS-K330-AMT-RETIRE-CPF2          PIC S9(6)V99.                   
           05  WS-K330-AMT-RETIRE-CPF3          PIC S9(6)V99.                   
           05  WS-K330-AMT-RETIRE-CPF4          PIC S9(6)V99.                   
           05  WS-K330-AMT-REGN-DEPOSIT-CPF1    PIC 9(6)V99.                    
           05  WS-K330-AMT-REGN-DEPOSIT-CPF2    PIC 9(6)V99.                    
           05  WS-K330-AMT-REGN-DEPOSIT-CPF3    PIC 9(6)V99.                    
           05  WS-K330-AMT-REGN-DEPOSIT-CPF4    PIC 9(6)V99.                    
           05  WS-K330-AMT-REGN-DEPOSIT-CASH    PIC 9(6)V99.                    
           05  WS-K330-AMT-SER-CONTRA-ICP       PIC 9(5)V99.                    
           05  WS-K330-AMT-SER-CONTRA.                                          
               10 WS-K330-AMT-SER-CONTRA-STPFEE PIC 9(5)V99.                    
               10 WS-K330-AMT-SER-CONTRA-CONVY  PIC 9(5)V99.                    
           05  FILLER                           PIC X(947).                     
       01  BP13WS-K330-TP-REC.                                                  
           05  FILLER                           PIC X(413).                     
           05  WS-K330-AMT-CD-CASH              PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF1              PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF2              PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF3              PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF4              PIC S9(7)V99.                   
           05  FILLER                           PIC X(74).                      
           05  WS-K330-AMT-CD-CASH-UPD          PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF1-UPD          PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF2-UPD          PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF3-UPD          PIC S9(7)V99.                   
           05  WS-K330-AMT-CD-CPF4-UPD          PIC S9(7)V99.                   
           05  FILLER                           PIC X(217).                     
           05  WS-K330-CDE-HOLDING-PREV         PIC X(2).                       
           05  WS-K330-NUM-SHR1-PREV            PIC 9(3).                       
           05  WS-K330-NUM-SHR2-PREV            PIC 9(3).                       
           05  WS-K330-NUM-SHR3-PREV            PIC 9(3).                       
           05  WS-K330-NUM-SHR4-PREV            PIC 9(3).                       
           05  WS-K330-NUM-SHR-BASE-PREV        PIC 9(3).                       
           05  FILLER                           PIC X(1189).                    
                                                                                
      *----------------------------------------------------------------*        
      *   COMMON HEADER USE FOR BOTH REPORTS                           *        
      *----------------------------------------------------------------*        
       01  L323-HDG1.                                                           
           05  PRT-REPORT-TITLE         PIC X(9)  VALUE 'BP13L323'.             
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(7)  VALUE 'DATE :'.               
           05  PRT-DATE                 PIC X(10).                              
                                                                                
       01  L323-HDG2-1.                                                         
           05  FILLER                   PIC X(33) VALUE SPACES.                 
           05  FILLER                   PIC X(71) VALUE                         
               'LIST OF OCS CASES WHICH IS DUE'.                                
           05  FILLER                   PIC X(7)  VALUE 'PAGE : '.              
           05  PRT-PAGE-1               PIC ZZZ9.                               
                                                                                
       01  L323-HDG2-A.                                                         
           05  FILLER                   PIC X(33) VALUE SPACES.                 
           05  FILLER                   PIC X(71) VALUE                         
               'LIST OF OCSD CASES WHICH IS DUE'.                               
           05  FILLER                   PIC X(7)  VALUE 'PAGE : '.              
           05  PRT-PAGE-A               PIC ZZZ9.                               
                                                                                
       01  L323-HDG2-11.                                                        
           05  FILLER                   PIC X(34) VALUE SPACES.                 
           05  FILLER                   PIC X(30) VALUE                         
               'OCS WITH TIMBER DOORS & SINK'.                                  
                                                                                
       01  L323-HDG2-AA.                                                        
           05  FILLER                 PIC X(20) VALUE SPACES.                   
           05  FILLER                 PIC X(84) VALUE                           
           'OCSD WITH TIMBER DOORS ONLY - OPTION AT SALE AGREEMENT'.            
           05  FILLER                 PIC X(07) VALUE 'USER :'.                 
           05  FILLER                 PIC X(14) VALUE 'AGREEMENT TEAM'.         
                                                                                
       01  L323-HDG3.                                                           
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(46) VALUE                         
               'T O T A L   N U M B E R   O F   U N I T S = '.                  
           05  PRT-TOTAL                PIC ZZZZ9 VALUE ZEROES.                 
                                                                                
       01  L323-HDG4.                                                           
           05  FILLER                   PIC X(98) VALUE SPACES.                 
           05  FILLER                   PIC X(34) VALUE                         
               'AGREEMENT   REQUEST FOR'.                                       
                                                                                
       01  L323-HDG5.                                                           
           05  FILLER                   PIC X(74) VALUE SPACES.                 
           05  FILLER                   PIC X(24) VALUE 'SALES REGN'.           
           05  FILLER                   PIC X(34) VALUE                         
               '  ORDER     INSTALLATION'.                                      
                                                                                
       01  L323-HDG6.                                                           
           05  FILLER                   PIC X(78) VALUE                         
               ' S/NO   SCHEME A/C NO   BLOCK   ADDRESS'.                       
           05  FILLER                   PIC X(54) VALUE                         
               'NO       DUE DATE     DATE       OF COMPENTS (Y/N)'.            
                                                                                
       01  L323-HDG7.                                                           
           05  FILLER                   PIC X(132) VALUE ALL '-'.               
                                                                                
      *----------------------------------------------------------------*        
      *   DETAIL RECORD FOR KEY NOT AVAILABLE/KEY AVAILABLE CASES      *        
      *----------------------------------------------------------------*        
       01  L323-DTL.                                                            
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  PRT-SEQ-CNT              PIC ZZZ9  VALUE ZEROS.                  
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-SCH-ACC-NO           PIC X(14) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-BLOCK                PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE '  #'.                  
           05  PRT-HOUSE-NO             PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  PRT-STREET               PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE ' S('.                  
           05  PRT-POSTAL-CODE          PIC X(6)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE ') '.                   
           05  PRT-CURR-REGN-NO         PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-DTE-OCS              PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-DTE-AGMT-ORDER       PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE SPACES.                 
           05  PRT-CDE-OCS-TAG          PIC X     VALUE SPACE.                  
                                                                                
      *----------------------------------------------------------------*        
      *   LINKAGE VARIABLES PASS TO SUB-PROGRAM  BP13C913              *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(4)  VALUE SPACES.                 
               10  WS-LINK-NUM-ACC      PIC X(5)  VALUE SPACES.                 
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
      ******************************************************************        
       000-MAIN.                                                                
      ******************************************************************        
           PERFORM   100-OPEN-ROUTINE      THRU    100-EXIT.                    
           PERFORM   150-START-K100        THRU    150-EXIT.                    
           PERFORM   200-READ-K100         THRU    200-EXIT.                    
                                                                                
           IF WS-EOF-K100 NOT = 'Y'                                             
              MOVE    K100-BLK-NO          TO      WS-PREV-BLOCK                
              MOVE    K100-BLK-NO          TO      WS-PREV-BLOCK-L323A.         
                                                                                
           PERFORM   300-PROCESS-REPORT    THRU    300-EXIT                     
                         UNTIL WS-EOF-K100 = 'Y'.                               
           PERFORM   900-PRINT-TOTAL       THRU    900-EXIT.                    
           PERFORM   900-PRINT-TOTAL-L323A THRU    900-L323A-EXIT.              
           PERFORM   999-CLOSE-ROUTINE     THRU    999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
      ******************************************************************        
           OPEN INPUT BM06K100                                                  
                      BM06K110                                                  
                      BP13K330                                                  
                      BP13K800                                                  
               OUTPUT BP13L323                                                  
                      P13L323A.                                                 
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K100-STATUS = ' K100-STATUS               
              MOVE     K100-STATUS TO RETURN-CODE                               
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K110-STATUS = ' K110-STATUS               
              MOVE     K110-STATUS TO RETURN-CODE                               
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
           IF K330-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K330-STATUS = ' K330-STATUS               
              MOVE     K330-STATUS TO RETURN-CODE                               
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K800-STATUS = ' K800-STATUS               
              MOVE     K800-STATUS TO RETURN-CODE                               
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
           MOVE ZEROES TO  WS-COUNTERS.                                         
           MOVE 50     TO  WS-REC-CNT  WS-REC-CNT-L323A.                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       150-START-K100.                                                          
      ******************************************************************        
           MOVE LOW-VALUES TO K100-KEY-FLD.                                     
           START BM06K100 KEY NOT < K100-KEY-FLD INVALID                        
                 DISPLAY 'START ERROR, BM06K100-STATUS = ' K100-STATUS          
                 MOVE     K100-STATUS  TO RETURN-CODE                           
                 PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                      
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-K100.                                                           
      ******************************************************************        
           MOVE SPACES TO WS-EOF-K100.                                          
                                                                                
           READ BM06K100 NEXT                                                   
                AT END MOVE 'Y'  TO  WS-EOF-K100                                
                       GO TO 200-EXIT.                                          
                                                                                
           ADD  1  TO  WS-READ-K100-CNT.                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-REPORT.                                                      
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    TO CHECK WHETHER THE THE BLOCK IS DUE IN 5 DAYS TIME   *             
      *-----------------------------------------------------------*             
           IF   K100-DTE-OCS-DUE = SPACES OR LOW-VALUES                         
                MOVE ZEROES       TO K100-DTE-OCS-DUE.                          
                                                                                
           MOVE K100-DTE-OCS-DUE  TO WS-ED-COMM-DATE                            
                                     WS-OCS-DATE.                               
                                                                                
           IF WS-ED-COMM-DATE > ZEROES                                          
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 200-READ-K100  THRU 200-EXIT                              
              GO TO   300-EXIT.                                                 
                                                                                
           PERFORM 400-COMPARE-DATES THRU 400-EXIT.                             
                                                                                
           IF WS-DIFF-DAY > 5  OR WS-DIFF-DAY < -5                              
              PERFORM 200-READ-K100  THRU 200-EXIT                              
              GO TO   300-EXIT.                                                 
                                                                                
           MOVE       SPACES           TO      WS-EOF-K110.                     
           PERFORM    500-START-K110   THRU    500-EXIT.                        
           PERFORM    550-READ-K110    THRU    550-EXIT                         
                          UNTIL WS-EOF-K110 = 'Y'.                              
           PERFORM    200-READ-K100    THRU    200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-COMPARE-DATES.                                                       
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    TO CALCULATE THE OCS DUE DATE                          *             
      *-----------------------------------------------------------*             
           MOVE FUNCTION CURRENT-DATE (1:8) TO WS-CURR-DATE.                    
                                                                                
           COMPUTE WS-CURR-DATE-INTEGER =                                       
                   FUNCTION INTEGER-OF-DATE (WS-CURR-DATE-9).                   
           COMPUTE WS-OCS-DATE-INTEGER =                                        
                   FUNCTION INTEGER-OF-DATE (WS-OCS-DATE).                      
                                                                                
           COMPUTE WS-DIFF-DAY = WS-OCS-DATE-INTEGER -                          
                                 WS-CURR-DATE-INTEGER.                          
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-START-K110.                                                          
      ******************************************************************        
           MOVE K100-KEY-FLD TO K110-AIX1.                                      
           START BM06K110 KEY = K110-AIX1 INVALID                               
                 MOVE 'Y' TO WS-EOF-K110                                        
                 DISPLAY 'START ERROR, BM06K110-STATUS = ' K110-STATUS          
                 DISPLAY '                 K110-AIX1   = ' K110-AIX1.           
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       550-READ-K110.                                                           
      ******************************************************************        
           MOVE SPACES TO WS-EOF-K110.                                          
                                                                                
           READ BM06K110 NEXT                                                   
                AT END MOVE 'Y'  TO  WS-EOF-K110                                
                       GO TO 550-EXIT.                                          
                                                                                
           IF K110-AIX1 = K100-KEY-FLD                                          
              CONTINUE                                                          
           ELSE                                                                 
           IF K110-AIX1 >  K100-KEY-FLD                                         
              MOVE 'Y'  TO WS-EOF-K110                                          
              GO TO 550-EXIT.                                                   
                                                                                
           IF K110-REGN-NO(1:2) = SPACES OR LOW-VALUES OR '##'                  
              GO TO 550-EXIT.                                                   
                                                                                
            ADD 1 TO WS-READ-K110-CNT.                                          
                                                                                
            MOVE    K110-REGN-NO          TO    K800-NUM-REGN                   
            MOVE    SPACES                TO    K800-REC-FOUND                  
            PERFORM 650-READ-K800         THRU  650-EXIT                        
            IF (K800-DTE-BALLOT >= '199804') AND (K800-REC-FOUND = 'Y')         
               GO TO 550-EXIT.                                                  
                                                                                
           MOVE      LOW-VALUES      TO    K330-CUR-REG.                        
           MOVE      'N'             TO    WS-EOF-K330.                         
           PERFORM   590-START-K330  THRU  590-EXIT.                            
           PERFORM   600-READ-K330   THRU  600-EXIT UNTIL                       
                         WS-EOF-K330    = 'Y'  OR                               
                         K330-REC-FOUND = 'N'  OR                               
                         K330-CUR-REG > K110-REGN-NO.                           
                                                                                
      *-----------------------------------------------------------*             
      *    DO NOT PRINT IF OCS-TAG = "Y"                          *             
      *-----------------------------------------------------------*             
            IF (K330-REC-FOUND = 'Y') AND                                       
               (WS-K330-CDE-OCS-TAG NOT = 'Y') AND                              
               (K100-NUM-OCS-TYPE = SPACE OR LOW-VALUE)                         
               PERFORM  700-PRINT-REPORT-L323   THRU   700-EXIT                 
            ELSE                                                                
            IF (K330-REC-FOUND = 'Y') AND                                       
               (WS-K330-CDE-OCS-TAG NOT = 'Y') AND                              
               (K100-NUM-OCS-TYPE = '1' OR 'D')                                 
               PERFORM  700-PRINT-REPORT-L323A  THRU   700-L323A-EXIT.          
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       590-START-K330.                                                          
      ******************************************************************        
           MOVE 'Y'                 TO K330-REC-FOUND.                          
           MOVE  K110-REGN-NO       TO WS-K330-CURR-REGN.                       
           MOVE '1'                 TO WS-K330-SEQ-NO.                          
           MOVE  LOW-VALUES         TO K330-KEY-FLD.                            
           MOVE  WS-K330-KEY-FLD    TO K330-KEY-FLD.                            
                                                                                
           START BP13K330 KEY =   K330-KEY-FLD INVALID                          
                 MOVE    'N'  TO  K330-REC-FOUND                                
                 DISPLAY 'START ERROR, BP13K330-STATUS  = ' K330-STATUS         
                         '                 K330-KEY-FLD = ' K330-KEY-FLD        
                 GO TO    590-EXIT.                                             
                                                                                
           MOVE SPACES TO WS-BP13K330-REC.                                      
                                                                                
       590-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-READ-K330.                                                           
      ******************************************************************        
           READ BP13K330                                                        
                AT END MOVE 'Y'  TO  WS-EOF-K330                                
                       GO TO 600-EXIT.                                          
                                                                                
           IF K330-CURR-REG =  K110-REGN-NO                                     
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 600-EXIT.                                                   
                                                                                
           MOVE BP13K330-REC TO WS-BP13K330-REC.                                
                                                                                
           IF K330-CDE-OCS-TAG = SPACES OR LOW-VALUES                           
              CONTINUE                                                          
           ELSE                                                                 
              MOVE K330-CDE-OCS-TAG TO WS-K330-CDE-OCS-TAG.                     
                                                                                
           ADD 1 TO WS-READ-K330-CNT.                                           
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       650-READ-K800.                                                           
      ******************************************************************        
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              MOVE 'Y' TO K800-REC-FOUND                                        
           ELSE                                                                 
           IF K800-STATUS = 23                                                  
              MOVE ZEROES TO K800-DTE-BALLOT                                    
              MOVE 'N'    TO K800-REC-FOUND                                     
              DISPLAY 'RECORD NOT FOUND IN BP13K800'                            
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
           ELSE                                                                 
              MOVE K800-STATUS TO RETURN-CODE                                   
              DISPLAY 'ERROR READING BP13K800 FILE : ' K800-STATUS              
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-PRINT-REPORT-L323.                                                   
      ******************************************************************        
           MOVE  SPACES          TO BP13COMM8-REC.                              
           INITIALIZE               BP13COMM8-REC.                              
           MOVE  K110-SCH-ACC-NO TO WS-LINK-REC.                                
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC BP13COMM8-REC.                     
                                                                                
           IF COMM8-NUM-BLK = WS-PREV-BLOCK                                     
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 900-PRINT-TOTAL   THRU   900-EXIT                         
              PERFORM 800-PRINT-HEADER  THRU   800-EXIT.                        
                                                                                
           IF WS-REC-CNT > 44                                                   
              PERFORM 800-PRINT-HEADER  THRU   800-EXIT.                        
                                                                                
           MOVE   SPACES                TO     PRINT-REC-1.                     
           ADD    1                     TO     WS-SEQ-CNT.                      
           MOVE   WS-SEQ-CNT            TO     PRT-SEQ-CNT.                     
           MOVE   K110-SCH-ACC-NO       TO     WS-SCH-ACC-NO.                   
           STRING WS-SCHEME '-' WS-ACCOUNT '-' WS-CHK '-' WS-LESSEE             
                   DELIMITED BY SIZE   INTO     PRT-SCH-ACC-NO.                 
                                                                                
           MOVE   COMM8-NUM-BLK         TO     PRT-BLOCK.                       
           MOVE   COMM8-NUM-LEVEL       TO     WS-LEVEL.                        
           MOVE   COMM8-NUM-UNIT-MAIN   TO     WS-MAIN-UNIT.                    
                                                                                
           IF COMM8-NUM-UNIT-SUB = SPACES OR LOW-VALUES                         
              MOVE SPACES               TO     WS-SLASH1 WS-SUB-UNIT            
           ELSE                                                                 
              MOVE '-'                  TO     WS-SLASH1                        
              MOVE COMM8-NUM-UNIT-SUB   TO     WS-SUB-UNIT.                     
                                                                                
           MOVE   WS-UNIT-NO            TO     PRT-HOUSE-NO.                    
           MOVE   COMM8-NME-STREET      TO     PRT-STREET.                      
           MOVE   COMM8-NUM-POSTAL-CODE TO     PRT-POSTAL-CODE.                 
                                                                                
           MOVE   K110-REGN-NO          TO     PRT-CURR-REGN-NO.                
           MOVE   K100-DTE-OCS-DUE      TO     WS-ED-COMM-DATE.                 
           STRING WS-COMM-DD  '/'  WS-COMM-MM '/' WS-COMM-CCYY                  
                  DELIMITED BY SIZE   INTO     PRT-DTE-OCS.                     
           STRING WS-K330-AGRORDER-DTE-DD '/'  WS-K330-AGRORDER-DTE-MM          
                  '/' WS-K330-AGRORDER-DTE-CC                                   
                      WS-K330-AGRORDER-DTE-YY                                   
                  DELIMITED BY SIZE   INTO     PRT-DTE-AGMT-ORDER.              
           MOVE   WS-K330-CDE-OCS-TAG   TO     PRT-CDE-OCS-TAG.                 
                                                                                
           WRITE  PRINT-REC-1           FROM   L323-DTL AFTER 1.                
           ADD    1                     TO     WS-REC-CNT.                      
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-PRINT-REPORT-L323A.                                                  
      ******************************************************************        
           MOVE  SPACES               TO BP13COMM8-REC.                         
           INITIALIZE                    BP13COMM8-REC.                         
                                                                                
           MOVE  K110-SCH-ACC-NO      TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC BP13COMM8-REC.                     
                                                                                
           IF COMM8-NUM-BLK = WS-PREV-BLOCK-L323A                               
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 900-PRINT-TOTAL-L323A    THRU  900-L323A-EXIT             
              PERFORM 800-PRINT-HEADER-L323A   THRU  800-L323A-EXIT.            
                                                                                
           IF WS-REC-CNT-L323A > 44                                             
              PERFORM 800-PRINT-HEADER-L323A   THRU  800-L323A-EXIT.            
                                                                                
           MOVE   SPACES                TO     PRINT-REC-2.                     
           ADD    1                     TO     WS-SEQ-CNT-L323A.                
           MOVE   WS-SEQ-CNT-L323A      TO     PRT-SEQ-CNT.                     
           MOVE   K110-SCH-ACC-NO       TO     WS-SCH-ACC-NO.                   
           STRING WS-SCHEME '-' WS-ACCOUNT '-' WS-CHK '-' WS-LESSEE             
                  DELIMITED BY SIZE   INTO     PRT-SCH-ACC-NO.                  
                                                                                
           MOVE   COMM8-NUM-BLK         TO     PRT-BLOCK.                       
           MOVE   COMM8-NUM-LEVEL       TO     WS-LEVEL.                        
           MOVE   COMM8-NUM-UNIT-MAIN   TO     WS-MAIN-UNIT.                    
                                                                                
           IF COMM8-NUM-UNIT-SUB = SPACES OR LOW-VALUES                         
              MOVE SPACES               TO     WS-SLASH1 WS-SUB-UNIT            
           ELSE                                                                 
              MOVE '-'                  TO     WS-SLASH1                        
              MOVE COMM8-NUM-UNIT-SUB   TO     WS-SUB-UNIT.                     
                                                                                
           MOVE   WS-UNIT-NO            TO     PRT-HOUSE-NO.                    
           MOVE   COMM8-NME-STREET      TO     PRT-STREET.                      
           MOVE   COMM8-NUM-POSTAL-CODE TO     PRT-POSTAL-CODE.                 
                                                                                
           MOVE   K110-REGN-NO          TO     PRT-CURR-REGN-NO.                
           MOVE   K100-DTE-OCS-DUE      TO     WS-ED-COMM-DATE.                 
           STRING WS-COMM-DD  '/'  WS-COMM-MM '/' WS-COMM-CCYY                  
                  DELIMITED BY SIZE   INTO     PRT-DTE-OCS.                     
           STRING WS-K330-AGRORDER-DTE-DD '/'  WS-K330-AGRORDER-DTE-MM          
                  '/' WS-K330-AGRORDER-DTE-CC                                   
                      WS-K330-AGRORDER-DTE-YY                                   
                  DELIMITED BY SIZE   INTO     PRT-DTE-AGMT-ORDER.              
           MOVE   WS-K330-CDE-OCS-TAG   TO     PRT-CDE-OCS-TAG.                 
                                                                                
           WRITE  PRINT-REC-2           FROM   L323-DTL AFTER 1.                
           ADD    1                     TO     WS-REC-CNT-L323A.                
                                                                                
       700-L323A-EXIT.                                                          
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PRINT-HEADER.                                                        
      ******************************************************************        
           ADD    1              TO    WS-PAGE.                                 
           MOVE   WS-PAGE        TO    PRT-PAGE-1.                              
           MOVE   FUNCTION CURRENT-DATE TO WS-CURR-DATE.                        
           MOVE   WS-CURR-DD     TO    WS-EDIT-DD.                              
           MOVE   WS-CURR-MM     TO    WS-EDIT-MM.                              
           MOVE   WS-CURR-CC     TO    WS-EDIT-CC.                              
           MOVE   WS-CURR-YY     TO    WS-EDIT-YY.                              
           MOVE   WS-EDIT-DATE   TO    PRT-DATE.                                
           MOVE   'BP13L323'     TO    PRT-REPORT-TITLE.                        
                                                                                
           MOVE   SPACES      TO   PRINT-REC-1.                                 
           WRITE  PRINT-REC-1 FROM L323-HDG1   AFTER PAGE.                      
           MOVE   SPACES      TO   PRINT-REC-1.                                 
           WRITE  PRINT-REC-1 FROM L323-HDG2-1 AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC-1.                                 
           WRITE  PRINT-REC-1 FROM L323-HDG2-11 AFTER 1.                        
           MOVE   SPACES      TO   PRINT-REC-1.                                 
           WRITE  PRINT-REC-1 FROM L323-HDG4   AFTER 2.                         
           MOVE   SPACES      TO   PRINT-REC-1.                                 
           WRITE  PRINT-REC-1 FROM L323-HDG5   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC-1.                                 
           WRITE  PRINT-REC-1 FROM L323-HDG6   AFTER 1.                         
           WRITE  PRINT-REC-1 FROM L323-HDG7   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC-1.                                 
           WRITE  PRINT-REC-1.                                                  
           MOVE   ZEROES      TO   WS-REC-CNT.                                  
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PRINT-HEADER-L323A.                                                  
      ******************************************************************        
           ADD    1                     TO  WS-PGE-CNT-L323A.                   
           MOVE   WS-PGE-CNT-L323A      TO  PRT-PAGE-A.                         
           MOVE   FUNCTION CURRENT-DATE TO  WS-CURR-DATE.                       
           MOVE   WS-CURR-DD            TO  WS-EDIT-DD.                         
           MOVE   WS-CURR-MM            TO  WS-EDIT-MM.                         
           MOVE   WS-CURR-CC            TO  WS-EDIT-CC.                         
           MOVE   WS-CURR-YY            TO  WS-EDIT-YY.                         
           MOVE   WS-EDIT-DATE          TO  PRT-DATE.                           
           MOVE   'P13L323A'            TO  PRT-REPORT-TITLE.                   
                                                                                
           MOVE   SPACES      TO   PRINT-REC-2.                                 
           WRITE  PRINT-REC-2 FROM L323-HDG1   AFTER PAGE.                      
           MOVE   SPACES      TO   PRINT-REC-2.                                 
           WRITE  PRINT-REC-2 FROM L323-HDG2-A AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC-2.                                 
           WRITE  PRINT-REC-2 FROM L323-HDG2-AA AFTER 1.                        
           MOVE   SPACES      TO   PRINT-REC-2.                                 
           WRITE  PRINT-REC-2 FROM L323-HDG4   AFTER 2.                         
           MOVE   SPACES      TO   PRINT-REC-2.                                 
           WRITE  PRINT-REC-2 FROM L323-HDG5   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC-2.                                 
           WRITE  PRINT-REC-2 FROM L323-HDG6   AFTER 1.                         
           WRITE  PRINT-REC-2 FROM L323-HDG7   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC-2.                                 
           WRITE  PRINT-REC-2.                                                  
           MOVE   ZEROES      TO   WS-REC-CNT-L323A.                            
                                                                                
       800-L323A-EXIT.                                                          
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-PRINT-TOTAL.                                                         
      ******************************************************************        
           IF WS-SEQ-CNT > ZEROES                                               
              MOVE   WS-SEQ-CNT    TO    PRT-TOTAL                              
              MOVE   SPACES        TO    PRINT-REC-1                            
              WRITE  PRINT-REC-1   FROM  L323-HDG3 AFTER 3.                     
                                                                                
           MOVE      ZEROES        TO    WS-SEQ-CNT.                            
           MOVE      ZEROES        TO    WS-REC-CNT.                            
           MOVE      COMM8-NUM-BLK TO    WS-PREV-BLOCK.                         
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-PRINT-TOTAL-L323A.                                                   
      ******************************************************************        
           IF WS-SEQ-CNT-L323A > ZEROES                                         
              MOVE   WS-SEQ-CNT-L323A   TO    PRT-TOTAL                         
              MOVE   SPACES             TO    PRINT-REC-2                       
              WRITE  PRINT-REC-2        FROM  L323-HDG3 AFTER 3.                
                                                                                
           MOVE      ZEROES             TO    WS-SEQ-CNT-L323A.                 
           MOVE      ZEROES             TO    WS-REC-CNT-L323A.                 
           MOVE      COMM8-NUM-BLK      TO    WS-PREV-BLOCK-L323A.              
                                                                                
       900-L323A-EXIT.                                                          
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BM06K100                                                       
                 BM06K110                                                       
                 BP13K330                                                       
                 BP13K800                                                       
                 BP13L323                                                       
                 P13L323A.                                                      
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K100-STATUS = ' K100-STATUS.              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K110-STATUS = ' K110-STATUS.              
                                                                                
           IF K330-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K330-STATUS = ' K330-STATUS.              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K800-STATUS = ' K800-STATUS.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
