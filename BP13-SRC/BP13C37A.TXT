       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C37A.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  26/02/2015.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO READ BM06F190 TO CREATE AB03F031 TO BE PASS TO  *        
      *             SMS                                                *        
      *                                                                *        
      * INPUT  FILES : BM06F190                                        *        
      * OUTPUT FILES : AB03F021                                        *        
      *                                                                *        
      * CHG-NO   BY    ON    - DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP135670 ESA1 26/02/15 NEW PROGRAM                             *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BM06F190      ASSIGN TO BM06F190.                           
                                                                                
           SELECT   AB03F021      ASSIGN TO AB03F021.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BM06F190                                                             
           RECORDING MODE  IS   F                                               
           RECORD CONTAINS 650 CHARACTERS.                                      
       COPY BM06F190.                                                           
                                                                                
       FD  AB03F021                                                             
           RECORDING MODE  IS   F                                               
           BLOCK  CONTAINS 0    RECORDS                                         
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL  RECORDS  ARE  STANDARD.                                       
       COPY AB03F021.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F190-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-F021-WRITE               PIC 9(7)  VALUE ZEROS.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F190-EOF                  PIC X(1)  VALUE 'N'.                
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-MAIN.                                                               
      *===============================================================*         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BM06F190       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-F190-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       1000-OPEN-FILES.                                                         
      *===============================================================*         
                                                                                
           OPEN INPUT BM06F190                                                  
               OUTPUT AB03F021.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2000-READ-BM06F190.                                                      
      *===============================================================*         
                                                                                
           READ BM06F190                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-F190-EOF                          
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F190-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3000-PROCESS-RECORD.                                                     
      *===============================================================*         
                                                                                
           PERFORM 4000-WRITE-AB03F021    THRU 4000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BM06F190     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4000-WRITE-AB03F021.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                   TO AB03F021-REC.                       
           MOVE 'C'                      TO F021-CDE-TRANS-FIS.                 
           MOVE 'OT'                     TO F021-CDE-REASON-FOR-CHGE.           
           MOVE 'BP13'                   TO F021-CDE-SYSTEM.                    
           MOVE 'D'                      TO F021-CDE-ACTION.                    
           MOVE F190-DTE-TRANS           TO F021-DTE-UPDTE                      
                                            F021-DTE-CANCEL-WEF.                
           MOVE F190-SYS-TIME            TO F021-DATA-ENTRY-TIME.               
           MOVE F190-SCH-ACC-NO          TO F021-NUM-HDB-REF.                   
           STRING F190-SCH-ACC-NO(1:9)  F190-REC1(617:2)                        
                     DELIMITED BY SIZE INTO F021-NUM-HDB-REF-EXLESSEE.          
                                                                                
           WRITE AB03F021-REC.                                                  
           ADD 1                          TO CNT-F021-WRITE.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       9000-CLOSE-FILES.                                                        
      *===============================================================*         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13C37A'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BM06F190 READ          : ' CNT-F190-READ.            
           DISPLAY 'NO. OF RECS AB04F021 WRITTEN  : ' CNT-F021-WRITE.           
                                                                                
           CLOSE BM06F190                                                       
                 AB03F021.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
