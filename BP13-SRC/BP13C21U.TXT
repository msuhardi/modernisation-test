      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C21U.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *-----------------------------------------------------------*             
                                                                                
      *===========================================================*             
      *        SOC - S Y S T E M  O F  C O M M I T M E N T        *             
      *===========================================================*             
      *  OBJECTIVES :                                             *             
      *                                                           *             
      *    FILTER OUT QUEUE NUMBERS FOR THE NEXT DAY, TO BE       *             
      *    DISPLAYED IN THE CSO PAGE.                             *             
      *                                                           *             
      *    INPUT FILES : BP13F290 - QUEUE FILE                    *             
      *                  SY02F001 - DATE FILE                     *             
      *                                                           *             
      *    INPUT-OUTPUT: P13F290A - QUEUE FILE OUTPUT             *             
      *                                                           *             
      *---------------------------------------------------------- *             
      *===========================================================*             
      *  REVISION HISTORY :                                       *             
      *  CHG-REQ#  BY    DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP133770  RB12  12/01/10    NEW PROGRAM                  *             
      ************************************************************              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F290 ASSIGN TO BP13F290.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT P13F290A ASSIGN TO P13F290A.                                  
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  BP13F290    BLOCK  CONTAINS 0 RECORDS                                
                       RECORD CONTAINS 100 CHARACTERS                           
                       LABEL RECORD IS STANDARD                                 
                       RECORDING MODE IS F.                                     
       COPY BP13F290.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13F290A                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F290A-REC            PIC X(100).                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F290-EOF-SW             PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F290-READ-CTR           PIC 9(4)    VALUE  0.                 
           05  WS-F290-SKIP-CTR           PIC 9(4)    VALUE  0.                 
           05  WS-F290A-WRITE-CTR         PIC 9(4)    VALUE  0.                 
           05  WS-CTR                     PIC 9(2)    VALUE  0.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-DATE-VAR                PIC 9(8)    VALUE ZEROES.             
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY               PIC X(4)    VALUE SPACES.             
           05  WS-CURR-MM                 PIC X(2)    VALUE SPACES.             
           05  WS-CURR-DD                 PIC X(2)    VALUE SPACES.             
                                                                                
       01  WS-DATE REDEFINES WS-CURR-DATE PIC 9(8).                             
                                                                                
       01  WS-CONSTANT-TEXTS.                                                   
           05  WS-ERROR-MESSAGES.                                               
               10  K-F290-BLANK           PIC X(40)   VALUE                     
                   'F290-NUM-REGN IS BLANK      '.                              
           05  WS-TOTAL-DESC.                                                   
               10  K-F290-RECORDS-READ    PIC X(50)   VALUE                     
                      'TOTAL RECORDS READ IN BP13F290            : '.           
               10  K-F290-RECORDS-SKIP    PIC X(50)   VALUE                     
                      'TOTAL RECORDS SKIPPED IN BP13F290         : '.           
               10  K-F290A-RECORDS-WRITE  PIC X(50)   VALUE                     
                      'TOTAL RECORDS WRITTEN TO TO P13F290A      : '.           
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *-------------*                                                           
       0000-CONTROL.                                                            
      *-------------*                                                           
            PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                       
            PERFORM 1500-READ-SY02F001    THRU 1500-EXIT.                       
            PERFORM 4000-PROCESS-F290-REC THRU 4000-EXIT                        
               UNTIL WS-F290-EOF-SW = 'Y'.                                      
            PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
           OPEN INPUT  SY02F001                                                 
                       BP13F290                                                 
                OUTPUT P13F290A.                                                
                                                                                
           PERFORM  3000-READ-F290 THRU 3000-EXIT.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
        1500-READ-SY02F001.                                                     
      ******************************************************************        
                                                                                
            READ SY02F001.                                                      
                                                                                
            MOVE F001-DTE-CURRENT      TO WS-CURR-DATE.                         
                                                                                
            COMPUTE WS-DATE-VAR = FUNCTION INTEGER-OF-DATE(WS-DATE).            
                                                                                
            ADD 1 TO WS-DATE-VAR.                                               
                                                                                
            COMPUTE WS-DATE-VAR = FUNCTION DATE-OF-INTEGER(WS-DATE-VAR).        
                                                                                
            MOVE WS-DATE-VAR TO WS-CURR-DATE.                                   
                                                                                
            DISPLAY 'WS-CURR-DATE: ' WS-CURR-DATE.                              
                                                                                
        1500-EXIT.                                                              
            EXIT.                                                               
                                                                                
      *---------------*                                                         
       3000-READ-F290.                                                          
      *---------------*                                                         
           READ BP13F290                                                        
              AT END MOVE 'Y' TO WS-F290-EOF-SW                                 
              GO TO 3000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD +1             TO WS-F290-READ-CTR.                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------*                                                  
       4000-PROCESS-F290-REC.                                                   
      *----------------------*                                                  
           IF F290-DTE-APPMT = WS-CURR-DATE                                     
              PERFORM 4500-BP13F290-WRITE  THRU 4500-EXIT                       
           ELSE                                                                 
              ADD  1                       TO   WS-F290-SKIP-CTR                
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-F290          THRU 3000-EXIT.                      
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------*                                                    
       4500-BP13F290-WRITE.                                                     
      *--------------------*                                                    
           MOVE BP13F290-REC TO P13F290A-REC.                                   
                                                                                
           WRITE P13F290A-REC.                                                  
                                                                                
           ADD 1 TO WS-F290A-WRITE-CTR.                                         
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           DISPLAY ' ************** BP13C21U *****************'.                
           DISPLAY K-F290-RECORDS-READ WS-F290-READ-CTR.                        
           DISPLAY K-F290-RECORDS-SKIP WS-F290-SKIP-CTR.                        
           DISPLAY K-F290A-RECORDS-WRITE WS-F290A-WRITE-CTR.                    
                                                                                
           CLOSE BP13F290                                                       
                 SY02F001                                                       
                 P13F290A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
