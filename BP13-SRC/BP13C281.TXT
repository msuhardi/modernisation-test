       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C281.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  29/08/97.                                                 
      * =========================================================== *           
      *  SYSTEM OF COMMITMENT (BP13)                                *           
      * =========================================================== *           
      *  OBJECTIVE :  TO UPDATE DISCOUNTED SELLING PRICE BY OFFER   *           
      *               SELLING PRICE AFTER REJECTION (AFTER BOOKING  *           
      *               OR AFTER SOCA).                               *           
      *                                                             *           
      * CHG-NO   BY   ON      DESCRIPTION                           *           
      * ------   --  ------  ------------                           *           
      * BP130364 LMS 290897   NEW PGM                               *           
      * BP130488 SIA 070198   LOG BEFR IMAGE OF BM06K110 TO BM6K190 *           
      * BP130418 LMS 210998   Y2000 CHANGES.                        *           
      * BP130778 GBR 061099   REMOVE CALLING BP13C904.              *           
      * BP130938 SN  190101   ADD P13L281A, P13L281B                *           
      * BP131035 SN  110601   ADD P13L281C                          *           
      * BP131036 SN  190601   ADD NEW SERS-CD = SFC, SRC            *           
      * BP131049 SSS 160801   REMOVE P13L281A                       *           
      * BP132031 MALC031001   CATER FOR SALE OF SERS FLAT TO SINGLES*           
      * BP132357 SK  140703   NO UPDATION FOR BM06K110              *           
      * BP132708 GAN1070905   USE OF SERS TABLE (SERSCH)            *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K110-KEY-FLD                                    
                  FILE STATUS IS BM06K110-STATUS.                               
                                                                                
           SELECT P13L281B ASSIGN TO P13L281B.                                  
           SELECT P13L281C ASSIGN TO P13L281C.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F310.                                                           
                                                                                
       FD  P13L281B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L281B-REC                   PIC X(132).                           
                                                                                
       FD  P13L281C                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L281C-REC                   PIC X(132).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  BM06K110-STATUS         PIC 99       VALUE ZEROS.                    
       01  WS-K110-MATCH           PIC X.                                       
       01  WS-EOF-F310             PIC X.                                       
       01  WS-PCT20-SPRICE         PIC 9(8)     VALUE ZEROS.                    
       01  WS-MAX-DISCOUNT-AMT     PIC 9(5)     VALUE 30000.                    
       01  WS-CNT-READ-F310        PIC 9(5)     VALUE ZEROS.                    
       01  WS-DISCOUNT-AMT         PIC 9(8)     VALUE ZEROS.                    
       01  WS-CURR-SPRICE          PIC 9(8)     VALUE ZEROS.                    
       01  WS-CNT-LOG              PIC 9(5)     VALUE ZEROS.                    
       01  WS-DATE                 PIC X(8)     VALUE SPACES.                   
       01  WS-FLAGB                PIC X        VALUE 'N'.                      
       01  WS-FLAGC                PIC X        VALUE 'N'.                      
       01  SW-SERS-ALLO-SCHEME     PIC X(01)    VALUE SPACES.                   
           88  C-SERS-ALLO-SCHEME  VALUE 'Y'.                                   
                                                                                
       COPY SERSCH.                                                             
                                                                                
       01  WS-LISTING-CONTROLS.                                                 
           05  WS-SEQB          PIC 9(5)        VALUE ZEROS.                    
           05  WS-SEQC          PIC 9(5)        VALUE ZEROS.                    
           05  CNT-PAGEB        PIC 9(5)        VALUE ZEROS.                    
           05  CNT-PAGEC        PIC 9(5)        VALUE ZEROS.                    
           05  CNT-NUM-LINEB    PIC 9(5)        VALUE ZEROS.                    
           05  CNT-NUM-LINEC    PIC 9(5)        VALUE ZEROS.                    
                                                                                
       01  L281B-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'P13L281B'.              
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                   'SYSTEM OF COMMITMENT'.                      
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L281B-DATE          PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L281B-PAGENO        PIC ZZZZ9 .                                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
                                                                                
       01  L281C-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'P13L281C'.              
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                   'SYSTEM OF COMMITMENT'.                      
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L281C-DATE          PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L281C-PAGENO        PIC ZZZZ9 .                                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
                                                                                
       01  L281-PR-HEAD-02.                                                     
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE                          
           'REPORT ON UPDATE OF CURRENT SELLING PRICE FOR   '.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(32)     VALUE                          
           'USER : SALES UNIT, ERU, RHS'.                                       
                                                                                
       01  L281-PR-HEAD-03.                                                     
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
           'APPLICATIONS UNDER SERS/RED SCHEME - AFTER REJECTION'.              
                                                                                
       01  L281-PR-HEAD-04.                                                     
           05  FILLER              PIC X(14) VALUE ' S/NO  REGN NO'.            
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  FILLER              PIC X(11) VALUE 'HDB-REF    '.               
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'OFFER    '.                 
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'CURRENT  '.                 
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'DISCOUNT '.                 
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(30) VALUE 'FLAT ADDRESS'.              
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'REJECTION '.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE 'ALLOC'.                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'FLAT'.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'REJ '.                      
                                                                                
       01  L281-PR-HEAD-05.                                                     
           05  FILLER              PIC X(33) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'SELL PRICE'.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'SELL PRICE'.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'AMOUNT    '.                
           05  FILLER              PIC X(36) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'DATE      '.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE 'SCH  '.                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'TYPE'.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'CODE'.                      
                                                                                
       01  L281-PR-DETAIL.                                                      
           05  L281-NO             PIC ZZZZ9.                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L281-CUR-REGN-NO    PIC X(8).                                    
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L281-SCHEME         PIC 9(4).                                    
           05  L281-D1             PIC X.                                       
           05  L281-ACC            PIC 9(4).                                    
           05  L281-D2             PIC X.                                       
           05  L281-CHK            PIC 9.                                       
           05  L281-D3             PIC X.                                       
           05  L281-ACC-TYPE       PIC 9(2).                                    
           05  FILLER              PIC X(2) VALUE SPACES.                       
           05  L281-OFFR-SPRICE    PIC ZZ,ZZZ,ZZ9.                              
           05  FILLER              PIC X(2) VALUE SPACES.                       
           05  L281-CURR-SPRICE    PIC ZZ,ZZZ,ZZ9.                              
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L281-AMT-DISCOUNT   PIC ZZ,ZZ9.                                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
           05  L281-BLK            PIC X(04)     VALUE 'BLK '.                  
           05  L281-BLK-NO         PIC X(05).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L281-FLAT-NO.                                                    
               10  L281-HEX        PIC X(02)     VALUE '# '.                    
               10  L281-LEVEL-NO   PIC X(02).                                   
               10  L281-DASH       PIC X(01)     VALUE '-'.                     
               10  L281-UNIT-NO    PIC X(04).                                   
               10  L281-SUB-UNIT   PIC X(03).                                   
           05  FILLER              PIC X(11)  VALUE SPACES.                     
           05  L281-DTE-REJECT     PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(2)   VALUE SPACES.                     
           05  L281-ALLOC-SCH      PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  L281-FLAT-TYPE      PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  L281-CDE-REJECT     PIC X(02)  VALUE SPACES.                     
                                                                                
       01  L281-PR-DETAIL-A.                                                    
           05  FILLER              PIC X(69)     VALUE SPACES.                  
           05  L281-STREET         PIC X(20).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE 'S('.                    
           05  L281-POSTAL-CODE    PIC X(6).                                    
           05  FILLER              PIC X(1)      VALUE ')'.                     
       01  L281-PR-LINE.                                                        
           05  FILLER              PIC X         VALUE SPACES.                  
           05  FILLER              PIC X(131)    VALUE ALL '-'.                 
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       MAIN.                                                                    
      *-------------------------------------------------------------            
           PERFORM 000-OPENING-ROUTINE THRU 000-EXIT.                           
           PERFORM 100-READ-F310       THRU 100-EXIT.                           
           PERFORM 200-PROCESS-F310    THRU 200-EXIT                            
                   UNTIL WS-EOF-F310 = 'Y'.                                     
           PERFORM 999-CLOSING-ROUTINE.                                         
                                                                                
                                                                                
       000-OPENING-ROUTINE.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F310                                                 
                       BM06K110                                                 
                OUTPUT P13L281B                                                 
                       P13L281C.                                                
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSING-ROUTINE.                                      
                                                                                
           MOVE 60                   TO CNT-NUM-LINEB                           
                                        CNT-NUM-LINEC.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-READ-F310.                                                           
      *-------------------------------------------------------------            
            READ BP13F310 AT END                                                
                MOVE 'Y' TO WS-EOF-F310                                         
                GO TO 100-EXIT.                                                 
                                                                                
            ADD 1                     TO WS-CNT-READ-F310.                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-PROCESS-F310.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES       TO WS-K110-MATCH.                                  
           MOVE F310-SCH-ACC TO K110-KEY-FLD.                                   
           PERFORM 320-READ-K110 THRU 320-EXIT.                                 
           IF WS-K110-MATCH = 'Y'                                               
              IF K110-REGN-NO(1:2) = SPACES OR LOW-VALUES OR '##'               
                 PERFORM 500-PROCESS-LIST                                       
                    THRU 500-EXIT                                               
              ELSE                                                              
                 DISPLAY 'F310 --> SCH-ACC, REGN'                               
                          F310-SCH-ACC ',' F310-REGN-NO                         
                 DISPLAY 'K110 --> SCH-ACC, REGN'                               
                          K110-KEY-FLD ',' K110-REGN-NO                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-F310 THRU 100-EXIT.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       320-READ-K110.                                                           
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110.                                                       
           IF BM06K110-STATUS = 0                                               
              MOVE 'Y' TO WS-K110-MATCH                                         
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 MOVE 'N' TO WS-K110-MATCH                                      
                 DISPLAY 'REC NOT FOUND IN K110 ' K110-KEY-FLD                  
              ELSE                                                              
                 DISPLAY 'READ ERROR, K110-STATUS = ' BM06K110-STATUS           
                 DISPLAY 'K110-KEY-FLD = ' K110-KEY-FLD                         
                 MOVE BM06K110-STATUS TO RETURN-CODE                            
                 GO TO 999-CLOSING-ROUTINE.                                     
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PROCESS-LIST.                                                        
      *-------------------------------------------------------------            
           IF CNT-NUM-LINEB > 53                                                
               PERFORM 610-PRINT-LIST THRU 610-EXIT                             
           END-IF.                                                              
                                                                                
           IF CNT-NUM-LINEC > 53                                                
               PERFORM 620-PRINT-LIST THRU 620-EXIT                             
           END-IF.                                                              
                                                                                
           PERFORM 550-PREP-LIST  THRU 550-EXIT.                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       550-PREP-LIST.                                                           
      *-------------------------------------------------------------            
           MOVE F310-REGN-NO         TO L281-CUR-REGN-NO.                       
           MOVE '-'                  TO L281-D1 L281-D2                         
                                        L281-D3.                                
           MOVE K110-SCHEME          TO L281-SCHEME.                            
           MOVE K110-ACC             TO L281-ACC.                               
           MOVE K110-CHK             TO L281-CHK.                               
           MOVE K110-ACC-TYPE        TO L281-ACC-TYPE.                          
                                                                                
           PERFORM 560-GET-ADDRESS   THRU 560-EXIT.                             
                                                                                
           MOVE F310-NUM-ALLOC-SCH   TO L281-ALLOC-SCH.                         
                                                                                
           STRING F310-DTE-DD '/'                                               
                  F310-DTE-MM '/'                                               
                  F310-DTE-CC                                                   
                  F310-DTE-YY                                                   
                  DELIMITED BY SIZE INTO L281-DTE-REJECT.                       
                                                                                
           MOVE K110-1ST-OFFER-SELLING-PRICE TO L281-OFFR-SPRICE.               
           MOVE K110-CURR-SELLING-PRICE      TO L281-CURR-SPRICE.               
           COMPUTE WS-DISCOUNT-AMT = K110-1ST-OFFER-SELLING-PRICE -             
                                     K110-CURR-SELLING-PRICE.                   
           MOVE WS-DISCOUNT-AMT              TO L281-AMT-DISCOUNT.              
           MOVE F310-CDE-TRANS               TO L281-CDE-REJECT.                
                                                                                
           PERFORM 888-CHK-SERS-ALSCH       THRU 888-EXIT                       
           IF C-SERS-ALLO-SCHEME                                                
              IF WS-FLAGB = 'N'                                                 
                 PERFORM 660-PRINT-HEADER-B THRU 660-EXIT                       
              END-IF                                                            
                                                                                
              ADD 1                      TO WS-SEQB                             
              MOVE WS-SEQB               TO L281-NO                             
              WRITE P13L281B-REC         FROM L281-PR-DETAIL                    
              WRITE P13L281B-REC         FROM L281-PR-DETAIL-A                  
              MOVE SPACES TO P13L281B-REC                                       
              WRITE P13L281B-REC                                                
              ADD 3                      TO CNT-NUM-LINEB                       
           END-IF.                                                              
                                                                                
           IF F310-NUM-ALLOC-SCH = 'RED'                                        
              IF WS-FLAGC = 'N'                                                 
                 PERFORM 670-PRINT-HEADER-C THRU 670-EXIT                       
              END-IF                                                            
                                                                                
              ADD 1                      TO WS-SEQC                             
              MOVE WS-SEQC               TO L281-NO                             
              WRITE P13L281C-REC         FROM L281-PR-DETAIL                    
              WRITE P13L281C-REC         FROM L281-PR-DETAIL-A                  
              MOVE SPACES TO P13L281C-REC                                       
              WRITE P13L281C-REC                                                
              ADD 3                      TO CNT-NUM-LINEC                       
           END-IF.                                                              
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       560-GET-ADDRESS.                                                         
      *-------------------------------------------------------------            
           MOVE F310-NUM-BLK            TO L281-BLK-NO.                         
           MOVE F310-NME-STREET         TO L281-STREET.                         
           MOVE F310-NUM-LEVEL          TO L281-LEVEL-NO.                       
           MOVE F310-NUM-MAIN (1:4)     TO L281-UNIT-NO.                        
           MOVE F310-NUM-MAIN (5:1)     TO L281-SUB-UNIT.                       
           MOVE F310-NUM-POSTAL         TO L281-POSTAL-CODE.                    
           MOVE F310-CDE-TYP-ACTUSE     TO L281-FLAT-TYPE.                      
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       610-PRINT-LIST.                                                          
      *-------------------------------------------------------------            
            PERFORM 660-PRINT-HEADER-B THRU 660-EXIT.                           
            MOVE SPACES           TO P13L281B-REC.                              
            ADD 1                 TO CNT-NUM-LINEB.                             
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       620-PRINT-LIST.                                                          
      *-------------------------------------------------------------            
            PERFORM 670-PRINT-HEADER-C THRU 670-EXIT.                           
            MOVE SPACES           TO P13L281C-REC.                              
            ADD 1                 TO CNT-NUM-LINEC.                             
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       660-PRINT-HEADER-B.                                                      
      *-------------------------------------------------------------            
           PERFORM 888-CHK-SERS-ALSCH   THRU 888-EXIT.                          
           IF C-SERS-ALLO-SCHEME                                                
              ADD 1             TO CNT-PAGEB                                    
              MOVE CNT-PAGEB    TO L281B-PAGENO                                 
              MOVE 8            TO CNT-NUM-LINEB                                
              MOVE FUNCTION CURRENT-DATE TO WS-DATE                             
              STRING WS-DATE (7:2) '/'                                          
                     WS-DATE (5:2) '/'                                          
                     WS-DATE (1:4)                                              
                     DELIMITED BY SIZE INTO L281B-DATE                          
                                                                                
               WRITE P13L281B-REC FROM L281B-PR-HEAD-01 AFTER PAGE              
               WRITE P13L281B-REC FROM L281-PR-HEAD-02                          
               WRITE P13L281B-REC FROM L281-PR-HEAD-03                          
               WRITE P13L281B-REC FROM L281-PR-HEAD-04 AFTER 3                  
               WRITE P13L281B-REC FROM L281-PR-HEAD-05                          
               WRITE P13L281B-REC FROM L281-PR-LINE                             
                                                                                
               MOVE SPACES           TO P13L281B-REC                            
               WRITE P13L281B-REC                                               
               MOVE 'Y'              TO WS-FLAGB                                
                                                                                
            END-IF.                                                             
                                                                                
       660-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       670-PRINT-HEADER-C.                                                      
      *-------------------------------------------------------------            
           IF F310-NUM-ALLOC-SCH = 'RED'                                        
              ADD 1             TO CNT-PAGEC                                    
              MOVE CNT-PAGEC    TO L281C-PAGENO                                 
              MOVE 8            TO CNT-NUM-LINEC                                
              MOVE FUNCTION CURRENT-DATE TO WS-DATE                             
              STRING WS-DATE (7:2) '/'                                          
                     WS-DATE (5:2) '/'                                          
                     WS-DATE (1:4)                                              
                     DELIMITED BY SIZE INTO L281C-DATE                          
                                                                                
              WRITE P13L281C-REC FROM L281C-PR-HEAD-01 AFTER PAGE               
              WRITE P13L281C-REC FROM L281-PR-HEAD-02                           
              WRITE P13L281C-REC FROM L281-PR-HEAD-03                           
              WRITE P13L281C-REC FROM L281-PR-HEAD-04 AFTER 3                   
              WRITE P13L281C-REC FROM L281-PR-HEAD-05                           
              WRITE P13L281C-REC FROM L281-PR-LINE                              
                                                                                
              MOVE SPACES           TO P13L281C-REC                             
              WRITE P13L281C-REC                                                
              MOVE 'Y'              TO WS-FLAGC                                 
                                                                                
           END-IF.                                                              
                                                                                
       670-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       888-CHK-SERS-ALSCH.                                                      
      *-------------------------------------------------------------            
      *----------------------------------------------------------------         
      * SEARCH SERS TABLE TO CHECK ALLOC SCHEME                                 
      *----------------------------------------------------------------         
           SET WS-SERS-PTR TO 1.                                                
           SEARCH WS-SERS-SCH                                                   
              AT END                                                            
                 MOVE 'N'      TO SW-SERS-ALLO-SCHEME                           
              WHEN WS-SERS-SCHEME (WS-SERS-PTR) = F310-NUM-ALLOC-SCH            
                 MOVE 'Y'      TO SW-SERS-ALLO-SCHEME                           
           END-SEARCH.                                                          
                                                                                
       888-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSING-ROUTINE.                                                     
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '- BP13C281 CONTROL TOTALS -----------------'.               
           DISPLAY 'TOTAL NO OF REC READ FROM BP13F310 = '                      
                   WS-CNT-READ-F310.                                            
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F310                                                       
                 BM06K110                                                       
                 P13L281B                                                       
                 P13L281C.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
              DISPLAY 'CLOSING ERROR, K110-STATUS ' BM06K110-STATUS.            
                                                                                
           STOP RUN.                                                            
