      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C24S.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  04 JUNE 2014.                                             
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  TO SORT GRO UNIT ACCORDING TO BP13F205      *          
      *                                                              *          
      *    INPUT FILES  : BP13F230                                   *          
      *                 : BP13F205                                   *          
      *                 : BP13K060                                   *          
      *    OUTPUT       : P13F230A                                  *           
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP135404 ESA1 04062014 NEW PROGRAM                            *          
      *BP135463 ESA1 10092014 CATER FOR MULTIPLE BP13F205            *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13F230 ASSIGN TO BP13F230.                                  
                                                                                
           SELECT BP13K060  ASSIGN        TO BP13K060                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K060-KEY-FLD                       
                            FILE STATUS   IS WS-K060-STATUS.                    
                                                                                
                                                                                
           SELECT P13F230A ASSIGN TO P13F230A.                                  
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F230                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       COPY BP13K230.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25  CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   P13F230A                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F230A-REC      PIC  X(450).                                      
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F230-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-K060-STATUS             PIC 9(2)  VALUE 0.                    
                                                                                
           05  WS-F230-COUNTER            PIC 9(2)  VALUE 0.                    
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-NUM-NEW-TOWN            PIC X(20) VALUE SPACES.               
           05  WS-CNT-F230-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F205-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F230-WRITE          PIC 9(8)  VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1200-READ-F205         THRU 1200-EXIT.                       
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
               UNTIL WS-F205-EOF = 'Y'.                                         
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13K060                                                 
                OUTPUT P13F230A.                                                
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K060 STATUS ' WS-K060-STATUS               
              MOVE     WS-K060-STATUS TO RETURN-CODE                            
              PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F205.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F205-READ.                                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 2200-GET-NME-TOWN    THRU 2200-EXIT.                         
                                                                                
           OPEN INPUT BP13F230.                                                 
           PERFORM 2100-READ-F230       THRU 2100-EXIT.                         
                                                                                
           MOVE    'N'                  TO   WS-F230-EOF.                       
                                                                                
           PERFORM 2050-PROCESS         THRU 2050-EXIT                          
                UNTIL WS-F230-EOF = 'Y'.                                        
                                                                                
           CLOSE  BP13F230.                                                     
                                                                                
           PERFORM 1200-READ-F205         THRU 1200-EXIT.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2050-PROCESS.                                                            
      *--------------------------------------------------------------*          
                                                                                
                                                                                
           IF ((F205-DTE-BALLOT    = K230-DTE-BALLOT)   AND                     
               (F205-NUM-SELECTION = K230-NUM-SELECTION) AND                    
               (WS-NUM-NEW-TOWN    = K230-NUM-NEW-TOWN))                        
              MOVE K230-DETAIL-REC   TO P13F230A-REC                            
              WRITE P13F230A-REC                                                
              ADD  1                 TO WS-CNT-F230-WRITE                       
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-F230       THRU 2100-EXIT.                         
                                                                                
       2050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2100-READ-F230.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F230                                                        
                AT END  MOVE 'Y' TO WS-F230-EOF                                 
                GO TO 2100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F230-READ.                                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       2200-GET-NME-TOWN.                                                       
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                TO WS-NUM-NEW-TOWN.                       
           MOVE SPACES                TO K060-KEY-FLD.                          
           MOVE 02                    TO K060-SERIAL-NO.                        
           MOVE F205-NUM-NT-ZONE      TO K060-CODE.                             
                                                                                
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS = 00                                               
              MOVE K060-DESC(1:20)    TO WS-NUM-NEW-TOWN                        
           ELSE                                                                 
              IF WS-K060-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K060, KEY: '                  
                         K060-KEY-FLD                                           
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13K060, STATUS '                 
                          WS-K060-STATUS                                        
                         ', KEY: ' K060-KEY-FLD                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F205                                                       
                 BP13K060                                                       
                 P13F230A.                                                      
                                                                                
           IF WS-K060-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K060-STAT ' WS-K060-STATUS             
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C24S CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY 'NO. OF READ (BP13F230) = ' WS-CNT-F230-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECS WRITE      = '.                                 
           DISPLAY '    (P13F230A)         = ' WS-CNT-F230-WRITE.               
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
