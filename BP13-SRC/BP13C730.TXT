      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C730.                                                 
      *AUTHOR.        CLT1.                                                     
      *DATE-WRITTEN.  30/07/2003.                                               
                                                                                
      ******************************************************************        
      *    SYSTEM NAME :  SYSTEM OF COMMITMENT                         *        
      *                                                                *        
      *    SYSTEM ID   :  BP13                                         *        
      *                                                                *        
      *    OBJECTIVE   :  TO UPDATE K762-NUM-DEMAND FIELD IN BP13K762  *        
      *                   BY SUMMING-UP BP13K730 FOR EACH UNIQUE       *        
      *                   BTO CONTRACT AND FLAT TYPE.                  *        
      *    INPUT FILES :                                               *        
      *         1.  BP13FMAT  -  SEQ. FILE OF BP13K730 SORTED BY       *        
      *                          K730-CDE-NT1, K730-CDE-FLAT-TYPE AND  *        
      *                          K730-CDE-HOUSEHOLD                    *        
      *         2.  SY02F001  -  DATE FILE                             *        
      *                                                                *        
      *    I-O   FILES :                                               *        
      *         1.  BP13K762  -  SUMMARY FILE FOR DEMAND/SUPPLY        *        
      *                                                                *        
      *    LISTING     :                                               *        
      *         1.  BP13L730  -  UPDATE REPORT                         *        
      *                                                                *        
      *                                                                *        
      ******************************************************************        
      *    CHG-NO    BY      DATE     DESCRIPTION                      *        
      *    -------   ---   --------   ------------------------------   *        
      *    BP132368  CLT   30/07/03   SAME AS BP13C791                 *        
      *    BP132454  CLT   08/12/03   FOR DEC03 BTO                    *        
      *    BP132494  JF4   11/03/04   FOR MAR04 BTO                    *        
      *    BP132533  JF4   13/05/04   FOR MAY04 BTO DUXTON PLAIN       *        
      *    BP132588  JF4   27/09/04   STANDARDISE NT DESC              *        
      *    BP133008  CLT1  27/03/07   CATER FOR 3R                     *        
      *    BP133439  CCC5  02/10/08   CATER FOR QTRLY SALE OCT08 WHERE *        
      *                               SEPARATE DEMAND FOR SA & 2R/3R   *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FMAT  ASSIGN                TO  BP13FMAT.                 
                                                                                
           SELECT BP13K762  ASSIGN                TO  BP13K762                  
                            ACCESS                IS  RANDOM                    
                            ORGANIZATION          IS  INDEXED                   
                            RECORD KEY            IS  K762-KEY-FLD              
                            FILE STATUS           IS  WS-K762-STATUS.           
                                                                                
           SELECT BP13L730  ASSIGN                TO  BP13L730.                 
           SELECT SY02F001  ASSIGN                TO  SY02F001.                 
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FMAT                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80 CHARACTERS.                                      
       01   BP13FMAT-REC.                                                       
            05 FMAT-KEY-FLD.                                                    
               10  FMAT-CDE-NT1        PIC X(03).                               
               10  FMAT-CDE-FLAT-TYPE  PIC X(02).                               
            05 FILLER                  PIC X(75).                               
                                                                                
       FD   BP13K762                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K762.                                                           
                                                                                
       FD   BP13L730                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L730-REC                   PIC X(132).                          
                                                                                
       COPY SY02F001.                                                           
                                                                                
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K762-STATUS          PIC 9(2)   VALUE 00.                     
                                                                                
       01  WS-SWITCHES.                                                         
           05  EOF-FMAT-IND            PIC X      VALUE 'N'.                    
               88  EOF-FMAT                       VALUE 'Y'.                    
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-NUM-READ             PIC 9(4)   VALUE ZERO.                   
           05  WS-UPDATE-CTR           PIC 9(4)   VALUE ZERO.                   
           05  WS-NOUPDT-CTR           PIC 9(4)   VALUE ZERO.                   
           05  WS-K762-READ            PIC 9(4)   VALUE ZERO.                   
           05  WS-DEMAND-CTR           PIC 9(4)   VALUE ZERO.                   
           05  WS-MAX-LINE-CTR         PIC 9(05)  VALUE 58.                     
           05  WS-LINE-CTR             PIC 9(05)  VALUE 99.                     
           05  WS-PAGE-CTR             PIC 9(05)  VALUE 0.                      
                                                                                
       01  WS-SAVE-KEY.                                                         
           03  WS-K730-CDE-NT1         PIC X(03).                               
           03  WS-K730-CDE-FLAT-TYPE   PIC X(02).                               
                                                                                
       01  WS-SYSTEM-DATETIME.                                                  
           05  WS-SYS-DATE             PIC 9(08) VALUE ZEROES.                  
           05  WS-SYS-TIME             PIC 9(08) VALUE ZEROES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
                                                                                
       01  WS-F001-DTE-CURRENT.                                                 
           05  WS-F001-CCYYMMDD        PIC X(08).                               
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *         LAY-OUT FOR UPDATE REPORT                              *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       01  WS-LINER                 PIC X(132) VALUE ALL '~'.                   
                                                                                
       01  WS-L730-LINER1.                                                      
           10  FILLER               PIC X(132) VALUE ALL "-".                   
                                                                                
       01  WS-L730-HEADER1.                                                     
           10  FILLER               PIC X(05)  VALUE SPACES.                    
           10  FILLER               PIC X(20)  VALUE                            
               'NEW TOWN/FT/HH      '.                                          
           10  FILLER               PIC X(32)  VALUE                            
               '* NEW TOWN DESCRIPTION          '.                              
           10  FILLER               PIC X(24)  VALUE                            
               '*OLD DEMAND * NEW DEMAND'.                                      
           10  FILLER               PIC X(51)  VALUE                            
               '   R E M A R K S                '.                              
                                                                                
       01  WS-L730-TITLE1.                                                      
           05  FILLER              PIC X(08)   VALUE 'BP13L730'.                
           05  FILLER              PIC X(06)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'HDB3'.                    
           05  FILLER              PIC X(36)   VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE 'SOC  SYSTEM'.             
           05  FILLER              PIC X(32)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'DATE : '.                 
           05  WS-L730-CUR-DATE.                                                
              10  WS-L730-CUR-DD   PIC X(02).                                   
              10  FILLER           PIC X(01)   VALUE '/'.                       
              10  WS-L730-CUR-MM   PIC X(02).                                   
              10  FILLER           PIC X(01)   VALUE '/'.                       
              10  WS-L730-CUR-CC   PIC X(02).                                   
              10  WS-L730-CUR-YY   PIC X(02).                                   
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'PAGE : '.                 
           05  WS-L730-PAGENO      PIC ZZZZ.                                    
                                                                                
       01  WS-L730-TITLE2.                                                      
           05  FILLER              PIC X(40)   VALUE SPACES.                    
           05  FILLER              PIC X(41)   VALUE                            
           '    SUMMARIZATION OF FLAT DEMAND REQUESTS'.                         
           05  FILLER              PIC X(51)   VALUE SPACES.                    
                                                                                
       01  WS-L730-DETAIL1.                                                     
           05  FILLER               PIC X(10)  VALUE SPACES.                    
           05  WS-L730-KEY-FLD      PIC X(17).                                  
           05  WS-L730-NT-DESC      PIC X(30).                                  
           05  FILLER               PIC X(04)  VALUE SPACES.                    
           05  WS-L730-OLD-DEMAND   PIC X(04).                                  
           05  FILLER               PIC X(09)  VALUE SPACES.                    
           05  WS-L730-NEW-DEMAND   PIC X(04).                                  
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-L730-REMARKS      PIC X(48).                                  
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *----------------------*                                                  
       000-MAIN-ROUTINE.                                                        
      *----------------------*                                                  
           PERFORM  100-OPEN-ROUTINE      THRU   100-EXIT.                      
           PERFORM  150-READ-SY02F001     THRU   150-EXIT.                      
           PERFORM  200-READ-BP13FMAT     THRU   200-EXIT.                      
                                                                                
      *---------------------------------------------------*                     
      * CONVERT EXEC FLAT TO FLAT TYPE '5' BEFORE UPDATING                      
      *---------------------------------------------------*                     
           IF FMAT-CDE-FLAT-TYPE = 'E '                                         
              MOVE '5 '  TO  FMAT-CDE-FLAT-TYPE                                 
           END-IF.                                                              
                                                                                
      *    IF FMAT-CDE-NT1 = '3R'                                               
      *       MOVE '3 '  TO  FMAT-CDE-FLAT-TYPE                                 
      *    END-IF.                                                              
                                                                                
           MOVE     FMAT-KEY-FLD          TO WS-SAVE-KEY.                       
                                                                                
           PERFORM  300-PROCESS-ROUTINE   THRU   300-EXIT                       
                        UNTIL EOF-FMAT.                                         
           PERFORM  900-CLOSE-ROUTINE     THRU   900-EXIT.                      
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                       *        
      ******************************************************************        
      *----------------------*                                                  
       100-OPEN-ROUTINE.                                                        
      *----------------------*                                                  
           OPEN INPUT  BP13FMAT                                                 
                       SY02F001                                                 
                I-O    BP13K762                                                 
                OUTPUT BP13L730.                                                
                                                                                
           IF WS-K762-STATUS NOT = 00 AND 97                                    
              DISPLAY ' OPENING BP13K762 ERROR  ' WS-K762-STATUS                
              MOVE    WS-K762-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATETIME.                  
           DISPLAY 'WS-SYS-DATE = ' WS-SYS-DATE.                                
                                                                                
           MOVE WS-SYS-DATE(1:4)        TO WS-DISP-CCYY.                        
           MOVE WS-SYS-DATE(5:2)        TO WS-DISP-MM.                          
           MOVE WS-SYS-DATE(7:2)        TO WS-DISP-DD.                          
           MOVE WS-DISP-DATE            TO WS-L730-CUR-DATE.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ SYSTEM DATE FROM SY02F001                              *        
      ******************************************************************        
      *----------------------*                                                  
       150-READ-SY02F001.                                                       
      *----------------------*                                                  
           READ SY02F001 AT END                                                 
                DISPLAY 'SY02F001 - SYSTEM DATE FILE IS EMPTY'                  
                MOVE 99 TO RETURN-CODE                                          
                PERFORM 900-CLOSE-ROUTINE.                                      
                                                                                
            MOVE F001-DTE-CURRENT TO WS-F001-CCYYMMDD.                          
            CLOSE SY02F001.                                                     
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ SEQUENTIAL FILE BP13FMAT AT END SET EOF-IND TO 'Y'     *        
      ******************************************************************        
      *----------------------*                                                  
       200-READ-BP13FMAT.                                                       
      *----------------------*                                                  
           READ BP13FMAT AT END                                                 
                         MOVE HIGH-VALUES TO FMAT-KEY-FLD                       
                         GO TO 200-EXIT.                                        
                                                                                
           ADD 1 TO WS-NUM-READ.                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PROCESS RECORD, ACCUMULATING EACH NEW-TOWN,FLAT-TYPE & HHLD *        
      ******************************************************************        
      *----------------------*                                                  
       300-PROCESS-ROUTINE.                                                     
      *----------------------*                                                  
      *---------------------------------------------------*                     
      * CONVERT EXEC FLAT TO FLAT TYPE '5' BEFORE UPDATING                      
      *---------------------------------------------------*                     
           IF FMAT-CDE-FLAT-TYPE = 'E '                                         
              MOVE '5 '  TO  FMAT-CDE-FLAT-TYPE                                 
           END-IF.                                                              
                                                                                
      *    IF FMAT-CDE-NT1 = '3R'                                               
      *       MOVE '3 '  TO  FMAT-CDE-FLAT-TYPE                                 
      *    END-IF.                                                              
                                                                                
      *    DISPLAY 'FMAT-KEY-FLD = ' FMAT-KEY-FLD.                              
      *    DISPLAY 'WS-SAVE-KEY  = ' WS-SAVE-KEY.                               
                                                                                
           IF FMAT-KEY-FLD NOT = WS-SAVE-KEY                                    
              MOVE 'BTO/BE '     TO WS-L730-NT-DESC                             
              PERFORM 400-UPDATE-K762-DEMAND THRU 400-EXIT                      
              MOVE ZEROES        TO WS-DEMAND-CTR                               
              MOVE FMAT-KEY-FLD  TO WS-SAVE-KEY                                 
           END-IF.                                                              
                                                                                
           ADD 1                 TO WS-DEMAND-CTR.                              
                                                                                
           IF FMAT-KEY-FLD = HIGH-VALUES                                        
              MOVE 'Y'           TO EOF-FMAT-IND                                
           ELSE                                                                 
              PERFORM  200-READ-BP13FMAT  THRU   200-EXIT                       
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ VSAM FILE BP13K762 TO UPDATE K762-NUM-DEMAND           *        
      ******************************************************************        
      *----------------------*                                                  
       400-UPDATE-K762-DEMAND.                                                  
      *----------------------*                                                  
           MOVE  WS-SAVE-KEY   TO  K762-KEY-FLD.                                
           READ  BP13K762.                                                      
                                                                                
           EVALUATE WS-K762-STATUS                                              
           WHEN 00                                                              
              ADD   1                   TO    WS-K762-READ                      
              MOVE  K762-NUM-DEMAND     TO    WS-L730-OLD-DEMAND                
              MOVE  WS-DEMAND-CTR       TO    K762-NUM-DEMAND                   
                                              WS-L730-NEW-DEMAND                
                                                                                
              STRING   WS-SAVE-KEY  (1:3) '/'                                   
                       WS-SAVE-KEY  (4:1)                                       
                       DELIMITED BY SIZE                                        
                       INTO    WS-L730-KEY-FLD                                  
              END-STRING                                                        
                                                                                
              MOVE WS-SYS-DATE             TO   K762-DTE-UPDATE                 
              PERFORM 500-REWRITE-BP13K762 THRU 500-EXIT                        
                                                                                
           WHEN 23                                                              
              DISPLAY ' BP13K762 RECORD NOT FOUND, KEY : ' WS-SAVE-KEY          
              ADD 1                     TO    WS-NOUPDT-CTR                     
              MOVE  '    '              TO    WS-L730-OLD-DEMAND                
              MOVE  WS-DEMAND-CTR       TO    WS-L730-NEW-DEMAND                
              STRING   WS-SAVE-KEY  (1:3) '/'                                   
                       WS-SAVE-KEY  (4:1)                                       
                       DELIMITED BY SIZE                                        
                       INTO    WS-L730-KEY-FLD                                  
              END-STRING                                                        
              MOVE '*** NOT UPDATED, BP13K762 RECORD NOT FOUND'                 
                                        TO    WS-L730-REMARKS                   
              PERFORM 800-PRT-REPORT THRU 800-EXIT                              
                                                                                
           WHEN OTHER                                                           
              DISPLAY ' ERROR READING BP13K762    '  WS-K762-STATUS             
              MOVE      WS-K762-STATUS TO RETURN-CODE                           
              PERFORM   900-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    UPDATE K762-NUM-DEMAND                                      *        
      ******************************************************************        
      *----------------------*                                                  
       500-REWRITE-BP13K762.                                                    
      *----------------------*                                                  
           REWRITE BP13K762-REC.                                                
                                                                                
           EVALUATE WS-K762-STATUS                                              
           WHEN 00                                                              
                ADD 1                          TO WS-UPDATE-CTR                 
                MOVE 'UPDATED SUCCESSFULLY'    TO WS-L730-REMARKS               
                PERFORM 800-PRT-REPORT THRU 800-EXIT                            
                                                                                
           WHEN OTHER                                                           
                ADD 1                          TO WS-NOUPDT-CTR                 
                DISPLAY 'BP13K762 - ERROR REWRITING RECORD, STATUS: '           
                         WS-K762-STATUS                                         
                MOVE 'NOT UPDATED, BP13K762 - ERROR REWRITING REC'              
                                               TO WS-L730-REMARKS               
                PERFORM 800-PRT-REPORT THRU 800-EXIT                            
                                                                                
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       800-PRT-REPORT.                                                          
      *----------------------*                                                  
           IF WS-LINE-CTR > WS-MAX-LINE-CTR                                     
              ADD 1              TO   WS-PAGE-CTR                               
              MOVE WS-PAGE-CTR   TO   WS-L730-PAGENO                            
              WRITE BP13L730-REC FROM WS-L730-TITLE1  AFTER PAGE                
              WRITE BP13L730-REC FROM WS-L730-TITLE2  AFTER 1                   
              WRITE BP13L730-REC FROM WS-L730-LINER1  AFTER 2                   
              WRITE BP13L730-REC FROM WS-L730-HEADER1 AFTER 1                   
              WRITE BP13L730-REC FROM WS-L730-LINER1  AFTER 1                   
              MOVE 5             TO   WS-LINE-CTR                               
           END-IF.                                                              
                                                                                
           WRITE BP13L730-REC    FROM WS-L730-DETAIL1 AFTER 1.                  
           ADD 1                 TO   WS-LINE-CTR.                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    -  DISPLAY NECESSARY STATISTICS                             *        
      *    -  CLOSE ALL FILES   AND   STOP RUN                         *        
      ******************************************************************        
      *----------------------*                                                  
       900-CLOSE-ROUTINE.                                                       
      *----------------------*                                                  
           DISPLAY 'PROGRAM ID   : BP13C730'.                                   
           DISPLAY 'CURRENT-DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
                                                                                
           DISPLAY 'NO OF RECS READ    FROM BP13FMAT:' WS-NUM-READ.             
           DISPLAY 'NO OF RECS READ    FROM BP13K762:' WS-K762-READ.            
           DISPLAY 'NO OF RECS UPDATED INTO BP13K762:' WS-UPDATE-CTR.           
           DISPLAY 'NO OF RECS NOT UPDATED- BP13K762:' WS-NOUPDT-CTR.           
           DISPLAY 'SYSTEM DATE USED TO UPDATE  K762:' WS-F001-CCYYMMDD.        
                                                                                
           CLOSE BP13FMAT                                                       
                 BP13K762                                                       
                 BP13L730.                                                      
                                                                                
           IF WS-K762-STATUS NOT = 00                                           
              DISPLAY ' CLOSING BP13K762 ERROR  ' WS-K762-STATUS                
              MOVE WS-K762-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
