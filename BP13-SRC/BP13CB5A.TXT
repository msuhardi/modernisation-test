      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB5A.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  05 APRIL 2016.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM UPDATE BP13KD05 FOR THOSE      *           
      *                 CASES THAT ARE MATCHED FROM CPF             *           
      *                                                             *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  AB02F935 -- RETURN FILE FROM CPF BOARD                *           
      *   2.  BP13KD05 -- SOC GRANT FILE                            *           
      *                                                             *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136181 05/04/2016 FNP1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD05 ASSIGN TO BP13KD05                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD05-KEY-FLD                               
                  FILE STATUS     IS BP13KD05-STATUS.                           
                                                                                
                                                                                
           SELECT AB02F935   ASSIGN       TO AB02F935.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD05                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD   AB02F935                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AB02F935.                                                   00055007
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-F935-EOF                PIC X       VALUE SPACE.              
           05  WS-KD05-EOF                PIC X       VALUE SPACE.              
           05  WS-KD05-FND                PIC X       VALUE SPACE.              
           05  BP13KD05-STATUS            PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT                     PIC 9       VALUE ZEROES.             
           05  WS-CTR                     PIC 9       VALUE ZEROES.             
           05  CNT-F935-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-KD05-FND               PIC 9(5)    VALUE ZEROES.             
           05  WS-REWRITE-BP13KD05        PIC 9(5)    VALUE ZEROS.              
                                                                                
                                                                                
       01  WS-DATE                        PIC X(08)   VALUE SPACES.             
       01  WS-CURRENT-DATE.                                                     
           02  WS-CURRENT-DATE1.                                                
               05  WS-DTE-CCYY            PIC X(4).                             
               05  WS-DTE-MM              PIC X(2).                             
               05  WS-DTE-DD              PIC X(2).                             
           02  WS-CURRENT-TIME            PIC X(9).                             
           02  FILLER                     PIC X(4).                             
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F935    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F935-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  AB02F935                                                 
                I-O    BP13KD05.                                                
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD05 : ' BP13KD05-STATUS             
              MOVE BP13KD05-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F935.                                                          
      *---------------*                                                         
                                                                                
           READ AB02F935 AT END                                                 
                MOVE HIGH-VALUES TO AB02F935-DETAIL-REC                         
                MOVE 'Y' TO WS-F935-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F935-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3100-PROCESS-REC     THRU 3100-EXIT.                         
                                                                                
           PERFORM 2000-READ-F935    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES                       TO KD05-KEY-FLD.                   
           MOVE F935-NUM-REGN                TO KD05-NUM-REGN.                  
           MOVE F935-NUM-CPF-ACCT            TO KD05-NUM-NRIC.                  
                                                                                
           READ BP13KD05.                                                       
           EVALUATE BP13KD05-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 5000-REWRITE-BP13KD05  THRU 5000-EXIT                  
                 ADD 1 TO CNT-KD05-FND                                          
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13KD05: ' KD05-KEY-FLD          
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13KD05'                               
                 MOVE BP13KD05-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-REWRITE-BP13KD05.                                                   
      *-----------------*                                                       
           MOVE 'C'               TO KD05-NUM-CPF-SEND-STATUS.                  
           MOVE 'BP13CB5A'        TO KD05-NUM-USERID.                           
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-CURRENT-DATE.                      
           MOVE WS-CURRENT-DATE1       TO KD05-DTE-UPDATE.                      
           MOVE WS-CURRENT-TIME        TO KD05-TME-UPDATE.                      
                                                                                
           REWRITE BP13KD05-REC.                                                
                                                                                
           IF BP13KD05-STATUS = 00 OR 02                                        
              ADD 1 TO  WS-REWRITE-BP13KD05                                     
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13KD05:' KD05-KEY-FLD                  
              MOVE BP13KD05-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB5A *************'.                            
           DISPLAY 'NO OF RECS READ FROM  AB02F935  : ' CNT-F935-READ.          
           DISPLAY 'NO OF RECS FOUND IN   BP13KD05  : ' CNT-KD05-FND.           
           DISPLAY 'NO OF RECS REWRITE TO BP13KD05  : '                         
                                               WS-REWRITE-BP13KD05.             
           CLOSE BP13KD05                                                       
                 AB02F935.                                                      
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD05-STATUS ' BP13KD05-STATUS          
            MOVE BP13KD05-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
