       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C92Y.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  25/05/2017.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVES  : CREATE BP13FW3E FROM BM06F130               *           
      *                                                             *           
      *   INPUT FILES : BM06F130 - RECORD TO BE PROCESSED           *           
      *                 BM06K510 - GET NME-NEW-TOWN                 *           
      *   OUTPUT FILE : BP13FW3E - FILE TO BE WRITTEN               *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ   DATE      BY    DESCRIPTIONS                      *           
      * --------  --------  ----  --------------------------------- *           
      * BP136299  25052017  SMR2  NEW PROGRAM                       *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F130 ASSIGN       TO BM06F130.                            
                                                                                
           SELECT BM06K510 ASSIGN       TO BM06K510                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K510-ESTCDE                          
                           FILE STATUS  IS WS-K510-STATUS.                      
                                                                                
           SELECT BP13FW3E ASSIGN       TO BP13FW3E.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BM06F130                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BM06F130.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS  60 CHARACTERS.                                     
       COPY BM06K510.                                                           
                                                                                
       FD   BP13FW3E                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13FW3E.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K510-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F130-EOF                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F130-READ                PIC 9(06)   VALUE ZEROES.            
           05  WS-K510-FND                 PIC 9(06)   VALUE ZEROES.            
           05  WS-K510-NOTFND              PIC 9(06)   VALUE ZEROES.            
           05  WS-FW3E-WRITE               PIC 9(06)   VALUE ZEROES.            
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                     PIC X(08)   VALUE SPACES.            
           05  WS-DATE-EDIT                PIC X(10)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NME-NT                   PIC X(20)   VALUE SPACES.            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BM06F130       THRU 2000-EXIT                      
             UNTIL WS-F130-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BM06F130                                                 
                       BM06K510                                                 
                OUTPUT BP13FW3E.                                                
                                                                                
           IF WS-K510-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BM06K510 ERROR ' WS-K510-STATUS                  
              MOVE WS-K510-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(1:4) '-' WS-DATE(5:2) '-' WS-DATE(7:2)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BM06F130.                                                      
      *-------------------*                                                     
                                                                                
           READ BM06F130 AT END                                                 
                MOVE 'Y'                    TO WS-F130-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F130-READ.                    
                                                                                
           PERFORM 3000-WRITE-BP13FW3E      THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3000-WRITE-BP13FW3E.                                                     
      *--------------------*                                                    
                                                                                
           MOVE SPACES                      TO BP13FW3E-REC.                    
           INITIALIZE                          BP13FW3E-REC.                    
                                                                                
           PERFORM 4000-READ-BM06K510       THRU 4000-EXIT.                     
                                                                                
           MOVE WS-NME-NT                   TO FW3E-NME-NEW-TOWN.               
           MOVE F130-NEIGHBOURHOOD          TO FW3E-NEIGHBOURHOOD.              
           MOVE F130-CONTRACT-NO            TO FW3E-CONTRACT-NO.                
                                                                                
           MOVE F130-ESTATE                 TO FW3E-ESTATE.                     
           MOVE F130-NT-CODE                TO FW3E-NUM-NT.                     
           MOVE F130-NME-LOCAL-PRCNCT       TO FW3E-NME-LOCAL-PRCNCT.           
                                                                                
           MOVE ZEROES                      TO FW3E-NUM-UNIT-01                 
                                               FW3E-NUM-UNIT-2A                 
                                               FW3E-NUM-UNIT-2B                 
                                               FW3E-NUM-UNIT-03                 
                                               FW3E-NUM-UNIT-04                 
                                               FW3E-NUM-UNIT-05                 
                                               FW3E-NUM-UNIT-5G                 
                                               FW3E-NUM-BOOK-01                 
                                               FW3E-NUM-BOOK-2A                 
                                               FW3E-NUM-BOOK-2B                 
                                               FW3E-NUM-BOOK-03                 
                                               FW3E-NUM-BOOK-04                 
                                               FW3E-NUM-BOOK-05                 
                                               FW3E-NUM-BOOK-5G.                
                                                                                
           MOVE F130-NME-STREET             TO FW3E-NME-STREET.                 
           MOVE F130-NUM-POSTAL-CODE        TO FW3E-NUM-POSTAL.                 
                                                                                
           WRITE BP13FW3E-REC.                                                  
           ADD 1                            TO WS-FW3E-WRITE.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-READ-BM06K510.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO K510-REC.                        
           INITIALIZE                          K510-REC.                        
                                                                                
           MOVE SPACES                      TO WS-NME-NT.                       
                                                                                
           MOVE F130-ESTATE                 TO K510-ESTCDE.                     
                                                                                
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K510-NT-NAME           TO WS-NME-NT                        
                ADD 1                       TO WS-K510-FND                      
                                                                                
           WHEN 23                                                              
                MOVE SPACES                 TO WS-NME-NT                        
                ADD 1                       TO WS-K510-NOTFND                   
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BM06K510 : ' WS-K510-STATUS              
                MOVE WS-K510-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*==============================================*'.          
           DISPLAY '  BP13C92Y CONTROL TOTAL        : ' WS-DATE-EDIT.           
           DISPLAY '*----------------------------------------------*'.          
           DISPLAY '  NO OF BM06F130 RECS READ      : ' WS-F130-READ.           
           DISPLAY '  NO OF BM06K510 RECS FOUND     : ' WS-K510-FND.            
           DISPLAY '  NO OF BM06K510 RECS NOT FOUND : ' WS-K510-NOTFND.         
           DISPLAY '  NO OF BP13FW3E RECS WRITTEN   : ' WS-FW3E-WRITE.          
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BM06F130                                                       
                 BM06K510                                                       
                 BP13FW3E.                                                      
                                                                                
           IF WS-K510-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BM06K510 ERROR ' WS-K510-STATUS                  
              MOVE WS-K510-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
