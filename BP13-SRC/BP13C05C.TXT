       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C05C.                                                 
       AUTHOR.        JGO1.                                                     
       DATE-WRITTEN.  22 APR 2014.                                              
      ************************************************************              
      *   SYSTEM NAME : SOC - EMAIL ALERT FOR AMR UPDATE         *              
      *                                                          *              
      *   OBJECTIVE:                                             *              
      *      GENERATE EMAIL ALERT FOR AMR UPDATE THRU KIV        *              
      *                                                          *              
      *  FILES :                                                 *              
      *  INPUT :     BP13F310  (BP13.F310.TRANS)                 *              
      *  OUTPUT:     BP13FRPT                                    *              
      *                                                          *              
      * CHG-NO  BY   ON       DESCRIPTION                        *              
      *-------  ---  ------   ----------                         *              
      *BP135318 JGO1 22/04/14 INITIAL CODING                     *              
      *BP135537 RJB1 03/03/15 CHANGE THE EMAIL RECIPIENT FROM    *              
      *                       NICKNAME INTO COMPLETE NAME        *              
      *BP136477 RJB1 18/10/16 TO REPLACE KIM HUAT TO YEE MIEN    *              
      *BP139030 ZAR7 07/01/22 REPLACE CHUA YEE MIEN TO LIM KOK CHUN             
      *BP139276 KAC1 05/04/24 REPLACING LIM KOK CHUN BY *                       
      *                                 KATHIJAH_BEE_ALI_MOHAMED *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN TO BP13F310.                                 
           SELECT BP13FRPT  ASSIGN TO BP13FRPT.                                 
           SELECT SY08K010  ASSIGN       TO SY08K010                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K010-NUM-USERID                     
                            FILE STATUS  IS WS-K010-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD  BP13F310                   RECORD CONTAINS 150 CHARACTERS            
                                      RECORDING MODE IS F                       
                                      LABEL RECORD IS OMITTED.                  
       COPY BP13F310.                                                           
                                                                                
       FD   SY08K010                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY SY08K010.                                                           
                                                                                
       FD  BP13FRPT                   RECORD CONTAINS 130 CHARACTERS            
                                      RECORDING MODE IS F                       
                                      LABEL RECORD IS OMITTED.                  
       01 FRPT-REC                      PIC X(130).                             
                                                                                
      *-------------------------                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------                                                
       01  WS-FLAGS-INDICATORS.                                                 
           05  WS-F310-EOF              PIC X      VALUE 'N'.                   
           05  WS-F310-FIRST            PIC X      VALUE 'Y'.                   
           05  WS-ERROR-FND             PIC X      VALUE 'N'.                   
           05  WS-GOTO-NEXT-BATCH       PIC X      VALUE 'N'.                   
           05  WS-K010-STATUS           PIC 9(2)   VALUE  00.                   
       01  WS-CNT-F310-RD               PIC 9(5)   VALUE ZEROES.                
       01  WS-CNT-FRPT-WRITE            PIC 9(5)   VALUE ZEROES.                
       01  WS-AMT                       PIC 9(7)V99 VALUE ZEROES.               
       01  WS-REJECT-MSG                PIC X(100) VALUE SPACES.                
                                                                                
      *VARIABLES FOR EMAIL                                                      
       01  WS-HDR-LINES.                                                        
           05 WS-SUBJ                   PIC X(8)                                
              VALUE 'SUBJECT:'.                                                 
           05 WS-SUBJ2                  PIC X(31)                               
              VALUE 'UPDATED AMR RECORDS THRU KIV  '.                           
                                                                                
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(88)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1A.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID     PIC X(24)  VALUE 'KATHIJAH_BEE_ALI_MOHAMED'.         
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(11)  VALUE 'TAN_KIM_ENG'.                  
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(13)  VALUE 'CHUA_YUH_MIIN'.                
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1D.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(12)  VALUE 'JANICE_ML_OH'.                 
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(96)  VALUE SPACES.                         
                                                                                
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC - EMAIL ALERT'.                                             
           05  FILLER          PIC X(69)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4A.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  RCPT-ID     PIC X(24)  VALUE 'KATHIJAH_BEE_ALI_MOHAMED'.         
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL4B.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  RCPT-ID         PIC X(11)  VALUE 'TAN_KIM_ENG'.                  
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL4C.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  RCPT-ID         PIC X(13)  VALUE 'CHUA_YUH_MIIN'.                
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL4D.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  RCPT-ID         PIC X(12)  VALUE 'JANICE_ML_OH'.                 
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-TRAILER.                                                        
           05  FILLER          PIC X(01)  VALUE '.'.                            
                                                                                
                                                                                
      *VARIABLES FOR REPORT                                                     
       01  WS-RPT-HDR1.                                                         
           05 FILLER                PIC X(43)                                   
              VALUE 'CASES WITH AMR MASTER UPDATED              '.              
                                                                                
       01  WS-RPT-DASH              PIC X(130) VALUE ALL '-'.                   
       01  WS-RPT-HDR2.                                                         
           05 FILLER                PIC X(05) VALUE SPACES.                     
           05 WS-HDR-SERNO          PIC X(08) VALUE 'SERIAL #'.                 
           05 FILLER                PIC X(05) VALUE SPACES.                     
           05 WS-HDR-REGN           PIC X(08) VALUE 'REGN NUM'.                 
           05 FILLER                PIC X(05) VALUE SPACES.                     
           05 WS-HDR-AMT            PIC X(10) VALUE '    AMOUNT'.               
           05 FILLER                PIC X(05) VALUE SPACES.                     
           05 WS-HDR-OIC            PIC X(10) VALUE 'OIC       '.               
                                                                                
       01  WS-RPT-DTL.                                                          
           05 FILLER                PIC X(08) VALUE SPACES.                     
           05 WS-DTL-SERNO          PIC ZZZZ9.                                  
           05 FILLER                PIC X(05) VALUE SPACES.                     
           05 WS-DTL-REGN           PIC X(08) VALUE SPACES.                     
           05 FILLER                PIC X(05) VALUE SPACES.                     
           05 WS-DTL-AMT            PIC ZZZZZZ9.99.                             
           05 FILLER                PIC X(05) VALUE SPACES.                     
           05 WS-DTL-OIC            PIC X(10) VALUE SPACES.                     
                                                                                
       01  WS-RPT-BLANK.                                                        
           05 FILLER                PIC X(55) VALUE SPACES.                     
                                                                                
       01  WS-RPT-END.                                                          
           05 FILLER                PIC X(55) VALUE ALL '-'.                    
           05 FILLER                PIC X(20) VALUE                             
              '    END OF REPORT   '.                                           
           05 FILLER                PIC X(55) VALUE ALL '-'.                    
                                                                                
       01  WS-RPT-END2.                                                         
           05 FILLER                PIC X(55) VALUE                             
              'PLEASE VERIFY THAT THE UPDATES ARE IN ORDER'.                    
                                                                                
      *-------------------------                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------                                                
       0000-MAIN.                                                               
                                                                                
            PERFORM 0500-INITIALIZE     THRU 0500-EXIT.                         
            PERFORM 1000-PROCESS        THRU 1000-EXIT                          
                    UNTIL WS-F310-EOF = 'Y'.                                    
            IF WS-CNT-FRPT-WRITE > 0                                            
               PERFORM 6000-WRITE-TRAILER THRU 6000-EXIT                        
            END-IF.                                                             
            PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       0500-INITIALIZE.                                                         
      *-------------------------                                                
                                                                                
            OPEN INPUT       BP13F310                                           
                             SY08K010                                           
                 OUTPUT      BP13FRPT.                                          
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING SY08K010, STATUS = ' WS-K010-STATUS        
              MOVE     WS-K010-STATUS    TO    RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES  THRU  9000-EXIT                        
           END-IF.                                                              
                                                                                
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------                                                
       1000-PROCESS.                                                            
      *-------------------------                                                
                                                                                
            PERFORM 2000-READ-F310    THRU 2000-EXIT.                           
            PERFORM 3000-WRITE-TO-REPORT THRU 3000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       2000-READ-F310.                                                          
      *-------------------------                                                
                                                                                
            READ BP13F310                                                       
                 AT END MOVE 'Y' TO WS-F310-EOF                                 
                 GO TO 1000-EXIT.                                               
            ADD 1 TO WS-CNT-F310-RD.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       3000-WRITE-TO-REPORT.                                                    
      *-------------------------                                                
                                                                                
            IF WS-CNT-FRPT-WRITE = 0                                            
               PERFORM 4000-WRITE-REPORT-HEADER THRU 4000-EXIT                  
            END-IF.                                                             
            PERFORM 5000-WRITE-DETAIL THRU 5000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       4000-WRITE-REPORT-HEADER.                                                
      *-------------------------                                                
                                                                                
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-HDR1.                                   
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-HDR2.                                   
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL1A.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL1B.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL1C.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL1D.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL2.                                   
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL3.                                   
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL4A.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL4B.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL4C.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM MAIL-DTL4D.                                  
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC    FROM WS-HDR-LINES.                                
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC.                                                     
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC FROM  WS-RPT-HDR1.                                   
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC FROM  WS-RPT-DASH.                                   
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC FROM  WS-RPT-HDR2.                                   
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC FROM  WS-RPT-DASH.                                   
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       5000-WRITE-DETAIL.                                                       
      *-------------------------                                                
                                                                                
            MOVE   F310-REGN-NO          TO WS-DTL-REGN.                        
            MOVE   F310-AMT-AMR          TO WS-AMT.                             
            MOVE   WS-AMT                TO WS-DTL-AMT.                         
            MOVE   F310-USERID           TO K010-NUM-USERID.                    
            PERFORM 5500-READ-SY08K010   THRU 5500-EXIT.                        
            MOVE   K010-NUM-LOTUS-NCKNM  TO WS-DTL-OIC.                         
                                                                                
            ADD 1 TO WS-CNT-FRPT-WRITE.                                         
            MOVE WS-CNT-FRPT-WRITE TO WS-DTL-SERNO.                             
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC FROM  WS-RPT-DTL.                                    
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       5500-READ-SY08K010.                                                      
      *-------------------------                                                
                                                                                
             READ SY08K010.                                                     
                                                                                
             IF WS-K010-STATUS = 00                                             
                CONTINUE                                                        
             ELSE                                                               
               IF WS-K010-STATUS  = 23                                          
                  DISPLAY 'SY08K010 - RECORD NOT FND ('                         
                          K010-NUM-USERID ')'                                   
                  GO TO 9000-CLOSE-FILES                                        
               ELSE                                                             
                  DISPLAY 'SY08K010 - ERROR READ (' WS-K010-STATUS ')'          
                  MOVE WS-K010-STATUS              TO RETURN-CODE               
                  GO TO 9000-CLOSE-FILES                                        
               END-IF                                                           
             END-IF.                                                            
                                                                                
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       6000-WRITE-TRAILER.                                                      
      *-------------------------                                                
                                                                                
            INITIALIZE FRPT-REC.                                                
            WRITE FRPT-REC FROM  WS-RPT-END.                                    
            WRITE FRPT-REC FROM  WS-RPT-BLANK.                                  
            WRITE FRPT-REC FROM  WS-RPT-BLANK.                                  
            WRITE FRPT-REC FROM  WS-RPT-END2.                                   
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------                                                
                                                                                
            DISPLAY '*********************'.                                    
            DISPLAY '*  BP13C05E TOTALS  *'.                                    
            DISPLAY '*********************'.                                    
            DISPLAY 'INP READ   : ' WS-CNT-F310-RD.                             
            DISPLAY 'REC WRITEN : ' WS-CNT-FRPT-WRITE.                          
                                                                                
            CLOSE BP13F310                                                      
                  SY08K010                                                      
                  BP13FRPT.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
