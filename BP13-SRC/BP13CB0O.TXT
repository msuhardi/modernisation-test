      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB0O.                                                 
      *AUTHOR.        MARGE LAM KO.                                             
      *DATE-WRITTEN.  15 MAY 2017.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM CREATES EC REPORT CONTAINING   *           
      *                 COUNT OF CASES PER FINANCIAL YEAR           *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13RPT1                                              *           
      *   2.  BP13RPT2                                              *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13REPT                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136810 15/05/2017 MRN1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13RPT1   ASSIGN       TO BP13RPT1.                          
                                                                                
           SELECT BP13RPT2   ASSIGN       TO BP13RPT2.                          
                                                                                
           SELECT BP13REPT   ASSIGN       TO BP13REPT.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13RPT1                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13RPT1-REC.                                                     
           05  WS-RPT1-REGN-NO      PIC X(08).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-GRANT-TYPE   PIC X(03).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-NRIC1        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-NRIC2        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-NRIC3        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-NRIC4        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-DTE-OTP      PIC X(08).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-DTE-POST     PIC X(08).                                  
           05  FILLER               PIC X(230).                                 
                                                                                
       FD   BP13RPT2                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13RPT2-REC.                                                     
           05  WS-RPT2-REGN-NO      PIC X(08).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-GRANT-TYPE   PIC X(03).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-NRIC1        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-NRIC2        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-NRIC3        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-NRIC4        PIC X(09).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-DTE-OTP      PIC X(08).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-DTE-POST     PIC X(08).                                  
           05  FILLER               PIC X(230).                                 
                                                                                
       FD   BP13REPT                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13REPT-REC          PIC X(300).                                 
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-RPT1-CNT              PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT2-CNT              PIC 9(05) VALUE ZEROES.                     
       01  WS-FINCL-YEAR            PIC X(01) VALUE 'N'.                        
       01  WS-RPT1-EOF              PIC X(01) VALUE 'N'.                        
       01  WS-RPT2-EOF              PIC X(01) VALUE 'N'.                        
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05 WS-DTE-CCYY           PIC 9(04).                                  
           05 WS-DTE-MM             PIC 9(02).                                  
           05 WS-DTE-DD             PIC 9(02).                                  
       01  WS-TIME-HHMMSS.                                                      
           05 WS-TIME-HH            PIC X(02) VALUE SPACES.                     
           05 WS-TIME-MM            PIC X(02) VALUE SPACES.                     
           05 WS-TIME-SS            PIC X(02) VALUE SPACES.                     
           05 WS-TIME-MS            PIC X(02) VALUE SPACES.                     
                                                                                
       01  WS-DTE-FINCL-CCYYMM      PIC X(06).                                  
       01  WS-YEAR                  PIC 9(04).                                  
       01  WS-YEAR-PREV             PIC 9(04).                                  
       01  WS-YEAR-NEXT             PIC 9(04).                                  
                                                                                
       01  WS-REPT-LINE.                                                        
           05 FILLER                PIC X(150) VALUE ALL '-'.                   
           05 FILLER                PIC X(150) VALUE SPACES.                    
                                                                                
       01  WS-REPT-TITLE1.                                                      
           05 FILLER                PIC X(42) VALUE SPACES.                     
           05 FILLER                PIC X(49) VALUE                             
             'CASES WHEREBY THE HDB IMPOSED THE $10K PREMIUM OR'.               
           05 FILLER                PIC X(29) VALUE                             
             ' REDUCED THE GRANT FOR SC/SPR'.                                   
           05 FILLER                PIC X(180) VALUE SPACES.                    
                                                                                
       01  WS-REPT-TITLE2.                                                      
           05 FILLER                PIC X(44) VALUE SPACES.                     
           05 FILLER                PIC X(55) VALUE                             
             'FY10/11    FY11/12    FY12/13    FY13/14    FY14/15    '.         
           05 FILLER                PIC X(55) VALUE                             
             'FY15/16    FY16/17    FY17/18    NO-POSTDTE  TOTAL*    '.         
           05 FILLER                PIC X(146) VALUE SPACES.                    
                                                                                
       01  WS-REPT-TITLE3.                                                      
           05 FILLER                PIC X(31) VALUE                             
             'EC FLATS WITH CPF HOUSING GRANT'.                                 
           05 FILLER                PIC X(269) VALUE SPACES.                    
                                                                                
       01  WS-REPT-SCP-ROW1.                                                    
           05  FILLER                 PIC X(45) VALUE                           
             'NO OF SC/SPR HOUSEHOLDS GIVEN                '.                   
           05  WS-SCP-FY1011        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-FY1112        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-FY1213        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-FY1314        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-FY1415        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-FY1516        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-FY1617        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-FY1718        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-NO-PD         PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-SCP-TOTAL         PIC 9(06)  VALUE ZEROES.                    
           05  FILLER               PIC X(150) VALUE SPACES.                    
                                                                                
       01  WS-REPT-SCP-ROW2.                                                    
           05 FILLER                PIC X(25) VALUE                             
             'GRANT UNDER SCP HOUSEHOLD'.                                       
           05 FILLER                PIC X(275) VALUE SPACES.                    
                                                                                
       01  WS-REPT-TUG-ROW1.                                                    
           05  FILLER                 PIC X(45) VALUE                           
             'NO. OF HOUSEHOLDS CONVERTED TO               '.                   
           05  WS-TUG-FY1011        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-FY1112        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-FY1213        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-FY1314        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-FY1415        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-FY1516        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-FY1617        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-FY1718        PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-NO-PD         PIC 9(05)  VALUE ZEROES.                    
           05  FILLER               PIC X(06)  VALUE SPACES.                    
           05  WS-TUG-TOTAL         PIC 9(06)  VALUE ZEROES.                    
           05  FILLER               PIC X(150) VALUE SPACES.                    
                                                                                
       01  WS-REPT-TUG-ROW2.                                                    
           05 FILLER                PIC X(40) VALUE                             
             'SC HOUSEHOLD AND APPLIED FOR CITIZENSHIP'.                        
           05 FILLER                PIC X(260) VALUE SPACES.                    
                                                                                
       01  WS-REPT-TUG-ROW3.                                                    
           05 FILLER                PIC X(40) VALUE                             
             'TOP-UP AS AT MAR 2017 (EAPG)            '.                        
           05 FILLER                PIC X(260) VALUE SPACES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-RPT1    THRU 2000-EXIT                             
                   UNTIL WS-RPT1-EOF = 'Y'.                                     
           PERFORM 4000-READ-RPT2    THRU 4000-EXIT                             
                   UNTIL WS-RPT2-EOF = 'Y'.                                     
           PERFORM 6000-CREATE-REPORT THRU 6000-EXIT.                           
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13RPT1                                                 
                       BP13RPT2                                                 
                OUTPUT BP13REPT.                                                
                                                                                
           ACCEPT WS-DTE-CCYYMMDD   FROM   DATE YYYYMMDD.                       
           ACCEPT WS-TIME-HHMMSS    FROM   TIME.                                
                                                                                
           MOVE ZEROES TO WS-SCP-FY1011, WS-SCP-FY1112, WS-SCP-FY1213,          
                          WS-SCP-FY1314, WS-SCP-FY1415, WS-SCP-FY1516,          
                          WS-SCP-FY1617, WS-SCP-FY1718, WS-SCP-TOTAL,           
                          WS-TUG-FY1011, WS-TUG-FY1112, WS-TUG-FY1213,          
                          WS-TUG-FY1314, WS-TUG-FY1415, WS-TUG-FY1516,          
                          WS-TUG-FY1617, WS-TUG-FY1718, WS-TUG-TOTAL.           
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-RPT1.                                                          
      *---------------*                                                         
                                                                                
           READ BP13RPT1 AT END                                                 
              MOVE 'Y' TO WS-RPT1-EOF                                           
              GO TO 2000-EXIT.                                                  
                                                                                
           IF WS-RPT1-REGN-NO = 'REGN NUM'                                      
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1 TO WS-RPT1-CNT.                                                
                                                                                
           EVALUATE TRUE                                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201004' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201103'                             
                  ADD 1 TO WS-SCP-FY1011                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201104' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201203'                             
                  ADD 1 TO WS-SCP-FY1112                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201204' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201303'                             
                  ADD 1 TO WS-SCP-FY1213                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201304' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201403'                             
                  ADD 1 TO WS-SCP-FY1314                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201404' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201503'                             
                  ADD 1 TO WS-SCP-FY1415                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201504' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201603'                             
                  ADD 1 TO WS-SCP-FY1516                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201604' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201703'                             
                  ADD 1 TO WS-SCP-FY1617                                        
             WHEN WS-RPT1-DTE-POST(1:6) >= '201704' AND                         
                  WS-RPT1-DTE-POST(1:6) <= '201803'                             
                  ADD 1 TO WS-SCP-FY1718                                        
             WHEN WS-RPT1-DTE-POST = SPACES                                     
                  ADD 1 TO WS-SCP-NO-PD                                         
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4000-READ-RPT2.                                                          
      *---------------*                                                         
                                                                                
           READ BP13RPT2 AT END                                                 
              MOVE 'Y' TO WS-RPT2-EOF                                           
              GO TO 4000-EXIT.                                                  
                                                                                
           IF WS-RPT2-REGN-NO = 'REGN NUM'                                      
              GO TO 4000-EXIT.                                                  
                                                                                
           ADD 1 TO WS-RPT2-CNT.                                                
                                                                                
           EVALUATE TRUE                                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201004' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201103'                             
                  ADD 1 TO WS-TUG-FY1011                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201104' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201203'                             
                  ADD 1 TO WS-TUG-FY1112                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201204' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201303'                             
                  ADD 1 TO WS-TUG-FY1213                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201304' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201403'                             
                  ADD 1 TO WS-TUG-FY1314                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201404' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201503'                             
                  ADD 1 TO WS-TUG-FY1415                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201504' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201603'                             
                  ADD 1 TO WS-TUG-FY1516                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201604' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201703'                             
                  ADD 1 TO WS-TUG-FY1617                                        
             WHEN WS-RPT2-DTE-POST(1:6) >= '201704' AND                         
                  WS-RPT2-DTE-POST(1:6) <= '201803'                             
                  ADD 1 TO WS-TUG-FY1718                                        
             WHEN WS-RPT2-DTE-POST = SPACES                                     
                  ADD 1 TO WS-TUG-NO-PD                                         
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6000-CREATE-REPORT.                                                      
      *****************************************************************         
                                                                                
           MOVE SPACES TO WS-BP13REPT-REC.                                      
           WRITE WS-BP13REPT-REC FROM WS-REPT-LINE.                             
           WRITE WS-BP13REPT-REC FROM WS-REPT-TITLE1.                           
           WRITE WS-BP13REPT-REC FROM WS-REPT-LINE.                             
           WRITE WS-BP13REPT-REC FROM WS-REPT-TITLE2.                           
           WRITE WS-BP13REPT-REC FROM WS-REPT-LINE.                             
           WRITE WS-BP13REPT-REC FROM WS-REPT-TITLE3.                           
           WRITE WS-BP13REPT-REC FROM WS-REPT-LINE.                             
                                                                                
           COMPUTE WS-SCP-TOTAL = WS-SCP-FY1011 + WS-SCP-FY1112 +               
                                  WS-SCP-FY1213 + WS-SCP-FY1314 +               
                                  WS-SCP-FY1415 + WS-SCP-FY1516 +               
                                  WS-SCP-FY1617 + WS-SCP-FY1718 +               
                                  WS-SCP-NO-PD.                                 
                                                                                
           COMPUTE WS-TUG-TOTAL = WS-TUG-FY1011 + WS-TUG-FY1112 +               
                                  WS-TUG-FY1213 + WS-TUG-FY1314 +               
                                  WS-TUG-FY1415 + WS-TUG-FY1516 +               
                                  WS-TUG-FY1617 + WS-TUG-FY1718 +               
                                  WS-TUG-NO-PD.                                 
                                                                                
           WRITE WS-BP13REPT-REC FROM WS-REPT-SCP-ROW1.                         
           WRITE WS-BP13REPT-REC FROM WS-REPT-SCP-ROW2.                         
           WRITE WS-BP13REPT-REC FROM WS-REPT-LINE.                             
                                                                                
           WRITE WS-BP13REPT-REC FROM WS-REPT-TUG-ROW1.                         
           WRITE WS-BP13REPT-REC FROM WS-REPT-TUG-ROW2.                         
           WRITE WS-BP13REPT-REC FROM WS-REPT-TUG-ROW3.                         
           WRITE WS-BP13REPT-REC FROM WS-REPT-LINE.                             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB0O *************'.                            
           DISPLAY 'NO OF RECS READ FROM SCP/TUG FILE: ' WS-RPT1-CNT.           
           DISPLAY 'NO OF RECS READ FROM TUG FILE    : ' WS-RPT2-CNT.           
                                                                                
           CLOSE BP13RPT1                                                       
                 BP13RPT2                                                       
                 BP13REPT.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
