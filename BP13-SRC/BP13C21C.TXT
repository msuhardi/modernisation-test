       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21C.                                                 
       AUTHOR.        LMS.                                                      
       DATE-WRITTEN.  06/02/2006.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT         (BP13)                        *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO DELETE RECORDS BASED ON THE             *            
      *                 FOLLOWING TRANSACTION FILES:               *            
      *                                                            *            
      *                   DELETE                                   *            
      *  INPUT TRANS  :  BP13F240                                  *            
      *  I-O FILES    :  BP13K240                                  *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY  ON       DESCRIPTION                          *            
      * -------- --- -------- -----------                          *            
      * BP132562 LMS 06/02/06 NEW PROGRAM.                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240   ASSIGN TO BP13F240.                                
                                                                                
           SELECT BP13K240  ASSIGN       TO BP13K240                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K240-KEY-FLD                        
                            FILE STATUS  IS BP13K240-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F240                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS.                                     
       01   BP13F240-REC    PIC X(450).                                         
                                                                                
       FD   BP13K240                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K240.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  BP13K240-STATUS           PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F240-READ-CTR          PIC 9(8)   VALUE ZEROES.               
           05  WS-K240-DELETE-CTR        PIC 9(8)   VALUE ZEROES.               
           05  WS-F240-FLAG              PIC X(1)   VALUE 'N'.                  
               88 WS-F240-EOF            VALUE 'Y'.                             
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-MM           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-CCYY         PIC 9999.                                    
                                                                                
       01  WS-DTE-TMP              PIC X(10).                                   
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9999.                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       01  WS-LINER           PIC X(96) VALUE ALL '~'.                          
                                                                                
       01  WS-LINER1.                                                           
           10  FILLER         PIC X(90) VALUE ALL "-".                          
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE SECTION.                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 3000-READ-BP13F240      THRU 3000-EXIT                       
           PERFORM 2000-PROCESS-BP13F240   THRU 2000-EXIT                       
                   UNTIL WS-F240-EOF.                                           
           PERFORM 9000-CLOSE-RTN          THRU 9000-EXIT.                      
                                                                                
       0000-MAIN-EXIT.                                                          
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F240                                                 
                I-O    BP13K240.                                                
                                                                                
           IF BP13K240-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K240 : ' BP13K240-STATUS               
              MOVE BP13K240-STATUS             TO RETURN-CODE                   
              PERFORM 9000-CLOSE-RTN           THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-PROCESS-BP13F240.                                                   
      *-------------------------------------------------------------            
           MOVE BP13F240-REC  TO K240-HEADER-REC.                               
           PERFORM 2100-DELETE-BP13K240 THRU 2100-EXIT.                         
           PERFORM 3000-READ-BP13F240   THRU 3000-EXIT.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-DELETE-BP13K240.                                                    
      *-------------------------------------------------------------            
           READ BP13K240 KEY IS K240-KEY-FLD.                                   
           EVALUATE BP13K240-STATUS                                             
           WHEN 00                                                              
                DELETE BP13K240                                                 
                IF BP13K240-STATUS = 00                                         
                   MOVE '03' TO K240-CDE-HEADER                                 
                   PERFORM 2200-DELETE-BP13K240-TRAILER THRU 2200-EXIT          
                   ADD 1 TO WS-K240-DELETE-CTR                                  
                ELSE                                                            
                   DISPLAY 'INVALID DELETE BP13K240 KEY: '                      
                           K240-KEY-FLD                                         
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'RECORD NOT FOUND, BP13K240, KEY: '                     
                                           K240-KEY-FLD                         
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-DELETE-BP13K240-TRAILER.                                            
      *-------------------------------------------------------------            
           READ BP13K240 KEY IS K240-KEY-FLD.                                   
           EVALUATE BP13K240-STATUS                                             
           WHEN 00                                                              
                DELETE BP13K240                                                 
                IF BP13K240-STATUS = 00                                         
                   ADD 1 TO WS-K240-DELETE-CTR                                  
                ELSE                                                            
                   DISPLAY 'INVALID DELETE BP13K240 KEY: '                      
                           K240-KEY-FLD                                         
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'RECORD NOT FOUND, BP13K240, KEY: '                     
                                           K240-KEY-FLD                         
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F240.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F240 AT END                                                 
                MOVE 'Y' TO WS-F240-FLAG                                        
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-F240-READ-CTR.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-RTN.                                                          
      *-------------------------------------------------------------            
           DISPLAY WS-LINER.                                                    
           DISPLAY ' PROGRAM : BP13C21C        '.                               
           DISPLAY ' PURPOSE : UPDATE RECORDS: '.                               
           DISPLAY ' RUN DATE: ' WS-EDT-DATE.                                   
           DISPLAY WS-LINER.                                                    
           DISPLAY ' '.                                                         
           DISPLAY WS-LINER1.                                                   
           DISPLAY 'BP13F240 RECORDS READ    => ' WS-F240-READ-CTR.             
           DISPLAY 'BP13K240 RECORDS DELETED => ' WS-K240-DELETE-CTR.           
           DISPLAY WS-LINER1.                                                   
                                                                                
           CLOSE BP13F240,                                                      
                 BP13K240.                                                      
                                                                                
           IF BP13K240-STATUS NOT = 0 AND 97                                    
              MOVE BP13K240-STATUS       TO RETURN-CODE                         
              DISPLAY 'BP13K240 CLOSE ERROR : ' BP13K240-STATUS                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
