       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C046.                                                 
      *AUTHOR.        NINO ANGELO GARILLO.                                      
      *DATE-WRITTEN.  09/05/2005.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE   :  MATCHING PROGRAM                            *           
      *                                                             *           
      *  INPUT FILES :  BP13F80A                                    *           
      *                 BP13OUTA                                    *           
      *  OUT FILE    :  BP13OUTB                                    *           
      *------------------------------------------------------------ *           
      * CHG REF  DATE     BY    DESCRIPTION                         *           
      *--------  -------- ----- -----------                         *           
      * BP132691 20050509 GAN1  NEW PROGRAM                         *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F80A  ASSIGN TO BP13F80A.                                 
           SELECT BP13OUTA  ASSIGN TO BP13OUTA.                                 
           SELECT BP13OUTB  ASSIGN TO BP13OUTB.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F80A                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS.                                     
       01   BP13F80A-REC.                                                       
            05 F80A-REGN-NRIC.                                                  
               10 F80A-NUM-REGN       PIC X(08).                                
               10 F80A-NUM-NRIC       PIC X(09).                                
            05 F80A-DTE-BALLOT        PIC X(06).                                
            05 F80A-NUM-ALLOCAT       PIC X(03).                                
            05 F80A-NUM-FTYPE         PIC X(02).                                
            05 F80A-NUM-HH            PIC X(01).                                
            05 FILLER                 PIC X(70).                                
            05 F80A-CDE-NEWAPP        PIC X(01).                                
                                                                                
       FD   BP13OUTA                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       01   BP13OUTA-REC.                                                       
            05 OUTA-CAN-REGN-NRIC.                                              
               10 OUTA-CAN-NUM-REGN   PIC X(08).                                
               10 OUTA-CAN-NUM-NRIC   PIC X(09).                                
            05 OUTA-CAN-DTE-BALLOT    PIC X(06).                                
            05 OUTA-CAN-NUM-ALLOCAT   PIC X(03).                                
            05 OUTA-CAN-NUM-FTYPE     PIC X(02).                                
            05 OUTA-CAN-NUM-HH        PIC X(01).                                
            05 FILLER                 PIC X(71).                                
                                                                                
       FD   BP13OUTB                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS.                                     
       01   BP13OUTB-REC    PIC X(100).                                         
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-MATCH            PIC 9(8)    VALUE 0.                         
           05  WS-CREATED          PIC 9(8)    VALUE 0.                         
           05  WS-BYPASSED         PIC 9(8)    VALUE 0.                         
           05  WS-READ-F80A        PIC 9(8)    VALUE 0.                         
           05  WS-READ-OUTA        PIC 9(8)    VALUE 0.                         
           05  WS-WRYT-OUTB        PIC 9(8)    VALUE 0.                         
                                                                                
       01  WS-DATE                 PIC X(8)    VALUE SPACES.                    
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F80A     THRU 2000-EXIT.                           
           PERFORM 2100-READ-OUTA     THRU 2100-EXIT.                           
                                                                                
           PERFORM 3000-COMPARE       THRU 3000-EXIT                            
             UNTIL F80A-NUM-REGN      = HIGH-VALUES                             
               AND OUTA-CAN-NUM-REGN  = HIGH-VALUES.                            
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F80A                                                 
                       BP13OUTA                                                 
                OUTPUT BP13OUTB.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F80A.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13F80A                                                        
              AT END MOVE HIGH-VALUES TO F80A-NUM-REGN                          
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F80A.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-OUTA.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13OUTA                                                        
              AT END MOVE HIGH-VALUES TO OUTA-CAN-NUM-REGN                      
              GO TO 2100-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-OUTA.                                               
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-COMPARE.                                                            
      *-------------------------------------------------------------            
           IF  F80A-NUM-REGN = OUTA-CAN-NUM-REGN                                
           AND F80A-NUM-NRIC = OUTA-CAN-NUM-NRIC                                
      *       MOVE    SPACES               TO  BP13OUTB-REC                     
      *       INITIALIZE                       BP13OUTB-REC                     
      *       MOVE    BP13OUTA-REC         TO  BP13OUTB-REC                     
      *       ADD     1                    TO  WS-MATCH                         
      *       PERFORM 4000-WRITE-RECORDS THRU  4000-EXIT                        
              PERFORM 3500-READ-NEXT     THRU  3500-EXIT                        
               UNTIL (OUTA-CAN-NUM-REGN  NOT = F80A-NUM-REGN)                   
                  OR (OUTA-CAN-NUM-NRIC  NOT = F80A-NUM-NRIC)                   
              PERFORM 2000-READ-F80A     THRU  2000-EXIT                        
           ELSE                                                                 
              IF F80A-REGN-NRIC > OUTA-CAN-REGN-NRIC                            
                 ADD 1 TO WS-BYPASSED                                           
                 PERFORM 2100-READ-OUTA  THRU  2100-EXIT                        
              ELSE                                                              
                 MOVE    SPACES               TO  BP13OUTB-REC                  
                 INITIALIZE                       BP13OUTB-REC                  
                 MOVE    'Y'                  TO  F80A-CDE-NEWAPP               
                 MOVE    BP13F80A-REC         TO  BP13OUTB-REC                  
                 ADD     1                    TO  WS-CREATED                    
                 PERFORM 4000-WRITE-RECORDS THRU  4000-EXIT                     
                 PERFORM 2000-READ-F80A     THRU  2000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-READ-NEXT.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE    SPACES               TO  BP13OUTB-REC.                       
           INITIALIZE                       BP13OUTB-REC.                       
           MOVE    BP13OUTA-REC         TO  BP13OUTB-REC.                       
           ADD     1                    TO  WS-MATCH.                           
           PERFORM 4000-WRITE-RECORDS THRU  4000-EXIT.                          
                                                                                
           PERFORM 2100-READ-OUTA     THRU  2100-EXIT.                          
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-RECORDS.                                                      
      *-------------------------------------------------------------            
                                                                                
           WRITE BP13OUTB-REC.                                                  
           ADD 1 TO WS-WRYT-OUTB.                                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '--- BP13C046 CONTROL LIST ---------------'.                 
           DISPLAY '     RUN DATE : ' WS-DATE.                                  
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC READ  FROM BP13F80A  : ' WS-READ-F80A.            
           DISPLAY 'NO OF REC READ  FROM BP13OUTA  : ' WS-READ-OUTA.            
           DISPLAY 'NO OF REC WRITTEN TO BP13OUTB  : ' WS-WRYT-OUTB.            
           DISPLAY 'NO OF MATCHED RECORDS          : ' WS-MATCH.                
           DISPLAY 'NO OF CREATED REC FROM F80A    : ' WS-CREATED.              
           DISPLAY 'NO OF BYPASSED RECORDS         : ' WS-BYPASSED.             
           DISPLAY '------------------------------------------'.                
                                                                                
           CLOSE BP13F80A                                                       
                 BP13OUTA                                                       
                 BP13OUTB.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
