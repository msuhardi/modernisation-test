       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C50C.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  10 APR 2017.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  IF MARRIAGE RECORD IS FOUND, VALIDATE PAIRINGS*                
      *          IF BOTH COUPLE RETURN WITH MARRIAGE DATA      *                
      *          BP13K800 - UPDATE ELIG-SCH = FS TO PA         *                
      *          BP13K820 - UPDATE MARITAL STATUS = 1 TO 2     *                
      *          BP13K810 - FOR ENQUIRY ON CHG OF ELIG-SCH     *                
      *          BP13K848 - BACKUP BP13K800                    *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136646 FNP1 100417  NEW PROGRAM                      *                
      * BP13XXXX FNP1 010318  ADD NEW REPORT                   *                
      * BP136646 FNP1 270318  MOVE 'SOC' TO K810-NME-REQUEST   *                
      *                       SAVE ELIG SCH AFTER UPDATE TO K810                
      * BP137918 KV6  090719  TO CATER FOR REMOVAL OF DTE-DEFER*                
      *                       -EXPIRY AND NUM-DEFER-ZONE-FT    *                
      * BP137791 KV6  080719  MATCH ALL NON-MARRIED APPLICANTS *                
      *                       (EXCLUDING FS) & PRINT TO NEW RE-*                
      *                       PORT IF THEY HAVE ALREADY MARRIED*                
      * BP138613 KR13 260321  TO REMOVE I-O FOR K022.          *                
      * BP138833 FP6  300921  CREATE NEW OUTPUT TO SEND ALL CASES               
      *                       VIA EMAIL                        *                
      * BP138833 FP6  031121  MODIFY TITLE AND DETAILS OF THE  *                
      *                       REPORT AND ADD RECIPIENTS        *                
      * BP139262 KAC1 250722  TO REMOVE GOH_Li_Li@hdb.gov.sg   *                
      * BP139652 KAC1 280723  To remove WONG_Guoyin@hdb.gov.sg *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F517     ASSIGN        TO BP13F517.                       
           SELECT BP13K517     ASSIGN        TO BP13K517                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K517-KEY-FLD                    
                               FILE STATUS   IS WS-K517-STATUS.                 
                                                                                
           SELECT BP13K800     ASSIGN        TO BP13K800                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K800-NUM-REGN                   
                               FILE STATUS   IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K820     ASSIGN        TO BP13K820                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K820-KEY-FLD                    
                               FILE STATUS   IS WS-K820-STATUS.                 
                                                                                
           SELECT BP13K848     ASSIGN        TO BP13K848                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K848-KEY-FLD                    
                               FILE STATUS   IS WS-K848-STATUS.                 
                                                                                
           SELECT BP13K810     ASSIGN        TO BP13K810                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K810-KEY-FLD                    
                               FILE STATUS   IS WS-K810-STATUS.                 
                                                                                
           SELECT BP13K022     ASSIGN        TO BP13K022                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K022-KEY-FLD                    
                               ALTERNATE KEY IS K022-NUM-REGN                   
                               FILE STATUS   IS WS-K022-STATUS.                 
                                                                                
           SELECT P13F517A     ASSIGN        TO P13F517A.                       
           SELECT BP13F50C     ASSIGN        TO BP13F50C.                       
           SELECT BP13L50C     ASSIGN        TO BP13L50C.                       
           SELECT P13LC50D     ASSIGN        TO P13LC50D.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F517                                                            
            RECORD CONTAINS 250  CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F517.                                                           
                                                                                
       FD   BP13K517                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K517.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K848                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K848.                                                           
                                                                                
       FD   BP13K810                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K810.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   P13F517A                                                            
            RECORD CONTAINS 250  CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       01   P13F517A-REC       PIC X(250).                                      
                                                                                
       FD   BP13F50C                RECORD CONTAINS 150 CHARACTERS              
                                    LABEL RECORDS ARE OMITTED                   
                                    RECORDING MODE IS F.                        
       01   BP13F50C-REC.                                                       
            05  F50C-REGN            PIC X(08).                                 
            05  FIL01                PIC X(01).                                 
            05  F50C-OIC             PIC X(05).                                 
            05  FIL02                PIC X(01).                                 
            05  F50C-AP-NRIC         PIC X(09).                                 
            05  FIL03                PIC X(01).                                 
            05  F50C-AP-NAME         PIC X(20).                                 
            05  FIL04                PIC X(01).                                 
            05  F50C-NUM-MARRIAGE    PIC X(15).                                 
            05  FIL05                PIC X(01).                                 
            05  F50C-DTE-MARRIAGE    PIC X(08).                                 
            05  FIL06                PIC X(01).                                 
            05  F50C-SP-NRIC         PIC X(09).                                 
            05  FIL07                PIC X(01).                                 
            05  F50C-SP-NAME         PIC X(20).                                 
            05  FIL08                PIC X(01).                                 
            05  FILLER               PIC X(48).                                 
                                                                                
       FD   BP13L50C                                                            
            RECORD CONTAINS 133  CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       01   BP13L50C-REC       PIC X(133).                                      
                                                                                
       FD   P13LC50D                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 160 CHARACTERS.                                     
       01   P13L50D-REC                 PIC X(160).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K517-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K800-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K820-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K848-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K810-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  WS-K022-STATUS       PIC 9(2)    VALUE ZEROES.                   
           05  F517-EOF             PIC X(01)   VALUE ' '.                      
           05  WS-DTE-CURRENT       PIC X(08)   VALUE SPACES.                   
           05  WS-PREV-REGN         PIC X(08)   VALUE SPACES.                   
           05  WS-ELIG-BEF          PIC X(03)   VALUE SPACES.                   
           05  WS-ELIG-AFT          PIC X(03)   VALUE SPACES.                   
           05  WS-NME-RQST          PIC X(05)   VALUE SPACES.                   
                                                                                
       01  WS-SYSTEM-TIME.                                                      
           05 WS-HH                 PIC 9(2).                                   
           05 WS-MM                 PIC 9(2).                                   
           05 WS-SS                 PIC 9(2).                                   
           05 WS-MI                 PIC 9(2).                                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 K517-READ             PIC 9(7)    VALUE ZEROES.                   
           05 F517-READ             PIC 9(7)    VALUE ZEROES.                   
           05 F517-WRITE            PIC 9(7)    VALUE ZEROES.                   
           05 CNT-L50C-WRITE        PIC 9(7)    VALUE ZEROES.                   
           05 CNT-F50C-WRITE        PIC 9(7)    VALUE ZEROES.                   
           05 CNT-F50C-BYPASS       PIC 9(7)    VALUE ZEROES.                   
           05 CNT-F517-BYPASS       PIC 9(7)    VALUE ZEROES.                   
           05 F517-NO-MARRIAGE      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-VALID-F517-FS     PIC 9(7)    VALUE ZEROES.                   
           05 CNT-VALID-F517-NONFS  PIC 9(7)    VALUE ZEROES.                   
           05 CNT-VALID-K820-FS     PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-REC          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-NOTVALID-K820-FS  PIC 9(7)    VALUE ZEROES.                   
           05 CNT-NOTVALID-FS-DATA  PIC 9(7)    VALUE ZEROES.                   
           05 CNT-VALID-K800-FS     PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-FND          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-NOTFND       PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-FND-NONFS    PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-NOTFND-NONFS PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-FND          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-NOTFND       PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-FND-NONFS    PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-NOTFND-NONFS PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K517-UPDATED      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K820-UPDATED      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K800-UPDATED      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-F517-WRITTEN      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K848-WRITTEN      PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K810-WRITTEN      PIC 9(7)    VALUE ZEROES.                   
           05 CNT                   PIC 9       VALUE ZEROES.                   
           05 CTR                   PIC 9       VALUE ZEROES.                   
           05 CNT-CASES-UPD1        PIC 9(7)    VALUE ZEROES.                   
           05 CNT-CASES-UPD2        PIC 9(7)    VALUE ZEROES.                   
           05 WS-L50D-WRTN          PIC 9(05)   VALUE ZEROS.                    
           05 WS-NUM-SNO-LD50       PIC 9(04)   VALUE ZEROES.                   
                                                                                
       01  WS-DETAILS.                                                          
         03  WS-AP-SP-DTLS OCCURS 2 TIMES.                                      
           05  WS-AP-NRIC           PIC X(9)    VALUE SPACES.                   
           05  WS-AP-NAME           PIC X(66)   VALUE SPACES.                   
           05  WS-AP-SEX            PIC X       VALUE SPACES.                   
           05  WS-SP-NRIC           PIC X(9)    VALUE SPACES.                   
           05  WS-SP-NAME           PIC X(66)   VALUE SPACES.                   
           05  WS-SP-SEX            PIC X       VALUE SPACES.                   
           05  WS-NUM-REGN          PIC X(8)    VALUE SPACES.                   
           05  WS-NUM-MARRIAGE      PIC X(15)   VALUE SPACES.                   
           05  WS-DTE-MARRIAGE      PIC X(8)    VALUE SPACES.                   
                                                                                
      *---------------------------------------------------------------*         
      *        MAIL HEADER                                            *         
      *---------------------------------------------------------------*         
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(160) VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(160) VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-TO.                                               
           05  FILLER PIC X(160) VALUE 'RCPT TO:<TAN_SIEW_LAY@HDB.GOV.SG        
      -      '>'.                                                               
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC1.                                              
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Samuel_TB_ANG@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC2.                                              
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Jerlynn_JJ_MA@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC3.                                              
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Serene_YL_TAN@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC4.                                              
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Patricia_YJ_TAY@hdb.gov        
      -      '.sg>'.                                                            
                                                                                
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC7.                                              
           05  FILLER PIC X(160) VALUE 'RCPT TO:<NG_Siew_Meng@hdb.gov.sg        
      -      '>'.                                                               
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC8.                                              
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Soffia_RAPHAY@hdb.gov.s        
      -      'g>'.                                                              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC9.                                              
           05  FILLER PIC X(160) VALUE 'RCPT TO:<KHOR_Li_Ting@hdb.gov.sg        
      -      '>'.                                                               
                                                                                
       01  MAIL-HDR-MAIL-RCPT-CC10.                                             
           05  FILLER PIC X(160) VALUE 'RCPT TO:<Geraldine_WS_QUEK@hdb.g        
      -      'ov.sg>'.                                                          
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(160) VALUE 'DATA'.                                  
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(160) VALUE 'FROM:Soc System - Email Alert'.         
                                                                                
       01  MAIL-HDR-MAIL-TO.                                                    
           05  FILLER PIC X(160)                                                
               VALUE 'TO:<tan_siew_lay@hdb.gov.sg>'.                            
                                                                                
       01  MAIL-HDR-MAIL-CC1.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<Samuel_TB_ANG@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-HDR-MAIL-CC2.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<Jerlynn_JJ_MA@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-HDR-MAIL-CC3.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<Serene_YL_TAN@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-HDR-MAIL-CC4.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<Patricia_YJ_TAY@hdb.gov.sg>'.                         
                                                                                
                                                                                
       01  MAIL-HDR-MAIL-CC7.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<NG_Siew_Meng@hdb.gov.sg>'.                            
                                                                                
       01  MAIL-HDR-MAIL-CC8.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<Soffia_RAPHAY@hdb.gov.sg>'.                           
                                                                                
       01  MAIL-HDR-MAIL-CC9.                                                   
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<KHOR_Li_Ting@hdb.gov.sg>'.                            
                                                                                
       01  MAIL-HDR-MAIL-CC10.                                                  
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<Geraldine_WS_QUEK@hdb.gov.sg>'.                       
                                                                                
       01  MAIL-SUBJECT-LC50D.                                                  
           05  FILLER PIC X(160) VALUE 'SUBJECT : Report for Non-FS and         
      -       'Married for cases with key avail or 3 months before PCD'.        
                                                                                
       01  MAIL-BODY-LC50D             PIC X(160) VALUE SPACES.                 
                                                                                
       01  MAIL-END-LC50D              PIC X(160) VALUE SPACES.                 
                                                                                
       01  MAIL-HDR-DATE.                                                       
           05  FILLER                  PIC X(6)   VALUE 'DATE: '.               
           05  MAIL-DATE               PIC X(10)  VALUE SPACES.                 
           05  FILLER                  PIC X(124) VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(160) VALUE SPACES.                 
                                                                                
       01  L50C-HEADER.                                                         
           05  FILLER               PIC X(15)   VALUE SPACES.                   
           05  FILLER               PIC X(42)   VALUE                           
                         'REPORT ON BUYERS UPDATED TO MARRIED STATUS'.          
           05  FILLER               PIC X(8)    VALUE '  DATE: '.               
           05  L50C-NUM-DTE         PIC X(10)   VALUE SPACES.                   
                                                                                
       01  L50C-HEADER1.                                                        
           05  FILLER               PIC X(11)   VALUE 'REGN NUMBER'.            
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(12)   VALUE 'ELIG SCH BEF'.           
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(12)   VALUE 'ELIG SCH AFT'.           
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(12)   VALUE 'NRIC BUYER A'.           
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(12)   VALUE 'NRIC BUYER B'.           
           05  FILLER               PIC X(02)   VALUE SPACES.                   
                                                                                
       01  L50C-DETAIL1.                                                        
           05  L50C-REGN            PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(09)   VALUE SPACES.                   
           05  L50C-ELIG-BEF        PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(10)   VALUE SPACES.                   
           05  L50C-ELIG-AFT        PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(08)   VALUE SPACES.                   
           05  L50C-NRIC1           PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(05)   VALUE SPACES.                   
           05  L50C-NRIC2           PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
                                                                                
       01  L50C-SUMMARY.                                                        
           05  FILLER               PIC X(51)   VALUE                           
                  'SUMMARY TOTAL'.                                              
                                                                                
       01  L50C-SUMMARY1.                                                       
           05  FILLER               PIC X(51)   VALUE                           
                 'NUMBER OF CASES UPDATED (AFTER BOOKING BEFORE SOCA)'.         
           05  FILLER               PIC X(04)   VALUE SPACES.                   
           05  L50C-CASES-UPD1      PIC ZZZZZZ9   VALUE ZEROES.                 
                                                                                
       01  L50C-SUMMARY2.                                                       
           05  FILLER               PIC X(51)   VALUE                           
                 'NUMBER OF CASES UPDATED (AFTER SOCA BEFORE TP)'.              
           05  FILLER               PIC X(04)   VALUE SPACES.                   
           05  L50C-CASES-UPD2      PIC ZZZZZZ9   VALUE ZEROES.                 
                                                                                
      *---------------------------------------------------------------*         
      *                      REPORT DETAILS - P13LC50D                *         
      *---------------------------------------------------------------*         
                                                                                
       01  L50D-HEADER.                                                         
           05  FILLER               PIC X(29)   VALUE SPACES.                   
           05  FILLER               PIC X(97)   VALUE                           
                       'Report for non-FS and marital status not Married        
      -         ' for cases with key-avail or 3 months before PCD'.             
           05  FILLER               PIC X(13)   VALUE SPACES.                   
           05  FILLER               PIC X(6)    VALUE 'DATE: '.                 
           05  L50D-NUM-DTE         PIC X(10)   VALUE SPACES.                   
                                                                                
       01  L50D-LINE                PIC X(160)  VALUE ALL '-'.                  
                                                                                
       01  L50D-HEADER1.                                                        
           05  FILLER              PIC X(04)   VALUE ' S/N'.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'REGN'.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'POST OIC'.                
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'BUYER NRIC'.              
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(20)   VALUE 'BUYER NAME'.              
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'MARRIAGE CERT NO'.        
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'DATE OF MARRIAGE'.        
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(09)   VALUE 'SPOUSE IC'.               
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(20)   VALUE 'SPOUSE NAME'.             
                                                                                
                                                                                
       01  L50D-DETAIL1.                                                        
           05  L50D-SNO             PIC ZZZ9    VALUE ZEROES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L50D-REGN            PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L50D-OIC             PIC X(05)   VALUE SPACES.                   
           05  FILLER               PIC X(05)   VALUE SPACES.                   
           05  L50D-AP-NRIC         PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  L50D-AP-NAME         PIC X(20)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L50D-NUM-MARRIAGE    PIC X(15)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  L50D-DTE-MARRIAGE    PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(10)   VALUE SPACES.                   
           05  L50D-SP-NRIC         PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L50D-SP-NAME         PIC X(20)   VALUE SPACES.                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1200-PRINT-L50D-HEADER THRU 1200-EXIT.                       
           PERFORM 2100-READ-BP13F517   THRU 2100-EXIT.                         
           MOVE F517-NUM-REGN  TO WS-PREV-REGN.                                 
           PERFORM 3000-PROCESS-RECORDS THRU 3000-EXIT                          
              UNTIL F517-EOF = 'Y'.                                             
           PERFORM 5600-WRITE-SUMMARY   THRU 5600-EXIT.                         
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F517                                                 
                       BP13K022                                                 
                I-O    BP13K800                                                 
                       BP13K820                                                 
                       BP13K517                                                 
                       BP13K848                                                 
                       BP13K810                                                 
               OUTPUT  P13F517A                                                 
                       BP13F50C                                                 
                       BP13L50C                                                 
                       P13LC50D.                                                
                                                                                
           IF WS-K517-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K517 OPEN ERROR ' WS-K517-STATUS                     
              MOVE WS-K517-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR ' WS-K800-STATUS                     
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 OPEN ERROR ' WS-K820-STATUS                     
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K848-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K848 OPEN ERROR ' WS-K848-STATUS                     
              MOVE WS-K848-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K810-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K810 OPEN ERROR ' WS-K810-STATUS                     
              MOVE WS-K810-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K022 OPEN ERROR ' WS-K022-STATUS                     
              MOVE WS-K022-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DTE-CURRENT.                   
           STRING WS-DTE-CURRENT(1:4) '/'                                       
                  WS-DTE-CURRENT(5:2) '/'                                       
                  WS-DTE-CURRENT(7:2)                                           
                  DELIMITED BY SIZE INTO L50C-NUM-DTE.                          
                                                                                
           WRITE BP13L50C-REC FROM L50C-HEADER.                                 
           WRITE BP13L50C-REC FROM L50C-HEADER1 AFTER 5.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1200-PRINT-L50D-HEADER.                                                  
      *-------------------------------------------------------------            
           MOVE L50C-NUM-DTE                TO MAIL-DATE                        
                                               L50D-NUM-DTE.                    
                                                                                
           WRITE P13L50D-REC                FROM MAIL-HDR-HELO.                 
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-FROM.            
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-TO.         
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-CC1.        
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-CC2.        
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-CC3.        
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-CC4.        
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-CC7.        
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-CC8.        
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-RCPT-CC9.        
           WRITE P13L50D-REC               FROM MAIL-HDR-MAIL-RCPT-CC10.        
           WRITE P13L50D-REC                FROM MAIL-HDR-DATA.                 
           WRITE P13L50D-REC                FROM MAIL-HDR-FROM.                 
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-TO.              
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC1.             
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC2.             
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC3.             
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC4.             
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC7.             
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC8.             
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC9.             
           WRITE P13L50D-REC                FROM MAIL-HDR-MAIL-CC10.            
           WRITE P13L50D-REC                FROM MAIL-SUBJECT-LC50D.            
           WRITE P13L50D-REC                FROM MAIL-HDR-DATE.                 
                                                                                
           WRITE P13L50D-REC                FROM MAIL-SPACES.                   
           WRITE P13L50D-REC                FROM MAIL-SPACES.                   
                                                                                
           WRITE P13L50D-REC                FROM L50D-HEADER.                   
           WRITE P13L50D-REC                FROM MAIL-SPACES.                   
                                                                                
           WRITE P13L50D-REC                FROM L50D-LINE.                     
           WRITE P13L50D-REC                FROM L50D-HEADER1.                  
           WRITE P13L50D-REC                FROM L50D-LINE.                     
           WRITE P13L50D-REC                FROM MAIL-SPACES.                   
                                                                                
           MOVE ZEROES                      TO WS-NUM-SNO-LD50.                 
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2100-READ-BP13F517.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F517 AT END                                                 
              MOVE 'Y' TO F517-EOF                                              
              GO TO 2100-EXIT.                                                  
                                                                                
           ADD 1 TO F517-READ.                                                  
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           IF F517-NUM-REGN NOT = SPACES                                        
              IF (F517-NUM-NRIC-SPOUSE = SPACES OR LOW-VALUES) AND              
                 (F517-NUM-SPOUSE-SEX  = SPACES OR LOW-VALUES) AND              
                 (F517-NME-SPOUSE      = SPACES OR LOW-VALUES) AND              
                 (F517-NUM-MARRIAGE    = SPACES OR LOW-VALUES) AND              
                 (F517-DTE-MARRIAGE    = SPACES OR LOW-VALUES)                  
                 ADD 1 TO F517-NO-MARRIAGE                                      
                 PERFORM 4000-READ-BP13K800   THRU 4000-EXIT                    
                 PERFORM 2100-READ-BP13F517   THRU  2100-EXIT                   
                 GO TO 3000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (F517-NUM-TRAN = 'P') AND                                         
              (F517-NUM-MARRIAGE NOT = SPACES AND LOW-VALUES) AND               
              (F517-DTE-MARRIAGE NOT = SPACES AND LOW-VALUES)                   
              ADD 1 TO CNT-VALID-F517-NONFS                                     
              PERFORM 4000-READ-BP13K800    THRU 4000-EXIT                      
              IF K800-DTE-ACCEPTANCE > F517-DTE-MARRIAGE                        
                 ADD 1 TO CNT-F50C-BYPASS                                       
              ELSE                                                              
                 PERFORM 5700-WRITE-BP13F50C   THRU 5700-EXIT                   
                 PERFORM 6000-WRITE-L50D-REPORT THRU 6000-EXIT                  
              END-IF                                                            
              PERFORM 2100-READ-BP13F517    THRU 2100-EXIT                      
              GO TO 3000-EXIT                                                   
           END-IF                                                               
                                                                                
           MOVE ZEROS TO CNT.                                                   
           PERFORM 3050-VALIDATE-FS THRU 3050-EXIT                              
                   UNTIL F517-NUM-REGN NOT = WS-PREV-REGN                       
                   OR F517-EOF = 'Y'.                                           
                                                                                
           PERFORM 3200-MATCH-RECORDS THRU 3200-EXIT.                           
           MOVE F517-NUM-REGN TO WS-PREV-REGN.                                  
                                                                                
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3050-VALIDATE-FS.                                                        
                                                                                
           ADD 1 TO CNT-K820-REC.                                               
           IF F517-NUM-REGN = WS-PREV-REGN                                      
              IF (F517-NUM-NRIC-SPOUSE NOT = SPACES AND LOW-VALUES) AND         
                 (F517-NUM-SPOUSE-SEX  NOT = SPACES AND LOW-VALUES) AND         
                 (F517-NME-SPOUSE      NOT = SPACES AND LOW-VALUES) AND         
                 (F517-NUM-MARRIAGE    NOT = SPACES AND LOW-VALUES) AND         
                 (F517-DTE-MARRIAGE    NOT = SPACES AND LOW-VALUES) AND         
                 F517-NUM-REMARK = 'SEND'                                       
                 ADD 1 TO CNT                                                   
                 MOVE F517-NUM-NRIC         TO WS-AP-NRIC(CNT)                  
                 MOVE F517-NME              TO WS-AP-NAME(CNT)                  
                 MOVE F517-NUM-NRIC-SPOUSE  TO WS-SP-NRIC(CNT)                  
                 MOVE F517-NME-SPOUSE       TO WS-SP-NAME(CNT)                  
                 MOVE F517-NUM-SPOUSE-SEX   TO WS-SP-SEX (CNT)                  
                 MOVE F517-NUM-MARRIAGE     TO WS-NUM-MARRIAGE(CNT)             
                 MOVE F517-DTE-MARRIAGE     TO WS-DTE-MARRIAGE(CNT)             
                 MOVE F517-NUM-REGN         TO WS-NUM-REGN(CNT)                 
                 ADD 1 TO CNT-VALID-F517-FS                                     
                 PERFORM 3100-READ-BP13K820  THRU 3100-EXIT                     
              ELSE                                                              
                 ADD 1 TO CNT-F517-BYPASS                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F517   THRU  2100-EXIT.                        
                                                                                
       3050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3100-READ-BP13K820.                                                      
      *------------------------------------------------------------             
           MOVE SPACES                 TO K820-KEY-FLD.                         
           MOVE F517-NUM-REGN          TO K820-NUM-REGN.                        
           MOVE F517-NUM-NRIC          TO K820-NUM-NRIC.                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              ADD 1 TO CNT-K820-FND                                             
              MOVE K820-NUM-SEX     TO WS-AP-SEX(CNT)                           
           ELSE                                                                 
              IF WS-K820-STATUS = 23 OR 10                                      
                 ADD 1 TO CNT-K820-NOTFND                                       
      **         DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY IS: '               
      **                         K820-KEY-FLD                                   
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR NEXT READ : ' WS-K820-STATUS         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3150-READ-BP13K820.                                                      
      *------------------------------------------------------------             
           MOVE SPACES                 TO BP13K820-REC.                         
           INITIALIZE BP13K820-REC.                                             
           MOVE F517-NUM-REGN          TO K820-NUM-REGN.                        
           MOVE F517-NUM-NRIC          TO K820-NUM-NRIC.                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              ADD 1 TO CNT-K820-FND-NONFS                                       
           ELSE                                                                 
              IF WS-K820-STATUS = 23 OR 10                                      
                 ADD 1 TO CNT-K820-NOTFND-NONFS                                 
      **         DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY IS: '               
      **                         K820-KEY-FLD                                   
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR NEXT READ : ' WS-K820-STATUS         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3200-MATCH-RECORDS.                                                      
      *------------------------------------------------------------             
           IF WS-NUM-MARRIAGE(1) NOT = SPACES AND                               
              WS-NUM-MARRIAGE(2) NOT = SPACES AND                               
              WS-DTE-MARRIAGE(1) NOT = SPACES AND                               
              WS-DTE-MARRIAGE(2) NOT = SPACES AND                               
              WS-AP-SEX(1)       NOT = SPACES AND                               
              WS-AP-SEX(2)       NOT = SPACES                                   
              IF WS-NUM-MARRIAGE(1) = WS-NUM-MARRIAGE(2) AND                    
                 WS-DTE-MARRIAGE(1) = WS-DTE-MARRIAGE(2) AND                    
                 WS-NUM-REGN(1)     = WS-NUM-REGN(2)     AND                    
                 WS-AP-SEX(1)   NOT = WS-AP-SEX(2) AND                          
                 WS-AP-NRIC(1)      = WS-SP-NRIC(2) AND                         
                 WS-SP-NRIC(1)      = WS-AP-NRIC(2)                             
                 ADD 1 TO CNT-VALID-K820-FS                                     
                 PERFORM 3300-READ-BP13K800  THRU 3300-EXIT                     
              ELSE                                                              
                 ADD 1 TO CNT-NOTVALID-FS-DATA                                  
              END-IF                                                            
           ELSE                                                                 
               ADD 1 TO CNT-NOTVALID-K820-FS                                    
           END-IF.                                                              
                                                                                
           MOVE 0 TO CNT.                                                       
           PERFORM VARYING CTR FROM 1 BY 1 UNTIL CTR > 2                        
               MOVE SPACES      TO WS-AP-NRIC(CTR)                              
                                   WS-AP-NAME(CTR)                              
                                   WS-AP-SEX(CTR)                               
                                   WS-NUM-MARRIAGE(CTR)                         
                                   WS-DTE-MARRIAGE(CTR)                         
                                   WS-NUM-REGN(CTR)                             
                                   WS-SP-NRIC(CTR)                              
                                   WS-SP-NAME(CTR)                              
                                   WS-SP-SEX(CTR)                               
           END-PERFORM.                                                         
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3300-READ-BP13K800.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K800-NUM-REGN.                      
           MOVE WS-PREV-REGN             TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                                                                                
                    ADD 1 TO CNT-K800-FND                                       
                    IF WS-DTE-MARRIAGE(1) IS NOT NUMERIC                        
                       MOVE ZEROS TO WS-DTE-MARRIAGE(1)                         
                    END-IF                                                      
                    IF K800-DTE-REGN      IS NOT NUMERIC                        
                       MOVE ZEROS TO K800-DTE-REGN                              
                    END-IF                                                      
                    IF K800-NUM-ELIG-SCHEME = 'FS ' AND                         
                       WS-DTE-MARRIAGE(1) > K800-DTE-REGN                       
                       MOVE K800-NUM-ELIG-SCHEME TO WS-ELIG-BEF                 
                       ADD 1 TO CNT-VALID-K800-FS                               
                       PERFORM 5000-UPDATE-SOC-RECORDS THRU 5000-EXIT           
                    END-IF                                                      
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO CNT-K800-NOTFND                                    
                    PERFORM 3450-DELETE-BP13K517 THRU 3450-EXIT                 
      **            DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY IS: '            
      **                   K800-NUM-REGN                                        
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       3301-READ-BP13K800.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE BP13K800-MASTER.                                          
           MOVE F517-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO CNT-K800-FND-NONFS                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO CNT-K800-NOTFND-NONFS                              
      **            DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY IS: '            
      **                   K800-NUM-REGN                                        
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3301-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       3310-WRITE-K800-IMAGE.                                                   
      *--------------------------------------------------------------*          
                                                                                
           MOVE BP13K800-MASTER TO BP13K848-MASTER.                             
           MOVE 'C50C'          TO K848-NUM-TRANS-BKIMAGE.                      
                                                                                
           MOVE 'BP13C50C'      TO K848-NUM-USERID.                             
           MOVE WS-DTE-CURRENT  TO K848-DTE-BKIMAGE.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
                                                                                
           MOVE WS-SYSTEM-TIME  TO K848-TME-BKIMAGE.                            
           MOVE K848-NUM-REGN   TO K848-NUM-REGN-BKIMAGE.                       
                                                                                
           WRITE BP13K848-MASTER.                                               
                                                                                
           EVALUATE WS-K848-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO CNT-K848-WRITTEN                                   
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K848. STATUS IS '                
                            WS-K848-STATUS                                      
                    PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3350-WRITE-BP13K810.                                                     
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES                 TO BP13K810-MASTER.                      
           INITIALIZE                     BP13K810-MASTER.                      
                                                                                
           MOVE K800-NUM-REGN          TO K810-NUM-REGN                         
           MOVE WS-DTE-CURRENT         TO K810-DTE-UPDATE.                      
                                                                                
           MOVE WS-SYSTEM-TIME(1:7)    TO K810-TME-UPDATE.                      
           MOVE SPACES                 TO K810-NUM-REF.                         
           MOVE K800-NUM-STATUS        TO K810-NUM-STATUS                       
           MOVE K800-NUM-NRIC1         TO K810-NUM-NRIC1.                       
           MOVE K800-NUM-NRIC2         TO K810-NUM-NRIC2.                       
           MOVE K800-NUM-NRIC3         TO K810-NUM-NRIC3.                       
           MOVE K800-NUM-NRIC4         TO K810-NUM-NRIC4.                       
           MOVE K800-NUM-ZONE          TO K810-NUM-ZONE.                        
           MOVE K800-NUM-FLAT-TYPE     TO K810-NUM-FLAT-TYPE.                   
           MOVE K800-NUM-NEW-TOWN      TO K810-NUM-NEW-TOWN.                    
           MOVE K800-NUM-IDP-SCHEME    TO K810-NUM-IDP-SCHEME.                  
           MOVE K800-NUM-ALLO-SCHEME   TO K810-NUM-ALLO-SCHEME.                 
           MOVE K800-NUM-ELIG-SCHEME   TO K810-NUM-ELIG-SCHEME.                 
           MOVE K800-NUM-CAT           TO K810-NUM-CAT.                         
           MOVE K800-NUM-HOUSEHOLD     TO K810-NUM-HOUSEHOLD.                   
           MOVE SPACES                 TO K810-DTE-CHANGE-ZONE-FT.              
           MOVE K800-QTY-JT-BALLOT     TO K810-QTY-JT-BALLOT.                   
           MOVE K800-NUM-JT-BALLOT-MTF TO K810-NUM-JT-BALLOT.                   
           MOVE K800-NUM-SCH-ACC       TO K810-NUM-SCH-ACC.                     
           MOVE K800-NUM-CUR-REGN      TO K810-NUM-CUR-REGN.                    
           MOVE SPACES                 TO K810-NUM-DEFER-TAG.                   
           MOVE K800-NUM-WAIT-TIME     TO K810-NUM-WAIT-TIME.                   
           MOVE K800-NUM-ZONE-FT-QUEUE TO K810-NUM-ZONE-FT-QUEUE.               
           MOVE K800-NUM-5YR-OCCUPN    TO K810-NUM-5YR-OCCUPN.                  
           MOVE K800-NUM-PARENT-CHILD  TO K810-NUM-PARENT-CHILD.                
           MOVE K800-NUM-HOLDING-TYPE  TO K810-NUM-HOLDING-TYPE.                
           MOVE K800-NUM-SHORTLIST-PA-HHTY                                      
                                       TO K810-NUM-SHORTLIST-PA-HHTY.           
           MOVE K800-NUM-SHORTLIST-PA-CAT                                       
                                       TO K810-NUM-SHORTLIST-PA-CAT.            
           MOVE K800-NUM-SHORTLIST-PRIO-HHTY                                    
                                       TO K810-NUM-SHORTLIST-PRIO-HHTY.         
           MOVE K800-NUM-SHORTLIST-PRIO-CAT                                     
                                       TO K810-NUM-SHORTLIST-PRIO-CAT.          
           MOVE K800-DTE-BALLOT        TO K810-DTE-BALLOT.                      
                                                                                
           MOVE K800-TME-QUEUE         TO K810-TME-QUEUE.                       
           MOVE K800-TME-QUEUE         TO K810-TME-QUEUE.                       
           MOVE K800-NUM-JT-BALLOT-REGN TO K810-NUM-JT-BALLOT-REGN-1.           
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-2.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-3.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-4.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-5.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-6.            
           MOVE K800-NUM-LOAN-TAG      TO K810-NUM-LOAN-TAG.                    
           MOVE 'BP13C50C'             TO K810-NUM-USERID.                      
                                                                                
           IF  K800-NUM-NRIC1      NOT = SPACES AND LOW-VALUES                  
               IF K800-NUM-NRIC1   = WS-AP-NRIC(1)                              
                  MOVE WS-AP-NAME(1)   TO K810-NME-HA1                          
               ELSE                                                             
                  IF K800-NUM-NRIC1 = WS-AP-NRIC(2)                             
                     MOVE WS-AP-NAME(2) TO K810-NME-HA1                         
                  END-IF                                                        
               END-IF                                                           
           ELSE                                                                 
               MOVE SPACES             TO K810-NME-HA1                          
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                       
               IF K800-NUM-NRIC2   = WS-AP-NRIC(1)                              
                  MOVE WS-AP-NAME(1)   TO K810-NME-HA2                          
               ELSE                                                             
                  IF K800-NUM-NRIC2 = WS-AP-NRIC(2)                             
                     MOVE WS-AP-NAME(2)   TO K810-NME-HA2                       
                  END-IF                                                        
               END-IF                                                           
           ELSE                                                                 
               MOVE SPACES             TO K810-NME-HA2                          
           END-IF.                                                              
                                                                                
           MOVE SPACES                 TO K810-NME-HA3.                         
           MOVE SPACES                 TO K810-NME-HA4.                         
                                                                                
           MOVE 'C50C'                 TO K810-NUM-SCREENID.                    
           MOVE WS-NME-RQST            TO K810-NME-REQUEST.                     
                                                                                
           WRITE BP13K810-MASTER.                                               
                                                                                
           EVALUATE WS-K810-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO CNT-K810-WRITTEN                                   
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K810. STATUS IS '                
                            WS-K810-STATUS                                      
                    DISPLAY K810-KEY-FLD ' KEY 810'                             
                    PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3400-REWRITE-BP13K800.                                                   
      *------------------------------------------------------------             
           MOVE 'PA ' TO K800-NUM-ELIG-SCHEME                                   
                         WS-ELIG-AFT                                            
           MOVE WS-DTE-CURRENT         TO K800-DTE-UPDATE.                      
                                                                                
           REWRITE BP13K800-MASTER.                                             
           IF WS-K800-STATUS = 00                                               
              ADD 1 TO CNT-K800-UPDATED                                         
           ELSE                                                                 
               IF WS-K800-STATUS = 23                                           
                  DISPLAY 'ERROR NOT FOUND BP13K800, KEY IS: '                  
                        K800-NUM-REGN                                           
               ELSE                                                             
                  DISPLAY 'ERROR UPDATING BP13K800, KEY IS: '                   
                        K800-NUM-REGN                                           
                  MOVE WS-K800-STATUS  TO RETURN-CODE                           
                  PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3450-DELETE-BP13K517.                                                    
                                                                                
           PERFORM VARYING CTR FROM 1 BY 1 UNTIL CTR > CNT                      
              MOVE WS-AP-NRIC(CTR)        TO K517-NUM-NRIC                      
                                                                                
              PERFORM 4200-READ-BP13K517  THRU 4200-EXIT                        
                                                                                
              WRITE P13F517A-REC FROM BP13K517-REC                              
              DELETE BP13K517                                                   
              IF WS-K517-STATUS = 00 OR 02                                      
                 ADD 1 TO F517-WRITE                                            
              ELSE                                                              
                 DISPLAY 'ERR DELETE BP13K517,STATUS: ' WS-K517-STATUS          
                         ' KEY: ' K517-KEY-FLD                                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3460-DELETE-BP13K517.                                                    
      *------------------------------------------------------------             
                                                                                
           MOVE F517-NUM-NRIC          TO K517-NUM-NRIC.                        
           WRITE P13F517A-REC FROM BP13K517-REC.                                
                                                                                
           PERFORM 4200-READ-BP13K517  THRU 4200-EXIT.                          
                                                                                
           DELETE BP13K517.                                                     
                                                                                
           IF WS-K517-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR DELETING RECORD, KEY IS: '                         
                    K517-NUM-NRIC                                               
           ELSE                                                                 
              ADD 1 TO F517-WRITE                                               
           END-IF.                                                              
                                                                                
                                                                                
       3460-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3500-READ-UPD-K820.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              PERFORM 3600-REWRITE-BP13K820  THRU 3600-EXIT                     
           ELSE                                                                 
              IF WS-K820-STATUS = 23 OR 10                                      
                 CONTINUE                                                       
      *          DISPLAY 'RECORD NOT FND IN BP13K820, STATUS: '                 
      *                  WS-K820-STATUS                                         
      *                  ' KEY= ' K820-KEY-FLD                                  
              ELSE                                                              
                 DISPLAY 'BP13K820 - ERROR NEXT READ : ' WS-K820-STATUS         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3600-REWRITE-BP13K820.                                                   
      *------------------------------------------------------------             
                                                                                
           MOVE '2' TO K820-NUM-MARITAL-STATUS.                                 
           MOVE WS-SP-NRIC(CTR) TO K820-NUM-NRIC-SPOUSE.                        
           IF K820-NUM-RELATIONSHIP = '09'                                      
              MOVE '20'       TO K820-NUM-RELATIONSHIP                          
           END-IF.                                                              
                                                                                
           REWRITE BP13K820-REC                                                 
           IF WS-K820-STATUS = 00 OR 02                                         
              ADD 1 TO CNT-K820-UPDATED                                         
           ELSE                                                                 
              DISPLAY 'ERROR UPDATE BP13K820, STATUS: ' WS-K820-STATUS          
                      'KEY= ' K820-KEY-FLD                                      
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4000-READ-BP13K800.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K800-NUM-REGN.                      
           MOVE F517-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                    ADD 1 TO CNT-K800-FND                                       
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO CNT-K800-NOTFND                                    
                    PERFORM 3460-DELETE-BP13K517 THRU 3460-EXIT                 
                    DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY IS: '            
                           K800-NUM-REGN                                        
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       4100-REWRITE-BP13K517.                                                   
      *------------------------------------------------------------             
                                                                                
           PERFORM 4200-READ-BP13K517  THRU 4200-EXIT.                          
                                                                                
           MOVE 'UPDATED'              TO K517-NUM-REMARK.                      
                                                                                
           REWRITE BP13K517-REC                                                 
           IF WS-K517-STATUS = 00 OR 02                                         
              ADD 1 TO CNT-K517-UPDATED                                         
           ELSE                                                                 
              DISPLAY 'ERROR UPDATE BP13K517, STATUS: ' WS-K517-STATUS          
                      ' KEY=' K517-KEY-FLD                                      
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4200-READ-BP13K517.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13K517.                                                       
                                                                                
           EVALUATE WS-K517-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO K517-READ                                          
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN BP13K517, KEY IS: '            
                    K517-KEY-FLD                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K517 READ ERROR ' WS-K517-STATUS               
                            ' K517 KEY: ' K517-KEY-FLD                          
                    MOVE WS-K517-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       5000-UPDATE-SOC-RECORDS.                                                 
      *------------------------------------------------------------             
           IF (K800-DTE-ACCEPTANCE NOT = SPACES AND ZEROES                      
                                        AND LOW-VALUES)                         
              IF K800-DTE-SALE-AGMT = SPACES OR LOW-VALUES OR ZEROES            
                 ADD 1 TO CNT-CASES-UPD1                                        
              ELSE                                                              
                 ADD 1 TO CNT-CASES-UPD2                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3310-WRITE-K800-IMAGE   THRU 3310-EXIT.                      
           MOVE SPACES                     TO WS-NME-RQST.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
           PERFORM 3350-WRITE-BP13K810     THRU 3350-EXIT.                      
           PERFORM 3400-REWRITE-BP13K800   THRU 3400-EXIT.                      
           MOVE 'SOC'                      TO WS-NME-RQST.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
           ADD 1                           TO WS-SS.                            
           PERFORM 3350-WRITE-BP13K810     THRU 3350-EXIT.                      
                                                                                
           PERFORM VARYING CTR FROM 1 BY 1 UNTIL CTR > CNT                      
              MOVE SPACES                    TO K820-KEY-FLD                    
              MOVE WS-NUM-REGN(CTR)          TO K820-NUM-REGN                   
              MOVE WS-AP-NRIC(CTR)        TO K820-NUM-NRIC                      
                                             K517-NUM-NRIC                      
              PERFORM 3500-READ-UPD-K820     THRU 3500-EXIT                     
              PERFORM 4100-REWRITE-BP13K517  THRU 4100-EXIT                     
           END-PERFORM.                                                         
                                                                                
           PERFORM 5500-WRITE-REPORT      THRU 5500-EXIT.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5500-WRITE-REPORT.                                                       
      *------------------------------------------------------------             
           INITIALIZE           BP13L50C-REC.                                   
           MOVE SPACES      TO  BP13L50C-REC.                                   
                                                                                
           MOVE WS-PREV-REGN            TO L50C-REGN.                           
           MOVE WS-ELIG-BEF             TO L50C-ELIG-BEF.                       
           MOVE WS-ELIG-AFT             TO L50C-ELIG-AFT.                       
           MOVE WS-AP-NRIC(1)           TO L50C-NRIC1.                          
           MOVE WS-AP-NRIC(2)           TO L50C-NRIC2.                          
                                                                                
           WRITE BP13L50C-REC  FROM L50C-DETAIL1.                               
           ADD 1 TO CNT-L50C-WRITE.                                             
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5600-WRITE-SUMMARY.                                                      
      *------------------------------------------------------------             
           INITIALIZE           BP13L50C-REC.                                   
           MOVE SPACES      TO  BP13L50C-REC.                                   
                                                                                
           WRITE BP13L50C-REC FROM L50C-SUMMARY AFTER 6.                        
                                                                                
           MOVE CNT-CASES-UPD1          TO L50C-CASES-UPD1.                     
           WRITE BP13L50C-REC FROM L50C-SUMMARY1 AFTER 2.                       
                                                                                
           MOVE CNT-CASES-UPD2          TO L50C-CASES-UPD2.                     
           WRITE BP13L50C-REC FROM L50C-SUMMARY2 AFTER 2.                       
                                                                                
                                                                                
       5600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5700-WRITE-BP13F50C.                                                     
      *------------------------------------------------------------             
           INITIALIZE BP13F50C-REC.                                             
           MOVE SPACES                  TO BP13F50C-REC.                        
                                                                                
           MOVE ';'   TO FIL01  FIL02  FIL03  FIL04                             
                         FIL05  FIL06  FIL07  FIL08.                            
                                                                                
           INITIALIZE P13L50D-REC.                                              
           MOVE SPACES                  TO P13L50D-REC.                         
                                                                                
           ADD 1                        TO CNT-F50C-WRITE                       
                                                                                
      *    PERFORM 3301-READ-BP13K800   THRU 3301-EXIT.                         
      *    PERFORM 3150-READ-BP13K820   THRU 3150-EXIT.                         
      *                                                                         
      *    MOVE K800-NUM-SCH-ACC        TO F50C-SCH-ACC                         
      *                                    L50D-SCH-ACC.                        
      *    MOVE K800-NUM-FLAT-TYPE      TO F50C-FLAT-TYPE                       
      *                                    L50D-FLAT-TYPE.                      
      *    MOVE K800-DTE-ACCEPTANCE     TO F50C-DTE-BOOK                        
      *                                    L50D-DTE-BOOK.                       
      *    MOVE K800-NUM-ELIG-SCHEME    TO F50C-ELIG-SCH                        
      *                                    L50D-ELIG-SCH.                       
      *    MOVE K800-NUM-ALLO-SCHEME    TO F50C-ALLO-SCH                        
      *                                    L50D-ALLO-SCH.                       
      *    MOVE K820-NUM-MARITAL-STATUS TO F50C-MARITAL-ST                      
      *                                    L50D-MARITAL-ST.                     
           MOVE F517-NUM-NRIC           TO F50C-AP-NRIC                         
                                           L50D-AP-NRIC.                        
           MOVE F517-NME                TO F50C-AP-NAME                         
                                           L50D-AP-NAME.                        
           MOVE F517-NUM-MARRIAGE       TO F50C-NUM-MARRIAGE                    
                                           L50D-NUM-MARRIAGE.                   
           MOVE F517-DTE-MARRIAGE       TO F50C-DTE-MARRIAGE                    
                                           L50D-DTE-MARRIAGE.                   
           MOVE F517-NUM-NRIC-SPOUSE    TO F50C-SP-NRIC                         
                                           L50D-SP-NRIC.                        
           MOVE F517-NME-SPOUSE         TO F50C-SP-NAME                         
                                           L50D-SP-NAME.                        
      *    MOVE F517-NUM-IDNO-TYPE      TO F50C-SP-IDTYPE                       
      *                                    L50D-SP-IDTYPE.                      
           MOVE F517-NUM-REGN           TO F50C-REGN                            
                                           L50D-REGN.                           
                                                                                
           PERFORM 5750-CALL-BP13K022  THRU 5750-EXIT.                          
           MOVE K022-NUM-OIC            TO F50C-OIC                             
                                           L50D-OIC.                            
                                                                                
           WRITE BP13F50C-REC.                                                  
                                                                                
       5700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5750-CALL-BP13K022.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                         TO BP13K022-REC                  
           INITIALIZE                             BP13K022-REC                  
                                                                                
           MOVE F517-NUM-REGN                  TO K022-NUM-REGN                 
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF WS-K022-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K022-STATUS = 23                                            
                 MOVE SPACES                   TO K022-NUM-OIC                  
              ELSE                                                              
                 DISPLAY 'BP13K022 - READ ERROR (' WS-K022-STATUS ')'           
                 PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5750-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-WRITE-L50D-REPORT.                                                  
      *---------------------------------------------------------------*         
           ADD 1                            TO WS-L50D-WRTN                     
                                               WS-NUM-SNO-LD50                  
                                                                                
           MOVE WS-NUM-SNO-LD50         TO L50D-SNO.                            
                                                                                
           WRITE P13L50D-REC               FROM L50D-DETAIL1.                   
           WRITE P13L50D-REC               FROM MAIL-SPACES.                    
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13C50C END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM K517   ' F517-READ.                 
           DISPLAY 'NO OF RECORDS BYPASS  IN  F517 ' CNT-F517-BYPASS.           
           DISPLAY 'NO OF RECORDS VALID FS IN F517 ' CNT-VALID-F517-FS.         
           DISPLAY 'NO OF NONFS RECS VALID IN F517 '                            
                                                  CNT-VALID-F517-NONFS.         
           DISPLAY 'NO OF RECORDS NO MARRIAGE F517 ' F517-NO-MARRIAGE           
           DISPLAY 'NO OF RECORDS READ FROM K517   ' K517-READ.                 
           DISPLAY 'NO OF RECORDS UPDATED IN  K517 ' CNT-K517-UPDATED.          
           DISPLAY 'NO OF REC DEL, BACKUP  P13F517 ' F517-WRITE.                
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS FOUND   IN  K820 ' CNT-K820-FND.              
           DISPLAY 'NO OF RECORDS NOTFD   IN  K820 ' CNT-K820-NOTFND.           
           DISPLAY 'NO OF NON-FS RECS FOUND   IN  K820 '                        
                                                    CNT-K820-FND-NONFS.         
           DISPLAY 'NO OF NON-FS RECS NOTFD IN  K820 '                          
                                                 CNT-K820-NOTFND-NONFS.         
           DISPLAY 'NO OF RECORDS CASES    IN K820 ' CNT-K820-REC.              
           DISPLAY 'NO OF RECORDS VALID FS IN K820 ' CNT-VALID-K820-FS.         
           DISPLAY 'NO OF FS DATA NOT TALLY K820   '                            
                                                 CNT-NOTVALID-FS-DATA.          
           DISPLAY 'NO OF REC NOT VALID FS IN K820 '                            
                                              CNT-NOTVALID-K820-FS.             
           DISPLAY 'NO OF RECORDS UPDATED IN  K820 ' CNT-K820-UPDATED.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF NON-FS REC FOUND IN K800 '                            
                                                    CNT-K800-FND-NONFS.         
           DISPLAY 'NO OF NON-FS REC NOTFD IN K800 '                            
                                                 CNT-K800-NOTFND-NONFS.         
           DISPLAY 'NO OF RECORDS FOUND   IN  K800 ' CNT-K800-FND.              
           DISPLAY 'NO OF RECORDS NOTFD   IN  K800 ' CNT-K800-NOTFND.           
           DISPLAY 'NO OF RECORDS VALID FS IN K800 ' CNT-VALID-K800-FS.         
           DISPLAY 'NO OF RECORDS UPDATED IN  K800 ' CNT-K800-UPDATED.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS WRITE   IN  K810 ' CNT-K810-WRITTEN.          
           DISPLAY 'NO OF RECORDS UPDATED IN  K848 ' CNT-K848-WRITTEN.          
           DISPLAY 'NO OF RECORDS WRITTEN IN  L50C ' CNT-L50C-WRITE.            
           DISPLAY 'NO OF RECORDS WRITTEN IN  F50C ' CNT-F50C-WRITE.            
           DISPLAY 'NO OF RECORDS WRITTEN IN  L50D ' WS-L50D-WRTN.              
           DISPLAY 'NO OF RECORDS W/ MRG DTE BEF DTE ACPT '                     
                                                 CNT-F50C-BYPASS.               
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BP13K517                                                       
                 BP13F517                                                       
                 BP13K848                                                       
                 BP13K810                                                       
                 BP13K022                                                       
                 P13F517A                                                       
                 BP13F50C                                                       
                 BP13L50C                                                       
                 P13LC50D.                                                      
                                                                                
           IF WS-K517-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K517 CLOSE ERROR ' WS-K517-STATUS                    
              MOVE WS-K517-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 CLOSE ERROR ' WS-K800-STATUS                    
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 CLOSE ERROR ' WS-K820-STATUS                    
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K848-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K848 CLOSE ERROR ' WS-K848-STATUS                    
              MOVE WS-K848-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K810-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K810 CLOSE ERROR ' WS-K810-STATUS                    
              MOVE WS-K810-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K022 CLOSE ERROR ' WS-K022-STATUS                    
              MOVE WS-K022-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
