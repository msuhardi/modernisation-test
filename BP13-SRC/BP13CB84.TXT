       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB84.                                                 
      *AUTHOR.        ZDD1                                                      
      *DATE-WRITTEN.  25JAN2010.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. UPDATE RESULT STATUS BASE ON QUEUE NUMBER        *                
      *                                                        *                
      *                                                        *                
      *                                                        *                
      *    INPUT FILES  : BP13F740                             *                
      *                   BP13K813                             *                
      *          I-O    : BP13K595                             *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP133838 ZDD 20100125  NEW PGM                          *                
      *BP134510 ZDD 20120424  CHECK IF THE SAME QUEUE BEF UPD  *                
      *BP135190 SMR 20131204  EXPAND BP13K813 TO 1000          *                
      *BP135369 SMR 20140613  INITIALIZE BP13K595              *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS WS-K813-STATUS.                            
                                                                                
           SELECT BP13K595 ASSIGN TO BP13K595                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K595-KEY-FLD                               
                  FILE STATUS     IS WS-K595-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F740-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-F595-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-K813-STATUS                 PIC X(2)  VALUE SPACES.           
           05  WS-K595-STATUS                 PIC X(2)  VALUE SPACES.           
           05  WS-K595-FND                    PIC X(1)  VALUE 'N'.              
           05  WS-K813-FND                    PIC X(1)  VALUE 'N'.              
           05  WS-F740-READ                   PIC 9(6)  VALUE ZEROS.            
           05  WS-K813-FOUND                  PIC 9(6)  VALUE ZEROS.            
           05  WS-K813-NOT-FOUND              PIC 9(6)  VALUE ZEROS.            
           05  WS-K595-FOUND                  PIC 9(6)  VALUE ZEROS.            
           05  WS-K595-NOT-FOUND              PIC 9(6)  VALUE ZEROS.            
           05  WS-K595-UPDATED                PIC 9(6)  VALUE ZEROS.            
                                                                                
                                                                                
       01  WS-EDITED-DATE                PIC X(10) VALUE SPACES.                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE               PIC X(08) VALUE SPACES.                
           05  WS-SYS-TIME               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE SPACES.                
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F740         THRU 2000-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-F740      THRU 3000-EXIT                        
                  UNTIL WS-F740-EOF    = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K813                                                 
                I-O    BP13K595.                                                
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 OPEN ERROR, STATUS IS ' WS-K813-STATUS          
              MOVE WS-K813-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K595-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K595 OPEN ERROR, STATUS IS ' WS-K595-STATUS          
              MOVE WS-K595-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
                                                                                
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
           DELIMITED BY SIZE INTO WS-EDITED-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F740.                                                          
      ****************************************************************          
                                                                                
           READ BP13F740                                                        
              AT END MOVE 'Y' TO WS-F740-EOF                                    
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-F740-READ.                              
                                                                                
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2500-READ-BP13K813.                                                      
      ****************************************************************          
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
             WHEN 00                                                            
               ADD 1     TO WS-K813-FOUND                                       
               MOVE 'Y'  TO WS-K813-FND                                         
             WHEN 23                                                            
               ADD 1     TO WS-K813-NOT-FOUND                                   
               MOVE 'N'  TO WS-K813-FND                                         
               DISPLAY 'RECORD NOT FOUND' K813-KEY-FLD                          
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BP13K813'                                 
               MOVE WS-K813-STATUS TO RETURN-CODE                               
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F740.                                                       
      ****************************************************************          
                                                                                
           MOVE  SPACES                      TO K813-KEY-FLD.                   
           MOVE  F740-CDE-NT1                TO K813-NUM-ZONE.                  
           MOVE  F740-DTE-BALLOT             TO K813-DTE-BALLOT.                
           MOVE  F740-CDE-FLAT-TYPE          TO K813-NUM-FLAT-TYPE.             
                                                                                
           PERFORM  2500-READ-BP13K813       THRU 2500-EXIT.                    
                                                                                
           IF WS-K813-FND = 'Y'                                                 
             IF F740-NUM-NT-FT-QUEUE <= K813-NUM-QUEUE-LAST-SUCC                
                PERFORM 4000-START-BP13K595      THRU 4000-EXIT                 
                IF WS-K595-FND  = 'Y'                                           
                   MOVE 'P' TO K595-CDE-REQUEST-STATUS                          
                   PERFORM 5000-UPDATE-BP13K595  THRU 5000-EXIT                 
                END-IF                                                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM  2000-READ-F740            THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-START-BP13K595.                                                     
      ****************************************************************          
                                                                                
           MOVE SPACES                      TO BP13K595-REC.                    
           INITIALIZE                          BP13K595-REC.                    
                                                                                
           MOVE F740-NUM-REGN               TO K595-NUM-REGN-REF.               
           MOVE 'N' TO WS-K595-FND   WS-F595-EOF.                               
                                                                                
           START BP13K595 KEY >= K595-KEY-FLD                                   
                                                                                
           EVALUATE WS-K595-STATUS                                              
             WHEN 00                                                            
             WHEN 02                                                            
             WHEN 22                                                            
                 PERFORM 4500-READNEXT-BP13K595  THRU 4500-EXIT                 
                    UNTIL WS-F595-EOF = 'Y' OR                                  
                      F740-NUM-REGN NOT = K595-NUM-REGN-REF OR                  
                         WS-K595-FND   = 'Y'                                    
             WHEN 23                                                            
                 ADD 1 TO WS-K595-NOT-FOUND                                     
             WHEN OTHER                                                         
               DISPLAY 'ERROR START READING BP13K595'                           
               MOVE WS-K595-STATUS TO RETURN-CODE                               
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4500-READNEXT-BP13K595.                                                  
      ****************************************************************          
                                                                                
           READ BP13K595 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K595-STATUS                                              
             WHEN 00                                                            
             WHEN 02                                                            
             WHEN 22                                                            
                 IF F740-NUM-REGN  = K595-NUM-REGN-REF                          
                   IF F740-NUM-NT-FT-QUEUE = K595-NUM-NT-FT-QUEUE               
                     IF K595-CDE-REQUEST-STATUS = 'R' OR 'W'                    
                        MOVE 'Y' TO WS-K595-FND                                 
                        ADD 1 TO WS-K595-FOUND                                  
                     END-IF                                                     
                   END-IF                                                       
                 END-IF                                                         
             WHEN 10                                                            
                 MOVE 'Y' TO WS-F595-EOF                                        
                 ADD 1 TO WS-K595-NOT-FOUND                                     
             WHEN OTHER                                                         
               DISPLAY 'ERROR  READING BP13K595'                                
               MOVE WS-K595-STATUS TO RETURN-CODE                               
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-UPDATE-BP13K595.                                                    
      ****************************************************************          
                                                                                
            REWRITE BP13K595-REC.                                               
                                                                                
            EVALUATE WS-K595-STATUS                                             
                                                                                
            WHEN 00                                                             
               ADD 1 TO WS-K595-UPDATED                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR REWRITING BP13K595. STATUS IS '                   
                        WS-K595-STATUS                                          
               MOVE WS-K595-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13F740 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F740 RECORDS READ        = ' WS-F740-READ.              
           DISPLAY 'NO OF REC FOUND BP13K813     = ' WS-K813-FOUND.             
           DISPLAY 'NO OF REC NOT FOUND BP13K813 = ' WS-K813-NOT-FOUND.         
           DISPLAY 'NO OF REC UPDATED BP13K595   = ' WS-K595-UPDATED.           
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F740                                                       
                 BP13K595                                                       
                 BP13K813.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 CLOSE ERROR, STATUS IS ' WS-K813-STATUS         
              MOVE WS-K813-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K595-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K595 CLOSE ERROR, STATUS IS ' WS-K595-STATUS         
              MOVE WS-K595-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
