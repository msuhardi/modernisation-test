      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C412.                                                 
       AUTHOR.        TAN M K.                                                  
       DATE-WRITTEN.  11/10/89.                                                 
                                                                                
      ************************************************************              
      *    SYSTEM NAME :   SYSTEM OF COMMITMENT                  *              
      ************************************************************              
      *    OBJECTIVE   :                                         *              
      *      1.  PROGRAM TO UPDATE PIDB INTO TRANSACTION FILE    *              
      *          FOR BATCH PROCESSING.                           *              
      *                                                          *              
      *    INPUT FILES :                                         *              
      *      1.  BP13K310 - TRANSACTION FILE                     *              
      *      2.  BP13K315 - TRANSACTION FILE (STAMPING)          *              
      *                                                          *              
      *   OUTPUT FILES :                                         *              
      *      1.  BP13F310 - OUTPUT TRANSACTION FLAT FILE         *              
      *      1.  BP13F315 - OUTPUT TRANSACTION FLAT FILE         *              
      *                                                          *              
      *   SUB-PROGRAM :                                          *              
      *      1.  BP13C913 - CALLED SUB-ROUTINE TO RETRIEVE       *              
      *                     PIDB INFORMATION.                    *              
      ************************************************************              
      *                                                          *              
      *  CHG-REQ     DATE     BY    DESCRIPTION                  *              
      *  -------   --------   ---   ---------------------------  *              
      *  SOC-PH8   29/07/93   JEP   CALL PROGRAM BP13C904 TO     *              
      *                             RETRIEVE F310 OTHER DETAILS  *              
      * BP130316   12/08/97   LJL   TO RETREIVE TC CODE FROM     *              
      *                             ED TABLE.                    *              
      * BP130368   26/08/97   LMS   DO NOT CALL BP13C904 FOR     *              
      *                             TRANS-TYPE = 'EA' OR 'ER'.   *              
      * BP130696   23/07/99   GR    READ/WRITE BP13K315/BP13F315 *              
      * BP130778   15/12/99   GBR   TO MOVE STRATA FROM BP13C909 *              
      *                             TO F310                      *              
      * BP130883   08/08/00   RAS   TO CHANGE BP13C909 TO        *              
      *                             BP13C913                                    
      ************************************************************              
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K310   ASSIGN        TO  BP13K310                         
                             ACCESS MODE   IS  DYNAMIC                          
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  K310-KEY-FLD                     
                             FILE STATUS   IS  WS-K310-STATUS.                  
                                                                                
           SELECT BP13K315   ASSIGN        TO  BP13K315                         
                             ACCESS MODE   IS  DYNAMIC                          
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  K315-KEY-FLD                     
                             FILE STATUS   IS  WS-K315-STATUS.                  
                                                                                
           SELECT BP13F310   ASSIGN        TO  BP13F310.                        
                                                                                
           SELECT BP13F315   ASSIGN        TO  BP13F315.                        
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13K310                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K310.                                                           
                                                                                
       FD   BP13F310                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K315                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K315.                                                           
                                                                                
       FD   BP13F315                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F315.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-ACCCESS-STATUS.                                              
           05  WS-K310-STATUS          PIC 9(2)    VALUE ZEROS.                 
           05  WS-K315-STATUS          PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-K310-IND         PIC X       VALUE SPACE.                 
               88  WS-EOF-K310                     VALUE 'Y'.                   
           05  WS-EOF-K315-IND         PIC X       VALUE SPACE.                 
               88  WS-EOF-K315                     VALUE 'Y'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-K310-READ        PIC 9(5)    VALUE 0.                     
           05  WS-CNT-K315-READ        PIC 9(5)    VALUE 0.                     
           05  WS-CNT-F310-WRITE       PIC 9(5)    VALUE 0.                     
           05  WS-CNT-F315-WRITE       PIC 9(5)    VALUE 0.                     
           05  WS-CNT-F310-NON-PIDB    PIC 9(5)    VALUE 0.                     
           05  WS-CNT-PIDB-FND         PIC 9(5)    VALUE 0.                     
           05  WS-CNT-PIDB-NOTFND      PIC 9(5)    VALUE 0.                     
                                                                                
       05  WS-NUM-HSEMAIN.                                                      
           10  WS-NUM-HSE-MAIN         PIC X(4).                                
           10  WS-NUM-HSE-SUB.                                                  
               15  WS-NUM-HSE-SUB1     PIC X.                                   
               15  WS-NUM-HSE-SUB2     PIC XX.                                  
                                                                                
       01  SQL-CODE                    PIC 9(4).                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *--------------------*                                                    
       PROCEDURE DIVISION.                                                      
      *--------------------*                                                    
                                                                                
      *-----------------*                                                       
       000-MAIN-ROUTINE.                                                        
      *-----------------*                                                       
      **** BP13K310 PROCESSING ****                                             
           PERFORM  100-OPEN-ROUTINE        THRU  100-EXIT.                     
           PERFORM  200-START-BP13K310      THRU  200-EXIT.                     
           PERFORM  300-READ-BP13K310       THRU  300-EXIT.                     
           PERFORM  400-PROCESS-ROUTINE     THRU  400-EXIT                      
                        UNTIL WS-EOF-K310.                                      
           PERFORM  900-CONTROL-LIST-K310.                                      
                                                                                
      **** BP13K315 PROCESSING ****                                             
           MOVE ZEROES  TO  WS-CNT-PIDB-FND.                                    
           MOVE ZEROES  TO  WS-CNT-PIDB-NOTFND.                                 
                                                                                
           PERFORM  250-START-BP13K315      THRU  250-EXIT.                     
           PERFORM  350-READ-BP13K315       THRU  350-EXIT.                     
           PERFORM  450-PROCESS-ROUTINE     THRU  450-EXIT                      
                        UNTIL WS-EOF-K315.                                      
           PERFORM  900-CONTROL-LIST-K315.                                      
                                                                                
      **** CLOSE ROUTINE ****                                                   
           PERFORM  900-CLOSE-ROUTINE.                                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       100-OPEN-ROUTINE.                                                        
      *-----------------*                                                       
           OPEN INPUT  BP13K310 BP13K315                                        
                OUTPUT BP13F310 BP13F315.                                       
                                                                                
           IF WS-K310-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K310-STATUS = '                       
                       WS-K310-STATUS                                           
              MOVE     WS-K310-STATUS TO RETURN-CODE                            
              PERFORM  900-CONTROL-LIST-K310   THRU  900-EXIT                   
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K315-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K315-STATUS = '                       
                       WS-K315-STATUS                                           
              MOVE     WS-K315-STATUS TO RETURN-CODE                            
              PERFORM  900-CONTROL-LIST-K315   THRU  900-EXIT                   
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       200-START-BP13K310.                                                      
      *-----------------*                                                       
           MOVE  LOW-VALUES  TO K310-KEY-FLD.                                   
                                                                                
           START BP13K310 KEY > K310-KEY-FLD.                                   
                                                                                
           IF WS-K310-STATUS NOT = 00                                           
              DISPLAY 'ERROR START BP13K310, STATUS = ' WS-K310-STATUS          
              MOVE     WS-K310-STATUS TO RETURN-CODE                            
              PERFORM  900-CONTROL-LIST-K310 THRU 900-EXIT                      
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       250-START-BP13K315.                                                      
      *-----------------*                                                       
           MOVE  LOW-VALUES  TO K315-KEY-FLD.                                   
                                                                                
           START BP13K315 KEY > K315-KEY-FLD.                                   
                                                                                
           IF WS-K315-STATUS NOT = 00                                           
              DISPLAY 'ERROR START BP13K315, STATUS = ' WS-K315-STATUS          
              MOVE     WS-K315-STATUS TO RETURN-CODE                            
              PERFORM  900-CONTROL-LIST-K315                                    
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BP13K310.                                                       
      *-----------------*                                                       
           READ BP13K310 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K310-IND                                     
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-CNT-K310-READ.                                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       350-READ-BP13K315.                                                       
      *-----------------*                                                       
           READ BP13K315 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K315-IND                                     
                GO TO 350-EXIT.                                                 
                                                                                
           ADD 1 TO WS-CNT-K315-READ.                                           
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       400-PROCESS-ROUTINE.                                                     
      *--------------------*                                                    
           MOVE  SPACES                TO  BP13F310-TRANS-REC.                  
           INITIALIZE                      BP13F310-TRANS-REC.                  
                                                                                
           MOVE  BP13K310-TRANS-REC    TO  BP13F310-TRANS-REC.                  
                                                                                
           IF (K310-SCH-ACC NOT = SPACES AND ZEROES AND LOW-VALUES) OR          
              (K310-CDE-TRANS NOT = 'DO' AND 'IO' AND                           
                                    'AD' AND 'EA' AND                           
                                    'ER')                                       
                                                                                
               MOVE  SPACES               TO     BP13COMM8-REC                  
               INITIALIZE                        BP13COMM8-REC                  
               MOVE  K310-SCH-ACC         TO     WS-LINK-REC                    
               PERFORM 500-CALL-BP13C913  THRU   500-EXIT                       
                                                                                
               IF COMM8-CDE-SYSERR = 0                                          
                  ADD 1 TO WS-CNT-PIDB-FND                                      
                  PERFORM 600-MOVE-PIDB-DETLS THRU 600-EXIT                     
               ELSE                                                             
                  ADD 1 TO WS-CNT-PIDB-NOTFND                                   
               END-IF                                                           
           ELSE                                                                 
               ADD 1 TO  WS-CNT-F310-NON-PIDB                                   
           END-IF.                                                              
                                                                                
           IF F310-NUM-PREMIUM-RATE IS NOT NUMERIC                              
              MOVE ZERO TO F310-NUM-PREMIUM-RATE                                
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           IF F310-AMT-STAMP-DUTIES IS NOT NUMERIC                              
              MOVE ZERO TO F310-AMT-STAMP-DUTIES                                
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           WRITE     BP13F310-TRANS-REC.                                        
           ADD 1 TO  WS-CNT-F310-WRITE.                                         
                                                                                
           PERFORM 300-READ-BP13K310 THRU 300-EXIT.                             
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       450-PROCESS-ROUTINE.                                                     
      *-------------------*                                                     
           MOVE  SPACES                TO    BP13F315-TRANS-REC.                
           INITIALIZE                        BP13F315-TRANS-REC.                
                                                                                
           MOVE  BP13K315-TRANS-REC    TO    BP13F315-TRANS-REC.                
                                                                                
           MOVE  SPACES                TO    BP13COMM8-REC.                     
           INITIALIZE                        BP13COMM8-REC.                     
           MOVE  K315-SCH-ACC          TO    WS-LINK-REC.                       
           PERFORM 500-CALL-BP13C913   THRU  500-EXIT.                          
                                                                                
           IF COMM8-CDE-SYSERR = 0                                              
              ADD 1 TO WS-CNT-PIDB-FND                                          
              PERFORM 650-MOVE-PIDB-DETLS THRU 650-EXIT                         
           ELSE                                                                 
              ADD 1 TO WS-CNT-PIDB-NOTFND                                       
           END-IF.                                                              
                                                                                
           WRITE     BP13F315-TRANS-REC.                                        
           ADD 1 TO  WS-CNT-F315-WRITE.                                         
                                                                                
           PERFORM 350-READ-BP13K315 THRU 350-EXIT.                             
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-CALL-BP13C913.                                                       
      *------------------*                                                      
           CALL 'BP13C913'     USING  WS-LINK-REC,                              
                                      BP13COMM8-REC.                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       600-MOVE-PIDB-DETLS.                                                     
      *-------------------*                                                     
           MOVE COMM8-NUM-MGMT        TO  F310-CDE-BO.                          
           MOVE COMM8-NUM-ED-GL       TO  F310-CDE-CC.                          
           MOVE COMM8-NUM-MNTNC       TO  F310-CDE-TC.                          
           MOVE COMM8-NUM-UNIT-MAIN   TO  WS-NUM-HSE-MAIN.                      
           MOVE COMM8-NUM-UNIT-SUB    TO  WS-NUM-HSE-SUB.                       
           MOVE COMM8-NUM-BLK         TO  F310-NUM-BLK.                         
           MOVE COMM8-NME-STREET      TO  F310-NME-STREET.                      
           MOVE COMM8-NUM-LEVEL       TO  F310-NUM-LEVEL.                       
           MOVE WS-NUM-HSEMAIN        TO  F310-NUM-MAIN.                        
           MOVE COMM8-NUM-POSTAL-CODE TO  F310-NUM-POSTAL.                      
           MOVE COMM8-CDE-BUC         TO  F310-CDE-BUC.                         
                                                                                
           IF   COMM8-QTY-AREA-TOTAL IS NOT NUMERIC                             
                MOVE  ZERO                 TO  F310-QTY-AREA-STRATA             
           ELSE                                                                 
                MOVE COMM8-QTY-AREA-TOTAL  TO  F310-QTY-AREA-STRATA             
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       650-MOVE-PIDB-DETLS.                                                     
      *-------------------*                                                     
           MOVE COMM8-NUM-MGMT        TO  F315-CDE-BO.                          
           MOVE COMM8-NUM-ED-GL       TO  F315-CDE-CC.                          
           MOVE COMM8-NUM-MNTNC       TO  F315-CDE-TC.                          
           MOVE COMM8-NUM-UNIT-MAIN   TO  WS-NUM-HSE-MAIN.                      
           MOVE COMM8-NUM-UNIT-SUB    TO  WS-NUM-HSE-SUB.                       
           MOVE COMM8-NUM-BLK         TO  F315-NUM-BLK.                         
           MOVE COMM8-NME-STREET      TO  F315-NME-STREET.                      
           MOVE COMM8-NUM-LEVEL       TO  F315-NUM-LEVEL.                       
           MOVE WS-NUM-HSEMAIN        TO  F315-NUM-MAIN.                        
           MOVE COMM8-NUM-POSTAL-CODE TO  F315-NUM-POSTAL.                      
           MOVE COMM8-CDE-BUC         TO  F315-CDE-BUC.                         
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       900-CONTROL-LIST-K310.                                                   
      *----------------------*                                                  
           DISPLAY '                                         '.                 
           DISPLAY '*****     BP13C412 CONTROL TOTAL    *****'.                 
           DISPLAY '                                         '.                 
           DISPLAY '  ***    ------------------------   ***  '.                 
           DISPLAY '  ***      BP13K310 PROCESSING      ***  '.                 
           DISPLAY '  ***    ------------------------   ***  '.                 
           DISPLAY '                                         '.                 
           DISPLAY 'NO OF K310 REC READ        = ' WS-CNT-K310-READ.            
           DISPLAY 'NO OF K310 REC SKIPPED     = ' WS-CNT-F310-NON-PIDB.        
           DISPLAY 'NO SCH-ACC FND IN PIDB     = ' WS-CNT-PIDB-FND.             
           DISPLAY 'NO SCH-ACC NOT FND IN PIDB = ' WS-CNT-PIDB-NOTFND.          
           DISPLAY 'NO OF F310 REC UPDTE       = ' WS-CNT-F310-WRITE.           
           DISPLAY '                                         '.                 
           DISPLAY '                                         '.                 
                                                                                
                                                                                
      *----------------------*                                                  
       900-CONTROL-LIST-K315.                                                   
      *----------------------*                                                  
           DISPLAY '                                         '.                 
           DISPLAY '  ***      -------------------      ***  '.                 
           DISPLAY '  ***      BP13K315 PROCESSING      ***  '.                 
           DISPLAY '  ***      -------------------      ***  '.                 
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF K315 REC READ        = ' WS-CNT-K315-READ.           
           DISPLAY 'NO. SCH-ACC FND IN PIDB     = ' WS-CNT-PIDB-FND.            
           DISPLAY 'NO. SCH-ACC NOT FND IN PIDB = ' WS-CNT-PIDB-NOTFND.         
           DISPLAY 'NO. OF F315 REC UPDTE       = ' WS-CNT-F315-WRITE.          
           DISPLAY '                                         '.                 
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       900-CLOSE-ROUTINE.                                                       
      *------------------*                                                      
           CLOSE BP13K310 BP13K315                                              
                 BP13F310 BP13F315.                                             
                                                                                
           IF WS-K310-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K310, STATUS = '                       
                   WS-K310-STATUS                                               
              MOVE WS-K310-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           IF WS-K315-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K315, STATUS = '                       
                  WS-K315-STATUS                                                
              MOVE WS-K315-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
