/*-----------------------------------------------------------------*/           
/* SYSTEM OF COMMITMENT - DBSS  (BP13)                             */           
/*-----------------------------------------------------------------*/           
/*-----------------------------------------------------------------*/           
/* PROGRAM TO PRINT GL POSTING REPORT FOR EVERY MONTH              */           
/*-----------------------------------------------------------------*/           
/* CHG-NO   BY  ON           DESCRIPTION                           */           
/* -------- --  --           ------------                          */           
/* BP132934 BA2 16/10/2006   NEW PRG.                              */           
/* BP132934 BA2 23/01/2007   SORT BY GL-ACC AND TRANSACTION DESC   */           
/* BP132973 BA2 05/02/2007   SUB TITLE  CATER BY GL-ACC AND DESC   */           
/*-----------------------------------------------------------------*/           
                                                                                
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
     DATA BP13F180;                                                             
     INFILE BP13F180 END=EOF;                                                   
     INPUT  @01   JR_ENTRY_NO        $CHAR6.                                    
            @07   PERIOD             $CHAR2.                                    
            @15   GL_ACC_NO          $CHAR13.                                   
            @45   DB_CR_AMT          ZD13.2                                     
            @58   TRANS_DESC         $CHAR40.                                   
            @98   SCH_ACC            $CHAR11.                                   
            @109  TRANS_DATE         $CHAR8.;                                   
                                                                                
     PROC SORT; BY GL_ACC_NO TRANS_DESC SCH_ACC TRANS_DATE;                     
                                                                                
     DATA SY02;                                                                 
     INFILE SY02F001 END = EOF;                                                 
     INPUT  @01  DATE_CYM_CURR        YYMMDD8.                                  
            @21  DATE_CYM_PREV        $CHAR6.                                   
            @21  DATE_CY_PREV         $CHAR4.                                   
            @58  PREV_MTH_TXT         $CHAR3.;                                  
                                                                                
         CALL SYMPUT ('PREV_MTH',PREV_MTH_TXT);                                 
         CALL SYMPUT ('PREV_CY',DATE_CY_PREV);                                  
         CALL SYMPUT ('CURR_DTE',(PUT(DATE_CYM_CURR,DDMMYY10.)));               
         CALL SYMPUT ('PREVDTE', DATE_CYM_PREV);                                
         CALL SYMPUT ('PREMTXT', PREV_MTH_TXT);                                 
                                                                                
                                                                                
     DATA _NULLS_;                                                              
                                                                                
     IF TOTAL = 0 THEN                                                          
        DO;                                                                     
          PUT _PAGE_;                                                           
          PUT // @1 '*** NO RECORDS PRINTED IN THIS RUN';                       
        END;                                                                    
                                                                                
       SET BP13F180 END=EOF NOBS=TOTAL;                                         
       BY GL_ACC_NO TRANS_DESC SCH_ACC TRANS_DATE;                              
                                                                                
       OPTIONS PAGESIZE=55 LS=132 NODATE;                                       
       FILE BP13LC07 HEADER=HH NOTITLES LL=LEFTL;                               
                                                                                
       IF _N_ = 1 THEN                                                          
          DO;                                                                   
            LENGTH SNO               5.;                                        
            LENGTH WS_GL_NO          $18;                                       
            LENGTH WS_TRANS_DT       $10;                                       
            LENGTH WS_SCH_NO         $14;                                       
            LENGTH SUB_TOT_DEBT      8;                                         
            LENGTH SUB_TOT_CRDT      8;                                         
            LENGTH GRT_TOT_DEBT      8;                                         
            LENGTH GRT_TOT_CRDT      8;                                         
            LENGTH WS_PREV_DATE      $8;                                        
            LENGTH HH_GL_DES         $70;                                       
            RETAIN SUB_TOT_DEBT SUB_TOT_CRDT GRT_TOT_DEBT GRT_TOT_CRDT          
                   WS_TRANS_DT SNO WS_PREV_DATE HH_GL_DES;                      
            SUB_TOT_CRDT=0;                                                     
            SUB_TOT_DEBT=0;                                                     
          END;                                                                  
                                                                                
    WS_GL_NO = SUBSTR(GL_ACC_NO,1,1) || '-' || SUBSTR(GL_ACC_NO,2,2) ||         
       '-' || SUBSTR(GL_ACC_NO,4,2) || '-' || SUBSTR(GL_ACC_NO,6,2)  ||         
       '-' || SUBSTR(GL_ACC_NO,8,2) || '-' || SUBSTR(GL_ACC_NO,10,4);           
                                                                                
    WS_PREV_DATE = SUBSTR("&PREMTXT",1,3) || ' ' ||                             
                   SUBSTR("&PREV_CY",1,4);                                      
                                                                                
    IF TRANS_DATE ^= '        ' THEN                                            
      DO;                                                                       
       WS_TRANS_DT = SUBSTR(TRANS_DATE,7,2) || '/' ||                           
                     SUBSTR(TRANS_DATE,5,2) || '/' ||                           
                     SUBSTR(TRANS_DATE,1,4);                                    
      END;                                                                      
    ELSE                                                                        
      DO;                                                                       
         WS_TRANS_DT = '        ';                                              
      END;                                                                      
                                                                                
    WS_SCH_NO   = SUBSTR(SCH_ACC,1,4) || '-' ||                                 
                  SUBSTR(SCH_ACC,5,4) || '-' ||                                 
                  SUBSTR(SCH_ACC,9,1) || '-' ||                                 
                  SUBSTR(SCH_ACC,10,2);                                         
                                                                                
    HH_GL_DES   = TRIM(SUBSTR(GL_ACC_NO,1,13))  || ' ( ' ||                     
                  TRIM(TRANS_DESC) || ' ) ';                                    
                                                                                
       IF (FIRST.GL_ACC_NO OR FIRST.TRANS_DESC) AND _N_ ^= 1 THEN               
         DO;                                                                    
         SNO=0;                                                                 
         IF LEFTL < 40 THEN                                                     
            DO;                                                                 
              LINK SUB_HH;                                                      
            END;                                                                
            ELSE                                                                
              DO;                                                               
                PUT _PAGE_;                                                     
              END;                                                              
         END;                                                                   
                                                                                
       SNO+1;                                                                   
       IF DB_CR_AMT > 0 THEN                                                    
          DO;                                                                   
           PUT @01  SNO                                                         
               @09  JR_ENTRY_NO                                                 
               @21  PERIOD                                                      
               @30  WS_TRANS_DT                                                 
               @43  WS_SCH_NO                                                   
               @64  DB_CR_AMT COMMA12.2;                                        
           END;                                                                 
       ELSE                                                                     
          DO;                                                                   
           DB_CR_AMT_ED = DB_CR_AMT * -1;                                       
           PUT @01  SNO                                                         
               @09  JR_ENTRY_NO                                                 
               @21  PERIOD                                                      
               @30  WS_TRANS_DT                                                 
               @43  WS_SCH_NO                                                   
               @85  DB_CR_AMT_ED COMMA12.2;                                     
          END;                                                                  
                                                                                
       IF DB_CR_AMT > 0 THEN                                                    
         DO;                                                                    
            SUB_TOT_DEBT + DB_CR_AMT;                                           
            GRT_TOT_DEBT + DB_CR_AMT;                                           
         END;                                                                   
       ELSE                                                                     
         DO;                                                                    
            SUB_TOT_CRDT + DB_CR_AMT;                                           
            GRT_TOT_CRDT + DB_CR_AMT;                                           
         END;                                                                   
                                                                                
       IF LAST.GL_ACC_NO OR LAST.TRANS_DESC THEN                                
         DO;                                                                    
                                                                                
           IF LEFTL < 5 THEN                                                    
              DO;                                                               
                PUT _PAGE_;                                                     
              END;                                                              
                                                                                
            IF SUB_TOT_CRDT ^= 0 THEN                                           
             SUB_TOT_CRDT_ED = SUB_TOT_CRDT * -1;                               
            ELSE                                                                
             SUB_TOT_CRDT_ED = 0;                                               
                                                                                
               PUT @2 ' '/;                                                     
               PUT @67  '----------'                                            
                   @87  '----------';                                           
               PUT @52 'TOTAL '                                                 
                   @64 SUB_TOT_DEBT COMMA12.2                                   
                   @85 SUB_TOT_CRDT_ED COMMA12.2;                               
               PUT @67  '----------'                                            
                   @87  '----------';                                           
               PUT @2 ' '/;                                                     
                                                                                
            SUB_TOT_DEBT=0;                                                     
            SUB_TOT_CRDT=0;                                                     
            SUB_TOT_CRDT_ED=0;                                                  
            SNO=0;                                                              
         END;                                                                   
                                                                                
       IF EOF THEN                                                              
         DO;                                                                    
            GRT_TOT_CRDT_ED = GRT_TOT_CRDT * -1;                                
            PUT @67  '----------'                                               
            PUT @87  '----------';                                              
            PUT @52 'GRAND TOTAL'                                               
                @64 GRT_TOT_DEBT COMMA12.2                                      
                @85 GRT_TOT_CRDT_ED COMMA12.2;                                  
            PUT @67  '----------'                                               
            PUT @87  '----------';                                              
         END;                                                                   
     RETURN;                                                                    
                                                                                
   HH:                                                                          
     PAGE+1;                                                                    
     CURRENT=DATE();                                                            
     PUT ////@1        'BP13LC07'                                               
         @12       'HDBCAT3'                                                    
         @46  'S Y S T E M   O F   C O M M I T M E N T'                         
         @105 'PAGE : ' @113 PAGE 4./                                           
         @42       'MONTHLY GENERAL LEDGER POSTING REPORT FOR '                 
         @84  WS_PREV_DATE                                                      
         @105 'DATE : ' @113 CURRENT DDMMYY10./                                 
     PUT @1 ' '/;                                                               
     LINK SUB_HH;                                                               
     RETURN;                                                                    
                                                                                
    SUB_HH:                                                                     
     PUT @01   'GL-ACCOUNT-NO  : '                                              
         @18    HH_GL_DES;                                                      
     PUT /@01   'S/NO'                                                          
         @09   'JOURNL-NO'                                                      
         @21   'PERIOD'                                                         
         @30   'TRANS-DATE'                                                     
         @43   'SCH-ACCNT-NO'                                                   
         @67   'DEBIT($)'                                                       
         @87   'CREDIT($)';                                                     
     PUT @2 ' '/;                                                               
        RETURN;                                                                 
