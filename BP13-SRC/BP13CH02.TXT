       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH02.                                                 
      *AUTHOR.        MICHAEL ARRIOLA.                                          
      *DATE-WRITTEN.  19/06/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                             
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO EXTRACT APPLICATION SCHEDULE FOR SEELCTION  *            
      *             MATCH BY SYSTEM DATE.                          *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133367  MSD1  20080619  NEW PGM                          *            
      * BP133921  LSB1  20100616  BYPASS 'NC' AND 'NF' WITH CANCEL *            
      *                           STATUS                           *            
      * BP133921  PCL3  20100726  BYPASS FLAT TYPE NOT = '1 ' AND  *            
      *                           '2 '.                            *            
      * BP133990  PCL3  20100830  BYPASS FLAT TYPE NOT = '1A' AND  *            
      *                           '2A'.                            *            
      * BP134353  PCL3  20110909  TO HANDLE FOR FLAT TYPE IF       *            
      *                           K813-NUM-FT-PREMIUM = 'P'        *            
      * BP134388  ZDD1  20120217  TO USE F800-NUM-BTO-OPTION-SUCC  *            
      *                           IN READING BP13K813              *            
      * BP134681  KAM4  20120924  REPLACE F800-NUM-BTO-OPTION-SUCC *            
      *                           BY F800-NUM-BTO-ZONE             *            
      * BP135190  SMR2  20131204  EXPAND BP13K813 TO 1000          *            
      * BP135377  ESA1  20140929  TO CATER FOR 3P FLAT TYPE FOR SBF*            
      * BP136160  FNP1  20160310  TO CATER FOR 2F FLATS            *            
      * BP136160  FNP1  20160315  DO NOT APPEND P TO 3ROOM PREMIUM *            
      * BP138565  KR13  20210125  TO CATER FOR FLAT-TYPE - '1F'.   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800  ASSIGN     TO BP13F800.                             
           SELECT SY02F001  ASSIGN     TO SY02F001.                             
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS BP13K813-STATUS.                   
                                                                                
           SELECT P13F800A  ASSIGN     TO P13F800A.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORDING MODE IS F.                                                
       01 P13F800A-REC                 PIC X(2000).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ              PIC 9(08) VALUE ZEROES.                  
      *   05 WS-K813-READ              PIC 9(08) VALUE ZEROES.                  
          05 WS-F800-WRITE             PIC 9(08) VALUE ZEROES.                  
          05 WS-F800-NWRTE             PIC 9(08) VALUE ZEROES.                  
                                                                                
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13K813-STATUS           PIC 9(02) VALUE ZEROES.                  
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-FT-PREMIUM             PIC X(1).                                
          05 WS-NUM-NEW-TOWN           PIC X(3).                                
          05 WS-DTE-BK-APPT            PIC X(8).                                
          05 WS-BK-APPT-FMT  REDEFINES WS-DTE-BK-APPT.                          
             10 WS-DTE-BK-APPT-FMT     PIC 9(8).                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F800-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K813                                                 
                       SY02F001                                                 
               OUTPUT  P13F800A.                                                
                                                                                
           IF BP13K813-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K813 : ' BP13K813-STATUS             
              MOVE     BP13K813-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
                MOVE HIGH-VALUES TO F800-NUM-REGN                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F800-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F800-NUM-STATUS = 'C' AND                                         
              (F800-NUM-ALLOC-TAG = 'NC' OR F800-NUM-ALLOC-TAG = 'NF')          
              ADD 1   TO WS-F800-NWRTE                                          
              PERFORM 2000-READ-BP13F800 THRU 2000-EXIT                         
              GO TO  3000-EXIT                                                  
           END-IF.                                                              
                                                                                
           MOVE SPACES TO  WS-NUM-NEW-TOWN.                                     
           MOVE F800-DTE-BK-APPT    TO WS-DTE-BK-APPT.                          
                                                                                
           IF WS-DTE-BK-APPT-FMT = F001-DTE-CURRENT                             
              IF F800-NUM-CAT = 4                                               
                 MOVE 3 TO F800-NUM-CAT                                         
              END-IF                                                            
                                                                                
              PERFORM 3100-DECODE-NT    THRU 3100-EXIT                          
                                                                                
              IF F800-NUM-NEW-TOWN = SPACES                                     
                 MOVE WS-NUM-NEW-TOWN TO F800-NUM-NEW-TOWN                      
              END-IF                                                            
                                                                                
              IF F800-NUM-FLAT-TYPE NOT = '1A' AND '2A' AND '2F'                
                                                        AND '1F'                
                 IF  F800-NUM-FLAT-TYPE = '3P' AND                              
                     F800-NUM-ALLO-CAT  = 'SBF'                                 
                     CONTINUE                                                   
                 ELSE                                                           
                     MOVE SPACES     TO F800-NUM-ROOM-TYPE                      
                 END-IF                                                         
                                                                                
      *          IF WS-FT-PREMIUM = 'P'                                         
      *             MOVE 'P'         TO F800-NUM-ROOM-TYPE                      
      *          END-IF                                                         
              END-IF                                                            
                                                                                
              WRITE P13F800A-REC  FROM BP13F800-MASTER                          
              ADD 1   TO WS-F800-WRITE                                          
           ELSE                                                                 
              ADD 1   TO WS-F800-NWRTE                                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-DECODE-NT.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           MOVE SPACES                         TO K813-KEY-FLD                  
                                                  WS-FT-PREMIUM.                
                                                                                
           MOVE F800-NUM-BTO-ZONE              TO K813-NUM-ZONE.                
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K813-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3200-READ-BP13K813  THRU 3200-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K813: ' BP13K813-STATUS         
                            ' KEY = ' K813-KEY-FLD                              
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3200-READ-BP13K813.                                                      
      *===============================================================*         
                                                                                
            READ  BP13K813 NEXT AT END                                          
                  MOVE SPACES               TO K813-KEY-FLD.                    
                                                                                
            EVALUATE BP13K813-STATUS                                            
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K813-NUM-ZONE = F800-NUM-BTO-ZONE                       
                        IF K813-NUM-FT-PREMIUM NOT = SPACES AND                 
                                                     LOW-VALUES                 
                           MOVE K813-NUM-FT-PREMIUM   TO WS-FT-PREMIUM          
                        END-IF                                                  
                                                                                
                        IF K813-CDE-NT NOT = SPACES AND LOW-VALUES              
                           MOVE K813-CDE-NT         TO WS-NUM-NEW-TOWN          
                        END-IF                                                  
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K813: ' BP13K813-STATUS         
                             ' KEY = ' K813-KEY-FLD                             
                     PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                
            END-EVALUATE.                                                       
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH02 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F800 : ' WS-F800-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO P13F800A : ' WS-F800-WRITE.           
           DISPLAY 'NO OF RECS NOT WRITE  BP13F800 : ' WS-F800-NWRTE.           
                                                                                
           CLOSE BP13F800                                                       
                 BP13K813                                                       
                 P13F800A                                                       
                 SY02F001.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K813 : ' BP13K813-STATUS             
              MOVE BP13K813-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
