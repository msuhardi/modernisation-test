       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD11.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  25/01/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES:                                                *            
      * USING REGN-NO, MATCH BP13FB78 WITH BP13F730, CREATE FILE   *            
      *   FOR UNMATCHED CASES:                                     *            
      * 1) ERROR TCP                                               *            
      * 2) ERROR MCP                                               *            
      * 3) ERROR CATEGORY                                          *            
      *                                                            *            
      *  INPUT FILE :   BP13FB78 -                                 *            
      *                 BP13F730 -                                 *            
      *  OUTPUT FILE:   BP13F73A -                                 *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133244  MSD1  20080128  NEW PGM                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FB78  ASSIGN        TO BP13FB78.                          
           SELECT BP13F730  ASSIGN        TO BP13F730.                          
                                                                                
           SELECT BP13F73A  ASSIGN        TO BP13F73A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FB78                                                            
            RECORD CONTAINS 3300 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FB78.                                                           
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F73A                                                            
            RECORDING MODE IS F.                                                
       01   BP13F73A-REC         PIC X(500).                                    
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-FB78-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F730-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F73A-WRITE        PIC 9(08) VALUE ZEROES.                       
                                                                                
          05 WS-MATCH-FND         PIC 9(08) VALUE ZEROES.                       
          05 WS-TCP-ERROR-CNT     PIC 9(08) VALUE ZEROES.                       
          05 WS-MCP-ERROR-CNT     PIC 9(08) VALUE ZEROES.                       
          05 WS-CAT-ERROR-CNT     PIC 9(08) VALUE ZEROES.                       
      *   05 WS-FB78-NOT-FND      PIC 9(08) VALUE ZEROES.                       
      *   05 WS-F730-NOT-FND      PIC 9(08) VALUE ZEROES.                       
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 4000-PROCESS-DATA   THRU 4000-EXIT                           
              UNTIL FB78-NUM-REGN = HIGH-VALUES                                 
                AND F730-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FB78                                                 
                       BP13F730                                                 
               OUTPUT  BP13F73A.                                                
                                                                                
           PERFORM 2000-READ-BP13FB78  THRU 2000-EXIT.                          
           PERFORM 3000-READ-BP13F730  THRU 3000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FB78.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FB78                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO FB78-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-FB78-READ                                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F730.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F730                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F730-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F730-READ                                     
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           EVALUATE TRUE                                                        
              WHEN F730-NUM-REGN  =  FB78-NUM-REGN                              
                 ADD  1                        TO WS-MATCH-FND                  
                 PERFORM 4100-CHECK-ERROR      THRU 4100-EXIT                   
                 PERFORM 3000-READ-BP13F730    THRU 3000-EXIT                   
                                                                                
              WHEN F730-NUM-REGN  <  FB78-NUM-REGN                              
                 PERFORM 3000-READ-BP13F730    THRU 3000-EXIT                   
                                                                                
              WHEN F730-NUM-REGN  >  FB78-NUM-REGN                              
                 PERFORM 2000-READ-BP13FB78    THRU 2000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-CHECK-ERROR.                                                        
      *------------------------------------ ------------------------            
                                                                                
      ****CHECK FOR TCP ERROR                                                   
           IF FB78-CDE-ALLOC-SCH = 'TCP' OR F730-CDE-ALLOC-SCH = 'TCP'          
              IF F730-CDE-ALLOC-SCH NOT = FB78-CDE-ALLOC-SCH                    
                 WRITE BP13F73A-REC FROM BP13F730-REC                           
                 ADD 1                TO WS-F73A-WRITE                          
                                         WS-TCP-ERROR-CNT                       
                 DISPLAY 'ERROR TCP: ' F730-NUM-REGN                            
                         ' KB78=' FB78-CDE-ALLOC-SCH                            
                         ' K730=' F730-CDE-ALLOC-SCH                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
      ****CHECK FOR MCP ERROR                                                   
           IF FB78-NUM-MCP-REQ = 'Y'                                            
              IF F730-NUM-MCPS NOT = SPACES                                     
              AND (F730-CDE-RESIDENT-HA1 = '1' OR 'T' OR 'A')                   
                 CONTINUE                                                       
              ELSE                                                              
                 WRITE BP13F73A-REC FROM BP13F730-REC                           
                 ADD 1                TO WS-F73A-WRITE                          
                                         WS-MCP-ERROR-CNT                       
                 DISPLAY 'ERROR MCP: ' F730-NUM-REGN                            
                        ' FB78-NUM-MCP-REQ=' FB78-NUM-MCP-REQ                   
                        ' F730-NUM-MCPS=' F730-NUM-MCPS                         
                        ' F730-CDE-RESIDENT-HA1=' F730-CDE-RESIDENT-HA1         
              END-IF                                                            
           ELSE                                                                 
              IF F730-NUM-MCPS  NOT = SPACES                                    
              AND (F730-CDE-RESIDENT-HA1 = '1' OR 'T' OR 'A')                   
                 WRITE BP13F73A-REC FROM BP13F730-REC                           
                 ADD 1                TO WS-F73A-WRITE                          
                                         WS-MCP-ERROR-CNT                       
                 DISPLAY 'ERROR MCP: ' F730-NUM-REGN                            
                 DISPLAY 'ERROR MCP: ' F730-NUM-REGN                            
                        ' FB78-NUM-MCP-REQ=' FB78-NUM-MCP-REQ                   
                        ' F730-NUM-MCPS=' F730-NUM-MCPS                         
                        ' F730-CDE-RESIDENT-HA1=' F730-CDE-RESIDENT-HA1         
              END-IF                                                            
           END-IF.                                                              
                                                                                
      ****CHECK FOR RACE/CATEGORY ERROR                                         
           IF (FB78-NUM-APPLT1-RACE = 'C  ' AND                                 
               F730-CDE-CATEGORY NOT = '2')                                     
           OR (FB78-NUM-APPLT1-RACE = 'I  ' AND                                 
               F730-CDE-CATEGORY NOT = '3')                                     
           OR (FB78-NUM-APPLT1-RACE = 'M  ' AND                                 
               F730-CDE-CATEGORY NOT = '1')                                     
           OR (FB78-NUM-APPLT1-RACE = 'O  ' AND                                 
               F730-CDE-CATEGORY NOT = '4')                                     
               WRITE BP13F73A-REC FROM BP13F730-REC                             
               ADD 1                TO WS-F73A-WRITE                            
                                       WS-CAT-ERROR-CNT                         
               DISPLAY 'ERROR CAT: ' F730-NUM-REGN                              
                      ' FB78-NUM-APPLT1-RACE=' FB78-NUM-APPLT1-RACE             
                      ' F730-CDE-CATEGORY=' F730-CDE-CATEGORY                   
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CD11 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F730 : ' WS-F730-READ.            
           DISPLAY 'NO OF RECS READ  FROM BP13FB78 : ' WS-FB78-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13F73A : ' WS-F73A-WRITE.           
           DISPLAY 'NO OF RECS MATCHED BY REGN     : ' WS-MATCH-FND.            
           DISPLAY 'NO OF TCP ERROR DETECTED       : ' WS-TCP-ERROR-CNT.        
           DISPLAY 'NO OF MCP ERROR DETECTED       : ' WS-MCP-ERROR-CNT.        
           DISPLAY 'NO OF CAT ERROR DETECTED       : ' WS-CAT-ERROR-CNT.        
                                                                                
           CLOSE BP13FB78                                                       
                 BP13F730                                                       
                 BP13F73A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
