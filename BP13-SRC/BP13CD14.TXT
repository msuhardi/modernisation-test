       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD14.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  11/02/2008.                                               
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      **********************************************************                
      *                                                        *                
      *    OBJECTIVES  :                                       *                
      *                                                        *                
      *    1.  USE F740-KEY-FLD TO READ BP13K740, IF FOUND &   *                
      *        K740-CDE-SUCC-NT-ZONE NOT = SPACES, UPDATE      *                
      *        K740-CDE-ALLOC-SCH = MCP.                       *                
      *                                                        *                
      *    INPUT FILE  : BP13F740                              *                
      *          I-O   : BP13K740                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY    DATE   DETAILS                          *                
      * ======= ====  ======  ================================ *                
      * BP13209  ED1  110208  NEW PROGRAM                      *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
             SELECT BP13F740  ASSIGN TO BP13F740.                               
                                                                                
             SELECT BP13K740                                                    
                       ASSIGN        TO BP13K740                                
                       ACCESS MODE   IS RANDOM                                  
                       ORGANIZATION  IS INDEXED                                 
                       RECORD KEY    IS K740-KEY-FLD                            
                       FILE STATUS   IS K740-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13F740             RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS  F .                           
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740           RECORD CONTAINS 500 CHARACTERS.                  
       COPY BP13K740.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-F740-EOF                  PIC X(01)  VALUE 'N'.                 
          05 K740-STATUS                  PIC 9(02)  VALUE ZERO.                
                                                                                
       01 WS-VBLES.                                                             
          05 WS-DATE                      PIC X(08)  VALUE SPACE.               
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F740-READ                 PIC 9(08)  VALUE ZERO.                
          05 WS-K740-FND                  PIC 9(08)  VALUE ZERO.                
          05 WS-K740-NOT-FND              PIC 9(08)  VALUE ZERO.                
          05 WS-K740-UPD                  PIC 9(08)  VALUE ZERO.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 1500-READ-F740       THRU 1500-EXIT.                         
           PERFORM 2000-PROCESS-RTN     THRU 2000-EXIT                          
                   UNTIL WS-F740-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K740.                                                
                                                                                
           IF K740-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K740 : ' K740-STATUS                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1500-READ-F740.                                                          
      *-------------------------------------------------------------            
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-F740-EOF                                         
                GO TO 1500-EXIT.                                                
                                                                                
            ADD 1 TO WS-F740-READ.                                              
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-PROCESS-RTN.                                                        
      *-------------------------------------------------------------            
           MOVE F740-KEY-FLD TO K740-KEY-FLD.                                   
                                                                                
           READ BP13K740.                                                       
           EVALUATE K740-STATUS                                                 
           WHEN 00                                                              
                IF K740-CDE-SUCC-NT-ZONE NOT = SPACES AND LOW-VALUES            
                   MOVE 'MCP'    TO  K740-CDE-ALLOC-SCH                         
                   PERFORM 4000-REWRITE-BP13K740 THRU 4000-EXIT                 
                END-IF                                                          
           WHEN 23                                                              
                ADD   1           TO WS-K740-NOT-FND                            
                DISPLAY 'BP13K740 NOT FOUND KEY : ' K740-KEY-FLD                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K740 - ' K740-STATUS                 
           END-EVALUATE.                                                        
                                                                                
           PERFORM 1500-READ-F740           THRU 1500-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-REWRITE-BP13K740.                                                   
      *-------------------------------------------------------------            
           REWRITE BP13K740-REC.                                                
                                                                                
           IF K740-STATUS = 00                                                  
              ADD 1    TO WS-K740-UPD                                           
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING K740, STATUS ' K740-STATUS               
                      ', KEY IS  '    K740-KEY-FLD                              
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           CLOSE      BP13F740                                                  
                      BP13K740.                                                 
                                                                                
           IF K740-STATUS NOT = ZEROES                                          
              DISPLAY 'ERROR CLOSING BP13K740 : ' K740-STATUS                   
           END-IF.                                                              
                                                                                
           DISPLAY ' RUN DATE : ' WS-DATE.                                      
           DISPLAY '============================================'.              
           DISPLAY ' NO. OF BP13F740 RECORDS READ ' WS-F740-READ.               
           DISPLAY ' NO. OF BP13K740 NOT FOUND    ' WS-K740-NOT-FND.            
           DISPLAY ' NO. OF BP13K740 UPDATED      ' WS-K740-UPD.                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
