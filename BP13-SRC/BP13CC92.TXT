       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC92.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  04/10/2007.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  PRINT LETTER ON MORTGAGE LOAN STATEMENT     *           
      *                 OF ACCOUNT FOR YEAR END                     *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD63                                *           
      *                 2.  BP13K800 - SOC APPLICATION MASTER       *           
      *                 3.  BP13K820 - SOC OCCUPIER FILE            *           
      *                 4.  BP13K830 - SOC CORR ADDRESS FILE        *           
      *                 5.  BP13KD00                                *           
      *                 6.  BP13KD61                                *           
      *                 7.  SY02F001                                *           
      *                                                             *           
      * OUTPUT       :  1.  BP13PC92 - STATEMENT OF ACCT            *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---  -------- ------------------------------------ *           
      * BP133171 ED1  20071004 NEW PROGRAM                          *           
      * BP133091 LMS1 20071215 EXPANSION OF BP13KD00 FROM 1200 TO   *           
      *                        1600.                                *           
      * BP133489 LSB1 20090122 CHG CASH TO PR, PR-L                 *           
      * BP135161 IMC1 20140122 FIX OPENING BALANCE MISSING CR       *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13FD63  ASSIGN       TO BP13FD63.                           
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
                                                                                
           SELECT BP13KD00  ASSIGN       TO BP13KD00                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD00-NUM-REGN                       
                            FILE STATUS  IS WS-KD00-STATUS.                     
                                                                                
           SELECT BP13KD61  ASSIGN       TO BP13KD61                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD61-KEY-FLD                        
                            FILE STATUS  IS WS-KD61-STATUS.                     
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS WS-K830-STATUS.                     
                                                                                
           SELECT BP13PC92  ASSIGN       TO BP13PC92.                           
           SELECT BP13LC92  ASSIGN       TO BP13LC92.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD63                                                            
            RECORDING MODE  IS    F                                             
            LABEL RECORDS   ARE   STANDARD                                      
            BLOCK  CONTAINS    0  RECORDS                                       
            RECORD CONTAINS  150 CHARACTERS.                                    
       COPY BP13FD63.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD61                                                            
            RECORD CONTAINS 250  CHARACTERS.                                    
       COPY BP13KD61.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13PC92                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   PC92-PRTREC                 PIC X(132).                             
                                                                                
       FD   BP13LC92                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   LC92-PRTREC                 PIC X(132).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-KD00-STATUS           PIC 99     VALUE 00.                    
           05  WS-KD61-STATUS           PIC 99     VALUE 00.                    
           05  WS-K800-STATUS           PIC 99     VALUE 00.                    
           05  WS-K820-STATUS           PIC 99     VALUE 00.                    
           05  WS-K830-STATUS           PIC 99     VALUE 00.                    
           05  WS-EOF-FD63              PIC X      VALUE 'N'.                   
           05  WS-KD61-EOF              PIC X      VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-FD63-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-K800-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-PIDB-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-LETTER-PRINTED        PIC 9(6)   VALUE ZEROS.                 
           05  WS-LC92-WRT-CTR          PIC 9(6)   VALUE ZEROES.                
           05  WS-SNO                   PIC 9(5)   VALUE ZEROES.                
           05  WS-PAGE-CTR              PIC 9(4)   VALUE ZEROES.                
           05  WS-PAGE-LTR              PIC 9(4)   VALUE ZEROES.                
           05  WS-LINE-CTR              PIC 9(2)   VALUE ZEROES.                
           05  WS-WRITE-CTR             PIC 9(2)   VALUE ZEROES.                
                                                                                
       01  WS-LETTER-TYPE               PIC X.                                  
       01  WS-AMT-LOAN                  PIC S9(8)V99 VALUE ZEROS.               
       01  WS-TOTAL-AMT                 PIC S9(8)V99 VALUE ZEROS.               
                                                                                
       01  WS-CURR-KEY.                                                         
           05 WS-CURR-REGN              PIC X(8) VALUE SPACES.                  
           05 WS-CURR-YEAR              PIC X(4) VALUE SPACES.                  
                                                                                
       01  WS-PREV-KEY.                                                         
           05 WS-PREV-REGN              PIC X(8) VALUE SPACES.                  
           05 WS-PRINT-YEAR             PIC X(4) VALUE SPACES.                  
                                                                                
       01  WS-PRT-FIRST-DTE.                                                    
           05  WS-PRT-YEAR                  PIC X(4) VALUE SPACES.              
           05  FILLER                       PIC X(4) VALUE '0101'.              
                                                                                
       01  WS-DATE                      PIC X(8).                               
       01  WS-TEMP-DATE                 PIC X(8).                               
       01  WS-TEMP-DATE-R REDEFINES WS-TEMP-DATE.                               
           05 WS-CCYY                   PIC 9(4).                               
           05 WS-MM                     PIC 9(2).                               
           05 WS-DD                     PIC 9(2).                               
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                                
       01  WS-NAME                      PIC X(66)  VALUE SPACES.                
       01  WS-MAXLINE                   PIC 99     VALUE 58.                    
       01  WS-MAXLINE-LTR               PIC 99     VALUE 36.                    
       01  WS-CURRENT-DATE              PIC 9(08)  VALUE ZEROES.                
       01  WS-TMP-DATE                  PIC 9(07)  VALUE ZEROES.                
                                                                                
      *-------------------------------------------------------------            
      * FORMAT LETTER                                                           
      *-------------------------------------------------------------            
       01  PC92-LINE01A.                                                        
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC92-HDB-REF         PIC X(14) VALUE SPACES.                     
                                                                                
       01  PC92-LINE01.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC92-OUR-REF         PIC X(8)  VALUE SPACES.                     
                                                                                
       01  PC92-LINE02.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC92-DATE-HDR        PIC X(10) VALUE SPACES.                     
                                                                                
       01  PC92-LINE03A.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-NAME1           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC92-LINE03B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-NAME2           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC92-LINE03C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-NAME3           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC92-LINE03D.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-NAME4           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC92-LINE04A-ADD.                                                    
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-BLK-UNIT        PIC X(28) VALUE SPACES.                     
                                                                                
       01  PC92-LINE04A.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-BLK             PIC X(16) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  PC92-UNIT            PIC X(11) VALUE SPACES.                     
                                                                                
       01  PC92-LINE04B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-ADDR-L2         PIC X(65) VALUE SPACES.                     
                                                                                
       01  PC92-LINE04C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-ADDR-L3         PIC X(20) VALUE SPACES.                     
                                                                                
       01  PC92-LINE05.                                                         
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PC92-BAR-CODE        PIC X(09) VALUE SPACES.                     
                                                                                
       01  PC92-LINE06.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC92-FLAT-ADDRESS    PIC X(100) VALUE SPACES.                    
                                                                                
       01  PC92-LINE06A.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  FILLER               PIC X(44) VALUE                             
               'MORTGAGE LOAN STATEMENT OF ACCOUNT FOR YEAR '.                  
           05  FILLER               PIC X(13) VALUE                             
               'ENDED 31 DEC '.                                                 
           05  PC92-YEAR            PIC X(04) VALUE SPACES.                     
                                                                                
       01  PC92-LINE07.                                                         
           05  FILLER               PIC X(15) VALUE SPACES.                     
           05  PC92-DTE             PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(15) VALUE SPACES.                     
           05  PC92-DESC            PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(16) VALUE SPACES.                     
           05  PC92-AMOUNT          PIC ZZ,ZZZ,ZZ9.99 VALUE ZEROS.              
           05  PC92-CDE-CRDR        PIC X(02) VALUE SPACES.                     
                                                                                
       01  PC92-LINE08.                                                         
           05  FILLER               PIC X(28) VALUE SPACES.                     
           05  PC92-DTE-EXPIRY      PIC X(09) VALUE SPACES.                     
           05  FILLER               PIC X(46) VALUE SPACES.                     
           05  PC92-PAGE            PIC ZZ9.                                    
                                                                                
       01  PC92-LINER1.                                                         
           05  FILLER               PIC X(61) VALUE SPACES.                     
           05  FILLER               PIC X(13) VALUE ALL '-'.                    
                                                                                
       01  PC92-LINER2.                                                         
           05  FILLER               PIC X(61) VALUE SPACES.                     
           05  FILLER               PIC X(13) VALUE ALL '='.                    
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * CONTROL REPORT                                                          
      *-------------------------------------------------------------            
       01  LC92-HEADER01.                                                       
           05  FILLER              PIC X(08)     VALUE 'BP13LC92'.              
           05  FILLER              PIC X(09)     VALUE '     HDB3'.             
           05  FILLER              PIC X(31)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE                          
               'HOUSING & DEVELOPMENT BOARD'.                                   
           05  FILLER              PIC X(23)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC92-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC92-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC92-HEADER02.                                                       
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(49)     VALUE                          
               'REPORT ON MORTGAGE LOAN STATEMENT OF ACCOUNT '.                 
                                                                                
       01  LC92-HEADER03.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE ' HDB REF-NO'.           
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'TOTAL($)'.              
                                                                                
       01 LC92-DETAIL1.                                                         
          05 LC92-DTL-SERIAL      PIC ZZZZ9      VALUE ZEROES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC92-REGNO           PIC X(08)      VALUE SPACES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC92-HDBREF          PIC X(14)      VALUE SPACES.                  
          05 FILLER               PIC X(02)      VALUE SPACES.                  
          05 LC92-TOTAL-AMT       PIC ZZ,ZZZ,ZZ9.99.                            
          05 LC92-CRDR            PIC X(02)      VALUE SPACES.                  
                                                                                
       01  WS-BLANK-LINE          PIC X(132)     VALUE SPACES.                  
       01  WS-DASH-LINE           PIC X(132)     VALUE ALL '-'.                 
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13FD63        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-RECORD       THRU  3000-EXIT                   
                    UNTIL WS-EOF-FD63 =  'Y'.                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
            OPEN INPUT  BP13FD63                                                
                        BP13KD00                                                
                        BP13KD61                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP13K830                                                
                        SY02F001                                                
                 OUTPUT BP13PC92                                                
                        BP13LC92.                                               
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KD00-STATUS ' WS-KD00-STATUS           
           END-IF.                                                              
                                                                                
           IF WS-KD61-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KD61-STATUS ' WS-KD61-STATUS           
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K800-STATUS ' WS-K800-STATUS           
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K820-STATUS ' WS-K820-STATUS           
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K830-STATUS ' WS-K830-STATUS           
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-LAST-DAY-PREV TO WS-DATE.                              
           MOVE F001-DTE-LAST-DAY-PREV(1:4) TO WS-PRINT-YEAR PC92-YEAR.         
           MOVE F001-DTE-LAST-DAY-PREV(1:4) TO WS-PRT-YEAR.                     
      * GET CURRENT-DATE + 1                                                    
           MOVE F001-DTE-CURRENT            TO WS-CURRENT-DATE                  
           COMPUTE WS-TMP-DATE =                                                
             FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE)                          
           COMPUTE WS-TMP-DATE = WS-TMP-DATE + 1                                
           COMPUTE WS-CURRENT-DATE =                                            
             FUNCTION DATE-OF-INTEGER(WS-TMP-DATE)                              
           STRING  WS-CURRENT-DATE(7:2) '/'                                     
                   WS-CURRENT-DATE(5:2) '/'                                     
                   WS-CURRENT-DATE(1:4)                                         
                   DELIMITED BY SIZE INTO PC92-DATE-HDR                         
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD63.                                                      
      *-------------------------------------------------------------            
           READ BP13FD63                                                        
                AT END MOVE 'Y' TO WS-EOF-FD63                                  
                GO TO  2000-EXIT.                                               
                                                                                
           MOVE FD63-NUM-REGN         TO WS-CURR-REGN.                          
           MOVE FD63-DTE-PROCESS(1:4) TO WS-CURR-YEAR.                          
           ADD  1             TO WS-FD63-READ.                                  
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           MOVE FD63-NUM-REGN TO WS-PREV-REGN.                                  
                                                                                
           PERFORM 3100-INITIALIZE-FIELDS THRU 3100-EXIT.                       
           PERFORM 3200-READ-BP13K800     THRU 3200-EXIT.                       
                                                                                
           IF WS-K800-STATUS = 00 AND                                           
              WS-PREV-KEY    = WS-CURR-KEY AND                                  
              K800-NUM-LOAN-TAG = 'H'                                           
                                                                                
              MOVE 0  TO WS-TOTAL-AMT                                           
              MOVE 0  TO WS-PAGE-LTR                                            
              PERFORM 3300-READ-BP13KD00 THRU 3300-EXIT                         
              PERFORM 5000-GET-NAMES     THRU 5000-EXIT                         
              PERFORM 6000-GET-ADDRESS   THRU 6000-EXIT                         
              PERFORM 7000-GET-FLAT-ADDR THRU 7000-EXIT                         
              PERFORM 8000-PRINT-LETTER  THRU 8000-EXIT                         
                      UNTIL WS-PREV-KEY NOT = WS-CURR-KEY                       
                      OR    WS-EOF-FD63     = 'Y'                               
                                                                                
              ADD 1 TO WS-LETTER-PRINTED                                        
                                                                                
              PERFORM 8200-MOVE-REPORT-DTLS THRU 8200-EXIT                      
              PERFORM 8300-PRINT-REPORT     THRU 8300-EXIT                      
                                                                                
           ELSE                                                                 
             PERFORM 2000-READ-BP13FD63     THRU 2000-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-INITIALIZE-FIELDS.                                                  
      *-------------------------------------------------------------            
           MOVE SPACES TO  PC92-LINE01A PC92-LINE01                             
                           PC92-LINE03A PC92-LINE03B                            
                           PC92-LINE03C PC92-LINE03D                            
                           PC92-LINE04A PC92-LINE04B PC92-LINE04C               
                           PC92-LINE05  PC92-LINE06  PC92-LINE07.               
                                                                                
           MOVE ZEROS  TO  PC92-AMOUNT.                                         
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3300-READ-BP13KD00.                                                      
      *-------------------------------------------------------------            
           MOVE FD63-NUM-REGN TO KD00-NUM-REGN.                                 
                                                                                
           READ BP13KD00.                                                       
           EVALUATE WS-KD00-STATUS                                              
           WHEN 00                                                              
                IF KD00-DTE-LOAN-EXPIRY NOT = SPACES AND LOW-VALUES             
                                                     AND ZEROS                  
                   MOVE KD00-DTE-LOAN-EXPIRY TO WS-TEMP-DATE                    
                   STRING WS-MONTH(WS-MM) ' ' WS-CCYY                           
                   DELIMITED BY SIZE INTO PC92-DTE-EXPIRY                       
                ELSE                                                            
                   MOVE SPACES         TO PC92-DTE-EXPIRY                       
                END-IF                                                          
           WHEN 23                                                              
                DISPLAY 'BP13KD00 REC NOTFND, KEY ' KD00-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'BP13KD00 READ ERR, STATUS ' WS-KD00-STATUS             
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3200-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE FD63-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD  1  TO   WS-K800-READ                                       
           WHEN 23                                                              
                DISPLAY 'BP13K800 REC NOTFND, KEY ' K800-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'BP13K800 READ ERR, STATUS ' WS-K800-STATUS             
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-STARTBR-KD61.                                                       
      *-------------------------------------------------------------            
           MOVE 'N'              TO WS-KD61-EOF.                                
           MOVE SPACES           TO KD61-KEY-FLD.                               
           MOVE FD63-NUM-REGN    TO KD61-NUM-REGN.                              
           MOVE WS-PRT-FIRST-DTE TO KD61-DTE-TRANS.                             
                                                                                
           START BP13KD61 KEY IS >= KD61-KEY-FLD.                               
           EVALUATE WS-KD61-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
           WHEN 22                                                              
             PERFORM 4100-READNXT-KD61 THRU 4100-EXIT                           
                              UNTIL WS-KD61-EOF = 'Y'                           
           WHEN 10                                                              
           WHEN 23                                                              
             CONTINUE                                                           
           WHEN OTHER                                                           
             DISPLAY 'BP13KD61 STARTBR ERROR : ' WS-KD61-STATUS                 
                     ',KEY IS '  KD61-KEY-FLD                                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-READNXT-KD61.                                                       
      *-------------------------------------------------------------            
           READ BP13KD61 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-KD61-EOF.                                 
                                                                                
           EVALUATE WS-KD61-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
           WHEN 22                                                              
             IF KD61-AMT-OPEN-LOAN-BAL NOT NUMERIC                              
                MOVE 0 TO KD61-AMT-OPEN-LOAN-BAL                                
             END-IF                                                             
                                                                                
             IF KD61-NUM-REGN  = FD63-NUM-REGN AND                              
                KD61-DTE-TRANS = WS-PRT-FIRST-DTE AND                           
                KD61-AMT-OPEN-LOAN-BAL NOT = 0                                  
                                                                                
                MOVE '01/01'                TO PC92-DTE                         
                MOVE 'BAL B/F '             TO PC92-DESC                        
                MOVE KD61-AMT-OPEN-LOAN-BAL TO PC92-AMOUNT                      
                IF KD61-AMT-OPEN-LOAN-BAL < ZEROES                              
                   MOVE 'CR'                   TO PC92-CDE-CRDR                 
                ELSE                                                            
                   MOVE '  '                   TO PC92-CDE-CRDR                 
                END-IF                                                          
                                                                                
                ADD  KD61-AMT-OPEN-LOAN-BAL TO WS-TOTAL-AMT                     
                                                                                
                WRITE PC92-PRTREC FROM PC92-LINE07                              
                ADD 1 TO WS-WRITE-CTR                                           
                MOVE 'Y' TO WS-KD61-EOF                                         
             END-IF                                                             
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
             MOVE 'Y' TO WS-KD61-EOF                                            
           WHEN OTHER                                                           
             MOVE 'Y' TO WS-KD61-EOF                                            
             DISPLAY 'BP13KD61 READNXT ERROR : ' WS-KD61-STATUS                 
                     ',KEY IS '  KD61-KEY-FLD                                   
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       5000-GET-NAMES.                                                          
      *-------------------------------------------------------------            
           MOVE    K800-NUM-NRIC1          TO   K820-NUM-NRIC.                  
           PERFORM 5500-READ-BP13K820      THRU 5500-EXIT.                      
           MOVE    WS-NAME                 TO   PC92-NAME1.                     
                                                                                
           IF K800-NUM-NRIC2 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC2       TO  K820-NUM-NRIC                   
              PERFORM  5500-READ-BP13K820   THRU 5500-EXIT                      
              MOVE     WS-NAME              TO  PC92-NAME2                      
           ELSE                                                                 
              MOVE     SPACES               TO  PC92-NAME2                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC3       TO  K820-NUM-NRIC                   
              PERFORM  5500-READ-BP13K820   THRU 5500-EXIT                      
              MOVE     WS-NAME              TO  PC92-NAME3                      
           ELSE                                                                 
              MOVE     SPACES               TO  PC92-NAME3                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT =  SPACES AND  LOW-VALUES                      
              MOVE     K800-NUM-NRIC4       TO  K820-NUM-NRIC                   
              PERFORM  5500-READ-BP13K820   THRU 5500-EXIT                      
              MOVE     WS-NAME              TO  PC92-NAME4                      
           ELSE                                                                 
              MOVE     SPACES               TO  PC92-NAME4                      
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5500-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES        TO WS-NAME.                                       
           MOVE K800-NUM-REGN TO K820-NUM-REGN.                                 
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE  K820-NME-OCCP           TO    WS-NAME                       
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'RECORD NOTFND IN BP13K820 = ' WS-K820-STATUS             
                      ', KEY-FLD = ' K820-KEY-FLD                               
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820, STAT ' WS-K820-STATUS            
                      ', KEY-FLD = ' K820-KEY-FLD                               
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6000-GET-ADDRESS.                                                        
      *-------------------------------------------------------------            
           MOVE FD63-NUM-REGN     TO    K830-NUM-REGN.                          
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
           WHEN 00                                                              
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                                                                                
                 IF K830-NUM-BLK(1:4) = SPACES OR LOW-VALUES                    
                    MOVE K830-NUM-BLK(5:12) TO PC92-BLK                         
                 ELSE                                                           
                    MOVE K830-NUM-BLK       TO PC92-BLK                         
                 END-IF                                                         
                                                                                
                 MOVE K830-NUM-FLAT        TO PC92-UNIT                         
                 MOVE K830-NME-STREET-BLDG TO PC92-ADDR-L2                      
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC92-ADDR-L3                      
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC92-ADDR-L3                   
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC92-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                                                                                
                 IF K830-NUM-MAIL-BLK(1:4) = SPACES OR LOW-VALUES               
                    MOVE K830-NUM-MAIL-BLK(5:12) TO PC92-BLK                    
                 ELSE                                                           
                    MOVE K830-NUM-MAIL-BLK       TO PC92-BLK                    
                 END-IF                                                         
                                                                                
                 MOVE K830-NUM-MAIL-FLAT       TO PC92-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC92-ADDR-L2                  
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC92-ADDR-L3                  
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC92-ADDR-L3                  
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC92-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           WHEN 23                                                              
              MOVE SPACES TO PC92-BLK                                           
                             PC92-UNIT                                          
                             PC92-ADDR-L2                                       
                             PC92-ADDR-L3                                       
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ', KEY ' K830-KEY-FLD                                     
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K830, STATUS ' WS-K830-STATUS          
                      ', KEY ' K830-KEY-FLD                                     
           END-EVALUATE.                                                        
                                                                                
           MOVE SPACES TO PC92-LINE04A-ADD.                                     
           STRING PC92-BLK  DELIMITED BY SIZE                                   
             ' '  PC92-UNIT DELIMITED BY SIZE                                   
             INTO PC92-BLK-UNIT                                                 
           END-STRING.                                                          
                                                                                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6500-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7000-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                ADD 1 TO WS-PIDB-READ                                           
                STRING 'BLK '  COMM8-NUM-BLK  DELIMITED BY SIZE                 
                       ' #'  COMM8-NUM-LEVEL   '-'                              
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STFULL       DELIMITED BY '   '                
                       INTO PC92-FLAT-ADDRESS                                   
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD63-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD63-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
           MOVE  0                       TO WS-WRITE-CTR.                       
           MOVE  FD63-NUM-REGN           TO PC92-OUR-REF.                       
                                                                                
           STRING K800-NUM-SCH-ACC(1:4) '-'                                     
                  K800-NUM-SCH-ACC(5:4) '-'                                     
                  K800-NUM-SCH-ACC(9:1) '-'                                     
                  K800-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO PC92-HDB-REF.                          
                                                                                
           MOVE SPACES TO PC92-PRTREC.                                          
                                                                                
           PERFORM 8050-PRT-COMMON-DTLS THRU 8050-EXIT.                         
                                                                                
           IF WS-PAGE-LTR = 1                                                   
              PERFORM 4000-STARTBR-KD61 THRU 4000-EXIT                          
           END-IF.                                                              
                                                                                
           PERFORM 8100-PRINT-DETAILS THRU 8100-EXIT                            
                   UNTIL WS-PREV-KEY NOT = WS-CURR-KEY                          
                      OR WS-WRITE-CTR    = WS-MAXLINE-LTR                       
                      OR WS-EOF-FD63     = 'Y'.                                 
                                                                                
           IF WS-PREV-KEY NOT = WS-CURR-KEY OR                                  
              WS-EOF-FD63     = 'Y'                                             
              MOVE '31/12'           TO PC92-DTE                                
              MOVE 'BAL C/F '        TO PC92-DESC                               
                                                                                
              MOVE WS-TOTAL-AMT      TO PC92-AMOUNT                             
              IF WS-TOTAL-AMT < 0                                               
                 MOVE 'CR' TO PC92-CDE-CRDR                                     
              ELSE                                                              
                 MOVE '  ' TO PC92-CDE-CRDR                                     
              END-IF                                                            
                                                                                
              IF (WS-WRITE-CTR = WS-MAXLINE-LTR) OR                             
                 (WS-WRITE-CTR >  33           )                                
                                                                                
                 IF WS-WRITE-CTR NOT = WS-MAXLINE-LTR                           
                    COMPUTE WS-WRITE-CTR = 36 - WS-WRITE-CTR                    
                    WRITE PC92-PRTREC FROM WS-BLANK-LINE                        
                                     AFTER WS-WRITE-CTR                         
                 END-IF                                                         
                                                                                
                 WRITE PC92-PRTREC   FROM PC92-LINE08 AFTER 2                   
                 WRITE PC92-PRTREC   FROM WS-BLANK-LINE AFTER PAGE              
                 PERFORM 8050-PRT-COMMON-DTLS THRU 8050-EXIT                    
                 WRITE PC92-PRTREC   FROM PC92-LINER1 AFTER 2                   
                 WRITE PC92-PRTREC   FROM PC92-LINE07                           
                 WRITE PC92-PRTREC   FROM PC92-LINER2                           
                 WRITE PC92-PRTREC   FROM PC92-LINE08 AFTER 34                  
                                                                                
              ELSE                                                              
                 WRITE PC92-PRTREC   FROM PC92-LINER1 AFTER 2                   
                 WRITE PC92-PRTREC   FROM PC92-LINE07                           
                 WRITE PC92-PRTREC   FROM PC92-LINER2                           
                                                                                
                 IF WS-WRITE-CTR NOT = 33                                       
                    ADD 4 TO WS-WRITE-CTR                                       
                    COMPUTE WS-WRITE-CTR = 36 - WS-WRITE-CTR                    
                    WRITE PC92-PRTREC FROM WS-BLANK-LINE                        
                                      AFTER WS-WRITE-CTR                        
                    WRITE PC92-PRTREC FROM PC92-LINE08 AFTER 2                  
                 ELSE                                                           
                    WRITE PC92-PRTREC FROM PC92-LINE08                          
                 END-IF                                                         
                                                                                
              END-IF                                                            
                                                                                
           ELSE                                                                 
                 WRITE PC92-PRTREC FROM PC92-LINE08 AFTER 2                     
           END-IF.                                                              
                                                                                
                                                                                
      * PRINT OVERLEAF LETTER                                                   
      * ----------------------------                                            
           WRITE PC92-PRTREC   FROM WS-BLANK-LINE AFTER PAGE.                   
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8050-PRT-COMMON-DTLS.                                                    
      *-------------------------------------------------------------            
           WRITE PC92-PRTREC        FROM PC92-LINE01A AFTER PAGE.               
           WRITE PC92-PRTREC        FROM PC92-LINE01.                           
           WRITE PC92-PRTREC        FROM PC92-LINE02.                           
           WRITE PC92-PRTREC        FROM PC92-LINE03A AFTER 4.                  
           WRITE PC92-PRTREC        FROM PC92-LINE03B.                          
           WRITE PC92-PRTREC        FROM PC92-LINE03C.                          
           WRITE PC92-PRTREC        FROM PC92-LINE03D.                          
           WRITE PC92-PRTREC        FROM PC92-LINE04A-ADD.                      
           WRITE PC92-PRTREC        FROM PC92-LINE04B.                          
           WRITE PC92-PRTREC        FROM PC92-LINE04C.                          
           WRITE PC92-PRTREC        FROM PC92-LINE05 AFTER 2.                   
           WRITE PC92-PRTREC        FROM PC92-LINE06 AFTER 2.                   
           WRITE PC92-PRTREC        FROM PC92-LINE06A.                          
           WRITE PC92-PRTREC        FROM WS-BLANK-LINE AFTER 3.                 
                                                                                
           ADD   1                       TO WS-PAGE-LTR.                        
           MOVE  WS-PAGE-LTR             TO PC92-PAGE.                          
                                                                                
       8050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8100-PRINT-DETAILS.                                                      
      *-------------------------------------------------------------            
           IF FD63-TXT-DESC = 'INST    ' OR  'ARRADJ  '                         
              PERFORM 2000-READ-BP13FD63  THRU 2000-EXIT                        
              GO TO 8100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE  SPACES                  TO PC92-LINE07.                        
           MOVE  0                       TO WS-AMT-LOAN.                        
           INITIALIZE                       PC92-LINE07.                        
           MOVE  FD63-TXT-DESC           TO PC92-DESC.                          
                                                                                
           IF FD63-AMT-LOAN IS NOT NUMERIC                                      
              MOVE 0 TO FD63-AMT-LOAN                                           
           END-IF.                                                              
           MOVE  FD63-AMT-LOAN           TO PC92-AMOUNT.                        
                                                                                
           IF (FD63-TXT-DESC(1:4) = 'CPF ' OR 'PR  ' OR 'GIRO') OR              
              (FD63-TXT-DESC(1:6) = 'CPF-L ' OR 'PR-L  ' OR                     
                                    'CPF-A ' OR 'GIRO-L')                       
              MOVE 'CR' TO PC92-CDE-CRDR                                        
              COMPUTE WS-AMT-LOAN   = 0 - FD63-AMT-LOAN                         
                                                                                
           ELSE                                                                 
           IF FD63-TXT-DESC = 'INT-R   ' OR                                     
              FD63-TXT-DESC(1:2) = 'JV'                                         
              IF FD63-CDE-CR-DR = 'C'                                           
                 MOVE 'CR' TO PC92-CDE-CRDR                                     
                 COMPUTE WS-AMT-LOAN   = 0 - FD63-AMT-LOAN                      
              ELSE                                                              
                 MOVE '  ' TO PC92-CDE-CRDR                                     
                 MOVE  FD63-AMT-LOAN        TO WS-AMT-LOAN                      
              END-IF                                                            
                                                                                
           ELSE                                                                 
              MOVE '  ' TO PC92-CDE-CRDR                                        
              MOVE  FD63-AMT-LOAN        TO WS-AMT-LOAN                         
           END-IF.                                                              
                                                                                
           ADD   WS-AMT-LOAN             TO WS-TOTAL-AMT.                       
                                                                                
           IF FD63-DTE-PROCESS NOT = SPACES AND LOW-VALUES AND ZEROS            
              STRING FD63-DD-PROCESS '/' FD63-MM-PROCESS                        
              DELIMITED BY SIZE INTO PC92-DTE                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES TO PC92-PRTREC.                                          
           WRITE PC92-PRTREC FROM PC92-LINE07.                                  
                                                                                
           ADD 1 TO WS-WRITE-CTR.                                               
                                                                                
           PERFORM 2000-READ-BP13FD63       THRU 2000-EXIT.                     
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8200-MOVE-REPORT-DTLS.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES               TO LC92-DETAIL1.                           
           MOVE PC92-DATE-HDR        TO LC92-DATE-HDR.                          
           MOVE PC92-OUR-REF         TO LC92-REGNO.                             
           MOVE PC92-HDB-REF         TO LC92-HDBREF.                            
           MOVE WS-TOTAL-AMT         TO LC92-TOTAL-AMT.                         
                                                                                
           IF WS-TOTAL-AMT < 0                                                  
              MOVE 'CR' TO LC92-CRDR                                            
           ELSE                                                                 
              MOVE '  ' TO LC92-CRDR                                            
           END-IF.                                                              
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8300-PRINT-REPORT.                                                       
      *--------------------------------------------------------------           
           ADD   1                   TO  WS-SNO.                                
           MOVE  WS-SNO              TO  LC92-DTL-SERIAL.                       
                                                                                
           IF WS-LINE-CTR > WS-MAXLINE OR                                       
              WS-SNO       = 1                                                  
              ADD            1  TO WS-PAGE-CTR                                  
              MOVE ZEROES       TO LC92-PAGE-HDR                                
              MOVE WS-PAGE-CTR  TO LC92-PAGE-HDR                                
              PERFORM 8400-PRINT-HEADING THRU 8400-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC92-PRTREC FROM LC92-DETAIL1.                                 
           ADD   1              TO WS-LINE-CTR.                                 
           ADD   1              TO WS-LC92-WRT-CTR.                             
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       8400-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE LC92-PRTREC FROM LC92-HEADER01 AFTER PAGE.                    
            WRITE LC92-PRTREC FROM LC92-HEADER02.                               
            WRITE LC92-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC92-PRTREC FROM LC92-HEADER03.                               
            WRITE LC92-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  5 TO WS-LINE-CTR.                                             
                                                                                
       8400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' -- BP13CC92 CONTROL TOTAL -------'.                        
           DISPLAY ' '.                                                         
           DISPLAY ' TOTAL READ FROM FD63 : ' WS-FD63-READ.                     
           DISPLAY ' TOTAL LETTER PRINTED : ' WS-LETTER-PRINTED.                
           DISPLAY ' TOTAL REPORT PRINTED : ' WS-LC92-WRT-CTR.                  
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FD63                                                       
                 BP13KD00                                                       
                 BP13KD61                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13PC92                                                       
                 BP13LC92.                                                      
                                                                                
           IF WS-KD00-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13KD00, STAT ' WS-KD00-STATUS.           
                                                                                
           IF WS-KD61-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13KD61  STAT ' WS-KD61-STATUS.           
                                                                                
           IF WS-K800-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K800, STAT ' WS-K800-STATUS.           
                                                                                
           IF WS-K820-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K820, STAT ' WS-K820-STATUS.           
                                                                                
           IF WS-K830-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K830, STAT ' WS-K830-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
