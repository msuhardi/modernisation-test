       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C927.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  06/11/2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES:                                                *            
      * USING REGN-NO, MATCH BP13F800 WITH BP13F820, CRAETE FILE   *            
      * TO ICDB DATABASE.                                          *            
      * OUTPUT BP13FW01 APPLICATION INFOR FROM BP13K800, CANCELLED *            
      * CASE DURING THE MONTH, AND KEY-ISSUED DURING THE MONTH.    *            
      * (FOUND IN BP13K893).                                       *            
      * OUTPUT THE CORRESPONDING OCCUPIER RECORDS FROM BP13K820    *            
      * AND BP13K895.                                              *            
      *                                                            *            
      * GET LOAN INFOR FROM SALES ORDER FILE BP13K410              *            
      *                                      BP13KD00              *            
      *                                      BP13KD45              *            
      * GET PACD DATE  FROM FIS BLOCK   FILE BM06K100              *            
      * GET KEY-ISUUE-DATE  FIS UNIT    FILE BM06K110              *            
      * DECODE DESCRIPTION              FILE BP13K060              *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F800 - APPLICATOIN MASTER            *            
      *                2. BP13F820 - OCCUPIER FILE                 *            
      *                3. SY02F001 - DATE FILE                     *            
      *                4. BP13K060 - CODE TRANSCRIPTION            *            
      *                5. BM06K110 - FLAT INVENTORY UNIT           *            
      *                6. BM06K100 - FLAT INVENTORY BLOCK          *            
      *                7. BP13K410 - MONTHLY SALES ORDER           *            
      *                8. BP13KD00 - SOC - DBSS MASTER FILE        *            
      *                9. BP13KD45 - PROGRESS PAYMENT-ACCUM (VSAM) *            
      *  OUTPUT FILE:  1. BP13FW01 - APPLICATION REC TO ICDB       *            
      *                2. BP13FW02 - OCCUPIER REC   TO ICDB        *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133145  MSD1  20071106  NEW PGM                          *            
      * BP133091  LMS1  20071215  EXPANSION OF BP13KD00 FROM 1200  *            
      *                           TO 1600.                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT BP13F820  ASSIGN        TO BP13F820.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13K060  ASSIGN        TO BP13K060                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K060-KEY-FLD                       
                            FILE STATUS   IS BP13K060-STATUS.                   
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS BM06K100-STATUS.                   
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K410-KEY-FLD                       
                            FILE STATUS   IS BP13K410-STATUS.                   
                                                                                
           SELECT BP13KD00  ASSIGN        TO BP13KD00                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS KD00-KEY-FLD                       
                            FILE STATUS   IS BP13KD00-STATUS.                   
                                                                                
           SELECT BP13KD45  ASSIGN        TO BP13KD45                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS KD45-KEY-FLD                       
                            FILE STATUS   IS BP13KD45-STATUS.                   
                                                                                
           SELECT BP13FW01  ASSIGN        TO BP13FW01.                          
           SELECT BP13FW02  ASSIGN        TO BP13FW02.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F820                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F820.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500  CHARACTERS.                                   
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K410.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD45                                                             
           RECORD CONTAINS 1550 CHARACTERS.                                     
       COPY BP13KD45.                                                           
                                                                                
       FD   BP13FW01                                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW01.                                                           
                                                                                
       FD   BP13FW02                                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW02.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F820-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F820-NOTFD        PIC 9(08) VALUE ZEROES.                       
          05 WS-FW01-WRITE        PIC 9(08) VALUE ZEROES.                       
          05 WS-FW02-WRITE        PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13K060-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BP13K410-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BP13KD00-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BP13KD45-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BM06K110-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BM06K100-STATUS      PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-AMT-OPTION-CPF    PIC 9(06)V99 VALUE ZEROS.                     
          05 WS-AMT-OPTION-CASH   PIC 9(05)V99  VALUE ZEROS.                    
          05 WS-AMT-CD-CASH       PIC S9(07)V99  VALUE ZEROS.                   
          05 WS-AMT-CD-CPF        PIC S9(08)V99  VALUE ZEROS.                   
          05 WS-AMT-ICP-CASH      PIC S9(07)V99  VALUE ZEROS.                   
          05 WS-AMT-ICP-CPF       PIC S9(08)V99  VALUE ZEROS.                   
                                                                                
       01  WS-KD45-EOF            PIC X(1)     VALUE 'N'.                       
                                                                                
       01  WS-NUM-TOWN-TABLE.                                                   
           05 FILLER              PIC X(05) VALUE 'AMKNE'.                      
           05 FILLER              PIC X(05) VALUE 'BB W '.                      
           05 FILLER              PIC X(05) VALUE 'BD E '.                      
           05 FILLER              PIC X(05) VALUE 'BH C '.                      
           05 FILLER              PIC X(05) VALUE 'BM C '.                      
           05 FILLER              PIC X(05) VALUE 'BP W '.                      
           05 FILLER              PIC X(05) VALUE 'BT C '.                      
           05 FILLER              PIC X(05) VALUE 'CCKW '.                      
           05 FILLER              PIC X(05) VALUE 'CL W '.                      
           05 FILLER              PIC X(05) VALUE 'CT C '.                      
           05 FILLER              PIC X(05) VALUE 'GL C '.                      
           05 FILLER              PIC X(05) VALUE 'HG NE'.                      
           05 FILLER              PIC X(05) VALUE 'JE W '.                      
           05 FILLER              PIC X(05) VALUE 'JW W '.                      
           05 FILLER              PIC X(05) VALUE 'KWNC '.                      
           05 FILLER              PIC X(05) VALUE 'MP C '.                      
           05 FILLER              PIC X(05) VALUE 'PG NE'.                      
           05 FILLER              PIC X(05) VALUE 'PRCE '.                      
           05 FILLER              PIC X(05) VALUE 'QT C '.                      
           05 FILLER              PIC X(05) VALUE 'SB N '.                      
           05 FILLER              PIC X(05) VALUE 'SK NE'.                      
           05 FILLER              PIC X(05) VALUE 'SGNNE'.                      
           05 FILLER              PIC X(05) VALUE 'TAPE '.                      
           05 FILLER              PIC X(05) VALUE 'TP C '.                      
           05 FILLER              PIC X(05) VALUE 'WL N '.                      
           05 FILLER              PIC X(05) VALUE 'YS N '.                      
                                                                                
       01  WS-REGION-TABLE REDEFINES WS-NUM-TOWN-TABLE.                         
           05 WS-REGION-TAB  OCCURS 26 TIMES INDEXED BY IDX-TOWN.               
              10 WS-TBL-NUM-TOWN   PIC X(3).                                    
              10 WS-TBL-RGN-TOWN   PIC X(2).                                    
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-READ-BP13F820  THRU 3000-EXIT.                          
           PERFORM 4000-PROCESS-DATA   THRU 4000-EXIT                           
              UNTIL F800-NUM-REGN = HIGH-VALUES                                 
                AND F820-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F820                                                 
                       SY02F001                                                 
                       BP13K060                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K410                                                 
                       BP13KD00                                                 
                       BP13KD45                                                 
               OUTPUT  BP13FW01                                                 
                       BP13FW02.                                                
                                                                                
           IF BP13K060-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K060 : ' BP13K060-STATUS             
              MOVE     BP13K060-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE     BM06K110-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BM06K100 : ' BM06K100-STATUS             
              MOVE     BM06K100-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K410-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K410 : ' BP13K410-STATUS             
              MOVE     BP13K410-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13KD00-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE     BP13KD00-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13KD45-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KD45 : ' BP13KD45-STATUS             
              MOVE     BP13KD45-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F800-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F800-READ                                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F820.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F820                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F820-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F820-READ                                     
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROS TO WS-AMT-ICP-CPF.                                        
           MOVE ZEROS TO WS-AMT-ICP-CASH.                                       
           MOVE ZEROS TO WS-AMT-CD-CPF.                                         
           MOVE ZEROS TO WS-AMT-CD-CASH.                                        
                                                                                
           EVALUATE TRUE                                                        
              WHEN F800-NUM-REGN  =  F820-NUM-REGN                              
                 PERFORM 4100-CHECK-SOC-STATUS THRU 4100-EXIT                   
                 PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                   
                                                                                
              WHEN F800-NUM-REGN  <  F820-NUM-REGN                              
                 ADD 1    TO WS-F820-NOTFD                                      
                 PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                   
                                                                                
              WHEN F820-NUM-REGN  <  F800-NUM-REGN                              
                 PERFORM 3000-READ-BP13F820    THRU 3000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-CHECK-SOC-STATUS.                                                   
      *-------------------------------------------------------------            
           IF F800-NUM-STATUS  = 'C'                                            
              IF F800-DTE-CANCEL(1:6) = F001-DTE-CURRENT(1:6)                   
                 PERFORM  5000-WRITE-BP13FW01 THRU 5000-EXIT                    
                 PERFORM  6000-WRITE-BP13FW02 THRU 6000-EXIT                    
                    UNTIL (F820-NUM-REGN NOT = F800-NUM-REGN)  OR               
                          (F820-NUM-REGN = HIGH-VALUES)                         
              ELSE                                                              
                 PERFORM  3000-READ-BP13F820  THRU 3000-EXIT                    
                    UNTIL (F820-NUM-REGN NOT = F800-NUM-REGN)  OR               
                          (F820-NUM-REGN = HIGH-VALUES)                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM  5000-WRITE-BP13FW01    THRU 5000-EXIT                    
              PERFORM  6000-WRITE-BP13FW02    THRU 6000-EXIT                    
                 UNTIL (F820-NUM-REGN NOT = F800-NUM-REGN)  OR                  
                       (F820-NUM-REGN = HIGH-VALUES)                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-BP13FW01.                                                     
      *-------------------------------------------------------------            
            MOVE SPACES                      TO BP13FW01-REC.                   
            INITIALIZE                          BP13FW01-REC.                   
                                                                                
            MOVE F800-NUM-REGN               TO FW01-NUM-REGN.                  
            MOVE F800-NUM-SCH-ACC            TO FW01-NUM-SCH-ACCNT.             
            MOVE F800-DTE-REGN               TO FW01-DTE-REGN.                  
            MOVE F800-NUM-EXEC-COND-TAG      TO FW01-NUM-EXEC.                  
            MOVE F800-QTY-JT-BALLOT          TO FW01-QTY-JT-B.                  
            MOVE F800-NUM-PPO                TO FW01-NUM-PPO.                   
            MOVE F800-NUM-RESIDENT-SCHEME    TO FW01-RES-SCH.                   
            MOVE F800-NUM-CUR-REGN           TO FW01-CUR-REGN.                  
                                                                                
            IF F800-AMT-CD IS NUMERIC                                           
               MOVE F800-AMT-CD              TO FW01-AMT-CD.                    
                                                                                
            IF F800-AMT-CD-CASH IS NUMERIC                                      
               MOVE F800-AMT-CD-CASH         TO FW01-AMT-CD-CASH.               
                                                                                
            IF  F800-AMT-CD-CPF1 IS NUMERIC                                     
            AND F800-AMT-CD-CPF2 IS NUMERIC                                     
            AND F800-AMT-CD-CPF3 IS NUMERIC                                     
            AND F800-AMT-CD-CPF4 IS NUMERIC                                     
                COMPUTE FW01-AMT-CD-CPF     =  F800-AMT-CD-CPF1                 
                                            +  F800-AMT-CD-CPF2                 
                                            +  F800-AMT-CD-CPF3                 
                                            +  F800-AMT-CD-CPF4.                
                                                                                
            MOVE F800-DTE-RAISE-AO           TO FW01-DTE-AO.                    
            MOVE F800-DTE-BALLOT             TO FW01-DTE-BALOT.                 
            MOVE F800-NUM-HOUSEHOLD          TO FW01-NUM-HOUSEHOLD.             
            MOVE F800-NUM-CAT                TO FW01-NUM-CAT.                   
            MOVE F800-NUM-JT-BALLOT-REGN     TO FW01-NUM-JB-REGN.               
                                                                                
            IF F800-AMT-HSE-INCOME IS NUMERIC                                   
               MOVE F800-AMT-HSE-INCOME      TO FW01-AMT-HSE-INCOME.            
                                                                                
            MOVE F800-NUM-JT-APPL            TO FW01-NUM-JT-APPL.               
            MOVE F800-DTE-CANCEL             TO FW01-DTE-CANCEL.                
            MOVE F800-NUM-SHORTLIST-PA-HHTY  TO FW01-SHORTLIST.                 
            MOVE F800-TME-QUEUE              TO FW01-TME-QUEUE.                 
            MOVE F800-DTE-UPDATE             TO FW01-DTE-UPDATE.                
            MOVE F800-NUM-REOPT              TO FW01-NUM-REOPT.                 
            MOVE F800-NUM-NRIC1              TO FW01-NRIC1.                     
            MOVE F800-NUM-NRIC2              TO FW01-NRIC2.                     
            MOVE F800-NUM-NRIC3              TO FW01-NRIC3.                     
            MOVE F800-NUM-NRIC4              TO FW01-NRIC4.                     
            MOVE F800-NUM-ALLO-CAT           TO FW01-NUM-ALLO-CAT.              
                                                                                
            IF F800-AMT-CA-INCOME IS NUMERIC                                    
               MOVE F800-AMT-CA-INCOME          TO FW01-AMT-CA-INCOME.          
                                                                                
            MOVE F800-NUM-LOAN-TAG           TO FW01-NUM-LOAN-TAG.              
            MOVE F800-NUM-AHG                TO FW01-NUM-AHG.                   
                                                                                
            PERFORM 5200-MOVE-DECODED      THRU 5200-EXIT.                      
                                                                                
            IF F800-NUM-SCH-ACC NOT  =  SPACES AND LOW-VALUES                   
               MOVE  F800-NUM-SCH-ACC        TO K110-KEY-FLD                    
               MOVE  SPACES                  TO K110-DTE-KEY-ISSUED             
               PERFORM  5300-READ-BM06K110 THRU 5300-EXIT.                      
                                                                                
            IF F800-NUM-ALLO-CAT = 'DBS'                                        
                PERFORM  5500-READ-BP13KD00 THRU 5500-EXIT                      
            ELSE                                                                
                IF K110-DTE-KEY-ISSUED NOT = SPACES AND ZEROS                   
                    PERFORM  5400-READ-BP13K410 THRU 5400-EXIT                  
                END-IF                                                          
            END-IF.                                                             
                                                                                
            WRITE BP13FW01-REC.                                                 
            ADD 1 TO WS-FW01-WRITE.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-READ-K060.                                                          
      *-------------------------------------------------------------            
                                                                                
            READ BP13K060.                                                      
                                                                                
            EVALUATE BP13K060-STATUS                                            
              WHEN '00'                                                         
                 CONTINUE                                                       
              WHEN '23'                                                         
                 DISPLAY 'BP13K060 RECORD NOT FOUND : '                         
                           K060-SERIAL-NO ' ' K060-CODE                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K060 : ' BP13K060-STATUS            
                 MOVE     BP13K060-STATUS       TO RETURN-CODE                  
                 PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5200-MOVE-DECODED.                                                       
      *-------------------------------------------------------------            
            IF F800-NUM-STATUS NOT = SPACES AND LOW-VALUES AND ALL '0'          
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '01'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-STATUS            TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE F800-NUM-STATUS            TO FW01-NUM-STAT                 
               MOVE K060-DESC                  TO FW01-NME-STAT                 
            END-IF.                                                             
                                                                                
      **************** TRANSFORMATION ON FLAT TYPE  ****************            
            MOVE F800-NUM-FLAT-TYPE         TO FW01-NUM-FLAT.                   
      **************** TRANSFORMATION ON FLAT TYPE  ****************            
                                                                                
            IF F800-NUM-ROOM NOT = SPACES AND LOW-VALUES AND ALL '0'            
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '34'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-ROOM              TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE K060-DESC                  TO FW01-NME-FLAT                 
            END-IF.                                                             
                                                                                
            IF F800-NUM-ZONE NOT = SPACES AND LOW-VALUES AND ALL '0'            
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '45'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-ZONE              TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE F800-NUM-ZONE              TO FW01-NUM-ZONE                 
               MOVE K060-DESC                  TO FW01-NME-ZONE                 
            END-IF.                                                             
                                                                                
            IF F800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                     
               MOVE SPACES               TO BP13K060-REC                        
               MOVE '02'                 TO K060-SERIAL-NO                      
               MOVE F800-NUM-NEW-TOWN    TO K060-CODE                           
               PERFORM 5100-READ-K060  THRU 5100-EXIT                           
               MOVE F800-NUM-NEW-TOWN    TO FW01-NUM-TOWN                       
               MOVE K060-DESC            TO FW01-NME-TOWN                       
                                                                                
               SET  IDX-TOWN TO 1                                               
               SEARCH WS-REGION-TAB                                             
                  WHEN WS-TBL-NUM-TOWN (IDX-TOWN) = F800-NUM-NEW-TOWN           
                       MOVE WS-TBL-RGN-TOWN (IDX-TOWN)                          
                                         TO FW01-NUM-REGION                     
            END-IF.                                                             
                                                                                
            IF F800-NUM-IDP-SCHEME NOT = SPACES AND LOW-VALUES                  
                                                AND ALL '0'                     
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '04'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-IDP-SCHEME        TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE F800-NUM-IDP-SCHEME        TO FW01-NUM-IDPS                 
               MOVE K060-DESC                  TO FW01-NME-IDPS                 
            END-IF.                                                             
                                                                                
            IF F800-NUM-ALLO-SCHEME NOT = SPACES AND LOW-VALUES                 
                                                 AND ALL '0'                    
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '05'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-ALLO-SCHEME       TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE F800-NUM-ALLO-SCHEME       TO FW01-NUM-ALLS                 
               MOVE K060-DESC                  TO FW01-NME-ALCT                 
            END-IF.                                                             
                                                                                
            IF F800-NUM-JT-BALLOT-MTF NOT = SPACES AND LOW-VALUES               
                                                   AND ALL '0'                  
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '08'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-JT-BALLOT-MTF     TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE F800-NUM-JT-BALLOT-MTF     TO FW01-NUM-JT-MTF               
               MOVE K060-DESC                  TO FW01-NME-JT-MTF               
            END-IF.                                                             
                                                                                
                                                                                
            IF F800-NUM-CANCEL-REASON NOT = SPACES AND LOW-VALUES               
                                                   AND ALL '0'                  
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '36'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-CANCEL-REASON     TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE F800-NUM-CANCEL-REASON     TO FW01-NUM-CANCEL               
               MOVE K060-DESC                  TO FW01-NME-CANCEL               
            END-IF.                                                             
                                                                                
            IF F800-NUM-ELIG-SCHEME NOT = SPACES AND LOW-VALUES                 
                                                 AND ALL '0'                    
               MOVE SPACES                     TO BP13K060-REC                  
               MOVE '06'                       TO K060-SERIAL-NO                
               MOVE F800-NUM-ELIG-SCHEME       TO K060-CODE                     
               PERFORM 5100-READ-K060        THRU 5100-EXIT                     
               MOVE F800-NUM-ELIG-SCHEME       TO FW01-NUM-ELIG                 
               MOVE K060-DESC                  TO FW01-NME-ELIG                 
            END-IF.                                                             
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5300-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
                PERFORM 5310-READ-BM06K100  THRU 5310-EXIT                      
             WHEN 23                                                            
                DISPLAY 'RECORD NOT FOUND IN BM06K110 : ' K110-KEY-FLD          
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING - BM06K110 : ' BM06K110-STATUS           
                        ', K110-KEY-FLD : ' K110-KEY-FLD                        
                MOVE   BM06K110-STATUS        TO RETURN-CODE                    
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5310-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
           MOVE K110-ESTATE                  TO K100-ESTATE.                    
           MOVE K110-NEIGHBOURHOOD           TO K100-NEIGHBOURHOOD.             
           MOVE K110-CONTRACT-NO             TO K100-CONTRACT-NO.               
           MOVE K110-BLK-NO                  TO K100-BLK-NO.                    
           READ BM06K100.                                                       
                                                                                
           EVALUATE BM06K100-STATUS                                             
             WHEN 00                                                            
                MOVE   K100-DTE-PACD         TO FW01-DTE-PACD                   
             WHEN 23                                                            
                DISPLAY 'RECORD NOT FOUND IN BM06K100 : ' K110-AIX1             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING - BM06K100 : ' BM06K100-STATUS           
                      ', K100-KEY-FLD : ' K100-KEY-FLD                          
                MOVE    BM06K100-STATUS      TO RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5400-READ-BP13K410.                                                      
      *-------------------------------------------------------------            
           INITIALIZE  BP13K410-REC.                                            
           MOVE  F800-NUM-REGN           TO K410-KEY-FLD.                       
                                                                                
           READ  BP13K410.                                                      
                                                                                
           EVALUATE BP13K410-STATUS                                             
             WHEN 00                                                            
                PERFORM 5410-K410-MOVE-COMPUTE   THRU 5410-EXIT                 
             WHEN 23                                                            
                DISPLAY 'BP13K410 RECORD NOT FOUND  = ' K410-KEY-FLD            
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING - BP13K410 : ' BP13K410-STATUS           
                        ', K410-KEY-FLD : ' K410-KEY-FLD                        
                MOVE     BP13K410-STATUS      TO RETURN-CODE                    
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5410-K410-MOVE-COMPUTE.                                                  
      *-------------------------------------------------------------            
            IF K410-AMT-LOAN IS NUMERIC                                         
               MOVE K410-AMT-LOAN          TO FW01-AMT-LOAN.                    
                                                                                
            IF K410-AMT-ICP-CASH IS NUMERIC                                     
               MOVE K410-AMT-ICP-CASH      TO FW01-AMT-ICP-CASH.                
                                                                                
            IF K410-AMT-MTHLY-CASH IS NUMERIC                                   
               MOVE K410-AMT-MTHLY-CASH    TO FW01-AMT-MTHLY-CASH.              
                                                                                
            IF K410-NUM-REPAY-PERIOD IS NUMERIC                                 
               MOVE K410-NUM-REPAY-PERIOD  TO FW01-NUM-REPAY-PERIOD.            
                                                                                
            IF  K410-AMT-ICP-CPF1 IS NUMERIC                                    
            AND K410-AMT-ICP-CPF2 IS NUMERIC                                    
            AND K410-AMT-ICP-CPF3 IS NUMERIC                                    
            AND K410-AMT-ICP-CPF4 IS NUMERIC                                    
                COMPUTE FW01-AMT-ICP-CPF =  K410-AMT-ICP-CPF1                   
                                            +  K410-AMT-ICP-CPF2                
                                            +  K410-AMT-ICP-CPF3                
                                            +  K410-AMT-ICP-CPF4.               
                                                                                
                                                                                
            IF  K410-AMT-MTHLY-CPF1 IS NUMERIC                                  
            AND K410-AMT-MTHLY-CPF2 IS NUMERIC                                  
            AND K410-AMT-MTHLY-CPF3 IS NUMERIC                                  
            AND K410-AMT-MTHLY-CPF4 IS NUMERIC                                  
                COMPUTE FW01-AMT-MTHLY-CPF =  K410-AMT-MTHLY-CPF1               
                                               +  K410-AMT-MTHLY-CPF2           
                                               +  K410-AMT-MTHLY-CPF3           
                                               +  K410-AMT-MTHLY-CPF4.          
                                                                                
       5410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5500-READ-BP13KD00.                                                      
      *-------------------------------------------------------------            
           INITIALIZE  BP13KD00-REC.                                            
           MOVE  F800-NUM-REGN           TO KD00-KEY-FLD.                       
                                                                                
           READ  BP13KD00.                                                      
                                                                                
           EVALUATE BP13KD00-STATUS                                             
             WHEN 00                                                            
                PERFORM 5510-KD00-MOVE-COMPUTE THRU 5510-EXIT                   
             WHEN 23                                                            
                DISPLAY 'BP13KD00 RECORD NOT FOUND  = ' KD00-KEY-FLD            
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS           
                        ', KD00-KEY-FLD : ' KD00-KEY-FLD                        
                MOVE     BP13KD00-STATUS      TO RETURN-CODE                    
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5510-KD00-MOVE-COMPUTE.                                                  
      *-------------------------------------------------------------            
                                                                                
            IF  KD00-AMT-INSTAL-CPF1 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-INSTAL-CPF1                               
            END-IF.                                                             
            IF  KD00-AMT-INSTAL-CPF2 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-INSTAL-CPF2                               
            END-IF.                                                             
            IF  KD00-AMT-INSTAL-CPF3 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-INSTAL-CPF3                               
            END-IF.                                                             
            IF  KD00-AMT-INSTAL-CPF4 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-INSTAL-CPF4                               
            END-IF.                                                             
            IF  KD00-AMT-INSTAL-CASH IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-INSTAL-CASH                               
            END-IF.                                                             
            IF  KD00-AMT-LOAN-GRANTED IS NOT NUMERIC                            
               MOVE ZEROS TO KD00-AMT-LOAN-GRANTED                              
            END-IF.                                                             
                                                                                
            MOVE KD00-AMT-LOAN-GRANTED  TO FW01-AMT-LOAN.                       
                                                                                
            MOVE KD00-AMT-INSTAL-CASH   TO FW01-AMT-MTHLY-CASH.                 
                                                                                
            MOVE KD00-NUM-LOAN-REPYMT-PERIOD                                    
                                           TO FW01-NUM-REPAY-PERIOD.            
                                                                                
            COMPUTE FW01-AMT-MTHLY-CPF =  KD00-AMT-INSTAL-CPF1                  
                                           +  KD00-AMT-INSTAL-CPF2              
                                           +  KD00-AMT-INSTAL-CPF3              
                                           +  KD00-AMT-INSTAL-CPF4.             
                                                                                
            PERFORM 5520-STARTBR-BP13KD45   THRU 5520-EXIT.                     
                                                                                
            MOVE WS-AMT-ICP-CASH           TO FW01-AMT-ICP-CASH.                
            MOVE WS-AMT-ICP-CPF            TO FW01-AMT-ICP-CPF.                 
            MOVE WS-AMT-CD-CASH            TO FW01-AMT-CD-CASH.                 
            MOVE WS-AMT-CD-CPF             TO FW01-AMT-CD-CPF.                  
            COMPUTE FW01-AMT-CD = FW01-AMT-CD-CASH +                            
                                  FW01-AMT-CD-CPF.                              
                                                                                
       5510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5520-STARTBR-BP13KD45.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13KD45-REC.                     
           INITIALIZE                         BP13KD45-REC.                     
                                                                                
           MOVE 'N'                        TO WS-KD45-EOF.                      
                                                                                
           MOVE F800-NUM-REGN              TO KD45-NUM-REGN-HIST.               
           MOVE SPACES                     TO KD45-DTE-UPDATE-HIST              
                                              KD45-TME-UPDATE-HIST.             
                                                                                
           START BP13KD45 KEY >= KD45-KEY-FLD.                                  
                                                                                
           EVALUATE BP13KD45-STATUS                                             
           WHEN 00                                                              
              PERFORM 5530-READNXT-BP13KD45 THRU 5530-EXIT                      
                 UNTIL WS-KD45-EOF = 'Y'                                        
                       OR KD45-NUM-REGN-HIST NOT = F800-NUM-REGN                
           WHEN 10                                                              
           WHEN 23                                                              
              DISPLAY 'BP13KD45 RECORD NOT FOUND  = ' KD45-KEY-FLD              
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTBR - BP13KD45 : ' BP13KD45-STATUS             
                      ', KD45-KEY-FLD : ' KD45-KEY-FLD                          
              MOVE     BP13KD45-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5530-READNXT-BP13KD45.                                                   
      *-------------------------------------------------------------            
           READ BP13KD45 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13KD45-STATUS                                             
           WHEN 00                                                              
              IF KD45-NUM-REGN-HIST = F800-NUM-REGN                             
                 PERFORM 5540-COMPUTE-AMOUNT  THRU 5540-EXIT                    
              ELSE                                                              
                 MOVE    'Y'                     TO   WS-KD45-EOF               
              END-IF                                                            
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE    'Y'                        TO   WS-KD45-EOF               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING - BP13KD45 : ' BP13KD45-STATUS             
                      ', KD45-KEY-FLD : ' KD45-KEY-FLD                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5530-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5540-COMPUTE-AMOUNT.                                                     
      *-------------------------------------------------------------            
                                                                                
            IF  KD00-AMT-OPTION-CPF1 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-OPTION-CPF1                               
            END-IF.                                                             
            IF  KD00-AMT-OPTION-CPF2 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-OPTION-CPF2                               
            END-IF.                                                             
            IF  KD00-AMT-OPTION-CPF3 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-OPTION-CPF3                               
            END-IF.                                                             
            IF  KD00-AMT-OPTION-CPF4 IS NOT NUMERIC                             
               MOVE ZEROS TO KD00-AMT-OPTION-CPF4                               
            END-IF.                                                             
                                                                                
            IF KD45-AMT-PP-CPF1 IS NOT NUMERIC                                  
               MOVE ZEROS TO KD45-AMT-PP-CPF1                                   
            END-IF.                                                             
            IF KD45-AMT-PP-CPF2 IS NOT NUMERIC                                  
               MOVE ZEROS TO KD45-AMT-PP-CPF2                                   
            END-IF.                                                             
            IF KD45-AMT-PP-CPF3 IS NOT NUMERIC                                  
               MOVE ZEROS TO KD45-AMT-PP-CPF3                                   
            END-IF.                                                             
            IF KD45-AMT-PP-CPF4 IS NOT NUMERIC                                  
               MOVE ZEROS TO KD45-AMT-PP-CPF4                                   
            END-IF.                                                             
            IF KD45-AMT-PP-CASH IS NOT NUMERIC                                  
               MOVE ZEROS TO KD45-AMT-PP-CASH                                   
            END-IF.                                                             
                                                                                
            COMPUTE WS-AMT-OPTION-CPF = KD00-AMT-OPTION-CPF1                    
                                      + KD00-AMT-OPTION-CPF2                    
                                      + KD00-AMT-OPTION-CPF3                    
                                      + KD00-AMT-OPTION-CPF4.                   
                                                                                
            COMPUTE WS-AMT-OPTION-CASH =                                        
                   KD00-AMT-OPTION - WS-AMT-OPTION-CPF.                         
                                                                                
                                                                                
            IF KD45-NUM-PP-SCHDL = '01'                                         
               COMPUTE WS-AMT-CD-CASH = WS-AMT-OPTION-CASH  +                   
                                        KD45-AMT-PP-CASH                        
                                                                                
               COMPUTE WS-AMT-CD-CPF = WS-AMT-OPTION-CPF   +                    
                                      (KD45-AMT-PP-CPF1 +                       
                                       KD45-AMT-PP-CPF2 +                       
                                       KD45-AMT-PP-CPF3 +                       
                                       KD45-AMT-PP-CPF4)                        
            ELSE                                                                
              IF KD45-NUM-PP-SCHDL > 01                                         
                 COMPUTE WS-AMT-ICP-CASH = WS-AMT-ICP-CASH +                    
                                           KD45-AMT-PP-CASH                     
                                                                                
                 COMPUTE WS-AMT-ICP-CPF = (WS-AMT-ICP-CPF   +                   
                                           KD45-AMT-PP-CPF1 +                   
                                           KD45-AMT-PP-CPF2 +                   
                                           KD45-AMT-PP-CPF3 +                   
                                           KD45-AMT-PP-CPF4)                    
                END-IF                                                          
              END-IF.                                                           
                                                                                
       5540-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-BP13FW02.                                                     
      *-------------------------------------------------------------            
           IF F820-NUM-REGN = F800-NUM-REGN                                     
              MOVE SPACES                    TO BP13FW02-REC                    
              INITIALIZE                        BP13FW02-REC                    
                                                                                
              MOVE  F820-NUM-REGN            TO FW02-NUM-REGN                   
              MOVE  F820-NUM-NRIC            TO FW02-NUM-NRIC                   
              MOVE  F820-NME-OCCP            TO FW02-NME-OCCP                   
              MOVE  F820-NUM-ETHNIC          TO FW02-NUM-ETHNIC                 
              MOVE  F820-NUM-SEX             TO FW02-NUM-SEX                    
              MOVE  F820-NUM-MARITAL-STATUS  TO FW02-NUM-STATUS                 
              MOVE  F820-DTE-BIRTH           TO FW02-DTE-BIRTH                  
              MOVE  F820-AMT-INCOME          TO FW02-AMT-INCOME                 
              MOVE  F820-AMT-INCOME-TP       TO FW02-AMT-INCOME-TP              
              MOVE  F820-NUM-LESSEE-CHECK    TO FW02-NUM-LESSEE-CHECK           
              MOVE  F820-NUM-EXIST-NT        TO FW02-EXIST-NT                   
              MOVE  F820-NUM-EXIST-FT        TO FW02-EXIST-FT                   
              MOVE  F820-NUM-EXIST-ROLE      TO FW02-EXIST-ROLE                 
                                                                                
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '35'                    TO K060-SERIAL-NO                    
              MOVE F820-NUM-RELATIONSHIP   TO K060-CODE                         
              PERFORM 5100-READ-K060     THRU 5100-EXIT                         
              MOVE F820-NUM-RELATIONSHIP   TO FW02-NUM-RELATIONSHIP             
              MOVE K060-DESC               TO FW02-NME-RELATIONSHIP             
                                                                                
              MOVE F820-NUM-CITIZENSHIP   TO FW02-NUM-CITIZEN                   
                                                                                
              MOVE SPACES                    TO BP13K060-REC                    
              MOVE '25'                      TO K060-SERIAL-NO                  
              MOVE FW02-NUM-CITIZEN          TO K060-CODE                       
              PERFORM 5100-READ-K060       THRU 5100-EXIT                       
              MOVE K060-DESC                 TO FW02-NME-CITIZEN                
                                                                                
              WRITE BP13FW02-REC                                                
              ADD 1 TO WS-FW02-WRITE                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F820    THRU 3000-EXIT.                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C927 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F800  : ' WS-F800-READ.            
           DISPLAY 'NO OF RECS READ FROM BP13F820  : ' WS-F820-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13FW01 : ' WS-FW01-WRITE.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13FW02 : ' WS-FW02-WRITE.           
           DISPLAY 'NO OF RECS F820 NOTFD          : ' WS-F820-NOTFD.           
                                                                                
           CLOSE BP13F800                                                       
                 BP13F820                                                       
                 SY02F001                                                       
                 BP13K060                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13K410                                                       
                 BP13KD00                                                       
                 BP13KD45                                                       
                 BP13FW01                                                       
                 BP13FW02.                                                      
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K060 : ' BP13K060-STATUS             
              MOVE BP13K060-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BM06K100 : ' BM06K100-STATUS             
              MOVE BM06K100-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K410-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K410 : ' BP13K410-STATUS             
              MOVE BP13K410-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD00-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD45-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KD45 : ' BP13KD45-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
