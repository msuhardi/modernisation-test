       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C883.                                                 
       AUTHOR.        SIA.                                                      
       DATE-WRITTEN.  13 06 1996.                                               
                                                                                
      ******************************************************************        
      * APPLICATION : SYSTEM OF COMMITMENT (BP13)                               
      *                                                                         
      *-----------------------------------------------------------------        
      * OBJECTIVES :                                                            
      *       TO GET THE RANDOM OF THE SMALLEST REF. NO. AND ASSIGN THE         
      * SAME RANDOM-NO FOR THE WHOLE JB-LINK.                                   
      *                                                                         
      * ================================================================        
      * FILE DESCRIPTION                                                        
      *                                                                         
      *  INPUT                                                                  
      *  1. BP13F740                                                            
      *  2. BP13K740                                                            
      *                                                                         
      *  OUTPUT                                                                 
      *  1. BP13F565                                                            
      *                                                                         
      *=================================================================        
      * MODIFICATIONS                                                           
      *                                                                         
      * CHGE     DATE     BY   DESCRIPTION                                      
      * -------- -------- ---- -----------                                      
      * BP130652 22021999 NOR  EXPAND F740 & K740 RECL FROM 300 TO 500          
      * BP132826 02032006 YCH2 EXPAND REFNO FROM 6 TO 8                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT  BP13F740  ASSIGN        TO BP13F740.                         
                                                                                
           SELECT  BP13F565  ASSIGN        TO BP13F565.                         
                                                                                
           SELECT  BP13K740  ASSIGN        TO BP13K740                          
                             ACCESS        IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K740-KEY-FLD                      
                             ALTERNATE KEY IS K740-NUM-REGN                     
                             FILE STATUS   IS WS-K740-STATUS.                   
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORDING MODE IS F                                                 
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F565                                                            
            RECORDING MODE IS F                                                 
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 550 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F565.                                                           
                                                                                
       FD   BP13K740.                                                           
       COPY BP13K740.                                                           
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  FILE-STATUS.                                                         
           05 WS-K740-STATUS        PIC 9(02) VALUE 00.                         
                                                                                
      *------------------------------------------------------------*            
      *    TEMPORARY WORKING STORAGE                               *            
      *------------------------------------------------------------*            
                                                                                
       01  WS-SYS-DATE.                                                         
           05  WS-DTE-YY            PIC 9999  VALUE ZEROES.                     
           05  WS-DTE-MM            PIC 99    VALUE ZEROES.                     
           05  WS-DTE-DD            PIC 99    VALUE ZEROES.                     
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05 WS-F740-READ          PIC 9(06)   VALUE ZEROES.                   
           05 WS-K740-READ          PIC 9(06)   VALUE ZEROES.                   
           05 WS-F565-WRITTEN       PIC 9(06)   VALUE ZEROES.                   
           05 WS-LINK-CTR           PIC 9(06)   VALUE ZEROES.                   
                                                                                
       01  WS-F740-EOF              PIC X(1)    VALUE 'N'.                      
       01  WS-K740-FOUND            PIC X(1)    VALUE SPACE.                    
      *01  WS-REF                   PIC X(6)    VALUE SPACE.                    
       01  WS-REF                   PIC X(8)    VALUE SPACE.                    
       01  WS-RANDOM                PIC X(28)   VALUE SPACE.                    
       01  WS-LINK-MAX              PIC 9(01)   VALUE 9.                        
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *--------------*                                                          
       0000-MAIN.                                                               
      *--------------*                                                          
                                                                                
            PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                 
            PERFORM 2000-MAIN-ROUTINE           THRU 2000-EXIT.                 
            PERFORM 9000-CLOSE-FILES            THRU 9000-EXIT.                 
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       1000-OPEN-FILES.                                                         
      *-----------------*                                                       
                                                                                
           OPEN INPUT BP13F740                                                  
                      BP13K740                                                  
               OUTPUT BP13F565.                                                 
                                                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K740 ' WS-K740-STATUS                  
              MOVE     WS-K740-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           ACCEPT WS-SYS-DATE           FROM DATE YYYYMMDD.                     
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------*                                                  
       2000-MAIN-ROUTINE.                                                       
      *----------------------*                                                  
                                                                                
            PERFORM 2100-READ-F740           THRU 2100-EXIT.                    
            PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                     
                    UNTIL WS-F740-EOF = 'Y'.                                    
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       2100-READ-F740.                                                          
      *-----------------*                                                       
                                                                                
            READ BP13F740                                                       
                 AT END     MOVE 'Y'        TO  WS-F740-EOF                     
                 GO TO 2100-EXIT                                                
            END-READ.                                                           
                                                                                
            ADD  1                  TO  WS-F740-READ.                           
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------*                                                
       3000-PROCESS-RECORDS.                                                    
      *------------------------*                                                
                                                                                
            MOVE    'Y'               TO  WS-K740-FOUND                         
            MOVE    0                 TO  WS-LINK-CTR                           
            MOVE    F740-NUM-REF      TO  WS-REF                                
            MOVE    F740-NUM-RANDOM   TO  WS-RANDOM                             
                                                                                
            IF F740-CDE-JB NOT = '8'                                            
               MOVE  F740-NUM-RANDOM  TO   WS-RANDOM                            
            ELSE                                                                
               MOVE F740-NUM-JB-REF   TO   K740-NUM-REGN                        
               PERFORM 3200-READ-K740 THRU 3200-EXIT                            
               UNTIL K740-NUM-JB-REF  =    F740-NUM-REGN(1:6) OR                
                     WS-K740-FOUND    =   'N'                 OR                
                     WS-LINK-CTR      =    WS-LINK-MAX                          
            END-IF.                                                             
                                                                                
            IF WS-LINK-CTR = WS-LINK-MAX                                        
               DISPLAY 'BP13F740-RECORD NO LINK: ' F740-NUM-REGN                
               MOVE  SPACES           TO   WS-RANDOM.                           
                                                                                
            IF WS-K740-FOUND = 'Y'                                              
               DISPLAY 'WRITING ......' F740-NUM-JB-REF ' >' WS-REF             
               MOVE  BP13F740-REC     TO   BP13F565-REC                         
               MOVE  WS-RANDOM        TO   F565-NUM-RANDOM-JT-ALLOC             
               WRITE BP13F565-REC                                               
               ADD   1                TO   WS-F565-WRITTEN                      
            END-IF.                                                             
                                                                                
            PERFORM 2100-READ-F740         THRU 2100-EXIT.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       3200-READ-K740.                                                          
      *-----------------*                                                       
                                                                                
           IF K740-NUM-REGN = SPACES OR LOW-VALUES                              
                MOVE   WS-LINK-MAX          TO  WS-LINK-CTR                     
                GO                          TO  3200-EXIT.                      
                                                                                
           READ BP13K740                KEY IS  K740-NUM-REGN.                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN 00                                                              
                IF K740-NUM-REF < WS-REF                                        
                   MOVE K740-NUM-REF        TO  WS-REF                          
                   MOVE K740-NUM-RANDOM     TO  WS-RANDOM                       
                END-IF                                                          
                ADD  1                      TO  WS-K740-READ                    
                ADD  1                      TO  WS-LINK-CTR                     
                MOVE K740-NUM-JB-REF        TO  K740-NUM-REGN                   
                                                                                
           WHEN 23                                                              
                MOVE    'N'                 TO  WS-K740-FOUND                   
                DISPLAY 'BP13K740-RECORD NOT FOUND: ' K740-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING  BP13K740 : ' WS-K740-STATUS             
                DISPLAY 'K740 KEY FIELD          : ' K740-KEY-FLD               
                MOVE     WS-K740-STATUS         TO   RETURN-CODE                
                PERFORM  9000-CLOSE-FILES       THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------*                                                      
       9000-CLOSE-FILES.                                                        
      *------------------*                                                      
                                                                                
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13F565.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K740 ' WS-K740-STATUS                  
              MOVE     WS-K740-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
                                                                                
           DISPLAY '                                      '.                    
           DISPLAY ' *****  BP13C883 CONTROL TOTALS  *****'.                    
           DISPLAY ' DATE : ' WS-SYS-DATE                                       
           DISPLAY '                                      '.                    
           DISPLAY 'RECORDS READ    FROM F740  = ' WS-F740-READ                 
           DISPLAY 'RECORDS READ    FROM K740  = ' WS-K740-READ                 
           DISPLAY 'RECORDS WRITTEN TO   F565  = ' WS-F565-WRITTEN              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
