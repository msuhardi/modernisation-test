       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C191.                                                 
       AUTHOR.        BALAKRISHNAN S.                                           
      *DATE-WRITTEN.  28/06/1999.                                               
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO DELETE BP13F200 RECORDS BY ZONE/NT, FLAT-TYPE *                
      *       AND BALLOT-QUARTER.                              *                
      *                                                        *                
      *    INPUT  FILES : BP13F200                             *                
      *                   BP13F205                             *                
      *                   BP18K800                             *                
      *                                                        *                
      *    I/O   FILE   :                                      *                
      *                                                        *                
      *    OUTPUT FILES : BP18F800                             *                
      *                                                        *                
      *    LISTING      : NIL                                  *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * ------   --  ------   -------------------------------- *                
      * BP130671 BALA 19990708 - NEW PGM                       *                
      * BP130758 BALA 20000428 - READ BP13F200 UNTIL CHG AL-DTE*                
      * BP131018 LSB  20010515 - CATER FOR RED                 *                
      * BP132095 MTD  20020422 - EXPAND LENGTH OF BP13F200     *                
      *                          (FROM 100 TO 300)             *                
      * BP135533 ESA1 20141113 - TO CATER FOR ALLOC SCHEMES    *                
      *                           'REG' & 'RNG'                *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS BP13K800-STATUS.                     
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F205                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS  80 CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K800 RECORD CONTAINS 2000 CHARACTERS.                           
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   BP13F800-REC    PIC X(2000).                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K800-STATUS               PIC X(2) VALUE SPACES.             
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F200-CONTROL-FIELDS.                                          
               10  WS-F200-NUM-NT-ZONE        PIC X(3).                         
               10  WS-F200-DTE-ALLOC          PIC X(8).                         
           05  WS-F205-CONTROL-FIELDS.                                          
               10  WS-F205-NUM-NT-ZONE        PIC X(3).                         
               10  WS-F205-DTE-ALLOCN         PIC X(8).                         
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F200-READ-CTR               PIC 9(7)  VALUE ZEROS.            
           05  WS-F205-READ-CTR               PIC 9(7)  VALUE ZEROS.            
           05  WS-K800-READ-CTR               PIC 9(7)  VALUE ZEROS.            
           05  WS-F800-WRITE-CTR              PIC 9(7)  VALUE ZEROS.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-ZONE-CNT                    PIC 9(7)   VALUE ZEROS.           
           05  WS-MATCH-CNT                   PIC 9(7)   VALUE ZEROS.           
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-YY                  PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDT-DATE.                                                     
               10  WS-EDT-DD                  PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDT-MM                  PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDT-YY                  PIC 9(2).                         
                                                                                
       01  WS-OTHERS.                                                           
           10  WS-EDT-VAR                     PIC Z,ZZZ,ZZ9.                    
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F200         THRU 2000-EXIT.                       
           PERFORM 3000-READ-F205         THRU 3000-EXIT.                       
                                                                                
           PERFORM 4000-PROCESS-RECORDS   THRU 4000-EXIT                        
              UNTIL WS-F200-CONTROL-FIELDS = HIGH-VALUES AND                    
                    WS-F205-CONTROL-FIELDS = HIGH-VALUES.                       
                                                                                
           GO TO   9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13F205                                                 
                       BP13K800                                                 
                OUTPUT BP13F800.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY "OPEN BP13K800 FAIL STATUS " BP13K800-STATUS              
              MOVE BP13K800-STATUS    TO RETURN-CODE                            
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           ACCEPT WS-SYSTEM-DATE      FROM DATE.                                
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDT-DD.                             
           MOVE   WS-SYS-MM           TO WS-EDT-MM.                             
           MOVE   WS-SYS-YY           TO WS-EDT-YY.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       2000-READ-F200.                                                          
      *************************************************************             
                                                                                
           READ BP13F200                                                        
              AT END  MOVE HIGH-VALUES   TO WS-F200-CONTROL-FIELDS              
              GO TO   2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE F200-NUM-NT-ZONE         TO WS-F200-NUM-NT-ZONE.                
           MOVE F200-DTE-ALLOC           TO WS-F200-DTE-ALLOC.                  
                                                                                
           ADD   +1     TO WS-F200-READ-CTR.                                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-READ-F205.                                                          
      ****************************************************************          
                                                                                
           READ BP13F205                                                        
              AT END MOVE   HIGH-VALUES TO WS-F205-CONTROL-FIELDS               
                     GO TO  3000-EXIT                                           
           END-READ.                                                            
                                                                                
           MOVE F205-NUM-NT-ZONE         TO WS-F205-NUM-NT-ZONE.                
           MOVE F205-DTE-ALLOCN          TO WS-F205-DTE-ALLOCN.                 
                                                                                
           ADD   +1     TO   WS-F205-READ-CTR.                                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           IF (WS-F200-CONTROL-FIELDS = WS-F205-CONTROL-FIELDS)                 
              PERFORM 4100-PROCESS-MATCHED THRU 4100-EXIT                       
                      UNTIL                                                     
                       (WS-F200-CONTROL-FIELDS > WS-F205-CONTROL-FIELDS)        
              PERFORM 3000-READ-F205 THRU 3000-EXIT                             
           ELSE                                                                 
              IF (WS-F200-CONTROL-FIELDS < WS-F205-CONTROL-FIELDS)              
                 PERFORM 2000-READ-F200  THRU 2000-EXIT                         
              ELSE                                                              
                 PERFORM 3000-READ-F205  THRU 3000-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4100-PROCESS-MATCHED.                                                    
      *************************************************************             
                                                                                
           ADD 1 TO WS-MATCH-CNT.                                               
                                                                                
           IF F200-NUM-NT-ZONE = 'NE' OR 'NW' OR 'WW' OR 'PG'                   
              ADD 1 TO WS-ZONE-CNT                                              
           ELSE                                                                 
              MOVE SPACES                 TO BP13K800-MASTER                    
              MOVE F200-NUM-REGN          TO K800-NUM-REGN                      
              READ BP13K800                                                     
                                                                                
              IF BP13K800-STATUS = '00'                                         
                 ADD 1 TO WS-K800-READ-CTR                                      
                                                                                
                 IF K800-NUM-ALLO-SCHEME = 'REN' OR 'RED' OR                    
                                           'REG' OR 'RNG'                       
                    MOVE SPACES           TO BP13F800-REC                       
                    WRITE BP13F800-REC    FROM BP13K800-MASTER                  
                    ADD 1                 TO WS-F800-WRITE-CTR                  
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY "READ BP13K800 STATUS : " BP13K800-STATUS              
                         " KEY : " K800-NUM-REGN                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F200 THRU 2000-EXIT.                               
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F200                                                       
                 BP13F205                                                       
                 BP13K800                                                       
                 BP13F800.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '=========================================='.                
           DISPLAY ' PROGRAM : BP13C191    DATE : ' WS-EDT-DATE.                
           DISPLAY '=========================================='.                
           DISPLAY SPACES.                                                      
           MOVE WS-F200-READ-CTR   TO WS-EDT-VAR.                               
           DISPLAY ' BP13F200 RECORDS READ       = ' WS-EDT-VAR.                
           MOVE WS-F205-READ-CTR   TO WS-EDT-VAR.                               
           DISPLAY ' BP13F205 RECORDS READ       = ' WS-EDT-VAR.                
           MOVE WS-MATCH-CNT       TO WS-EDT-VAR.                               
           DISPLAY ' TOTAL    RECORDS MATCHED    = ' WS-EDT-VAR.                
           MOVE WS-K800-READ-CTR   TO WS-EDT-VAR.                               
           DISPLAY ' BP13K800 RECORDS READ       = ' WS-EDT-VAR.                
           MOVE WS-F800-WRITE-CTR  TO WS-EDT-VAR.                               
           DISPLAY ' BP13F800 RECORDS WRITE      = ' WS-EDT-VAR.                
           MOVE WS-ZONE-CNT        TO WS-EDT-VAR.                               
           DISPLAY ' ZONE FOUND IN MATCHED CASES = ' WS-EDT-VAR.                
           DISPLAY SPACES.                                                      
           DISPLAY '=========================================='.                
                                                                                
           STOP RUN.                                                            
                                                                                
