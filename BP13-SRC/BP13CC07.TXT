       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC07.                                                 
       AUTHOR.        B.BALA AMUTHAN.                                           
       DATE-WRITTEN.  02/06/2006.                                               
      *****************************************************************         
      * OBJECTIVE  - TO UPDATE TRANSACTION HISTORY ENTRIES TO THE               
      *              TRANSACTION HISTORY FILE.                                  
      *                                                                         
      * INPUT         - BP13F125 SIMILAR AB03F001                               
      * I-O           - BP13KD63 SIMILAR AB03K045                               
      *                                                                         
      * IDENTICAL PGM - AB03C253                                                
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO      DATE    BY   AMENDMENTS/ENHANCEMENTS                        
      * -------     ----    ---  -----------------------                        
      * BP13B2857  020606   BA2  NEW PROGRAME.                                  
      * BP13B2845  050706   BA3  CHANGED 'INST' TO 'INS' AND                    
      *                                  'INT' TO 'IT'                          
      *            100107   BA3  INCLUDE 'JV' FOR 'INS'                         
      * BP13B2973  060207   ED1  INCLUDED 'INT-R' FOR TRANS TYPE 68             
      * BP132939   060207   BA3  INCLUDED 'LS ' AND 'CPF-R'                     
      * BP133052   280507   BA3  CHANGE F125-DTE-POST TO F125-DTE-TRANS         
      *            110607   BA3  INCLUDE 'JVL'                                  
      * BP133052   250607   BA3  MOVE 8 CHARS TO NUM-REF AFTER 'MAB'            
      * BP133173   181007   BA3  ADD KD63-AMT-INTEREST                          
      * BP133175   121107   ED1  INCLUDE 'JV-I', JV-L','JV-N'                   
      *                          & 'INTR-ADJ'                                   
      * BP133175   030108   ED1  ADDED 'CPF-A'                                  
      * BP133209   090408   ED1  CHANGED DESC INTO INTRADJ                      
      * BP133360   140808   ED1  CHANGED DESC 'JV-L' INTO 'JV '                 
      * BP133387   131008   JB8  CONVERT TXT-DESC                               
      * BP133480   030209   JB8  TO CATER FOR 'IPN'                             
      * BP133572   200409   JB8  TO CATER FOR 'JVI'                             
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F125  ASSIGN TO BP13F125.                                
                                                                                
           SELECT  BP13KD63  ASSIGN TO BP13KD63                                 
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD63-KEY-FLD                       
                             FILE STATUS  IS KD63-STATUS                        
                             ACCESS MODE  IS RANDOM.                            
                                                                                
           SELECT BP13KD60   ASSIGN TO BP13KD60                                 
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD60-KEY-FLD                       
                             FILE STATUS  IS KD60-STATUS                        
                             ACCESS       IS RANDOM.                            
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F125                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 150 CHARACTERS          
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
       COPY BP13F125.                                                           
                                                                                
       FD  BP13KD63                     RECORD CONTAINS 150 CHARACTERS.         
                                                                                
       COPY BP13KD63.                                                           
                                                                                
       FD  BP13KD60                     RECORD CONTAINS 250 CHARACTERS.         
                                                                                
       COPY BP13KD60.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       77  WS-NUM-SEQ-KD63                      PIC 9(03) VALUE 001.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  KD63-STATUS                      PIC 99    VALUE ZEROES.         
           05  KD60-STATUS                      PIC 99    VALUE ZEROES.         
           05  WS-CNT-KD63-WRT                  PIC 9(7)  VALUE ZEROES.         
           05  WS-CNT-KD60-FD                   PIC 9(7)  VALUE ZEROES.         
           05  WS-CNT-KD60-WRT                  PIC 9(7)  VALUE ZEROES.         
           05  WS-CNT-F125-RD                   PIC 9(7)  VALUE ZEROES.         
           05  WS-NUM-SEQ                       PIC 9(03) VALUE ZEROES.         
                                                                                
       01  WS-DTE-POST.                                                         
           05  WS-POST-CC                       PIC X(2).                       
           05  WS-POST-YY                       PIC X(2).                       
           05  WS-POST-MM                       PIC X(2).                       
           05  WS-POST-DD                       PIC X(2).                       
                                                                                
       01  WS-EOF-KD63                          PIC X     VALUE 'N'.            
           88  EOF-KD63                                   VALUE 'Y'.            
                                                                                
       01  WS-EOF-KD60                          PIC X     VALUE 'N'.            
           88  EOF-KD60                                   VALUE 'Y'.            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ********************************************************                  
       0000-MAIN-LOGIC.                                                         
      ********************************************************                  
                                                                                
           PERFORM 1000-START-UP                   THRU 1000-EXIT.              
           PERFORM 2000-READ-BP13F125              THRU 2000-EXIT.              
           PERFORM 3000-MAIN-PROCESS               THRU 3000-EXIT               
                   UNTIL EOF-KD63                                               
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
      ********************************************************                  
       1000-START-UP.                                                           
      ********************************************************                  
                                                                                
           OPEN INPUT  BP13F125                                                 
                I-O    BP13KD63                                                 
                       BP13KD60.                                                
                                                                                
           IF KD63-STATUS NOT = 00 AND 97                                       
              DISPLAY ' ERROR OPENING BP13KD63 ' KD63-STATUS                    
              MOVE KD63-STATUS                     TO RETURN-CODE               
              GO TO 9999-WRAP-UP.                                               
                                                                                
           IF KD60-STATUS NOT = 00 AND 97                                       
              DISPLAY ' ERROR OPENING BP13KD60 ' KD60-STATUS                    
              MOVE KD60-STATUS                     TO RETURN-CODE               
              GO TO 9999-WRAP-UP.                                               
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       2000-READ-BP13F125.                                                      
      ********************************************************                  
           READ BP13F125 AT END                                                 
                MOVE 'Y'                           TO WS-EOF-KD63               
                GO TO 2000-EXIT.                                                
           ADD +1 TO WS-CNT-F125-RD.                                            
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       3000-MAIN-PROCESS.                                                       
      ********************************************************                  
           PERFORM 3100-MOVE-VALUES                THRU 3100-EXIT.              
           PERFORM 3200-WRITE-BP13KD63             THRU 3200-EXIT.              
           IF F125-CDE-PAYMENT-TYPE = 'LON'                                     
              PERFORM 4000-READ-KD60 THRU 4000-EXIT                             
           END-IF.                                                              
           PERFORM 2000-READ-BP13F125              THRU 2000-EXIT.              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       3100-MOVE-VALUES.                                                        
      ********************************************************                  
                                                                                
           IF F125-AMT-INTEREST IS NOT NUMERIC                                  
              MOVE ZEROES             TO F125-AMT-INTEREST                      
           END-IF.                                                              
                                                                                
           MOVE SPACES                   TO BP13KD63-REC.                       
           INITIALIZE                    BP13KD63-REC.                          
           MOVE F125-NUM-REGN            TO KD63-NUM-REGN.                      
           MOVE F125-DTE-TRANS           TO KD63-DTE-PROCESS                    
                                            WS-DTE-POST.                        
           MOVE WS-NUM-SEQ-KD63          TO KD63-NUM-SEQ                        
                                            WS-NUM-SEQ.                         
           MOVE WS-POST-MM               TO KD63-DTE-PYMT-MTH.                  
           MOVE F125-AMT-RECEIPT         TO KD63-AMT-LOAN.                      
           MOVE F125-AMT-INTEREST        TO KD63-AMT-INTEREST.                  
           MOVE F125-CDE-CRDR            TO KD63-CDE-CR-DR.                     
                                                                                
           IF F125-NUM-RECPT-JRNO(1:3) = 'MAB'                                  
              MOVE F125-NUM-RECPT-JRNO(4:8) TO KD63-NUM-REF                     
           ELSE                                                                 
              MOVE F125-NUM-RECPT-JRNO   TO KD63-NUM-REF                        
           END-IF.                                                              
           MOVE F125-NUM-SCH-ACCT        TO KD63-NUM-SCH-ACCNT.                 
                                                                                
           EVALUATE F125-CDE-PAYMENT-TYPE                                       
              WHEN 'INS'                                                        
                IF F125-CDE-TRANS-TYPE = '12'                                   
                    IF F125-NUM-DOC = 'ARR-L'                                   
                       MOVE 'CPF-A  '      TO  KD63-TXT-DESC                    
                    ELSE                                                        
                       MOVE 'CPF    '      TO  KD63-TXT-DESC                    
                    END-IF                                                      
                ELSE                                                            
                  IF F125-CDE-TRANS-TYPE = '11'                                 
                      MOVE 'PR      ' TO  KD63-TXT-DESC                         
                  ELSE                                                          
                     IF F125-CDE-TRANS-TYPE = '14'                              
                        MOVE 'JV      ' TO  KD63-TXT-DESC                       
                     ELSE                                                       
                        IF F125-CDE-TRANS-TYPE = '64'                           
                           MOVE 'GIRO    '  TO  KD63-TXT-DESC                   
                        ELSE                                                    
                           IF F125-CDE-TRANS-TYPE = '61'                        
                              MOVE 'INST   '  TO  KD63-TXT-DESC                 
                           END-IF                                               
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
                END-IF                                                          
              WHEN 'IT '                                                        
                IF F125-CDE-TRANS-TYPE = '62'                                   
                    MOVE F125-NUM-RECPT-JRNO(1:8)   TO  KD63-TXT-DESC           
                ELSE                                                            
                  IF F125-CDE-TRANS-TYPE = '67'                                 
                     STRING 'IPN' F125-NUM-RECPT-JRNO(3:5)                      
                        DELIMITED BY SIZE INTO KD63-TXT-DESC                    
                  ELSE                                                          
                     IF F125-CDE-TRANS-TYPE = '68'                              
                        MOVE 'INT-R '    TO  KD63-TXT-DESC                      
                     END-IF                                                     
                  END-IF                                                        
                                                                                
                END-IF                                                          
              WHEN 'LON'                                                        
                IF F125-CDE-TRANS-TYPE = '66'                                   
                    MOVE 'NEW-LOAN'  TO  KD63-TXT-DESC                          
                END-IF                                                          
              WHEN 'LS '                                                        
                IF F125-CDE-TRANS-TYPE = '11'                                   
                    MOVE 'PR-L    '  TO  KD63-TXT-DESC                          
                ELSE                                                            
                   IF F125-CDE-TRANS-TYPE = '12'                                
                      MOVE 'CPF-L   '  TO  KD63-TXT-DESC                        
                   ELSE                                                         
                     IF F125-CDE-TRANS-TYPE = '64'                              
                        MOVE 'GIRO-L  '  TO  KD63-TXT-DESC                      
                     END-IF                                                     
                  END-IF                                                        
                END-IF                                                          
              WHEN 'JVA'                                                        
                IF F125-CDE-TRANS-TYPE = '69'                                   
                    MOVE 'INTRADJ'  TO  KD63-TXT-DESC                           
                END-IF                                                          
              WHEN 'JVL'                                                        
              WHEN 'JVI'                                                        
              WHEN 'JVP'                                                        
                  PERFORM 3150-DECODE-JVL-JVP THRU 3150-EXIT                    
              END-EVALUATE.                                                     
                                                                                
       3100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       3150-DECODE-JVL-JVP.                                                     
      ********************************************************                  
            EVALUATE F125-NUM-LOAN-INTR                                         
            WHEN 'I'                                                            
                IF F125-CDE-TRANS-TYPE = '69'                                   
                   MOVE 'JV-I    '  TO  KD63-TXT-DESC                           
                END-IF                                                          
            WHEN 'L'                                                            
                IF F125-CDE-TRANS-TYPE = '69'                                   
                   MOVE 'JV      '  TO  KD63-TXT-DESC                           
                END-IF                                                          
            WHEN 'N'                                                            
                IF F125-CDE-TRANS-TYPE = '69'                                   
                   MOVE 'JV-N    '  TO  KD63-TXT-DESC                           
                END-IF                                                          
            WHEN OTHER                                                          
                IF F125-CDE-TRANS-TYPE = '69'                                   
                   MOVE 'JV-ADJ  '  TO  KD63-TXT-DESC                           
                END-IF                                                          
            END-EVALUATE.                                                       
                                                                                
       3150-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ********************************************************                  
       3200-WRITE-BP13KD63.                                                     
      ********************************************************                  
                                                                                
           WRITE BP13KD63-REC.                                                  
           IF KD63-STATUS = ZEROES                                              
              ADD +1 TO WS-CNT-KD63-WRT                                         
           ELSE                                                                 
             IF KD63-STATUS = 22                                                
                COMPUTE  WS-NUM-SEQ     =  WS-NUM-SEQ + 1                       
                MOVE     WS-NUM-SEQ     TO KD63-NUM-SEQ                         
                GO TO 3200-WRITE-BP13KD63                                       
             ELSE                                                               
                DISPLAY 'WRITE FAILED FOR BP13KD63 ' KD63-STATUS                
                MOVE KD63-STATUS TO RETURN-CODE                                 
                GO TO 9999-WRAP-UP                                              
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3200-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-KD60.                                                          
      ******************************************************************        
                                                                                
           MOVE  F125-NUM-REGN       TO     KD60-NUM-REGN.                      
           READ BP13KD60.                                                       
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1                  TO     WS-CNT-KD60-FD                      
           ELSE                                                                 
             IF KD60-STATUS = 23                                                
                PERFORM 4500-MOVE-VALUE-KD60 THRU 4500-EXIT                     
             ELSE                                                               
                DISPLAY 'BP13KD60-READ ERROR. STATUS: '                         
                       KD60-STATUS                                              
                MOVE   KD60-STATUS   TO   RETURN-CODE                           
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4500-MOVE-VALUE-KD60.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO BP13KD60-REC.                         
           INITIALIZE                     BP13KD60-REC.                         
           MOVE F125-NUM-REGN          TO KD60-NUM-REGN.                        
           MOVE '001'                  TO KD60-NUM-SEQ.                         
           MOVE F125-DTE-TRANS(1:6)    TO KD60-DTE-TRANS.                       
           MOVE '01'                   TO KD60-DTE-TRANS(7:2).                  
           MOVE F125-NUM-SCH-ACCT      TO KD60-NUM-SCH-ACCNT.                   
           MOVE ZEROS                  TO KD60-AMT-OPEN-LOAN-BAL.               
           MOVE ZEROS                  TO KD60-AMT-OPEN-ARR-INST.               
           MOVE ZEROS                  TO KD60-AMT-OPEN-INTEREST.               
           MOVE F125-DTE-POST          TO KD60-DTE-UPDATE.                      
           MOVE 'BP13CC07'             TO KD60-NUM-USERID.                      
                                                                                
           PERFORM 4600-WRITE-BP13KD60  THRU 4600-EXIT.                         
                                                                                
       4500-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       4600-WRITE-BP13KD60.                                                     
      ******************************************************************        
           WRITE BP13KD60-REC.                                                  
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1 TO WS-CNT-KD60-WRT                                          
           ELSE                                                                 
              DISPLAY ' BP13KD60-WRINTING ERROR, STATUS: '                      
                       KD60-STATUS                                              
              MOVE KD60-STATUS           TO   RETURN-CODE                       
              PERFORM 9999-WRAP-UP        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       4600-EXIT.  EXIT.                                                        
                                                                                
      ********************************************************                  
       9999-WRAP-UP.                                                            
      ********************************************************                  
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CC07'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY '                         '.                                 
           DISPLAY ' NUMBER OF BP13F125 RECORDS READ     : '                    
                   WS-CNT-F125-RD.                                              
           DISPLAY ' NUMBER OF BP13KD63 RECS UPDATED     : '                    
                   WS-CNT-KD63-WRT.                                             
           DISPLAY ' NUMBER OF BP13KD60 RECS FOUND       : '                    
                   WS-CNT-KD60-FD.                                              
           DISPLAY ' NUMBER OF BP13KD60 RECS UPDATED     : '                    
                   WS-CNT-KD60-WRT.                                             
                                                                                
           CLOSE BP13F125                                                       
                 BP13KD63                                                       
                 BP13KD60.                                                      
                                                                                
           IF KD63-STATUS NOT = 00                                              
              DISPLAY ' ERROR CLOSING BP13KD63 ' KD63-STATUS                    
              MOVE KD63-STATUS                     TO RETURN-CODE.              
                                                                                
           IF KD60-STATUS NOT = 00                                              
              DISPLAY ' ERROR CLOSING BP13KD60 ' KD60-STATUS                    
              MOVE KD60-STATUS                     TO RETURN-CODE.              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
