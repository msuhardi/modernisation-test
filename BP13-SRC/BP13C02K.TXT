      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C02K.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DTE WRITTEN.   01/08/2006.                                               
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      **********************************************************                
      *                                                        *                
      *    OBJECTIVES  :                                       *                
      *                                                        *                
      *    1.  TO CREATE ACCEPTED RECORDS FOR THE DAY. OUTPUT  *                
      *        THE BLOCK AND UNIT DATA.                        *                
      *                                                        *                
      *                                                        *                
      *    INPUT FILES : BP13FA20 - BLOCK INFORMATION FILE     *                
      *                : BP13FA30 - UNIT INFORMATION FILE      *                
      *                                                        *                
      *    OUTPUT FILE : P13FA20A - TODAY, WHICH BLOCK HAS BEEN*                
      *                  ACCPETED.                             *                
      *                : P13FA30A - TODAY, WHICH UNIT HAS BEEN *                
      *                  ACCPETED.                             *                
      *--------------------------------------------------------*                
      * CHG-NO   BY    DATE   DETAILS                          *                
      * ======= ====  ======  ================================ *                
      * BP132888 BA2  010806  NEW PROGRAM                      *                
      * BP134109 ZDD1 050411  CHANGE DTE-ACCEPT TO DTE-UPDATE  *                
      * ====================================================== *                
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-9121.                                               
       OBJECT-COMPUTER. IBM-9121.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA20 ASSIGN    TO BP13FA20.                               
                                                                                
           SELECT BP13FA30 ASSIGN    TO BP13FA30.                               
                                                                                
           SELECT P13FA20A ASSIGN    TO P13FA20A.                               
                                                                                
           SELECT P13FA30A ASSIGN    TO P13FA30A.                               
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FA20                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 200  CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS  F.                                          
       COPY BP13FA20.                                                           
                                                                                
                                                                                
       FD   BP13FA30                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 200  CHARACTERS                                  
            LABEL     RECORDS  ARE OMITTED                                      
            RECORDING MODE     IS  F.                                           
       COPY BP13FA30.                                                           
                                                                                
       FD   P13FA20A                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 200  CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS  F.                                          
       01   WS-P13FA20A-REC     PIC X(200).                                     
                                                                                
       FD   P13FA30A                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 200  CHARACTERS                                  
            LABEL     RECORDS  ARE OMITTED                                      
            RECORDING MODE     IS  F.                                           
       01   WS-P13FA30A-REC     PIC X(200).                                     
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
      *SYSTEM-DATE.                                                             
      *-----------------------*                                                 
       01  WS-SYSTEM-DATE.                                                      
           05  WS-TODAY-DATE.                                                   
               10  WS-SYS-CC              PIC X(2).                             
               10  WS-SYS-YY              PIC X(2).                             
               10  WS-SYS-MM              PIC X(2).                             
               10  WS-SYS-DD              PIC X(2).                             
           05  WS-TIME.                                                         
               10  WS-HH                  PIC X(2).                             
               10  WS-MM                  PIC X(2).                             
               10  WS-SS                  PIC X(2).                             
               10  WS-GG                  PIC X(2).                             
                                                                                
      *-----------------------*                                                 
      *FLAGE-VARIABLES.                                                         
      *-----------------------*                                                 
       01  WS-SWITCH.                                                           
           05  WS-FA20-EOF-SW             PIC X(1)    VALUE 'N'.                
               88 C-FA20-EOF-SW           VALUE 'Y'.                            
           05  WS-FA30-EOF-SW             PIC X(1)    VALUE 'N'.                
               88 C-FA30-EOF-SW           VALUE 'Y'.                            
           05  WS-ACCEPT-DTE              PIC X(1)    VALUE 'N'.                
               88 C-ACCEPT-DTE            VALUE 'Y'.                            
                                                                                
      *-----------------------*                                                 
      *TOTAL-RECORDS-COUNTER.                                                   
      *-----------------------*                                                 
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-FA20                PIC 9(5) COMP  VALUE 0.               
           05  WS-CNT-FA30                PIC 9(5) COMP  VALUE 0.               
           05  WS-CNT-F20A                PIC 9(5) COMP  VALUE 0.               
           05  WS-CNT-F30A                PIC 9(5) COMP  VALUE 0.               
                                                                                
      *-----------------------*                                                 
      *OTHER-VARIABLES.                                                         
      *-----------------------*                                                 
       01  WS-FA30-KEY.                                                         
           05  WS-FA30-NUM-PRJCT          PIC X(03) VALUE SPACES.       00181303
           05  WS-FA30-NUM-BLK            PIC X(05) VALUE SPACES.       00181803
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM  1000-OPEN-FILES         THRU 1000-EXIT.                     
           PERFORM  2000-BP13FA30-READ      THRU 2000-EXIT.                     
           PERFORM  3000-BP13FA20-READ      THRU 3000-EXIT.                     
           PERFORM  4000-MAIN-PROCESS       THRU 4000-EXIT                      
              UNTIL FA20-KEY-FLD = HIGH-VALUE AND                               
                    WS-FA30-KEY = HIGH-VALUE                                    
           PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13FA20                                                 
                       BP13FA30                                                 
                OUTPUT P13FA20A                                                 
                       P13FA30A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-BP13FA30-READ.                                                      
      ******************************************************************        
           READ BP13FA30                                                        
              AT END MOVE 'Y'          TO WS-FA30-EOF-SW                        
                     MOVE HIGH-VALUE   TO WS-FA30-KEY                           
              GO TO  2000-EXIT                                                  
           END-READ.                                                            
           MOVE FA30-NUM-PRJCT    TO WS-FA30-NUM-PRJCT.                         
           MOVE FA30-NUM-BLK      TO WS-FA30-NUM-BLK.                           
           ADD +1                 TO WS-CNT-FA30.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-BP13FA20-READ.                                                      
      ******************************************************************        
           READ BP13FA20                                                        
              AT END MOVE 'Y'          TO WS-FA20-EOF-SW                        
                     MOVE HIGH-VALUE   TO FA20-KEY-FLD                          
              GO TO  3000-EXIT                                                  
           END-READ.                                                            
           ADD +1                 TO WS-CNT-FA20.                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       4000-MAIN-PROCESS.                                                       
      **********************************************************                
                                                                                
           MOVE 'N'    TO   WS-ACCEPT-DTE.                                      
                                                                                
           IF WS-FA30-KEY = FA20-KEY-FLD                                        
              PERFORM 5000-PROCESS-REC       THRU 5000-EXIT                     
                 UNTIL WS-FA30-KEY NOT = FA20-KEY-FLD OR                        
                       C-FA30-EOF-SW                                            
              IF C-ACCEPT-DTE                                                   
                 PERFORM 6500-F20A-WRITE     THRU 6500-EXIT                     
              END-IF                                                            
              PERFORM 3000-BP13FA20-READ     THRU 3000-EXIT                     
           ELSE                                                                 
             IF WS-FA30-KEY > FA20-KEY-FLD                                      
                PERFORM 3000-BP13FA20-READ   THRU 3000-EXIT                     
             ELSE                                                               
                PERFORM 2000-BP13FA30-READ   THRU 2000-EXIT                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       5000-PROCESS-REC.                                                        
      **********************************************************                
                                                                                
           IF FA30-DTE-ACCEPT  = WS-TODAY-DATE                                  
              PERFORM 6000-F30A-WRITE    THRU 6000-EXIT                         
              MOVE 'Y'                   TO WS-ACCEPT-DTE                       
           ELSE                                                                 
              IF ((FA30-DTE-ACCEPT  = ZEROES) AND                               
                  (FA30-DTE-UPDATE = WS-TODAY-DATE) AND                         
                  (FA30-NUM-ACCEPT = SPACES))                                   
                 PERFORM 6100-F30A-WRITE-REJECT THRU 6100-EXIT                  
                 MOVE 'Y'                       TO WS-ACCEPT-DTE                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13FA30-READ    THRU 2000-EXIT.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       6000-F30A-WRITE.                                                         
      **********************************************************                
           MOVE SPACES           TO  WS-P13FA30A-REC.                           
           INITIALIZE                WS-P13FA30A-REC.                           
           MOVE    BP13FA30-REC  TO  WS-P13FA30A-REC                            
           WRITE   WS-P13FA30A-REC.                                             
                                                                                
           ADD +1                 TO WS-CNT-F30A.                               
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       6100-F30A-WRITE-REJECT.                                                  
      **********************************************************                
           MOVE SPACES TO WS-P13FA30A-REC.                                      
           INITIALIZE     WS-P13FA30A-REC.                                      
           MOVE SPACES TO FA30-NUM-REGN.                                        
      *    MOVE SPACES TO FA30-NUM-SERIAL.                                      
           MOVE 'R'    TO FA30-NUM-ACCEPT.                                      
                                                                                
           MOVE    BP13FA30-REC  TO  WS-P13FA30A-REC                            
           WRITE   WS-P13FA30A-REC.                                             
                                                                                
           ADD +1                 TO WS-CNT-F30A.                               
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       6500-F20A-WRITE.                                                         
      **********************************************************                
           MOVE    SPACES        TO   WS-P13FA20A-REC.                          
           INITIALIZE                 WS-P13FA20A-REC.                          
           MOVE    BP13FA20-REC  TO  WS-P13FA20A-REC                            
           WRITE   WS-P13FA20A-REC.                                             
                                                                                
           ADD +1                 TO WS-CNT-F20A.                               
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       9000-CLOSE-FILES.                                                        
      **********************************************************                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C02K'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FA20 RECORDS READ..........: '                    
                    WS-CNT-FA20.                                                
           DISPLAY 'NO OF BP13FA30 RECORDS READ..........: '                    
                    WS-CNT-FA30.                                                
           DISPLAY 'NO OF P13FA20A RECORDS WRITTEN.......: '                    
                    WS-CNT-F20A.                                                
           DISPLAY 'NO OF P13FA30A RECORDS WRITTEN.......: '                    
                    WS-CNT-F30A.                                                
                                                                                
           CLOSE BP13FA20                                                       
                 BP13FA30                                                       
                 P13FA20A                                                       
                 P13FA30A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
