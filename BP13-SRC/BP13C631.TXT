      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.   BP13C631.                                                  
      *AUTHOR.       RAYMOND H. BUNADO                                          
      *DATE-WRITTEN. 04/02/98.                                                  
                                                                                
      ***********************************************************               
      *                                                         *               
      * OBJECTIVE : TO TAG DEBARMENT FOR NUM-CA-TAG = CA, CD    *               
      *             CASES FOR 30 MTHS.                          *               
      *                                                         *               
      *  INPUT FILE :- BP13F410                                 *               
      *                BP13K800                                 *               
      *                BP13K820                                 *               
      *  I-O           BP13K665                                 *               
      *  OUTPUT FILE:- BP04K010                                 *               
      *                                                         *               
      ***********************************************************               
      *                                                         *               
      * BP130302 RHB 04/02/1998  NEW PROGRAM.                   *               
      * BP130402 RHB 24/02/1998  TO CATER FOR EXPANSION OF      *               
      *                          DEBARMENT REFERENCE.           *               
      * BP130418 RHB 06/06/1998  TO Y2K                         *               
      * BP130799 JMS 29/03/2000  TO PRINT CASES THAT ARE TAGGED *               
      *                          CA OR CD AND HAVE NO LOAN TAG  *               
      * BP130799 JMS 29/03/2000  TO SKIP CASES WITH NO CA TAG   *               
      * BP130883 RAS 08/08/2000  TO CHANGE BP13C904 TO BP13C913 *               
      * BP130943 MCC 12/11/2000  TO SKIP CASES THAT HAVE NO LOAN*               
      * BP133070 BA2 20/08/2007  NOT PROCESSING CANCEL CASE     *               
      * BP135599 PCL 11/12/2014  TO UPDATE K010-INTERFACE-SYS   *               
      ***********************************************************               
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K820-KEY-FLD                              
                  FILE STATUS      IS WS-K820-STATUS.                           
                                                                                
           SELECT BP13K665 ASSIGN TO BP13K665                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS WS-K665-KEY-FLD                            
                  FILE STATUS     IS WS-K665-STATUS.                            
                                                                                
           SELECT BP04K010 ASSIGN TO BP04K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13L631 ASSIGN TO BP13L631.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F410   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F410.                                                           
                                                                                
       FD  BP13K665   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 50 CHARACTERS                             
                      LABEL RECORDS ARE STANDARD.                               
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD  BP13K800   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K800.                                                           
                                                                                
       FD BP13K820    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K820.                                                           
                                                                                
       FD BP04K010    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 300 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY P04K010.                                                            
                                                                                
       FD BP13L631    RECORD CONTAINS 132 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 BP13L631-REC                   PIC X(132).                            
                                                                                
                                                                                
      *----------------------*                                                  
       WORKING-STORAGE SECTION.                                                 
      *----------------------*                                                  
                                                                                
      *----------------------*                                                  
      * LINKAGE FOR BP13C913 *                                                  
      *----------------------*                                                  
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-ACC          PIC X(5).                           
                                                                                
        COPY P13COMM8.                                                          
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF               PIC X    VALUE 'N'.                    
               88 F410-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
           05  WS-DEBARMENT-TAG          PIC X    VALUE SPACES.                 
           05  WS-DEBAR-TAG              PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F410          PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K010-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-LINE-CTR               PIC 9(6) VALUE 60.                     
           05  WS-LINE-CTR1              PIC 9(6) VALUE 60.                     
           05  WS-PAGE-CTR               PIC 9(6) VALUE ZEROS.                  
           05  WS-PAGE-CTR1              PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO                 PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO1                PIC 9(6) VALUE ZEROS.                  
           05  WS-DIRECT-PURCHASE        PIC 9(6) VALUE ZEROS.                  
                                                                                
       01  WS-TEMPORARY-VARS.                                                   
           05  WS-DEBAR.                                                        
               10 WS-DEBREF-DIGIT        PIC 9(7) VALUE ZEROES.                 
               10 WS-DEB-REFN  REDEFINES WS-DEBREF-DIGIT.                       
                  15 WS-DEB              PIC X(7).                              
           05  WS-PREV-REGN              PIC X(8) VALUE SPACES.                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K665-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K800-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K820-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K010-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC                 PIC 9(02).                             
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY              PIC 9(02).                             
              10 WS-CURR-MM              PIC 9(02).                             
              10 WS-CURR-DD              PIC 9(02).                             
                                                                                
       COPY BP13K665.                                                           
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  L631-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L631'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L631-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L631-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L631-PR-HEAD-02.                                                     
           05  FILLER              PIC X(49)     VALUE SPACES .                 
           05  FILLER              PIC X(32)     VALUE                          
               'CREDIT ASSESSMENT TAGGING REPORT'.                              
           05  FILLER              PIC X(46)     VALUE SPACES.                  
                                                                                
       01  L631-PR-HEAD-03.                                                     
           05  FILLER              PIC X(52)     VALUE SPACES .                 
           05  FILLER              PIC X(28)     VALUE                          
               'SALES UNIT - AGREEMENT TEAM '.                                  
           05  FILLER              PIC X(52)     VALUE SPACES.                  
                                                                                
       01  L631-PR-HEAD-04.                                                     
           05  FILLER              PIC X(31)     VALUE                          
               'SEQ.    REGN   SCH & A/C   NAME'.                               
           05  FILLER              PIC X(24)     VALUE SPACES .                 
           05  FILLER              PIC X(18)     VALUE                          
               'I/C NO.   ADDRESS'.                                             
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE                          
               'DEB-REF       CODE EFF-DATE'.                                   
                                                                                
       01  L631-PR-DTL-01.                                                      
           05  L631-SEQ-NO         PIC ZZZ9.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L631-NUM-REGN       PIC X(08).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L631-NUM-SCHEME     PIC X(11).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L631-NME-OCCP       PIC X(25).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L631-NUM-NRIC       PIC X(09).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L631-ADDRESS        PIC X(36).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L631-DEB-REF-NO     PIC X(13).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L631-DEB-CODE       PIC X(02).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L631-EFF-DATE       PIC X(10).                                   
                                                                                
       01  L631-PR-LINE            PIC X(132)    VALUE ALL '-'.                 
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
                                                                                
      *-----------------                                                        
       000-MAIN-LOGIC.                                                          
      *-----------------                                                        
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F410    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-K665 THRU 300-EXIT                              
                    UNTIL F410-EOF.                                             
            PERFORM 800-PRINT-CONTROL-TOTAL THRU 800-EXIT.                      
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BP13F410                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                I-O    BP13K665                                                 
                       BP04K010                                                 
                OUTPUT BP13L631.                                                
                                                                                
           IF WS-K665-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/'                                                
                  WS-CURR-MM '/'                                                
                  WS-CURR-CC                                                    
                  WS-CURR-YY DELIMITED BY SIZE                                  
                             INTO L631-DATE.                                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       200-READ-F410.                                                           
      *-----------------                                                        
           READ BP13F410 AT END                                                 
                MOVE 'Y'                      TO WS-F410-EOF                    
                GO TO 200-EXIT.                                                 
           ADD   1     TO WS-READ-CNT-F410.                                     
           MOVE SPACES TO WS-FOUND.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       300-PROCESS-K665.                                                        
      *-----------------                                                        
      *--> VALIDATE REGN FROM MASTER                                            
                                                                                
           MOVE F410-NUM-REGN              TO K800-NUM-REGN.                    
           READ BP13K800                                                        
                INVALID KEY                                                     
                DISPLAY 'RECORD NOT FOUND IN BP13K800 KEY IS '                  
                          K800-NUM-REGN                                         
                PERFORM  200-READ-F410     THRU 200-EXIT                        
                GO TO 300-EXIT.                                                 
                                                                                
           IF K800-NUM-STATUS = 'C'                                             
              PERFORM  200-READ-F410     THRU 200-EXIT                          
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           ADD  1                          TO WS-K800-READ.                     
           MOVE LOW-VALUES                 TO K820-NUM-NRIC.                    
           MOVE K800-NUM-REGN              TO K820-NUM-REGN.                    
                                                                                
           START BP13K820 KEY IS >= K820-KEY-FLD                                
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K820-STATUS ' WS-K820-STATUS           
                 MOVE WS-K820-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT.                         
                                                                                
           MOVE 'N' TO WS-DEBAR-TAG.                                            
           PERFORM 0900-PROCESS-K820  THRU 0900-EXIT                            
                   UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                      
                   OR    K820-KEY-FLD      = HIGH-VALUES.                       
                                                                                
           PERFORM 200-READ-F410                THRU 200-EXIT.                  
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       0900-PROCESS-K820.                                                       
      *--------------------                                                     
            READ BP13K820 NEXT                                                  
                 AT END MOVE HIGH-VALUES TO K820-KEY-FLD                        
                 GO TO 0900-EXIT.                                               
                                                                                
            ADD 1 TO WS-K820-READ.                                              
      *     DISPLAY 'NRIC :' K820-NUM-NRIC.                                     
      *     DISPLAY 'REGN :' K820-NUM-REGN ' ' K800-NUM-REGN.                   
      *     DISPLAY 'LOAN :' K820-NUM-LOAN-TAG.                                 
      *     DISPLAY 'CA   :' K820-NUM-CA-TAG.                                   
                                                                                
            IF (K820-NUM-REGN = K800-NUM-REGN)  AND                             
               (K820-NUM-CA-TAG = 'CA' OR 'CD') AND                             
               (F410-AMT-LOAN > ZEROES)                                         
               IF WS-DEBAR-TAG = 'N'                                            
                  PERFORM 400-GET-DEBARMENT-NO                                  
                  MOVE 'Y' TO WS-DEBAR-TAG                                      
               END-IF                                                           
               PERFORM 500-PREPARE-DEBARMENT-REC    THRU 500-EXIT               
            END-IF.                                                             
                                                                                
       0900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------                                                     
       400-GET-DEBARMENT-NO.                                                    
      *--------------------                                                     
           MOVE  LOW-VALUES    TO WS-K665-KEY-FLD.                              
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K665-STATUS ' WS-K665-STATUS           
                 MOVE WS-K665-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT.                         
                                                                                
           PERFORM 410-READ-BP13K665 THRU 410-EXIT                              
                   UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                         
                                                                                
           MOVE K665-NUM-LAST-DEBREF-ID      TO WS-DEB-REFN.                    
                                                                                
           IF WS-DEBREF-DIGIT NOT NUMERIC                                       
              MOVE ZEROES TO WS-DEBREF-DIGIT.                                   
                                                                                
           COMPUTE WS-DEBREF-DIGIT = WS-DEBREF-DIGIT + 1.                       
                                                                                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO                 
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-DEBREF-ID.                     
           MOVE  WS-DEB-REFN        TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13C631'          TO  K665-NUM-USERID.                        
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              WRITE   WS-BP13K665-REC FROM BP13K665-REC.                        
                                                                                
           EVALUATE WS-K665-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
              ADD   1                 TO  WS-K665-WRITE                         
                                                                                
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STATUS '                   
                      WS-K665-STATUS                                            
                      MOVE WS-K665-STATUS       TO RETURN-CODE                  
                      PERFORM 999-CLOSE-FILES THRU 999-EXIT                     
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-WRITE                         
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS     TO   RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       410-READ-BP13K665.                                                       
      *--------------------                                                     
           READ BP13K665 NEXT                                                   
                AT END MOVE HIGH-VALUES TO WS-K665-KEY-FLD                      
                GO TO 410-EXIT.                                                 
                                                                                
           ADD 1 TO WS-K665-READ.                                               
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC.                             
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------                                              
       500-PREPARE-DEBARMENT-REC.                                               
      *---------------------------                                              
           MOVE SPACES TO P04K010-REC.                                          
                                                                                
           PERFORM 530-WRITE-BP04K010   THRU 530-EXIT.                          
           PERFORM 700-PRINT-L631       THRU 700-EXIT.                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       530-WRITE-BP04K010.                                                      
      *--------------------                                                     
           MOVE SPACES                TO BP13COMM8-REC.                         
           INITIALIZE                    BP13COMM8-REC.                         
                                                                                
           MOVE K800-NUM-SCH-ACC      TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF (COMM8-CDE-SYSERR = 0) AND                                        
              (COMM8-NUM-MGMT   NOT = SPACES)                                   
              STRING 'BLK' COMM8-NUM-BLK                                        
                     '#'   COMM8-NUM-LEVEL                                      
                     '-'   COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB               
                     ' '   COMM8-NME-STREET(1:20)                               
                     '('   COMM8-NUM-POSTAL-CODE ')'                            
                     DELIMITED BY SIZE INTO L631-ADDRESS                        
           ELSE                                                                 
              DISPLAY 'SQL CODE = ' COMM8-CDE-SYSERR                            
              MOVE SPACES TO L631-ADDRESS.                                      
                                                                                
           MOVE 'BP13'                   TO K010-INTERFACE-SYS.                 
           MOVE K820-NUM-CA-TAG          TO K010-REASON-CODE.                   
           MOVE K820-NUM-NRIC            TO K010-NUM-UINFIN.                    
           MOVE K820-NME-OCCP            TO K010-NAME.                          
           MOVE K665-NUM-LAST-DEBARMENT  TO K010-DEB-REF-NO.                    
           MOVE F410-DTE-SO-EXECUTION    TO K010-EFF-DATE.                      
           MOVE K800-NUM-SCH-ACC(1:9)    TO K010-EX-SCH-ACCTNO.                 
           MOVE K800-NUM-SCH-ACC(10:2)   TO K010-LSE-TEN-CODE.                  
           MOVE '1'                      TO K010-PROP-TYPE.                     
           MOVE '030'                    TO K010-PERIOD-DEBAR.                  
           MOVE ZEROS                    TO K010-AMT-OWING.                     
                                                                                
           MOVE COMM8-NUM-BLK            TO K010-BLOCK.                         
           MOVE COMM8-NME-STREET         TO K010-STREETNAME.                    
           MOVE COMM8-NUM-POSTAL-CODE    TO K010-POSTCODE.                      
                                                                                
           STRING    COMM8-NUM-LEVEL                                            
                     COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                     
                     DELIMITED BY SIZE INTO K010-HOUSE-NO.                      
                                                                                
           MOVE WS-CURR-DATE             TO K010-INCLUSION-DATE                 
                                            K010-LUPDATE.                       
                                                                                
           WRITE P04K010-REC.                                                   
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K010-WRITE                                          
           WHEN 22                                                              
                DISPLAY 'DUPLICATE RECORD BP04K010: ' K010-PRIME-KEY            
           WHEN OTHER                                                           
                MOVE WS-K010-STATUS       TO RETURN-CODE                        
                DISPLAY 'ERROR IN WRITING BP04K010: ' K010-PRIME-KEY            
                DISPLAY K800-NUM-REGN                                           
                DISPLAY WS-K010-STATUS                                          
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       530-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       700-PRINT-L631.                                                          
      *--------------------                                                     
           ADD  1                        TO WS-SEQ-NO.                          
           MOVE WS-SEQ-NO                TO L631-SEQ-NO.                        
           MOVE F410-NUM-REGN            TO L631-NUM-REGN.                      
           MOVE K665-NUM-LAST-DEBARMENT  TO L631-DEB-REF-NO.                    
           MOVE K820-NUM-CA-TAG          TO L631-DEB-CODE.                      
           MOVE K820-NUM-NRIC            TO L631-NUM-NRIC.                      
           MOVE K820-NME-OCCP            TO L631-NME-OCCP.                      
           MOVE K800-NUM-SCH-ACC         TO L631-NUM-SCHEME.                    
                                                                                
           STRING K010-EFF-DATE(7:2) '-'                                        
                  K010-EFF-DATE(5:2) '-'                                        
                  K010-EFF-DATE(1:4) DELIMITED BY SIZE                          
                  INTO L631-EFF-DATE.                                           
                                                                                
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 6              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   L631-PAGENO                              
              WRITE BP13L631-REC  FROM L631-PR-HEAD-01 AFTER PAGE               
              WRITE BP13L631-REC  FROM L631-PR-HEAD-02                          
              WRITE BP13L631-REC  FROM L631-PR-HEAD-03                          
              WRITE BP13L631-REC  FROM L631-PR-LINE    AFTER 2                  
              WRITE BP13L631-REC  FROM L631-PR-HEAD-04                          
              WRITE BP13L631-REC  FROM L631-PR-LINE.                            
                                                                                
           WRITE BP13L631-REC  FROM L631-PR-DTL-01.                             
           ADD   1             TO   WS-LINE-CTR .                               
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       800-PRINT-CONTROL-TOTAL.                                                 
      *----------------------------------------------------------------*        
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 6              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   L631-PAGENO                              
              WRITE BP13L631-REC  FROM L631-PR-HEAD-01 AFTER PAGE               
              WRITE BP13L631-REC  FROM L631-PR-HEAD-02                          
              WRITE BP13L631-REC  FROM L631-PR-HEAD-03                          
              WRITE BP13L631-REC  FROM L631-PR-LINE    AFTER 2                  
              WRITE BP13L631-REC  FROM L631-PR-HEAD-04                          
              WRITE BP13L631-REC  FROM L631-PR-LINE.                            
                                                                                
           MOVE    '*-------------- CONTROL TOTALS -------------*'              
                   TO L631-PR-LINE.                                             
           WRITE BP13L631-REC  FROM L631-PR-LINE AFTER 2.                       
           STRING  '1. NO OF REC READ        BP13F410   = '                     
                    WS-READ-CNT-F410 DELIMITED BY SIZE                          
                    INTO L631-PR-LINE.                                          
           WRITE BP13L631-REC  FROM L631-PR-LINE AFTER 2.                       
           STRING  '2. NO OF CASES TAGGED    BP04K010   = '                     
                    WS-K010-WRITE DELIMITED BY SIZE                             
                    INTO L631-PR-LINE.                                          
           WRITE BP13L631-REC  FROM L631-PR-LINE AFTER 2.                       
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       999-CLOSE-FILES.                                                         
      *----------------------------------------------------------------*        
           CLOSE BP13F410                                                       
                 BP13K665                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP04K010                                                       
                 BP13L631.                                                      
                                                                                
           IF WS-K665-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K665, STATUS ' WS-K665-STATUS          
              MOVE WS-K665-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP04K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY '*------------------ BP13C631 -------------------'.          
           DISPLAY '                  ' WS-CURR-DATE                            
           DISPLAY 'NO OF REC READ        BP13F410   = '                        
                    WS-READ-CNT-F410.                                           
           DISPLAY 'NO OF REC READ        BP13K665   = '                        
                    WS-K665-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP13K665   = '                        
                    WS-K665-WRITE                                               
           DISPLAY 'NO OF REC READ        BP13K820   = '                        
                    WS-K820-READ                                                
           DISPLAY 'NO OF REC READ        BP13K800   = '                        
                    WS-K800-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP04K010   = '                        
                    WS-K010-WRITE                                               
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
