       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13CKA1.                                                    
      *AUTHOR. RJB1.                                                            
      *DATE-WRITTEN. 07/12/16.                                                  
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (SOC)                             *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE : DETAILS OF CASES WHERE THE KEYS ARE     *                
      *                AVAILABLE/NOT AVAILABLE AS AT           *                
      *                ( REPORT RUN MONTH )                    *                
      *                                                        *                
      *  INPUT FILES:                                          *                
      *    1. BP13FC40                                         *                
      *    2. BP13K800                                         *                
      *    3. BP13K024                                         *                
      *    4. BM06K110                                         *                
      *    5. BM06K100                                         *                
      *    6. BB14K500                                         *                
      *    7. BP13KB05                                         *                
      *                                                        *                
      *  OUTPUT FILE:                                          *                
      *    1. BP13LKA1                                         *                
      *--------------------------------------------------------*                
      * CHG REF  BY   DATE      DESCRIPTION                    *                
      * -------- ---- --------  -----------                    *                
      * BP136438 RJB1 07/12/16  NEW PROGRAM                    *                
      * BP136525 RJB1 22/12/16  ADD COLUMNS ON REPORT          *                
      * BP136525 RJB1 22/12/16  REPLACED HOLDING TYPE INTO     *                
      *                         HOUSEHOLD FOR 'HHOLD TYPE'     *                
      * BP136566 RJB1 28/12/16  REPLACED DTE-1ST-LETTER INTO   *                
      *                         DTE-1ST-TP-LETTER              *                
      * BP136665 RJB1 08/03/17  CONVERT OUTPUT TO ALLOW SENDMAIL                
      *                         AND ADD LEASE TENURE COLUMN    *                
      * BP136930 RJB1 25/08/17  CHANGE THE COMPUTATION OF MTHS O/S              
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K024   ASSIGN TO BP13K024                                 
                  ACCESS MODE  IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K024-KEY-FLD                                  
                  FILE STATUS  IS BP13K024-STATUS.                              
                                                                                
           SELECT BM06K110   ASSIGN TO BM06K110                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K110-KEY-FLD                                  
                  FILE STATUS  IS BM06K110-STATUS.                              
                                                                                
           SELECT BM06K100   ASSIGN TO BM06K100                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K100-KEY-FLD                                  
                  FILE STATUS  IS BM06K100-STATUS.                              
                                                                                
           SELECT BB14K500   ASSIGN TO BB14K500                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K500-KEY-FLD                                  
                  FILE STATUS  IS BB14K500-STATUS.                              
                                                                                
           SELECT BP13K800   ASSIGN TO BP13K800                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS BP13K800-STATUS.                              
                                                                                
           SELECT BP13KB05   ASSIGN TO BP13KB05                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS KB05-KEY-FLD                                  
                  FILE STATUS  IS BP13KB05-STATUS.                              
                                                                                
           SELECT BP13FC40  ASSIGN TO BP13FC40.                                 
                                                                                
           SELECT BP13LKA1  ASSIGN TO BP13LKA1.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FC40                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13FC40.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       FD   BP13KB05                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13KB05.                                                           
                                                                                
       FD   BP13LKA1                                                            
            RECORDING MODE IS V                                                 
            LABEL  RECORDS  ARE OMITTED.                                        
       01   BP13LKA1-REC                 PIC X(546).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FC40-READ            PIC 9(08)    VALUE ZERO.                     
       01  WS-FC40-OUTPUT          PIC 9(08)    VALUE ZERO.                     
       01  FC40-EOF                PIC X        VALUE 'N'.                      
       01  WS-SQL-CODE             PIC 9(05)    VALUE ZEROES.                   
       01  WS-CNT                  PIC 9(04)    VALUE ZEROES.                   
       01  BM06K110-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BM06K100-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BP13K800-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BP13K024-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BB14K500-STATUS         PIC 9(02)    VALUE ZEROES.                   
       01  BP13KB05-STATUS         PIC 9(02)    VALUE ZEROES.                   
                                                                                
       01  WS-DATE.                                                             
           05  WS-DATE-CCYY        PIC 9(4)     VALUE ZEROES.                   
           05  WS-DATE-MM          PIC 9(2)     VALUE ZEROES.                   
           05  WS-DATE-DD          PIC 9(2)     VALUE ZEROES.                   
                                                                                
       01  WS-NUM-OIC                  PIC X(5)   VALUE SPACES.                 
       01  WS-K800-FND                 PIC X      VALUE 'N'.                    
       01  WS-K110-FND                 PIC X      VALUE 'N'.                    
       01  WS-K100-FND                 PIC X      VALUE 'N'.                    
       01  WS-TEAM                     PIC X      VALUE SPACES.                 
       01  WS-COMPUTE-MTH              PIC S9(2)  VALUE ZEROES.                 
       01  WS-INT-CURR                 PIC 9(6)   VALUE ZEROES.                 
       01  WS-INT-START                PIC 9(6)   VALUE ZEROES.                 
       01  WS-PROCESS-DATE.                                                     
           05  WS-PROCESS-CCYY         PIC 9(4).                                
           05  WS-PROCESS-MM           PIC 9(2).                                
           05  WS-PROCESS-DD           PIC 9(2).                                
       01  WS-DTE-COMPUTE.                                                      
           05  WS-COMPUTE-CCYY         PIC 9(4).                                
           05  WS-COMPUTE-MM           PIC 9(2).                                
           05  WS-COMPUTE-DD           PIC 9(2).                                
       01  WS-MONTH-DIFF               PIC 9(3).                                
       01  WS-MONTH-DIFF-RED REDEFINES WS-MONTH-DIFF.                           
           05  WS-MONTH-DIFF-R         PIC X(3).                                
                                                                                
       COPY PENDRSN.                                                            
       01  WS-REASON-CODE-VARS.                                                 
           05  WS-CDE-REASON               PIC X(03) VALUE SPACES.              
           05  WS-CDE-REASON-FND           PIC X(01) VALUE 'N'.                 
                                                                                
       COPY PENDCAT.                                                            
       01  WS-CATEGORY-CODE-VARS.                                               
           05  WS-CDE-CATEGORY             PIC X(03) VALUE SPACES.              
           05  WS-CDE-CATEGORY-FND         PIC X(01) VALUE 'N'.                 
                                                                                
      *-----------------------------------------------------------*             
      * BP13C913 PARAMETERS                                                     
      *-----------------------------------------------------------*             
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
                                                                                
       01  LKA1-PR-HEAD-01.                                                     
           05  FILLER              PIC X(14)   VALUE 'BP13LKA1      '.          
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  LKA1-DATE           PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
                                                                                
       01  LKA1-PR-HEAD-02.                                                     
           05  FILLER              PIC X(29)   VALUE SPACES.                    
           05  FILLER              PIC X(66)   VALUE                            
           'DETAILS OF CASES WHERE THE KEYS ARE AVAILABLE/NOT AVAILABLE         
      -    'AS AT '.                                                            
           05  LKA1-CD-DATE.                                                    
               10 LKA1-CD-DD          PIC X(2)    VALUE SPACES.                 
               10 FILLER              PIC X       VALUE '/'.                    
               10 LKA1-CD-MM          PIC X(2)    VALUE SPACES.                 
               10 FILLER              PIC X       VALUE '/'.                    
               10 LKA1-CD-CCYY        PIC X(4)    VALUE SPACES.                 
                                                                                
       01  LKA1-HDG1.                                                           
           05  FILLER              PIC X(03)  VALUE 'S/N'.                      
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(07)  VALUE 'REGN NO'.                  
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'FLAT TYPE'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(12)  VALUE 'LEASE TENURE'.             
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(11)  VALUE 'FLAT STATUS'.              
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'HOLD TYPE'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(11)  VALUE 'ACCEPT DATE'.              
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(15)  VALUE 'KEYS AVAIL DATE'.          
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(03)  VALUE 'ECD'.                      
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'APPT TYPE'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(15)  VALUE '1ST APT LTR DTE'.          
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'APPT DATE'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(10)  VALUE 'APPLN TYPE'.               
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(09)  VALUE 'LOAN TYPE'.                
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(08)  VALUE 'PROV TAG'.                 
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(03)  VALUE 'OIC'.                      
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(08)  VALUE 'KEY LOCN'.                 
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(05)  VALUE 'PRICE'.                    
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(04)  VALUE 'TEAM'.                     
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(11)  VALUE 'MTHS OF O/S'.              
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(08)  VALUE 'CATEGORY'.                 
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(06)  VALUE 'REASON'.                   
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(13)  VALUE 'EXP CLEAR DTE'.            
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(07)  VALUE 'REMARKS'.                  
           05  FILLER              PIC X(02)  VALUE  SPACES.                    
           05  FILLER              PIC X(13)  VALUE 'LATE COMP INT'.            
                                                                                
       01  LKA1-PR-DETAIL.                                                      
           05  LKA1-SN             PIC 9(4)  VALUE ZEROS.                       
           05  WS-FIL1             PIC X(1)  VALUE ';'.                         
           05  LKA1-REGN           PIC X(08) VALUE SPACES.                      
           05  WS-FIL2             PIC X(1)  VALUE ';'.                         
           05  LKA1-FLAT-TY        PIC X(02) VALUE SPACES.                      
           05  WS-FIL3             PIC X(1)  VALUE ';'.                         
           05  LKA1-LEASE-TENURE   PIC X(02) VALUE SPACES.                      
           05  WS-FIL3A            PIC X(1)  VALUE ';'.                         
           05  LKA1-FLAT-STATUS    PIC X(02) VALUE SPACES.                      
           05  WS-FIL4             PIC X(1)  VALUE ';'.                         
           05  LKA1-HOLD-TY        PIC X(03) VALUE SPACES.                      
           05  WS-FIL5             PIC X(1)  VALUE ';'.                         
           05  LKA1-DTE-ACCEPT     PIC X(08) VALUE SPACES.                      
           05  WS-FIL6             PIC X(1)  VALUE ';'.                         
           05  LKA1-DTE-KEY-AVL    PIC X(08) VALUE SPACES.                      
           05  WS-FIL7             PIC X(1)  VALUE ';'.                         
           05  LKA1-ECD            PIC X(08) VALUE SPACES.                      
           05  WS-FIL8             PIC X(1)  VALUE ';'.                         
           05  LKA1-APPT-TY        PIC X(04) VALUE SPACES.                      
           05  WS-FIL9             PIC X(1)  VALUE ';'.                         
           05  LKA1-DTE-1ST-LET    PIC X(08) VALUE SPACES.                      
           05  WS-FIL10            PIC X(1)  VALUE ';'.                         
           05  LKA1-DTE-APPT       PIC X(08) VALUE SPACES.                      
           05  WS-FIL11            PIC X(1)  VALUE ';'.                         
           05  LKA1-APPLN-TY       PIC X(03) VALUE SPACES.                      
           05  WS-FIL12            PIC X(1)  VALUE ';'.                         
           05  LKA1-LOAN-TY        PIC X(1)  VALUE SPACES.                      
           05  WS-FIL13            PIC X(1)  VALUE ';'.                         
           05  LKA1-PROV-TAG       PIC X(1)  VALUE SPACES.                      
           05  WS-FIL14            PIC X(1)  VALUE ';'.                         
           05  LKA1-OIC            PIC X(05) VALUE SPACES.                      
           05  WS-FIL15            PIC X(1)  VALUE ';'.                         
           05  LKA1-KEY-LOCN       PIC X(04) VALUE SPACES.                      
           05  WS-FIL16            PIC X(1)  VALUE ';'.                         
           05  LKA1-PRICE          PIC 9(8)  VALUE ZEROES.                      
           05  WS-FIL17            PIC X(1)  VALUE ';'.                         
           05  LKA1-TEAM           PIC X(1)  VALUE SPACES.                      
           05  WS-FIL18            PIC X(1)  VALUE ';'.                         
           05  LKA1-MONTHS-OS      PIC X(3)  VALUE SPACES.                      
           05  WS-FIL19            PIC X(1)  VALUE ';'.                         
           05  LKA1-CATEGORY       PIC X(34) VALUE SPACES.                      
           05  WS-FIL20            PIC X(1)  VALUE ';'.                         
           05  LKA1-REASON         PIC X(100) VALUE SPACES.                     
           05  WS-FIL21            PIC X(1)  VALUE ';'.                         
           05  LKA1-CLEAR-DTE      PIC X(08) VALUE SPACES.                      
           05  WS-FIL22            PIC X(1)  VALUE ';'.                         
           05  LKA1-REMARKS        PIC X(230) VALUE SPACES.                     
           05  WS-FIL23            PIC X(1)  VALUE ';'.                         
           05  LKA1-LATE-COMP      PIC X(24) VALUE SPACES.                      
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-READ-FC40 THRU 2000-EXIT.                               
           PERFORM 3000-PROCESS   THRU 3000-EXIT                                
                   UNTIL FC40-EOF = 'Y'.                                        
           PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                         
       0000-FINISH.                                                             
            EXIT.                                                               
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
            OPEN INPUT  BM06K110                                                
                        BM06K100                                                
                        BP13FC40                                                
                        BP13K800                                                
                        BP13K024                                                
                        BB14K500                                                
                        BP13KB05                                                
                 OUTPUT BP13LKA1.                                               
                                                                                
            IF BP13K800-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K800-STATUS ' BP13K800-STATUS            
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BB14K500-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K500-STATUS ' BB14K500-STATUS            
               MOVE BB14K500-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BM06K110-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS            
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BM06K100-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K100-STATUS ' BM06K100-STATUS            
               MOVE BM06K100-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BP13K024-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, K024-STATUS ' BP13K024-STATUS            
               MOVE BP13K024-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF BP13KB05-STATUS NOT = ZEROES AND 97                              
               DISPLAY 'OPENING ERROR, KB05-STATUS ' BP13KB05-STATUS            
               MOVE BP13KB05-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
            COMPUTE WS-INT-CURR = (WS-DATE-CCYY * 12) + WS-DATE-MM.             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-READ-FC40.                                                          
      *-------------------------------------------------------------            
            READ BP13FC40  AT END MOVE 'Y' TO FC40-EOF                          
                 GO TO 2000-EXIT.                                               
            ADD 1 TO WS-FC40-READ.                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-PROCESS.                                                            
      *-------------------------------------------------------------            
           IF FC40-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              MOVE SPACES            TO LKA1-PR-DETAIL                          
                                                                                
              MOVE ';' TO  WS-FIL1  WS-FIL2  WS-FIL3  WS-FIL4  WS-FIL5          
                           WS-FIL6  WS-FIL7  WS-FIL8  WS-FIL9  WS-FIL10         
                           WS-FIL11 WS-FIL12 WS-FIL13 WS-FIL14 WS-FIL15         
                           WS-FIL16 WS-FIL17 WS-FIL18 WS-FIL19 WS-FIL20         
                           WS-FIL21 WS-FIL22 WS-FIL23 WS-FIL3A                  
                                                                                
              PERFORM 4000-PRINT-ROUTINE THRU 4000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FC40     THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-PRINT-ROUTINE.                                                      
      *-------------------------------------------------------------            
           IF WS-FC40-OUTPUT < 1                                                
              STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)             
                     DELIMITED BY SIZE INTO LKA1-DATE                           
                                                                                
              WRITE BP13LKA1-REC FROM LKA1-PR-HEAD-01                           
                                                                                
              MOVE  WS-DATE-CCYY    TO LKA1-CD-CCYY                             
              MOVE  WS-DATE-MM      TO LKA1-CD-MM                               
              MOVE  WS-DATE-DD      TO LKA1-CD-DD                               
                                                                                
              WRITE BP13LKA1-REC FROM LKA1-PR-HEAD-02                           
              WRITE BP13LKA1-REC FROM LKA1-HDG1                                 
              MOVE  SPACES       TO   BP13LKA1-REC                              
              WRITE BP13LKA1-REC                                                
           END-IF.                                                              
                                                                                
           ADD  1                                TO WS-CNT.                     
           MOVE WS-CNT                           TO LKA1-SN.                    
           MOVE FC40-NUM-REGN                    TO LKA1-REGN.                  
           MOVE FC40-NUM-OIC                     TO LKA1-OIC.                   
           MOVE FC40-DTE-PEND-CLEAR              TO LKA1-CLEAR-DTE.             
                                                                                
           EVALUATE FC40-NUM-LATE-COMP                                          
           WHEN 'A'                                                             
              MOVE 'HA NOT INFORMED YET'         TO LKA1-LATE-COMP              
           WHEN 'B'                                                             
              MOVE 'HA INFORMED, PEND REPLY'     TO LKA1-LATE-COMP              
           WHEN 'C'                                                             
              MOVE 'AGREED TO PAY'               TO LKA1-LATE-COMP              
           WHEN 'D'                                                             
              MOVE 'APPEALED TO WAIVE/REDUCE'    TO LKA1-LATE-COMP              
           WHEN OTHER                                                           
              MOVE FC40-NUM-LATE-COMP            TO LKA1-LATE-COMP              
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4100-READ-BP13K800          THRU 4100-EXIT.                  
           IF WS-K800-FND = 'Y'                                                 
              PERFORM 4200-READ-BM06K110       THRU 4200-EXIT                   
              IF WS-K110-FND = 'Y'                                              
                 PERFORM 4210-READ-BM06K100    THRU 4210-EXIT                   
                 IF WS-K100-FND = 'Y'                                           
                    PERFORM 4220-READ-BB14K500 THRU 4220-EXIT                   
                 END-IF                                                         
              END-IF                                                            
              PERFORM 4300-CALL-BP13C913       THRU 4300-EXIT                   
              IF K800-DTE-PRINCIPAL-SALES-AGMT > ZEROES                         
                 MOVE 'TP'                 TO LKA1-APPT-TY                      
              ELSE                                                              
                 MOVE 'SATP'               TO LKA1-APPT-TY                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4400-READ-BP13K024         THRU 4400-EXIT.                   
                                                                                
      **COMPUTE MTHS OF O/S BY CURRENT DATE MINUS FC40-DTE-START                
      **                                                                        
           MOVE ZEROES                        TO WS-MONTH-DIFF                  
           MOVE FC40-DTE-START                TO WS-DTE-COMPUTE                 
           COMPUTE WS-INT-START = (WS-COMPUTE-CCYY * 12) + WS-COMPUTE-MM        
           COMPUTE WS-MONTH-DIFF = WS-INT-CURR - WS-INT-START                   
           MOVE WS-MONTH-DIFF-R               TO LKA1-MONTHS-OS                 
                                                                                
           PERFORM 5000-READ-BP13KB05    THRU 5000-EXIT.                        
                                                                                
           MOVE FC40-NUM-PEND-KEY-RSN        TO WS-CDE-REASON.                  
           PERFORM 6000-CHECK-REASON       THRU 6000-EXIT.                      
           IF WS-CDE-REASON-FND = 'Y'                                           
              MOVE WS-PEND-REASON-DESC(WS-PEND-RSN-PTR)                         
                                             TO LKA1-REASON                     
              MOVE WS-PEND-REASON-CAT(WS-PEND-RSN-PTR)                          
                                             TO WS-CDE-CATEGORY                 
                                                                                
              PERFORM 6500-CHECK-CATEGORY  THRU 6500-EXIT                       
              IF WS-CDE-CATEGORY-FND = 'Y'                                      
                 MOVE WS-PEND-CATEGORY-DESC(WS-CATEGORY-PTR)                    
                                             TO LKA1-CATEGORY                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES TO BP13LKA1-REC                                          
           WRITE BP13LKA1-REC FROM LKA1-PR-DETAIL.                              
                                                                                
           ADD 1 TO WS-FC40-OUTPUT.                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4100-READ-BP13K800.                                                      
      *------------------------*                                                
           MOVE SPACES                     TO BP13K800-MASTER.                  
           INITIALIZE                         BP13K800-MASTER.                  
           MOVE 'N'                        TO WS-K800-FND.                      
                                                                                
           MOVE FC40-NUM-REGN              TO K800-NUM-REGN.                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'                     TO WS-K800-FND                       
              MOVE K800-NUM-HOUSEHOLD      TO LKA1-HOLD-TY                      
              MOVE K800-DTE-1ST-TP-LETTER  TO LKA1-DTE-1ST-LET                  
              MOVE K800-DTE-SO-APPT        TO LKA1-DTE-APPT                     
              MOVE K800-NUM-ALLO-CAT       TO LKA1-APPLN-TY                     
              MOVE K800-NUM-LOAN-TAG       TO LKA1-LOAN-TY                      
              MOVE K800-NUM-PROV-TAG       TO LKA1-PROV-TAG                     
              IF   K800-NUM-LEASE-TENURE > 0                                    
                 MOVE K800-NUM-LEASE-TENURE TO LKA1-LEASE-TENURE                
              ELSE                                                              
                 MOVE SPACES                TO LKA1-LEASE-TENURE                
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K800 : '                         
                                                K800-NUM-REGN                   
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K800 : ' BP13K800-STATUS               
              DISPLAY 'K800 KEY FIELD         : ' K800-NUM-REGN                 
              MOVE    BP13K800-STATUS      TO   RETURN-CODE                     
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4200-READ-BM06K110.                                                      
      *------------------------*                                                
           MOVE SPACES                     TO K110-REC.                         
           INITIALIZE                         K110-REC.                         
           MOVE 'N'                        TO WS-K110-FND.                      
                                                                                
           MOVE K800-NUM-SCH-ACC           TO K110-KEY-FLD.                     
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'                     TO WS-K110-FND                       
              MOVE K110-FLAT-STATUS        TO LKA1-FLAT-STATUS                  
              MOVE K110-DTE-ACCEPTANCE     TO LKA1-DTE-ACCEPT                   
              MOVE K110-DTE-KEY-AVAIL      TO LKA1-DTE-KEY-AVL                  
              MOVE K110-CURR-SELLING-PRICE TO LKA1-PRICE                        
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BM06K110 : '                         
                                                K110-KEY-FLD                    
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K110 : ' BM06K110-STATUS               
              DISPLAY 'K110 KEY FIELD         : ' K110-KEY-FLD                  
              MOVE     BM06K110-STATUS     TO   RETURN-CODE                     
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4210-READ-BM06K100.                                                      
      *------------------------*                                                
           MOVE SPACES                     TO K100-REC.                         
           INITIALIZE                         K100-REC.                         
           MOVE 'N'                        TO WS-K100-FND.                      
                                                                                
           MOVE K110-AIX1                  TO K100-KEY-FLD.                     
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE BM06K100-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'                     TO WS-K100-FND                       
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BM06K100 : '                         
                                                K100-KEY-FLD                    
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' BM06K100-STATUS               
              DISPLAY 'K100 KEY FIELD         : ' K100-KEY-FLD                  
              MOVE     BM06K100-STATUS     TO   RETURN-CODE                     
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4210-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4220-READ-BB14K500.                                                      
      *------------------------*                                                
           MOVE SPACES                     TO K500-KEY-FLD.                     
           INITIALIZE                         K500-KEY-FLD.                     
                                                                                
           MOVE K100-CDE-BLK               TO K500-KEY-FLD.                     
                                                                                
           READ BB14K500.                                                       
                                                                                
           EVALUATE BB14K500-STATUS                                             
           WHEN 00                                                              
              MOVE K500-DTE-BLDG-COMPLETE  TO LKA1-ECD                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BB14K500 : '                         
                                                K500-KEY-FLD                    
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BB14K500 : ' BB14K500-STATUS               
              DISPLAY 'K500 KEY FIELD         : ' K500-KEY-FLD                  
              MOVE     BB14K500-STATUS     TO   RETURN-CODE                     
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4220-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4300-CALL-BP13C913.                                                      
      *------------------------*                                                
           MOVE SPACES                TO BP13COMM8-REC.                         
           INITIALIZE                    BP13COMM8-REC.                         
                                                                                
           MOVE K800-NUM-SCH-ACC  TO WS-LINK-REC.                               
                                                                                
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR TO WS-SQL-CODE                              
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB TABLE  ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PIDB TABLE  ' WS-SQL-CODE               
                 MOVE 99 TO RETURN-CODE                                         
                 PERFORM 9000-CLOSING-ROUTINE  THRU 9000-EXIT                   
              END-IF                                                            
           ELSE                                                                 
              MOVE COMM8-RM-TYPE    TO LKA1-FLAT-TY                             
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4400-READ-BP13K024.                                                      
      *------------------------*                                                
           MOVE SPACES                     TO BP13K024-REC.                     
           INITIALIZE                         BP13K024-REC.                     
                                                                                
           MOVE FC40-NUM-OIC               TO K024-NUM-OIC.                     
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K024-STATUS                                             
           WHEN  00                                                             
                 MOVE K024-CDE-TEAM        TO LKA1-TEAM                         
                                                                                
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT FOUND IN BP13K024 : '                      
                                                K024-NUM-OIC                    
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13K024 : ' BP13K024-STATUS            
                 DISPLAY 'K024 OIC               : ' K024-NUM-OIC               
                 MOVE     BP13K024-STATUS       TO   RETURN-CODE                
                 PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6000-CHECK-REASON.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF WS-CDE-REASON NOT = LOW-VALUES AND SPACES                         
              SET WS-PEND-RSN-PTR          TO   1                               
              SEARCH WS-PEND-RSN                                                
                 AT END                                                         
                    MOVE 'N'               TO   WS-CDE-REASON-FND               
                 WHEN WS-PEND-REASON-CODE(WS-PEND-RSN-PTR) =                    
                      WS-CDE-REASON                                             
                    MOVE 'Y'               TO   WS-CDE-REASON-FND               
              END-SEARCH                                                        
           ELSE                                                                 
              MOVE 'N'                     TO   WS-CDE-REASON-FND               
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6500-CHECK-CATEGORY.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-CDE-CATEGORY NOT = LOW-VALUES AND SPACES                       
              SET WS-CATEGORY-PTR          TO   1                               
              SEARCH WS-CATEGORY                                                
                 AT END                                                         
                    MOVE 'N'               TO   WS-CDE-CATEGORY-FND             
                 WHEN WS-PEND-CATEGORY(WS-CATEGORY-PTR) =                       
                      WS-CDE-CATEGORY                                           
                    MOVE 'Y'               TO   WS-CDE-CATEGORY-FND             
              END-SEARCH                                                        
           ELSE                                                                 
              MOVE 'N'                     TO   WS-CDE-CATEGORY-FND             
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5000-READ-BP13KB05.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13KB05-REC.                     
           MOVE FC40-NUM-REGN              TO KB05-NUM-REGN.                    
           MOVE 'KI'                       TO KB05-NUM-MSG-TYPE.                
           MOVE FC40-DTE-MSG               TO KB05-DTE-MSG.                     
           MOVE FC40-TME-MSG               TO KB05-TME-MSG.                     
                                                                                
           READ BP13KB05.                                                       
                                                                                
           EVALUATE BP13KB05-STATUS                                             
           WHEN  00                                                             
                 MOVE KB05-TXT-MSG         TO LKA1-REMARKS                      
                                                                                
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT FOUND IN BP13KB05 : '                      
                                                KB05-KEY-FLD                    
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13KB05 : ' BP13KB05-STATUS            
                 DISPLAY 'KB05 KEY               : ' KB05-KEY-FLD               
                 MOVE     BP13KB05-STATUS       TO   RETURN-CODE                
                 PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
             DISPLAY '- BP13CKA1 CONTROL TOTAL ----------'.                     
             DISPLAY ' FC40-READ      ' WS-FC40-READ.                           
             DISPLAY ' FC40-OUTPUT    ' WS-FC40-OUTPUT.                         
             DISPLAY ' '.                                                       
                                                                                
           CLOSE      BB14K500                                                  
                      BM06K110                                                  
                      BM06K100                                                  
                      BP13FC40                                                  
                      BP13K800                                                  
                      BP13K024                                                  
                      BP13KB05                                                  
                      BP13LKA1.                                                 
                                                                                
           IF BP13K800-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K800-STATUS '                            
                        BP13K800-STATUS.                                        
                                                                                
           IF BM06K100-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K100-STATUS '                            
                        BM06K100-STATUS.                                        
                                                                                
           IF BM06K110-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K110-STATUS '                            
                        BM06K110-STATUS.                                        
                                                                                
           IF BB14K500-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K500-STATUS '                            
                        BB14K500-STATUS.                                        
                                                                                
           IF BP13K024-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, K024-STATUS '                            
                        BP13K024-STATUS.                                        
                                                                                
           IF BP13KB05-STATUS NOT = 0 AND NOT = 97                              
              DISPLAY ' CLOSING ERROR, KB05-STATUS '                            
                        BP13KB05-STATUS.                                        
                                                                                
             STOP RUN.                                                          
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
