       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C894.                                                  
      *DATE-WRITTEN. 10/07/2006.                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:  TO DETERMINE SERS HOUSEHOLD TYPE              *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-NO   CHG-DATE  BY   DESCRIPTION                        *            
      * ------   --------  --   -----------                        *            
      * BP132893 10072006 YCH2  UPD SERS HOUSEHOLD TYPE TO BP13K740*            
      * BP135168 10122013 YCH2  MOVE FT2T TO K740                  *            
      * BP137208 12022018 YCH2  UPDATE ELDERLY TAG AND 3GEN        *            
      * BP137724 21062019 AM25  UPDATE HALF-IDP                    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K740-KEY-FLD                               
                  ALTERNATE RECORD KEY                                          
                                  IS K740-NUM-REGN                              
                  FILE STATUS     IS BP13K740-STATUS.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F500                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F500.                                                           
                                                                                
       FD   BP13K740                                                            
                                   RECORD CONTAINS 500 CHARACTERS.              
       COPY BP13K740.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K740-STATUS             PIC 9(2)    VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F500-READ            PIC 9(8)    VALUE ZEROES.                
           05  WS-K740-REWRITE         PIC 9(8)    VALUE ZEROES.                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CUR-DATE             PIC X(8)    VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
           PERFORM 1000-INITIALIZATION  THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F500   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-RECORD  THRU 3000-EXIT                          
             UNTIL F500-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F500                                                 
               I-O     BP13K740.                                                
                                                                                
           IF BP13K740-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K740 - OPEN FILE ERROR, ' BP13K740-STATUS            
              MOVE BP13K740-STATUS     TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F500.                                                      
      *-------------------------------------------------------------            
           READ BP13F500 AT END                                                 
               MOVE HIGH-VALUES TO F500-NUM-REGN                                
               GO TO 2000-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F500-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE F500-NUM-REGN TO K740-NUM-REGN.                                 
           READ BP13K740 KEY IS K740-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K740-STATUS                                             
              WHEN  00                                                          
                    PERFORM 3100-UPDATE-K740 THRU 3100-EXIT                     
              WHEN  23                                                          
                    DISPLAY 'RECORD NOT FOUND IN K740 ' K740-NUM-REGN           
              WHEN  OTHER                                                       
                    DISPLAY 'READ  K740 ERROR ' BP13K740-STATUS                 
                    DISPLAY 'KEY IS ' K740-NUM-REGN                             
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F500   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3100-UPDATE-K740.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE F500-CDE-HOUSEHOLD TO K740-CDE-HOUSEHOLD.                       
           MOVE F500-NUM-FT2T      TO K740-NUM-FT2T.                            
           MOVE F500-NUM-HALF-IDP  TO K740-NUM-HALF-IDP.                        
                                                                                
           IF F500-CDE-FLAT-TYPE = '5 '                                         
              MOVE F500-NUM-GEN3      TO K740-NUM-GEN3                          
           END-IF.                                                              
                                                                                
           IF F500-CDE-FLAT-TYPE = '2F'                                         
              IF F500-NUM-ELDERLY NOT = SPACES AND LOW-VALUES                   
                 MOVE F500-NUM-ELDERLY TO K740-NUM-ELDERLY                      
                 MOVE F500-NUM-NTIMER  TO K740-NUM-NTIMER                       
                 MOVE F500-NUM-PPO     TO K740-NUM-PPO                          
              END-IF                                                            
           END-IF                                                               
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE BP13K740-STATUS                                             
             WHEN   00                                                          
                    ADD 1 TO WS-K740-REWRITE                                    
             WHEN   OTHER                                                       
                    DISPLAY ' ERROR UPDATING BP13K740 REC '                     
                         ' STATUS ' BP13K740-STATUS                             
                    DISPLAY ' KEY IS ' K740-NUM-REGN                            
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C894 ------------- * '.                  
           DISPLAY ' '.                                                         
           DISPLAY 'CURRENT DATE : ' WS-CUR-DATE.                               
           DISPLAY 'NO OF REC READ BP13F500     = ' WS-F500-READ.               
           DISPLAY 'NO OF K740 UPDATED          = ' WS-K740-REWRITE.            
                                                                                
           CLOSE BP13F500                                                       
                 BP13K740.                                                      
                                                                                
           IF BP13K740-STATUS NOT = 00                                          
              DISPLAY 'BP13K740 - CLOSE FILE ERROR, ' BP13K740-STATUS           
              MOVE BP13K740-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
