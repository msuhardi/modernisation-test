      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C424.                                                 
       AUTHOR.        JOEL E PARRENO.                                           
       DATE-WRITTEN.  21/07/93.                                                 
       DATE-COMPILED.                                                           
      * ======================================================= *               
      *              SYSTEM OF COMMITMENT  (BP13)               *               
      * ======================================================= *               
      *                                                         *               
      *    OBJECTIVE   :                                        *               
      *      1.  THIS IS A PROGRAM TO PRINT BLOCK WHICH EQUAL   *               
      *          FIRST DATE OF SALES FOR FLAT FOR THAT MONTH.   *               
      *                                                         *               
      *     INPUT FILES :                                       *               
      *       1.  BM06K100   (BLOCK  FILE)                      *               
      *       2.  BM06F260   (DATE   FILE)                      *               
      *       3.  BM06K510   (ESTATE FILE)                      *               
      *                                                         *               
      *     OUTPUT LIST :                                       *               
      *       1.   BP13L424                                     *               
      *                                                         *               
      * CHG-NO   DATE   BY   DESCRIPTION                        *               
      * ------   ------ --   -----------                        *               
      *          951101 SSS  CHG POSITION OF LISTING ID         *               
      * BP130037 951201 LMS  TO CATER FOR LOW-VALUES FOR 1ST    *               
      *                      ISSUE DATE.                        *               
      * BP130128 960522 LJL  TO AMEND ADDRESS OF VALUATION AND  *               
      *                      ALIENATION UNIT.                   *               
      * BP130418 980310 SIA  CHGE BM06K210 TO BM06K100          *               
      * ======================================================= *               
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K100  ASSIGN       TO BM06K100                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS K100-STATUS.                        
                                                                                
           SELECT BM06F260  ASSIGN       TO BM06F260.                           
                                                                                
           SELECT BM06K510  ASSIGN       TO BM06K510                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K510-ESTCDE                         
                            FILE STATUS  IS K510-STATUS.                        
                                                                                
           SELECT BP13L424  ASSIGN       TO BP13L424.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BM06K100                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06F260                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 10 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD.                                         
       COPY BM06F260.                                                           
                                                                                
       FD   BM06K510                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 60 CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD   BP13L424                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL  RECORD   IS  OMITTED                                         
            DATA   RECORD   IS  L501-REC.                                       
       01   L424-REC                 PIC X(132).                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  K100-STATUS           PIC 99      VALUE ZEROES.                  
           05  K510-STATUS           PIC 99      VALUE ZEROES.                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-K100           PIC X(3)    VALUE 'NO'.                    
               88  EOF-OF-K100                   VALUE 'YES'.                   
           05  WS-EOF-K510           PIC X(3)    VALUE 'NO'.                    
               88  EOF-OF-K510                   VALUE 'YES'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-LINE-CTR           PIC 99      VALUE 61.                      
           05  WS-PAGE-CTR           PIC 9(4)    VALUE 0.                       
           05  WS-READ-K100          PIC 9(4)    VALUE 0.                       
                                                                                
       01  WS-DATE                   PIC X(8)    VALUE SPACES.                  
                                                                                
       01  WS-1ST-ISSUE-DATE.                                                   
           05  WS-1ST-ISSUE-CC       PIC 99      VALUE 00.                      
           05  WS-1ST-ISSUE-YY       PIC 99      VALUE 00.                      
           05  WS-1ST-ISSUE-MM       PIC 99      VALUE 00.                      
           05  WS-1ST-ISSUE-DD       PIC 99      VALUE 00.                      
                                                                                
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
                                                                                
           05  L424-HDG-LINE.                                                   
               10  FILLER            PIC X(8)    VALUE 'BP13L424'.              
               10  FILLER            PIC X(25)   VALUE SPACES.                  
               10  FILLER            PIC X(43)   VALUE                          
                   'S Y S T E M     O F     C O M M I T M E N T'.               
               10  FILLER            PIC X(18)   VALUE SPACES.                  
               10  FILLER            PIC X(8)    VALUE 'DATE :'.                
               10  L424-DTE          PIC X(10)   VALUE SPACES.                  
               10  FILLER            PIC X(8)    VALUE SPACES.                  
               10  FILLER            PIC X(7)    VALUE 'PAGE : '.               
               10  L424-PAGE-CTR     PIC ZZZ9.                                  
                                                                                
           05  L424-SHDG-LINE.                                                  
               10  FILLER            PIC X(94)   VALUE SPACES.                  
               10  FILLER            PIC X(28)   VALUE                          
                   'USER :  SALES AGREEMENT TEAM'.                              
                                                                                
           05  L424-WORD-LINE.                                                  
               10  FILLER            PIC X(5)    VALUE SPACES.                  
               10  L424-WORD-DESC    PIC X(50)   VALUE SPACES.                  
                                                                                
           05  L424-WORD-LINE2.                                                 
               10  FILLER            PIC X(11)   VALUE SPACES.                  
               10  FILLER            PIC X(52)   VALUE                          
                  'TECHNICAL RESOURCE & STRATA SURVEY APPRAISAL UNIT'.          
                                                                                
           05  L424-WORD-LINE3.                                                 
               10  FILLER            PIC X(11)   VALUE SPACES.                  
               10  FILLER            PIC X(40)   VALUE                          
                   'BUILDING QUALITY DEPARTMENT'.                               
                                                                                
           05  L424-DETL-TITLE1.                                                
               10  FILLER            PIC X(11)   VALUE SPACES.                  
               10  FILLER            PIC X(27)   VALUE 'ESTATE'.                
               10  FILLER            PIC X(10)   VALUE SPACES.                  
               10  FILLER            PIC X(13)   VALUE 'NEIGHBOURHOOD'.         
               10  FILLER            PIC X(10)   VALUE SPACES.                  
               10  FILLER            PIC X(8)    VALUE 'CONTRACT'.              
               10  FILLER            PIC X(10)   VALUE SPACES.                  
               10  FILLER            PIC X(5)    VALUE 'BLOCK'.                 
               10  FILLER            PIC X(10)   VALUE SPACES.                  
               10  FILLER            PIC X(13)   VALUE 'FIRST DATE OF'.         
                                                                                
           05  L424-DETL-TITLE2.                                                
               10  FILLER            PIC X(109)  VALUE SPACES.                  
               10  FILLER            PIC X(4)    VALUE 'SALE'.                  
                                                                                
           05  L424-DETL-LINE.                                                  
               10  FILLER            PIC X(11)   VALUE SPACES.                  
               10  L424-ESTATE       PIC X(27)   VALUE SPACES.                  
               10  FILLER            PIC X(15)   VALUE SPACES.                  
               10  L424-NEIGHBOUR    PIC X(4)    VALUE SPACES.                  
               10  FILLER            PIC X(16)   VALUE SPACES.                  
               10  L424-CONTRACT     PIC X(4)    VALUE SPACES.                  
               10  FILLER            PIC X(12)   VALUE SPACES.                  
               10  L424-BLOCK        PIC X(5)    VALUE SPACES.                  
               10  FILLER            PIC X(13)   VALUE SPACES.                  
               10  L424-DATE-SALE.                                              
                   15  L424-DATE-DD  PIC X(2)    VALUE SPACES.                  
                   15  L424-DATE-S1  PIC X       VALUE '/'.                     
                   15  L424-DATE-MM  PIC X(2)    VALUE SPACES.                  
                   15  L424-DATE-S2  PIC X       VALUE '/'.                     
                   15  L424-DATE-CC  PIC X(2)    VALUE SPACES.                  
                   15  L424-DATE-YY  PIC X(2)    VALUE SPACES.                  
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *    MAIN LOGIC PROCESS SECTION                                  *        
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
           PERFORM  100-OPEN-ROUTINE        THRU   100-EXIT.                    
           PERFORM  200-START-ROUTINE       THRU   200-EXIT.                    
           PERFORM  300-READ-BM06K100       THRU   300-EXIT.                    
           PERFORM  400-PROCESS-PRINTING    THRU   400-EXIT                     
                        UNTIL WS-EOF-K100 = 'YES'.                              
           PERFORM  700-PRINT-FOOTINGS      THRU   700-EXIT                     
           PERFORM  900-CLOSE-ROUTINE       THRU   900-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                       *        
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
           OPEN INPUT   BM06K100                                                
                        BM06F260                                                
                        BM06K510                                                
                OUTPUT  BP13L424.                                               
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BM06K100, K100-STATUS = '                  
                                               K100-STATUS                      
              MOVE K100-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           IF K510-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BM06K510, K510-STATUS = '                  
                                               K510-STATUS                      
              MOVE K510-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    SET READ POINTER FOR VSAM FILE BM06K100 TO THE BEGINNING    *        
      *        AND  ALSO READ SEQUENTIAL FILE BM06F260                 *        
      ******************************************************************        
       200-START-ROUTINE.                                                       
           MOVE  ZEROES      TO K100-ESTATE.                                    
           MOVE  SPACES      TO K100-NEIGHBOURHOOD                              
                                K100-CONTRACT-NO                                
                                K100-BLK-NO.                                    
                                                                                
           START BM06K100 KEY > K100-KEY-FLD.                                   
                                                                                
           IF K100-STATUS NOT = 00 AND 96                                       
              DISPLAY 'ERROR START BM06K100, K100-STATUS = ' K100-STATUS        
              MOVE K100-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           READ BM06F260.                                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *   READ VSAM FILE BM06K100 AT END SET EOF-IND TO 'YES'          *        
      ******************************************************************        
       300-READ-BM06K100.                                                       
           READ BM06K100 NEXT AT END                                            
                              MOVE 'YES' TO WS-EOF-K100.                        
                              GO TO 300-EXIT.                                   
                                                                                
           ADD 1 TO WS-READ-K100.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    - CHECK THE MONTH OF BM06F260 AND BM06K100-1ST-ISSUE-DATE   *        
      *            IF  IT IS EQUAL, THEN PROCEED PROCESSING RECORD     *        
      *    - READ VSAM FILE BM06K100 UNTIL END OF FILE IS REACHED      *        
      ******************************************************************        
       400-PROCESS-PRINTING.                                                    
           IF K100-DTE-1ST-KEY-ISSUE IS NOT NUMERIC                             
              MOVE ZEROES TO WS-1ST-ISSUE-DATE                                  
           ELSE                                                                 
              MOVE K100-DTE-1ST-KEY-ISSUE TO WS-1ST-ISSUE-DATE                  
           END-IF.                                                              
                                                                                
           IF (F260-MM  =  WS-1ST-ISSUE-MM) AND                                 
              (F260-YY  =  WS-1ST-ISSUE-YY) AND                                 
              (F260-CC  =  WS-1ST-ISSUE-CC)                                     
              PERFORM 500-READ-BM06K510   THRU 500-EXIT                         
              PERFORM 600-PRINT-DETL-LINE THRU 600-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 300-READ-BM06K100      THRU 300-EXIT.                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ VSAM FILE BM06K510 AND MOVE NECESSARY VALUE TO         *        
      *         PRINT-ESTATE FIELD ACCORDING TO THE K510-STATUS        *        
      ******************************************************************        
       500-READ-BM06K510.                                                       
           MOVE K100-ESTATE TO K510-ESTCDE.                                     
                                                                                
           READ BM06K510.                                                       
                                                                                
           IF K510-STATUS = 00                                                  
              MOVE K510-ESTATE-NAME TO L424-ESTATE                              
           ELSE                                                                 
           IF K510-STATUS = 23                                                  
              MOVE SPACES           TO L424-ESTATE                              
           ELSE                                                                 
              DISPLAY 'ERROR READ BM06K510, K510-STATUS = ' K510-STATUS         
              DISPLAY '                     K510-KEY    = ' K510-ESTCDE         
              MOVE K510-STATUS  TO RETURN-CODE                                  
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINTS THE REPORT DETAIL LINE                               *        
      ******************************************************************        
       600-PRINT-DETL-LINE.                                                     
           IF WS-LINE-CTR > 50                                                  
              PERFORM 610-PRINT-HEADINGS THRU 610-EXIT.                         
                                                                                
           MOVE  K100-NEIGHBOURHOOD      TO   L424-NEIGHBOUR.                   
           MOVE  K100-CONTRACT-NO        TO   L424-CONTRACT.                    
           MOVE  K100-BLK-NO             TO   L424-BLOCK.                       
           MOVE  WS-1ST-ISSUE-DD         TO   L424-DATE-DD.                     
           MOVE  WS-1ST-ISSUE-MM         TO   L424-DATE-MM.                     
           MOVE  WS-1ST-ISSUE-CC         TO   L424-DATE-CC.                     
           MOVE  WS-1ST-ISSUE-YY         TO   L424-DATE-YY.                     
           IF    WS-1ST-ISSUE-DATE NOT = SPACES OR LOW-VALUES                   
                 MOVE '/' TO   L424-DATE-S1   L424-DATE-S2.                     
           WRITE L424-REC FROM L424-DETL-LINE AFTER ADVANCING 1.                
           ADD   1                       TO   WS-LINE-CTR.                      
           MOVE  SPACES                  TO   L424-DETL-LINE.                   
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINTS THE HEADING LINES                                    *        
      ******************************************************************        
       610-PRINT-HEADINGS.                                                      
           ADD    1                            TO    WS-PAGE-CTR.               
           MOVE   WS-PAGE-CTR                  TO    L424-PAGE-CTR.             
           MOVE   FUNCTION CURRENT-DATE        TO    WS-DATE.                   
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L424-DTE.                              
           WRITE  L424-REC FROM L424-HDG-LINE  AFTER ADVANCING PAGE.            
           WRITE  L424-REC FROM L424-SHDG-LINE AFTER ADVANCING 1.               
                                                                                
           IF WS-PAGE-CTR = 1                                                   
              MOVE  'TO:   HOU             '      TO    L424-WORD-DESC          
              WRITE  L424-REC FROM L424-WORD-LINE  AFTER 4                      
              WRITE  L424-REC FROM L424-WORD-LINE2 AFTER 1                      
              WRITE  L424-REC FROM L424-WORD-LINE3 AFTER 1.                     
                                                                                
           MOVE  'FIRST DATE OF SALE FOR FLATS SOLD' TO L424-WORD-DESC.         
           WRITE L424-REC FROM L424-WORD-LINE  AFTER 3.                         
                                                                                
           WRITE L424-REC FROM L424-DETL-TITLE1 AFTER ADVANCING 3.              
           WRITE L424-REC FROM L424-DETL-TITLE2 AFTER ADVANCING 1.              
                                                                                
           MOVE  13   TO  WS-LINE-CTR.                                          
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINTS THE FOOTING LINES                                    *        
      ******************************************************************        
       700-PRINT-FOOTINGS.                                                      
           MOVE  'FOR HEAD, SALES UNIT'         TO    L424-WORD-DESC.           
           WRITE  L424-REC FROM L424-WORD-LINE  AFTER ADVANCING 5.              
                                                                                
           MOVE  'ESTATE ADMIN & PROPERTY DEPT' TO    L424-WORD-DESC.           
           WRITE  L424-REC FROM L424-WORD-LINE  AFTER ADVANCING 1.              
                                                                                
           MOVE  'ESTATES GROUP           '     TO    L424-WORD-DESC.           
           WRITE  L424-REC FROM L424-WORD-LINE  AFTER ADVANCING 1.              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CLOSE ALL FILES   AND   STOP RUN                            *        
      ******************************************************************        
       900-CLOSE-ROUTINE.                                                       
                                                                                
           CLOSE   BM06K100                                                     
                   BM06F260                                                     
                   BM06K510                                                     
                   BP13L424.                                                    
                                                                                
           IF K100-STATUS NOT = 00                                              
              DISPLAY 'ERROR CLOSING BM06K100, K100-STATUS = '                  
                                               K100-STATUS                      
              MOVE K100-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K510-STATUS NOT = 00                                              
              DISPLAY 'ERROR CLOSING BM06K510, K510-STATUS = '                  
                                               K510-STATUS                      
              MOVE K510-STATUS TO RETURN-CODE.                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
