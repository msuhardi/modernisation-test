                                                                                
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C251.                                                    
       AUTHOR. JAI.                                                             
       DATE-WRITTEN. 18/11/94.                                                  
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  GET RECORDS FROM F230 AND F240 AND PUT TO AN  *                
      *          OUTPUT FILE WHERE F230-DTE-ACCEPT EQUAL TO    *                
      *          CURRENT DATE AND F240-DTE-UPDATE  EQUAL TO    *                
      *          CURRENT DATE.                                 *                
      *  CHG REQ    DATE   OIC DESCRIPTION                     *                
      * --------- -------- --- ------------------------------- *                
      * BP130369  130997   LMS TO READ CURRENT DATE FROM       *                
      *                        SY02F001 INSTEAD OF SYSTEM DATE.*                
      * BP130956  100101   LMS TO CHANGE F230/F240 FROM 130 TO *                
      *                        250.                            *                
      * BP132278  280303   JF4 CHGE BP13F230/F240 LRECL TO 450.*                
      * BP139777  050424  MRR5 REMOVE DATE CHECKING            *                
      *                        (FILTERING DONE IN JCL)         *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F230   ASSIGN       TO BP13F230.                          
           SELECT BP13F240   ASSIGN       TO BP13F240.                          
           SELECT P13F230A   ASSIGN       TO P13F230A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F230                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13K230.                                                           
                                                                                
       FD   BP13F240                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13K240.                                                           
                                                                                
       FD   P13F230A                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F230A-REC           PIC X(450).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F230-READ            PIC 9(08) VALUE 0.                           
       01  WS-F240-READ            PIC 9(08) VALUE 0.                           
       01  WS-F230A-WRITE          PIC 9(08) VALUE 0.                           
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-CC                PIC 9(02).                                   
           05 WS-SYS-DATE.                                                      
              10 WS-YY             PIC 9(02).                                   
              10 WS-MM             PIC 9(02).                                   
              10 WS-DD             PIC 9(02).                                   
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      **********                                                                
       000-MAIN.                                                                
      **********                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F230-REC THRU 200-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-F230  THRU 300-EXIT                              
                   UNTIL K230-CDE-INTV = HIGH-VALUE.                            
                                                                                
           PERFORM 400-READ-F240-REC THRU 400-EXIT.                             
                                                                                
           PERFORM 500-PROCESS-F240  THRU 500-EXIT                              
                   UNTIL K240-CDE-INTV = HIGH-VALUE.                            
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILES.                                                          
      ****************                                                          
                                                                                
           OPEN INPUT  BP13F230                                                 
                       BP13F240.                                                
           OPEN OUTPUT P13F230A.                                                
                                                                                
           ACCEPT WS-SYS-DATE FROM DATE.                                        
                                                                                
           IF WS-YY > '93'                                                      
              MOVE '19'  TO WS-CC                                               
           ELSE                                                                 
              MOVE '20'  TO WS-CC                                               
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       200-READ-F230-REC.                                                       
      *******************                                                       
                                                                                
           READ BP13F230 AT END                                                 
                MOVE HIGH-VALUE TO K230-CDE-INTV                                
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F230-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================*                          
      *       DOCUMENTATION                          *                          
      *==============================================*                          
                                                                                
      *****************                                                         
       300-PROCESS-F230.                                                        
      *****************                                                         
                                                                                
           IF  K230-CDE-DETAIL = '02'                                           
               MOVE K230-DETAIL-REC  TO P13F230A-REC                            
               WRITE P13F230A-REC                                               
               ADD  1                TO  WS-F230A-WRITE                         
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F230-REC THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       400-READ-F240-REC.                                                       
      *******************                                                       
                                                                                
           READ BP13F240 AT END                                                 
                MOVE HIGH-VALUE TO K240-CDE-INTV                                
                GO TO 400-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F240-READ.                                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************                                                         
       500-PROCESS-F240.                                                        
      *****************                                                         
                                                                                
           IF  K240-CDE-HEADER  = '01'                                          
               MOVE K240-HEADER-REC  TO P13F230A-REC                            
               WRITE P13F230A-REC                                               
               ADD  1                TO WS-F230A-WRITE                          
           END-IF.                                                              
                                                                                
           PERFORM 400-READ-F240-REC THRU 400-EXIT.                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       900-CLOSE-ROUTINE.                                                       
      ******************                                                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C251 '.                                    
           DISPLAY '                                      '.                    
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM  F230  ' WS-F230-READ.              
           DISPLAY 'NO OF RECORDS READ FROM  F240  ' WS-F240-READ.              
           DISPLAY 'NO OF RECORDS WRITTEN TO F230A ' WS-F230A-WRITE.            
                                                                                
           CLOSE BP13F230                                                       
                 BP13F240                                                       
                 P13F230A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
