       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C91E.                                                 
      *AUTHOR.        SHEILA MAE PATIAG.                                        
      *DATE-WRITTEN.  16/03/2012.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  SELECT CHILD UNIT BASED                        *         
      *                                                               *         
      *  INPUT      :  BP13F10P  BP13.F100.MGPUNIT.PARENT             *         
      *                BP13FLE2  BP13.F100.SLCTUNIT.CHILD             *         
      *  OUTPUT     :  BP13FLE3  BP13.F100.CHILD.OPTUNIT              *         
      *                BP13FREM  BP13.F100.CHILD.REMUNIT              *         
      *                                                               *         
      *===============================================================*         
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP134482 SMR2 16032012 NEW PROGRAM                            *         
      * BP134558 SMR2 25052012 WRITE BP13FREM IF BP13F10P & BP13FLE2  *         
      *                        (NT, NEIGH, CNTRCT) IS EQUAL           *         
      * BP134558 SMR2 13062012 INCLUDE BLOCK IN WS-F10P-KEY-FLD AND   *         
      *                        WS-FLE2-KEY-FLD                        *         
      * BP134558 SMR2 14062012 REMOVE BLOCK IN WS-F10P-PR-KEY AND     *         
      *                        WS-FLE2-PR-KEY                         *         
      * BP134627 IL5  09082012 REMOVE WS-F10P-PR-KEY AND              *         
      *                        WS-FLE2-PR-KEY                         *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F10P  ASSIGN       TO BP13F10P.                           
           SELECT BP13FLE2  ASSIGN       TO BP13FLE2.                           
           SELECT BP13FLE3  ASSIGN       TO BP13FLE3.                           
           SELECT BP13FREM  ASSIGN       TO BP13FREM.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F10P                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       COPY BP13F100.                                                           
                                                                                
       FD  BP13FLE2                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01  BP13FLE2-REC.                                                        
           05 FLE2-NUM-HDB-REF       PIC X(11).                                 
           05 FILLER                 PIC X(07).                                 
           05 FLE2-NUM-RANDOM        PIC X(28).                                 
           05 FLE2-NUM-PRIO          PIC X(01).                                 
           05 FLE2-NUM-FT-BALLOT     PIC X(02).                                 
           05 FLE2-NUM-PRIO-UPDATE   PIC X(01).                                 
           05 FILLER                 PIC X(05).                                 
           05 FLE2-NUM-ESTATE        PIC X(03).                                 
           05 FLE2-NUM-NGHBRD        PIC X(04).                                 
           05 FLE2-NUM-CNTRCT        PIC X(04).                                 
           05 FLE2-NUM-BLOCK         PIC X(05).                                 
           05 FLE2-NUM-TYP-ACTUSE    PIC X(02).                                 
           05 FLE2-NUM-MODL-DESG     PIC X(02).                                 
           05 FLE2-NME-STREET        PIC X(32).                                 
           05 FLE2-NUM-UNIT.                                                    
              10 FLE2-NUM-LEVEL      PIC X(02).                                 
              10 FLE2-NUM-MAIN       PIC X(04).                                 
              10 FLE2-NUM-SUB        PIC X(03).                                 
           05 FLE2-NUM-WARD          PIC X(03).                                 
           05 FLE2-NUM-NT            PIC X(03).                                 
           05 FLE2-NUM-ALLO-CAT      PIC X(03).                                 
           05 FLE2-DTE-BALLOT        PIC X(06).                                 
           05 FILLER                 PIC X(669).                                
                                                                                
       FD  BP13FLE3                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01  BP13FLE3-REC           PIC X(800).                                   
                                                                                
       FD  BP13FREM                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01  BP13FREM-REC           PIC X(800).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-F10P-EOF             PIC X(01) VALUE 'N'.                          
          88 F10P-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-FLE2-EOF             PIC X(01) VALUE 'N'.                          
          88 FLE2-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F10P-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-FLE2-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-FLE3-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-FREM-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-F10P-NTFND-CNT       PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-F10P-KEY-FLD.                                                      
          05 WS-F10P-NT        PIC X(03).                                       
          05 WS-F10P-NEIGH     PIC X(04).                                       
          05 WS-F10P-CNTRCT    PIC X(04).                                       
          05 WS-F10P-BLK       PIC X(05).                                       
          05 WS-F10P-LEVEL     PIC X(02).                                       
                                                                                
       01 WS-FLE2-KEY-FLD.                                                      
          05 WS-FLE2-NT        PIC X(03).                                       
          05 WS-FLE2-NEIGH     PIC X(04).                                       
          05 WS-FLE2-CNTRCT    PIC X(04).                                       
          05 WS-FLE2-BLK       PIC X(05).                                       
          05 WS-FLE2-LEVEL     PIC X(02).                                       
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F10P      THRU 2000-EXIT.                      
           PERFORM 3000-READ-BP13FLE2      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
             UNTIL FLE2-END-OF-FILE.                                            
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F10P                                                 
                       BP13FLE2                                                 
                OUTPUT BP13FLE3                                                 
                       BP13FREM.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F10P.                                                      
      *---------------------------------------------------------------*         
           READ BP13F10P                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F10P-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE F100-NUM-NT                TO WS-F10P-NT.                       
           MOVE F100-NUM-NGHBRD            TO WS-F10P-NEIGH.                    
           MOVE F100-NUM-CNTRCT            TO WS-F10P-CNTRCT.                   
           MOVE F100-NUM-BLOCK             TO WS-F10P-BLK.                      
           MOVE F100-NUM-LEVEL             TO WS-F10P-LEVEL.                    
                                                                                
           ADD 1                           TO WS-F10P-READ-CNT.                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13FLE2.                                                      
      *---------------------------------------------------------------*         
           READ BP13FLE2                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-FLE2-EOF                       
                GO TO 3000-EXIT.                                                
                                                                                
           MOVE FLE2-NUM-NT                TO WS-FLE2-NT.                       
           MOVE FLE2-NUM-NGHBRD            TO WS-FLE2-NEIGH.                    
           MOVE FLE2-NUM-CNTRCT            TO WS-FLE2-CNTRCT.                   
           MOVE FLE2-NUM-BLOCK             TO WS-FLE2-BLK.                      
           MOVE FLE2-NUM-LEVEL             TO WS-FLE2-LEVEL.                    
                                                                                
           ADD 1                           TO WS-FLE2-READ-CNT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           IF F10P-END-OF-FILE                                                  
              WRITE BP13FREM-REC        FROM BP13FLE2-REC                       
              ADD 1                     TO WS-FREM-WRITE-CNT                    
              PERFORM 3000-READ-BP13FLE2   THRU 3000-EXIT                       
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
           WHEN WS-FLE2-KEY-FLD = WS-F10P-KEY-FLD                               
              WRITE BP13FLE3-REC           FROM BP13FLE2-REC                    
              ADD 1                        TO WS-FLE3-WRITE-CNT                 
              PERFORM 3000-READ-BP13FLE2   THRU 3000-EXIT                       
           WHEN WS-FLE2-KEY-FLD < WS-F10P-KEY-FLD                               
              WRITE BP13FREM-REC        FROM BP13FLE2-REC                       
              ADD 1                     TO WS-FREM-WRITE-CNT                    
              PERFORM 3000-READ-BP13FLE2   THRU 3000-EXIT                       
           WHEN WS-FLE2-KEY-FLD > WS-F10P-KEY-FLD                               
              PERFORM 2000-READ-BP13F10P   THRU 2000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           CLOSE   BP13F10P                                                     
                   BP13FLE2                                                     
                   BP13FLE3                                                     
                   BP13FREM.                                                    
                                                                                
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C91E       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F10P-READ-CNT           TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F10P  : ' WS-EDIT1.                
           MOVE WS-FLE2-READ-CNT           TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13FLE2  : ' WS-EDIT1.                
           MOVE WS-FLE3-WRITE-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13FLE3 : ' WS-EDIT1.                
           MOVE WS-FREM-WRITE-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13FREM : ' WS-EDIT1.                
           MOVE WS-F10P-NTFND-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS NTFND FROM BP13F10P : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
