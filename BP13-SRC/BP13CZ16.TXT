      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13CZ16.                                                    
      *AUTHOR. ZARITA DANE SARMIENTO.                                           
      *DATE-WRITTEN.   16/03/2012.                                              
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730  ASSIGN        TO BP13F730.                          
           SELECT P13F730N  ASSIGN        TO P13F730N.                          
                                                                                
           SELECT AB03F089   ASSIGN TO AB03F089.                                
           SELECT BP13F725   ASSIGN TO BP13F725.                                
           SELECT P13F725A   ASSIGN TO P13F725A.                                
                                                                                
           SELECT BP13K730   ASSIGN        TO BP13K730                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K730-KEY-NEW                      
                             FILE STATUS   IS WS-K730-STATUS.                   
                                                                                
           SELECT BP13K813   ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
           SELECT BM06K510   ASSIGN        TO BM06K510                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K510-ESTCDE                       
                             FILE STATUS   IS WS-K510-STATUS.                   
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F730.                                                           
                                                                                
       FD  P13F730N                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  P13F730N-REC  PIC X(500).                                            
                                                                                
       FD  BP13K730                                                             
                               RECORD CONTAINS 500 CHARACTERS.                  
       COPY BP13K730.                                                           
                                                                                
       FD  BP13K813                                                             
                               RECORD CONTAINS 1000 CHARACTERS.                 
       COPY BP13K813.                                                           
                                                                                
       FD  BM06K510                                                             
                               RECORD CONTAINS 60  CHARACTERS.                  
       COPY BM06K510.                                                           
                                                                                
       FD   AB03F089                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY AB03F089.                                                           
                                                                                
       FD   BP13F725                                                            
            RECORD CONTAINS 211  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F725.                                                           
                                                                                
       FD   P13F725A                                                            
            RECORD CONTAINS 211  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       01   P13F725A-REC     PIC X(211).                                        
                                                                                
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F730              PIC 9(8)    VALUE 0.                    
           05 WS-READ-K730              PIC 9(8)    VALUE 0.                    
           05 WS-UPD-K730               PIC 9(8)    VALUE 0.                    
           05 WS-READ-F089              PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-K730-UPD               PIC 9(8)    VALUE 0.                    
           05 WS-F730-N                 PIC 9(8)    VALUE 0.                    
           05 WS-F730-NOTMAT            PIC 9(8)    VALUE 0.                    
           05 WS-F089-NOTMAT            PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F725             PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F725A            PIC 9(8)    VALUE 0.                    
           05 WS-SAME-NT                PIC 9(08)   VALUE ZEROES.               
           05 WS-F730-STATUS            PIC 9(02)   VALUE ZEROES.               
           05 WS-K730-STATUS            PIC 9(02)   VALUE ZEROES.               
           05 WS-K510-STATUS            PIC 9(02)   VALUE ZEROES.               
           05 WS-K813-STATUS            PIC 9(02)   VALUE ZEROES.               
           05 WS-K813-NT                PIC X(03)   VALUE ZEROES.               
           05 WS-K510-NT                PIC X(03)   VALUE SPACES.               
           05 WS-SAME-NT-FOUND          PIC X(01)   VALUE 'N'.                  
           05 WS-F730-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K730-FOUND             PIC X(1)    VALUE 'N'.                  
                                                                                
       01  WS-EDITED-DATE               PIC X(10).                              
                                                                                
       01 WS-CURR-DATE                  PIC X(08) VALUE ZEROES.                 
       01 WS-NUM-CURR-DATE  REDEFINES WS-CURR-DATE.                             
          05 WS-CURR-CCYYMM             PIC 9(06).                              
          05 WS-CURR-DD                 PIC 9(02).                              
       01 WS-SALE-EFF-DATE.                                                     
          05 WS-SALE-EFF-CCYYMM         PIC 9(06).                              
          05 WS-SALE-EFF-DD             PIC 9(02).                              
                                                                                
       01 WS-AGE                        PIC 9(06) VALUE ZEROES.                 
       01 WS-NUM-AGE  REDEFINES WS-AGE.                                         
          05 WS-AGE1                    PIC 9(04).                              
          05 WS-NUM-REMNG               PIC 9(02).                              
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F730     THRU 2000-EXIT.                           
           PERFORM 3000-READ-F089     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
                   UNTIL BP13F730-REC(1:9) =  HIGH-VALUES                       
                     AND F089-NUM-SCH-ACCNT = HIGH-VALUES.                      
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F730                                                 
                       AB03F089                                                 
                       BM06K510                                                 
                       BP13K813                                                 
                I-O    BP13K730                                                 
               OUTPUT  BP13F725                                                 
                       P13F725A                                                 
                       P13F730N.                                                
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K730 OPEN ERROR ' WS-K730-STATUS                     
              MOVE WS-K730-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K813 OPEN ERROR ' WS-K813-STATUS                     
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K510 OPEN ERROR ' WS-K510-STATUS                     
              MOVE WS-K510-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-CURR-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F730.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F730                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F730-EOF                                      
                   MOVE HIGH-VALUES TO BP13F730-REC(1:9)                        
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F730                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F089.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ AB03F089                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F089-NUM-SCH-ACCNT                       
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F089                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES TO WS-K510-NT.                                           
           MOVE 'N'    TO WS-SAME-NT-FOUND.                                     
                                                                                
           IF BP13F730-REC(1:9) = F089-NUM-SCH-ACCNT                            
                                                                                
              ADD 1 TO WS-MATCH                                                 
              PERFORM 4200-READ-BM06K510     THRU 4200-EXIT                     
              PERFORM 4100-PROCESS-ALL-F730  THRU 4100-EXIT                     
                UNTIL BP13F730-REC(1:9) NOT = F089-NUM-SCH-ACCNT                
                   OR BP13F730-REC(1:9) = HIGH-VALUES                           
                                                                                
              PERFORM 3000-READ-F089   THRU 3000-EXIT                           
           ELSE                                                                 
              IF BP13F730-REC(1:9) > F089-NUM-SCH-ACCNT                         
                 ADD 1 TO WS-F089-NOTMAT                                        
                 PERFORM 3000-READ-F089   THRU 3000-EXIT                        
              ELSE                                                              
                 ADD 1 TO WS-F730-NOTMAT                                        
                 PERFORM 2000-READ-F730 THRU 2000-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4100-PROCESS-ALL-F730.                                                   
      *--------------------------------------------------------*                
                                                                                
           PERFORM 4300-READ-BP13K813     THRU 4300-EXIT.                       
           PERFORM 4400-PROCESS-BP13K730  THRU 4400-EXIT.                       
                                                                                
           PERFORM 2000-READ-F730         THRU 2000-EXIT.                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4200-READ-BM06K510.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                     TO K510-REC                          
           MOVE F089-NUM-ESTATE-CIM        TO K510-ESTCDE.                      
                                                                                
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
                 MOVE K510-ALPHA-NTCDE TO WS-K510-NT                            
              WHEN 23                                                           
                 MOVE SPACES           TO WS-K510-NT                            
                 DISPLAY 'EST CODE NOT FOUND ' K510-ESTCDE                      
              WHEN OTHER                                                        
                DISPLAY 'BM06K510 READ ERROR ' WS-K510-STATUS                   
                MOVE WS-K510-STATUS        TO RETURN-CODE                       
                                                                                
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4300-READ-BP13K813.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                     TO BP13K813-REC                      
           MOVE F730-CDE-NT1               TO K813-NUM-ZONE                     
           MOVE F730-CDE-FLAT-TYPE         TO K813-NUM-FLAT-TYPE                
           MOVE F730-DTE-BALLOT            TO K813-DTE-BALLOT                   
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN 00                                                           
                 MOVE K813-CDE-NT      TO WS-K813-NT                            
              WHEN 23                                                           
                 MOVE SPACES           TO WS-K813-NT                            
                 DISPLAY 'CODE NT  NOT FOUND ' K813-KEY-FLD                     
              WHEN OTHER                                                        
                DISPLAY 'BP13K813 READ ERROR ' WS-K813-STATUS                   
                MOVE WS-K813-STATUS        TO RETURN-CODE                       
                                                                                
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4400-PROCESS-BP13K730.                                                   
      *--------------------------------------------------------*                
                                                                                
           PERFORM 4500-READ-BP13K730     THRU 4500-EXIT.                       
                                                                                
           DISPLAY ' F730=' F730-NUM-REGN                                       
                   ' K813= ' WS-K813-NT ' K510-'  WS-K510-NT                    
                   ' APPS= ' K730-NUM-APPS                                      
           IF WS-K813-NT = WS-K510-NT                                           
              MOVE 'Y'    TO WS-SAME-NT-FOUND                                   
              ADD 1 TO WS-SAME-NT                                               
              IF K730-NUM-APPS = 'Y'                                            
                 MOVE '1'   TO K730-NUM-APPS-ELIG-TAG                           
              ELSE                                                              
                 IF K730-NUM-APPS = SPACES                                      
                    MOVE '1'   TO K730-NUM-APPS-ELIG-TAG                        
                    MOVE 'F'   TO K730-NUM-APPS                                 
                 END-IF                                                         
              END-IF                                                            
              PERFORM 5000-WRITE-F725        THRU 5000-EXIT                     
              WRITE P13F725A-REC    FROM BP13F725-REC                           
              ADD 1 TO WS-WRITE-F725A                                           
           ELSE                                                                 
              PERFORM 5000-WRITE-F725        THRU 5000-EXIT                     
              WRITE BP13F725-REC                                                
              ADD 1 TO WS-WRITE-F725                                            
              MOVE F730-NUM-NRIC1  TO F730-NUM-RES-NRIC1                        
              WRITE P13F730N-REC    FROM BP13F730-REC                           
              ADD 1 TO WS-F730-N                                                
           END-IF.                                                              
           DISPLAY ' APPS ELIG TAG=' K730-NUM-APPS-ELIG-TAG                     
                   ' NEW APPS= ' K730-NUM-APPS                                  
                                                                                
           IF WS-K730-FOUND = 'Y'                                               
              PERFORM 4600-UPDATE-BP13K730    THRU 4600-EXIT                    
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4500-READ-BP13K730.                                                      
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                     TO BP13K730-REC.                     
           MOVE F730-NUM-REGN              TO K730-NUM-REGN                     
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y'  TO WS-K730-FOUND                                     
              WHEN 23                                                           
                 MOVE 'N'  TO WS-K730-FOUND                                     
                 DISPLAY 'REGN NUM NOT FOUND ' K730-NUM-REGN                    
              WHEN OTHER                                                        
                DISPLAY 'BP13K730 READ ERROR ' WS-K730-STATUS                   
                DISPLAY 'REGN NUM NOT FOUND ' K730-NUM-REGN                     
                MOVE WS-K730-STATUS        TO RETURN-CODE                       
                                                                                
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4600-UPDATE-BP13K730.                                                    
      *--------------------------------------------------------*                
                                                                                
           REWRITE BP13K730-REC                                                 
                                                                                
           EVALUATE WS-K730-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K730-UPD                                           
              WHEN OTHER                                                        
                DISPLAY 'BP13K730 UPD  ERROR ' WS-K730-STATUS                   
                DISPLAY 'REGN NUM NOT FOUND ' K730-NUM-REGN                     
                MOVE WS-K730-STATUS        TO RETURN-CODE                       
                                                                                
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5000-WRITE-F725.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                   TO BP13F725-REC.                       
           INITIALIZE                       BP13F725-REC.                       
           MOVE F730-NUM-REGN            TO F725-NUM-REGN.                      
           MOVE F730-NUM-NRIC1           TO F725-NUM-NRIC.                      
           MOVE F089-NUM-BLK             TO F725-NUM-BLK-HSE.                   
           MOVE F089-NME-STREET          TO F725-NME-STREET                     
           MOVE F089-NUM-LVL             TO F725-NUM-STOREY                     
           MOVE F089-NUM-HSE             TO F725-NUM-UNIT                       
           MOVE F089-NUM-POSTAL-DSTRCT   TO F725-CDE-POSTAL                     
           MOVE 'A'                      TO F725-NUM-ADDR-TYPE.                 
                                                                                
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13CZ16 **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13F730) :' WS-READ-F730.             
           DISPLAY 'NO. OF RECORDS READ (AB03F089) :' WS-READ-F089.             
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS MATCH           :' WS-MATCH.                 
           DISPLAY 'NO. OF RECORDS NOT MATCH       :' WS-F730-NOTMAT.           
           DISPLAY 'NO. OF RECORDS NOT MATCH       :' WS-F089-NOTMAT.           
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS UPD  (BP13K730) :' WS-K730-UPD.              
           DISPLAY 'NO. OF RECORDS WRITE(P13F730N) :' WS-F730-N.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS SAME NT         :' WS-SAME-NT.               
           DISPLAY 'NO. OF RECORDS NOT EQ NT(F725) :' WS-WRITE-F725.            
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS EQUAL  NT(F725) :' WS-WRITE-F725A.           
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE       BP13F730                                                 
                       AB03F089                                                 
                       BM06K510                                                 
                       BP13K813                                                 
                       BP13K730                                                 
                       BP13F725                                                 
                       P13F730N.                                                
                                                                                
           EVALUATE WS-F730-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'CLOSE BP13F730 FAIL. STATUS - '                       
                                            WS-F730-STATUS                      
                 MOVE  WS-F730-STATUS       TO RETURN-CODE                      
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
