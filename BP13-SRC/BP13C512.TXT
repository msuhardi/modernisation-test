       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C512.                                                 
      *AUTHOR.        TMK.                                                      
      *DATE-WRITTEN.  12/12/89.                                                 
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  TO ASSIGN REGISTRATION NUMBER TO SUCCESSFUL *                
      *            CASES IN BP13K740                           *                
      *        2.  TO PRINT CONTROL LIST OF THE ASSIGNMENTS.   *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13F740  :  BALLOT FILE                      *                
      *       2. BP13K530  :  DATE/REGN FILE                   *                
      *       3. BP13K825  :  OCCUPIER FILE                    *                
      *       4. BP13K830  :  ADDRESS FILE                     *                
      *                                                        *                
      *     I-O FILES.                                         *                
      *       1. BP13K530  :  REGISTRATION NUMBER INVENTORY    *                
      *                       FILE                             *                
      *       2. BP13K740  :  BALLOT FILE                      *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *       1. BP13L512  : CONTROL LIST                      *                
      *                                                        *                
      *  CHG NO   BY   ON        DESCRIPTION                   *                
      *  ------   --   --        -----------                   *                
      * C910337  YPH  150491     TO INCLUDE PROCESSING OF 3 &  *                
      *                          4S FLAT TYPE FOR 'REN' SCHEME *                
      * C910523  YPH  150591     TO INCLUDE FIELD FOR THE LAST *                
      *                          UNSUCCESSFUL REF NO.          *                
      * C920206  YPH  210291     TO HANDLE '#' IN NRIC2        *                
      *                                                        *                
      * N930012  RML  240593     TO ASSIGN REGN. NO. AS TO SUC-*                
      *                          CESSFUL CASES; UPDATE REGN NO *                
      *                          AS TO ADDRESS FILE (BP13K830) *                
      *                          AND PRINT CONTROL LIST OF THE *                
      *                          ASSIGNMENTS.                  *                
      * N930012  JF   250893     CONVERT P10K740 TO BP13K530   *                
      * C942160  BAL  070294     INCLUDE ELIGIBILITY SCHEME    *                
      *                          IN THE REPORT                 *                
      * SOC-PH9  GJ   081294     CHANGE F520 AND K520 TO F740  *                
      *                          AND K740, CHANGE K568 TO K825.*                
      * C9412B0  RGM  090195     CREATE NEW K830-REC IF DUP FND*                
      *                          OR K830-REC NOT-FND           *                
      * BP130117 LSB  080796     CHG FOR EXEC CONDO, READ ADDR *                
      *                          FILE BY REF-NO                *                
      * BP130418 MPA  240498     TO CATER FOR Y2000            *                
      * BP130509 LSB  160898     PRINT SA-RANK FOR SA FLAT     *                
      * BP130615 GV   160898     USE 4 DIGIT YR FOR COMPUTATION*                
      * BP130652 NOR  220299     EXPAND F740 & K740 RECL 300-500*               
      * BP132255 ANC1 281102     EXPAND GRO-RANK TO 2BYTES     *                
      * BP132317 ROM1 210303     BTO CHANGES - INCLUDE BP13K520*                
      * BP132555 CKK7 190704     CHECK FOR BALLOT DATE WHEN    *                
      *                          DECIDING BTO OR BE            *                
      * BP132583 SK   270804     CATER FOR K520-DTE-REGN       *                
      * BP132596 CKK  071004     TO WRITE TO K740 ONLY         *                
      * BP132626 SK   241104     CHANGE DTE-REGN TO DTE-REQUEST*                
      * BP132829 CKK  030406     CHANGE REF FROM 6 TO 8 BYTES  *                
      * BP133267 YCH2 280208     CHECK STATUS ON WRITE BP13K530*                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN  TO  BP13F740.                               
           SELECT BP13K740  ASSIGN  TO  BP13K740                                
                ACCESS       IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K740-KEY-FLD                                    
                FILE STATUS  IS BP13K740-STATUS.                                
                                                                                
           SELECT BP13K520  ASSIGN  TO  BP13K520                                
                ACCESS       IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K520-KEY-NEW                                    
                FILE STATUS  IS BP13K520-STATUS.                                
                                                                                
           SELECT BP13K530  ASSIGN  TO  BP13K530                                
                ACCESS       IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K530-KEY-FLD                                    
                FILE STATUS  IS BP13K530-STATUS.                                
                                                                                
           SELECT BP13K825  ASSIGN  TO  BP13K825                                
                ACCESS       IS DYNAMIC                                         
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K825-KEY-FLD                                    
                FILE STATUS  IS BP13K825-STATUS.                                
                                                                                
           SELECT BP13K830  ASSIGN  TO  BP13K830                                
                ACCESS       IS DYNAMIC                                         
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K830-KEY-FLD                                    
                FILE STATUS  IS BP13K830-STATUS.                                
                                                                                
           SELECT BP13L512  ASSIGN  TO  BP13L512.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK     CONTAINS 0   RECORDS                                       
           RECORD    CONTAINS 500 CHARACTERS                                    
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K740                                                             
           RECORD    CONTAINS 500 CHARACTERS.                                   
       COPY BP13K740.                                                           
                                                                                
       FD  BP13K520                                                             
           RECORD    CONTAINS 500 CHARACTERS.                                   
       COPY BP13K520.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD    CONTAINS 200 CHARACTERS.                                   
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K530                                                             
           RECORD    CONTAINS 50 CHARACTERS.                                    
       COPY BP13K530.                                                           
                                                                                
       FD  BP13K830                                                             
           RECORD    CONTAINS 250 CHARACTERS.                                   
       COPY BP13K830.                                                           
                                                                                
       FD  BP13L512                                                             
           BLOCK     CONTAINS   0 RECORDS                                       
           RECORD    CONTAINS 132 CHARACTERS                                    
           LABEL     RECORDS  ARE OMITTED                                       
           RECORDING MODE IS F.                                                 
       01  BP13L512-PRT-REC    PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05 BP13K740-STATUS           PIC 99 VALUE 0.                         
              88 BP13K740-WRITE-OK          VALUE 0.                            
           05 BP13K520-STATUS           PIC 99 VALUE 0.                         
              88 BP13K520-WRITE-OK          VALUE 0.                            
           05 BP13K530-STATUS           PIC 99 VALUE 0.                         
              88 BP13K530-REC-FOUND         VALUE 0.                            
              88 BP13K530-REC-NOTFND        VALUE 23.                           
           05 BP13K825-STATUS           PIC 99 VALUE 0.                         
              88 BP13K825-REC-FOUND         VALUE 0.                            
              88 BP13K825-REC-NOTFND        VALUE 23.                           
           05 BP13K830-STATUS           PIC 99 VALUE 0.                         
              88 BP13K830-REC-FOUND         VALUE 0.                            
              88 BP13K830-REC-NOTFND        VALUE 23.                           
              88 BP13K830-WRITE-OK          VALUE 0.                            
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-NUM-READ-BP13F740      PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-REWRITE-BP13K740   PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-REWRITE-BP13K530   PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-REWRITE-BP13K830   PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-DELETE-BP13K520    PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K520     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K530     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-DELETE-BP13K825    PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K825     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K830     PIC 9(06) VALUE ZERO.                   
           05 WS-LINECT                 PIC 9(02) VALUE 60.                     
           05 WS-PAGECT                 PIC 9(04) VALUE ZERO.                   
           05 WS-SEQCT                  PIC 9(04) VALUE ZERO.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05 EOF-BP13F740-IND          PIC X(01) VALUE SPACE.                  
              88 EOF-BP13F740               VALUE 'Y'.                          
           05 WS-DATE-FOUND-IND         PIC X(01) VALUE SPACE.                  
              88 WS-DATE-FOUND              VALUE 'Y'.                          
                                                                                
       01  WS-K530-CURDTE-NOTFND        PIC X(01) VALUE SPACE.                  
       01  WS-K825-UPDATED              PIC X(01) VALUE SPACE.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-MM                PIC 9(02).                              
           05 FILLER                    PIC X(01).                              
           05 WS-CURR-DD                PIC 9(02).                              
           05 FILLER                    PIC X(01).                              
           05 WS-CURR-CCYY              PIC 9(04).                              
                                                                                
       01  WS-CURR-CCYYMMDD.                                                    
           05 WS-CCYY                   PIC 9(04).                              
           05 WS-MM                     PIC 9(02).                              
           05 WS-DD                     PIC 9(02).                              
                                                                                
      *01  FILLER  REDEFINES  WS-CURR-CCYYMMDD.                                 
      *    05 WS-CURR-YEAR.                                                     
      *       10 WS-YEAR-YR             PIC 9(04).                              
      *    05 WS-YEAR REDEFINES WS-CURR-YEAR  PIC 9(04).                        
      *    05 FILLER                    PIC 9(04).                              
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-CCYY              PIC 9(04).                               
           05 WS-SYS-MM                PIC 9(02).                               
           05 WS-SYS-DD                PIC 9(02).                               
                                                                                
       01  WS-RESULT1                  PIC 9(4)  VALUE 0.                       
       01  WS-RESULT2                  PIC 9(4)  VALUE 0.                       
       01  WS-RESULT3                  PIC 9(4)  VALUE 0.                       
       01  WS-REM1                     PIC 9(03) VALUE 0.                       
       01  WS-REM2                     PIC 9(03) VALUE 0.                       
       01  WS-REM3                     PIC 9(03) VALUE 0.                       
       01  WS-BP13K520-REC             PIC X(500) VALUE SPACES.                 
                                                                                
      ************************************************************              
      *    TABLE CONTAINING THE THE LAST DAY OF THE MONTH        *              
      ************************************************************              
                                                                                
       01  WS-MTHLDAY-TABLE.                                                    
           05 FILLER                   PIC X(04) VALUE '0131'.                  
           05 FILLER                   PIC X(04) VALUE '0228'.                  
           05 FILLER                   PIC X(04) VALUE '0331'.                  
           05 FILLER                   PIC X(04) VALUE '0430'.                  
           05 FILLER                   PIC X(04) VALUE '0531'.                  
           05 FILLER                   PIC X(04) VALUE '0630'.                  
           05 FILLER                   PIC X(04) VALUE '0731'.                  
           05 FILLER                   PIC X(04) VALUE '0831'.                  
           05 FILLER                   PIC X(04) VALUE '0930'.                  
           05 FILLER                   PIC X(04) VALUE '1031'.                  
           05 FILLER                   PIC X(04) VALUE '1130'.                  
           05 FILLER                   PIC X(04) VALUE '1231'.                  
                                                                                
       01  WS-MTHLDAY-TBL   REDEFINES  WS-MTHLDAY-TABLE.                        
           05 WS-MTHLDAY-REC               OCCURS 12 TIMES                      
              INDEXED BY WS-MLDAY-IDX.                                          
              10 WS-MTH                PIC X(02).                               
              10 WS-LDAY               PIC X(02).                               
                                                                                
      ************************************************************              
      *    VARIABLES FOR CREATION OF REGN NO. CHECK DIGIT        *              
      ************************************************************              
                                                                                
       01  WS-REGN-TAB.                                                         
           05 FILLER                   PIC X(33) VALUE                          
             '01A02B03C04D05E06F07G08H09I10Z11J'.                               
                                                                                
       01  WS-REGNO-TAB REDEFINES WS-REGN-TAB.                                  
           05 REGN-TAB                     OCCURS 11 TIMES                      
              INDEXED BY WS-CDG.                                                
              10 REGN-REMAINDER        PIC 9(02).                               
              10 REGN-CDG              PIC X(01).                               
                                                                                
       01  WS-REG                      PIC X(08) VALUE SPACES.                  
                                                                                
       01  WS-REGN-NUMERIC.                                                     
           05 REGN-NUM OCCURS 7 TIMES  PIC 9(01).                               
           05 REGN-FLD                 PIC X(01).                               
                                                                                
       01  WS-FLD.                                                              
           05 WS-FLD1                  PIC 9(04) VALUE 0.                       
           05 WS-FLD2                  PIC 9(04) VALUE 0.                       
           05 WS-FLD3                  PIC 9(02) VALUE 0.                       
                                                                                
       01  WS-CHK-DGT                  PIC X(01) VALUE SPACE.                   
                                                                                
       01  WS-TEMP-REG-NO.                                                      
           05 WS-TEMP-REG-NUM          PIC 9(07).                               
           05 WS-TEMP-CHK-DGT          PIC X(01).                               
                                                                                
      **************************************************************            
      *           LAY-OUT FOR CONTROL LISING                       *            
      **************************************************************            
                                                                                
       01  L512-HDG1.                                                           
           05 FILLER                   PIC X(14) VALUE 'BP13L512'.              
           05 FILLER                   PIC X(08) VALUE 'HDB3'.                  
           05 FILLER                   PIC X(18) VALUE SPACES.                  
           05 FILLER                   PIC X(39) VALUE                          
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                   PIC X(18) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'DATE: '.                
           05 HDG1-DATE                PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'PAGE: '.                
           05 HDG1-PAGECT              PIC ZZZZ9.                               
                                                                                
       01  L512-HDG2.                                                           
           05 FILLER                   PIC X(95) VALUE SPACES.                  
           05 FILLER                   PIC X(32) VALUE                          
              'SALES UNIT (APPLICATION TEAM)'.                                  
                                                                                
       01  L512-HDG3.                                                           
           05 FILLER                   PIC X(33) VALUE SPACES.                  
           05 FILLER                   PIC X(60) VALUE                          
              'LIST OF SUCCESSFUL REQUEST ASSIGNED REGISTRATION NO'.            
                                                                                
       01  L512-HDG4.                                                           
           05 FILLER                   PIC X(05) VALUE ' S/N'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(29) VALUE                          
             '       RANDOM NO'.                                                
           05 FILLER                   PIC X(08) VALUE 'PREV-REF'.              
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(08) VALUE 'CURR-REF'.              
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'REGN NO'.               
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE 'CAT'.                   
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(11) VALUE 'NRIC1/NRIC2'.           
           05 FILLER                   PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE ' JB'.                   
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(05) VALUE 'RANK'.                  
           05 FILLER                   PIC X(03) VALUE 'ALC'.                   
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'ELIG'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' NT '.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' FT '.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' JB '.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(06) VALUE 'OPTION'.                
                                                                                
       01  L512-HDG5.                                                           
           05 FILLER                   PIC X(67) VALUE SPACES.                  
           05 FILLER                   PIC X(14) VALUE                          
             'APPLICANT NAME'.                                                  
           05 FILLER                   PIC X(06) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'CODE'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(06) VALUE '   SCH'.                
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' SCH'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'CODE'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'TYPE'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(06) VALUE 'REF-NO'.                
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'CASE'.                  
                                                                                
       01  L512-DTL1.                                                           
           05 SERIAL-NO                PIC Z(4)9.                               
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 RANDOM-NO                PIC X(28) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACE.                   
           05 PREV-RN                  PIC X(06) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 CUR-RN                   PIC X(08) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 REGN-NO                  PIC X(08) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 CAT                      PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 NRIC-1                   PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE '/'.                     
           05 NRIC-2                   PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 JB-CODE                  PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 GRO-RANK                 PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 ALLOC-SCH                PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L-CDE-ELIG               PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 NT-CODE                  PIC X(04) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FT-TYPE                  PIC X(04) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 JB-REF                   PIC X(06) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 OPTION-CASE              PIC X(01) VALUE SPACES.                  
                                                                                
       01  L512-DTL2.                                                           
           05 FILLER                   PIC X(67) VALUE SPACES.                  
           05 NAME                     PIC X(66) VALUE SPACES.                  
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------------*                                                        
       010-OPEN-ROUTINE.                                                        
      *----------------*                                                        
      *    READY TRACE.                                                         
                                                                                
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K530                                                 
                       BP13K740                                                 
                       BP13K520                                                 
                       BP13K825                                                 
                       BP13K830                                                 
                OUTPUT BP13L512.                                                
                                                                                
           IF BP13K740-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K740 FILE, STATUS '                   
                      BP13K740-STATUS                                           
              MOVE BP13K740-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K520-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K520 FILE, STATUS '                   
                      BP13K520-STATUS                                           
              MOVE BP13K520-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K825-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K825 FILE, STATUS '                   
                      BP13K825-STATUS                                           
              MOVE BP13K825-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K530-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K530 FILE, STATUS '                   
                      BP13K530-STATUS                                           
              MOVE BP13K530-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K830 FILE, STATUS '                   
                      BP13K830-STATUS                                           
              MOVE BP13K830-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
      *-------------------*                                                     
       020-PROCESS-ROUTINE.                                                     
      *-------------------*                                                     
                                                                                
      ** SET CURRENT DATE FORMAT FOR REGN AND CONTROL LISTING **                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-CCYYMMDD.                 
           MOVE WS-DD             TO WS-CURR-DD  WS-SYS-DD.                     
           MOVE WS-MM             TO WS-CURR-MM  WS-SYS-MM.                     
           MOVE WS-CCYY           TO WS-CURR-CCYY  WS-SYS-CCYY.                 
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                    
                  DELIMITED BY SIZE INTO HDG1-DATE                              
           END-STRING.                                                          
      *                                                                         
           PERFORM 100-PROCESS-BP13K530 THRU 100-EXIT                           
              UNTIL WS-DATE-FOUND.                                              
      *                                                                         
           PERFORM 200-PROCESS-BP13F740 THRU 200-EXIT                           
              UNTIL EOF-BP13F740.                                               
      *                                                                         
           PERFORM 700-UPDATE-BP13K530  THRU 700-EXIT.                          
      *                                                                         
           PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT.                             
      *                                                                         
       020-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       030-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           CLOSE BP13F740                                                       
                 BP13K530                                                       
                 BP13K740                                                       
                 BP13K520                                                       
                 BP13K825                                                       
                 BP13K830                                                       
                 BP13L512.                                                      
                                                                                
           IF BP13K740-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING BP13K740 FILE, STATUS '                   
                      BP13K740-STATUS                                           
           END-IF.                                                              
                                                                                
           IF BP13K520-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING BP13K520 FILE, STATUS '                   
                      BP13K520-STATUS                                           
           END-IF.                                                              
                                                                                
           IF BP13K530-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING BP13K530 FILE, STATUS '                   
                      BP13K530-STATUS                                           
           END-IF.                                                              
                                                                                
           IF BP13K825-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING K825 FILE, STATUS '                       
                     BP13K825-STATUS                                            
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING K830 FILE, STATUS '                       
                     BP13K830-STATUS                                            
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' ** BP13C512 ** '.                                          
           DISPLAY ' NO. OF RECORDS READ FROM    BP13F740  = '                  
                     WS-NUM-READ-BP13F740.                                      
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K740  = '                  
                     WS-NUM-REWRITE-BP13K740.                                   
           DISPLAY ' NO. OF RECORDS WRITTEN   TO BP13K520  = '                  
                     WS-NUM-WRITE-BP13K520.                                     
           DISPLAY ' NO. OF RECORDS DELETED FROM BP13K520  = '                  
                     WS-NUM-DELETE-BP13K520.                                    
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K530  = '                  
                     WS-NUM-REWRITE-BP13K530.                                   
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K830  = '                  
                     WS-NUM-REWRITE-BP13K830.                                   
           DISPLAY ' NO. OF RECORDS WRITTEN   TO BP13K830  = '                  
                     WS-NUM-WRITE-BP13K830.                                     
           DISPLAY ' '.                                                         
           DISPLAY ' NO. OF RECORDS DELETED FROM BP13K825  = '                  
                     WS-NUM-DELETE-BP13K825.                                    
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K825  = '                  
                     WS-NUM-WRITE-BP13K825.                                     
           STOP RUN.                                                            
                                                                                
       030-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  1.  READ BP13K530 FILE USING CURRENT DATE              *               
      *        OR CURRENT DATE - 1                              *               
      *  2.  IF NOT FOUND                                       *               
      *         SUBTRACT 1 FROM DATE AND REPEAT ROUTINE         *               
      *         UNTIL DATE FOUND                                *               
      ***********************************************************               
                                                                                
      *--------------------*                                                    
       100-PROCESS-BP13K530.                                                    
      *--------------------*                                                    
                                                                                
           MOVE WS-SYS-DATE TO K530-KEY-FLD.                                    
                                                                                
           READ BP13K530.                                                       
                                                                                
           IF BP13K530-REC-FOUND                                                
              MOVE 'Y'                TO WS-DATE-FOUND-IND                      
              MOVE K530-NUM-LAST-REGN TO WS-TEMP-REG-NO                         
           ELSE                                                                 
             IF BP13K530-REC-NOTFND                                             
                PERFORM 110-CONV-DATE THRU 110-EXIT                             
                MOVE 'Y' TO WS-K530-CURDTE-NOTFND                               
             ELSE                                                               
                DISPLAY ' ERROR READING BP13K530, STATUS '                      
                          BP13K530-STATUS                                       
                MOVE BP13K530-STATUS  TO RETURN-CODE                            
                PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************************************************************          
      *  IF CURRENT DAY IS 1ST DAY OF THE YEAR                       *          
      *     SET CURRENT DATE TO LAST DAY OF LAST YEAR                *          
      *  ELSE IF CURRENT DAY IS 1ST DAY OF THE MONTH                 *          
      *     CALL ROUTINE TO SET CURRENT DATE TO LAST DAY OF LAST MTH *          
      *  ELSE                                                        *          
      *     REDUCE CURRENT DAY BY 1                                  *          
      ****************************************************************          
                                                                                
      *--------------*                                                          
       110-CONV-DATE.                                                           
      *--------------*                                                          
                                                                                
           IF WS-SYS-MM = 1 AND WS-SYS-DD = 1                                   
              COMPUTE WS-SYS-CCYY = WS-SYS-CCYY - 1                             
      *       MOVE WS-SYS-CCYY TO WS-YEAR-YR                                    
              MOVE 12          TO WS-SYS-MM                                     
              MOVE 31          TO WS-SYS-DD                                     
           ELSE                                                                 
              IF WS-SYS-DD = 1                                                  
                 PERFORM 140-GET-LAST-DAY-OF-LAST-MTH THRU 140-EXIT             
              ELSE                                                              
                 COMPUTE WS-SYS-DD = WS-SYS-DD - 1                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *   COMPUTE LAST MONTH  THEN                              *               
      *   SEARCH THE MONTH TABLE TO GET LAST DAY OF LAST MONTH  *               
      ***********************************************************               
                                                                                
      *----------------------------*                                            
       140-GET-LAST-DAY-OF-LAST-MTH.                                            
      *----------------------------*                                            
                                                                                
      ** COMPUTE FOR LAST MONTH **                                              
                                                                                
           COMPUTE WS-SYS-MM = WS-SYS-MM - 1.                                   
                                                                                
      ** SEARCH THE TABLE **                                                    
                                                                                
           SET WS-MLDAY-IDX TO 1.                                               
                                                                                
           SEARCH WS-MTHLDAY-REC                                                
               AT END MOVE 0 TO WS-SYS-DD                                       
                  DISPLAY ' ERROR IN OBTAINING LAST DAY OF MONTH '              
                          WS-SYS-MM                                             
                  PERFORM 030-CLOSE-ROUTINE                                     
             WHEN WS-MTH(WS-MLDAY-IDX) =  WS-SYS-MM                             
                  MOVE WS-LDAY(WS-MLDAY-IDX) TO WS-SYS-DD                       
           END-SEARCH.                                                          
                                                                                
      * COMPUTE TO CHECK FOR LEAP YEAR *                                        
                                                                                
           DIVIDE WS-SYS-CCYY BY 4 GIVING WS-RESULT1                            
                               REMAINDER WS-REM1.                               
      *                                                                         
           DIVIDE WS-SYS-CCYY BY 100 GIVING WS-RESULT2                          
                               REMAINDER WS-REM2.                               
      *                                                                         
           DIVIDE WS-SYS-CCYY BY 400 GIVING WS-RESULT3                          
                               REMAINDER WS-REM3.                               
      *                                                                         
      *  IF LEAP YEAR AND LAST MONTH IS FEB, LET LAST DAY BE 29                 
      *                                                                         
                                                                                
           IF (WS-REM1 = 0 AND WS-REM2 NOT = 0) OR WS-REM3 = 0                  
              IF WS-SYS-MM = 2                                                  
                 MOVE 29 TO WS-SYS-DD                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
       140-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  1.  READ BP13F740 FILE AT END EXIT                     *               
      *  2.  IF (F740-CDE-UNSUCC NOT = SPACES AND LOW-VALUES)   *               
      *       AND FLAT-TYPE NOT = 3 OR 4S                       *               
      *         ASSIGN REGISTRATION NUMBER                      *               
      ***********************************************************               
                                                                                
      *--------------------*                                                    
       200-PROCESS-BP13F740.                                                    
      *--------------------*                                                    
                                                                                
           READ BP13F740  AT END                                                
                MOVE 'Y' TO EOF-BP13F740-IND                                    
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-NUM-READ-BP13F740.                                       
                                                                                
           IF F740-CDE-ALLOC-SCH NOT = 'PA'                                     
              IF F740-CDE-FLAT-TYPE = '3 ' OR '4S'                              
                 GO TO 200-PROCESS                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F740-CDE-SUCC-NT-ZONE = SPACES OR LOW-VALUES                      
              GO TO 200-EXIT                                                    
           END-IF.                                                              
                                                                                
       200-PROCESS.                                                             
                                                                                
           MOVE 'N'                       TO WS-K825-UPDATED.                   
                                                                                
           MOVE F740-KEY-FLD           TO K740-KEY-FLD.                         
           PERFORM 300-UPDATE-BP13K740 THRU 300-EXIT.                           
                                                                                
           PERFORM 400-UPDATE-BP13K830    THRU  400-EXIT.                       
           PERFORM 500-UPDATE-BP13K825                                          
              THRU 500-EXIT                                                     
             UNTIL WS-K825-UPDATED = 'Y'.                                       
                                                                                
           PERFORM 600-PRINT-CONTROL-LIST THRU  600-EXIT.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  1.  INCREMENT LAST REG NO.                             *               
      *  2.  ASSIGN REGISTRATION NUMBER AND UPDATE              *               
      *      BP13K740 WITH CURRENT DATE AND REG. NO.            *               
      ***********************************************************               
                                                                                
      *-------------------*                                                     
       300-UPDATE-BP13K740.                                                     
      *-------------------*                                                     
           READ BP13K740.                                                       
                                                                                
           IF BP13K740-STATUS = 23                                              
               MOVE BP13K740-STATUS TO RETURN-CODE                              
               DISPLAY 'BP13K740 RECORD NOT FOUND' BP13K740-STATUS              
            END-IF.                                                             
                                                                                
           IF BP13K740-STATUS NOT = 00 AND 23                                   
               DISPLAY 'ERROR READING BP13K740'    BP13K740-STATUS              
               PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                          
            END-IF.                                                             
                                                                                
           ADD 1 TO WS-TEMP-REG-NUM.                                            
           PERFORM 320-FIND-CHK-DGT THRU 320-EXIT.                              
                                                                                
           MOVE WS-TEMP-REG-NO   TO K740-NUM-REGN  F740-NUM-REGN.               
           IF K740-DTE-REQUEST = SPACES OR LOW-VALUES OR ALL '0'                
              MOVE WS-CURR-CCYYMMDD TO K740-DTE-REQUEST                         
           END-IF.                                                              
           MOVE 'R'              TO K740-CDE-PRINT-STATUS.                      
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           IF BP13K740-WRITE-OK                                                 
              ADD 1 TO WS-NUM-REWRITE-BP13K740                                  
              DISPLAY  'REG-NO '    K740-NUM-REGN                               
                       '--------------------------------------------'           
           ELSE                                                                 
              DISPLAY ' ERROR REWRITING BP13K740, STATUS '                      
                  BP13K740-STATUS                                               
              MOVE BP13K740-STATUS TO RETURN-CODE                               
              DISPLAY  'REGN NO' WS-TEMP-REG-NO                                 
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K530-CURDTE-NOTFND = 'Y'                                       
              MOVE SPACES TO BP13K530-REC                                       
              MOVE WS-CURR-CCYYMMDD TO K530-KEY-FLD                             
                                       WS-SYS-DATE                              
              MOVE WS-TEMP-REG-NO TO K530-NUM-FIRST-REGN                        
                                     K530-NUM-LAST-REGN                         
              WRITE BP13K530-REC                                                
              MOVE SPACE TO WS-K530-CURDTE-NOTFND                               
              IF BP13K530-STATUS NOT = ZEROES                                   
                 MOVE BP13K530-STATUS TO RETURN-CODE                            
                 DISPLAY 'WRITE ERROR - BP13K530 ' BP13K530-STATUS              
                 PERFORM  030-CLOSE-ROUTINE THRU 030-EXIT                       
              ELSE                                                              
                ADD 1 TO  WS-NUM-WRITE-BP13K530                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *     CREATE REGISTRATION NUMBER CHECK DIGIT               *              
      ************************************************************              
                                                                                
      *----------------*                                                        
       320-FIND-CHK-DGT.                                                        
      *----------------*                                                        
                                                                                
            MOVE WS-TEMP-REG-NO  TO WS-REG.                                     
            MOVE WS-REG          TO WS-REGN-NUMERIC.                            
            COMPUTE WS-FLD1 = REGN-NUM (1) * 2 + REGN-NUM (2) * 7 +             
                              REGN-NUM (3) * 6 + REGN-NUM (4) * 5 +             
                              REGN-NUM (5) * 4 + REGN-NUM (6) * 3 +             
                              REGN-NUM (7) * 2.                                 
            DIVIDE WS-FLD1 BY 11 GIVING WS-FLD2 REMAINDER WS-FLD3               
                COMPUTE WS-FLD3 = 11 - WS-FLD3.                                 
            SET WS-CDG TO 1.                                                    
                                                                                
            SEARCH REGN-TAB                                                     
                   AT END GO TO 320-EXIT                                        
                   WHEN REGN-REMAINDER (WS-CDG) = WS-FLD3                       
                        MOVE REGN-CDG (WS-CDG) TO WS-CHK-DGT                    
            END-SEARCH.                                                         
                                                                                
            MOVE WS-CHK-DGT TO WS-TEMP-CHK-DGT.                                 
                                                                                
       320-EXIT.                                                                
            EXIT.                                                               
                                                                                
      ***********************************************************               
      *  1.  INCREMENT LAST REG NO.                             *               
      *  2.  ASSIGN REGISTRATION NUMBER AND UPDATE              *               
      *      BP13K520 WITH CURRENT DATE AND REG. NO.            *               
      ***********************************************************               
                                                                                
      ***********************************************************               
      *      UPDATE BP13K830 FILE                               *               
      ***********************************************************               
                                                                                
      *-------------------*                                                     
       400-UPDATE-BP13K830.                                                     
      *-------------------*                                                     
      *---------------------------------------------------------*               
      * CDE-NT-WITH-LAND USED TO DISTINGUISH BETWEEN BOOKING AND*               
      *   WIS.                                                  *               
      *---------------------------------------------------------*               
      * AS OF DEC 94, REQUESTS UNDER BOOKING AND RFS HAS VALUE  *               
      * OF CDE-NT-WITH-LAND AS:                                 *               
      *                                                         *               
      *   'Y' FOR BOOKING RESERVE LIST IN NEW ESTATE            *               
      *                                                         *               
      *   'N' FOR BOOKING RESERVE LIST IN MATURE ESTATE         *               
      *                                                         *               
      *   'S' FOR SST REQUESTS                                  *               
      *                                                         *               
      *   ' ' FOR WIS / SURF REQUESTS                           *               
      *                                                         *               
      *---------------------------------------------------------*               
           MOVE SPACES                    TO BP13K830-REC.                      
                                                                                
           IF (F740-CDE-NT-WITH-LAND = 'Y' OR 'N') OR                           
              (F740-CDE-FLAT-TYPE    = 'EC')                                    
              MOVE F740-NUM-REF    TO  K830-NUM-REF                             
           ELSE                                                                 
              MOVE F740-NUM-NRIC1  TO  K830-NUM-NRIC-WIS                        
           END-IF.                                                              
                                                                                
           READ BP13K830.                                                       
                                                                                
           IF BP13K830-STATUS NOT = 00 AND 02 AND 23                            
               MOVE BP13K830-STATUS TO RETURN-CODE                              
               DISPLAY 'ERROR READING BP13K830 '    BP13K830-STATUS             
                      ' KEY IS ' K830-KEY-FLD                                   
               PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS = 00 OR 02                                        
              IF K830-NUM-REGN (1:1) = '#' OR SPACES OR LOW-VALUES              
                 MOVE WS-TEMP-REG-NO TO K830-NUM-REGN                           
                                                                                
                 REWRITE BP13K830-REC                                           
                                                                                
                 IF BP13K830-STATUS = 00 OR 02                                  
                   ADD 1 TO WS-NUM-REWRITE-BP13K830                             
                 ELSE                                                           
                   DISPLAY 'ERROR REWRITING BP13K830 ' BP13K830-STATUS          
                          ' KEY IS ' K830-KEY-FLD                               
                   MOVE BP13K830-STATUS TO RETURN-CODE                          
                   PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE WS-TEMP-REG-NO TO K830-NUM-REGN                           
                                        K830-KEY-FLD                            
                 PERFORM 410-WRITE-BP13K830 THRU 410-EXIT                       
                 DISPLAY 'REG-NO ' K830-NUM-REGN                                
                     ' --> CREATED WITH ADDRESS.'                               
              END-IF                                                            
           ELSE                                                                 
              IF BP13K830-STATUS = 23                                           
                 MOVE SPACES        TO BP13K830-REC                             
                 MOVE WS-TEMP-REG-NO TO K830-NUM-REGN                           
                                        K830-KEY-FLD                            
                 PERFORM 410-WRITE-BP13K830 THRU 410-EXIT                       
                 DISPLAY 'REG-NO ' K830-NUM-REGN                                
                     ' --> CREATED WITHOUT ADDRESS! PLEASE UPDATE <--'          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *      WRITE  BP13K830 FILE                               *               
      ***********************************************************               
                                                                                
      *------------------*                                                      
       410-WRITE-BP13K830.                                                      
      *------------------*                                                      
           WRITE BP13K830-REC.                                                  
                                                                                
           IF BP13K830-STATUS = 00                                              
             ADD      1          TO WS-NUM-WRITE-BP13K830                       
           ELSE                                                                 
             DISPLAY 'ERROR WRITING BP13K830 ' BP13K830-STATUS                  
                    ' KEY IS ' K830-KEY-FLD                                     
             MOVE BP13K830-STATUS      TO   RETURN-CODE                         
             PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                            
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *      UPDATE BP13K825 FILE                               *               
      ***********************************************************               
                                                                                
      *-------------------*                                                     
       500-UPDATE-BP13K825.                                                     
      *-------------------*                                                     
      *---------------------------------------------------------*               
      * N940017    SOC PH9                                      *               
      *---------------------------------------------------------*               
      * UNDER RFS (OCT 94 ONWARDS),                             *               
      *    ORIGINAL OCCUPIER RECS (BP13K825) WILL BE DELETED,   *               
      *    AND REPLACED BY A NEW OCCUPIER REC WITH THE KEY OF   *               
      *    THE NEW REGNO.                                       *               
      *                                                         *               
      *    THIS IS TO BRING SUCCESSSFUL REQUESTS (OTHER THAN    *               
      *    THOSE OF RFS) TO BE IN SYCHN WITH RFS.               *               
      *                                                         *               
      * NOTE:                                                   *               
      *   'Y' FOR BOOKING RESERVE LIST IN NEW ESTATE            *               
      *   'N' FOR BOOKING RESERVE LIST IN MATURE ESTATE         *               
      *   'S' FOR SST REQUESTS                                  *               
      *   ' ' FOR WIS / SURF REQUESTS                           *               
      *                                                         *               
      *---------------------------------------------------------*               
           MOVE SPACES                    TO BP13K825-REC.                      
                                                                                
      *    IF F740-CDE-NT-WITH-LAND = 'Y'                                       
      *       MOVE 'Y'                    TO WS-K825-UPDATED                    
      *       GO TO 500-EXIT                                                    
      *    ELSE                                                                 
           MOVE F740-NUM-REF           TO K825-NUM-REF.                         
                                                                                
           START BP13K825 KEY > K825-KEY-FLD.                                   
                                                                                
           IF BP13K825-STATUS NOT = 00 AND 02                                   
              MOVE 'Y'                    TO WS-K825-UPDATED                    
              GO                          TO 500-EXIT.                          
                                                                                
       500-READNEXT-BP13K825.                                                   
                                                                                
           READ BP13K825 NEXT AT END                                            
                MOVE 'Y'                  TO WS-K825-UPDATED                    
                GO TO 500-EXIT.                                                 
                                                                                
           IF K825-NUM-REF NOT = F740-NUM-REF                                   
               MOVE 'Y'                   TO WS-K825-UPDATED                    
               GO TO 500-EXIT                                                   
                                                                                
           ELSE                                                                 
              DELETE BP13K825                                                   
              ADD 1 TO WS-NUM-DELETE-BP13K825                                   
                                                                                
              MOVE WS-TEMP-REG-NO         TO K825-NUM-REGN                      
              WRITE BP13K825-REC                                                
                                                                                
              IF BP13K825-STATUS = 00 OR 02                                     
                 ADD 1 TO WS-NUM-WRITE-BP13K825                                 
                                                                                
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K825 '  BP13K825-STATUS           
                        ' KEY IS ' K825-KEY-FLD                                 
                 MOVE BP13K825-STATUS  TO RETURN-CODE                           
                 PERFORM 030-CLOSE-ROUTINE.                                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *      PRINT CONTROL LIST                                 *               
      ***********************************************************               
                                                                                
      *----------------------*                                                  
       600-PRINT-CONTROL-LIST.                                                  
      *----------------------*                                                  
                                                                                
           IF WS-LINECT > 50                                                    
              PERFORM 650-PRINT-HEADING                                         
           END-IF.                                                              
                                                                                
           ADD  1 TO WS-SEQCT.                                                  
                                                                                
           MOVE SPACES           TO BP13K825-REC.                               
           MOVE F740-NUM-REGN    TO K825-NUM-REGN.                              
           MOVE F740-NUM-NRIC1   TO K825-NUM-NRIC.                              
                                                                                
           READ BP13K825.                                                       
                                                                                
           IF BP13K825-STATUS = 00                                              
             MOVE K825-NME-OCCP     TO NAME                                     
           ELSE                                                                 
              IF BP13K825-STATUS = 23                                           
                DISPLAY 'K825-NOTFND' F740-NUM-NRIC1                            
                MOVE SPACES  TO NAME                                            
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K825' BP13K825-STATUS               
                        ' KEY IS ' K825-KEY-FLD                                 
                 PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F740-NUM-NRIC2 (2:1) NOT = '#'                                    
              MOVE F740-NUM-NRIC2  TO NRIC-2                                    
           ELSE                                                                 
              MOVE SPACES          TO NRIC-2                                    
           END-IF.                                                              
                                                                                
           MOVE WS-SEQCT            TO SERIAL-NO.                               
           MOVE K740-NUM-REGN       TO REGN-NO.                                 
           MOVE F740-NUM-REF        TO CUR-RN.                                  
           MOVE F740-NUM-PREV-REF   TO PREV-RN.                                 
           MOVE F740-NUM-NRIC1      TO NRIC-1.                                  
           MOVE F740-CDE-CATEGORY   TO CAT.                                     
           MOVE F740-CDE-JB         TO JB-CODE.                                 
           MOVE F740-CDE-ALLOC-SCH  TO ALLOC-SCH.                               
           MOVE F740-NUM-GRO-RANK   TO GRO-RANK.                                
           MOVE F740-CDE-ELIG-SCH   TO L-CDE-ELIG.                              
           MOVE F740-CDE-SUCC-NT-ZONE    TO NT-CODE.                            
           MOVE F740-CDE-FLAT-TYPE  TO FT-TYPE.                                 
           MOVE F740-NUM-JB-REF     TO JB-REF.                                  
                                                                                
           IF F740-NUM-PREV-REF = SPACES OR LOW-VALUES                          
               MOVE SPACES              TO OPTION-CASE                          
                                           PREV-RN                              
           ELSE                                                                 
               MOVE 'X'                 TO OPTION-CASE                          
               MOVE F740-NUM-PREV-REF   TO PREV-RN                              
           END-IF.                                                              
                                                                                
           MOVE F740-NUM-RANDOM     TO RANDOM-NO.                               
                                                                                
           ADD  2                   TO WS-LINECT.                               
           WRITE BP13L512-PRT-REC   FROM L512-DTL1  AFTER 2.                    
           MOVE SPACES              TO BP13L512-PRT-REC.                        
                                                                                
           ADD  1                   TO WS-LINECT.                               
           WRITE BP13L512-PRT-REC   FROM L512-DTL2  AFTER 1.                    
           MOVE SPACES              TO BP13L512-PRT-REC.                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       650-PRINT-HEADING.                                                       
      *-----------------*                                                       
                                                                                
           ADD   1 TO WS-PAGECT.                                                
           MOVE  1 TO WS-LINECT.                                                
           MOVE  WS-PAGECT TO HDG1-PAGECT.                                      
           WRITE BP13L512-PRT-REC FROM L512-HDG1 AFTER PAGE.                    
           WRITE BP13L512-PRT-REC FROM L512-HDG2 AFTER 1.                       
           WRITE BP13L512-PRT-REC FROM L512-HDG3 AFTER 2.                       
           WRITE BP13L512-PRT-REC FROM L512-HDG4 AFTER 2.                       
           WRITE BP13L512-PRT-REC FROM L512-HDG5 AFTER 1.                       
           MOVE  SPACES       TO BP13L512-PRT-REC.                              
           WRITE BP13L512-PRT-REC.                                              
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *      UPDATE BP13K530 FILE                               *               
      ***********************************************************               
                                                                                
      *-------------------*                                                     
       700-UPDATE-BP13K530.                                                     
      *-------------------*                                                     
                                                                                
           MOVE WS-SYS-DATE TO K530-KEY-FLD.                                    
                                                                                
           READ BP13K530.                                                       
                                                                                
           IF BP13K530-STATUS NOT = 0                                           
               MOVE BP13K530-STATUS TO RETURN-CODE                              
               DISPLAY 'READ ERROR - BP13K530 ' BP13K530-STATUS                 
               PERFORM  030-CLOSE-ROUTINE THRU 030-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE WS-TEMP-REG-NO TO K530-NUM-LAST-REGN.                           
                                                                                
           REWRITE BP13K530-REC.                                                
                                                                                
           IF BP13K530-STATUS NOT = 0                                           
               MOVE BP13K530-STATUS TO RETURN-CODE                              
               DISPLAY 'REWRITE ERROR - BP13K530 ' BP13K530-STATUS              
               PERFORM  030-CLOSE-ROUTINE THRU 030-EXIT                         
           ELSE                                                                 
               ADD 1 TO  WS-NUM-REWRITE-BP13K530                                
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
