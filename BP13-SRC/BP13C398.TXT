      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C398.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  29/09/2003.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :                                                   *         
      *  1. TO RETRIEVE DATA FOR PRINTING OF UNDERTAKING LETTER.      *         
      *                                                               *         
      * FILES USED :                                                  *         
      *  1. BP13F520                                                  *         
      *  2. BP13K740                                                  *         
      *  3. BP13K820                                                  *         
      *  4. BP13K398                                                  *         
      *  5. BP13K399                                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * CHG REF  BY    DATE     DESCRIPTION                           *         
      * -------- ---  --------  -----------                           *         
      * BP132412 JF4  29/09/03  NEW PROGRAM.                          *         
      * BP132463 JF4  08/01/04  CHECK MCPS SCHEME.                    *         
      * BP132482 JF4  20/02/04  COMPUTE LTR DATE AS 10 DAYS BEHIND    *         
      *                         ALLOCATION DATE.                      *         
      * BP132528 SK   17/02/05  WRITE TO BP13K398 & BP13K399          *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520  ASSIGN        TO BP13F520.                          
                                                                                
           SELECT BP13K740  ASSIGN        TO BP13K740                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K740-KEY-FLD                       
                            FILE STATUS   IS WS-K740-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K398  ASSIGN        TO BP13K398                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K398-KEY-FLD                       
                            FILE STATUS   IS WS-K398-STATUS.                    
                                                                                
           SELECT BP13K399  ASSIGN        TO BP13K399                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K399-KEY-FLD                       
                            FILE STATUS   IS WS-K399-STATUS.                    
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  500  CHARACTERS.                                   
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS  500  CHARACTERS.                                   
       COPY BP13K740.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS   400 CHARACTERS.                                   
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K398                                                            
            RECORD CONTAINS   600 CHARACTERS.                                   
       COPY BP13K398.                                                           
                                                                                
       FD   BP13K399                                                            
            RECORD CONTAINS   800 CHARACTERS.                                   
       COPY BP13K399.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *------------------------------*                                          
      *   WORKING STORAGE VARIABLES  *                                          
      *------------------------------*                                          
       01 WS-F520-EOF             PIC X(01) VALUE 'N'.                          
          88 F520-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-K820-EOF             PIC X(01) VALUE 'N'.                          
          88 K820-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-K740-STATUS          PIC 9(02) VALUE ZEROES.                       
       01 WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                       
       01 WS-K398-STATUS          PIC 9(02) VALUE ZEROES.                       
       01 WS-K399-STATUS          PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-F520-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-K740-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-K820-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-K398-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-K399-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-PRINT-CASE-CNT       PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-K740-REGN-FND        PIC X(01) VALUE SPACES.                       
       01 WS-COUNTER              PIC 9(02) VALUE ZEROES.                       
       01 WS-NRIC-CNT             PIC 9(02) VALUE ZEROES.                       
       01 WS-CTR1                 PIC 9(02) VALUE ZEROES.                       
       01 WS-MTH-FLAG             PIC X(01) VALUE SPACES.                       
                                                                                
       01 WS-CURR-DATE            PIC X(08) VALUE ZEROES.                       
       01 WS-NUM-CURR-DATE  REDEFINES WS-CURR-DATE.                             
          05 WS-DATE              PIC 9(08).                                    
       01 WS-BIRTH-DATE           PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-AGE                  PIC 9(08) VALUE ZEROES.                       
       01 WS-NUM-AGE  REDEFINES WS-AGE.                                         
          05 WS-AGE-CMPLTD        PIC 9(04).                                    
          05 WS-NUM-REMNG         PIC 9(04).                                    
                                                                                
       01 WS-DTE-LETTER           PIC 9(08) VALUE ZEROES.                       
       01 WS-NUM-DTE-LETTER       PIC 9(07) VALUE ZEROES.                       
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-C398-TABLE.                                                        
          05 WS-C398-OCCUPIERS  OCCURS 12 TIMES.                                
             10 WS-C398-NRIC      PIC X(09).                                    
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F520      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-DATA       THRU 3000-EXIT                       
                                           UNTIL F520-END-OF-FILE.              
           PERFORM 9000-CLOSE-PARA         THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F520                                                
                        BP13K740                                                
                        BP13K820                                                
                 I-O    BP13K398                                                
                        BP13K399.                                               
                                                                                
           IF WS-K740-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K740 - OPEN ERROR, STATUS ' WS-K740-STATUS           
              MOVE    WS-K740-STATUS     TO RETURN-CODE                         
              PERFORM 9000-CLOSE-PARA                                           
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K820 - OPEN ERROR, STATUS ' WS-K820-STATUS           
              MOVE    WS-K820-STATUS     TO RETURN-CODE                         
              PERFORM 9000-CLOSE-PARA                                           
           END-IF.                                                              
                                                                                
           IF WS-K398-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K398 - OPEN ERROR, STATUS ' WS-K398-STATUS           
              MOVE    WS-K398-STATUS     TO RETURN-CODE                         
              PERFORM 9000-CLOSE-PARA                                           
           END-IF.                                                              
                                                                                
           IF WS-K399-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K399 - OPEN ERROR, STATUS ' WS-K399-STATUS           
              MOVE    WS-K399-STATUS     TO RETURN-CODE                         
              PERFORM 9000-CLOSE-PARA                                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CURR-DATE.                     
           STRING WS-CURR-DATE(7:2), '/',                                       
                  WS-CURR-DATE(5:2), '/',                                       
                  WS-CURR-DATE(1:4)  DELIMITED BY SIZE                          
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F520.                                                      
      *---------------------------------------------------------------*         
           READ BP13F520 AT END                                                 
                          MOVE 'Y' TO WS-F520-EOF                               
                         NOT AT END                                             
                          ADD 1    TO WS-F520-READ-CNT                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
      *---------------------------------------------------*                     
      *MOVE APPLICANT DETAILS TO ARRAY POSITIONS 1 TO 4   *                     
      *MOVE OCCUPIER DETAILS TO ARRAY POSITIONS 5 TO 12   *                     
      *---------------------------------------------------*                     
           MOVE  ZEROS      TO WS-COUNTER.                                      
           MOVE   'N'       TO WS-K820-EOF                                      
                               WS-MTH-FLAG.                                     
           MOVE  SPACES     TO BP13K398-REC                                     
                               BP13K399-REC                                     
                               WS-C398-TABLE.                                   
           INITIALIZE          BP13K398-REC                                     
                               BP13K399-REC.                                    
                                                                                
           PERFORM 3100-READ-BP13K740         THRU 3100-EXIT.                   
                                                                                
           IF WS-K740-REGN-FND  =  'Y'                                          
              PERFORM 3200-START-BP13K820     THRU 3200-EXIT                    
              PERFORM 3300-READNXT-BP13K820   THRU 3300-EXIT                    
                      UNTIL (K820-NUM-REGN  NOT =  F520-NUM-REGN)  OR           
                            (K820-END-OF-FILE)                                  
                                                                                
              MOVE F520-NUM-REGN         TO K398-NUM-REGN                       
              MOVE F520-CDE-ELIG-SCH     TO K398-CDE-SCHEME                     
                                                                                
              IF F520-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'                      
                 MOVE 'Y'   TO K398-MCPS-FRM                                    
              END-IF                                                            
                                                                                
              MOVE 'Y'    TO  K398-ESS-MBRS-FRM                                 
                                                                                
              IF F520-CDE-ELIG-SCH = 'OR '                                      
                 MOVE 'Y'   TO  K398-ORPHAN-SCH-FRM                             
              END-IF                                                            
                                                                                
              IF F520-NUM-APPLN-TYPE = 'I'                                      
                 MOVE 'Y'   TO  K398-APPLN-FRM                                  
              END-IF                                                            
                                                                                
      *       IF WS-MTH-FLAG = "Y"                                              
      *          MOVE 'Y'   TO  K398-MONTH-INCOME-FRM                           
      *       END-IF                                                            
                                                                                
              MOVE 'N'   TO  K398-MONTH-INCOME-FRM                              
                                                                                
              PERFORM 5000-WRITE-BP13K398   THRU 5000-EXIT                      
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F520         THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-READ-BP13K740.                                                      
      *---------------------------------------------------------------*         
           MOVE F520-KEY-FLD    TO K740-KEY-FLD.                                
           READ BP13K740.                                                       
           EVALUATE WS-K740-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'         TO WS-K740-REGN-FND                             
              ADD   1           TO WS-K740-READ-CNT                             
           WHEN 23                                                              
              MOVE   SPACES     TO WS-K740-REGN-FND                             
              DISPLAY 'BP13K740 REG NO NOT FOUND : ' F520-NUM-REGN              
           WHEN OTHER                                                           
              DISPLAY 'BP13K740 - READ ERROR, STATUS ' WS-K740-STATUS           
              MOVE WS-K740-STATUS    TO RETURN-CODE                             
              PERFORM 9000-CLOSE-PARA                                           
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-START-BP13K820.                                                     
      *---------------------------------------------------------------*         
           MOVE  ZEROES            TO WS-NRIC-CNT.                              
           MOVE  LOW-VALUES        TO K820-KEY-FLD.                             
           MOVE  F520-NUM-REGN     TO K820-NUM-REGN.                            
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              DISPLAY 'BP13K820 REG NO NOT FOUND : ' F520-NUM-REGN              
           WHEN 46                                                              
              MOVE    'Y'          TO WS-K820-EOF                               
           WHEN OTHER                                                           
              DISPLAY 'BP13K820 - START ERROR, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS  TO   RETURN-CODE                             
              PERFORM 9000-CLOSE-PARA                                           
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3300-READNXT-BP13K820.                                                   
      *---------------------------------------------------------------*         
           READ BP13K820 NEXT                                                   
                         AT END MOVE 'Y' TO WS-K820-EOF                         
                         GO TO 3300-EXIT.                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
             WHEN 00                                                            
                IF K820-NUM-REGN = F520-NUM-REGN                                
                   ADD    1   TO WS-NRIC-CNT                                    
                   PERFORM 4000-PROCESS-K820     THRU 4000-EXIT                 
                   ADD    1   TO WS-K820-READ-CNT                               
                END-IF                                                          
             WHEN 46                                                            
                MOVE    'Y'   TO WS-K820-EOF                                    
             WHEN OTHER                                                         
                DISPLAY 'BP18K820 - READNXT ERROR, STATUS: '                    
                                                     WS-K820-STATUS             
                MOVE     WS-K820-STATUS    TO RETURN-CODE                       
                PERFORM  9000-CLOSE-PARA                                        
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-K820.                                                       
      *---------------------------------------------------------------*         
      *-----------------------------------------*                               
      * CALCULATE COMPLETED AGE FROM BIRTH DATE *                               
      *-----------------------------------------*                               
           MOVE K820-DTE-BIRTH        TO WS-BIRTH-DATE.                         
           COMPUTE WS-AGE  =  WS-DATE  -  WS-BIRTH-DATE.                        
                                                                                
      *---------------------------------------------------*                     
      * IF NRIC FOUND IN BP13K740, PERFORM APPLICANT PARA *                     
      * ELSE PERFORM OCCUPIER PARA                        *                     
      *---------------------------------------------------*                     
           IF  (K820-NUM-NRIC  =  K740-NUM-NRIC1)  OR                           
               (K820-NUM-NRIC  =  K740-NUM-NRIC2)  OR                           
               (K820-NUM-NRIC  =  K740-NUM-NRIC3)  OR                           
               (K820-NUM-NRIC  =  K740-NUM-NRIC4)                               
                                                                                
               PERFORM 4100-PROCESS-APPLICANT   THRU 4100-EXIT                  
           ELSE                                                                 
               PERFORM 4500-PROCESS-OCCUPIER    THRU 4500-EXIT                  
           END-IF.                                                              
                                                                                
           PERFORM 5100-WRITE-BP13K399   THRU 5100-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-PROCESS-APPLICANT.                                                  
      *---------------------------------------------------------------*         
      * K399-CDE-OCCP = 1 (MAIN APPLICANT USED FOR JET FORM PRINTING  *         
      *                   (BP13B398/B399)                             *         
      *---------------------------------------------------------------*         
                                                                                
           ADD   1     TO WS-COUNTER.                                           
           MOVE F520-NUM-REGN       TO K399-NUM-REGN.                           
           MOVE K820-NUM-NRIC       TO K399-NUM-NRIC                            
                                       WS-C398-NRIC(WS-COUNTER).                
           IF K740-NUM-NRIC1 = K820-NUM-NRIC                                    
              MOVE '1'              TO K399-CDE-OCCP                            
           ELSE                                                                 
           IF K740-NUM-NRIC2 = K820-NUM-NRIC                                    
              MOVE '2'              TO K399-CDE-OCCP                            
           ELSE                                                                 
           IF K740-NUM-NRIC3 = K820-NUM-NRIC                                    
              MOVE '3'              TO K399-CDE-OCCP                            
           ELSE                                                                 
           IF K740-NUM-NRIC4 = K820-NUM-NRIC                                    
              MOVE '4'              TO K399-CDE-OCCP                            
           END-IF.                                                              
                                                                                
           MOVE K820-NME-OCCP       TO K399-NME-OCCP.                           
           MOVE WS-AGE-CMPLTD       TO K399-NUM-AGE.                            
                                                                                
           MOVE 'Y'                 TO K399-ID-CARD.                            
                                                                                
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06' OR                   
                                      '02' OR '12'                              
               MOVE   'Y'           TO K399-BIRTH-CERTF                         
           END-IF.                                                              
                                                                                
           IF F520-CDE-ALLOC-SCH  =  'MTF'                                      
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 MOVE   'Y'         TO K399-BIRTH-CERTF                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (K820-NUM-CITIZENSHIP  =  '20') AND                               
              (K399-CDE-OCCP  NOT =  '1')                                       
              MOVE   'Y'          TO K399-ENTRY-PERMIT                          
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
               MOVE   'Y'         TO K399-MARRIAGE-CERTF                        
               PERFORM VARYING WS-CTR1 FROM 1 BY 1 UNTIL                        
                       WS-CTR1 = WS-NRIC-CNT                                    
                 IF K820-NUM-NRIC-SPOUSE = WS-C398-NRIC(WS-CTR1)                
                    MOVE   'N'    TO K399-MARRIAGE-CERTF                        
                 END-IF                                                         
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
           IF K820-AMT-INCOME NOT = LOW-VALUES AND SPACES                       
              IF K820-AMT-INCOME  >  0                                          
                  MOVE   'Y'         TO K399-PAY-SLIP                           
                                        WS-MTH-FLAG                             
              ELSE                                                              
                  IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30)           
                     MOVE   'Y'      TO K399-UNEMPLY-DECL                       
                                        K399-STUDENT-PASS                       
                                        K399-STAT-DECL                          
                  ELSE                                                          
                     IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)        
                        MOVE   'Y'      TO K399-UNEMPLY-DECL                    
                                           K399-STAT-DECL                       
                     END-IF                                                     
                  END-IF                                                        
              END-IF                                                            
           ELSE                                                                 
              IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30)               
                 MOVE   'Y'      TO K399-UNEMPLY-DECL                           
                                    K399-STUDENT-PASS                           
                                    K399-STAT-DECL                              
              ELSE                                                              
                 IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)            
                    MOVE   'Y'      TO K399-UNEMPLY-DECL                        
                                       K399-STAT-DECL                           
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
               MOVE   'Y'         TO K399-DEATH-CERTF                           
               MOVE K820-NUM-NRIC-SPOUSE   TO                                   
                                     K399-NRIC-DEATH-CERTF                      
               MOVE K820-NME-SPOUSE        TO                                   
                                     K399-NME-DEATH-CERTF                       
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
               MOVE   'Y'         TO K399-DIVORCE-CERTF                         
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '5' OR 'P'                            
               MOVE   'Y'         TO K399-APPL-OCCP-SEP                         
           END-IF.                                                              
                                                                                
           MOVE   'Y'             TO K399-PHOTOGRAPH.                           
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4500-PROCESS-OCCUPIER.                                                   
      *---------------------------------------------------------------*         
           ADD   1     TO WS-COUNTER.                                           
           MOVE F520-NUM-REGN     TO K399-NUM-REGN.                             
           MOVE K820-NUM-NRIC     TO K399-NUM-NRIC                              
                                     WS-C398-NRIC(WS-COUNTER).                  
           MOVE   'O'             TO K399-CDE-OCCP.                             
           MOVE   K820-NME-OCCP   TO K399-NME-OCCP.                             
           MOVE   WS-AGE-CMPLTD   TO K399-NUM-AGE.                              
                                                                                
           IF (K820-NUM-CITIZENSHIP = '10' OR '20')  AND                        
              (WS-AGE-CMPLTD >= 15)                                             
              MOVE   'Y'          TO K399-ID-CARD                               
           END-IF.                                                              
                                                                                
           IF (K820-NUM-CITIZENSHIP  NOT =  '10' AND '20')                      
              MOVE   'Y'          TO K399-PASS-PERMIT                           
           END-IF.                                                              
                                                                                
           IF (K820-NUM-CITIZENSHIP  =  '20')                                   
              MOVE   'Y'          TO K399-ENTRY-PERMIT                          
           END-IF.                                                              
                                                                                
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06' OR                   
                                      '02' OR '12'                              
               MOVE   'Y'         TO K399-BIRTH-CERTF                           
           END-IF.                                                              
                                                                                
           IF F520-CDE-ALLOC-SCH  =  'MTF'                                      
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 MOVE   'Y'       TO K399-BIRTH-CERTF                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
              MOVE   'Y'          TO K399-MARRIAGE-CERTF                        
              PERFORM VARYING WS-CTR1 FROM 1 BY 1 UNTIL                         
                                             WS-CTR1 = WS-NRIC-CNT              
                 IF K820-NUM-NRIC-SPOUSE = WS-C398-NRIC(WS-CTR1)                
                    MOVE   'N'    TO K399-MARRIAGE-CERTF                        
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF K820-AMT-INCOME NOT = LOW-VALUES AND SPACES                       
              IF K820-AMT-INCOME  >  0                                          
                  MOVE   'Y'         TO K399-PAY-SLIP                           
                                        WS-MTH-FLAG                             
              ELSE                                                              
                  IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 55)           
                     MOVE   'Y'      TO K399-UNEMPLY-DECL                       
                                        K399-STUDENT-PASS                       
                                        K399-STAT-DECL                          
                  ELSE                                                          
                    IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)         
                       MOVE   'Y'      TO K399-UNEMPLY-DECL                     
                                          K399-STAT-DECL                        
                    END-IF                                                      
                  END-IF                                                        
              END-IF                                                            
           ELSE                                                                 
               IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 55)              
                  MOVE   'Y'      TO K399-UNEMPLY-DECL                          
                                     K399-STUDENT-PASS                          
                                     K399-STAT-DECL                             
                ELSE                                                            
                   IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)          
                      MOVE   'Y'      TO K399-UNEMPLY-DECL                      
                                         K399-STAT-DECL                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
               MOVE   'Y'         TO K399-DEATH-CERTF                           
               MOVE K820-NUM-NRIC-SPOUSE   TO                                   
                                     K399-NRIC-DEATH-CERTF                      
               MOVE K820-NME-SPOUSE        TO                                   
                                     K399-NME-DEATH-CERTF                       
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
               MOVE   'Y'         TO K399-DIVORCE-CERTF                         
           END-IF.                                                              
                                                                                
           IF K820-NUM-MARITAL-STATUS  =  '5' OR 'P'                            
               MOVE   'Y'         TO K399-APPL-OCCP-SEP                         
           END-IF.                                                              
                                                                                
           IF (K820-NUM-RELATIONSHIP  =  '09')  AND                             
              (WS-AGE-CMPLTD >= 18  AND  WS-AGE-CMPLTD < 21)                    
               MOVE   'Y'         TO K399-MARRIAGE-CONSNT                       
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-WRITE-BP13K398.                                                     
      *---------------------------------------------------------------*         
           MOVE WS-CURR-DATE    TO K398-DTE-UPDATE.                             
           MOVE 'BP13C398'      TO K398-NUM-USERID.                             
           WRITE BP13K398-REC.                                                  
                                                                                
           EVALUATE WS-K398-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K398-WRITE-CNT                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN WRITING - BP13K398, STATUS -'                
                                               WS-K398-STATUS                   
                 MOVE     WS-K398-STATUS    TO RETURN-CODE                      
                 PERFORM  9000-CLOSE-PARA                                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5100-WRITE-BP13K399.                                                     
      *---------------------------------------------------------------*         
                                                                                
           WRITE BP13K399-REC.                                                  
                                                                                
           EVALUATE WS-K399-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K399-WRITE-CNT                                     
                 MOVE  SPACES     TO BP13K399-REC                               
                 INITIALIZE          BP13K399-REC                               
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN WRITING - BP13K399, STATUS -'                
                                               WS-K399-STATUS                   
                 MOVE     WS-K399-STATUS    TO RETURN-CODE                      
                 PERFORM  9000-CLOSE-PARA                                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-PARA.                                                         
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C398       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F520-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13F520  : ' WS-EDIT1.                
           MOVE WS-K820-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13K820  : ' WS-EDIT1.                
           MOVE WS-K740-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13K740  : ' WS-EDIT1.                
           MOVE WS-K398-WRITE-CNT    TO WS-EDIT1.                               
           DISPLAY 'NO OF RECS WRITTEN TO BP13K398 : ' WS-EDIT1.                
           MOVE WS-K399-WRITE-CNT    TO WS-EDIT1.                               
           DISPLAY 'NO OF RECS WRITTEN TO BP13K399 : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F520                                                     
                   BP13K740                                                     
                   BP13K820                                                     
                   BP13K398                                                     
                   BP13K399.                                                    
                                                                                
           IF WS-K740-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K740 - CLOSE ERROR, STATUS ' WS-K740-STATUS          
              MOVE    WS-K740-STATUS     TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K820 - CLOSE ERROR, STATUS ' WS-K820-STATUS          
              MOVE    WS-K820-STATUS     TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K398-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K398 - CLOSE ERROR, STATUS ' WS-K398-STATUS          
              MOVE    WS-K398-STATUS     TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K399-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K399 - CLOSE ERROR, STATUS ' WS-K399-STATUS          
              MOVE    WS-K399-STATUS     TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
