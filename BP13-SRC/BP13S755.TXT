  /*---------------------------------------------------------------*/           
  /*  OBJECTIVES : TO TAB THE RESULT OF BALLOTING ON THE UPGRADER  */           
  /*               AND NEW HH FOR BOOKING SYSTEM                   */           
  /*---------------------------------------------------------------*/           
  /*  CHG-REQNO   BY   DATE   DESCRIPTION                          */           
  /*  BP130205   YCH  140695  ALLOW TO ACCEPT RESALE FLAT TYPE     */           
  /*  BP130261   YCH  070497  BREAKDOWN OF RFS APPLN               */           
  /*  0630       YCH  290699  DELETE DATE BALLOT                   */           
  /*---------------------------------------------------------------*/           
  PROC FORMAT;                                                                  
                                                                                
   VALUE $HH_FMT  "G1"="UPG EXISTING LESSEES            "                       
                  "G2"="UPG EXISTING RESALE FLAT LESSEES"                       
                  "G3"="UPG NON-LESSEES                 "                       
                  "H1"="NHH EXISTING RESALE FLAT LESSEES"                       
                  "H2"="NHH EX-RESALE FLAT LESSEES      "                       
                  "NT"="TENANTS   "                                             
                  "O "="OCCUPIERS "                                             
                  "OT"="OTHERS    ";                                            
                                                                                
   VALUE $RM_FMT  "1 ","01"="1-RM"                                              
                  "2 ","02"="2-RM"                                              
                  "3 ","03"="3-RM"                                              
                  "4 ","04"="4-RM"                                              
                  "5 ","05"="5-RM"                                              
                  "06"="EXEC"                                                   
                  "07"="HUDC"                                                   
                  "08"="MG";                                                    
                                                                                
   VALUE $FT_FMT  "01"="1-RM"                                                   
                  "02"="2-RM"                                                   
                  "03"="3-RM"                                                   
                  "04"="4-RM"                                                   
                  "05"="5-RM"                                                   
                  "0E"="EXEC"                                                   
                  ;                                                             
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
                                                                                
   DATA EXIST_GP EX_GP;                                                         
   INFILE BP13UPG END=EOF;                                                      
   INPUT @1   REG_NO   $CHAR8.  @18  DP_OM    $CHAR1.                           
         @40  NT_REQ   $CHAR3.                                                  
         @46  RM_REQ   $CHAR2.  @51  SCHEME   $CHAR4.                           
         @78  ZONE     $CHAR2.                                                  
         @86  NT_RES   $CHAR3.  @82  RM_RES   $CHAR2.                           
         @89  CDE_OCC  $CHAR1.                                                  
         @91  CDE_FILE $CHAR1.  @92  CDE_HH   $CHAR1.                           
         ;                                                                      
                                                                                
   IF _N_ = 1 THEN                                                              
      DO;                                                                       
        CUR_DTE=PUT(TODAY(),DDMMYY8.);                                          
        CALL SYMPUT('DATE',CUR_DTE);                                            
      END;                                                                      
                                                                                
   IF RM_RES="0E"  OR                                                           
      RM_RES="E " THEN                                                          
      RM_RES="06";                                                              
   IF RM_RES="0H" OR                                                            
      RM_RES="H " THEN                                                          
      RM_RES="07";                                                              
   IF RM_RES="0G" THEN                                                          
      RM_RES="08";                                                              
                                                                                
   IF DP_OM="D" THEN CDE_TYPE="G1";                                             
   IF DP_OM="O" THEN CDE_TYPE="G2";                                             
   IF DP_OM="R" THEN CDE_TYPE="G3";                                             
   IF DP_OM="S" THEN CDE_TYPE="H1";                                             
   IF DP_OM="X" THEN CDE_TYPE="H2";                                             
   IF DP_OM="Z" THEN                                                            
      DO;                                                                       
        IF (CDE_OCC="0" OR CDE_OCC="1") AND                                     
            SCHEME<="6999" THEN                                                 
            CDE_TYPE="NT";                                                      
        ELSE                                                                    
            IF CDE_OCC="2" THEN                                                 
               CDE_TYPE="O ";                                                   
            ELSE                                                                
               CDE_TYPE="OT";                                                   
      END;                                                                      
                                                                                
   IF DP_OM="D" OR DP_OM="S" OR DP_OM="Z" THEN OUTPUT EXIST_GP;                 
   IF DP_OM="O" OR DP_OM="R" OR DP_OM="X" THEN OUTPUT EX_GP;                    
   RUN;                                                                         
                                                                                
   /*------------------------------------------------*/                         
   /*    TABULATE ON EXISTING FLAT INFORMATION       */                         
   /*------------------------------------------------*/                         
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO UNIT=18 NEW;                                                    
   PROC TABULATE DATA=EXIST_GP MISSING FORMAT=7.;                               
   TITLE1 "BP13L755 HDB3                           SYSTEM OF COMMITMENT         
                                             PAGE : ";                          
   TITLE2 "                        BREAKDOWN OF UPGRADERS AND NEWHOUSEHOLD FOR B
OOKING SYSTEM                           DATE : &DATE";                          
                                                                                
   CLASS CDE_TYPE NT_REQ RM_REQ RM_RES ;                                        
                                                                                
   FORMAT RM_REQ   $FT_FMT.                                                     
          RM_RES   $RM_FMT.                                                     
          CDE_TYPE $HH_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
   TABLE (CDE_TYPE=" "*                                                         
         (RM_RES="EXISTING FLAT TYPE" ALL="SUBTOTAL")                           
         ALL),                                                                  
         (NT_REQ=" "*RM_REQ=" " ) ALL                                           
         /RTS=25 BOX=" NT/ZONE/FLAT TYPE APPLIED";                              
                                                                                
   /*------------------------------------------------*/                         
   /*    TABULATE ON EX-RESALE FLAT INFORMATION      */                         
   /*------------------------------------------------*/                         
                                                                                
   PROC TABULATE DATA=EX_GP MISSING FORMAT=7.;                                  
   TITLE1 "BP13L755 HDB3                           SYSTEM OF COMMITMENT         
                                             PAGE : ";                          
   TITLE2 "                        BREAKDOWN OF UPGRADERS AND NEWHOUSEHOLD FOR B
OOKING SYSTEM                           DATE : &DATE";                          
                                                                                
   CLASS CDE_TYPE NT_REQ RM_REQ RM_RES ;                                        
                                                                                
   FORMAT RM_REQ   $FT_FMT.                                                     
          RM_RES   $RM_FMT.                                                     
          CDE_TYPE $HH_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
   TABLE (CDE_TYPE=" "*                                                         
         (RM_RES="EX-DP FLAT TYPE" ALL="SUBTOTAL")                              
         ALL),                                                                  
         (NT_REQ=" "*RM_REQ=" " ) ALL                                           
         /RTS=25 BOX=" NT/ZONE/FLAT TYPE APPLIED";                              
                                                                                
