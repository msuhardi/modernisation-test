       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C548.                                                 
       AUTHOR.        RACHEL G MENDOZA.                                         
      *DATE-WRITTEN.  21 NOV 1994.                                              
      *REMARKS.                                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  CREATE RANGE OF SUCCESSFUL REGN NO FOR THE    *                
      *          QUARTERLY BALLOTING.                          *                
      *                                                        *                
      *     INPUT FILE:                                        *                
      *      1. BP13F520                                       *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *      1. BP13F747                                       *                
      *                                                        *                
      * CHG-NO   DATE   BY  DESCRIPTION                        *                
      * ------- ------  --  -----------                        *                
      * C950106 230195  GD  ADD FIRST & LAST WAIT TIME PROCESS *                
      * C950206 130395  YCH PIGEONHOLE TIME                    *                
      *BP130656 120499  NOR INCLUDE DTE-BALLOT IN F747 OUTPUT  *                
      *BP130652 200299  NOR EXPAND F520 RECL FROM 300 TO 500   *                
      *BP132463 311204  MJB EXPAND F747 RECL FROM  80 TO 150   *                
      *BP133432 200109  IL5 CATER TO NEW FIELDS LAYOUR FOR F747*                
      *BP134857 080413  SMR EVALUATE PARAMETER FROM JCL        *                
      *BP135255 260314  SMR INCLUDE AST IN PARAMETER FROM JCL  *                
      *BP135513 211014  SMR INCLUDE FTS IN PARAMETER FROM JCL  *                
      *BP135551 061114  SMR ADD NEW FIELD - NUM-MCPS           *                
      *BP135551 070215  IL5 DO NOT WRITE BLANK RECORD TO F747  *                
      *BP136259 010616  SMR CATER FOR ELDERLY - NON-SPS (NSP)  *                
      *BP136970 220817  SMR CATER FOR ROF - OTHER GROUP (OTH)  *                
      *                     TO WRITE 2 RECORDS IN BP13F747     *                
      *BP139275 220722  AM2 CHANGES FOR PA R-200%/W-300%       *                
      *BP139670 250823  AM2 CATER FOR FT(PMC)                  *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN       TO BP13F520.                            
                                                                                
           SELECT BP13F747 ASSIGN       TO BP13F747.                            
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  BP13F520                                                             
           RECORD    CONTAINS 500 CHARACTERS                                    
           LABEL     RECORD   IS  STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F747                                                             
           RECORD    CONTAINS 150 CHARACTERS                                    
           LABEL     RECORD   IS  STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F747.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CNT-F520-READ        PIC 9(7)    VALUE ZEROS.                 
           05  WS-CNT-F747-WRITE       PIC 9(7)    VALUE ZEROS.                 
           05  WS-F520-EOF             PIC X       VALUE 'N'.                   
           05  WS-CAT1-FND             PIC X       VALUE 'N'.                   
           05  WS-CAT2-FND             PIC X       VALUE 'N'.                   
           05  WS-CAT3-FND             PIC X       VALUE 'N'.                   
           05  WS-FIRST-REGN           PIC X(8)    VALUE SPACES.                
           05  WS-SAVE-REGN            PIC X(8)    VALUE SPACES.                
           05  WS-FIRST-SEQUENCE       PIC X(8)    VALUE SPACES.                
           05  WS-SAVE-SEQUENCE        PIC X(8)    VALUE SPACES.                
           05  WS-SAVE-ZONE            PIC X(3)    VALUE SPACES.                
           05  WS-SAVE-RANDOM          PIC X(28)   VALUE SPACES.                
           05  WS-SAVE-FLAT-TYPE       PIC X(2)    VALUE SPACES.                
           05  WS-SAVE-PRIOSCH         PIC X(3)    VALUE SPACES.                
           05  WS-SAVE-HHTY            PIC X       VALUE SPACES.                
           05  WS-SAVE-CATEGORY        PIC X(1)    VALUE SPACES.                
           05  WS-F520-CATEGORY        PIC X(1)    VALUE SPACES.                
           05  WS-FIRST-WAITTIME       PIC X(2)    VALUE SPACES.                
           05  WS-SAVE-WAITTIME        PIC X(2)    VALUE SPACES.                
           05  WS-SAVE-DTE-BALLOT      PIC X(06)   VALUE SPACES.                
           05  WS-SAVE-REQ-STATUS      PIC X(1)    VALUE SPACES.                
           05  WS-SAVE-COHORT          PIC X(2)    VALUE SPACES.                
           05  WS-SAVE-MCPS            PIC X(1)    VALUE SPACES.                
           05  WS-F520-MCPS            PIC X(1)    VALUE SPACES.                
           05  WS-F520-ALLOC-SCH       PIC X(3)    VALUE SPACES.                
           05  WS-PARM-NUM-CDE         PIC X(3).                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      **********************************************************                
       000-MAIN-ROUTINE.                                                        
      **********************************************************                
                                                                                
           PERFORM 100-OPEN-FILES                                               
              THRU 100-EXIT.                                                    
           PERFORM 200-READ-BP13F520                                            
              THRU 200-EXIT.                                                    
                                                                                
           IF F520-CDE-ZONE = SPACES OR LOW-VALUES                              
              MOVE F520-CDE-NT1     TO  WS-SAVE-ZONE                            
           ELSE                                                                 
              MOVE F520-CDE-ZONE    TO  WS-SAVE-ZONE.                           
           MOVE F520-CDE-FLAT-TYPE  TO  WS-SAVE-FLAT-TYPE.                      
           MOVE WS-F520-ALLOC-SCH   TO  WS-SAVE-PRIOSCH.                        
           MOVE WS-F520-MCPS        TO  WS-SAVE-MCPS.                           
           MOVE F520-CDE-BALLOT-HOUSEHOLD TO WS-SAVE-HHTY.                      
           MOVE WS-F520-CATEGORY    TO  WS-SAVE-CATEGORY.                       
           MOVE F520-NUM-SEQUENCE   TO  WS-FIRST-SEQUENCE.                      
           MOVE F520-NUM-REGN       TO  WS-FIRST-REGN.                          
           MOVE F520-TME-PIGEONHOLE TO  WS-FIRST-WAITTIME.                      
           MOVE F520-CDE-REQUEST-STATUS TO WS-SAVE-REQ-STATUS.                  
           MOVE F520-NUM-RANDOM     TO  WS-SAVE-RANDOM.                         
           MOVE F520-CDE-COHORT     TO  WS-SAVE-COHORT.                         
                                                                                
           PERFORM 300-PROCESS-F520                                             
              THRU 300-EXIT                                                     
             UNTIL WS-F520-EOF = 'Y'.                                           
           IF WS-PARM-NUM-CDE = 'PA1'                                           
              PERFORM 320-WRITE-PA1                                             
                 THRU 320-EXIT                                                  
           ELSE                                                                 
              PERFORM 400-WRITE-BP13F747                                        
                 THRU 400-EXIT                                                  
           END-IF.                                                              
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       100-OPEN-FILES.                                                          
      **********************************************************                
                                                                                
           OPEN INPUT  BP13F520                                                 
                OUTPUT BP13F747.                                                
                                                                                
      *** ACCEPT VALUE FROM JCL ***                                             
           ACCEPT WS-PARM-NUM-CDE.                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       200-READ-BP13F520.                                                       
      **********************************************************                
                                                                                
           READ BP13F520                                                        
              AT END                                                            
              MOVE 'Y' TO  WS-F520-EOF                                          
              GO       TO  300-EXIT.                                            
                                                                                
           ADD  1      TO  WS-CNT-F520-READ.                                    
           IF F520-CDE-CATEGORY = '4'                                           
              MOVE '3'               TO  WS-F520-CATEGORY                       
           ELSE                                                                 
              MOVE F520-CDE-CATEGORY TO  WS-F520-CATEGORY.                      
                                                                                
           IF WS-PARM-NUM-CDE = 'PPS' OR 'AST' OR 'FTS' OR 'MCP' OR             
                                'NSP' OR 'PP1'                                  
              MOVE WS-PARM-NUM-CDE    TO WS-F520-ALLOC-SCH                      
           ELSE                                                                 
              MOVE F520-CDE-ALLOC-SCH TO WS-F520-ALLOC-SCH                      
           END-IF.                                                              
                                                                                
           IF WS-F520-ALLOC-SCH = 'MCP'                                         
              MOVE F520-NUM-MCPS      TO WS-F520-MCPS                           
           ELSE                                                                 
              MOVE SPACES             TO WS-F520-MCPS                           
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       300-PROCESS-F520.                                                        
      **********************************************************                
                                                                                
            IF WS-F520-ALLOC-SCH = 'NSP' OR 'SAP' OR 'TCP' OR 'REN' OR          
                                   'OTH'                                        
              IF ((F520-CDE-ZONE   NOT = WS-SAVE-ZONE     ) AND                 
                  (F520-CDE-NT1    NOT = WS-SAVE-ZONE     )) OR                 
                 (F520-CDE-FLAT-TYPE NOT = WS-SAVE-FLAT-TYPE) OR                
                 (WS-F520-ALLOC-SCH  NOT = WS-SAVE-PRIOSCH ) OR                 
                 (WS-F520-CATEGORY NOT = WS-SAVE-CATEGORY ) OR                  
                 (F520-CDE-REQUEST-STATUS                                       
                                     NOT = WS-SAVE-REQ-STATUS) OR               
                 (F520-CDE-COHORT NOT = WS-SAVE-COHORT)                         
                 PERFORM 400-WRITE-BP13F747                                     
                    THRU 400-EXIT                                               
                                                                                
                 MOVE F520-NUM-SEQUENCE TO WS-FIRST-SEQUENCE                    
                 MOVE F520-NUM-REGN    TO  WS-FIRST-REGN                        
                 MOVE F520-TME-PIGEONHOLE TO WS-FIRST-WAITTIME                  
                                                                                
                 IF F520-CDE-ZONE = SPACES OR LOW-VALUES                        
                    MOVE F520-CDE-NT1     TO  WS-SAVE-ZONE                      
                 ELSE                                                           
                    MOVE F520-CDE-ZONE    TO  WS-SAVE-ZONE                      
                 END-IF                                                         
                 MOVE F520-CDE-FLAT-TYPE TO WS-SAVE-FLAT-TYPE                   
                 MOVE WS-F520-ALLOC-SCH  TO WS-SAVE-PRIOSCH                     
                 MOVE WS-F520-MCPS       TO WS-SAVE-MCPS                        
                 MOVE F520-CDE-BALLOT-HOUSEHOLD TO WS-SAVE-HHTY                 
                 MOVE WS-F520-CATEGORY TO  WS-SAVE-CATEGORY                     
                 MOVE F520-CDE-REQUEST-STATUS                                   
                                          TO WS-SAVE-REQ-STATUS                 
                 MOVE F520-CDE-COHORT                                           
                                          TO WS-SAVE-COHORT                     
              END-IF                                                            
            ELSE                                                                
              IF WS-PARM-NUM-CDE = 'PA1'                                        
                PERFORM 310-CHECK-CAT THRU 310-EXIT                             
                IF ((F520-CDE-ZONE   NOT = WS-SAVE-ZONE     ) AND               
                    (F520-CDE-NT1    NOT = WS-SAVE-ZONE     )) OR               
                   (F520-CDE-FLAT-TYPE NOT = WS-SAVE-FLAT-TYPE) OR              
                   (WS-F520-ALLOC-SCH  NOT = WS-SAVE-PRIOSCH ) OR               
                   (F520-CDE-BALLOT-HOUSEHOLD NOT = WS-SAVE-HHTY ) OR           
                   (F520-CDE-REQUEST-STATUS                                     
                                       NOT = WS-SAVE-REQ-STATUS) OR             
                   (F520-CDE-COHORT NOT = WS-SAVE-COHORT)                       
                   PERFORM 320-WRITE-PA1 THRU 320-EXIT                          
                   MOVE 'N'            TO WS-CAT1-FND                           
                                          WS-CAT2-FND                           
                                          WS-CAT3-FND                           
                   PERFORM 310-CHECK-CAT THRU 310-EXIT                          
                                                                                
                   MOVE F520-NUM-SEQUENCE TO WS-FIRST-SEQUENCE                  
                   MOVE F520-NUM-REGN    TO  WS-FIRST-REGN                      
                   MOVE F520-TME-PIGEONHOLE TO WS-FIRST-WAITTIME                
                                                                                
                   IF F520-CDE-ZONE = SPACES OR LOW-VALUES                      
                      MOVE F520-CDE-NT1     TO  WS-SAVE-ZONE                    
                   ELSE                                                         
                      MOVE F520-CDE-ZONE    TO  WS-SAVE-ZONE                    
                   END-IF                                                       
                   MOVE F520-CDE-FLAT-TYPE TO WS-SAVE-FLAT-TYPE                 
                   MOVE WS-F520-ALLOC-SCH  TO WS-SAVE-PRIOSCH                   
                   MOVE WS-F520-MCPS       TO WS-SAVE-MCPS                      
                   MOVE F520-CDE-BALLOT-HOUSEHOLD TO WS-SAVE-HHTY               
                   MOVE WS-F520-CATEGORY TO  WS-SAVE-CATEGORY                   
                   MOVE F520-CDE-REQUEST-STATUS                                 
                                            TO WS-SAVE-REQ-STATUS               
                   MOVE F520-CDE-COHORT                                         
                                            TO WS-SAVE-COHORT                   
                END-IF                                                          
              ELSE                                                              
                IF ((F520-CDE-ZONE   NOT = WS-SAVE-ZONE     ) AND               
                    (F520-CDE-NT1    NOT = WS-SAVE-ZONE     )) OR               
                   (F520-CDE-FLAT-TYPE NOT = WS-SAVE-FLAT-TYPE) OR              
                   (WS-F520-ALLOC-SCH  NOT = WS-SAVE-PRIOSCH ) OR               
                   (WS-F520-MCPS       NOT = WS-SAVE-MCPS) OR                   
                   (F520-CDE-BALLOT-HOUSEHOLD NOT = WS-SAVE-HHTY ) OR           
                   (WS-F520-CATEGORY NOT = WS-SAVE-CATEGORY ) OR                
                   (F520-CDE-REQUEST-STATUS                                     
                                       NOT = WS-SAVE-REQ-STATUS) OR             
                   (F520-CDE-COHORT NOT = WS-SAVE-COHORT)                       
                   PERFORM 400-WRITE-BP13F747                                   
                      THRU 400-EXIT                                             
                                                                                
                   MOVE F520-NUM-SEQUENCE TO WS-FIRST-SEQUENCE                  
                   MOVE F520-NUM-REGN    TO  WS-FIRST-REGN                      
                   MOVE F520-TME-PIGEONHOLE TO WS-FIRST-WAITTIME                
                                                                                
                   IF F520-CDE-ZONE = SPACES OR LOW-VALUES                      
                      MOVE F520-CDE-NT1     TO  WS-SAVE-ZONE                    
                   ELSE                                                         
                      MOVE F520-CDE-ZONE    TO  WS-SAVE-ZONE                    
                   END-IF                                                       
                   MOVE F520-CDE-FLAT-TYPE TO WS-SAVE-FLAT-TYPE                 
                   MOVE WS-F520-ALLOC-SCH  TO WS-SAVE-PRIOSCH                   
                   MOVE WS-F520-MCPS       TO WS-SAVE-MCPS                      
                   MOVE F520-CDE-BALLOT-HOUSEHOLD TO WS-SAVE-HHTY               
                   MOVE WS-F520-CATEGORY TO  WS-SAVE-CATEGORY                   
                   MOVE F520-CDE-REQUEST-STATUS                                 
                                            TO WS-SAVE-REQ-STATUS               
                   MOVE F520-CDE-COHORT                                         
                                            TO WS-SAVE-COHORT                   
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F520-DTE-BALLOT = SPACES OR LOW-VALUES                            
              MOVE SPACES              TO WS-SAVE-DTE-BALLOT                    
           ELSE                                                                 
              MOVE F520-DTE-BALLOT     TO  WS-SAVE-DTE-BALLOT                   
           END-IF.                                                              
                                                                                
           MOVE F520-NUM-SEQUENCE      TO  WS-SAVE-SEQUENCE.                    
           MOVE F520-NUM-REGN          TO  WS-SAVE-REGN.                        
           MOVE F520-TME-PIGEONHOLE    TO  WS-SAVE-WAITTIME.                    
           MOVE F520-NUM-RANDOM        TO  WS-SAVE-RANDOM.                      
           MOVE F520-CDE-REQUEST-STATUS                                         
                                       TO  WS-SAVE-REQ-STATUS                   
           MOVE F520-CDE-COHORT                                                 
                                       TO  WS-SAVE-COHORT                       
                                                                                
           PERFORM 200-READ-BP13F520                                            
              THRU 200-EXIT.                                                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       310-CHECK-CAT.                                                           
      **********************************************************                
                                                                                
           EVALUATE WS-F520-CATEGORY                                            
              WHEN '1'                                                          
                 MOVE 'Y'              TO WS-CAT1-FND                           
                                                                                
              WHEN '2'                                                          
                 MOVE 'Y'              TO WS-CAT2-FND                           
                                                                                
              WHEN '3'                                                          
                 MOVE 'Y'              TO WS-CAT3-FND                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       320-WRITE-PA1.                                                           
      **********************************************************                
                                                                                
           IF WS-CAT1-FND = 'Y'                                                 
             MOVE '1'                  TO WS-SAVE-CATEGORY                      
             PERFORM 400-WRITE-BP13F747 THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-CAT2-FND = 'Y'                                                 
             MOVE '2'                  TO WS-SAVE-CATEGORY                      
             PERFORM 400-WRITE-BP13F747 THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-CAT3-FND = 'Y'                                                 
             MOVE '3'                  TO WS-SAVE-CATEGORY                      
             PERFORM 400-WRITE-BP13F747 THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       400-WRITE-BP13F747.                                                      
      **********************************************************                
                                                                                
           MOVE  SPACES                TO  BP13F747-REC.                        
                                                                                
           MOVE  WS-SAVE-DTE-BALLOT    TO  F747-DTE-BALLOT.                     
           MOVE  WS-SAVE-ZONE          TO  F747-NUM-ZONE.                       
           MOVE  WS-SAVE-FLAT-TYPE     TO  F747-NUM-FLAT-TYPE.                  
           MOVE  WS-SAVE-PRIOSCH       TO  F747-NUM-PRIORITY-SCH.               
           MOVE  WS-SAVE-MCPS          TO  F747-NUM-MCPS.                       
           MOVE  WS-SAVE-HHTY          TO  F747-NUM-HHTY.                       
           MOVE  WS-SAVE-CATEGORY      TO  F747-NUM-CATEGORY.                   
           MOVE  WS-SAVE-REQ-STATUS    TO  F747-NUM-RESULT-STATUS.              
           MOVE  WS-SAVE-COHORT        TO  F747-NUM-COHORT.                     
                                                                                
           MOVE  WS-SAVE-REGN          TO  F747-NUM-LAST-REGN.                  
           MOVE  WS-SAVE-RANDOM        TO  F747-NUM-LAST-RANDOM-NO.             
                                                                                
           IF BP13F747-REC NOT = SPACES                                         
              WRITE BP13F747-REC                                                
              ADD   1                     TO  WS-CNT-F747-WRITE                 
           END-IF.                                                              
                                                                                
           IF WS-SAVE-PRIOSCH = 'NSP' OR 'SAP' OR 'TCP' OR 'REN' OR             
                                'OTH'                                           
              MOVE  SPACES                TO  BP13F747-REC                      
              IF WS-SAVE-HHTY = 'H'                                             
                MOVE  'G'                   TO  F747-NUM-HHTY                   
              ELSE                                                              
                MOVE  'H'                   TO  F747-NUM-HHTY                   
              END-IF                                                            
              MOVE WS-SAVE-DTE-BALLOT  TO  F747-DTE-BALLOT                      
              MOVE WS-SAVE-ZONE        TO  F747-NUM-ZONE                        
              MOVE WS-SAVE-FLAT-TYPE   TO  F747-NUM-FLAT-TYPE                   
              MOVE WS-SAVE-PRIOSCH     TO  F747-NUM-PRIORITY-SCH                
              MOVE WS-SAVE-MCPS        TO  F747-NUM-MCPS                        
              MOVE WS-SAVE-CATEGORY    TO  F747-NUM-CATEGORY                    
              MOVE WS-SAVE-REQ-STATUS  TO  F747-NUM-RESULT-STATUS               
              MOVE WS-SAVE-COHORT      TO  F747-NUM-COHORT                      
                                                                                
              MOVE WS-SAVE-REGN        TO  F747-NUM-LAST-REGN                   
              MOVE WS-SAVE-RANDOM      TO  F747-NUM-LAST-RANDOM-NO              
              WRITE BP13F747-REC                                                
              ADD   1                     TO  WS-CNT-F747-WRITE                 
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
       900-CLOSE-ROUTINE.                                                       
      **********************************************************                
                                                                                
           CLOSE BP13F520                                                       
                 BP13F747.                                                      
                                                                                
           DISPLAY ' ** BP13C548 ** '.                                          
           DISPLAY 'NO OF REC READ FROM REQUEST REGN FILE(BP13F520) : '         
                    WS-CNT-F520-READ.                                           
           DISPLAY 'NO OF REC WRITTEN TO SUCC REGN  FILE (BP13F747) : '         
                    WS-CNT-F747-WRITE.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
