       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C11M.                                                 
      *AUTHOR.        ZARITA SARMIENTO.                                         
      *DATE-WRITTEN.  13/11/2018.                                               
      * ========================================================== *            
      * OBJECTIVE    :  CREATE BP13KC65 HISTORY FILE               *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC55                               *            
      * I-O    FILES :  1.) BP13KC65                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP137466 ZS13 13/11/2018 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC55  ASSIGN        TO BP13FC55.                          
                                                                                
           SELECT BP13KC65  ASSIGN        TO BP13KC65                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC65-KEY-FLD                       
                            FILE STATUS   IS WS-KC65-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC55                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13FC55.                                                       
                                                                                
       FD  BP13KC65                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC65.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KC65-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC55-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC65-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC55-EOF               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13FC55-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC55-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC55                                                 
                I-O    BP13KC65.                                                
                                                                                
           IF WS-KC65-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC65 ERROR ' WS-KC65-STATUS                  
              MOVE     WS-KC65-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC55-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC55                                                        
              AT END MOVE 'Y'              TO   WS-FC55-EOF.                    
                                                                                
           IF WS-FC55-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-FC55-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    IF NOT YET UPDATED, SKIP RECORD                                      
      *-------------------------------------------------------------            
           IF FC55-NUM-PEND-SOCA-RSN = SPACES OR LOW-VALUES OR ZEROES           
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
      *-------------------------------------------------------------            
      *    CREATE HISTORY FILE                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13KC65-REC.                   
           MOVE BP13FC55-REC               TO   BP13KC65-REC(1:100).            
           MOVE FC55-NUM-OIC               TO   KC65-NUM-OIC-HIST.              
           MOVE FC55-NUM-REGN              TO   KC65-NUM-REGN-HIST.             
           MOVE FUNCTION CURRENT-DATE(1:8) TO   KC65-DTE-CREATE-HIST.           
           MOVE FUNCTION CURRENT-DATE(9:8) TO   KC65-TME-CREATE-HIST.           
                                                                                
           PERFORM 3010-BP13KC65-WRITE     THRU 3010-EXIT.                      
                                                                                
       3000-PROCEED-READ-NEXT.                                                  
                                                                                
           PERFORM 2000-BP13FC55-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13KC65-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC65-REC.                                                  
                                                                                
           EVALUATE WS-KC65-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-KC65-WRITE               
                                                                                
           WHEN 22                                                              
              DISPLAY 'DUPLICATE KC65 RECORD: ' FC55-NUM-REGN                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13KC65 ERROR ' WS-KC65-STATUS                    
              MOVE     WS-KC65-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC55                                                       
                 BP13KC65.                                                      
                                                                                
           IF WS-KC65-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC65 ERROR ' WS-KC65-STATUS                  
              MOVE     WS-KC65-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C11M CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FC55 READ      : ' WS-CNT-FC55-READ.          
           DISPLAY ' 2. NO OF BP13KC65 WRITE     : ' WS-CNT-KC65-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
