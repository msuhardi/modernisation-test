       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C173.                                                 
      *AUTHOR.        CKK7.                                                     
      *DATE-WRITTEN.  24/08/2004.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : TO SEND EMAIL TO CASES WHO HAS SET EMAIL ALERT*            
      *                                                            *            
      *                                                            *            
      * INPUT      : BP13F022 - INPUT FILE SET FOR EMAIL ALERT     *            
      *              BP13K800 - SOC MASTER FILE                    *            
      *              BP13K431 - BORROWER CODE FILE                 *            
      *              BP13K435 - BORROWER FILE. FOUND = BORROWED    *            
      * OUTPUT     : BM06F120 - OUTPUT FILE TO PASS TO BP13C172    *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTIONS                       *            
      * -------- -------- ----- ------------                       *            
      * BP132551 20040824 CKK7  NEW PROGRAM                        *            
      * BP134603 20120815 IMC1  INCLUDE BO CODE FOR NON-BO CASES   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
            SELECT BP13F022 ASSIGN TO BP13F022.                                 
                                                                                
            SELECT BM06K110 ASSIGN       TO BM06K110                            
                   ACCESS MODE           IS RANDOM                              
                   ORGANIZATION          IS INDEXED                             
                   RECORD KEY            IS K110-SCH-ACC-NO                     
                   FILE STATUS           IS BM06K110-STATUS.                    
                                                                                
            SELECT BP13K800 ASSIGN       TO BP13K800                            
                   ACCESS MODE           IS RANDOM                              
                   ORGANIZATION          IS INDEXED                             
                   RECORD KEY            IS K800-NUM-REGN                       
                   FILE STATUS           IS BP13K800-STATUS.                    
                                                                                
            SELECT BP13K431 ASSIGN       TO BP13K431                            
                   ACCESS MODE           IS RANDOM                              
                   ORGANIZATION          IS INDEXED                             
                   RECORD KEY            IS K431-KEY-FLD                        
                   FILE STATUS           IS BP13K431-STATUS.                    
                                                                                
            SELECT BP13K435 ASSIGN       TO BP13K435                            
                   ACCESS MODE           IS DYNAMIC                             
                   ORGANIZATION          IS INDEXED                             
                   RECORD KEY            IS K435-KEY-FLD                        
                   FILE STATUS           IS BP13K435-STATUS.                    
                                                                                
            SELECT BM06F120  ASSIGN TO BM06F120.                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F022                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F022.                                                           
                                                                                
       FD   BM06K110.                                                           
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K431.                                                           
       COPY BP13K431.                                                           
                                                                                
       FD   BP13K435.                                                           
       COPY BP13K435.                                                           
                                                                                
       FD   BM06F120                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            RECORDING  MODE  IS F.                                              
       COPY BM06F120.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 BM06K110-STATUS              PIC 9(2)   VALUE ZEROS.                  
       01 BP13K800-STATUS              PIC 9(2)   VALUE ZEROS.                  
       01 BP13K431-STATUS              PIC 9(2)   VALUE ZEROS.                  
       01 BP13K435-STATUS              PIC 9(2)   VALUE ZEROS.                  
       01 WS-F022-EOF                  PIC X      VALUE 'N'.                    
       01 WS-K435-EOF                  PIC X      VALUE 'N'.                    
       01 WS-K435-NOTFND               PIC X      VALUE 'N'.                    
       01 WS-REC-SKIP                  PIC X      VALUE 'N'.                    
       01 WS-K800-NOTFND               PIC 9(8)   VALUE ZEROS.                  
       01 WS-K431-NOTFND               PIC 9(8)   VALUE ZEROS.                  
       01 WS-K800-REC-UNMATCH          PIC 9(8)   VALUE ZEROS.                  
       01 WS-K110-NOTFND               PIC 9(8)   VALUE ZEROS.                  
       01 WS-F022-READ                 PIC 9(8)   VALUE ZEROS.                  
       01 WS-BP13K800-READ             PIC 9(8)   VALUE ZEROS.                  
       01 WS-BM06K110-READ             PIC 9(8)   VALUE ZEROS.                  
       01 WS-BM06F120-WRITE            PIC 9(8)   VALUE ZEROS.                  
       01 WS-BORROWER                  PIC X(10)  VALUE SPACES.                 
       01 WS-DTE-KEY-BORROW            PIC X(08)  VALUE SPACES.                 
                                                                                
       01 WS-DATE                      PIC X(8)   VALUE SPACES.                 
       01 WS-TIME                      PIC X(8)   VALUE SPACES.                 
       01 WS-RETCODE                   PIC -ZZZZ.                               
                                                                                
       01 WS-PREV-NUM-HDB-REF          PIC X(11)  VALUE SPACES.                 
       01 WS-PREV-BO-CODE              PIC X(2)   VALUE SPACES.                 
                                                                                
       01  WS-BO-CODE-LIST.                                                     
           05 FILLER                   PIC X(02)  VALUE 'AM'.                   
           05 FILLER                   PIC X(02)  VALUE 'BA'.                   
           05 FILLER                   PIC X(02)  VALUE 'BB'.                   
           05 FILLER                   PIC X(02)  VALUE 'BD'.                   
           05 FILLER                   PIC X(02)  VALUE 'BI'.                   
           05 FILLER                   PIC X(02)  VALUE 'BM'.                   
           05 FILLER                   PIC X(02)  VALUE 'BU'.                   
           05 FILLER                   PIC X(02)  VALUE 'CK'.                   
           05 FILLER                   PIC X(02)  VALUE 'CL'.                   
           05 FILLER                   PIC X(02)  VALUE 'CS'.                   
           05 FILLER                   PIC X(02)  VALUE 'GE'.                   
           05 FILLER                   PIC X(02)  VALUE 'HG'.                   
           05 FILLER                   PIC X(02)  VALUE 'IN'.                   
           05 FILLER                   PIC X(02)  VALUE 'PR'.                   
           05 FILLER                   PIC X(02)  VALUE 'QX'.                   
           05 FILLER                   PIC X(02)  VALUE 'SD'.                   
           05 FILLER                   PIC X(02)  VALUE 'SK'.                   
           05 FILLER                   PIC X(02)  VALUE 'SW'.                   
           05 FILLER                   PIC X(02)  VALUE 'TM'.                   
           05 FILLER                   PIC X(02)  VALUE 'TP'.                   
           05 FILLER                   PIC X(02)  VALUE 'UE'.                   
           05 FILLER                   PIC X(02)  VALUE 'UW'.                   
           05 FILLER                   PIC X(02)  VALUE 'WL'.                   
       01  FILLER  REDEFINES  WS-BO-CODE-LIST.                                  
           05 WS-BO-TABLE              OCCURS 23                                
                                       INDEXED BY WS-BO-IND.                    
              10 WS-BO-CODE            PIC X(02).                               
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2000-READ-BP13F022    THRU 2000-EXIT.                       
            IF F022-CDE-ALERT-TYPE = 'BO'                                       
               PERFORM 3000-PROCESS-DETAIL-BO   THRU 3000-EXIT                  
                       UNTIL WS-F022-EOF = 'Y'                                  
            ELSE                                                                
               PERFORM 3500-PROCESS-DETAIL-SO   THRU 3500-EXIT                  
                       UNTIL WS-F022-EOF = 'Y'                                  
            END-IF.                                                             
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT  BP13F022                                               
                         BM06K110                                               
                         BP13K800                                               
                         BP13K431                                               
                         BP13K435                                               
                  OUTPUT BM06F120.                                              
                                                                                
            IF BM06K110-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K110-STATUS IS ' BM06K110-STATUS          
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K431-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K431-STATUS IS ' BP13K431-STATUS          
               MOVE BP13K431-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K435-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K435-STATUS IS ' BP13K435-STATUS          
               MOVE BP13K435-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                         
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F022.                                                      
      *-------------------------------------------------------------            
            READ BP13F022                                                       
                 AT END MOVE 'Y' TO WS-F022-EOF                                 
                 GO TO 2000-EXIT.                                               
                                                                                
            MOVE 'N' TO WS-REC-SKIP.                                            
            ADD 1   TO WS-F022-READ.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL-BO.                                                  
      *-------------------------------------------------------------            
            PERFORM 3100-READ-BP13K800     THRU 3100-EXIT.                      
            IF WS-REC-SKIP NOT = 'Y'                                            
               PERFORM 4000-READ-BP13K435     THRU 4000-EXIT.                   
            PERFORM 2000-READ-BP13F022     THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES               TO K800-NUM-REGN.                         
            MOVE F022-NUM-REGN        TO K800-NUM-REGN.                         
                                                                                
            READ BP13K800.                                                      
            EVALUATE BP13K800-STATUS                                            
            WHEN 00                                                             
                 ADD  1 TO WS-BP13K800-READ                                     
                 IF K800-NUM-SCH-ACC NOT = SPACES AND                           
                                           LOW-VALUES AND                       
                                           ZEROES                               
                    PERFORM 3200-READ-BM06K110 THRU 3200-EXIT                   
                 ELSE                                                           
                    ADD  1 TO WS-K800-REC-UNMATCH                               
                    DISPLAY 'BP13K800 SCH ACC BLANK : ' K800-NUM-REGN           
                    MOVE 'Y' TO WS-REC-SKIP                                     
                 END-IF                                                         
            WHEN 23                                                             
                 ADD  1 TO WS-K800-NOTFND                                       
                 DISPLAY 'BP13K800 NOT FND : ' K800-NUM-REGN                    
                 MOVE 'Y' TO WS-REC-SKIP                                        
            WHEN OTHER                                                          
                 DISPLAY 'BP13K800 READ ERROR : ' BP13K800-STATUS               
                         ', REGN IS  ' K800-NUM-REGN                            
                 MOVE BP13K800-STATUS         TO   RETURN-CODE                  
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3200-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES               TO BP13COMM8-REC.                         
            INITIALIZE                   BP13COMM8-REC.                         
                                                                                
            MOVE SPACES               TO K110-SCH-ACC-NO.                       
            MOVE K800-NUM-SCH-ACC     TO K110-SCH-ACC-NO.                       
                                                                                
            READ BM06K110                                                       
            EVALUATE BM06K110-STATUS                                            
            WHEN 00                                                             
                 ADD  1 TO WS-BM06K110-READ                                     
                 MOVE K110-SCH-ACC-NO        TO WS-LINK-REC                     
                 PERFORM 3300-READ-PIDB      THRU 3300-EXIT                     
            WHEN 23                                                             
                 ADD  1 TO WS-K110-NOTFND                                       
                 DISPLAY 'BM06K110 NOT FND : ' K110-SCH-ACC-NO                  
                 MOVE 'Y' TO WS-REC-SKIP                                        
            WHEN OTHER                                                          
                 DISPLAY 'BM06K110 READ ERROR : ' BM06K110-STATUS               
                 DISPLAY 'BM06K110 KEY-FLD    : ' K110-SCH-ACC-NO               
                 MOVE BM06K110-STATUS         TO   RETURN-CODE                  
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3300-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
              CONTINUE                                                          
           WHEN +100                                                            
              MOVE SPACES   TO BP13COMM8-REC                                    
              INITIALIZE       BP13COMM8-REC                                    
              DISPLAY 'REC NOT FOUND IN PIDB: ' K110-SCH-ACC-NO                 
              MOVE 'Y' TO WS-REC-SKIP                                           
           WHEN OTHER                                                           
              MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                          
              DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                    
                      WS-RETCODE                                                
      *       MOVE 99                    TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-PROCESS-DETAIL-SO.                                                  
      *-------------------------------------------------------------            
            PERFORM 3100-READ-BP13K800     THRU 3100-EXIT.                      
            IF WS-REC-SKIP NOT = 'Y'                                            
               PERFORM 4000-READ-BP13K435     THRU 4000-EXIT.                   
            PERFORM 2000-READ-BP13F022     THRU 2000-EXIT.                      
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K435.                                                      
      *-------------------------------------------------------------            
           MOVE 'N'                TO  WS-K435-EOF                              
                                       WS-K435-NOTFND.                          
           MOVE SPACES             TO  WS-BORROWER.                             
                                                                                
           MOVE SPACES             TO  K435-KEY-FLD.                            
           MOVE K110-SCH-ACC-NO    TO  K435-NUM-HDB-REF                         
                                       WS-PREV-NUM-HDB-REF.                     
                                                                                
           START BP13K435 KEY >= K435-KEY-FLD.                                  
           EVALUATE BP13K435-STATUS                                             
           WHEN 00                                                              
                PERFORM 4100-READNEXT-BP13K435 THRU 4100-EXIT                   
                   UNTIL WS-K435-NOTFND = 'Y'                                   
                      OR WS-K435-EOF = 'Y'                                      
                IF WS-BORROWER = SPACES                                         
                   IF F022-CDE-ALERT-TYPE = 'SO'                                
                      PERFORM 5000-WRITE-F120     THRU 5000-EXIT                
                   ELSE                                                         
                      DISPLAY 'NOT FOUND IN BP13K435 ' K110-SCH-ACC-NO          
                   END-IF                                                       
                ELSE                                                            
                   IF F022-CDE-ALERT-TYPE = 'SO'                                
                      CONTINUE                                                  
                   ELSE                                                         
                      PERFORM 4200-READ-BP13K431  THRU 4200-EXIT                
                      PERFORM 5000-WRITE-F120     THRU 5000-EXIT                
                   END-IF                                                       
                END-IF                                                          
           WHEN 23                                                              
                IF F022-CDE-ALERT-TYPE = 'SO'                                   
                   PERFORM 5000-WRITE-F120     THRU 5000-EXIT                   
                ELSE                                                            
                   DISPLAY 'NOT FOUND IN BP13K435 ' K110-SCH-ACC-NO             
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K435, STATUS  '                        
                        BP13K435-STATUS                                         
                        ',  HDB REF ' K435-NUM-HDB-REF                          
                MOVE BP13K435-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4100-READNEXT-BP13K435.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K435 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K435-EOF.                                 
                                                                                
           EVALUATE BP13K435-STATUS                                             
           WHEN 00                                                              
                IF K435-NUM-HDB-REF = K110-SCH-ACC-NO                           
                   IF K435-NUM-BORROWER NOT = WS-BORROWER AND                   
                      K435-DTE-KEY-BORROW > WS-DTE-KEY-BORROW                   
                      MOVE K435-NUM-BORROWER TO WS-BORROWER                     
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y' TO WS-K435-NOTFND                                   
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y' TO WS-K435-EOF                                         
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K435, STATUS  '                      
                        BP13K435-STATUS                                         
                        ',  HDB REF ' K435-NUM-HDB-REF                          
                MOVE BP13K435-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4200-READ-BP13K431.                                                      
      ******************************************************************        
            MOVE SPACES                   TO K431-KEY-FLD.                      
            MOVE WS-BORROWER              TO K431-NUM-BORROWER.                 
                                                                                
            READ BP13K431.                                                      
            EVALUATE BP13K431-STATUS                                            
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   ADD  1 TO WS-K431-NOTFND                                     
                   DISPLAY 'RECORD NOT FOUND IN BP13K431, KEY: '                
                            K431-KEY-FLD                                        
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K431,STATU ' BP13K431-STATUS         
                   MOVE BP13K431-STATUS       TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-F120.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES                 TO F120-REC.                             
           INITIALIZE                     F120-REC.                             
                                                                                
           MOVE K110-REC               TO F120-REC(1:500).                      
                                                                                
           IF F022-CDE-ALERT-TYPE = 'SO'                                        
              MOVE F022-NUM-OIC        TO F120-REC(690:10)                      
           ELSE                                                                 
              SET WS-BO-IND TO 1                                                
              SEARCH WS-BO-TABLE                                                
                  AT END                                                        
                     MOVE SPACES TO F120-BO-CODE                                
                  WHEN (WS-BO-CODE (WS-BO-IND) = WS-BORROWER(1:2)) AND          
                       (WS-BORROWER(3:1) = '0')                                 
                        MOVE WS-BO-CODE (WS-BO-IND) TO F120-BO-CODE             
              END-SEARCH                                                        
              MOVE K431-NUM-EMAIL      TO F120-REC(690:10)                      
           END-IF.                                                              
                                                                                
      *    MOVE COMM8-NUM-MGMT(1:2)    TO F120-BO-CODE.                         
           MOVE COMM8-NME-STREET       TO F120-STREET-NAME.                     
           MOVE COMM8-NUM-LEVEL        TO F120-LEVEL-NO.                        
           MOVE COMM8-NUM-UNIT-MAIN    TO F120-MAIN-UNIT.                       
           MOVE COMM8-NUM-UNIT-SUB     TO F120-SUB-UNIT.                        
           MOVE COMM8-NUM-POSTAL-CODE  TO F120-POSTAL-CODE.                     
           MOVE COMM8-CDE-TYP-ACTLUSE  TO F120-CDE-TYP-ACTUSE.                  
                                                                                
           IF F120-BO-CODE = SPACES AND                                         
              F022-CDE-ALERT-TYPE = 'BO'                                        
              IF WS-K435-NOTFND NOT = 'Y'                                       
                 MOVE K435-NUM-KEY-LOCN   TO F120-BO-CODE                       
              ELSE                                                              
                 MOVE COMM8-NUM-MGMT(1:2) TO F120-BO-CODE                       
              END-IF                                                            
              MOVE 'NB'                   TO F022-CDE-ALERT-TYPE                
           END-IF                                                               
                                                                                
           MOVE F022-CDE-ALERT-TYPE  TO  F120-REC(688:2).                       
                                                                                
           WRITE F120-REC.                                                      
           ADD 1 TO WS-BM06F120-WRITE.                                          
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY ' '.                                                        
            DISPLAY '- BP13C173 CONTROL LIST ----------'                        
            DISPLAY '---------------------------------------------'.            
            DISPLAY ' 1) TOT BP13F022 READ     : ' WS-F022-READ.                
            DISPLAY ' 2) TOT BM06F120 WRITE    : ' WS-BM06F120-WRITE.           
            DISPLAY ' 3) TOT BP13K800 READ     : ' WS-BP13K800-READ.            
            DISPLAY ' 4) TOT BM06K110 READ     : ' WS-BM06K110-READ.            
            DISPLAY ' 5) TOT BP13K800 NOT FND  : ' WS-K800-NOTFND.              
            DISPLAY ' 6) TOT BM06K110 NOT FND  : ' WS-K110-NOTFND.              
            DISPLAY ' 7) TOT BP13K431 NOT FND  : ' WS-K431-NOTFND.              
            DISPLAY ' '.                                                        
                                                                                
            CLOSE  BP13F022                                                     
                   BM06K110                                                     
                   BP13K431                                                     
                   BP13K435                                                     
                   BP13K800                                                     
                   BM06F120.                                                    
                                                                                
            IF BP13K435-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR, BP13K435   ' BP13K435-STATUS             
               MOVE BP13K435-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
