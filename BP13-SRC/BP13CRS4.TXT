      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID. BP13CRS4.                                                    
       AUTHOR. MCC.                                                             
      *DATE-WRITTEN. 27/01/2003.                                                
                                                                                
      ******************************************************************        
      *                    SYSTEM OF COMMITMENT (BP13)                 *        
      *================================================================*        
      *  OBJECTIVE:  1.  TO DO A ONE TIME PROGRAM TO CREATE K761 FILE  *        
      *                                                                *        
      *  FILES : BP13F740   - INPUT                                    *        
      *          BP13K761   - I-O                                      *        
      *                                                                *        
      *  REPORT: BP13LC30   - OUTPUT                                   *        
      *                                                                *        
      *================================================================*        
      *  MODIFICATIONS :                                               *        
      *  CHGE REQ #  DATE          AUTHOR   REMARKS                    *        
      *  ----------  ----          ------   -------                    *        
      *  BP132288    22012002        MCC    NEW PROGRAM                *        
      *  BP133471    19112008        CCC    PASS DTE BALLOT &          *        
      *                                     NUM-ALLO-CAT INTO K761     *        
      *                                     FOR HOME-LINK              *        
      *  BP133920    08062010        ESD1   ADD BP13K816 FILE TO BE THE*        
      *                                     BASIS OF WHAT WILL BE THE  *        
      *                                     K761-NUM-RESULT TAG        *        
      *  BP13****    08112013        CCC    CATER FOR CDE-RESULT = 'K' *        
      *  BP135194    06012014        CCC    CATER FOR NUM-GEN3-ELIG,   *        
      *                                     CATER FOR NUM-FTS-APPLN    *        
      *  BP136634    04042017        CCC5   USE 'P' FOR SBF FLAT TYP 3P*        
      ******************************************************************        
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K761 ASSIGN TO BP13K761                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K761-KEY-FLD                               
                  FILE STATUS     IS WS-K761-STATUS.                            
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS WS-K813-STATUS.                            
                                                                                
           SELECT BP13LC30 ASSIGN TO BP13LC30.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K761                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       COPY BP13K761.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13LC30                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL  RECORD   IS  OMITTED                                         
            DATA   RECORD   IS  BP13LC30-REC                                    
            RECORDING MODE IS F.                                                
       01  BP13LC30-REC                  PIC X(132).                            
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CURR-DATE              PIC X(8).                              
           05  WS-NUM-UIN                PIC X(9)    VALUE SPACES.              
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F740-EOF               PIC X     VALUE 'N'.                   
           05  WS-K761-STATUS            PIC 9(2)  VALUE ZEROS.                 
           05  WS-K813-STATUS            PIC 9(2)  VALUE ZEROS.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-PAGE-CNT               PIC 9(4)  VALUE ZEROS.                 
           05  WS-LINE-CNT               PIC 9(5)  VALUE 56.                    
           05  WS-SNO                    PIC 9(5)  VALUE ZEROS.                 
           05  WS-REC-PRINTED            PIC 9(5)  VALUE ZEROS.                 
           05  WS-F740-READ              PIC 9(5)  VALUE ZEROS.                 
           05  WS-K761-WRITE             PIC 9(5)  VALUE ZEROS.                 
           05  WS-K761-REWRITE           PIC 9(5)  VALUE ZEROS.                 
                                                                                
      *---------------------------------------------------------------          
      *   LAY-OUT FOR BP13LC30 REPORT                                           
      *---------------------------------------------------------------          
       01  WS-PAGE-HDG1.                                                        
           05  FILLER                    PIC X(14) VALUE 'BP13LC30'.            
           05  FILLER                    PIC X(42) VALUE 'HDB3'.                
           05  FILLER                    PIC X(44)                              
               VALUE 'SYSTEM OF COMMITMENT'.                                    
           05  FILLER                    PIC X(7)  VALUE 'DATE : '.             
           05  LC30-PRINT-DATE           PIC X(10).                             
           05  FILLER                    PIC X(2)  VALUE SPACES.                
           05  FILLER                    PIC X(7)  VALUE 'PAGE : '.             
           05  LC30-PAGE                 PIC ZZZ9.                              
           05  FILLER                    PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-PAGE-HDG2.                                                        
           05  FILLER                    PIC X(54) VALUE SPACES.                
           05  FILLER                    PIC X(24)                              
               VALUE 'LIST OF RECORDS CREATED '.                                
           05  FILLER                    PIC X(54) VALUE SPACES.                
                                                                                
       01  WS-PAGE-HDG3.                                                        
           05  FILLER                    PIC X(50) VALUE SPACES.                
           05  FILLER                    PIC X(83)                              
               VALUE '(SALES UNIT - APPLN TEAM)    '.                           
                                                                                
       01  WS-PAGE-HDG4.                                                        
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '      S/NO     '.                                         
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '    REF NO     '.                                         
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '    NRIC NO    '.                                         
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '   EST/ZONE    '.                                         
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '   ROOM TYPE   '.                                         
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '    RESULT     '.                                         
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '  NT FT QUEUE  '.                                         
           05  FILLER                    PIC X(16) VALUE SPACES.                
                                                                                
       01  WS-LINE.                                                             
           05  FILLER                    PIC X(132)  VALUE ALL '-'.             
                                                                                
       01  WS-EMPTY-REPORT.                                                     
           05  FILLER                    PIC X(56) VALUE SPACES.                
           05  FILLER                    PIC X(20)                              
               VALUE '*** EMPTY REPORT ***'.                                    
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
       01  WS-DETAILS.                                                          
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  LC30-NUM-SERIAL           PIC ZZZZZ.                             
           05  FILLER                    PIC X(09) VALUE SPACES.                
           05  LC30-NUM-REFNO            PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  LC30-NUM-NRIC             PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  LC30-NUM-EST-ZONE         PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(12) VALUE SPACES.                
           05  LC30-NUM-ROOM             PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE SPACES.                
           05  LC30-NUM-RESULT           PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(13) VALUE SPACES.                
           05  LC30-NUM-NT-FT-QUEUE      PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(21) VALUE SPACES.                
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F740-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F740                                                 
                       BP13K813                                                 
                I-O    BP13K761                                                 
                OUTPUT BP13LC30.                                                
                                                                                
           IF WS-K761-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K761-STATUS ' WS-K761-STATUS              
              MOVE WS-K761-STATUS       TO RETURN-CODE.                         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                     
           STRING WS-CURR-DATE(7:2) '/'                                         
                  WS-CURR-DATE(5:2) '/'                                         
                  WS-CURR-DATE(1:4) DELIMITED BY SIZE                           
                  INTO LC30-PRINT-DATE.                                         
                                                                                
           PERFORM 8500-PRINT-HDG THRU 8500-EXIT.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F740.                                                      
      ******************************************************************        
           READ BP13F740                                                        
                AT END MOVE 'Y' TO WS-F740-EOF                                  
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS.                                                            
      ******************************************************************        
           MOVE    SPACES                 TO WS-NUM-UIN.                        
           MOVE    F740-NUM-NRIC1         TO WS-NUM-UIN.                        
           PERFORM 4000-READ-BP13K761     THRU 4000-EXIT.                       
                                                                                
           IF (F740-NUM-NRIC2 NOT = SPACES AND LOW-VALUES) AND                  
              (F740-NUM-NRIC2(1:1) NOT = '#')                                   
              MOVE    SPACES              TO WS-NUM-UIN                         
              MOVE    F740-NUM-NRIC2      TO WS-NUM-UIN                         
              PERFORM 4000-READ-BP13K761  THRU 4000-EXIT.                       
                                                                                
           IF (F740-NUM-NRIC3 NOT = SPACES AND LOW-VALUES) AND                  
              (F740-NUM-NRIC3(1:1) NOT = '#')                                   
              MOVE    SPACES              TO WS-NUM-UIN                         
              MOVE    F740-NUM-NRIC3      TO WS-NUM-UIN                         
              PERFORM 4000-READ-BP13K761  THRU 4000-EXIT.                       
                                                                                
           IF (F740-NUM-NRIC4 NOT = SPACES AND LOW-VALUES) AND                  
              (F740-NUM-NRIC4(1:1) NOT = '#')                                   
              MOVE    SPACES              TO WS-NUM-UIN                         
              MOVE    F740-NUM-NRIC4      TO WS-NUM-UIN                         
              PERFORM 4000-READ-BP13K761  THRU 4000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BP13F740  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K761.                                                      
      ******************************************************************        
           MOVE SPACES             TO K761-KEY-FLD.                             
           MOVE F740-NUM-REF       TO K761-NUM-REF-REGN.                        
           MOVE WS-NUM-UIN         TO K761-NUM-UIN.                             
                                                                                
           READ BP13K761.                                                       
                                                                                
           EVALUATE WS-K761-STATUS                                              
              WHEN 00                                                           
                   DISPLAY 'F740-NUM-REF = ' F740-NUM-REF                       
                   DISPLAY 'WS-NUM-UIN   = ' WS-NUM-UIN                         
                   PERFORM 5000-REWRITE-BP13K761 THRU 5000-EXIT                 
              WHEN 23                                                           
                   PERFORM 6000-WRITE-BP13K761 THRU 6000-EXIT                   
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K761, STATUS '                    
                   WS-K761-STATUS ' KEY ' F740-NUM-REF  WS-NUM-UIN              
                   INITIALIZE BP13K761-REC                                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-REWRITE-BP13K761.                                                   
      ******************************************************************        
           PERFORM 7000-MOVE-DETAILS     THRU 7000-EXIT.                        
                                                                                
           REWRITE BP13K761-REC.                                                
                                                                                
           EVALUATE WS-K761-STATUS                                              
           WHEN 00                                                              
              ADD   1            TO   WS-K761-REWRITE                           
              PERFORM 8000-PRINT-DETAILS THRU 8000-EXIT                         
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING BP13K761, STATUS ' WS-K761-STATUS        
                                                  ' KEY ' K761-KEY-FLD          
              MOVE    WS-K761-STATUS TO RETURN-CODE                             
              GO TO   9000-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-BP13K761.                                                     
      ******************************************************************        
           PERFORM 7000-MOVE-DETAILS     THRU 7000-EXIT.                        
                                                                                
           WRITE BP13K761-REC.                                                  
                                                                                
           EVALUATE WS-K761-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD   1            TO   WS-K761-WRITE                             
              PERFORM 8000-PRINT-DETAILS THRU 8000-EXIT                         
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING BP13K761, STATUS ' WS-K761-STATUS          
                                                ' KEY ' K761-KEY-FLD            
              MOVE    WS-K761-STATUS TO RETURN-CODE                             
              GO TO   9000-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       7000-MOVE-DETAILS.                                                       
      ******************************************************************        
           MOVE SPACES                   TO BP13K761-REC.                       
           INITIALIZE                       BP13K761-REC.                       
                                                                                
           MOVE F740-NUM-REF             TO K761-NUM-REF-REGN.                  
           MOVE F740-NUM-ALLO-CAT        TO K761-NUM-SALE-MODE.                 
           MOVE F740-DTE-BALLOT          TO K761-DTE-BALLOT.                    
           MOVE WS-NUM-UIN               TO K761-NUM-UIN.                       
           IF F740-CDE-FLAT-TYPE = '3P'                                         
              MOVE 'P'                      TO K761-NUM-ROOM                    
           ELSE                                                                 
              MOVE F740-CDE-FLAT-TYPE(1:1)  TO K761-NUM-ROOM                    
           END-IF.                                                              
                                                                                
           IF F740-CDE-ALLOC-SCH = 'FTS'                                        
              MOVE 'Y'                   TO K761-NUM-FTS-APPLN                  
           END-IF.                                                              
      ******************************************************************        
      * FOR MAY04 BTO, CDE-REQUEST-STATUS ARE ONLY 'S' OR 'R'                   
      * HARDCODE TO 'R' IF CDE-SUCC-NT-ZONE IS SPACES                           
      ******************************************************************        
                                                                                
           MOVE F740-CDE-NT1 TO K761-NUM-ESTATE-ZONE.                           
           PERFORM 7500-GET-NUM-RESULT THRU 7500-EXIT.                          
           MOVE F740-NUM-NT-FT-QUEUE     TO K761-NUM-NT-FT-QUEUE.               
                                                                                
           IF K761-NUM-RESULT = 'U'                                             
              MOVE SPACES TO K761-NUM-NT-FT-QUEUE                               
           END-IF.                                                              
                                                                                
           INSPECT BP13K761-REC REPLACING ALL LOW-VALUES BY SPACES.             
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7500-GET-NUM-RESULT.                                                     
      ******************************************************************        
           IF F740-CDE-NT1 = SPACES AND LOW-VALUES                              
              DISPLAY 'F740-CDE-NT1 HAS NO VALUE '                              
              MOVE    99             TO RETURN-CODE                             
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           ELSE                                                                 
              MOVE SPACES TO  K813-NUM-ZONE                                     
              MOVE SPACES TO  K813-NUM-FLAT-TYPE                                
              MOVE SPACES TO  K813-DTE-BALLOT                                   
              MOVE F740-CDE-NT1           TO  K813-NUM-ZONE                     
              MOVE F740-CDE-FLAT-TYPE     TO  K813-NUM-FLAT-TYPE                
              MOVE F740-DTE-BALLOT        TO  K813-DTE-BALLOT                   
                                                                                
              READ BP13K813                                                     
                                                                                
              EVALUATE WS-K813-STATUS                                           
                WHEN 00                                                         
                 IF F740-NUM-NT-FT-QUEUE   NOT = SPACES AND LOW-VALUES          
                                                                                
                   IF (K813-NUM-GEN3-TAG NOT = 'Y') OR                          
                       ((K813-NUM-GEN3-TAG = 'Y' AND                            
                         F740-NUM-GEN3 = 'Y'))                                  
                     IF F740-NUM-NT-FT-QUEUE    = K813-NUM-QUEUE-MID            
                        OR F740-NUM-NT-FT-QUEUE < K813-NUM-QUEUE-MID            
                        MOVE  'S'  TO  K761-NUM-RESULT                          
                        MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG               
                     ELSE                                                       
                        IF F740-NUM-NT-FT-QUEUE > K813-NUM-QUEUE-END            
                           MOVE  'W'  TO K761-NUM-RESULT                        
                           MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG            
                        ELSE                                                    
                           MOVE  'R'  TO K761-NUM-RESULT                        
                           MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG            
                        END-IF                                                  
                                                                                
                        IF F740-CDE-REQUEST-STATUS = 'K'                        
                           MOVE    'K'   TO K761-NUM-RESULT                     
                           MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG            
                        END-IF                                                  
                     END-IF                                                     
                   END-IF                                                       
                   IF  (K813-NUM-GEN3-TAG = 'Y' AND                             
                        F740-NUM-GEN3 NOT = 'Y')                                
                     IF F740-NUM-NT-FT-QUEUE =                                  
                                          K813-NUM-QUEUE-MID-MGPSGEN3           
                        OR F740-NUM-NT-FT-QUEUE <                               
                                          K813-NUM-QUEUE-MID-MGPSGEN3           
                        MOVE  'S'  TO  K761-NUM-RESULT                          
                        MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG               
                     ELSE                                                       
                        IF F740-NUM-NT-FT-QUEUE >                               
                                          K813-NUM-QUEUE-END-MGPSGEN3           
                           MOVE  'W'  TO K761-NUM-RESULT                        
                           MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG            
                        ELSE                                                    
                           MOVE  'R'  TO K761-NUM-RESULT                        
                           MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG            
                        END-IF                                                  
                                                                                
                        IF F740-CDE-REQUEST-STATUS = 'K'                        
                           MOVE    'K'   TO K761-NUM-RESULT                     
                           MOVE  F740-NUM-GEN3 TO K761-NUM-GEN3-ELIG            
                        END-IF                                                  
                     END-IF                                                     
                   END-IF                                                       
                  ELSE                                                          
                     IF F740-CDE-REQUEST-STATUS = 'U'                           
                        MOVE    'U'   TO K761-NUM-RESULT                        
                     ELSE                                                       
                        MOVE    'W'   TO K761-NUM-RESULT                        
                     END-IF                                                     
                  END-IF                                                        
                WHEN 23                                                         
                     DISPLAY 'RECORD NOT FOUND BP13K813, STATUS '               
                     WS-K813-STATUS ' KEY ' K813-NUM-ZONE WS-NUM-UIN            
                     MOVE    99             TO RETURN-CODE                      
                     PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                    
                                                                                
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K813, STATUS '                  
                     WS-K813-STATUS ' KEY ' F740-NUM-REF  WS-NUM-UIN            
                     MOVE    WS-K813-STATUS TO RETURN-CODE                      
                     PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                    
                                                                                
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       7500-EXIT.                                                               
            EXIT.                                                               
      ******************************************************************        
       8000-PRINT-DETAILS.                                                      
      ******************************************************************        
           IF WS-LINE-CNT > 55                                                  
              PERFORM 8500-PRINT-HDG THRU 8500-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE SPACES                        TO WS-DETAILS.                    
           ADD  1                             TO WS-SNO.                        
           MOVE WS-SNO                        TO LC30-NUM-SERIAL.               
           MOVE F740-NUM-REF                  TO LC30-NUM-REFNO.                
           MOVE WS-NUM-UIN                    TO LC30-NUM-NRIC.                 
           IF F740-CDE-FLAT-TYPE = '3P'                                         
              MOVE 'P'                      TO LC30-NUM-ROOM                    
           ELSE                                                                 
              MOVE F740-CDE-FLAT-TYPE(1:1)  TO LC30-NUM-ROOM                    
           END-IF.                                                              
                                                                                
           IF F740-CDE-NT1 NOT = SPACES AND LOW-VALUES                          
              MOVE F740-CDE-NT1               TO LC30-NUM-EST-ZONE              
              MOVE F740-CDE-REQUEST-STATUS    TO LC30-NUM-RESULT                
           ELSE                                                                 
              MOVE F740-CDE-NT1               TO LC30-NUM-EST-ZONE              
              MOVE F740-CDE-REQUEST-STATUS    TO LC30-NUM-RESULT                
           END-IF.                                                              
                                                                                
           MOVE F740-NUM-NT-FT-QUEUE          TO LC30-NUM-NT-FT-QUEUE.          
                                                                                
           MOVE SPACES TO BP13LC30-REC.                                         
           WRITE BP13LC30-REC FROM WS-DETAILS.                                  
                                                                                
           ADD 1                   TO WS-LINE-CNT                               
                                      WS-REC-PRINTED.                           
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       8500-PRINT-HDG.                                                          
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  PRINTS THE PAGE HEADER.                                       *        
      *----------------------------------------------------------------*        
           ADD  1                TO WS-PAGE-CNT.                                
           MOVE WS-PAGE-CNT      TO LC30-PAGE.                                  
           MOVE 1                TO WS-LINE-CNT.                                
                                                                                
           WRITE BP13LC30-REC    FROM WS-PAGE-HDG1 AFTER PAGE.                  
           WRITE BP13LC30-REC    FROM WS-PAGE-HDG2 AFTER 1.                     
           WRITE BP13LC30-REC    FROM WS-PAGE-HDG3 AFTER 1.                     
           WRITE BP13LC30-REC    FROM WS-PAGE-HDG4 AFTER 2.                     
           WRITE BP13LC30-REC    FROM WS-LINE      AFTER 1.                     
                                                                                
           MOVE SPACES TO BP13LC30-REC.                                         
           WRITE BP13LC30-REC.                                                  
                                                                                
           ADD 6                 TO WS-LINE-CNT.                                
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           IF WS-K761-WRITE   = ZEROES AND                                      
              WS-K761-REWRITE = ZEROES                                          
              MOVE  SPACES       TO   BP13LC30-REC                              
              WRITE BP13LC30-REC                                                
              MOVE  SPACES       TO   BP13LC30-REC                              
              WRITE BP13LC30-REC FROM WS-EMPTY-REPORT                           
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' *****  BP13CRS4  ***** '.                                  
           DISPLAY ' NO. OF REC READ FROM F740  = ' WS-F740-READ.               
           DISPLAY ' NO. OF REC WRITTEN         = ' WS-K761-WRITE.              
           DISPLAY ' NO. OF REC REWRITTEN       = ' WS-K761-REWRITE.            
           DISPLAY ' NO. OF REC PRINTED         = ' WS-REC-PRINTED.             
                                                                                
           CLOSE BP13F740                                                       
                 BP13K761                                                       
                 BP13K813                                                       
                 BP13LC30.                                                      
                                                                                
           IF WS-K761-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K761-STATUS ' WS-K761-STATUS              
              MOVE WS-K761-STATUS       TO RETURN-CODE.                         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
      * >>>>>>>>>>>>>>>>>>>>>>> END OF BP13AC30 <<<<<<<<<<<<<<<<<<<<<< *        
      *----------------------------------------------------------------*        
