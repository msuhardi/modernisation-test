       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF47.                                                 
       AUTHOR.        ZDD1.                                                     
       DATE-WRITTEN.  13/03/12.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    SIMILAR PGM  : BP13CF47                             *                
      *                                                        *                
      *    OBJECTIVE   :                                       *                
      *                  MATCHED HA1 BUT NO HDB-REF            *                
      *                                                        *                
      *    INPUT FILE  : BP13F730                              *                
      *    INPUT FILE  : BP13F558                              *                
      *                                                        *                
      *   OUTPUT FILE  : P13F730A                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  ---  ----     -------                          *                
      *BP134482 ZDD1 120312   NEW PGM                          *                
      * ====================================================== *                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558  ASSIGN       TO BP13F558.                           
           SELECT BP13F730  ASSIGN       TO BP13F730.                           
           SELECT P13F730A  ASSIGN       TO P13F730A.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
                                                                                
       COPY BP13F730.                                                           
                                                                                
       FD   P13F730A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
                                                                                
       01   P13F730A-REC.                                                       
            05 FILLER               PIC X(500).                                 
                                                                                
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY             PIC X(04) .                                 
           05 WS-SYS-MM             PIC X(02) .                                 
           05 WS-SYS-DD             PIC X(02) .                                 
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DAY           PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-MONTH         PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-YEAR          PIC X(04) .                                 
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-I                       PIC S9(4) COMP VALUE ZEROES.           
           05 WS-F730-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F730-MATCH              PIC 9(8)  VALUE ZEROES.                
           05 WS-F730-UPDATE             PIC 9(8)  VALUE ZEROES.                
           05 WS-F730-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F730A-WRITE             PIC 9(8)  VALUE ZEROES.                
           05 WS-F558-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F558-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-WITH-HDBREF             PIC 9(8)  VALUE ZEROES.                
           05 WS-LESSEE                  PIC X(1)  VALUE SPACES.                
           05 WS-DEBAR                   PIC X(1)  VALUE SPACES.                
           05 WS-F730-REC                PIC X(500) VALUE SPACES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05 EOF-F558-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F558                     VALUE 'YES'.                      
           05 EOF-F730-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F730                     VALUE 'YES'.                      
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           PERFORM 200-READ-BP13F730   THRU 200-EXIT.                           
           PERFORM 300-READ-BP13F558   THRU 300-EXIT.                           
           PERFORM 400-PROCESS-INDATA  THRU 400-EXIT                            
             UNTIL F730-NUM-REGN = HIGH-VALUE                                   
               OR  F558-NUM-REGN = HIGH-VALUE.                                  
           PERFORM 900-CLOSE-FILES     THRU 900-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  THIS PARA OPENS ALL THE FILES AND INITIALIZES THE      *               
      *  COUNTERS.                                              *               
      ***********************************************************               
                                                                                
      *------------------                                                       
       100-OPEN-FILES.                                                          
      *------------------                                                       
                                                                                
           OPEN INPUT  BP13F558                                                 
                       BP13F730                                                 
                OUTPUT P13F730A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYS-DATE.                   
           MOVE   WS-SYS-DD       TO WS-EDIT-DAY                                
           MOVE   WS-SYS-MM       TO WS-EDIT-MONTH                              
           MOVE   WS-SYS-YY       TO WS-EDIT-YEAR.                              
                                                                                
                                                                                
           MOVE 'NO '     TO EOF-F558-IND.                                      
           MOVE 'NO '     TO EOF-F730-IND.                                      
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  THIS PARA READS THE INPUT FILE AND GETS THE NAME AND   *               
      *  MAILING ADDRESS AND PRINTS LETTERS AND LISTING         *               
      ***********************************************************               
                                                                                
      *-----------------*                                                       
       200-READ-BP13F730.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F730  AT END                                                
                MOVE 'YES'       TO EOF-F730-IND                                
                MOVE HIGH-VALUES TO F730-NUM-REGN                               
                GO TO 200-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F730-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BP13F558.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F558  AT END                                                
                MOVE 'YES'       TO EOF-F558-IND                                
                MOVE HIGH-VALUES TO F558-NUM-REGN                               
                GO TO 300-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F558-READ.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       400-PROCESS-INDATA.                                                      
      *------------------*                                                      
                                                                                
           IF F730-NUM-NRIC1    =  F558-NUM-NRIC                                
               DISPLAY 'REG=' F730-NUM-REGN                                     
                       ' IC1 ' F730-NUM-NRIC1                                   
               ADD 1 TO  WS-F730-MATCH                                          
               ADD 1 TO  WS-F730-UPDATE                                         
               PERFORM 490-WRITE-P13F730A   THRU 490-EXIT                       
                   UNTIL F730-NUM-NRIC1  NOT  =  F558-NUM-NRIC OR               
                   F558-NUM-REGN = HIGH-VALUES                                  
               PERFORM 200-READ-BP13F730    THRU 200-EXIT                       
           ELSE                                                                 
              IF F730-NUM-NRIC1 <  F558-NUM-NRIC                                
                 ADD  1         TO  WS-F730-NOMATCH                             
                 PERFORM 200-READ-BP13F730   THRU 200-EXIT                      
              ELSE                                                              
                 ADD  1         TO  WS-F558-NOMATCH                             
                 PERFORM 300-READ-BP13F558 THRU 300-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       490-WRITE-P13F730A.                                                      
                                                                                
           IF F558-NUM-HDB-REF-SMS = SPACES OR LOW-VALUES                       
              WRITE  P13F730A-REC FROM BP13F730-REC                             
             ADD  1  TO WS-F730A-WRITE                                          
           ELSE                                                                 
             ADD 1 TO WS-WITH-HDBREF                                            
           END-IF.                                                              
                                                                                
           PERFORM 300-READ-BP13F558    THRU 300-EXIT.                          
                                                                                
       490-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------                                                         
       900-CLOSE-FILES.                                                         
      *----------------                                                         
                                                                                
           CLOSE BP13F730                                                       
                 P13F730A                                                       
                 BP13F558.                                                      
                                                                                
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CF47                 '.                    
           DISPLAY '                                      '.                    
           DISPLAY 'DATE : ' WS-EDIT-DATE.                                      
           DISPLAY '                                      '.                    
                                                                                
           DISPLAY '                                      '.                    
           DISPLAY ' *BP13C183                                  '.              
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS READ FROM F730  = ' WS-F730-READ.            
           DISPLAY 'NO. OF RECORDS READ FROM F558  = ' WS-F558-READ.            
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS WRITEN TO F520  = ' WS-F730A-WRITE.          
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS W/OUT HDB REF   = ' WS-F730-MATCH.           
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS WITH HDB REF    = ' WS-WITH-HDBREF.          
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF BP13F730 NOT FD IN F558 = ' WS-F730-NOMATCH.         
           DISPLAY 'NO. OF RECORDS NOT FOUND F730  = ' WS-F558-NOMATCH.         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
