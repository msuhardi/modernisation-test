       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C06A.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  19/10/2010.                                               
      * ========================================================== *            
      *             FLAT INVENTORY SYSTEM (BM06)                   *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES:                                                *            
      *        1. TO CREATE A REPORT FOR SC/SPR PREMIUM            *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP134054  ESA1  20101019  NEW PGM                          *            
      * BP134043  ESA1  20101203  TO ADD CHECKING FOR HH FOR SCP   *            
      * BP137132  KR13  20180730  CORRECTED LINE CNTR FOR PAGE BREAK            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT P13L06AA  ASSIGN        TO P13L06AA.                          
           SELECT P13L06AB  ASSIGN        TO P13L06AB.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD  P13L06AA                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  P13L06AA-REC                   PIC X(132).                           
                                                                                
       FD  P13L06AB                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  P13L06AB-REC                   PIC X(132).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-FILE-STATUS.                                                       
          05 BM06K110-STATUS       PIC 9(02) VALUE ZEROES.                      
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ          PIC 9(08) VALUE ZEROES.                      
          05 WS-K110-READ          PIC 9(08) VALUE ZEROES.                      
          05 WS-K110-UPDATE        PIC 9(08) VALUE ZEROES.                      
          05 WS-LINE-CTR1          PIC 9(02) VALUE ZEROES.                      
          05 WS-LINE-CTR2          PIC 9(02) VALUE ZEROES.                      
          05 WS-PAGE-CTR1          PIC 9(04) VALUE ZEROES.                      
          05 WS-PAGE-CTR2          PIC 9(04) VALUE ZEROES.                      
          05 WS-SNO-CTR1           PIC 9(04) VALUE ZEROES.                      
          05 WS-SNO-CTR2           PIC 9(04) VALUE ZEROES.                      
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-F800-EOF           PIC X(01) VALUE 'N'.                         
          05 WS-K110-FND           PIC X(01) VALUE 'N'.                         
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-DISP-DATE          PIC X(10) VALUE SPACES.                      
          05 WS-DATE               PIC X(08) VALUE SPACES.                      
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 L06A-HEADER-1.                                                        
          05 L06A-RPT-NAME       PIC X(08)  VALUE SPACES.                       
          05 FILLER              PIC X(05)  VALUE SPACES .                      
          05 FILLER              PIC X(08)  VALUE 'HDBCAT3'.                    
          05 FILLER              PIC X(22)  VALUE SPACES.                       
          05 FILLER              PIC X(40)  VALUE                               
             'S Y S T E M   O F   C O M M I T M E N T'.                         
          05 FILLER              PIC X(28)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'DATE :'.                     
          05 L06A-DATE           PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L06A-HEADER-2.                                                        
          10 FILLER              PIC X(35)  VALUE SPACES .                      
          10 FILLER              PIC X(35)  VALUE                               
             'DEBARMENT REPORT FOR SC/SPR OPTION'.                              
          10 FILLER              PIC X(21)  VALUE                               
             '- SC/SPR PREMIUM'.                                                
          10 FILLER              PIC X(20)  VALUE SPACES.                       
          10 FILLER              PIC X(07)  VALUE 'PAGE : '.                    
          10 L06A-PAGE           PIC ZZZ9 .                                     
                                                                                
       01 L06A-HEADER-2A.                                                       
          10 FILLER              PIC X(40)  VALUE SPACES .                      
          10 FILLER              PIC X(25)  VALUE                               
             'HOUSEHOLD CITIZENSHIP =  '.                                       
          10 L06A-HH             PIC X(03)  VALUE SPACES.                       
          10 L06A-DESC           PIC X(50)  VALUE SPACES.                       
                                                                                
       01 L06A-HEADER-3.                                                        
          05 FILLER              PIC X(55)  VALUE                               
             'S/NO     REGN NO.       SCH-ACC           DTE-ACCEPT   '.         
          05 FILLER              PIC X(55)  VALUE                               
             '      H/H CITIZENSHIP         HH       SC-SPR PREMIUM  '.         
          05 FILLER              PIC X(20)  VALUE                               
             'CURR-SELLING PRICE'.                                              
                                                                                
       01 L06A-DETAIL.                                                          
          05 L06A-SNO            PIC ZZZZ.                                      
          05 FILLER              PIC X(05)  VALUE SPACES.                       
          05 L06A-NUM-REGN       PIC X(08)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE SPACES.                       
          05 L06A-SCH-ACC        PIC X(11)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE SPACES.                       
          05 L06A-DTE-ACCEPT     PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(15)  VALUE SPACES.                       
          05 L06A-HH-CITZ        PIC X(03)  VALUE SPACES.                       
          05 FILLER              PIC X(15)  VALUE SPACES.                       
          05 L06A-NUM-HH         PIC X(1).                                      
          05 FILLER              PIC X(12)  VALUE SPACES.                       
          05 L06A-PREM-AMT       PIC ZZ,ZZ9.99.                                 
          05 FILLER              PIC X(07)  VALUE SPACES.                       
          05 L06A-CURR-SELL      PIC ZZ,ZZZ,ZZ9.99.                             
                                                                                
       01 L06A-LINE             PIC X(132)    VALUE ALL '-'.                    
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL WS-F800-EOF  =  'Y'.                                        
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                OUTPUT P13L06AA                                                 
                       P13L06AB.                                                
                                                                                
           IF BM06K110-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE     BM06K110-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2), '/',                                            
                  WS-DATE(5:2), '/',                                            
                  WS-DATE(1:4)  DELIMITED BY SIZE                               
                  INTO WS-DISP-DATE.                                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
           READ BP13F800 AT END                                                 
                MOVE 'Y'         TO WS-F800-EOF                                 
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F800-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                      TO WS-K110-FND.                        
           MOVE F800-NUM-SCH-ACC         TO K110-KEY-FLD.                       
                                                                                
           PERFORM 3100-READ-BM06K110    THRU 3100-EXIT.                        
                                                                                
           IF K110-AMT-SC-SPR-PREMIUM NOT NUMERIC                               
              MOVE ZEROES  TO K110-AMT-SC-SPR-PREMIUM                           
           END-IF.                                                              
                                                                                
           IF WS-K110-FND = 'Y'                                                 
              IF F800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH'                         
                 IF K110-AMT-SC-SPR-PREMIUM > ZEROES                            
                    PERFORM 3200-PRINT-REPORT-SCH THRU 3200-EXIT                
                 END-IF                                                         
              ELSE                                                              
                 IF F800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCP'                      
                    IF F800-NUM-HOUSEHOLD = 'G'                                 
                       IF K110-AMT-SC-SPR-PREMIUM = 10000                       
                          PERFORM 3500-PRINT-REPORT-SCP THRU 3500-EXIT          
                       END-IF                                                   
                    ELSE                                                        
                       IF K110-AMT-SC-SPR-PREMIUM <= ZEROES                     
                          PERFORM 3500-PRINT-REPORT-SCP THRU 3500-EXIT          
                       END-IF                                                   
                    END-IF                                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
                MOVE 'Y'           TO WS-K110-FND                               
                ADD              1 TO WS-K110-READ                              
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BM06K110'                             
                      ', K110-SCH-ACC-NO : ' K110-SCH-ACC-NO                    
             WHEN OTHER                                                         
                DISPLAY 'BM06K110 READ ERROR,STATUS : ' BM06K110-STATUS         
                      ', K110 KEY FLD : ' K110-KEY-FLD                          
                MOVE BM06K110-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-PRINT-REPORT-SCH.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3300-CHECK-LINE-CTR-SCH      THRU 3300-EXIT.                 
                                                                                
           MOVE SPACES    TO L06A-DETAIL.                                       
           WRITE P13L06AA-REC  FROM L06A-DETAIL.                                
           ADD 1          TO WS-LINE-CTR1.                                      
                                                                                
           PERFORM 3300-CHECK-LINE-CTR-SCH  THRU 3300-EXIT.                     
                                                                                
           MOVE SPACES                  TO L06A-DETAIL.                         
           ADD 1                        TO WS-SNO-CTR1.                         
           MOVE WS-SNO-CTR1             TO L06A-SNO.                            
           MOVE F800-NUM-REGN           TO L06A-NUM-REGN.                       
           MOVE F800-NUM-SCH-ACC        TO L06A-SCH-ACC.                        
           MOVE F800-CDE-CITIZENSHIP-HOUSEHOLD                                  
                                        TO L06A-HH-CITZ.                        
           MOVE F800-NUM-HOUSEHOLD      TO L06A-NUM-HH.                         
           STRING K110-DTE-ACCEPTANCE(7:2) '/'                                  
                  K110-DTE-ACCEPTANCE(5:2) '/'                                  
                  K110-DTE-ACCEPTANCE(1:4)                                      
                  DELIMITED BY SIZE INTO L06A-DTE-ACCEPT.                       
           MOVE K110-AMT-SC-SPR-PREMIUM TO L06A-PREM-AMT.                       
           MOVE K110-CURR-SELLING-PRICE TO L06A-CURR-SELL.                      
           WRITE P13L06AA-REC  FROM L06A-DETAIL.                                
           ADD 1          TO WS-LINE-CTR1.                                      
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-CHECK-LINE-CTR-SCH.                                                 
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CTR1 > 55  OR                                             
              WS-LINE-CTR1 = 0                                                  
              PERFORM 3400-PRINT-PAGE-BREAK-SCH  THRU 3400-EXIT                 
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-PRINT-PAGE-BREAK-SCH.                                               
      *-------------------------------------------------------------            
                                                                                
           ADD   1   TO WS-PAGE-CTR1.                                           
           MOVE WS-PAGE-CTR1   TO L06A-PAGE.                                    
           MOVE WS-DISP-DATE   TO L06A-DATE.                                    
           MOVE 'P13L06AA'     TO L06A-RPT-NAME.                                
           MOVE F800-CDE-CITIZENSHIP-HOUSEHOLD                                  
                               TO L06A-HH.                                      
           MOVE ' (WITH PREMIUM)   '    TO  L06A-DESC.                          
                                                                                
           WRITE P13L06AA-REC  FROM L06A-HEADER-1 AFTER PAGE.                   
           WRITE P13L06AA-REC  FROM L06A-HEADER-2.                              
           WRITE P13L06AA-REC  FROM L06A-HEADER-2A.                             
           WRITE P13L06AA-REC  FROM L06A-LINE    AFTER 2.                       
           WRITE P13L06AA-REC  FROM L06A-HEADER-3.                              
           WRITE P13L06AA-REC  FROM L06A-LINE.                                  
           MOVE 7         TO   WS-LINE-CTR1.                                    
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-PRINT-REPORT-SCP.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3600-CHECK-LINE-CTR-SCP      THRU 3600-EXIT.                 
                                                                                
           MOVE SPACES    TO L06A-DETAIL.                                       
           WRITE P13L06AB-REC  FROM L06A-DETAIL.                                
           ADD 1          TO WS-LINE-CTR2.                                      
                                                                                
           PERFORM 3600-CHECK-LINE-CTR-SCP    THRU 3600-EXIT.                   
                                                                                
           MOVE SPACES                  TO L06A-DETAIL.                         
           ADD 1                        TO WS-SNO-CTR2.                         
           MOVE WS-SNO-CTR2             TO L06A-SNO.                            
           MOVE F800-NUM-REGN           TO L06A-NUM-REGN.                       
           MOVE F800-NUM-SCH-ACC        TO L06A-SCH-ACC.                        
           MOVE F800-CDE-CITIZENSHIP-HOUSEHOLD                                  
                                        TO L06A-HH-CITZ.                        
           MOVE F800-NUM-HOUSEHOLD      TO L06A-NUM-HH.                         
           STRING K110-DTE-ACCEPTANCE(7:2) '/'                                  
                  K110-DTE-ACCEPTANCE(5:2) '/'                                  
                  K110-DTE-ACCEPTANCE(1:4)                                      
                  DELIMITED BY SIZE INTO L06A-DTE-ACCEPT.                       
           MOVE K110-AMT-SC-SPR-PREMIUM TO L06A-PREM-AMT.                       
           MOVE K110-CURR-SELLING-PRICE TO L06A-CURR-SELL.                      
           WRITE P13L06AB-REC  FROM L06A-DETAIL.                                
           ADD 1          TO WS-LINE-CTR2.                                      
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3600-CHECK-LINE-CTR-SCP.                                                 
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CTR2 > 55  OR                                             
              WS-LINE-CTR2 = 0                                                  
              PERFORM 3700-PRINT-PAGE-BREAK-SCP   THRU 3700-EXIT                
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3700-PRINT-PAGE-BREAK-SCP.                                               
      *-------------------------------------------------------------            
                                                                                
           ADD   1   TO WS-PAGE-CTR2.                                           
           MOVE WS-PAGE-CTR2   TO L06A-PAGE.                                    
           MOVE WS-DISP-DATE   TO L06A-DATE.                                    
           MOVE 'P13L06AB'     TO L06A-RPT-NAME.                                
           MOVE F800-CDE-CITIZENSHIP-HOUSEHOLD                                  
                               TO L06A-HH                                       
           MOVE ' (WITHOUT PREMIUM OR UPGRADER WITH PREMIUM)'                   
                               TO  L06A-DESC.                                   
           WRITE P13L06AB-REC  FROM L06A-HEADER-1 AFTER PAGE.                   
           WRITE P13L06AB-REC  FROM L06A-HEADER-2.                              
           WRITE P13L06AB-REC  FROM L06A-HEADER-2A.                             
           WRITE P13L06AB-REC  FROM L06A-LINE    AFTER 2.                       
           WRITE P13L06AB-REC  FROM L06A-HEADER-3.                              
           WRITE P13L06AB-REC  FROM L06A-LINE.                                  
           MOVE 7         TO   WS-LINE-CTR2.                                    
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' BP13C06A CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F800 : ' WS-F800-READ.            
           DISPLAY 'NO OF RECS READ  FROM BM06K110 : ' WS-K110-READ.            
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 P13L06AA                                                       
                 P13L06AB.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
