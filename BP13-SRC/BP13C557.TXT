       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C557.                                                 
       AUTHOR.        SELVY.                                                    
      *DATE-WRITTEN.  20/01/99.                                                 
                                                                                
      *****************************************************************         
      *                SYSTEM NAME (PROJECT CODE)                     *         
      *===============================================================*         
      *  OBJECTIVES : 1. TO UPDATE CDE-ELIG-TAG AS 'SS',TO RESERVE    *         
      *                  LIST FILE AFTER HOUSEHOLD AND 5-YRCHECK      *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                ACCESS      REC                                *         
      * DD NAME  I/O/E  MODE       LEN  KEY          REMARKS          *         
      * -------  ----- -------     ---  ---          --------         *         
      * BP13K700 I/O    RANDOM     300  K700-KEY-FLD                  *         
      * BP13K705 I/O    RANDOM     300  K705-KEY-FLD                  *         
      * BP13F500 I      SEQUENTIAL 300                                *         
      * P13L557A O      SEQUENTIAL 133                                *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ #  DATE      AUTHOR    REMARKS                       *         
      * ----------  ----      ------    -------                       *         
      * BP130624    20/01/99  SRA - NEW PROGRAM                       *         
      *             15/03/99  SRA - UPDATE BP13K705                   *         
      * BP130672    07/05/99  LSB - UPDATE 5YR-OCCUPN TO K700         *         
      * BP130630    19/02/99  NOR       EXPAND F500 RECL FR 300 TO 500*         
      * BP130652    23/02/99  SB        EXPAND F700,F705 RECL TO 500. *         
      * BP130837    09/05/00  MCC - DO NOT MOVE 0 TO F500-NUM-5YR-OCCP*         
      *                       UPDATE ELIG-TAG FROM BP13F500. INCLUDE  *         
      *                       ELIG-TAG COLUMN IN BP13L557 REPORT      *         
      * BP132603    28/10/04  JF4 - CHANGE 10-YR TIMEBAR TO 5-YR      *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K700 ASSIGN TO BP13K700                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K700-KEY-FLD                           
                           FILE STATUS IS WS-K700-STATUS.                       
                                                                                
           SELECT BP13K705 ASSIGN TO BP13K705                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K705-KEY-FLD                           
                           FILE STATUS IS WS-K705-STATUS.                       
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
           SELECT P13L557A ASSIGN TO P13L557A.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K700     RECORD CONTAINS 500  CHARACTERS.                        
       COPY BP13K700.                                                           
                                                                                
       FD  BP13K705     RECORD CONTAINS 500  CHARACTERS.                        
       COPY BP13K705.                                                           
                                                                                
       FD  BP13F500     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 500 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F500.                                                           
                                                                                
       FD  P13L557A     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 133 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  P13L557A-REC            PIC X(133).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  WS-K700-STATUS      PIC 9(2)      VALUE ZEROS.                   
           05  WS-K705-STATUS      PIC 9(2)      VALUE ZEROS.                   
           05  WS-F500-EOF         PIC X         VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F500-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K700-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K700-CNT-REWRT   PIC 9(8)      VALUE ZEROS.                   
           05  WS-K700-CNT-RECNFND PIC 9(8)      VALUE ZEROS.                   
           05  WS-K705-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K705-CNT-REWRT   PIC 9(8)      VALUE ZEROS.                   
           05  WS-K705-CNT-RECNFND PIC 9(8)      VALUE ZEROS.                   
           05  WS-L557A-CNT-WRT    PIC 9(8)      VALUE ZEROS.                   
           05  WS-L557A-CNT-LINE   PIC 9(8)      VALUE 99.                      
           05  WS-L557A-CNT-PAGE   PIC 9(8)      VALUE ZEROS.                   
                                                                                
       01  WS-DATE                 PIC X(08)     VALUE SPACES.                  
       01  WS-L557A-SNO            PIC 9(08)     VALUE ZEROES.                  
                                                                                
       01  L557A-HD-LINE1.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L557'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                     'SYSTEM OF COMMITMENT'.                                    
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'DATE :'.                
           05  L557A-DATE          PIC X(10).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'PAGE NO :'.             
           05  L557A-PAGE-NO       PIC Z(03)9.                                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L557A-HEADER1.                                                       
           05  FILLER              PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(56)     VALUE                          
               'UPDATE INELIGIBLE FOR 5-YR=N OR FOUND IN RFS '.                 
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'USER :'.                
           05  FILLER              PIC X(16)     VALUE                          
                      'APPLICATION TEAM'.                                       
           05  FILLER              PIC X(04)     VALUE SPACES.                  
                                                                                
       01  L557A-HEADER3.                                                       
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE '  S/NO  '.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGN-NO '.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE '  NRIC1  '.             
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE '5YR-OCCUP'.             
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'MTH-RESIDE'.            
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'DEBARMENT'.             
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'ELIG-TAG'.              
           05  FILLER              PIC X(39)     VALUE SPACES.                  
                                                                                
       01  L557A-HEADER4.                                                       
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(39)     VALUE SPACES.                  
                                                                                
       01  L557A-REC.                                                           
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L557A-SNO           PIC Z(07)9.                                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L557A-REGN-NO       PIC X(08).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L557A-NRIC1         PIC X(09).                                   
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  L557A-5YR-OCCUP     PIC X(01).                                   
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  L557A-MTH-RESIDE    PIC X(03).                                   
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  L557A-DEBAR-CODE    PIC X(02).                                   
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  L557A-ELIG-TAG      PIC X(02).                                   
           05  FILLER              PIC X(42)     VALUE SPACES.                  
                                                                                
       01  L557A-EMPTY-LINE        PIC X(132)    VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 1500-F500-READ       THRU 1500-EXIT.                         
           PERFORM 2000-MAIN-ROUTINE    THRU 2000-EXIT                          
                   UNTIL WS-F500-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN I-O    BP13K700                                                 
                I-O    BP13K705                                                 
                INPUT  BP13F500                                                 
                OUTPUT P13L557A.                                                
                                                                                
           IF WS-K700-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPEN ERROR,BP13K700-STATUS ' WS-K700-STATUS              
              MOVE WS-K700-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF WS-K705-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPEN ERROR,BP13K705-STATUS ' WS-K705-STATUS              
              MOVE WS-K705-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L557A-DATE.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1500-F500-READ.                                                          
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * READING BP13F500                                   *                    
      *----------------------------------------------------*                    
                                                                                
           READ BP13F500 AT END                                                 
                        MOVE 'Y' TO WS-F500-EOF                                 
                        GO TO 1500-EXIT.                                        
                                                                                
           ADD 1 TO WS-F500-CNT-READ.                                           
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * MAIN ROUTINE                                       *                    
      *----------------------------------------------------*                    
                                                                                
           PERFORM 2100-BP13K700-READ THRU 2100-EXIT.                           
           PERFORM 2200-BP13K705-READ THRU 2200-EXIT.                           
           PERFORM 1500-F500-READ     THRU 1500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2100-BP13K700-READ.                                                      
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * READING BP13K700                                   *                    
      *----------------------------------------------------*                    
                                                                                
           MOVE F500-NUM-RANDOM TO K700-NUM-RANDOM.                             
           READ BP13K700.                                                       
           EVALUATE WS-K700-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO WS-K700-CNT-READ                                   
                    PERFORM 2300-RWRTK700-PARA THRU 2300-EXIT                   
                    PERFORM 3000-WRITE-REPORT THRU 3000-EXIT                    
               WHEN 23                                                          
                    ADD 1 TO WS-K700-CNT-RECNFND                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800     = '                     
                                               WS-K700-STATUS                   
                    DISPLAY '      K700-KEY-FLD         = '                     
                                               K700-NUM-RANDOM                  
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
               END-EVALUATE.                                                    
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2200-BP13K705-READ.                                                      
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * READING BP13K705                                   *                    
      *----------------------------------------------------*                    
                                                                                
           MOVE BP13F500-REC(1:49) TO  BP13K705-REC(1:49).                      
           READ BP13K705.                                                       
           EVALUATE WS-K705-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO WS-K705-CNT-READ                                   
                    PERFORM 2400-RWRTK705-PARA THRU 2400-EXIT                   
               WHEN 23                                                          
                    ADD 1 TO WS-K705-CNT-RECNFND                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800     = '                     
                                               WS-K700-STATUS                   
                    DISPLAY '      K700-KEY-FLD         = '                     
                                               K700-NUM-RANDOM                  
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
               END-EVALUATE.                                                    
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2300-RWRTK700-PARA.                                                      
      *****************************************************************         
                                                                                
      *---------------------------------------------------*                     
      * REWRITING K700-CODE-ELIG-TAG.                     *                     
      *---------------------------------------------------*                     
                                                                                
           MOVE F500-NUM-5YR-OCCUPN TO K700-NUM-5YR-OCCUPN.                     
           MOVE F500-NUM-MTH-RESIDE TO K700-NUM-MTH-RESIDE.                     
                                                                                
           IF F500-NUM-5YR-OCCUPN = 'N'                                         
              MOVE 'SS'                TO K700-CDE-ELIG-TAG                     
           ELSE                                                                 
              MOVE F500-CDE-ELIG-TAG   TO K700-CDE-ELIG-TAG                     
           END-IF.                                                              
                                                                                
           REWRITE BP13K700-REC.                                                
           EVALUATE WS-K700-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO WS-K700-CNT-REWRT                                  
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WHILE REWRITING = ' WS-K700-STATUS           
                    DISPLAY 'K700-NUM-RANDOM       = ' K700-NUM-RANDOM          
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2400-RWRTK705-PARA.                                                      
      *****************************************************************         
                                                                                
      *---------------------------------------------------*                     
      * REWRITING K705-CODE-ELIG-TAG.                     *                     
      *---------------------------------------------------*                     
                                                                                
           MOVE F500-NUM-5YR-OCCUPN TO K705-NUM-5YR-OCCUPN.                     
           MOVE F500-NUM-MTH-RESIDE TO K705-NUM-MTH-RESIDE.                     
                                                                                
           IF F500-NUM-5YR-OCCUPN = 'N'                                         
              MOVE 'SS'                TO K705-CDE-ELIG-TAG                     
           ELSE                                                                 
              MOVE F500-CDE-ELIG-TAG   TO K705-CDE-ELIG-TAG                     
           END-IF.                                                              
                                                                                
           REWRITE BP13K705-REC.                                                
           EVALUATE WS-K705-STATUS                                              
              WHEN 00                                                           
                   ADD 1 TO WS-K705-CNT-REWRT                                   
              WHEN OTHER                                                        
                   DISPLAY 'ERROR WHILE REWRITING = ' WS-K705-STATUS            
                   DISPLAY 'K705-KEY-FLD          = ' BP13K705-REC(1:49)        
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3000-WRITE-REPORT.                                                       
      *****************************************************************         
                                                                                
           IF WS-L557A-CNT-LINE > 55                                            
              PERFORM 3500-WRITE-HEADER THRU 3500-EXIT                          
           END-IF.                                                              
           ADD 1                         TO WS-L557A-CNT-LINE.                  
           ADD 1                         TO WS-L557A-SNO.                       
           MOVE WS-L557A-SNO             TO L557A-SNO.                          
           MOVE F500-NUM-REGN            TO L557A-REGN-NO.                      
           MOVE F500-NUM-NRIC1           TO L557A-NRIC1.                        
           MOVE F500-NUM-5YR-OCCUPN      TO L557A-5YR-OCCUP.                    
           MOVE F500-NUM-MTH-RESIDE      TO L557A-MTH-RESIDE.                   
           MOVE F500-CDE-EXIST-FLAT-TYPE TO L557A-DEBAR-CODE.                   
           IF F500-NUM-5YR-OCCUPN = 'N'                                         
              MOVE 'SS'                  TO L557A-ELIG-TAG                      
           ELSE                                                                 
              MOVE F500-CDE-ELIG-TAG     TO L557A-ELIG-TAG                      
           END-IF.                                                              
           WRITE P13L557A-REC            FROM L557A-REC.                        
           ADD 1                         TO WS-L557A-CNT-WRT.                   
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       3500-WRITE-HEADER.                                                       
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * WRITES HEADER IN THE P13L557A                      *                    
      *----------------------------------------------------*                    
                                                                                
           ADD  1                TO WS-L557A-CNT-PAGE.                          
           MOVE WS-L557A-CNT-PAGE TO L557A-PAGE-NO.                             
           WRITE P13L557A-REC FROM L557A-HD-LINE1 AFTER PAGE.                   
           WRITE P13L557A-REC FROM L557A-EMPTY-LINE.                            
           WRITE P13L557A-REC FROM L557A-HEADER1.                               
           WRITE P13L557A-REC FROM L557A-EMPTY-LINE.                            
           WRITE P13L557A-REC FROM L557A-HEADER3.                               
           WRITE P13L557A-REC FROM L557A-HEADER4.                               
           WRITE P13L557A-REC FROM L557A-EMPTY-LINE.                            
           MOVE 8                TO WS-L557A-CNT-LINE.                          
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           DISPLAY 'TOTAL F500 REC READ     = ' WS-F500-CNT-READ.               
           DISPLAY 'TOTAL K700 REC READ     = ' WS-K700-CNT-READ.               
           DISPLAY 'TOTAL K700 REC REWRT    = ' WS-K700-CNT-REWRT.              
           DISPLAY 'TOTAL K700 REC RECNFND  = ' WS-K700-CNT-RECNFND.            
           DISPLAY 'TOTAL K705 REC READ     = ' WS-K705-CNT-READ.               
           DISPLAY 'TOTAL K705 REC REWRT    = ' WS-K705-CNT-REWRT.              
           DISPLAY 'TOTAL K705 REC RECNFND  = ' WS-K705-CNT-RECNFND.            
           DISPLAY 'TOTAL L557A REC WRITTEN  = ' WS-L557A-CNT-WRT.              
                                                                                
           CLOSE      BP13K700                                                  
                      BP13K705                                                  
                      BP13F500                                                  
                      P13L557A.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================  END OF PROGRAM  ====================*              
