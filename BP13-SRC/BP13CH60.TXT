       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH60.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  16/09/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    :  TO CREATE ONLINE FILE FOR SENDING SMS FOR  *            
      *                 PCD (BP13KH55 -> BP13KC50)                 *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FH55                               *            
      *                 2.) BP13K800                               *            
      *                 3.) BM06K110                               *            
      * I-O    FILES :  1.) BP13KH55                               *            
      *                 2.) BP13KC50                               *            
      * OUTPUT FILES :  1.) P13LH60A                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136431 KSJ3 16/09/2016 NEW PROGRAM                       *            
      * BP137452 ZS13 18/02/2018 CATER FOR M KH55-NUM-PCD-FORM     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FH55  ASSIGN       TO BP13FH55.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13KH55  ASSIGN       TO BP13KH55                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KH55-KEY-FLD                        
                            FILE STATUS  IS WS-KH55-STATUS.                     
                                                                                
           SELECT BP13KC50  ASSIGN       TO BP13KC50                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KC50-KEY-FLD                        
                            FILE STATUS  IS WS-KC50-STATUS.                     
                                                                                
           SELECT P13LH60A  ASSIGN       TO P13LH60A.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FH55                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS  500 CHARACTERS.                                     
           COPY BP13FH55.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
           COPY BM06K110.                                                       
                                                                                
       FD  BP13KH55                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
           COPY BP13KH55.                                                       
                                                                                
       FD  BP13KC50                                                             
           RECORD CONTAINS  100 CHARACTERS.                                     
           COPY BP13KC50.                                                       
                                                                                
       FD  P13LH60A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  P13LH60A-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K110-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KH55-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC50-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FH55-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FH55-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KH55-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC50-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-LH60A-WRITE        PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-LH60A-LINE         PIC 9(2)   VALUE 60.                   
           05  WS-CNT-LH60A-PAGE         PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LH60A-SNO          PIC 9(5)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FH55-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KH55-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC50-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DESC               PIC X(60)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT FOR EXCEPTIONS                                         
      *-------------------------------------------------------------            
       01  LH60A-PR-HEAD-01.                                                    
           05  FILLER                    PIC X(14)   VALUE 'BP13LH60A'.         
           05  FILLER                    PIC X(8)    VALUE 'HDB3    '.          
           05  FILLER                    PIC X(25)   VALUE SPACES.              
           05  FILLER                    PIC X(39)   VALUE                      
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(13)   VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'DATE: '.            
           05  LH60A-DTE-CURRENT         PIC X(10)   VALUE SPACES.              
           05  FILLER                    PIC X(3)    VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'PAGE: '.            
           05  LH60A-PAGE                PIC ZZZ9.                              
                                                                                
       01  LH60A-PR-HEAD-02.                                                    
           05  FILLER                    PIC X(44)   VALUE SPACES.              
           05  FILLER                    PIC X(43)   VALUE                      
              'EXCEPTION REPORT ON GENERATING PCD SMS FILE'.                    
                                                                                
       01  LH60A-PR-HEAD-03.                                                    
           05  FILLER                    PIC X(132)  VALUE                      
              '    S/NO   REGN      ERROR DESCRIPTION'.                         
                                                                                
       01  LH60A-PR-DTL.                                                        
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  LH60A-SNO                 PIC ZZZZ9.                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  LH60A-NUM-REGN            PIC X(8).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  LH60A-NUM-DESC            PIC X(60).                             
                                                                                
       01  LH60A-PR-DASH-LINE.                                                  
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  FILLER                    PIC X(90)   VALUE ALL '-'.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13FH55-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FH55-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FH55                                                 
                       BP13K800                                                 
                       BM06K110                                                 
                I-O    BP13KH55                                                 
                       BP13KC50                                                 
                OUTPUT P13LH60A.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KH55-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KH55 ERROR ' WS-KH55-STATUS                  
              MOVE     WS-KH55-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC50 ERROR ' WS-KC50-STATUS                  
              MOVE     WS-KC50-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FH55-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FH55                                                        
              AT END MOVE 'Y'              TO   WS-FH55-EOF.                    
                                                                                
           IF WS-FH55-EOF NOT = 'Y'                                             
              ADD  1                       TO   WS-CNT-FH55-READ                
              MOVE SPACES                  TO   WS-NUM-DESC                     
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    VALIDATE INPUT FILE                                                  
      *-------------------------------------------------------------            
           IF FH55-NUM-TAG-ONLINE = 'Y'                                         
              MOVE 'SMS ALREADY TAGGED IN ONLINE'                               
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF FH55-NUM-TAG-SMS NOT = 'Y'                                        
              MOVE 'REGN NOT ALLOWED TO SEND SMS'                               
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF FH55-NUM-PCD-FORM NOT = 'C' AND 'D' AND 'M'                       
              MOVE 'INVALID PCD FORM TYPE' TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF FH55-NUM-HP = SPACES OR LOW-VALUES OR ZEROES                      
              MOVE 'NO HANDPHONE NUMBER'   TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF FH55-NUM-HP(1:1) NOT = '3' AND '8' AND '9'                        
              MOVE 'INVALID HANDPHONE NUMBER'                                   
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET SCHEME ACCOUNT FROM SOC MASTER                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE FH55-NUM-REGN              TO   K800-NUM-REGN.                  
                                                                                
           PERFORM 3100-BP13K800-READ      THRU 3100-EXIT.                      
                                                                                
           IF WS-K800-FND = 'N'                                                 
              MOVE 'REGN NOT FOUND IN SOC MASTER'                               
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET FLAT INFO FROM FLAT INVENTORY FILE                               
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   K110-REC.                       
           MOVE K800-NUM-SCH-ACC           TO   K110-SCH-ACC-NO.                
                                                                                
           PERFORM 3200-BM06K110-READ      THRU 3200-EXIT.                      
                                                                                
           IF WS-K110-FND = 'N'                                                 
              MOVE 'SCHEME ACCOUNT NOT FOUND IN FLAT INVENTORY'                 
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CHECK IF ALREADY SENT AS SMS                                         
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13KC50-REC.                   
           MOVE FH55-NUM-REGN              TO   KC50-NUM-REGN.                  
                                                                                
           PERFORM 3300-BP13KC50-READ      THRU 3300-EXIT.                      
                                                                                
           IF WS-KC50-FND = 'Y'                                                 
              IF KC50-DTE-SMS-SENT NOT = SPACES AND LOW-VALUES AND              
                                         ZEROES                                 
                 STRING 'SMS WAS ALREADY SENT ON ' KC50-DTE-SMS-SENT            
                        DELIMITED BY SIZE  INTO WS-NUM-DESC                     
              ELSE                                                              
                 IF KC50-DTE-APPROVED NOT = SPACES AND LOW-VALUES AND           
                                            ZEROES                              
                    MOVE 'SMS APPROVED BUT NOT YET SENT'                        
                                           TO   WS-NUM-DESC                     
                 ELSE                                                           
                    MOVE 'PENDING SMS APPROVAL'                                 
                                           TO   WS-NUM-DESC                     
                 END-IF                                                         
              END-IF                                                            
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CREATE ONLINE PCD SMS FILE                                           
      *-------------------------------------------------------------            
           PERFORM 4000-CREATE-PCDSMS      THRU 4000-EXIT.                      
           PERFORM 4100-UPDATE-TAG-ONLINE  THRU 4100-EXIT.                      
                                                                                
       3000-CONTINUE-READ-NEXT.                                                 
                                                                                
           PERFORM 2000-BP13FH55-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13K800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K800-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K800-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K800 ERROR '    WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-BM06K110-READ.                                                      
      *=============================================================            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K110-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K110-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BM06K110 ERROR '    WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-BP13KC50-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC50.                                                       
                                                                                
           EVALUATE WS-KC50-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC50-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC50-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC50 ERROR '    WS-KC50-STATUS                  
              MOVE     WS-KC50-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-CREATE-PCDSMS.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13KC50-REC.                   
           MOVE K110-ESTATE                TO   KC50-ESTATE.                    
           MOVE K110-NEIGHBOURHOOD         TO   KC50-NEIGHBOURHOOD.             
           MOVE K110-CONTRACT-NO           TO   KC50-CONTRACT-NO.               
           MOVE K110-BLK-NO                TO   KC50-BLK-NO.                    
           MOVE FH55-NUM-REGN              TO   KC50-NUM-REGN.                  
           MOVE 'BP13CH60'                 TO   KC50-NUM-USERID.                
           MOVE WS-DTE-CURR-8              TO   KC50-DTE-UPDATE.                
                                                                                
           EVALUATE FH55-NUM-PCD-FORM                                           
           WHEN 'C'                                                             
           WHEN 'M'                                                             
              MOVE 'P6'                    TO   KC50-TYPE-APPMT                 
           WHEN 'D'                                                             
              MOVE 'PY'                    TO   KC50-TYPE-APPMT                 
           END-EVALUATE.                                                        
                                                                                
           MOVE FH55-NUM-HP                TO   KC50-NUM-HP.                    
           MOVE 'Y'                        TO   KC50-NUM-SMS-TAG.               
                                                                                
           WRITE BP13KC50-REC.                                                  
           ADD 1                           TO   WS-CNT-KC50-WRT.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-UPDATE-TAG-ONLINE.                                                  
      *=============================================================            
                                                                                
           MOVE 'Y'                        TO   FH55-NUM-TAG-ONLINE.            
           MOVE BP13FH55-REC               TO   BP13KH55-REC.                   
                                                                                
           PERFORM 4110-BP13KH55-REWRITE   THRU 4110-EXIT.                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4110-BP13KH55-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13KH55-REC.                                                
                                                                                
           EVALUATE WS-KH55-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-KH55-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13KH55 ERROR ' WS-KH55-STATUS                  
              MOVE     WS-KH55-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LH60A-LINE > 54                                            
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-LH60A-SNO                
                                                WS-CNT-LH60A-LINE               
                                                WS-CNT-FH55-SKIP.               
                                                                                
           MOVE WS-CNT-LH60A-SNO           TO   LH60A-SNO.                      
           MOVE FH55-NUM-REGN              TO   LH60A-NUM-REGN.                 
           MOVE WS-NUM-DESC                TO   LH60A-NUM-DESC.                 
                                                                                
           WRITE P13LH60A-REC              FROM LH60A-PR-DTL.                   
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-LH60A-PAGE.              
           MOVE WS-CNT-LH60A-PAGE          TO   LH60A-PAGE.                     
           MOVE WS-DTE-CURR-10             TO   LH60A-DTE-CURRENT.              
                                                                                
           WRITE P13LH60A-REC FROM LH60A-PR-HEAD-01 AFTER PAGE.                 
           WRITE P13LH60A-REC FROM LH60A-PR-HEAD-02.                            
           WRITE P13LH60A-REC FROM LH60A-PR-DASH-LINE AFTER 2.                  
           WRITE P13LH60A-REC FROM LH60A-PR-HEAD-03.                            
           WRITE P13LH60A-REC FROM LH60A-PR-DASH-LINE.                          
                                                                                
           MOVE SPACES                     TO   P13LH60A-REC.                   
           WRITE P13LH60A-REC.                                                  
                                                                                
           MOVE 7                          TO   WS-CNT-LH60A-LINE.              
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FH55                                                       
                 BP13K800                                                       
                 BM06K110                                                       
                 BP13KH55                                                       
                 BP13KC50                                                       
                 P13LH60A.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KH55-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KH55 ERROR ' WS-KH55-STATUS                  
              MOVE     WS-KH55-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC50 ERROR ' WS-KC50-STATUS                  
              MOVE     WS-KC50-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13CH60 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FH55 READ      : ' WS-CNT-FH55-READ.          
           DISPLAY ' 2. NO OF BP13FH55 SKIPPED   : ' WS-CNT-FH55-SKIP.          
           DISPLAY ' 3. NO OF BP13KH55 UPDATED   : ' WS-CNT-KH55-UPD.           
           DISPLAY ' 4. NO OF BP13KC50 WRITTEN   : ' WS-CNT-KC50-WRT.           
           DISPLAY ' 5. NO OF P13LH60A WRITTEN   : ' WS-CNT-LH60A-SNO.          
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
