       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22S.                                                 
       AUTHOR.        LLOYD CAPARRO.                                            
      *                                                                         
      ************************************************************              
      *               SYSTEM OF COMMITMENT (SOC)                 *              
      *==========================================================*              
      * DATE WRITTEN      : 05 APR 2005                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CANCELLATION OF BTO APPLICATIONS     *              
      *                                                          *              
      *                                                          *              
      * 2. INPUT          : BP13F310 -                           *              
      *                   : BP13K820 - BP13.K820.OCCUP           *              
      *                                                          *              
      * 3. I/O            : BP13K800 - BP13.K800.APPLN           *              
      *                   : BP13K840 - BP13.K840.APPLHIST        *              
      *                                                          *              
      * 4. OUTPUT         : BP13F915 -                           *              
      *                   : BP13FOUT -                           *              
      *----------------------------------------------------------*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  ======== ========== ==== ============================== *              
      *  BP132681 05/04/2005 CG7  NEW PROGRAM.                   *              
      *  BP134681 02/10/2012 ESA1 CATER FOR 3-BYTES ZONE         *              
      *  BP137200 08/03/2018 FNP1 CHANGE 'DE' TXN-TYPE TO 'TE' IN F915          
      *  BP137326 06/12/2018 FNP1 BLANK OFF OTHER DETAILS WHEN   *              
      *                           CREATING 'TE' FOR F915         *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310          ASSIGN TO BP13F310.                         
                                                                                
           SELECT BP13K800          ASSIGN TO BP13K800                          
                                    ACCESS MODE IS RANDOM                       
                                    ORGANIZATION IS INDEXED                     
                                    RECORD KEY IS K800-NUM-REGN                 
                                    STATUS IS WS-K800-STATUS.                   
                                                                                
           SELECT BP13K820          ASSIGN TO BP13K820                          
                                    ACCESS MODE IS DYNAMIC                      
                                    ORGANIZATION IS INDEXED                     
                                    RECORD KEY IS K820-KEY-FLD                  
                                    STATUS IS WS-K820-STATUS.                   
                                                                                
           SELECT BP13K840          ASSIGN TO BP13K840                          
                                    ACCESS MODE IS RANDOM                       
                                    ORGANIZATION IS INDEXED                     
                                    RECORD KEY IS K840-KEY-FLD                  
                                    STATUS IS WS-K840-STATUS.                   
                                                                                
           SELECT BP13F915          ASSIGN TO BP13F915.                         
                                                                                
           SELECT BP13FOUT          ASSIGN TO BP13FOUT.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
       FD   BP13F310                                                            
                                    RECORD CONTAINS 150 CHARACTERS              
                                    BLOCK CONTAINS 0 RECORDS                    
                                    LABEL RECORD IS STANDARD                    
                                    RECORDING MODE IS F.                        
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K800                                                            
                                    RECORD CONTAINS 2000 CHARACTERS.            
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
                                    RECORD CONTAINS 400 CHARACTERS.             
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K840                                                            
                                    RECORD CONTAINS 500 CHARACTERS.             
       COPY BP13K840.                                                           
                                                                                
       FD   BP13F915                                                            
                                    RECORD CONTAINS 800 CHARACTERS              
                                    BLOCK CONTAINS 0 RECORDS                    
                                    LABEL RECORD IS STANDARD                    
                                    RECORDING MODE IS F.                        
       COPY BP13F915.                                                           
                                                                                
       FD  BP13FOUT                                                             
                                    RECORD CONTAINS 150 CHARACTERS              
                                    BLOCK CONTAINS 0 RECORDS                    
                                    LABEL RECORD IS STANDARD                    
                                    RECORDING MODE F.                           
       01  BP13FOUT-REC.                                                        
           05  FOUT-REGN                 PIC X(08).                             
           05  FOUT-ZONE                 PIC X(03).                             
           05  FOUT-NT                   PIC X(03).                             
           05  FOUT-NRIC1                PIC X(09).                             
           05  FOUT-NRIC2                PIC X(09).                             
           05  FOUT-NRIC3                PIC X(09).                             
           05  FOUT-NRIC4                PIC X(09).                             
           05  FOUT-SCH-ACC              PIC X(11).                             
           05  FOUT-BLK                  PIC X(05).                             
           05  FOUT-STREET               PIC X(32).                             
           05  FOUT-LEVEL                PIC X(02).                             
           05  FOUT-MAIN                 PIC X(05).                             
           05  FOUT-DTE-BALLOT           PIC X(06).                             
           05  FOUT-FLAT-TYPE            PIC X(02).                             
           05  FOUT-AMT-BK-PAID          PIC S9(4)V99.                          
           05  FILLER                    PIC X(31).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K840-STATUS            PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F310-EOF               PIC X(01)   VALUE 'N'.                 
           05  WS-K820-EOF               PIC X(01)   VALUE 'N'.                 
           05  WS-K820-NTFND             PIC X(01)   VALUE 'N'.                 
           05  WS-REC-PROCESS            PIC X(01)   VALUE 'N'.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F310-READ              PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-FND               PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-NOTFND            PIC 9(06)  VALUE ZEROES.               
           05  WS-K820-FND               PIC 9(06)  VALUE ZEROES.               
           05  WS-K820-NOTFND            PIC 9(06)  VALUE ZEROES.               
           05  WS-K840-FND               PIC 9(06)  VALUE ZEROES.               
           05  WS-K840-NOTFND            PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-REWRT             PIC 9(06)  VALUE ZEROES.               
           05  WS-K840-REWRT             PIC 9(06)  VALUE ZEROES.               
           05  WS-F915-WRT               PIC 9(06)  VALUE ZEROES.               
           05  WS-FOUT-WRT               PIC 9(06)  VALUE ZEROES.               
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(08)  VALUE SPACES.               
           05  WS-CURRENT-TIME           PIC X(09)  VALUE SPACES.               
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-CURRENT-TIME1  PIC X(08).                             
                   10  WS-GM             PIC X(01).                             
           05  FILLER                    PIC X(04)  VALUE SPACES.               
                                                                                
       01  WS-TEMPORARY-VARIABLES.                                              
           05  WS-RUN-DATE               PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORD THRU 2000-EXIT                           
             UNTIL WS-F310-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F310                                                 
                       BP13K820                                                 
                I-O    BP13K800                                                 
                       BP13K840                                                 
                OUTPUT BP13F915                                                 
                       BP13FOUT.                                                
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR ' WS-K800-STATUS                     
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 OPEN ERROR ' WS-K820-STATUS                     
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K840-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K840 OPEN ERROR ' WS-K840-STATUS                     
              MOVE WS-K840-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE.                                                  
                                                                                
           PERFORM 1100-READ-BP13F310 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F310.                                                      
      *************************************************************             
           READ BP13F310                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F310-EOF                         
                MOVE HIGH-VALUES         TO F310-KEY-FLD                        
            NOT AT END                                                          
                ADD  1                   TO WS-F310-READ                        
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORD.                                                     
      *************************************************************             
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F310-REGN-NO             TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K800-FND                         
                                                                                
                    PERFORM 2100-REWRITE-BP13K800 THRU 2100-EXIT                
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'K800 REC NOT FOUND, KEY: ' K800-NUM-REGN           
                    ADD 1                TO WS-K800-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR : ' WS-K800-STATUS             
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           PERFORM 1100-READ-BP13F310 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2100-REWRITE-BP13K800.                                                   
      *************************************************************             
           MOVE SPACES                   TO K800-NUM-HALF-IDP.                  
           MOVE 'E7'                     TO K800-NUM-CANCEL-REASON.             
           MOVE 'C'                      TO K800-NUM-STATUS.                    
           MOVE  WS-CURRENT-DATE1        TO K800-DTE-CANCEL                     
                                            K800-DTE-UPDATE.                    
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K800-REWRT                       
                                                                                
                    PERFORM 3000-PROCESS-CDB THRU 3000-EXIT                     
                    PERFORM 4000-READ-BP13K840 THRU 4000-EXIT                   
                    PERFORM 5000-WRITE-BP13FOUT THRU 5000-EXIT                  
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 REWRITE ERROR : ' WS-K800-STATUS          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-PROCESS-CDB.                                                        
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE 'N'                      TO WS-K820-EOF                         
                                            WS-K820-NTFND                       
                                            WS-REC-PROCESS.                     
                                                                                
           MOVE K800-NUM-REGN            TO K820-NUM-REGN.                      
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BP13K820 THRU 3100-EXIT               
                      UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                   
                         OR WS-K820-EOF = 'Y'                                   
                         OR WS-K820-NTFND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 START BROWSE ERROR : '                    
                            WS-K820-STATUS                                      
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-BP13K820.                                                  
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    IF K820-NUM-REGN = K800-NUM-REGN                            
                       ADD 1             TO WS-K820-FND                         
                       MOVE 'Y'          TO WS-REC-PROCESS                      
                                                                                
                       PERFORM 3300-WRITE-BP13F915 THRU 3300-EXIT               
                    ELSE                                                        
                       IF WS-REC-PROCESS = 'N'                                  
                          ADD 1          TO WS-K820-NOTFND                      
                       END-IF                                                   
                                                                                
                       MOVE 'Y'          TO WS-K820-NTFND                       
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
                    MOVE 'Y'             TO WS-K820-NTFND                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ NEXT ERROR : ' WS-K820-STATUS        
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3300-WRITE-BP13F915.                                                     
      *************************************************************             
           IF K820-NUM-CSTMR-SOURCE NOT = SPACES AND LOW-VALUES                 
              MOVE SPACES                   TO BP13F915-REC                     
              INITIALIZE                       BP13F915-REC                     
                                                                                
              MOVE  'TE'                    TO F915-TXN-TYPE                    
              MOVE  'P'                     TO F915-CSTMR-TYPE                  
              MOVE  'BP13'                  TO F915-NUM-UPDATE-SOURCE           
              MOVE  'BP13C22S'              TO F915-NUM-UPDATE-USERID           
              MOVE  K820-NUM-CSTMR-SOURCE   TO F915-NUM-CSTMR-SOURCE            
                                                                                
              IF K820-NUM-NRIC = K800-NUM-NRIC1                                 
                 MOVE '011'                 TO F915-NUM-ROLE                    
              ELSE                                                              
                 IF (K820-NUM-NRIC = K800-NUM-NRIC2) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC3) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC4)                            
                     MOVE '012'             TO F915-NUM-ROLE                    
                 ELSE                                                           
                     MOVE '013'             TO F915-NUM-ROLE                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE  K820-NUM-REGN           TO F915-NUM-BSNS-REF                
                                                                                
              STRING K800-DTE-CANCEL(1:4) '-'                                   
                     K800-DTE-CANCEL(5:2) '-'                                   
                     K800-DTE-CANCEL(7:2)                                       
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-ROLE-END                                          
                                                                                
              STRING WS-CURRENT-DATE1(1:4) '-'                                  
                     WS-CURRENT-DATE1(5:2) '-'                                  
                     WS-CURRENT-DATE1(7:2)                                      
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-UPDATE-SOURCE                                     
                                                                                
              WRITE BP13F915-REC                                                
                                                                                
              ADD 1                         TO WS-F915-WRT                      
                                                                                
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       4000-READ-BP13K840.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K840-REC.                       
           INITIALIZE                       BP13K840-REC.                       
                                                                                
           MOVE K800-NUM-REGN            TO K840-NUM-REGN.                      
                                                                                
           READ BP13K840.                                                       
                                                                                
           EVALUATE WS-K840-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K840-FND                         
                                                                                
                    PERFORM 4100-REWRITE-BP13K840 THRU 4100-EXIT                
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'K840 REC NOT FOUND, KEY: ' K840-KEY-FLD            
                    ADD 1                TO WS-K840-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K840 READ ERROR : ' WS-K840-STATUS             
                    MOVE WS-K840-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       4100-REWRITE-BP13K840.                                                   
      *************************************************************             
           MOVE K800-NUM-NRIC1           TO K840-NUM-NRIC1.                     
                                                                                
           IF K800-NUM-NRIC2 = SPACES OR LOW-VALUES                             
              MOVE '#'                   TO K840-NUM-NRIC2(1:1)                 
              MOVE K800-NUM-REGN         TO K840-NUM-NRIC2(2:8)                 
           ELSE                                                                 
              MOVE K800-NUM-NRIC2        TO K840-NUM-NRIC2                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 = SPACES OR LOW-VALUES                             
              MOVE '#'                   TO K840-NUM-NRIC3(1:1)                 
              MOVE K800-NUM-REGN         TO K840-NUM-NRIC3(2:8)                 
           ELSE                                                                 
              MOVE K800-NUM-NRIC3        TO K840-NUM-NRIC3                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 = SPACES OR LOW-VALUES                             
              MOVE '#'                   TO K840-NUM-NRIC4(1:1)                 
              MOVE K800-NUM-REGN         TO K840-NUM-NRIC4(2:8)                 
           ELSE                                                                 
              MOVE K800-NUM-NRIC4        TO K840-NUM-NRIC4                      
           END-IF.                                                              
                                                                                
           MOVE WS-CURRENT-DATE1         TO K840-DTE-CANCEL                     
                                            K840-DTE-UPDATE.                    
           MOVE 'E7'                     TO K840-NUM-CANCEL.                    
           MOVE 'BP13C22S'               TO K840-NUM-USERID.                    
                                                                                
           REWRITE BP13K840-REC.                                                
                                                                                
           EVALUATE WS-K840-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1                TO WS-K840-REWRT                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K840 REWRITE ERROR : ' WS-K840-STATUS          
                    MOVE WS-K840-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       5000-WRITE-BP13FOUT.                                                     
      *************************************************************             
           MOVE SPACES                   TO BP13FOUT-REC.                       
           INITIALIZE                       BP13FOUT-REC.                       
                                                                                
           MOVE F310-REGN-NO             TO FOUT-REGN.                          
           MOVE F310-CDE-FLAT-TYPE       TO FOUT-FLAT-TYPE.                     
           IF K800-NUM-ALLO-CAT = 'BTO'                                         
              MOVE K800-NUM-BTO-ZONE     TO FOUT-ZONE                           
           ELSE                                                                 
              MOVE F310-NUM-ZONE         TO FOUT-ZONE                           
           END-IF.                                                              
           MOVE F310-CDE-NT              TO FOUT-NT.                            
           MOVE K800-NUM-NRIC1           TO FOUT-NRIC1.                         
           MOVE K800-NUM-NRIC2           TO FOUT-NRIC2.                         
           MOVE K800-NUM-NRIC3           TO FOUT-NRIC3.                         
           MOVE K800-NUM-NRIC4           TO FOUT-NRIC4.                         
           MOVE F310-SCH-ACC             TO FOUT-SCH-ACC.                       
           MOVE F310-NUM-BLK             TO FOUT-BLK.                           
           MOVE F310-NME-STREET          TO FOUT-STREET.                        
           MOVE F310-NUM-LEVEL           TO FOUT-LEVEL.                         
           MOVE F310-NUM-MAIN            TO FOUT-MAIN.                          
           MOVE K800-DTE-BALLOT          TO FOUT-DTE-BALLOT.                    
           MOVE K800-AMT-BK-PAID         TO FOUT-AMT-BK-PAID.                   
                                                                                
           WRITE BP13FOUT-REC.                                                  
                                                                                
           ADD 1                         TO WS-FOUT-WRT.                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C22S CONTROL TOTAL -------*'.                  
           DISPLAY ' RUN DATE : ' WS-RUN-DATE.                                  
           DISPLAY '   '.                                                       
           DISPLAY ' RECS READ FROM BP13F310 : ' WS-F310-READ.                  
           DISPLAY ' RECS FOUND IN K800      : ' WS-K800-FND.                   
           DISPLAY ' RECS NOT FOUND IN K800  : ' WS-K800-NOTFND.                
           DISPLAY ' RECS FOUND IN K820      : ' WS-K820-FND.                   
           DISPLAY ' RECS NOT FOUND IN K820  : ' WS-K820-NOTFND.                
           DISPLAY ' RECS FOUND IN K840      : ' WS-K840-FND.                   
           DISPLAY ' RECS NOT FOUND IN K840  : ' WS-K840-NOTFND.                
           DISPLAY ' RECS REWRITTEN TO K800  : ' WS-K800-REWRT.                 
           DISPLAY ' RECS REWRITTEN TO K840  : ' WS-K840-REWRT.                 
           DISPLAY ' RECS WRITTEN TO F915    : ' WS-F915-WRT.                   
           DISPLAY ' RECS WRITTEN TO FOUT    : ' WS-FOUT-WRT.                   
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K840                                                       
                 BP13F915                                                       
                 BP13FOUT.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 CLOSE ERROR ' WS-K800-STATUS                    
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 CLOSE ERROR ' WS-K820-STATUS                    
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K840-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K840 CLOSE ERROR ' WS-K840-STATUS                    
              MOVE WS-K840-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
