       IDENTIFICATION DIVISION.                                                 
         PROGRAM-ID.    BP13C135.                                               
      *  AUTHOR.        NPK.                                                    
      *  DATE-WRITTEN.  20.08.89.                                               
      *  DATE-COMPILED.                                                         
      *  REMARKS.                                                               
                                                                                
      ***************************************************************           
      *                                                             *           
      *  OBJECTIVES:                                                *           
      *  1. TO READ SEQUENTIALLY THE KIVTRAN FILE TO PRINT A        *           
      *     LISTING OF DELETED CASES IF THE K140-CDE-VALID IS       *           
      *     'D'.                                                    *           
      *  2. TO PHYSICALLY DELETE SUCH CASES FROM THE KIVTRAN        *           
      *     FILE AFTER PRINTING.                                    *           
      *                                                             *           
      *                                                             *           
      *  INPUT-OUTPUT FILES :                                       *           
      *  ====================                                       *           
      *  TYPE  FILE NAME             DD NAME   ACCESS   KEYLENGTH   *           
      *  ----  ---------             -------   ------   ---------   *           
      *  I-O   BP13T.K140.KIVTRAN    BP13K140  SEQ         18       *           
      *                                                             *           
      *  LISTING :                                                  *           
      *  =========                                                  *           
      *  BP13L135  - ERROR LISTING                                  *           
      *                                                             *           
      *                                                             *           
      *  HISTORY OF MODIFICATION :                                  *           
      *  =========================                                  *           
      * CHG-NO   BY   ON                                            *           
      * ----------------                                            *           
      * SOC-PH8  LSB 230793 - COBOL II                              *           
      * BP130103 LJL 160396 - TO CATER FOR SUB-TRANSFER.            *           
      * BP130227 LJL 030197 - TO INCLUDE DECIMAL POINTS FOR TOTAL.  *           
      * BP130418 RHB 260898 - TO CATER FOR Y2K.                     *           
      * BP132313 CKK 260303 - TO PRINT USER-ID AND VOUCHER NO       *           
      *                                                             *           
      ***************************************************************           
           EJECT                                                                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
      *---------------------                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
         SOURCE-COMPUTER.   IBM-3090.                                           
         OBJECT-COMPUTER.   IBM-3090.                                           
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
             ORGANIZATION IS INDEXED                                            
             ACCESS MODE IS SEQUENTIAL                                          
             RECORD KEY IS K140-KEY-FLD                                         
             FILE STATUS IS WS-K140-STATUS.                                     
                                                                                
           SELECT BP13F140 ASSIGN TO BP13F140.                                  
           SELECT BP13L135 ASSIGN TO BP13L135.                                  
                                                                                
       DATA DIVISION.                                                           
      *--------------                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13K140                                                     00007412
           BLOCK CONTAINS 0 RECORDS                                     00007512
           RECORD CONTAINS 150 CHARACTERS                               00007612
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13K140.                                                           
       FD  BP13F140                                                     00007412
           BLOCK CONTAINS 0 RECORDS                                     00007512
           RECORD CONTAINS 150 CHARACTERS                               00007612
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F140.                                                           
                                                                                
       FD  BP13L135                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L135-PRINT-REC              PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *-------------------------------------*                                   
      *  WORKING STORAGE AREA FOR COUNTERS. *                                   
      *-------------------------------------*                                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CTR-K140-DELETE       PIC 9(7)     VALUE 0.                    
           05 WS-CTR-K140-READ         PIC 9(7)     VALUE 0.                    
           05 WS-CTR-WRITE             PIC 9(7)     VALUE 0.                    
                                                                                
      *---------------------------------------------------------------*         
      *  WORKING STORAGE AREA FOR STORING FILE STATUS IN I-O ROUTINE. *         
      *---------------------------------------------------------------*         
                                                                                
       01  WS-K140-STATUS.                                                      
           05  WS-K140-STATUS1         PIC X.                                   
           05  WS-K140-STATUS2         PIC X.                                   
                                                                                
      *----------------------------------*                                      
      *  WORKING STORAGE AREA FOR FLAGS. *                                      
      *----------------------------------*                                      
                                                                                
       01  WS-EOF                      PIC X        VALUE 'N'.                  
                                                                                
       01  WS-PAGE-NO                  PIC 9(5)     VALUE 0.                    
                                                                                
       01  WS-LINE-COUNT               PIC 99       VALUE 99.                   
                                                                                
       01  WS-CASE-NO                  PIC 9999     VALUE 1.                    
                                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------*             
      *  WORKING STORAGE AREA FOR CHANGING DATE FORMAT TO YYMMDD. *             
      *-----------------------------------------------------------*             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CCYY                 PIC 9(4).                                
           05  WS-MM                   PIC 99.                                  
           05  WS-DD                   PIC 99.                                  
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-DD                   PIC 99.                                  
           05  FILLER                  PIC X         VALUE '/'.                 
           05  WS-MM                   PIC 99.                                  
           05  FILLER                  PIC X         VALUE '/'.                 
           05  WS-CCYY                 PIC 9(4).                                
                                                                                
       01  WS-DTE-CONVERT.                                                      
           05  WS-DTE-CC               PIC 99.                                  
           05  WS-DTE-YY               PIC 99.                                  
           05  WS-DTE-MM               PIC 99.                                  
           05  WS-DTE-DD               PIC 99.                                  
                                                                                
       01  WS-DTE-TRANSFER.                                                     
           05  WS-DTE-DD               PIC 99.                                  
           05  FILLER                  PIC X        VALUE '/'.                  
           05  WS-DTE-MM               PIC 99.                                  
           05  FILLER                  PIC X        VALUE '/'.                  
           05  WS-DTE-CC               PIC 99.                                  
           05  WS-DTE-YY               PIC 99.                                  
                                                                                
                                                                                
      *---------------------------------------*                                 
      *  WORKING STORAGE AREA FOR VARIABLES.  *                                 
      *---------------------------------------*                                 
                                                                                
       01  WS-VAR-PROC-TYP             PIC X(30)     VALUE SPACE.               
       01  WS-VAR-FILE-STATUS          PIC X(6)      VALUE SPACE.               
       01  WS-AMT-TOTAL-DEL            PIC S9(8)V99  VALUE ZEROES.              
                                                                                
                                                                                
                                                                                
      *---------------------------------------*                                 
      *  WORKING STORAGE FOR PRINTING HEADER  *                                 
      *---------------------------------------*                                 
                                                                                
       01  HL-HEADER1.                                                          
           05  FILLER                  PIC X(8) VALUE 'BP13L135'.               
           05  FILLER                  PIC X(6).                                
           05  FILLER                  PIC X(8) VALUE 'HDBCAT 3'.               
           05  FILLER                  PIC X(20).                               
           05  FILLER                  PIC X(40) VALUE                          
                      'S Y S T E M    O F   C O M M I T M E N T'.               
           05  FILLER                  PIC X(13).                               
           05  FILLER                  PIC X(7) VALUE 'DATE : '.                
           05  HL-DATE                 PIC X(10).                               
           05  FILLER                  PIC X(2).                                
           05  FILLER                  PIC X(7) VALUE 'PAGE : '.                
           05  HL-PAGE                 PIC ZZZZ9.                               
           05  FILLER                  PIC X(6).                                
                                                                                
       01  HL-HEADER2.                                                          
           05  FILLER                  PIC X(35).                               
           05  FILLER                  PIC X(53) VALUE                          
               'RECORDS DELETED FROM KIV (UNPOSTED TRANSACTIONS) FOR '.         
           05  HL-MTH-MM               PIC XX.                                  
           05  FILLER                  PIC X(3)  VALUE ' / '.                   
           05  HL-MTH-CCYY             PIC XXXX.                                
           05  FILLER                  PIC X(37).                               
                                                                                
       01  HL-HEADER3.                                                          
           05  FILLER                  PIC X(7).                                
           05  FILLER                  PIC X(7)     VALUE 'DATE OF'.            
           05  FILLER                  PIC X(19).                               
           05  FILLER                  PIC X(4)     VALUE '    '.               
           05  FILLER                  PIC X(6).                                
           05  FILLER                  PIC X(4)     VALUE 'HIST'.               
           05  FILLER                  PIC X(5).                                
           05  FILLER                  PIC X(11)    VALUE 'RECEIPT NO/'.        
           05  FILLER                  PIC X(6).                                
           05  FILLER                  PIC X(7)     VALUE 'DATE OF'.            
           05  FILLER                  PIC X(8).                                
           05  FILLER                  PIC X(7)     VALUE 'PAYMENT'.            
           05  FILLER                  PIC X(16).                               
           05  FILLER                  PIC X(11)    VALUE 'ADJ VOUCHER'.        
           05  FILLER                  PIC X(7).                                
           05  FILLER                  PIC X(4)     VALUE 'USER'.               
           05  FILLER                  PIC X(3)     VALUE SPACES.               
                                                                                
       01  HL-HEADER4.                                                          
           05  FILLER                  PIC X(4)     VALUE 'S/NO'.               
           05  FILLER                  PIC X(3).                                
           05  FILLER                  PIC X(8)     VALUE 'DELETION'.           
           05  FILLER                  PIC X(3).                                
           05  FILLER                  PIC X(11)    VALUE 'SCH-ACCT-NO'.        
           05  FILLER                  PIC X(4).                                
           05  FILLER                  PIC X(6)     VALUE 'REG-NO'.             
           05  FILLER                  PIC X(4).                                
           05  FILLER                  PIC X(5)     VALUE 'COUNT'.              
           05  FILLER                  PIC X(4).                                
           05  FILLER                  PIC X(11)    VALUE 'DOC REF    '.        
           05  FILLER                  PIC X(6).                                
           05  FILLER                  PIC X(11)    VALUE 'TRANSACTION'.        
           05  FILLER                  PIC X(4).                                
           05  FILLER                  PIC X(7)     VALUE ' TYPE  '.            
           05  FILLER                  PIC X(6).                                
           05  FILLER                  PIC X(6)     VALUE 'AMOUNT'.             
           05  FILLER                  PIC X(6).                                
           05  FILLER                  PIC X(11)    VALUE 'REFERENCE  '.        
           05  FILLER                  PIC X(5).                                
           05  FILLER                  PIC X(4)     VALUE 'ID  '.               
           05  FILLER                  PIC X(3)     VALUE SPACES.               
                                                                                
       01  HL-HEADER5.                                                          
           05  FILLER                  PIC X(86)    VALUE SPACES.               
           05  FILLER                  PIC X(8)     VALUE 'TOTAL : '.           
           05  WS-TOTAL-DEL            PIC $$,$$$,$$9.99.                       
                                                                                
      *---------------------------------------*                                 
      *  WORKING STORAGE FOR PRINTING DETAIL  *                                 
      *---------------------------------------*                                 
                                                                                
       01  WS-PRINT-DETAIL.                                                     
           05  WS-SERIAL-NUM           PIC ZZZ9.                                
           05  FILLER                  PIC X(3).                                
           05  WS-DTE-DELETE           PIC X(10).                               
           05  FILLER                  PIC X(1).                                
           05  WS-SCH-ACCT-NO          PIC X(11).                               
           05  FILLER                  PIC X(4).                                
           05  WS-REGN                 PIC X(8).                                
           05  FILLER                  PIC X(3).                                
           05  WS-HIST-COUNT           PIC X(3).                                
           05  FILLER                  PIC X(5).                                
           05  WS-RECPT-JRNO           PIC X(14).                               
           05  FILLER                  PIC X(4).                                
           05  WS-DTE-TRANS            PIC X(10).                               
           05  FILLER                  PIC X(6).                                
           05  WS-PAYMENT-TYPE         PIC XXX.                                 
           05  FILLER                  PIC X(5).                                
           05  WS-AMT-CASH             PIC $$$,$$9.99.                          
           05  FILLER                  PIC X(4).                                
           05  WS-NUM-DELETE-JRNO      PIC X(14).                               
           05  FILLER                  PIC X(3).                                
           05  WS-NUM-DELETE-USER      PIC X(7).                                
                                                                                
           EJECT                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------                                                      
                                                                                
       0000-MAIN-ROUTINE.                                                       
           PERFORM 1000-OPEN-FILES  THRU 1000-EXIT.                             
           PERFORM 3000-READ-K140   THRU 3000-EXIT.                             
           PERFORM 4000-PROCESS-K140 THRU 4000-EXIT                             
                                UNTIL WS-EOF = 'Y'.                             
           PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                             
                                                                                
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------*                                     
      *  ROUTINE TO OPEN FILES BP13K140   *                                     
      *-----------------------------------*                                     
                                                                                
       1000-OPEN-FILES.                                                         
           OPEN I-O    BP13K140                                                 
                OUTPUT BP13F140                                                 
                       BP13L135.                                                
                                                                                
           IF WS-K140-STATUS NOT = '00'    AND                                  
              WS-K140-STATUS NOT = '97'                                         
              MOVE 'OPENING BP13K140.' TO WS-VAR-PROC-TYP                       
              MOVE WS-K140-STATUS TO WS-VAR-FILE-STATUS                         
              PERFORM 2000-DISPLAY-ERROR THRU 2000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------*                          
      * ROUTINE TO DISPLAY ERROR MESSAGE ON CONSOLE. *                          
      *----------------------------------------------*                          
                                                                                
       2000-DISPLAY-ERROR.                                                      
                                                                                
           DISPLAY '     '                                                      
           DISPLAY '=========ERROR MSG========='                                
           DISPLAY 'ERROR ENCOUNTERED WHEN ' WS-VAR-PROC-TYP                    
                   ' (ERROR CODE = ' WS-VAR-FILE-STATUS ')'                     
           DISPLAY '     '                                                      
           MOVE WS-VAR-FILE-STATUS  TO   RETURN-CODE                            
           PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------*                                   
      * ROUTINE TO CONTROL PROCESSING LOOP  *                                   
      * FOR EACH RECORD READ FROM BP13K140  *                                   
      *-------------------------------------*                                   
                                                                                
       3000-READ-K140.                                                          
                                                                                
           READ BP13K140 AT END                                                 
                MOVE 'Y' TO WS-EOF                                              
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1 TO WS-CTR-K140-READ.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
      *  PROCESS K140 RECORD  *                                                 
      *-----------------------*                                                 
                                                                                
       4000-PROCESS-K140.                                                       
                                                                                
           IF K140-KEY-FLD = SPACES OR LOW-VALUES                               
              PERFORM 3000-READ-K140 THRU 3000-EXIT                             
              GO TO 4000-EXIT.                                                  
                                                                                
           IF K140-CDE-VALID = 'D'                                              
                                                                                
              COMPUTE WS-AMT-TOTAL-DEL = WS-AMT-TOTAL-DEL +                     
                                         K140-AMT-RECEIPT                       
              PERFORM 6000-PRINT-DETAIL THRU 6000-EXIT                          
              PERFORM 7000-DELETE-BP13K140 THRU 7000-EXIT.                      
                                                                                
           PERFORM 3000-READ-K140 THRU 3000-EXIT.                               
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
      *  PRINT DETAIL  *                                                        
      *----------------*                                                        
                                                                                
       6000-PRINT-DETAIL.                                                       
                                                                                
           IF WS-LINE-COUNT > 52                                                
              MOVE 1 TO WS-LINE-COUNT                                           
              PERFORM 8000-PRINT-HEADER THRU 8000-EXIT                          
           ELSE                                                                 
               ADD 1 TO WS-LINE-COUNT.                                          
                                                                                
           MOVE K140-AMT-RECEIPT TO WS-AMT-CASH.                                
           MOVE K140-DTE-TRANS TO WS-DTE-CONVERT.                               
           MOVE CORRESPONDING WS-DTE-CONVERT TO WS-DTE-TRANSFER.                
           MOVE WS-DTE-TRANSFER TO WS-DTE-TRANS.                                
           MOVE WS-CASE-NO TO WS-SERIAL-NUM.                                    
           MOVE K140-NUM-SCH-ACCT TO WS-SCH-ACCT-NO.                            
           MOVE K140-NUM-REGN TO WS-REGN.                                       
           MOVE K140-COUNT-HISTORY TO WS-HIST-COUNT.                            
           MOVE K140-NUM-DELETE-JRNO TO WS-NUM-DELETE-JRNO.                     
           MOVE K140-NUM-DELETE-USER TO WS-NUM-DELETE-USER.                     
           MOVE K140-NUM-RECPT-JRNO TO WS-RECPT-JRNO.                           
                                                                                
           MOVE K140-DTE-DELETE TO WS-DTE-CONVERT.                              
           MOVE CORRESPONDING WS-DTE-CONVERT TO WS-DTE-TRANSFER.                
           MOVE WS-DTE-TRANSFER TO WS-DTE-DELETE.                               
                                                                                
           MOVE K140-CDE-PAYMENT-TYPE TO WS-PAYMENT-TYPE                        
           WRITE L135-PRINT-REC FROM WS-PRINT-DETAIL                            
                 AFTER 1.                                                       
                                                                                
           ADD 1 TO WS-CASE-NO.                                                 
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *-------------------------------------*                                   
      * REWRITE UPDATED RECORD TO BP13K140. *                                   
      *-------------------------------------*                                   
                                                                                
       7000-DELETE-BP13K140.                                                    
           WRITE BP13F140-KIVTRAN FROM BP13K140-KIVTRAN.                        
           ADD 1 TO WS-CTR-WRITE.                                               
                                                                                
           DELETE  BP13K140.                                                    
           IF WS-K140-STATUS1 = 0                                               
              ADD 1 TO WS-CTR-K140-DELETE                                       
           ELSE                                                                 
              MOVE 'DELETING BP13K140' TO WS-VAR-PROC-TYP                       
              MOVE WS-K140-STATUS TO WS-VAR-FILE-STATUS                         
              PERFORM 2000-DISPLAY-ERROR THRU 2000-EXIT.                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
      *  PRINT HEADER  *                                                        
      *----------------*                                                        
                                                                                
       8000-PRINT-HEADER.                                                       
                                                                                
           ADD 1 TO WS-PAGE-NO.                                                 
           MOVE WS-PAGE-NO                 TO HL-PAGE.                          
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE CORRESPONDING WS-CURRENT-DATE TO WS-EDITED-DATE.                
           MOVE WS-EDITED-DATE             TO HL-DATE.                          
           MOVE WS-MM OF WS-CURRENT-DATE   TO HL-MTH-MM.                        
           MOVE WS-CCYY OF WS-CURRENT-DATE TO HL-MTH-CCYY.                      
                                                                                
           WRITE L135-PRINT-REC FROM HL-HEADER1                                 
                 AFTER ADVANCING PAGE.                                          
                                                                                
           WRITE L135-PRINT-REC FROM HL-HEADER2                                 
                 AFTER ADVANCING 1.                                             
           WRITE L135-PRINT-REC FROM HL-HEADER3                                 
                 AFTER ADVANCING 3.                                             
           WRITE L135-PRINT-REC FROM HL-HEADER4                                 
                 AFTER ADVANCING 1.                                             
       SKIP1                                                                    
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------*                                         
      * CLOSE ALL FILES AND STOP RUN. *                                         
      *-------------------------------*                                         
                                                                                
       9000-CLOSE-FILES.                                                        
                                                                                
           MOVE WS-AMT-TOTAL-DEL  TO  WS-TOTAL-DEL.                             
                                                                                
           WRITE L135-PRINT-REC FROM HL-HEADER5                                 
                 AFTER ADVANCING 1.                                             
                                                                                
           CLOSE BP13K140                                                       
                 BP13F140                                                       
                 BP13L135.                                                      
                                                                                
           IF WS-K140-STATUS NOT = ZERO                                         
              MOVE 'CLOSING BP13K140.' TO WS-VAR-PROC-TYP                       
              MOVE WS-K140-STATUS TO WS-VAR-FILE-STATUS                         
              PERFORM 2000-DISPLAY-ERROR THRU 2000-EXIT.                        
                                                                                
                                                                                
           DISPLAY '    '.                                                      
           DISPLAY ' ** BP13C135 ** '.                                          
           DISPLAY '    '.                                                      
           DISPLAY 'NUMBER OF RECORDS READ FROM KIVTRAN FILE  =  '              
                   WS-CTR-K140-READ.                                            
           DISPLAY '    '.                                                      
           DISPLAY 'NUMBER OF RECORDS UPDATED IN KIVTRAN FILE =  '              
                   WS-CTR-K140-DELETE.                                          
           DISPLAY '    '.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
