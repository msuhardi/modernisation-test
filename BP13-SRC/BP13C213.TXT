       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C213.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  20 JULY 2017.                                             
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE  :                                        *                
      *            TO RECOMPUTE K240-NUM-TOTAL FOR ROF SEL TYPE*                
      *            USING BP13F230.                             *                
      *                                                        *                
      *    INPUT    :  BP13F230                                *                
      *             :  BP13K240                                *                
      *    OUTPUT   :  BP13F240                                *                
      *--------------------------------------------------------*                
      *  CHG-NO  BY   DATE    DESCRIPTION                      *                
      *  ------- ---- ------  -----------                      *                
      * BP13XXXX ESA1 200717  NEW PROGRAM.                     *                
      * BP137014 ESA1 220118  TO CATER FOR GEN3 COMPUTATION    *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F230 ASSIGN TO BP13F230.                                  
           SELECT BP13F240 ASSIGN TO BP13F240.                                  
                                                                                
           SELECT BP13K240 ASSIGN TO BP13K240                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K240-KEY-FLD                               
                  FILE STATUS     IS BP13K240-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F230                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 450 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13K230.                                                           
                                                                                
       FD  BP13F240                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 450 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13F240-REC      PIC X(450).                                        
                                                                                
       FD  BP13K240                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K240.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K240-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  F230-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K240-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K240-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  F240-WRITE-CNT           PIC 9(6)  VALUE ZEROS.                      
       01  F240-DELETE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  WS-K240-NUM-TOTAL        PIC 9(5)  VALUE ZEROS.                      
       01  WS-K240-NUM-TOTAL-GEN3   PIC 9(5)  VALUE ZEROS.                      
       01  WS-MONTH                 PIC 9(2)  VALUE ZEROS.                      
       01  WS-PRINT-PAGE            PIC 9(3)  VALUE ZEROS.                      
       01  WS-PREV-K240-KEY-FLD     PIC X(52) VALUE SPACES.                     
                                                                                
       01  EOF-FLAG.                                                            
           05 EOF-F230              PIC X     VALUE 'N'.                        
                                                                                
       01  WS-DATE.                                                             
           05 WS-YY                 PIC 9(2).                                   
           05 WS-MM                 PIC 9(2).                                   
           05 WS-DD                 PIC 9(2).                                   
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-DD                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-MM                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-CC                 PIC 9(2)  VALUE ZEROS.                      
           05 WS-YY                 PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-TIME.                                                             
           05 WS-HH                 PIC X(2).                                   
           05 WS-MM                 PIC X(2).                                   
           05 WS-SS                 PIC X(2).                                   
                                                                                
       01  WS-EDITED-TIME.                                                      
           05 WS-SYS-TIME           PIC X(6).                                   
           05 WS-MICRO-SEC          PIC 9(1)  VALUE 0.                          
                                                                                
       05  WS-K240-KEY-FLD.                                                     
           10  WS-K240-CDE-HEADER      PIC X(02).                               
           10  WS-K240-DTE-ALLOC       PIC X(08).                               
           10  WS-K240-NUM-NEIGH       PIC X(04).                               
           10  WS-K240-NUM-CNTRCT      PIC X(04).                               
           10  FILLER                  PIC X(07).                               
           10  WS-K240-NUM-NEW-TOWN    PIC X(20).                               
           10  WS-K240-NUM-FLAT-TYPE   PIC X(02).                               
           10  WS-K240-NUM-BLK         PIC X(05).                               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                          
           PERFORM 150-READ-BP13F230    THRU 150-EXIT.                          
           PERFORM 200-PROCESS          THRU 200-EXIT                           
                   UNTIL EOF-F230 = 'Y'.                                        
                                                                                
           MOVE WS-K240-KEY-FLD      TO WS-PREV-K240-KEY-FLD.                   
           PERFORM 300-READ-K240-REC THRU 300-EXIT.                             
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F230                                                 
                I-O    BP13K240                                                 
                OUTPUT BP13F240.                                                
                                                                                
           IF BP13K240-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K240 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K240-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       150-READ-BP13F230.                                                       
      *-------------------------------------------------------------            
           READ BP13F230 AT END                                                 
                MOVE 'Y' TO EOF-F230                                            
                GO TO 150-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1   TO F230-READ-CNT.                                            
                                                                                
           MOVE SPACES                 TO WS-K240-KEY-FLD.                      
           MOVE '01'                   TO WS-K240-CDE-HEADER.                   
           MOVE K230-NUM-NEIGH         TO WS-K240-NUM-NEIGH.                    
           MOVE K230-NUM-CNTRCT        TO WS-K240-NUM-CNTRCT.                   
           MOVE K230-NUM-NEW-TOWN      TO WS-K240-NUM-NEW-TOWN.                 
           MOVE K230-NUM-FLAT-TYPE     TO WS-K240-NUM-FLAT-TYPE.                
           MOVE K230-NUM-BLK           TO WS-K240-NUM-BLK.                      
           MOVE K230-DTE-ALLOC         TO WS-K240-DTE-ALLOC.                    
                                                                                
           IF F230-READ-CNT = 1                                                 
             MOVE WS-K240-KEY-FLD      TO WS-PREV-K240-KEY-FLD                  
           END-IF.                                                              
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-PROCESS.                                                             
      *-------------------------------------------------------------            
           IF WS-K240-KEY-FLD NOT = WS-PREV-K240-KEY-FLD                        
              PERFORM 300-READ-K240-REC THRU 300-EXIT                           
              MOVE 1            TO WS-K240-NUM-TOTAL                            
              IF ((K230-NUM-FLAT-TYPE(1:1) = '5' OR                             
                   K230-NUM-FLAT-TYPE(2:1) = '5') AND                           
                  (K230-CDE-MODL-DESG = '90' OR '91' OR '95'))                  
                 MOVE 1            TO WS-K240-NUM-TOTAL-GEN3                    
              END-IF                                                            
              MOVE WS-K240-KEY-FLD TO WS-PREV-K240-KEY-FLD                      
           ELSE                                                                 
              ADD   1    TO WS-K240-NUM-TOTAL                                   
              IF ((K230-NUM-FLAT-TYPE(1:1) = '5' OR                             
                   K230-NUM-FLAT-TYPE(2:1) = '5') AND                           
                  (K230-CDE-MODL-DESG = '90' OR '91' OR '95'))                  
                 ADD  1            TO WS-K240-NUM-TOTAL-GEN3                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 150-READ-BP13F230 THRU 150-EXIT.                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-K240-REC.                                                       
      *-------------------------------------------------------------            
           MOVE WS-PREV-K240-KEY-FLD  TO K240-KEY-FLD.                          
                                                                                
           READ BP13K240.                                                       
                                                                                
           IF BP13K240-STATUS = 00                                              
              ADD 1 TO K240-READ-CNT                                            
              PERFORM 400-REWRITE-K240   THRU 400-EXIT                          
           ELSE                                                                 
              DISPLAY 'READ ERROR K240 : ' BP13K240-STATUS                      
              DISPLAY 'K240-KEY-FLD : ' K240-KEY-FLD                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-REWRITE-K240.                                                        
      *--------------------------------------------------------------*          
                                                                                
           IF K240-NUM-MAX-CAT1 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT1                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT2 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT2                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT3 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT3                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT1 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT1                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT2 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT2                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT3 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT3                            
           END-IF.                                                              
                                                                                
           COMPUTE K240-NUM-MAX-CAT1 = K240-NUM-MAX-CAT1 -                      
                                       K240-NUM-TOT-CAT1.                       
                                                                                
           COMPUTE K240-NUM-MAX-CAT2 = K240-NUM-MAX-CAT2 -                      
                                       K240-NUM-TOT-CAT2.                       
                                                                                
           COMPUTE K240-NUM-MAX-CAT3 = K240-NUM-MAX-CAT3 -                      
                                       K240-NUM-TOT-CAT3.                       
                                                                                
           COMPUTE K240-NUM-MAX-SPR  = K240-NUM-MAX-SPR  -                      
                                       K240-NUM-TOTAL-SPR.                      
                                                                                
           MOVE WS-K240-NUM-TOTAL      TO K240-NUM-TOTAL.                       
           MOVE WS-K240-NUM-TOTAL-GEN3 TO K240-NUM-TOTAL-GEN3.                  
           MOVE ZEROES               TO  K240-NUM-TOT-CAT1                      
                                         K240-NUM-TOT-CAT2                      
                                         K240-NUM-TOT-CAT3                      
                                         K240-NUM-TOTAL-SPR                     
                                         K240-NUM-TOT-CAT1-GEN3                 
                                         K240-NUM-TOT-CAT2-GEN3                 
                                         K240-NUM-TOT-CAT3-GEN3.                
           REWRITE K240-HEADER-REC.                                             
                                                                                
           EVALUATE BP13K240-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                 ADD 1    TO K240-UPDATE-CNT                                    
                 MOVE K240-HEADER-REC TO BP13F240-REC                           
                 WRITE BP13F240-REC                                             
               WHEN OTHER                                                       
                 DISPLAY 'ERROR WRITING BP13K240. STATUS IS '                   
                          BP13K240-STATUS                                       
                 PERFORM 900-CLOSE-ROUTINE   THRU 900-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
            CLOSE BP13F230                                                      
                  BP13K240                                                      
                  BP13F240.                                                     
                                                                                
            IF BP13K240-STATUS NOT = 00                                         
               DISPLAY 'BP13K240-STATUS ' BP13K240-STATUS                       
               MOVE BP13K240-STATUS     TO RETURN-CODE                          
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
