       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C111.                                                 
       AUTHOR.        KEVIN V. SANTOS                                           
       DATE-WRITTEN.  13/07/93.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
      **************************************************************            
      *                                                            *            
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                      *            
      *                                                            *            
      *    SYSTEM ID   : BP13                                      *            
      *                                                            *            
      *    OBJECTIVE   :                                           *            
      *                  THIS PROGRAMME WILL MATCH CPF PAYMENT     *            
      *                  FILE WITH SOC OCCUPIER FILE TO GET THE    *            
      *                  NRIC FOR BP13C101 PROGRAM.                *            
      *                                                            *            
      *     INPUT FILES:                                           *            
      *                  1. AB02F556 (CPF PAYMENT FILE)            *            
      *                  2. BP13F820 (SOC OCCUPIER FILE)           *            
      *                                                            *            
      *     OUTPUT FILES:                                          *            
      *                  1. BP13F103 (NRIC FILE)                   *            
      *                                                            *            
      *     LISTING:     NIL                                       *            
      *                                                            *            
      *                                                            *            
      *  CHG-NO   BY    DATE         DESCRIPTION                   *            
      *  ------   ---   --------     ----------------------------  *            
      * SOCPH8F  LJL  22/08/1995     TO INCLUDE SUSPENSE ACCOUNT   *            
      *                              FOR SUB-TRANSFER MODULE.      *            
      * BP130523 RHB  22/08/1998     TO CATER FOR CPFB Y2000.      *            
      * BP130418 NOR  20/08/1998     MAKE PRGM Y2K COMPLIANT       *            
      * BP132838 LSB  23/01/2007     CATER FOR MULTIPLE NRIC MATCH *            
      * BP135093 IMC  19/12/2013     CHANGE B04F056 TO AB02F556    *            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820 ASSIGN       TO BP13F820.                            
                                                                                
           SELECT AB02F556 ASSIGN       TO AB02F556.                            
                                                                                
           SELECT BP13F103 ASSIGN       TO BP13F103.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
      *FD  AB02F556                                                             
      *    RECORD CONTAINS 100 CHARACTERS                                       
      *    RECORDING MODE  IS  F.                                               
       COPY AB02F556.                                                           
                                                                                
       FD  BP13F820                                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE  IS  F.                                               
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F103                                                             
           RECORD CONTAINS 120 CHARACTERS                                       
           RECORDING MODE  IS  F.                                               
       COPY BP13F103.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-AB02F556-READ           PIC 9(5)       VALUE ZEROS.           
           05  WS-BP13F820-READ           PIC 9(5)       VALUE ZEROS.           
           05  WS-BP13F103-WRITE          PIC 9(5)       VALUE ZEROS.           
           05  WS-MATCHED-FD              PIC X(1)       VALUE SPACES.          
           05  WS-MATCHED                 PIC 9(5)       VALUE ZEROS.           
           05  WS-UNMATCHED               PIC 9(5)       VALUE ZEROS.           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN PROCESS                                            *        
      ******************************************************************        
       MAIN.                                                                    
                                                                                
            PERFORM 100-OPEN-FILE           THRU 100-EXIT.                      
            PERFORM 200-READ-AB02F556       THRU 200-EXIT.                      
            PERFORM 300-READ-BP13F820       THRU 300-EXIT.                      
            PERFORM 400-PROCESS-MATCHING    THRU 400-EXIT                       
                        UNTIL F556-NUM-CPF = HIGH-VALUES                        
                        AND   K820-NUM-CPF     = HIGH-VALUES.                   
            PERFORM 800-CLOSE-ROUTINE.                                          
                                                                                
       MAIN-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
      *    OPENING FILES SECTION                                      *         
      *****************************************************************         
       100-OPEN-FILE.                                                           
                                                                                
           OPEN INPUT  AB02F556                                                 
                       BP13F820                                                 
                OUTPUT BP13F103.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    200-READ-AB02F556                                           *        
      ******************************************************************        
       200-READ-AB02F556.                                                       
                                                                                
           READ AB02F556 AT END                                                 
                MOVE HIGH-VALUES TO F556-NUM-CPF                                
                                    K820-NUM-CPF                                
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-AB02F556-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    300-READ-BP13F820                                           *        
      ******************************************************************        
       300-READ-BP13F820.                                                       
                                                                                
           READ BP13F820 AT END                                                 
                MOVE HIGH-VALUES TO K820-NUM-CPF                                
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-BP13F820-READ.                                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    400-PROCESS-MATCHING                                       *         
      *****************************************************************         
       400-PROCESS-MATCHING.                                                    
                                                                                
           MOVE SPACES TO  WS-MATCHED-FD.                                       
           IF F556-NUM-CPF < K820-NUM-CPF                                       
              ADD  1                      TO WS-UNMATCHED                       
              PERFORM 500-PROCESS-MATCHED THRU 500-EXIT                         
              PERFORM 200-READ-AB02F556   THRU 200-EXIT                         
           ELSE                                                                 
           IF F556-NUM-CPF > K820-NUM-CPF                                       
              PERFORM 300-READ-BP13F820   THRU 300-EXIT                         
           ELSE                                                                 
           IF F556-NUM-CPF = K820-NUM-CPF                                       
              MOVE 'Y' TO  WS-MATCHED-FD                                        
              ADD  1                      TO WS-MATCHED                         
              PERFORM 450-HANDLE-MULTIPLE-NRIC THRU 450-EXIT                    
                    UNTIL F556-NUM-CPF NOT = K820-NUM-CPF                       
                    OR F556-NUM-CPF = HIGH-VALUE                                
              PERFORM 300-READ-BP13F820   THRU 300-EXIT.                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-HANDLE-MULTIPLE-NRIC.                                                
                                                                                
              PERFORM 500-PROCESS-MATCHED THRU 500-EXIT.                        
                                                                                
              PERFORM 200-READ-AB02F556   THRU 200-EXIT.                        
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************************           
      *     PROCESS MATCHED RECORDS.                                *           
      ***************************************************************           
       500-PROCESS-MATCHED.                                                     
                                                                                
           MOVE SPACES TO BP13F103-NCNTRAN.                                     
           INITIALIZE     BP13F103-NCNTRAN.                                     
                                                                                
           MOVE AB02F556-DET-REC TO BP13F103-NCNTRAN.                           
           IF  WS-MATCHED-FD = 'Y'                                              
               MOVE K820-NUM-NRIC TO F103-NUM-NRIC                              
           ELSE                                                                 
               MOVE SPACES        TO F103-NUM-NRIC.                             
                                                                                
           DISPLAY '* PROC-DATE : ' F103-PROC-DATE.                             
                                                                                
           WRITE BP13F103-NCNTRAN.                                              
           ADD 1 TO WS-BP13F103-WRITE.                                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************************           
      *     CLOSE ALL FILES AND STOP RUN.                           *           
      ***************************************************************           
       800-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'PROGRAM ID: BP13C111'.                                      
           DISPLAY '********************'.                                      
           DISPLAY 'NO OF F101 RECS READ   : ' WS-AB02F556-READ.                
           DISPLAY 'NO OF F820 RECS READ   : ' WS-BP13F820-READ.                
           DISPLAY 'NO OF F103 RECS WRITE  : ' WS-BP13F103-WRITE.               
           DISPLAY 'NO OF RECS MATCHED     : ' WS-MATCHED.                      
           DISPLAY 'NO OF RECS UNMATCHED   : ' WS-UNMATCHED.                    
           DISPLAY '********************'.                                      
                                                                                
           CLOSE AB02F556                                                       
                 BP13F820                                                       
                 BP13F103.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
