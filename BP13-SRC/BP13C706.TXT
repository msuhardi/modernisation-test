       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C706.                                                  
      *AUTHOR.       YPH.                                                       
      *DATE-WRITTEN. 15.07.93.                                                  
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : TO UPDATE REQUEST RECORDS AS OPTION,      *              
      *                REPEAT OR NEW REQUESTS.                   *              
      *                                                          *              
      *  BP13F520    : BALLOT REQUEST FILE                       *              
      *  BP13F558    : UPGRADER INFORMATION                      *              
      *  BP13F706    : MATCHED REQUEST RECORDS WITH HISTORY      *              
      *                                                          *              
      * CHG-NO    BY   ON       DESCRIPTION                      *              
      * ----------------------------------                       *              
      * C930457   YPH 150793                                     *              
      * BP13C0445 SSS 19980205  EXPAND F558 RECL FROM 150 TO 400 *              
      * BP130652  NOR 19990220  EXPAND F520 RECL FROM 300 TO 500 *              
      * BP130897  AKT 20000804  EXPAND F558 RECL FROM 400 TO 600 *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT BP13F706 ASSIGN TO BP13F706.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13OUT  ASSIGN TO BP13OUT.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500  CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F558.                                                           
                                                                                
       FD  BP13F706                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 50  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F706.                                                           
                                                                                
       FD  BP13OUT    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       01  BP13OUT-REC                     PIC X(500).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F520-CNT             PIC 9(8)    VALUE ZERO.                      
       01  WS-WRITE-CNT            PIC 9(8)    VALUE ZERO.                      
       01  WS-CNT-REPEAT           PIC 9(8)    VALUE ZERO.                      
       01  WS-CNT-NEW              PIC 9(8)    VALUE ZERO.                      
       01  WS-CNT-OPTION           PIC 9(8)    VALUE ZERO.                      
       01  WS-F706-CNT             PIC 9(8)    VALUE ZERO.                      
       01  WS-EOF-F520             PIC X(1).                                    
           88  EOF-OF-F520                     VALUE 'Y'.                       
       01  WS-EOF-F706             PIC X(1).                                    
           88  EOF-OF-F706                     VALUE 'Y'.                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION  THRU 20-EXIT.                             
           PERFORM 200-READ-F706      THRU 200-EXIT.                            
           PERFORM 300-READ-F520      THRU 300-EXIT.                            
           PERFORM 350-READ-F558      THRU 350-EXIT.                            
                                                                                
           PERFORM 400-MATCH-ROUTINE                                            
              THRU 400-EXIT                                                     
             UNTIL EOF-OF-F706 AND EOF-OF-F520.                                 
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13F520                                                 
                       BP13F558                                                 
                       BP13F706                                                 
                OUTPUT BP13OUT.                                                 
                                                                                
           MOVE ZEROS  TO WS-F520-CNT                                           
                          WS-WRITE-CNT                                          
                          WS-CNT-NEW                                            
                          WS-CNT-OPTION                                         
                          WS-CNT-REPEAT                                         
                          WS-F706-CNT.                                          
           MOVE 'N'    TO WS-EOF-F520                                           
                          WS-EOF-F706.                                          
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
       200-READ-F706.                                                           
            READ BP13F706                                                       
               AT END MOVE 'Y' TO WS-EOF-F706                                   
               MOVE HIGH-VALUE TO F706-NUM-NEW-REF                              
               GO TO 200-EXIT.                                                  
                                                                                
           ADD 1 TO WS-F706-CNT.                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F520.                                                           
           READ BP13F520                                                        
                AT END MOVE 'Y' TO WS-EOF-F520                                  
                MOVE HIGH-VALUE TO F520-NUM-REF                                 
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F520-CNT.                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-READ-F558.                                                           
           READ BP13F558                                                        
                AT END  MOVE HIGH-VALUE TO F558-NUM-REF                         
                GO TO 350-EXIT.                                                 
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      * MATCH THE TWO FILES BY REF-NO                                 *         
      *****************************************************************         
       400-MATCH-ROUTINE.                                                       
           IF EOF-OF-F706                                                       
              PERFORM 500-WRITE-OUT    THRU 500-EXIT                            
              PERFORM 300-READ-F520    THRU 300-EXIT                            
                                                                                
           ELSE                                                                 
           IF F706-CDE-1ST-REQUEST = 'Y'                                        
              PERFORM 200-READ-F706    THRU 200-EXIT                            
                                                                                
           ELSE                                                                 
           IF F706-NUM-NEW-REF     = F520-NUM-REF                               
              PERFORM 500-WRITE-OUT    THRU 500-EXIT                            
              PERFORM 300-READ-F520    THRU 300-EXIT                            
              PERFORM 200-READ-F706    THRU 200-EXIT                            
                                                                                
           ELSE                                                                 
           IF F706-NUM-NEW-REF     > F520-NUM-REF                               
              PERFORM 500-WRITE-OUT    THRU 500-EXIT                            
              PERFORM 300-READ-F520    THRU 300-EXIT                            
                                                                                
           ELSE                                                                 
              PERFORM 200-READ-F706    THRU 200-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-OUT.                                                           
           IF F520-NUM-PREV-REF(1:1) NOT = SPACES AND LOW-VALUES                
              ADD   1            TO WS-CNT-OPTION                               
              MOVE 'O'           TO F520-NUM-PREV-REF(1:1)                      
                                                                                
           ELSE                                                                 
           IF F706-NUM-NEW-REF = F520-NUM-REF                                   
              ADD   1            TO WS-CNT-REPEAT                               
              MOVE 'R'           TO F520-NUM-PREV-REF(1:1)                      
                                                                                
           ELSE                                                                 
              ADD   1            TO WS-CNT-NEW                                  
              MOVE 'W'           TO F520-NUM-PREV-REF(1:1).                     
                                                                                
           IF F520-CDE-HOUSEHOLD = 'G'                                          
              PERFORM 600-DIFF-UPG                                              
                 THRU 600-EXIT                                                  
                UNTIL F520-NUM-REF < F558-NUM-REF.                              
                                                                                
           MOVE BP13F520-REC     TO BP13OUT-REC.                                
           WRITE BP13OUT-REC.                                                   
                                                                                
           ADD 1                 TO WS-WRITE-CNT.                               
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-DIFF-UPG.                                                            
           IF F558-NUM-REF         = F520-NUM-REF                               
              MOVE F558-CDE-FILE TO F520-CDE-PRINT-STATUS.                      
                                                                                
           PERFORM 350-READ-F558       THRU 350-EXIT.                           
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
           SUBTRACT 1 FROM WS-F520-CNT.                                         
           DISPLAY '  '.                                                        
           DISPLAY '***** SUMMARY REPORT FOR BP13C706 *****'.                   
           DISPLAY 'TOTAL NO OF REC READ FROM BP13F520 = '                      
                   WS-F520-CNT.                                                 
           DISPLAY 'TOTAL NO OF REC READ FROM BP13F706 = '                      
                   WS-F706-CNT.                                                 
           DISPLAY 'TOTAL NO OF RECORDS OUTPUT         = '                      
                   WS-WRITE-CNT.                                                
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL NO OF OPTION RECORDS         = '                      
                   WS-CNT-OPTION.                                               
           DISPLAY 'TOTAL NO OF REPEAT RECORDS         = '                      
                   WS-CNT-REPEAT.                                               
           DISPLAY 'TOTAL NO OF NEW    RECORDS         = '                      
                   WS-CNT-NEW.                                                  
                                                                                
           CLOSE      BP13F520                                                  
                      BP13F558                                                  
                      BP13F706                                                  
                      BP13OUT.                                                  
                                                                                
           STOP RUN.                                                            
       900-EXIT.                                                                
           EXIT.                                                                
