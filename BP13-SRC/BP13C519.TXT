       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C519.                                                 
      *AUTHOR.        ANC1.                                                     
      *DATE-WRITTEN.  31/07/2003.                                               
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE:  TO CAPTURE EXISTING/EX-DP FLAT TYPE     *                
      *                MATCH REGN NO FROM BP13F716 TO BP13F520 *                
      *                IF MATCH,WRITE SMS/RSL/RDB OM/DP FLAT IN*                
      *                P13F520A FILE.                          *                
      *========================================================*                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP132384 ANC1 310703 NEW PROGRAM                       *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716   ASSIGN       TO BP13F716.                          
           SELECT BP13F520   ASSIGN       TO BP13F520.                          
           SELECT P13F520A   ASSIGN       TO P13F520A.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   P13F520A                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       01   P13F520A-REC                    PIC X(500).                         
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-MATCH             PIC 9(08)    VALUE ZEROES.                   
           05 WS-BYPASS            PIC 9(08)    VALUE ZEROES.                   
           05 WS-F520-READ         PIC 9(08)    VALUE ZEROES.                   
           05 WS-F716-READ         PIC 9(08)    VALUE ZEROES.                   
           05 WS-P13F520A-WRITE    PIC 9(08)    VALUE ZEROES.                   
           05 WS-F716-REGN         PIC X(08)    VALUE SPACES.                   
           05 WS-SUB               PIC 9(02)    VALUE ZEROES.                   
           05 WS-FLAT-TYPE         PIC X(02)    VALUE SPACES.                   
           05 WS-NUM-RDB-DP-OM     PIC X(01)    VALUE SPACES.                   
           05 WS-NUM-RDB-STATUS    PIC X(01)    VALUE SPACES.                   
           05 WS-NUM-RDB-FT        PIC X(02)    VALUE SPACES.                   
           05 WS-NUM-RSL-STATUS    PIC X(01)    VALUE SPACES.                   
           05 WS-NUM-RSL-FT        PIC X(02)    VALUE SPACES.                   
           05 WS-NUM-SMS-STATUS    PIC X(01)    VALUE SPACES.                   
           05 WS-NUM-SMS-FT        PIC X(02)    VALUE SPACES.                   
           05 WS-DTE-APPROVAL-RSL  PIC X(08)    VALUE SPACES.                   
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-YY                PIC 9(02).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
       01  WS-CURR-DATE            PIC X(10).                                   
       01  WS-CURR-CC              PIC 9(02).                                   
                                                                                
      *================================================================*        
       PROCEDURE DIVISION.                                                      
      *================================================================*        
                                                                                
      *-----------*                                                             
       000-CONTROL.                                                             
      *-----------*                                                             
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-F520     THRU 200-EXIT.                             
           PERFORM 300-READ-F716     THRU 300-EXIT.                             
           PERFORM 400-PROCESS-REC   THRU 400-EXIT                              
                   UNTIL F520-NUM-REGN = HIGH-VALUES AND                        
                         F716-NUM-REGN = HIGH-VALUES.                           
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
                                                                                
           OPEN INPUT  BP13F716                                                 
                       BP13F520                                                 
               OUTPUT  P13F520A.                                                
                                                                                
             ACCEPT WS-SYS-DATE  FROM DATE.                                     
                                                                                
             IF WS-YY > '90'                                                    
                MOVE  '19'  TO WS-CURR-CC                                       
             ELSE                                                               
                MOVE  '20'  TO WS-CURR-CC                                       
             END-IF.                                                            
                                                                                
             STRING WS-DD '/'                                                   
                    WS-MM '/'                                                   
                    WS-CURR-CC                                                  
                    WS-YY DELIMITED BY SIZE INTO WS-CURR-DATE.                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-F520.                                                           
      *-----------------*                                                       
           READ BP13F520 AT END                                                 
                MOVE HIGH-VALUES TO F520-NUM-REGN                               
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F520-READ.                                               
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-F716.                                                           
      *-----------------*                                                       
           READ BP13F716 AT END                                                 
                MOVE HIGH-VALUES TO F716-NUM-REGN                               
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F716-READ.                                               
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       400-PROCESS-REC.                                                         
      *-----------------*                                                       
                                                                                
           MOVE F716-NUM-REGN              TO WS-F716-REGN.                     
                                                                                
           IF F520-NUM-REGN =  F716-NUM-REGN                                    
              ADD        1                 TO WS-MATCH                          
              MOVE SPACES                  TO P13F520A-REC                      
              INITIALIZE                      P13F520A-REC                      
              PERFORM 500-GET-FLAT-INFO    THRU 500-EXIT                        
              WRITE P13F520A-REC           FROM BP13F520-REC                    
              ADD     1                    TO WS-P13F520A-WRITE                 
              PERFORM 200-READ-F520        THRU 200-EXIT                        
           ELSE                                                                 
              IF F520-NUM-REGN < F716-NUM-REGN                                  
                 WRITE P13F520A-REC        FROM BP13F520-REC                    
                 ADD  1                    TO WS-P13F520A-WRITE                 
                 PERFORM 200-READ-F520     THRU 200-EXIT                        
              ELSE                                                              
                 ADD  1                    TO WS-BYPASS                         
                 PERFORM 300-READ-F716     THRU 300-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       500-GET-FLAT-INFO.                                                       
      *-----------------*                                                       
           MOVE SPACES        TO  WS-FLAT-TYPE                                  
                                  WS-DTE-APPROVAL-RSL                           
                                  WS-NUM-RDB-DP-OM                              
                                  WS-NUM-RDB-STATUS                             
                                  WS-NUM-RDB-FT                                 
                                  WS-NUM-RSL-STATUS                             
                                  WS-NUM-RSL-FT                                 
                                  WS-NUM-SMS-STATUS                             
                                  WS-NUM-SMS-FT.                                
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1                                   
                   UNTIL F716-NUM-REGN NOT = WS-F716-REGN                       
                   OR F716-NUM-REGN = HIGH-VALUES                               
                                                                                
                 IF F716-NUM-NRIC = F520-NUM-NRIC1 OR                           
                    F716-NUM-NRIC = F520-NUM-NRIC2 OR                           
                    F716-NUM-NRIC = F520-NUM-NRIC3 OR                           
                    F716-NUM-NRIC = F520-NUM-NRIC4                              
                    PERFORM 510-CHECK-RDB-FOR-TENANT THRU 510-EXIT              
                 END-IF                                                         
                                                                                
                                                                                
                 IF F716-NUM-SMS-STATUS NOT = SPACES AND LOW-VALUES             
                    MOVE F716-NUM-SMS-STATUS  TO F520-NUM-SMS-STATUS            
                    IF F716-NUM-SMS-STATUS = WS-NUM-SMS-STATUS                  
                       IF F716-NUM-SMS-FT > F520-NUM-SMS-FT                     
                          MOVE F716-NUM-SMS-FT   TO F520-NUM-SMS-FT             
                       END-IF                                                   
                    ELSE                                                        
                       IF WS-NUM-SMS-FT = SPACES OR LOW-VALUES                  
                          MOVE F716-NUM-SMS-STATUS TO                           
                                                  F520-NUM-SMS-STATUS           
                          MOVE F716-NUM-SMS-FT      TO F520-NUM-SMS-FT          
                       ELSE                                                     
                          IF F716-NUM-SMS-STATUS = 'D'                          
                             MOVE F716-NUM-SMS-STATUS TO                        
                                                  F520-NUM-SMS-STATUS           
                             MOVE F716-NUM-SMS-FT   TO F520-NUM-SMS-FT          
                          ELSE                                                  
                             MOVE WS-NUM-SMS-STATUS TO                          
                                                  F520-NUM-SMS-STATUS           
                             MOVE WS-NUM-SMS-FT     TO F520-NUM-SMS-FT          
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                    MOVE F716-NUM-SMS-STATUS TO WS-NUM-SMS-STATUS               
                    MOVE F716-NUM-SMS-FT     TO WS-NUM-SMS-FT                   
                 END-IF                                                         
                                                                                
                 IF F716-NUM-RSL-STATUS NOT = SPACES AND LOW-VALUES             
                    MOVE F716-NUM-RSL-STATUS  TO F520-NUM-RSL-STATUS            
                    IF F716-NUM-RSL-STATUS = WS-NUM-RSL-STATUS                  
                       IF F716-DTE-APPROVAL-RSL = SPACES OR LOW-VALUES          
                          IF F716-NUM-RSL-FT > F520-NUM-RSL-FT                  
                             MOVE F716-NUM-RSL-FT TO F520-NUM-RSL-FT            
                          END-IF                                                
                       ELSE                                                     
                          IF F716-DTE-APPROVAL-RSL >                            
                                            WS-DTE-APPROVAL-RSL                 
                             MOVE F716-DTE-APPROVAL-RSL TO                      
                                              WS-DTE-APPROVAL-RSL               
                             MOVE F716-NUM-RSL-FT   TO F520-NUM-RSL-FT          
                          END-IF                                                
                       END-IF                                                   
                    ELSE                                                        
                       IF WS-NUM-RSL-FT = SPACES OR LOW-VALUES                  
                          MOVE F716-NUM-RSL-STATUS TO                           
                                                  F520-NUM-RSL-STATUS           
                          MOVE F716-NUM-RSL-FT     TO F520-NUM-RSL-FT           
                                                                                
                       ELSE                                                     
                          IF F716-NUM-RSL-STATUS = 'D'                          
                             MOVE F716-NUM-RSL-STATUS TO                        
                                                 F520-NUM-RSL-STATUS            
                             MOVE F716-NUM-RSL-FT TO F520-NUM-RSL-FT            
                          ELSE                                                  
                             MOVE WS-NUM-RSL-STATUS TO                          
                                                 F520-NUM-RSL-STATUS            
                             MOVE WS-NUM-RSL-FT TO F520-NUM-RSL-FT              
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                    MOVE F716-NUM-RSL-STATUS TO WS-NUM-RSL-STATUS               
                    MOVE F716-NUM-RSL-FT     TO WS-NUM-RSL-FT                   
                    MOVE F716-DTE-APPROVAL-RSL TO WS-DTE-APPROVAL-RSL           
                 END-IF                                                         
              PERFORM 300-READ-F716        THRU 300-EXIT                        
           END-PERFORM.                                                         
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-CHECK-RDB-FOR-TENANT.                                                
                                                                                
           IF (F716-CDE-OCCUPIER = '0' OR '1') AND                              
              (F716-NUM-HDB-REF(1:4) <= "6999")                                 
              MOVE 'T'      TO F520-NUM-RDB-STATUS                              
              MOVE F716-CDE-FLAT-TYPE-RESIDE                                    
                            TO F520-NUM-RDB-FT                                  
           ELSE                                                                 
              IF F716-CDE-OCCUPIER = '2'                                        
                 MOVE 'O'   TO F520-NUM-RDB-STATUS                              
                 MOVE F716-CDE-FLAT-TYPE-RESIDE                                 
                            TO F520-NUM-RDB-FT                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F520-NUM-RDB-STATUS = 'T' OR 'O'                                  
              IF F520-NUM-RDB-STATUS = WS-NUM-RDB-STATUS                        
                 IF F716-CDE-FLAT-TYPE-RESIDE >                                 
                    F520-NUM-RDB-FT                                             
                    MOVE F716-CDE-FLAT-TYPE-RESIDE                              
                                  TO F520-NUM-RDB-FT                            
                 END-IF                                                         
              ELSE                                                              
                 IF F520-NUM-RDB-STATUS = 'T'                                   
                    MOVE F716-CDE-FLAT-TYPE-RESIDE                              
                                  TO F520-NUM-RDB-FT                            
                 ELSE                                                           
                    IF WS-NUM-RDB-STATUS = 'T'                                  
                       MOVE WS-NUM-RDB-STATUS                                   
                                          TO F520-NUM-RDB-STATUS                
                       MOVE WS-NUM-RDB-FT TO F520-NUM-RDB-FT                    
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F520-NUM-RDB-STATUS  TO WS-NUM-RDB-STATUS.                      
           MOVE F716-CDE-FLAT-TYPE-RESIDE TO WS-NUM-RDB-FT.                     
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           DISPLAY '              PROGRAM-ID :  BP13C519'.                      
           DISPLAY '              ~~~~~~~~~~             '.                     
           DISPLAY '                                  '                         
                                       'CURRENT-DATE : ' WS-CURR-DATE.          
           DISPLAY ' '.                                                         
           DISPLAY '1. NO OF RECS READ FROM F716      : ' WS-F716-READ.         
           DISPLAY '2. NO OF RECS READ FROM F520      : ' WS-F520-READ.         
           DISPLAY '3. NO OF RECS MATCHED             : ' WS-MATCH.             
           DISPLAY '4. NO OF RECS BYPASS              : ' WS-BYPASS.            
           DISPLAY '5. NO OF RECS WRITTEN TO P13F520A : '                       
                                                     WS-P13F520A-WRITE.         
           DISPLAY '  '                                                         
                                                                                
           CLOSE BP13F716                                                       
                 BP13F520                                                       
                 P13F520A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------- END OF PROGRAM  ---------------------*              
                                                                                
                                                                                
