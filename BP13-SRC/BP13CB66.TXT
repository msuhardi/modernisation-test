       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB66.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  17/01/2018.                                               
      * ============================================================= *         
      *                  SYSTEM OF COMMITMENT (SOC)                   *         
      * ============================================================= *         
      *                                                               *         
      * OBJECTIVE   : GENERATE SHORTLISTING REPORTS TO BE USED FOR    *         
      *               CLEARANCE OF BALLOT RESULTS (SBF 3R & ABOVE)    *         
      *                                                               *         
      * INPUT FILES : BP13F740 - REQUEST FILE (ALL)                   *         
      *               BP13F730 - REQUEST FILE (SHORTLISTED IN APPZ)   *         
      *               BP13K010 - TENDER FILE (ACTUAL)                 *         
      *               BP13K030 - BALANCE FLAT FILE (BEFORE ADJUSTMENT)*         
      *               BP13K712 - REQUEST AND FLAT INFO FILE (ACTUAL)  *         
      *               BP13K813 - BTO/SBF SITE INFO FILE               *         
      * OUTPUT FILE : BP13LB66 - REPORT GENERATED                     *         
      *                                                               *         
      * ============================================================= *         
      * CHG-REF#  DATE      BY    DESCRIPTIONS                        *         
      * --------  --------  ----  ----------------------------------- *         
      * BP137173  17012018  SMR2  NEW PROGRAM                         *         
      * BP137455  17092018  FNP1  ADD NEW OUTPUT FILE BP13FW3I        *         
      * BP138214  25032020  AM25  ADD NEW DELIMITER FIELDS IN FW3I    *         
      * ============================================================= *         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN       TO BP13F740.                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K010 ASSIGN       TO BP13K010                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K010-KEY-FLD                         
                           FILE STATUS  IS WS-K010-STATUS.                      
                                                                                
           SELECT BP13K030 ASSIGN       TO BP13K030                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K030-KEY-FLD                         
                           FILE STATUS  IS WS-K030-STATUS.                      
                                                                                
           SELECT BP13K712 ASSIGN       TO BP13K712                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K712-KEY-FLD                         
                           FILE STATUS  IS WS-K712-STATUS.                      
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
           SELECT BP13LB66 ASSIGN       TO BP13LB66.                            
           SELECT BP13FW3I ASSIGN       TO BP13FW3I.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K010                                                            
            RECORD CONTAINS  600 CHARACTERS.                                    
       COPY BP13K010.                                                           
                                                                                
       FD   BP13K030                                                            
            RECORD CONTAINS  600 CHARACTERS.                                    
       COPY BP13K030.                                                           
                                                                                
       FD   BP13K712                                                            
            RECORD CONTAINS  350 CHARACTERS.                                    
       COPY BP13K712.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13LB66                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13LB66-REC                 PIC X(132).                            
                                                                                
       FD   BP13FW3I                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  4500 CHARACTERS.                                   
       COPY BP13FW3I.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K010-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-K030-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-K712-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-K813-STATUS            PIC 9(02) VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF               PIC X(01) VALUE 'N'.                   
           05  WS-F730-EOF               PIC X(01) VALUE 'N'.                   
           05  WS-LB66-HDR               PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-TIME.                                                         
               10  WS-TIME-HH            PIC 9(02) VALUE 0.                     
               10  WS-TIME-MM            PIC 9(02) VALUE 0.                     
               10  WS-TIME-SS            PIC 9(02) VALUE 0.                     
           05  WS-F740-READ              PIC 9(06) VALUE ZEROES.                
           05  WS-CNT-PAGE               PIC 9(04) VALUE ZEROES.                
           05  WS-CNT                    PIC 9(01) VALUE ZEROES.                
           05  WS-XCNT                   PIC 9(01) VALUE ZEROES.                
           05  WS-MAT-EST                PIC X(19) VALUE SPACES.                
           05  WS-PARM-NUM-CDE           PIC X(03).                             
           05  WS-FW3I-WRITE             PIC 9(07) VALUE ZEROES.                
           05  WS-SALES-MODE             PIC X(03) VALUE SPACES.                
                                                                                
       01  WS-TEMP-VAR.                                                         
           05  WS-F740-KEY-FLD.                                                 
               10  WS-F740-NT            PIC X(03).                             
               10  WS-F740-FT            PIC X(02).                             
           05  WS-F730-KEY-FLD.                                                 
               10  WS-F730-NT            PIC X(03).                             
               10  WS-F730-FT            PIC X(02).                             
           05  WS-PREV-KEY-FLD.                                                 
               10  WS-PREV-NT            PIC X(03).                             
               10  WS-PREV-FT            PIC X(02).                             
           05  WS-PREV-DTE-BAL           PIC X(06).                             
                                                                                
       01  WS-COUNT-VAR.                                                        
           05  WS-FLAT-SUPPLY            PIC 9(05) VALUE ZEROES.                
           05  WS-PRIO-CCC               PIC 9(05) VALUE ZEROES.                
           05  WS-PRIO-REN               PIC 9(05) VALUE ZEROES.                
           05  WS-PRIO-JB                PIC 9(05) VALUE ZEROES.                
           05  WS-PRIO-TCP               PIC 9(05) VALUE ZEROES.                
           05  WS-PRIO-SHORTLIST         PIC 9(05) VALUE ZEROES.                
           05  WS-FLAT-SUPPLY-NOPRIO     PIC 9(05) VALUE ZEROES.                
           05  WS-F740-CAT1              PIC 9(05) VALUE ZEROES.                
           05  WS-F740-CAT2              PIC 9(05) VALUE ZEROES.                
           05  WS-F740-CAT3              PIC 9(05) VALUE ZEROES.                
           05  WS-F740-TOTAL             PIC 9(05) VALUE ZEROES.                
           05  WS-K030-CAT1              PIC 9(05) VALUE ZEROES.                
           05  WS-K030-CAT2              PIC 9(05) VALUE ZEROES.                
           05  WS-K030-CAT3              PIC 9(05) VALUE ZEROES.                
           05  WS-K030-TOTAL             PIC 9(05) VALUE ZEROES.                
           05  WS-ACT-REQ-TOT            PIC 9(05) VALUE ZEROES.                
           05  WS-ACT-FLAT-TOT           PIC 9(05) VALUE ZEROES.                
           05  WS-ACT-REG-TOT            PIC 9(05) VALUE ZEROES.                
           05  WS-APPZ-NELD-NH           PIC 9(05) VALUE ZEROES.                
           05  WS-APPZ-NELD-UG           PIC 9(05) VALUE ZEROES.                
           05  WS-APPZ-NELD-TOT          PIC 9(05) VALUE ZEROES.                
           05  WS-SUCC-NELD-NH           PIC 9(05) VALUE ZEROES.                
           05  WS-SUCC-NELD-UG           PIC 9(05) VALUE ZEROES.                
           05  WS-SUCC-NELD-TOT          PIC 9(05) VALUE ZEROES.                
           05  WS-UNSUCC-NELD-NH         PIC 9(05) VALUE ZEROES.                
           05  WS-UNSUCC-NELD-UG         PIC 9(05) VALUE ZEROES.                
           05  WS-UNSUCC-NELD-TOT        PIC 9(05) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY                     PIC X(02).                             
           05  WS-MM                     PIC X(02).                             
           05  WS-DD                     PIC X(02).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(02).                             
                                                                                
      ***  DEMAND                                                               
       01  WS-SHORTLIST-TABLE.                                                  
           05  WS-SL-DESC             OCCURS 6 TIMES.                           
               10  WS-SL-DIV          OCCURS 3 TIMES.                           
                   15  WS-SL-QTY         PIC 9(5).                              
                                                                                
      ******************* REPORT LAYOUT *****************                       
                                                                                
       01  LB66-HEAD-1.                                                         
           05  FILLER                    PIC X(08) VALUE 'BP13LB66'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'HDBCAT3'.             
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'DATE : '.             
           05  LB66-DTE-CURRENT          PIC X(08).                             
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  LB66-TME-HH               PIC 9(02).                             
           05  FILLER                    PIC X(01) VALUE ':'.                   
           05  LB66-TME-MM               PIC 9(02).                             
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           05  LB66-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  LB66-HEAD-2.                                                         
           05  FILLER                    PIC X(41) VALUE SPACES.                
           05  LB66-MON                  PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  LB66-YEAR                 PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(31) VALUE                        
               ' SALE OF BALANCE FLATS EXERCISE'.                               
                                                                                
       01  LB66-HEAD-3.                                                         
           05  LB66-DESC                 PIC X(132) VALUE SPACES.               
                                                                                
       01  LB66-BLANK.                                                          
           05  FILLER                    PIC X(132) VALUE SPACES.               
                                                                                
      ***  REPORT A : FLAT SUPPLY                                               
       01  LB66A-LINE-1.                                                        
           05  FILLER                    PIC X(76)  VALUE ALL '='.              
                                                                                
       01  LB66A-LINE-2.                                                        
           05  FILLER                    PIC X(66)  VALUE ALL '-'.              
           05  FILLER                    PIC X(10)  VALUE                       
               '|---------'.                                                    
                                                                                
       01  LB66A-LINE-3.                                                        
           05  FILLER                    PIC X(39)  VALUE SPACES.               
           05  FILLER                    PIC X(37)  VALUE                       
               '|--------------------------|---------'.                         
                                                                                
       01  LB66A-DETAIL.                                                        
           05  FILLER                    PIC X(02)  VALUE SPACES.               
           05  LB66A-DESC                PIC X(63)  VALUE SPACES.               
           05  FILLER                    PIC X(04)  VALUE ' |  '.               
           05  LB66A-UNIT                PIC ZZZZ9  VALUE ZEROES.               
                                                                                
      ***  REPORT B : DEMAND                                                    
       01  LB66B-LINE-1.                                                        
           05  FILLER                    PIC X(68)  VALUE ALL '='.              
                                                                                
       01  LB66B-LINE-2.                                                        
           05  FILLER                    PIC X(44)  VALUE ALL '-'.              
           05  FILLER                    PIC X(24)  VALUE                       
               '|-------|-------|-------'.                                      
                                                                                
       01  LB66B-HEAD.                                                          
           05  FILLER                    PIC X(44)  VALUE SPACES.               
           05  FILLER                    PIC X(24)  VALUE                       
               '|   FT  |   ST  | TOTAL '.                                      
                                                                                
       01  LB66B-DETAIL.                                                        
           05  FILLER                    PIC X(02)  VALUE SPACES.               
           05  LB66B-DESC                PIC X(42)  VALUE SPACES.               
           05  LB66B-DIV              OCCURS 3 TIMES.                           
               10  FILLER                PIC X(02)  VALUE '| '.                 
               10  LB66B-QTY             PIC ZZZZ9  VALUE ZEROES.               
               10  FILLER                PIC X(01)  VALUE SPACES.               
                                                                                
      ***  REPORT C : ETHNIC QUOTA                                              
       01  LB66C-LINE-1.                                                        
           05  FILLER                    PIC X(81)  VALUE ALL '='.              
                                                                                
       01  LB66C-LINE-2.                                                        
           05  FILLER                    PIC X(54)  VALUE                       
               '-------------------------|-------------|-------------|'.        
           05  FILLER                    PIC X(27)  VALUE                       
               '-------------|-------------'.                                   
                                                                                
       01  LB66C-HEAD.                                                          
           05  FILLER                    PIC X(25)  VALUE SPACES.               
           05  FILLER                    PIC X(52)  VALUE                       
               '|    CAT 1    |    CAT 2    |  CAT 3 & 4  |    TOTAL'.          
                                                                                
       01  LB66C-DETAIL.                                                        
           05  FILLER                    PIC X(02)  VALUE SPACES.               
           05  LB66C-DESC                PIC X(22)  VALUE SPACES.               
           05  FILLER                    PIC X(06)  VALUE ' |    '.             
           05  LB66C-CAT1                PIC ZZZZ9.                             
           05  FILLER                    PIC X(09)  VALUE '    |    '.          
           05  LB66C-CAT2                PIC ZZZZ9.                             
           05  FILLER                    PIC X(09)  VALUE '    |    '.          
           05  LB66C-CAT3                PIC ZZZZ9.                             
           05  FILLER                    PIC X(09)  VALUE '    |    '.          
           05  LB66C-TOT                 PIC ZZZZ9.                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
           IF WS-F740-READ > ZEROES                                             
              PERFORM 2100-READ-BP13F730    THRU 2100-EXIT                      
           END-IF.                                                              
           PERFORM 2500-PROCESS-BP13F740    THRU 2500-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           IF WS-F740-READ > ZEROES                                             
              PERFORM 3000-PROCESS-RECORDS  THRU 3000-EXIT                      
           END-IF.                                                              
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13F730                                                 
                       BP13K010                                                 
                       BP13K030                                                 
                       BP13K712                                                 
                       BP13K813                                                 
                OUTPUT BP13LB66                                                 
                       BP13FW3I.                                                
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K030, STATUS ' WS-K030-STATUS          
              MOVE WS-K030-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K712-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K712, STATUS ' WS-K712-STATUS          
              MOVE WS-K712-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K813, STATUS ' WS-K813-STATUS          
              MOVE WS-K813-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           ACCEPT WS-DTE-CURRENT            FROM DATE.                          
           MOVE CORR WS-DTE-CURRENT         TO WS-DTE-TODATE.                   
           MOVE WS-DTE-TODATE               TO LB66-DTE-CURRENT.                
                                                                                
           ACCEPT WS-TIME                   FROM TIME.                          
           MOVE WS-TIME-HH                  TO LB66-TME-HH.                     
           MOVE WS-TIME-MM                  TO LB66-TME-MM.                     
                                                                                
           MOVE ZEROES                      TO WS-COUNT-VAR                     
                                               WS-SHORTLIST-TABLE.              
                                                                                
      *** ACCEPT VALUE FROM JCL ***                                             
           ACCEPT WS-PARM-NUM-CDE.                                              
                                                                                
           MOVE SPACES   TO BP13FW3I-REC.                                       
           INITIALIZE       BP13FW3I-REC.                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F740.                                                      
      *********************                                                     
                                                                                
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
                                                                                
           IF WS-F740-READ = 1                                                  
              MOVE F740-CDE-NT1             TO WS-PREV-NT                       
              MOVE F740-CDE-FLAT-TYPE       TO WS-PREV-FT                       
              MOVE F740-DTE-BALLOT          TO WS-PREV-DTE-BAL                  
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-NT1                TO WS-F740-NT.                      
           MOVE F740-CDE-FLAT-TYPE          TO WS-F740-FT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2100-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F730-EOF                      
                GO TO 2100-EXIT.                                                
                                                                                
           MOVE F730-CDE-NT1                TO WS-F730-NT.                      
           MOVE F730-CDE-FLAT-TYPE          TO WS-F730-FT.                      
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       2500-PROCESS-BP13F740.                                                   
      ************************                                                  
                                                                                
           IF WS-F740-KEY-FLD = WS-PREV-KEY-FLD                                 
              IF F740-CDE-CATEGORY = '1'                                        
                 ADD 1                      TO WS-F740-CAT1                     
              ELSE                                                              
                 IF F740-CDE-CATEGORY = '2'                                     
                    ADD 1                   TO WS-F740-CAT2                     
                 ELSE                                                           
                    IF F740-CDE-CATEGORY = '3' OR '4'                           
                       ADD 1                TO WS-F740-CAT3                     
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM 3000-PROCESS-RECORDS  THRU 3000-EXIT                      
              MOVE ZEROES                   TO WS-COUNT-VAR                     
                                               WS-SHORTLIST-TABLE               
              IF F740-CDE-CATEGORY = '1'                                        
                 ADD 1                      TO WS-F740-CAT1                     
              ELSE                                                              
                 IF F740-CDE-CATEGORY = '2'                                     
                    ADD 1                   TO WS-F740-CAT2                     
                 ELSE                                                           
                    IF F740-CDE-CATEGORY = '3' OR '4'                           
                       ADD 1                TO WS-F740-CAT3                     
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-NT1                TO WS-PREV-NT.                      
           MOVE F740-CDE-FLAT-TYPE          TO WS-PREV-FT.                      
           MOVE F740-DTE-BALLOT             TO WS-PREV-DTE-BAL.                 
           MOVE F740-NUM-ALLO-CAT           TO WS-SALES-MODE.                   
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       2600-PROCESS-BP13F730.                                                   
      ************************                                                  
                                                                                
           IF F730-NUM-ELDERLY = 'F' OR 'S'                                     
              CONTINUE                                                          
           ELSE                                                                 
              IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                  
                 ADD 1                      TO WS-APPZ-NELD-NH                  
                                               WS-APPZ-NELD-TOT                 
              ELSE                                                              
                 ADD 1                      TO WS-APPZ-NELD-UG                  
                                               WS-APPZ-NELD-TOT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F730       THRU 2100-EXIT.                     
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       3000-PROCESS-RECORDS.                                                    
      ***********************                                                   
                                                                                
           PERFORM 2600-PROCESS-BP13F730    THRU 2600-EXIT                      
             UNTIL WS-F730-KEY-FLD > WS-PREV-KEY-FLD                            
                OR WS-F730-EOF = 'Y'.                                           
           PERFORM 4000-READ-BP13K010       THRU 4000-EXIT.                     
           IF WS-PARM-NUM-CDE = '100'                                           
              PERFORM 4500-READ-BP13K030    THRU 4500-EXIT                      
           ELSE                                                                 
              PERFORM 4600-MOVE-QUOTA-AVAIL THRU 4600-EXIT                      
           END-IF.                                                              
           PERFORM 5000-READ-BP13K712       THRU 5000-EXIT.                     
           PERFORM 5500-READ-BP13K813       THRU 5500-EXIT.                     
           PERFORM 7000-SETUP-SL-TABLE      THRU 7000-EXIT.                     
           PERFORM 8000-WRITE-BP13LB66      THRU 8000-EXIT.                     
           PERFORM 8100-WRITE-BP13FW3I      THRU 8100-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K010.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                      TO BP13K010-TENDER.                 
           INITIALIZE                          BP13K010-TENDER.                 
                                                                                
           MOVE WS-PREV-KEY-FLD             TO K010-KEY-FLD.                    
                                                                                
           READ BP13K010.                                                       
           EVALUATE WS-K010-STATUS                                              
           WHEN ZEROES                                                          
                MOVE K010-NUM-TOT-UNIT      TO WS-FLAT-SUPPLY                   
                COMPUTE WS-PRIO-CCC       = K010-REG-CCC1 +                     
                                            K010-REG-CCC2 +                     
                                            K010-REG-CCC3                       
                COMPUTE WS-PRIO-REN       = K010-REG-REN1 +                     
                                            K010-REG-REN2 +                     
                                            K010-REG-REN3                       
                COMPUTE WS-PRIO-JB        = K010-REG-JB1  +                     
                                            K010-REG-JB2  +                     
                                            K010-REG-JB3                        
                COMPUTE WS-PRIO-TCP       = K010-REG-TCP1 +                     
                                            K010-REG-TCP2 +                     
                                            K010-REG-TCP3                       
                COMPUTE WS-PRIO-SHORTLIST = WS-PRIO-CCC   +                     
                                            WS-PRIO-REN   +                     
                                            WS-PRIO-JB    +                     
                                            WS-PRIO-TCP                         
                COMPUTE WS-FLAT-SUPPLY-NOPRIO = WS-FLAT-SUPPLY -                
                                                WS-PRIO-SHORTLIST               
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K010 - ' K010-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K010, STATUS ' WS-K010-STATUS        
                MOVE WS-K010-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4500-READ-BP13K030.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                      TO BP13K030-TENDER.                 
           INITIALIZE                          BP13K030-TENDER.                 
                                                                                
           MOVE WS-PREV-KEY-FLD             TO K030-KEY-FLD.                    
                                                                                
           READ BP13K030.                                                       
           EVALUATE WS-K030-STATUS                                              
           WHEN ZEROES                                                          
                COMPUTE WS-K030-CAT1 = K030-NUM-MAX-CCC1 +                      
                                       K030-NUM-MAX-REN1 +                      
                                       K030-NUM-MAX-JB1  +                      
                                       K030-NUM-MAX-PA1  +                      
                                       K030-NUM-MAX-TCP1                        
                COMPUTE WS-K030-CAT2 = K030-NUM-MAX-CCC2 +                      
                                       K030-NUM-MAX-REN2 +                      
                                       K030-NUM-MAX-JB2  +                      
                                       K030-NUM-MAX-PA2  +                      
                                       K030-NUM-MAX-TCP2                        
                COMPUTE WS-K030-CAT3 = K030-NUM-MAX-CCC3 +                      
                                       K030-NUM-MAX-REN3 +                      
                                       K030-NUM-MAX-JB3  +                      
                                       K030-NUM-MAX-PA3  +                      
                                       K030-NUM-MAX-TCP3                        
                MOVE K030-NUM-TOT-UNIT      TO WS-K030-TOTAL                    
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K030 - ' K030-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K030, STATUS ' WS-K030-STATUS        
                MOVE WS-K030-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       4600-MOVE-QUOTA-AVAIL.                                                   
      ************************                                                  
                                                                                
           COMPUTE WS-K030-CAT1 = K010-NUM-MAX-CCC1 +                           
                                  K010-NUM-MAX-REN1 +                           
                                  K010-NUM-MAX-JB1  +                           
                                  K010-NUM-MAX-PA1  +                           
                                  K010-NUM-MAX-TCP1.                            
           COMPUTE WS-K030-CAT2 = K010-NUM-MAX-CCC2 +                           
                                  K010-NUM-MAX-REN2 +                           
                                  K010-NUM-MAX-JB2  +                           
                                  K010-NUM-MAX-PA2  +                           
                                  K010-NUM-MAX-TCP2.                            
           COMPUTE WS-K030-CAT3 = K010-NUM-MAX-CCC3 +                           
                                  K010-NUM-MAX-REN3 +                           
                                  K010-NUM-MAX-JB3  +                           
                                  K010-NUM-MAX-PA3  +                           
                                  K010-NUM-MAX-TCP3.                            
           MOVE K010-NUM-TOT-UNIT           TO WS-K030-TOTAL.                   
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-READ-BP13K712.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                      TO BP13K712-REC.                    
           INITIALIZE                          BP13K712-REC.                    
                                                                                
           MOVE WS-PREV-NT                  TO K712-CDE-NT-ZONE.                
           MOVE WS-PREV-FT                  TO K712-CDE-FLAT-TY.                
           MOVE 'PA'                        TO K712-CDE-COHORT                  
                                               K712-CDE-ALLOC-SCH.              
                                                                                
           READ BP13K712.                                                       
           EVALUATE WS-K712-STATUS                                              
           WHEN ZEROES                                                          
                COMPUTE WS-ACT-REQ-TOT =                                        
                        K712-NUM-REQ-NH + K712-NUM-REQ-UG                       
                COMPUTE WS-ACT-FLAT-TOT =                                       
                        K712-NUM-FLAT-NH + K712-NUM-FLAT-UG                     
                COMPUTE WS-ACT-REG-TOT =                                        
                        K712-NUM-REG-NH + K712-NUM-REG-UG                       
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K712 - ' K712-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K712, STATUS ' WS-K712-STATUS        
                MOVE WS-K712-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5500-READ-BP13K813.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
                                                                                
           MOVE WS-PREV-NT                  TO K813-NUM-ZONE.                   
           MOVE WS-PREV-FT                  TO K813-NUM-FLAT-TYPE.              
           MOVE WS-PREV-DTE-BAL             TO K813-DTE-BALLOT.                 
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN ZEROES                                                          
                MOVE   K813-CDE-NT         TO FW3I-NUM-NT                       
                MOVE   K813-NUM-FLAT-TYPE  TO FW3I-NUM-FLAT-TYPE                
                MOVE   K813-NUM-ZONE       TO FW3I-NUM-BTO-ZONE                 
                                                                                
                MOVE FUNCTION UPPER-CASE(K813-NME-EST)                          
                                          TO FW3I-NME-NEW-TOWN                  
                MOVE FUNCTION UPPER-CASE(K813-NME-PROJECT-LTR)                  
                                          TO  FW3I-NME-LOCAL-PRCNCT             
                IF K813-NUM-GEN3-TAG = 'Y'                                      
                   MOVE K813-NUM-GEN3-TAG    TO  FW3I-NUM-GEN3-TAG              
                ELSE                                                            
                   MOVE 'N'                  TO  FW3I-NUM-GEN3-TAG              
                END-IF                                                          
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K813 - ' K813-KEY-FLD          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K813, STATUS ' WS-K813-STATUS        
                MOVE WS-K813-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7000-SETUP-SL-TABLE.                                                     
      **********************                                                    
                                                                                
           MOVE K712-NUM-REQ-NH             TO WS-SL-QTY ( 1 , 1 ).             
           MOVE K712-NUM-REQ-UG             TO WS-SL-QTY ( 1 , 2 ).             
           MOVE WS-ACT-REQ-TOT              TO WS-SL-QTY ( 1 , 3 ).             
                                                                                
           MOVE K712-NUM-FLAT-NH            TO WS-SL-QTY ( 2 , 1 ).             
           MOVE K712-NUM-FLAT-UG            TO WS-SL-QTY ( 2 , 2 ).             
           MOVE WS-ACT-FLAT-TOT             TO WS-SL-QTY ( 2 , 3 ).             
                                                                                
           MOVE K712-NUM-REG-NH             TO WS-SL-QTY ( 3 , 1 ).             
           MOVE K712-NUM-REG-UG             TO WS-SL-QTY ( 3 , 2 ).             
           MOVE WS-ACT-REG-TOT              TO WS-SL-QTY ( 3 , 3 ).             
                                                                                
           MOVE WS-APPZ-NELD-NH             TO WS-SL-QTY ( 4 , 1 ).             
           MOVE WS-APPZ-NELD-UG             TO WS-SL-QTY ( 4 , 2 ).             
           MOVE WS-APPZ-NELD-TOT            TO WS-SL-QTY ( 4 , 3 ).             
                                                                                
           COMPUTE WS-SUCC-NELD-NH  = K712-NUM-REG-NH +                         
                                      WS-APPZ-NELD-NH.                          
           COMPUTE WS-SUCC-NELD-UG  = K712-NUM-REG-UG +                         
                                      WS-APPZ-NELD-UG.                          
           COMPUTE WS-SUCC-NELD-TOT = WS-ACT-REG-TOT +                          
                                      WS-APPZ-NELD-TOT.                         
                                                                                
           MOVE WS-SUCC-NELD-NH             TO WS-SL-QTY ( 5 , 1 ).             
           MOVE WS-SUCC-NELD-UG             TO WS-SL-QTY ( 5 , 2 ).             
           MOVE WS-SUCC-NELD-TOT            TO WS-SL-QTY ( 5 , 3 ).             
                                                                                
           COMPUTE WS-UNSUCC-NELD-NH  = K712-NUM-REQ-NH -                       
                                        WS-SUCC-NELD-NH.                        
           COMPUTE WS-UNSUCC-NELD-UG  = K712-NUM-REQ-UG -                       
                                        WS-SUCC-NELD-UG.                        
           COMPUTE WS-UNSUCC-NELD-TOT = WS-ACT-REQ-TOT -                        
                                        WS-SUCC-NELD-TOT.                       
                                                                                
           MOVE WS-UNSUCC-NELD-NH           TO WS-SL-QTY ( 6 , 1 ).             
           MOVE WS-UNSUCC-NELD-UG           TO WS-SL-QTY ( 6 , 2 ).             
           MOVE WS-UNSUCC-NELD-TOT          TO WS-SL-QTY ( 6 , 3 ).             
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LB66.                                                     
      **********************                                                    
                                                                                
           IF WS-LB66-HDR = 'N'                                                 
              MOVE 'Y'                      TO WS-LB66-HDR                      
              ADD 1                         TO WS-CNT-PAGE                      
              MOVE WS-CNT-PAGE              TO LB66-CNT-PAGE                    
              WRITE BP13LB66-REC            FROM LB66-HEAD-1 AFTER PAGE         
                                                                                
              EVALUATE WS-PREV-DTE-BAL(5:2)                                     
              WHEN '01' MOVE 'JAN'          TO LB66-MON                         
              WHEN '02' MOVE 'FEB'          TO LB66-MON                         
              WHEN '03' MOVE 'MAR'          TO LB66-MON                         
              WHEN '04' MOVE 'APR'          TO LB66-MON                         
              WHEN '05' MOVE 'MAY'          TO LB66-MON                         
              WHEN '06' MOVE 'JUN'          TO LB66-MON                         
              WHEN '07' MOVE 'JUL'          TO LB66-MON                         
              WHEN '08' MOVE 'AUG'          TO LB66-MON                         
              WHEN '09' MOVE 'SEP'          TO LB66-MON                         
              WHEN '10' MOVE 'OCT'          TO LB66-MON                         
              WHEN '11' MOVE 'NOV'          TO LB66-MON                         
              WHEN '12' MOVE 'DEC'          TO LB66-MON                         
              END-EVALUATE                                                      
                                                                                
              MOVE WS-PREV-DTE-BAL(1:4)     TO LB66-YEAR                        
              WRITE BP13LB66-REC            FROM LB66-HEAD-2                    
           END-IF.                                                              
                                                                                
           WRITE BP13LB66-REC               FROM LB66-BLANK.                    
           WRITE BP13LB66-REC               FROM LB66-BLANK.                    
                                                                                
           IF K813-NUM-MATURE-EST-TAG = 'Y'                                     
              MOVE '(MATURE ESTATE)'        TO WS-MAT-EST                       
           ELSE                                                                 
              MOVE '(NON-MATURE ESTATE)'    TO WS-MAT-EST                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                      TO LB66-DESC.                       
           STRING 'SHORTLISTING RESULT FOR ' K813-NME-SITE ' '                  
                  FUNCTION UPPER-CASE(K813-NME-FLAT-TYPE-LTR) ' '               
                  WS-MAT-EST ' - ' WS-PARM-NUM-CDE '% FLAT SUPPLY'              
                  DELIMITED BY '  ' INTO LB66-DESC.                             
           WRITE BP13LB66-REC               FROM LB66-HEAD-3.                   
                                                                                
           WRITE BP13LB66-REC               FROM LB66A-LINE-1.                  
           MOVE SPACES                      TO LB66A-DESC.                      
           MOVE 'FLAT SUPPLY'               TO LB66A-DESC.                      
           MOVE WS-FLAT-SUPPLY              TO LB66A-UNIT                       
                                               FW3I-NUM-S-FT-TOT.               
           WRITE BP13LB66-REC               FROM LB66A-DETAIL.                  
           MOVE WS-PREV-DTE-BAL             TO FW3I-DTE-BALLOT.                 
           MOVE WS-PARM-NUM-CDE             TO FW3I-NUM-PERCENT.                
                                                                                
           IF WS-PARM-NUM-CDE = '100'                                           
              WRITE BP13LB66-REC            FROM LB66A-LINE-2                   
              MOVE SPACES                   TO LB66A-DESC                       
              IF WS-PRIO-SHORTLIST > ZEROES                                     
                 MOVE 'FLATS TAKEN UP BY PRIORITY SCHEMES   | TOTAL'            
                                            TO LB66A-DESC                       
              ELSE                                                              
                 MOVE 'FLATS TAKEN UP BY PRIORITY SCHEMES'                      
                                            TO LB66A-DESC                       
              END-IF                                                            
              MOVE WS-PRIO-SHORTLIST        TO LB66A-UNIT                       
                                               FW3I-NUM-S-FT-PRIO               
              WRITE BP13LB66-REC            FROM LB66A-DETAIL                   
                                                                                
              IF WS-PRIO-CCC > ZEROES                                           
                 WRITE BP13LB66-REC         FROM LB66A-LINE-3                   
                 MOVE SPACES                TO LB66A-DESC                       
                 MOVE '                                     | GRO'              
                                            TO LB66A-DESC                       
                 MOVE WS-PRIO-CCC           TO LB66A-UNIT                       
                                               FW3I-NUM-S-FT-GRO                
                 WRITE BP13LB66-REC         FROM LB66A-DETAIL                   
              END-IF                                                            
                                                                                
              IF WS-PRIO-REN > ZEROES                                           
                 WRITE BP13LB66-REC         FROM LB66A-LINE-3                   
                 MOVE SPACES                TO LB66A-DESC                       
                 MOVE '                                     | TPS/REN/SE        
      -               'RS/SLA/FSH/RHO'      TO LB66A-DESC                       
                 MOVE WS-PRIO-REN           TO LB66A-UNIT                       
                                               FW3I-NUM-S-FT-TPS                
                 WRITE BP13LB66-REC         FROM LB66A-DETAIL                   
              END-IF                                                            
                                                                                
              IF WS-PRIO-JB > ZEROES                                            
                 WRITE BP13LB66-REC         FROM LB66A-LINE-3                   
                 MOVE SPACES                TO LB66A-DESC                       
                 MOVE '                                     | MGPS'             
                                            TO LB66A-DESC                       
                 MOVE WS-PRIO-JB            TO LB66A-UNIT                       
                                               FW3I-NUM-S-FT-MGPS               
                 WRITE BP13LB66-REC         FROM LB66A-DETAIL                   
              END-IF                                                            
                                                                                
              IF WS-PRIO-TCP > ZEROES                                           
                 WRITE BP13LB66-REC         FROM LB66A-LINE-3                   
                 MOVE SPACES                TO LB66A-DESC                       
                 MOVE '                                     | TCP'              
                                            TO LB66A-DESC                       
                 MOVE WS-PRIO-TCP           TO LB66A-UNIT                       
                                               FW3I-NUM-S-FT-TCP                
                 WRITE BP13LB66-REC         FROM LB66A-DETAIL                   
              END-IF                                                            
                                                                                
              WRITE BP13LB66-REC            FROM LB66A-LINE-2                   
              MOVE SPACES                   TO LB66A-DESC                       
              MOVE 'REMAINING FLAT SUPPLY AFTER PRIORITY'                       
                                            TO LB66A-DESC                       
              MOVE WS-FLAT-SUPPLY-NOPRIO    TO LB66A-UNIT                       
                                               FW3I-NUM-S-FT-PRIO-BAL           
              WRITE BP13LB66-REC            FROM LB66A-DETAIL                   
           END-IF.                                                              
           WRITE BP13LB66-REC               FROM LB66A-LINE-1.                  
                                                                                
           WRITE BP13LB66-REC               FROM LB66-BLANK.                    
           WRITE BP13LB66-REC               FROM LB66B-LINE-1.                  
           WRITE BP13LB66-REC               FROM LB66B-HEAD.                    
           WRITE BP13LB66-REC               FROM LB66B-LINE-2.                  
                                                                                
           PERFORM 8500-PRINT-SHORTLIST     THRU 8500-EXIT                      
           VARYING WS-CNT FROM 1 BY 1                                           
             UNTIL WS-CNT > 6.                                                  
           WRITE BP13LB66-REC               FROM LB66B-LINE-1.                  
                                                                                
           WRITE BP13LB66-REC               FROM LB66-BLANK.                    
           WRITE BP13LB66-REC               FROM LB66C-LINE-1.                  
           WRITE BP13LB66-REC               FROM LB66C-HEAD.                    
           WRITE BP13LB66-REC               FROM LB66C-LINE-2.                  
                                                                                
           MOVE SPACES                      TO LB66C-DESC.                      
           MOVE 'ETHNIC QUOTA AVAILABLE'    TO LB66C-DESC.                      
           MOVE WS-K030-CAT1                TO LB66C-CAT1                       
                                               FW3I-NUM-S-EQA-C1                
           MOVE WS-K030-CAT2                TO LB66C-CAT2                       
                                               FW3I-NUM-S-EQA-C2                
           MOVE WS-K030-CAT3                TO LB66C-CAT3                       
                                               FW3I-NUM-S-EQA-C3                
           MOVE WS-K030-TOTAL               TO LB66C-TOT                        
                                               FW3I-NUM-S-EQA-TOT               
           WRITE BP13LB66-REC               FROM LB66C-DETAIL.                  
           WRITE BP13LB66-REC               FROM LB66C-LINE-2.                  
                                                                                
           MOVE SPACES                      TO LB66C-DESC.                      
           MOVE 'ETHNIC QUOTA TAKEN UP'     TO LB66C-DESC.                      
           MOVE WS-F740-CAT1                TO LB66C-CAT1                       
                                               FW3I-NUM-S-EQT-C1                
           MOVE WS-F740-CAT2                TO LB66C-CAT2                       
                                               FW3I-NUM-S-EQT-C2                
           MOVE WS-F740-CAT3                TO LB66C-CAT3                       
                                               FW3I-NUM-S-EQT-C3                
           COMPUTE WS-F740-TOTAL = WS-F740-CAT1 + WS-F740-CAT2 +                
                                   WS-F740-CAT3.                                
           MOVE WS-F740-TOTAL               TO LB66C-TOT                        
                                               FW3I-NUM-S-EQT-TOT               
           WRITE BP13LB66-REC               FROM LB66C-DETAIL.                  
           WRITE BP13LB66-REC               FROM LB66C-LINE-1.                  
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8100-WRITE-BP13FW3I.                                                     
      **********************                                                    
           PERFORM 8900-INITIALIZE-DELIMITER  THRU 8900-EXIT.                   
                                                                                
           MOVE WS-SALES-MODE      TO FW3I-SALES-MODE.                          
                                                                                
           WRITE BP13FW3I-REC.                                                  
           ADD 1 TO WS-FW3I-WRITE.                                              
                                                                                
           MOVE SPACES  TO BP13FW3I-REC.                                        
           INITIALIZE      BP13FW3I-REC.                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       8500-PRINT-SHORTLIST.                                                    
      ***********************                                                   
                                                                                
           MOVE SPACES                      TO LB66B-DESC.                      
           EVALUATE WS-CNT                                                      
           WHEN 1                                                               
                MOVE 'DEMAND'               TO LB66B-DESC                       
           WHEN 2                                                               
                MOVE 'HOUSEHOLD PROPORTION (FT 95%, ST 5%)'                     
                                            TO LB66B-DESC                       
           WHEN 3                                                               
                MOVE 'SHORTLISTED'          TO LB66B-DESC                       
           WHEN 4                                                               
                MOVE 'SHORTLISTED (BYPASS EIP)'                                 
                                            TO LB66B-DESC                       
           WHEN 5                                                               
                MOVE 'TOTAL'                TO LB66B-DESC                       
           WHEN 6                                                               
                MOVE 'UNSUCCESSFUL'         TO LB66B-DESC                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 8510-SETUP-SHORTLIST     THRU 8510-EXIT                      
           VARYING WS-XCNT FROM 1 BY 1                                          
             UNTIL WS-XCNT > 3.                                                 
                                                                                
           WRITE BP13LB66-REC               FROM LB66B-DETAIL.                  
                                                                                
           IF WS-CNT < 6                                                        
              WRITE BP13LB66-REC            FROM LB66B-LINE-2                   
           END-IF.                                                              
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       8510-SETUP-SHORTLIST.                                                    
      ***********************                                                   
                                                                                
           MOVE WS-SL-QTY ( WS-CNT , WS-XCNT )                                  
             TO LB66B-QTY ( WS-XCNT ).                                          
                                                                                
           MOVE WS-SL-QTY (1, 1) TO   FW3I-NUM-S-DEMAND-FT.                     
           MOVE WS-SL-QTY (1, 2) TO   FW3I-NUM-S-DEMAND-2T.                     
           MOVE WS-SL-QTY (2, 1) TO   FW3I-NUM-S-HH-PRO-FT.                     
           MOVE WS-SL-QTY (2, 2) TO   FW3I-NUM-S-HH-PRO-2T.                     
           MOVE WS-SL-QTY (3, 1) TO   FW3I-NUM-S-SHRT-FT.                       
           MOVE WS-SL-QTY (3, 2) TO   FW3I-NUM-S-SHRT-2T.                       
           MOVE WS-SL-QTY (4, 1) TO   FW3I-NUM-S-SHRT-EIP-FT.                   
           MOVE WS-SL-QTY (4, 2) TO   FW3I-NUM-S-SHRT-EIP-2T.                   
           MOVE WS-SL-QTY (6, 1) TO   FW3I-NUM-S-UNSUCC-FT                      
           MOVE WS-SL-QTY (6, 2) TO   FW3I-NUM-S-UNSUCC-2T.                     
                                                                                
       8510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       8900-INITIALIZE-DELIMITER.                                               
      ******************                                                        
                                                                                
           MOVE ';'   TO  FIL001  FIL002  FIL003  FIL004  FIL005                
                          FIL006  FIL007  FIL008  FIL009  FIL010                
                          FIL09A                                                
                          FIL011  FIL012  FIL013  FIL014  FIL015                
                          FIL016  FIL017  FIL018  FIL019  FIL020                
                          FIL021  FIL022  FIL023  FIL024  FIL025                
                          FIL026  FIL027  FIL028  FIL029  FIL030                
                          FIL031  FIL032  FIL033  FIL034  FIL035                
                          FIL036  FIL037  FIL038  FIL039  FIL040                
                          FIL40A  FIL43A                                        
                          FIL041  FIL042  FIL043  FIL044  FIL045                
                          FIL046  FIL047  FIL048  FIL049  FIL050                
                          FIL051  FIL052  FIL053                                
                          FIL700  FIL701  FIL702  FIL703  FIL704                
                          FIL705  FIL706  FIL707  FIL708  FIL709                
                          FIL710  FIL711  FIL712  FIL713  FIL714                
                          FIL715  FIL716  FIL717  FIL718  FIL719                
                          FIL720  FIL721  FIL722  FIL723  FIL724                
                          FIL725  FIL726  FIL727  FIL728  FIL729                
                          FIL730  FIL731  FIL732  FIL733  FIL734                
                          FIL735  FIL736  FIL737  FIL738  FIL739                
                          FIL740  FIL741  FIL742  FIL743  FIL744                
                          FIL745  FIL746  FIL747  FIL748  FIL749                
                          FIL750  FIL751  FIL752  FIL753  FIL754                
                          FIL755  FIL756  FIL757  FIL758  FIL759                
                          FIL760  FIL761  FIL762  FIL763  FIL764                
                          FIL765  FIL766  FIL767  FIL768  FIL769                
                          FIL770  FIL771  FIL772  FIL773  FIL774                
                          FIL775  FIL776  FIL777  FIL778  FIL779                
                          FIL780  FIL781  FIL782  FIL783  FIL784                
                          FIL785  FIL786  FIL787  FIL788  FIL789                
                          FIL790  FIL791  FIL792  FIL793                        
                          FIL054  FIL055  FIL56A                                
                          FIL056  FIL057  FIL058  FIL059  FIL060                
                          FIL061  FIL062  FIL063  FIL064  FIL065                
                          FIL066  FIL067  FIL068  FIL069  FIL070                
                          FIL071  FIL072  FIL073  FIL074  FIL075                
                          FIL076  FIL077  FIL078  FIL079  FIL080                
                          FIL081  FIL082  FIL083  FIL084  FIL085                
                          FIL086  FIL087  FIL088  FIL089  FIL090                
                          FIL091  FIL092  FIL093  FIL094  FIL095                
                          FIL096  FIL097  FIL098  FIL099  FIL100                
                          FIL101  FIL102  FIL103  FIL104  FIL105                
                          FIL106  FIL107  FIL108  FIL109  FIL110                
                          FIL111  FIL112  FIL113  FIL114  FIL115                
                          FIL116  FIL117  FIL118  FIL119  FIL120                
                          FIL121  FIL122  FIL123  FIL124  FIL125                
                          FIL126  FIL127  FIL128  FIL129  FIL130                
                          FIL131  FIL132  FIL133  FIL134  FIL135                
                          FIL136  FIL137  FIL138  FIL139  FIL140                
                          FIL141  FIL142  FIL143  FIL144  FIL145                
                          FIL146  FIL147  FIL148  FIL149  FIL150                
                          FIL151  FIL152  FIL153  FIL154  FIL155                
                          FIL156  FIL157  FIL158  FIL159  FIL160                
                          FIL161  FIL162  FIL163  FIL164  FIL165                
                          FIL166  FIL167  FIL168  FIL169  FIL170                
                          FIL171  FIL162  FIL173  FIL174  FIL175                
                          FIL176  FIL177  FIL178  FIL179  FIL180                
                          FIL181  FIL182  FIL183  FIL184  FIL185                
                          FIL166  FIL167  FIL168  FIL169  FIL170                
                          FIL171  FIL172  FIL173  FIL174  FIL175                
                          FIL176  FIL177  FIL178  FIL179  FIL180                
                          FIL181  FIL182  FIL183  FIL184  FIL185                
                          FIL186  FIL187  FIL188  FIL189  FIL190                
                          FIL191  FIL192  FIL193  FIL194  FIL195                
                          FIL196  FIL197  FIL198  FIL199  FIL200                
                          FIL201  FIL202  FIL203  FIL204  FIL205                
                          FIL206  FIL207  FIL208  FIL209  FIL210                
                          FIL211  FIL212  FIL213  FIL214  FIL215                
                          FIL216  FIL217  FIL218  FIL219  FIL220                
                          FIL221  FIL222  FIL223  FIL224  FIL225                
                          FIL226  FIL227  FIL228  FIL229  FIL230                
                          FIL231  FIL232  FIL233  FIL234  FIL235                
                          FIL236  FIL237  FIL238  FIL239  FIL240                
                          FIL241  FIL242  FIL243  FIL244  FIL245                
                          FIL246  FIL247  FIL248  FIL249  FIL250                
                          FIL251  FIL252  FIL253  FIL254  FIL255                
                          FIL256  FIL257  FIL258  FIL259  FIL260                
                          FIL261  FIL262  FIL263  FIL264  FIL265                
                          FIL266  FIL267  FIL268  FIL269  FIL270                
                          FIL271  FIL272  FIL273  FIL274  FIL275                
                          FIL276  FIL277  FIL278  FIL279  FIL280                
                          FIL281  FIL282  FIL283  FIL284  FIL285                
                          FIL286  FIL287  FIL288  FIL289  FIL290                
                          FIL291  FIL292  FIL293  FIL294  FIL295                
                          FIL296  FIL297  FIL298  FIL299  FIL300                
                          FIL301  FIL302  FIL303  FIL304  FIL305                
                          FIL306  FIL307  FIL308  FIL309  FIL310                
                                  FIL312  FIL313  FIL314  FIL315                
                          FIL316  FIL317  FIL318  FIL319  FIL320                
                          FIL321  FIL322  FIL323  FIL324  FIL325                
                          FIL326  FIL327  FIL328  FIL329  FIL330                
                          FIL331  FIL332  FIL333  FIL334  FIL335                
                          FIL336  FIL337  FIL338  FIL339  FIL340                
                          FIL341  FIL342  FIL343  FIL344  FIL345                
                          FIL346  FIL347  FIL348  FIL349  FIL350                
                          FIL351  FIL352  FIL353  FIL354  FIL355                
                          FIL356  FIL357  FIL358  FIL359  FIL360                
                          FIL361  FIL362  FIL363  FIL364  FIL365                
                          FIL366  FIL367  FIL368  FIL369  FIL370                
                          FIL371  FIL372  FIL373  FIL374  FIL375                
                          FIL376  FIL377  FIL378  FIL379  FIL380                
                          FIL381  FIL382  FIL383  FIL384  FIL385                
                          FIL386  FIL387  FIL388  FIL389  FIL390                
                          FIL391  FIL392  FIL393  FIL394  FIL395                
                          FIL396  FIL397  FIL398  FIL399  FIL400                
                          FIL401  FIL402  FIL403  FIL404  FIL405                
                          FIL406  FIL407  FIL408  FIL409  FIL410                
                          FIL411  FIL412  FIL413  FIL414  FIL415                
                          FIL416  FIL417  FIL418  FIL419  FIL420                
                          FIL421  FIL422  FIL423  FIL424  FIL425                
                          FIL426  FIL427  FIL428  FIL429  FIL430                
                          FIL431  FIL432  FIL433  FIL434  FIL435                
                          FIL436  FIL437  FIL438  FIL439  FIL440                
                          FIL441  FIL442  FIL443  FIL444  FIL445                
                          FIL446  FIL447  FIL448  FIL449  FIL450                
                          FIL451  FIL452  FIL453  FIL454  FIL455                
                          FIL456  FIL457  FIL458  FIL459  FIL460                
                          FIL461  FIL462  FIL463  FIL464  FIL465                
                          FIL466  FIL467  FIL468  FIL469  FIL470                
                          FIL471  FIL472  FIL473  FIL474  FIL475                
                          FIL476  FIL477  FIL478  FIL479  FIL480                
                          FIL481  FIL482  FIL483  FIL484  FIL485                
                          FIL486  FIL487  FIL488  FIL489  FIL490                
                          FIL491  FIL492  FIL493  FIL494  FIL495                
                          FIL496  FIL497  FIL498  FIL499  FIL500                
                          FIL501  FIL502  FIL503  FIL504  FIL505                
                          FIL506  FIL507  FIL508  FIL509  FIL510                
                          FIL511  FIL512  FIL513  FIL514  FIL515                
                          FIL516  FIL517  FIL518  FIL519  FIL520                
                          FIL521  FIL522  FIL523  FIL524  FIL525                
                          FIL526  FIL527  FIL528  FIL529  FIL530                
                          FIL531  FIL532  FIL533  FIL534  FIL535                
                          FIL536  FIL537  FIL538  FIL539  FIL540                
                          FIL541  FIL542  FIL543  FIL544  FIL545                
                          FIL546  FIL547  FIL548  FIL549  FIL550                
                          FIL551  FIL552  FIL553  FIL554  FIL555                
                          FIL556  FIL557  FIL558  FIL559  FIL560                
                          FIL561  FIL562  FIL563  FIL564  FIL565                
                          FIL566  FIL567  FIL568  FIL569  FIL570                
                          FIL571  FIL572  FIL573  FIL574  FIL575                
                          FIL576  FIL577  FIL578  FIL579  FIL580                
                          FIL581  FIL582  FIL583  FIL584  FIL585                
                          FIL586.                                               
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY '***  TOTAL NUMBER OF RECORDS PROCESSED = '                  
           WS-F740-READ.                                                        
           DISPLAY '***  TOTAL RECORDS WRITTEN IN BP13FW3I = '                  
           WS-FW3I-WRITE.                                                       
                                                                                
           CLOSE BP13F740                                                       
                 BP13F730                                                       
                 BP13K010                                                       
                 BP13K030                                                       
                 BP13K712                                                       
                 BP13K813                                                       
                 BP13LB66                                                       
                 BP13FW3I.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR CLOSING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR CLOSING BP13K030, STATUS ' WS-K030-STATUS          
              MOVE WS-K030-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K712-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR CLOSING BP13K712, STATUS ' WS-K712-STATUS          
              MOVE WS-K712-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR CLOSING BP13K813, STATUS ' WS-K813-STATUS          
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
