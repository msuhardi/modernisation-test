       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         BP13C02U.                                            
      *AUTHOR.             ZARITA SARMIENTO.                                    
      *DATE-WRITTEN.       16 SEP 2009.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  PROGRAM TO READ BP13FB60 TO                 *            
      *                SEND EMAIL                                  *            
      *                                                            *            
      *  INPUT FILE :  1. MAILFB60                                 *            
      *  OUTPUT FILE:  1. BP13MAIL                                 *            
      * ---------------------------------------------------------- *            
      * CR#      OIC  DATE     DESCRIPTION                         *            
      * ---------------------------------------------------------- *            
      * BP133400 ZDD1 16092009 NEW PROGRAM                         *            
      * BP134162 ESA1 09032011 REMOVE LHI IN RECIPIENT             *            
      * BP135537 RJB1 04082015 CHANGE THE EMAIL RECIPIENT FROM     *            
      *                        NICKNAME INTO COMPLETE NAME         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT MAILFB60 ASSIGN TO MAILFB60.                                 
                                                                                
            SELECT BP13MAIL ASSIGN TO BP13MAIL.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   MAILFB60                                                            
            RECORD CONTAINS 47 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       01   MAILFB60-REC.                                                       
            05 FB60-DEV-REF-NUM   PIC X(08).                                    
            05 FB60-UIN           PIC X(09).                                    
            05 FB60-REASON        PIC X(30).                                    
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                       PIC X(80).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-VARIABLES.                                                         
          05 EOF-FLAG-FB60               PIC X(01) VALUE 'N'.                   
          05 WS-FB60-READ                PIC 9(08) VALUE ZEROES.                
          05 WS-RECORD-WRITTEN           PIC 9(08) VALUE ZEROES.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CURR-DTE      PIC X(8)         VALUE SPACES.                   
           05 WS-CURR-TME      PIC X(8)         VALUE SPACES.                   
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CSXXXXX'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-TOMAILID   PIC X(25)  VALUE                                 
               'JOELLE_SF_LEE@HDB.GOV.SG>'.                                     
           05  FILLER          PIC X(46)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(17)  VALUE                                 
               'SOC - EMAIL ALERT'.                                             
           05  FILLER          PIC X(57)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-TOMAILID1  PIC X(25)  VALUE                                 
               'JOELLE_SF_LEE@HDB.GOV.SG>'.                                     
           05  FILLER          PIC X(51)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4A.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-TOMAILID1  PIC X(24)  VALUE                                 
               'LEE_HWEE_ING@HDB.GOV.SG>'.                                      
           05  FILLER          PIC X(52)  VALUE SPACES.                         
                                                                                
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(44)  VALUE                                 
               'FOR YOUR VERIFICATION - NEW CASES FROM DBSS '.                  
           05  FILLER          PIC X(09)  VALUE                                 
               'DEVELOPER'.                                                     
                                                                                
       01  MAIL-DTL6.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
                                                                                
       01  MAIL-BODY03.                                                         
           05 FILLER                   PIC X(4)   VALUE ' S/N'.                 
           05 FILLER                   PIC X(4)   VALUE SPACES.                 
           05 FILLER                   PIC X(10)  VALUE 'DEV-REF-NO'.           
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'UIN/FIN'.              
           05 FILLER                   PIC X(7)   VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'REASON'.               
                                                                                
       01  MAIL-BODY05.                                                         
           05 FILLER                   PIC X(77)  VALUE ALL '-'.                
                                                                                
       01  MAIL-DETAIL.                                                         
           05 MAIL-SNO                 PIC ZZZZ9.                               
           05 FILLER                   PIC X(1)   VALUE SPACES.                 
           05 MAIL-DEV-REF-TAG         PIC X(2)   VALUE SPACES.                 
           05 MAIL-DEV-REF-NO          PIC X(13)  VALUE SPACES.                 
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
           05 MAIL-UIN-TAG             PIC X(2)   VALUE SPACES.                 
           05 MAIL-UIN                 PIC X(9)   VALUE SPACES.                 
           05 FILLER                   PIC X(3)   VALUE SPACES.                 
           05 MAIL-REASON-TAG          PIC X(2)   VALUE SPACES.                 
           05 MAIL-REASON              PIC X(30)  VALUE SPACES.                 
           05 FILLER                   PIC X(2)   VALUE SPACES.                 
                                                                                
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       01  MAIL-SPACES1.                                                        
           05 FILLER                   PIC X(1)   VALUE '.'.                    
           05 FILLER                   PIC X(79)  VALUE SPACES.                 
                                                                                
       01  WS-EMAIL-HDR                PIC X(1)   VALUE 'N'.                    
       01  WS-SNO                      PIC 9(2)   VALUE ZEROES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-MAILFB60     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
                   UNTIL EOF-FLAG-FB60 = 'Y'.                                   
           PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  MAILFB60                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-READ-MAILFB60.                                                      
      ****************************                                              
                                                                                
           READ MAILFB60                                                        
              AT END MOVE 'Y'      TO    EOF-FLAG-FB60                          
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1                   TO    WS-FB60-READ.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-PROCESS-RECORDS.                                                    
      ****************************                                              
                                                                                
           MOVE SPACES                   TO MAIL-UIN                            
                                            MAIL-DEV-REF-NO                     
                                            MAIL-REASON.                        
           IF WS-EMAIL-HDR = 'N'                                                
              MOVE FUNCTION CURRENT-DATE(1:16) TO WS-CURRENT-DATE               
              STRING WS-CURR-DTE(7:2) '/'                                       
                     WS-CURR-DTE(5:2) '/'                                       
                     WS-CURR-DTE(1:4)                                           
                     DELIMITED BY SPACES INTO MAIL-DATE                         
              PERFORM 4100-GENERATE-HDR THRU 4100-EXIT                          
           END-IF.                                                              
                                                                                
           ADD    1                      TO WS-SNO                              
           MOVE   WS-SNO                 TO MAIL-SNO                            
           MOVE   FB60-UIN               TO MAIL-UIN                            
           MOVE   FB60-DEV-REF-NUM       TO MAIL-DEV-REF-NO                     
           MOVE   FB60-REASON            TO MAIL-REASON                         
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-DETAIL.                                  
                                                                                
           ADD 1 TO WS-RECORD-WRITTEN                                           
                                                                                
           PERFORM 2000-READ-MAILFB60  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************                                              
       4100-GENERATE-HDR.                                                       
      ****************************                                              
                                                                                
           MOVE 'Y' TO WS-EMAIL-HDR.                                            
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2                                     
           WRITE MAIL-PRTREC FROM MAIL-DTL1.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4A.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL5.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6.                                    
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-BODY03                                   
           WRITE MAIL-PRTREC FROM MAIL-BODY05.                                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9999-CLOSE-FILES.                                                        
      ****************************                                              
                                                                                
           DISPLAY '9999-CLOSE-FILES'.                                          
           DISPLAY SPACES.                                                      
           DISPLAY 'REPORT DESCRIPTION'.                                        
           DISPLAY '------------------'.                                        
           DISPLAY SPACES.                                                      
           DISPLAY 'NO MAILFB60 READ          :' WS-FB60-READ                   
           DISPLAY 'NO RECORD WRITTEN         :' WS-RECORD-WRITTEN              
           DISPLAY ' '                                                          
           CLOSE MAILFB60                                                       
                 BP13MAIL.                                                      
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
