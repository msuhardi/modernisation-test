       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C227.                                                 
      *AUTHOR.        JAP.                                                      
      *DATE-WRITTEN.  10/09/93                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO OUTPUT                           *            
      *                                                            *            
      *                1)  OUTPUT THE FLAT AVAILABLE FOR EACH      *            
      *                    NT/BLK/FT OF THE BOOKING EXERCISE TO    *            
      *                    INTV AT THE TIME OF PRINTING INVITATION *            
      *                    LETTER. THE NO OF UNIT IS THE SAME AS   *            
      *                    THE PRICE LIST.                         *            
      *                                                            *            
      *                2)  RE-FORMAT THE BM06F800 TO THE INTV FILE *            
      *                    FORMAT WITH 3 REC TYPE                  *            
      *                    - HEADER,  REC-TY = 01                  *            
      *                    - DETAIL,  REC-TY = 02                  *            
      *                    - TRAILER, REC-TY = 03                  *            
      *                    - START-END BOOKING APPT., REC-TY = 05  *            
      *                    - BLOCK           = 06                               
      *                                                            *            
      *  INPUT FILES :  1)  BP13F205  -  CONTROL RECORDS FILE      *            
      *                 2)  BM06F800  -  FLATLIST        FILE      *            
      *                 3)  BM06K510  -  ESTATE CODE     FILE      *            
      *                 4)  BP13K270  -  BOOKING PROGRAM FILE      *            
      *                 5)  BP13K816  -  BTO SITE INFORMATION      *            
      *                 6)  BP13K222  -  SUMMARY ALLOCATION FILE   *            
      *                                                            *            
      *  OUTPUT FILE :  1)  BP13F230  -  FLATLIST FILE(RT 01,02,03)*            
      *                 2)  P13F230B  -  FLATLIST FILE(RT 05,06)   *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC   DATE     DESCRIPTION                        *            
      * -------- ----  ------   -----------                        *            
      *  C930933 JAP   930928   NEW PROGRAM.                       *            
      *  SOC-PH8 JEP   931013   READ BM06CTL FILE.                 *            
      *  SOC-PH8 JEP   940402   CHANGE BM06CTL TO BP13F205.        *            
      *  SOC-PH9 LMS   941102   TO SELECT BY ZONE/NT, ALLOC-DATE & *            
      *                         FLAT-TYPE.                         *            
      *  C941092 YPH   950227   TO INCLUDE DTE-BALLOT              *            
      *  C941228 LMS   950315   TO INCLUDE FLAT-TYPE '4S'          *            
      * BP130052 LMS   951107   IF SYS-TIME IS < 17:00, MOVE '88'  *            
      *                         TO RC. IF F205-DTE-UPDATE IS NOT   *            
      *                         TODAY'S DATE, MOVE '99' TO RC.     *            
      * BP130143 LMS   961118   TO INCLUDE MODEL DESIGN '72' AND   *            
      *                         '73'.                              *            
      * BP130228 LMS   970221   TO CATER FOR DESIGN PLUS FLATS.    *            
      * BP130336 LMS   970813   TO CATER FOR JB IN SERS.           *            
      *  (BP130250 IN LCS)                                         *            
      * BP130393 SSS   971120   INCLUDE PG ZONE                    *            
      * BP130514 LMS   980722   TO INCLUDE 'PUNGGOL 21'.           *            
      * BP130418 LMS   980902   TO CHANGE BM06F800 FOR Y2K.        *            
      * BP130641 SRA   990226   TO INCLUE FLATTYPE '1A' AND '2A'   *            
      * BP130778 GBR   991213   TO INCLUDE 3 ROOM FLAT UNDER FLAT  *            
      *                         TYPE 'SE'                          *            
      * BP130807 GBR 20000216   ALLOW DESIGN MODEL CODES '34,71,72,*            
      *                         73,81,82' FOR ROOM TYP '04,05,06'. *            
      *                         TO REMOVE FLAT TYP '4P, 5P & 6P'.  *            
      * BP130956 AKT 20001120   INCREASE LRECL FROM 130 TO 250.    *            
      * BP130965 AKT 20010105   TO CHK MODL-DESG                   *            
      * BP130956 LMS 20010220   TO ADD DESIGN TYPE.                *            
      * BP130970 JAM 20010408   TO CORRECT THE CODE SELECTION TYPE *            
      *                         FOR HEADER RECORDS.                *            
      * BP131059 JAM 20010724   TO INCLUDE SELECTION TYPE 'BTO'    *            
      *                         AND ZONE '01'-'08' AND CREATE NEW  *            
      *                         TOWN USING K767-NME-EST+ ' SITE '  *            
      *                         + K767-NUM-SITE FROM BP13K767.     *            
      * BP131054 JAM 20010801   TO INCLUDE K230-SELECTION2         *            
      * BP132079 MTD 20020118   TO INCLUDE K230-SELECTION3         *            
      * BP132111 JAM 20011201   TO CREATE RECORD TYPE '05' IN A    *            
      *                         SEPARATE FILE P13F230B.            *            
      * BP13XXXX MTD 20020523   TO HANDLE NUM-SELECTION = '3R '    *            
      *                         WITH FLAT-TYPE = '03'              *            
      * BP132111 MTD 20020801   RESOLVE INCORRECT FIRST RECORD     *            
      *                         IN OUTPUT FILE                     *            
      * BP132286 CT2 20030114   REMOVE CHECK FOR TIME OF RUN AND   *            
      *                         CHECK FOR SUBMISSION OF DATE IN    *            
      *                         BP13F205.  REMOVE CHECK FOR '3R'.  *            
      * BP132299 MTD 20030213   ADD CHECKING OF F800-NUM-BTO-      *            
      *                         OPTION = '09' '10' '11' IF         *            
      *                         F205-NUM-NT-ZONE ='00'             *            
      * BP132278 JF4 20030328   CHGE BP13F230 LRECL TO 450.        *            
      *                         MOVE STREET,COMPLTN-DTE,LSE-COM-DTE*            
      *                         UPG-STATUS,NEIGH,CNTRCT TO BP13F230*            
      *                         CHG BM06F800 LRECL FROM 600 TO 800 *            
      * BP132361 JF4 20030530   CATER FOR CREATION OF K240-ALT-KEY4*            
      *                         AND K230-ALT-KEY4.                 *            
      * BP132379 JF4 20030716   IF F205-NUM-OPTION='SE', MOVE      *            
      *                         F205-DTE-END TO F230-DTE-END OF    *            
      *                         F230-START-END-REC INSTEAD OF      *            
      *                         K222-DTE-END-BKAPPMT. (610-READ-..)*            
      * BP132432 MTD 20031016   CHANGE F800-DTE-EST-COMPLETION TO  *            
      *                         F800-DTE-DLVRY-POSSESION           *            
      * BP132432 LMS 20031030   TO MOVE NUM-BTO-NEW-CONTRACT TO    *            
      *                         F230.                              *            
      * BP132432 MTD 20031101   REMOVE CHECKING OF NUM-BTO-OPTION= *            
      *                         '09' '10' '11'                     *            
      * BP132452 MTD 20040304   REMOVE CHECKING OF F800-CDE-INTV   *            
      * BP132500 JF4 20040316   CATER FOR DESIGN DESCRIPTION OF    *            
      *                         3-ROOM SERS CASES.                 *            
      * BP132500 LMS 20040326   CATER FOR SER                      *            
      * BP132530 MTD 20040515   ADD F230-ALT-KEY3 OF DETAIL-REC    *            
      * BP132530 LMS 20040531   TO INCLUDE ST 40 AS RP FLATS.      *            
      * BP132591 MTD 20040827   MOVE 'TAMAN JURONG' TO NEW-TOWN    *            
      *                         FOR 3R-YUAN CHING RD               *            
      * BP132762 LMS 20051013   TO REMOVE CHECKING OF ZONE FOR     *            
      *                         2-ROOM SER.                        *            
      * BP132785 LMS 20051221   TO CATER FOR COMBINE SA.           *            
      * BP132752 LMS 20051222   TO MOVE FLOOR AREA TO BP13K230.    *            
      * BP133349 LMS 20080618   TO CATER FOR SER 2-ROOM NEW FLATS. *            
      * BP133610 ESA120090427   TO REPLACE BP13K767 W/ BP13K816    *            
      * BP133723 RB1220090929   ADD CHECK FOR F205-NUM-FLAT-TYPE   *            
      * BP133724 ESA120090928   TO ADD BLOCK RECORD(INTV06)        *            
      * BP133851 ESA120100310   TO CATER FOR PAIR UNIT             *            
      *                         TO ADD READING OF BP13KQ30         *            
      * BP133854 ESA120100408   TO CATER FOR MODEL-DESG 86,87,88   *            
      * BP133854 DW5 20100413   TO CATER FOR DAWSON SA             *            
      * BP134006 ESA120100827   TO ADD 3P-ROOM                     *            
      * BP134045 ESA120101025   TO CATER FOR NE FOR NPL            *            
      * BP134043 ESA120101124   TO CHANGE CHECKING FOR NUM-DISPLAY *            
      * BP134327 ESA120110815   TO ADD CHECKING FOR BTO 201107     *            
      * BP134356 ESA120110915   TO HARDCODE SBF 201109             *            
      * BP134471 ESA120120313   TO CATER FOR SPRQUOTA              *            
      * BP134513 ESA120120418   TO CATER FOR MGPS                  *            
      * BP134587 ESA120120702   TO CATER FOR UPDATING F230-NUM-RP  *            
      * BP13XXXX ESA120120703   TO REPLACE BP13K816 W/ BP13K813    *            
      * BP134653 ESA120120918   TO CATER FOR MODL-DESG = 85 AS 1PA *            
      *                         TO ADD UPDATE OF F230-CURR-SELLING-PRICE        
      * BP134681 ESA120121001   TO CATER FOR 3-BYTES ZONE          *            
      * BP13XXXX PCL420121109   TO CATER FOR MODL-DESIGN 89        *            
      * BP134769 ZDD120130319   CHANGE PUNGGOL 21 TO PUNGGOL       *            
      * BP134827 ESA120130419   CATER FOR 201305 SBF               *            
      * BP134520 ESA120130520   TO CORRECT LOGIC IN READING BM06K100            
      * BP135086 ESA120130925   TO USE K510-NT-NAME FOR BTO NEWTOWN*            
      * BP135111 ESA120131029   TO CATER FOR 3GEN FLATLIST         *            
      * BP135119 ESA120131104   TO CATER FOR PARENT FLAT TYPE      *            
      * BP135095 EL2720131120   CATER FOR 201311 SBF               *            
      * BP135135 ESA120131121   CATER FOR MAX-CAT FOR NON-3GEN     *            
      * BP135190 SMR220131204   EXPAND BP13K813 TO 1000            *            
      * BP135203 ESA120140228   TO CATER FOR HARDCODING OF 1PA FOR *            
      *                         3GEN FLATS                         *            
      *              20140318   TO CATER FOR 3GEN PREMIUM MODL-DSG=91           
      * BP135304 ESA120140425   TO CATER FOR 3GEN PREMIUM MODL-DSG=91           
      *                         IN TOT-GEN3 COMPUTATION            *            
      * BP135363 ESA120140812   REMOVE MAX-CAT FOR NON-3GEN COMPUTATION         
      * BP135572 ESA120141121   TO CATER FOR NEIGH&CNTRCT IN       *            
      *                         BP13K240 KEY-FLD                   *            
      * BP135701 KG1220150331   TO WRITE F800-NEIGH & F800-CNTRCT  *            
      *                         INTO K230 NUM-NEIGH & NUM-CNTRCT   *            
      * BP135783 ESA120150525   TO CATER FOR K249-CDE-NT           *            
      * BP135783 KG1220150525   TO CATER FOR 3-ROOM NON-MATURE EST *            
      * BP135838 ESA120150701   TO CATER FOR MODL-DESIGN '92' & '95'            
      * BP135856 ESA120150903   TO CATER FOR FT-STAT ='ST' &       *            
      *                         RP-CODE = '20'                     *            
      * BP136002 ESA120151109   TO CATER FOR 2-ROOM FLEXI          *            
      * BP136086 ESA120160211   TO USE F230-NUM-BONUS TO DETERMINE *            
      *                         SERS SITE FOR FLAT AVAILABILITY    *            
      * BP136383 ESA120160909   TO CATER FOR CV FLAT STATUS        *            
      * BP136904 ESA120170828   TO CATER FOR F230-CDE-NT           *            
      * BP137034 ESA120171106   TO CATER FOR 3P IN NON-MATURE ONLY *            
      * BP136991 ESA120170919   TO CATER FOR MODEL-DSGN = '98'     *            
      * BP137207 EL2720180314   TO CATER 2 NEW SERS PROJ CL N4 N8  *            
      * BP138051 EL2720191120   REMOVE OVERWRITING OF SERS BONUS TAG*           
      * BP138227 EL2720200305   HARDCODE TP SHORT LEASE ONLY 2F    *            
      * BP13XXXX EL2720211213   HARDCODE CCK SHORT LEASE ONLY FOR 2F'           
      * BP139089 EL2720220310   REPLACE 2F SHORT-LSE HARDCODING WITH'           
      *                         K100-NUM-SHORT-LEASE-FT = 'Y'       '           
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN        TO  BP13F205.                         
                                                                                
           SELECT BM06F800  ASSIGN        TO  BM06F800.                         
                                                                                
           SELECT BM06K510  ASSIGN        TO  BM06K510                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K510-ESTCDE                       
                            FILE STATUS   IS  K510-STATUS.                      
                                                                                
           SELECT BM06K100  ASSIGN        TO  BM06K100                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K100-KEY-FLD                      
                            FILE STATUS   IS  K100-STATUS.                      
                                                                                
           SELECT BP13K270  ASSIGN        TO  BP13K270                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K270-KEY-FLD                      
                            FILE STATUS   IS  K270-STATUS.                      
                                                                                
           SELECT BP13K060  ASSIGN        TO  BP13K060                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K060-KEY-FLD                      
                            FILE STATUS   IS  K060-STATUS.                      
                                                                                
           SELECT BP13K813  ASSIGN        TO  BP13K813                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  DYNAMIC                           
                            RECORD KEY    IS  K813-KEY-FLD                      
                            FILE STATUS   IS  K813-STATUS.                      
                                                                                
           SELECT BP13K222  ASSIGN        TO  BP13K222                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K222-KEY-FLD                      
                            FILE STATUS   IS  K222-STATUS.                      
                                                                                
           SELECT BP13KQ30  ASSIGN        TO BP13KQ30                           
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS KQ30-KEY-FLD                        
                            ALTERNATE RECORD KEY IS KQ30-ALT-KEY1               
                            FILE STATUS  IS KQ30-STATUS.                        
                                                                                
           SELECT BP13KQ50  ASSIGN        TO BP13KQ50                           
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS KQ50-KEY-FLD                        
                            FILE STATUS  IS KQ50-STATUS.                        
                                                                                
           SELECT BP13F230  ASSIGN        TO  BP13F230.                         
                                                                                
           SELECT P13F230B  ASSIGN        TO  P13F230B.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BM06F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F800.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS 60 CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BP13K270                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K270.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K222                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K222.                                                           
                                                                                
       FD   BP13KQ30                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13KQ30.                                                           
                                                                                
       FD   BP13KQ50                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13KQ50.                                                           
                                                                                
       FD   BP13F230                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
       COPY BP13F230.                                                           
                                                                                
       FD   P13F230B                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
       01   P13F230B-REC        PIC X(450).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *----------------> ETHNIC PROPORTION TABLE <--------------*               
       COPY ETHNIC.                                                             
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F205                PIC X       VALUE 'N'.                
           05  WS-EOF-F800                PIC X       VALUE 'N'.                
           05  WS-K813-EOF                PIC X       VALUE 'N'.                
           05  WS-KQ30-FND                PIC X       VALUE 'N'.                
           05  WS-K100-FND                PIC X       VALUE 'N'.                
           05  WS-KQ50-FND                PIC X       VALUE 'N'.                
           05  WS-K813-FND                PIC X       VALUE 'N'.                
           05  K510-STATUS                PIC 9(2)    VALUE  00.                
           05  K060-STATUS                PIC 9(2)    VALUE  00.                
           05  K270-STATUS                PIC 9(2)    VALUE  00.                
           05  K813-STATUS                PIC 9(2)    VALUE  00.                
           05  K222-STATUS                PIC 9(2)    VALUE  00.                
           05  KQ30-STATUS                PIC 9(2)    VALUE  00.                
           05  K100-STATUS                PIC 9(2)    VALUE  00.                
           05  KQ50-STATUS                PIC 9(2)    VALUE  00.                
                                                                                
           05  WS-DETL-WRT-IND-F230       PIC X       VALUE 'N'.                
               88  DETAIL-REC-WRITTEN-F230            VALUE 'Y'.                
                                                                                
           05  WS-PAIRED-FLAG             PIC X       VALUE 'N'.                
           05  WS-FT-PREM                 PIC X       VALUE 'N'.                
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-READ-CNT-F800           PIC 9(5)    VALUE ZEROS.              
           05  WS-READ-CNT-F800-GEN3      PIC 9(5)    VALUE ZEROS.              
           05  WS-READ-TOTAL-CNT          PIC 9(5)    VALUE ZEROS.              
           05  WS-READ-CNT-KQ30           PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-TOTAL-CNT         PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-HEADER-F230   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-DETAIL-F230   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-TRAILER-F230  PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-BLOCK-F230    PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-RT05-F230     PIC 9(5)    VALUE ZEROS.              
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-PREV-F800-AIX1          PIC X(18)   VALUE SPACES.             
           05  WS-NT-NAME                 PIC X(20)   VALUE SPACES.             
           05  WS-CDE-NT                  PIC X(03)   VALUE SPACES.             
           05  WS-NME-PROJECT-LTR         PIC X(40)   VALUE SPACES.             
           05  WS-ZONE-NAME               PIC X(10)   VALUE SPACES.             
           05  WS-F800-CDE-SELTY          PIC X(03)   VALUE SPACES.             
           05  WS-F800-NUM-BONUS          PIC X(01)   VALUE SPACES.             
           05  WS-NUM-NT-ZONE             PIC X(03)   VALUE SPACES.             
           05  WS-DTE-END.                                                      
               10  WS-DTE-END-YY          PIC X(04).                            
               10  WS-DTE-END-MM          PIC X(02).                            
               10  WS-DTE-END-DD          PIC X(02).                            
           05  WS-ALLOC-MONTH-END         PIC X(02).                            
           05  WS-F800-CDE-ZONE           PIC X(02).                            
                                                                                
       01  WS-UNIT-NO.                                                          
           05 WS-MAIN-UNIT                PIC X(4)    VALUE SPACES.             
           05 WS-SUB-UNIT                 PIC X(1)    VALUE SPACES.             
                                                                                
       01  WS-F800-EST-FT.                                                      
           05 WS-F800-AIX1                PIC X(16)   VALUE SPACES.             
           05 WS-F800-FT                  PIC X(2)    VALUE SPACES.             
                                                                                
       01  WS-F800-NUM-BLK                PIC X(5)    VALUE SPACES.             
       01  WS-F800-DTE-BALLOT             PIC X(6)    VALUE SPACES.             
                                                                                
       01  WS-PREV-ALLOCN-DATE            PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-F230-NUM-BTO-ZONE           PIC X(3)    VALUE SPACES.             
                                                                                
       01  WS-F800-NUM-FLAT-TYPE          PIC X(2)    VALUE SPACES.             
       01  WS-F230-NUM-FLAT-TYPE          PIC X(2)    VALUE SPACES.             
       01  WS-F230-NUM-FLAT-TYPE2         PIC X(9)    VALUE SPACES.             
       01  WS-PAIRED-FLAT-TYPE            PIC X(9)    VALUE SPACES.             
                                                                                
       01  WS-F230-NUM-DESIGN             PIC X(4)    VALUE SPACES.             
       01  WS-F230-NUM-DESIGN-3R          PIC X(4)    VALUE SPACES.             
                                                                                
       01  WS-DATE                        PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-TIME                        PIC X(8)    VALUE SPACES.             
       01  WS-F800-STREET-NAME            PIC X(32)   VALUE SPACES.             
       01  WS-F800-NEIGHBOURHOOD          PIC X(04)   VALUE SPACES.             
       01  WS-F800-CONTRACT-NO            PIC X(04)   VALUE SPACES.             
       01  WS-F800-DTE-DLVRY-POSSESSION   PIC X(08)   VALUE SPACES.             
       01  WS-F800-DTE-LEASE-COMM         PIC X(08)   VALUE SPACES.             
       01  WS-F800-DTE-BLK-KEY-AVAIL      PIC X(08)   VALUE SPACES.             
       01  WS-F800-NUM-UPG-STATUS         PIC X(03)   VALUE SPACES.             
       01  WS-F800-NUM-BTO-NEW-CONTRACT   PIC X(01)   VALUE SPACES.             
       01  WS-F800-NUM-COMBINE-SA         PIC X(01)   VALUE SPACES.             
                                                                                
       01  WS-NEIGH                       PIC X(04) VALUE SPACES.               
       01  WS-CNTRCT                      PIC X(04) VALUE SPACES.               
                                                                                
       01  WS-LEFT-JUST-TXT               PIC X(04) VALUE SPACES.               
       01  WS-RIGHT-JUST-TXT              PIC X(04) VALUE SPACES                
                                          JUST RIGHT.                           
       01  WS-CHR-CNT                     PIC 9(02) VALUE ZEROES.               
       01  WS-BLANK-CNT                   PIC 9(02) VALUE ZEROES.               
       01  WS-CHR-LEN                     PIC 9(02) VALUE ZEROES.               
       01  WS-START-POSN                  PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-NEI-CAT-PCT.                                                      
           05  WS-NEI-CAT-1-PCT          PIC V9(2).                             
           05  WS-NEI-CAT-1-PCT-X        REDEFINES  WS-NEI-CAT-1-PCT            
                                         PIC X(2).                              
           05  WS-NEI-CAT-2-PCT          PIC V9(2).                             
           05  WS-NEI-CAT-2-PCT-X        REDEFINES  WS-NEI-CAT-2-PCT            
                                         PIC X(2).                              
           05  WS-NEI-CAT-3-PCT          PIC V9(2).                             
           05  WS-NEI-CAT-3-PCT-X        REDEFINES  WS-NEI-CAT-3-PCT            
                                         PIC X(2).                              
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-FILES        THRU   100-EXIT.                      
           PERFORM  200-READ-BP13F205     THRU   200-EXIT                       
                        UNTIL WS-EOF-F205 = 'Y'.                                
           PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BM06K510                                                 
                       BM06K100                                                 
                       BP13K060                                                 
                       BP13K813                                                 
                       BP13K270                                                 
                       BP13K222                                                 
                       BP13KQ30                                                 
                       BP13KQ50                                                 
                OUTPUT BP13F230                                                 
                       P13F230B.                                                
                                                                                
           IF K510-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BM06K510 - OPEN ERROR, K510-STATUS ' K510-STATUS         
              MOVE     K510-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BM06K100 - OPEN ERROR, K100-STATUS ' K100-STATUS         
              MOVE     K100-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K270 - OPEN ERROR, K270-STATUS ' K270-STATUS         
              MOVE     K270-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K060 - OPEN ERROR, K060-STATUS ' K060-STATUS         
              MOVE     K060-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K813 - OPEN ERROR, K813-STATUS ' K813-STATUS         
              MOVE     K813-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K222-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K222 - OPEN ERROR, K222-STATUS ' K222-STATUS         
              MOVE     K222-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF KQ30-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13KQ30 - OPEN ERROR, KQ30-STATUS ' KQ30-STATUS         
              MOVE     KQ30-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF KQ50-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13KQ50 - OPEN ERROR, KQ50-STATUS ' KQ50-STATUS         
              MOVE     KQ50-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           MOVE ZEROS                       TO WS-COUNTERS-POINTERS.            
           MOVE FUNCTION CURRENT-DATE (1:8) TO WS-DATE.                         
           MOVE FUNCTION CURRENT-DATE (9:8) TO WS-TIME.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y'         TO     WS-EOF-F205                      
                       GO TO 200-EXIT.                                          
                                                                                
           ADD     1                    TO     WS-READ-TOTAL-CNT.               
                                                                                
           MOVE 'N'                     TO WS-K813-FND WS-K813-EOF.             
           MOVE SPACES                  TO BP13K813-REC.                        
           MOVE F205-NUM-NT-ZONE        TO WS-NUM-NT-ZONE.                      
           MOVE WS-NUM-NT-ZONE          TO K813-NUM-ZONE.                       
           PERFORM 890-START-BP13K813   THRU 890-EXIT.                          
           IF K813-NUM-FT-PREMIUM = 'P'                                         
              MOVE  'Y'                 TO WS-FT-PREM                           
           END-IF.                                                              
           IF K813-NUM-FLAT-TYPE = '4P' OR '5P'                                 
              MOVE 'Y'                  TO     WS-PAIRED-FLAG                   
              IF K813-NUM-FLAT-TYPE = '4P'                                      
                 MOVE '4-ROOM-SP'       TO WS-PAIRED-FLAT-TYPE                  
              ELSE                                                              
                 MOVE '5-ROOM-SP'       TO WS-PAIRED-FLAT-TYPE                  
              END-IF                                                            
           ELSE                                                                 
              MOVE 'N'                  TO WS-PAIRED-FLAG                       
           END-IF.                                                              
                                                                                
           PERFORM 300-PROCESS-RECORD THRU  300-EXIT.                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
           OPEN    INPUT BM06F800.                                              
           PERFORM 320-READ-BM06F800      THRU   320-EXIT.                      
           MOVE    'N'                    TO     WS-DETL-WRT-IND-F230.          
           MOVE    'N'                    TO     WS-EOF-F800.                   
           PERFORM 400-CHECK-NT-ZONE-ALLOCDTE THRU 400-EXIT                     
                       UNTIL WS-EOF-F800  = 'Y'.                                
           IF DETAIL-REC-WRITTEN-F230                                           
              PERFORM  600-WRITE-HEADER      THRU   600-EXIT                    
              PERFORM  800-WRITE-TRAILER     THRU   800-EXIT                    
              PERFORM  605-WRITE-REC05       THRU   605-EXIT                    
              PERFORM  850-WRITE-BLOCK       THRU   850-EXIT                    
           END-IF.                                                              
                                                                                
           CLOSE   BM06F800.                                                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       320-READ-BM06F800.                                                       
      *-------------------------------------------------------------            
           READ BM06F800                                                        
                AT END MOVE 'Y'  TO  WS-EOF-F800                                
                       GO TO 320-EXIT.                                          
                                                                                
           MOVE F800-AIX1 TO WS-F800-AIX1.                                      
           MOVE 'N'       TO WS-KQ30-FND.                                       
           IF WS-PAIRED-FLAG = 'Y'                                              
              IF F800-CDE-TYP-ACTUSE = '01' OR '02'                             
                 PERFORM 870-READ-BP13KQ30    THRU 870-EXIT                     
              ELSE                                                              
                 PERFORM 860-READ-BP13KQ30    THRU 860-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 350-DECODE-FLAT-TYPE THRU 350-EXIT.                          
                                                                                
           MOVE WS-F800-NUM-FLAT-TYPE TO WS-F800-FT.                            
                                                                                
           IF F800-AIX1 NOT = K100-KEY-FLD                                      
              MOVE LOW-VALUES              TO K100-KEY-FLD                      
              MOVE F800-AIX1               TO K100-KEY-FLD                      
              PERFORM 650-BM06K100-READ     THRU 650-EXIT                       
           END-IF.                                                              
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       340-INITIALISE.                                                          
      *-------------------------------------------------------------            
           MOVE    F800-AIX1             TO   WS-PREV-F800-AIX1 (1:16)          
                                              WS-F800-AIX1.                     
           MOVE    WS-F800-NUM-FLAT-TYPE TO   WS-PREV-F800-AIX1 (17:2)          
                                              WS-F800-FT.                       
           MOVE    F800-ESTATE           TO   K510-ESTCDE.                      
           MOVE    F800-CDE-SELTY        TO   WS-F800-CDE-SELTY.                
           IF F800-CDE-SELTY = 'SER'                                            
              PERFORM 897-GET-SERS-SITE  THRU 897-EXIT                          
           ELSE                                                                 
              IF F800-NUM-BONUS = ' '                                           
                 MOVE 'N'                   TO   WS-F800-NUM-BONUS              
              ELSE                                                              
                 MOVE    F800-NUM-BONUS     TO   WS-F800-NUM-BONUS              
              END-IF                                                            
           END-IF.                                                              
           PERFORM 500-READ-BM06K510   THRU   500-EXIT.                         
           MOVE    F800-DTE-ALLOCN       TO   WS-PREV-ALLOCN-DATE.              
           MOVE    F800-BLK-NO           TO   WS-F800-NUM-BLK.                  
           MOVE    F800-STREET-NAME      TO   WS-F800-STREET-NAME.              
           MOVE    F800-NEIGHBOURHOOD    TO   WS-F800-NEIGHBOURHOOD.            
           MOVE    F800-CONTRACT-NO      TO   WS-F800-CONTRACT-NO.              
           MOVE    F800-DTE-DLVRY-POSSESSION  TO                                
                   WS-F800-DTE-DLVRY-POSSESSION.                                
           MOVE    F800-NUM-BTO-NEW-CONTRACT  TO                                
                   WS-F800-NUM-BTO-NEW-CONTRACT.                                
           MOVE    F800-NUM-COMBINE-SA        TO                                
                   WS-F800-NUM-COMBINE-SA.                                      
           MOVE    F800-DTE-LEASE-COMM   TO   WS-F800-DTE-LEASE-COMM.           
           MOVE    F800-DTE-BLK-KEY-AVAIL TO WS-F800-DTE-BLK-KEY-AVAIL.         
           MOVE    F800-NUM-UPG-STATUS   TO  WS-F800-NUM-UPG-STATUS.            
           MOVE    F800-DTE-BALLOT       TO   WS-F800-DTE-BALLOT.               
           MOVE    F800-CDE-ZONE         TO   WS-F800-CDE-ZONE.                 
           MOVE    'N'                   TO   WS-DETL-WRT-IND-F230.             
                                                                                
           IF F800-CDE-SELTY = '3R ' AND                                        
              F800-STREET-NAME = 'YUAN CHING RD       '                         
              MOVE 'TAMAN JURONG        '     TO WS-NT-NAME                     
           END-IF.                                                              
                                                                                
           IF F800-CDE-SELTY = 'SER'                                            
              MOVE SPACES        TO WS-ZONE-NAME                                
           ELSE                                                                 
              EVALUATE F800-CDE-ZONE                                            
                 WHEN 'PG'                                                      
                      MOVE 'PUNGGOL    ' TO WS-ZONE-NAME                        
                 WHEN 'NN'                                                      
                      MOVE 'NORTH      ' TO WS-ZONE-NAME                        
                 WHEN 'WW'                                                      
                      MOVE 'WEST       ' TO WS-ZONE-NAME                        
                 WHEN 'NE'                                                      
                      MOVE 'NORTH-EAST ' TO WS-ZONE-NAME                        
                 WHEN 'EE'                                                      
                      MOVE 'EAST       ' TO WS-ZONE-NAME                        
                 WHEN OTHER                                                     
                      MOVE SPACES        TO WS-ZONE-NAME                        
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 360-DECODE-FLAT-TYPE-F230 THRU 360-EXIT.                     
           PERFORM 370-DECODE-MODL-DESG      THRU 370-EXIT.                     
                                                                                
       340-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-DECODE-FLAT-TYPE.                                                    
      *-------------------------------------------------------------            
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '1A'       TO WS-F800-NUM-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '1 '       TO WS-F800-NUM-FLAT-TYPE                    
                 END-IF                                                         
           WHEN '02'                                                            
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '2A'       TO WS-F800-NUM-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '2 '       TO WS-F800-NUM-FLAT-TYPE                    
                 END-IF                                                         
           WHEN '03'                                                            
                 MOVE '3 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '04'                                                            
                 MOVE '4 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '05'                                                            
                 MOVE '5 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '06'                                                            
           WHEN '08'                                                            
                 MOVE 'E '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN OTHER                                                           
                 MOVE '  '       TO WS-F800-NUM-FLAT-TYPE                       
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       360-DECODE-FLAT-TYPE-F230.                                               
      *-------------------------------------------------------------            
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
             IF F800-NUM-COMBINE-SA = 'Y'                                       
               IF F800-ESTATE = '943'                                           
                  MOVE 'SP'        TO WS-F230-NUM-FLAT-TYPE                     
               ELSE                                                             
                  MOVE 'SA'        TO WS-F230-NUM-FLAT-TYPE                     
               END-IF                                                           
                                                                                
               IF WS-KQ30-FND = 'Y'                                             
                  MOVE WS-PAIRED-FLAT-TYPE TO WS-F230-NUM-FLAT-TYPE2            
               ELSE                                                             
                  MOVE 'SA       ' TO WS-F230-NUM-FLAT-TYPE2                    
               END-IF                                                           
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '1A'        TO WS-F230-NUM-FLAT-TYPE                    
                   IF WS-KQ30-FND = 'Y'                                         
                      MOVE WS-PAIRED-FLAT-TYPE TO WS-F230-NUM-FLAT-TYPE2        
                   ELSE                                                         
                      MOVE 'SA 35SQM ' TO WS-F230-NUM-FLAT-TYPE2                
                   END-IF                                                       
                ELSE                                                            
                   MOVE '01'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '1-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                END-IF                                                          
             END-IF                                                             
           WHEN '02'                                                            
             IF F800-CDE-MODL-DESG  = '97' OR '98'                              
                MOVE 'SA'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'SA       ' TO WS-F230-NUM-FLAT-TYPE2                      
                GO TO 360-EXIT                                                  
             END-IF                                                             
             IF K100-NUM-SHORT-LEASE-FT = 'Y'                                   
                MOVE 'SA'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'SA       ' TO WS-F230-NUM-FLAT-TYPE2                      
                GO TO 360-EXIT                                                  
             END-IF                                                             
             IF F800-NUM-DESIGN-OFFER = 'F'                                     
                MOVE '02'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                      
                GO TO 360-EXIT                                                  
             END-IF                                                             
             IF F800-NUM-COMBINE-SA = 'Y'                                       
               IF F800-ESTATE = '943'                                           
                  MOVE 'SP'        TO WS-F230-NUM-FLAT-TYPE                     
               ELSE                                                             
                  MOVE 'SA'        TO WS-F230-NUM-FLAT-TYPE                     
               END-IF                                                           
                                                                                
               IF WS-KQ30-FND = 'Y'                                             
                  MOVE WS-PAIRED-FLAT-TYPE TO WS-F230-NUM-FLAT-TYPE2            
               ELSE                                                             
                  MOVE 'SA       ' TO WS-F230-NUM-FLAT-TYPE2                    
               END-IF                                                           
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '2A'        TO WS-F230-NUM-FLAT-TYPE                    
                   IF WS-KQ30-FND = 'Y'                                         
                      MOVE WS-PAIRED-FLAT-TYPE TO WS-F230-NUM-FLAT-TYPE2        
                   ELSE                                                         
                      MOVE 'SA 45SQM ' TO WS-F230-NUM-FLAT-TYPE2                
                   END-IF                                                       
                ELSE                                                            
                   IF F800-FLAT-STATUS = 'NW' OR 'CV'                           
                      IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR           
                                              '72' OR '73'                      
                         MOVE '2D'        TO WS-F230-NUM-FLAT-TYPE              
                         MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR        
                                                 '87' OR '88' OR '85'           
                            MOVE '2P'        TO WS-F230-NUM-FLAT-TYPE           
                            MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2          
                         ELSE                                                   
                            MOVE '02'        TO WS-F230-NUM-FLAT-TYPE           
                            MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2          
                         END-IF                                                 
                      END-IF                                                    
                   ELSE                                                         
                      PERFORM 361-DECODE-FT-2R THRU 361-EXIT                    
                      MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                
                   END-IF                                                       
                END-IF                                                          
             END-IF                                                             
           WHEN '03'                                                            
             IF (F800-FLAT-STATUS = 'NW' OR 'CV') OR                            
                (F800-DTE-HANDOVER >= '20000101')                               
                IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR                 
                                        '72' OR '73'                            
                   MOVE '3D'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR              
                                           '87' OR '88' OR '85'                 
                      MOVE '3P'        TO WS-F230-NUM-FLAT-TYPE                 
                      IF ((F800-NUM-SELECTION = 'SBF' AND NOT                   
                         K813-NUM-MATURE-EST-TAG = 'Y') OR                      
                         (F800-NUM-SELECTION = 'BTO' AND                        
                          WS-FT-PREM = 'Y'))                                    
                         MOVE '3P-ROOM  ' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      END-IF                                                    
                   ELSE                                                         
                      IF F800-NUM-DESIGN-OFFER = 'P' AND                        
                         K813-NUM-MATURE-EST-TAG = 'N'                          
                         MOVE '3P'        TO WS-F230-NUM-FLAT-TYPE              
                         MOVE '3P-ROOM  ' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         MOVE '03'        TO WS-F230-NUM-FLAT-TYPE              
                         MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
             ELSE                                                               
                PERFORM 362-DECODE-FT-3R THRU 362-EXIT                          
                MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                      
             END-IF                                                             
           WHEN '04'                                                            
             IF F800-ESTATE = '942'                                             
                MOVE 'S1'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'S1       ' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR                 
                                        '72' OR '73'                            
                   MOVE '4D'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '4-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR              
                                           '87' OR '88' OR '85'                 
                      MOVE '4P'        TO WS-F230-NUM-FLAT-TYPE                 
                      IF WS-KQ30-FND = 'Y'                                      
                         MOVE '4-ROOM-SA' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         MOVE '4-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      END-IF                                                    
                   ELSE                                                         
                      MOVE '04'        TO WS-F230-NUM-FLAT-TYPE                 
                      IF WS-KQ30-FND = 'Y'                                      
                         MOVE '4-ROOM-SA' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         MOVE '4-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
             END-IF                                                             
           WHEN '05'                                                            
             IF F800-ESTATE = '942'                                             
                MOVE 'S2'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'S2       ' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR                 
                                        '72' OR '73'                            
                   MOVE '5D'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '5-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR              
                                           '87' OR '88' OR '85'                 
                      MOVE '5P'        TO WS-F230-NUM-FLAT-TYPE                 
                      IF WS-KQ30-FND = 'Y'                                      
                         MOVE '5-ROOM-SA' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         MOVE '5-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      END-IF                                                    
                   ELSE                                                         
                      MOVE '05'        TO WS-F230-NUM-FLAT-TYPE                 
                      IF WS-KQ30-FND = 'Y'                                      
                         MOVE '5-ROOM-SA' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         MOVE '5-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
             END-IF                                                             
           WHEN '06'                                                            
           WHEN '08'                                                            
             IF F800-CDE-MODL-DESG = '70' OR '71' OR '34' OR                    
                                     '72' OR '73'                               
                MOVE '6D'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'EXECUTIVE' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR                 
                                        '87' OR '88' OR '85'                    
                   MOVE '6P'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE 'EXECUTIVE' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   MOVE '06'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE 'EXECUTIVE' TO WS-F230-NUM-FLAT-TYPE2                   
                END-IF                                                          
             END-IF                                                             
           WHEN OTHER                                                           
             MOVE F800-CDE-TYP-ACTUSE TO WS-F230-NUM-FLAT-TYPE                  
           END-EVALUATE.                                                        
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
       361-DECODE-FT-2R.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES       TO WS-F230-NUM-FLAT-TYPE.                          
                                                                                
           EVALUATE F800-CDE-MODL-DESG                                          
              WHEN '03'                                                         
              WHEN '13'                                                         
              WHEN '15'                                                         
              WHEN '21'                                                         
              WHEN '30'                                                         
              WHEN '37'                                                         
                 MOVE '2I'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN OTHER                                                        
                 MOVE '02'     TO WS-F230-NUM-FLAT-TYPE                         
           END-EVALUATE.                                                        
                                                                                
       361-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       362-DECODE-FT-3R.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES       TO WS-F230-NUM-FLAT-TYPE.                          
                                                                                
           EVALUATE F800-CDE-MODL-DESG                                          
              WHEN '02'                                                         
              WHEN '20'                                                         
              WHEN '28'                                                         
                 MOVE '03'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '03'                                                         
              WHEN '13'                                                         
              WHEN '15'                                                         
              WHEN '21'                                                         
              WHEN '30'                                                         
              WHEN '37'                                                         
                 MOVE '3I'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '04'                                                         
              WHEN '16'                                                         
              WHEN '17'                                                         
              WHEN '23'                                                         
                 MOVE '3N'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '05'                                                         
              WHEN '14'                                                         
              WHEN '18'                                                         
              WHEN '24'                                                         
              WHEN '29'                                                         
              WHEN '35'                                                         
                 MOVE '3A'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '06'                                                         
              WHEN '22'                                                         
                 MOVE '3S'     TO WS-F230-NUM-FLAT-TYPE                         
           END-EVALUATE.                                                        
                                                                                
       362-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       365-CHECK-MODL-DESG-2R.                                                  
      *-------------------------------------------------------------            
                                                                                
           EVALUATE F800-CDE-MODL-DESG                                          
              WHEN '03'                                                         
              WHEN '13'                                                         
              WHEN '15'                                                         
              WHEN '21'                                                         
              WHEN '30'                                                         
              WHEN '37'                                                         
                 MOVE '2IMP'   TO WS-F230-NUM-DESIGN-3R                         
              WHEN OTHER                                                        
                 MOVE '1STD'   TO WS-F230-NUM-DESIGN-3R                         
           END-EVALUATE.                                                        
                                                                                
       365-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       366-CHECK-MODL-DESG-3R.                                                  
      *-------------------------------------------------------------            
                                                                                
           EVALUATE F800-CDE-MODL-DESG                                          
              WHEN '02'                                                         
              WHEN '20'                                                         
              WHEN '28'                                                         
                 MOVE '1STD'   TO WS-F230-NUM-DESIGN-3R                         
              WHEN '03'                                                         
              WHEN '13'                                                         
              WHEN '15'                                                         
              WHEN '21'                                                         
              WHEN '30'                                                         
              WHEN '37'                                                         
                 MOVE '2IMP'   TO WS-F230-NUM-DESIGN-3R                         
              WHEN '04'                                                         
              WHEN '16'                                                         
              WHEN '17'                                                         
              WHEN '23'                                                         
                 MOVE '3NG '   TO WS-F230-NUM-DESIGN-3R                         
              WHEN '05'                                                         
              WHEN '14'                                                         
              WHEN '18'                                                         
              WHEN '24'                                                         
              WHEN '29'                                                         
              WHEN '35'                                                         
                 MOVE '4MOA'   TO WS-F230-NUM-DESIGN-3R                         
              WHEN '06'                                                         
              WHEN '22'                                                         
                 MOVE '5SIM'   TO WS-F230-NUM-DESIGN-3R                         
           END-EVALUATE.                                                        
                                                                                
       366-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       370-DECODE-MODL-DESG.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO WS-F230-NUM-DESIGN.                                   
           MOVE SPACES TO WS-F230-NUM-DESIGN-3R.                                
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
             WHEN '01'                                                          
               IF F800-ESTATE = '943' OR '942'                                  
                  MOVE '1PA '          TO WS-F230-NUM-DESIGN                    
               ELSE                                                             
                  IF F800-CDE-MODL-DESG = '89'                                  
                     MOVE '1PA '       TO WS-F230-NUM-DESIGN                    
                  ELSE                                                          
                     MOVE '3STD'       TO WS-F230-NUM-DESIGN                    
                  END-IF                                                        
               END-IF                                                           
             WHEN '02'                                                          
                IF F800-NUM-DESIGN-OFFER = 'F'                                  
                   MOVE '3STD'       TO WS-F230-NUM-DESIGN                      
                   GO TO 370-EXIT                                               
                END-IF                                                          
                IF F800-FLAT-STATUS = 'NW' OR 'CV'                              
                   IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR              
                                           '87' OR '88' OR '85' OR '89'         
                      MOVE '1PA '          TO WS-F230-NUM-DESIGN                
                   ELSE                                                         
                      IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR           
                                              '72' OR '73'                      
                         MOVE '2DBP'       TO WS-F230-NUM-DESIGN                
                      ELSE                                                      
                         IF F800-ESTATE = '942'                                 
                            MOVE '1PA '          TO WS-F230-NUM-DESIGN          
                         ELSE                                                   
                            MOVE '3STD'      TO WS-F230-NUM-DESIGN              
                         END-IF                                                 
                      END-IF                                                    
                   END-IF                                                       
                ELSE                                                            
                   PERFORM 365-CHECK-MODL-DESG-2R THRU 365-EXIT                 
                END-IF                                                          
             WHEN '03'                                                          
                IF (F800-FLAT-STATUS = 'NW' OR 'CV') OR                         
                   (F800-DTE-HANDOVER >= '20000101')                            
                   IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR              
                                           '87' OR '88' OR '85'                 
                      MOVE '1PA '          TO WS-F230-NUM-DESIGN                
                   ELSE                                                         
                      IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR           
                                              '72' OR '73'                      
                         MOVE '2DBP'       TO WS-F230-NUM-DESIGN                
                      ELSE                                                      
                         IF F800-ESTATE = '942'                                 
                            MOVE '1PA '          TO WS-F230-NUM-DESIGN          
                         ELSE                                                   
                             IF F800-NUM-DESIGN-OFFER = 'P'                     
                                MOVE '1PA '      TO WS-F230-NUM-DESIGN          
                             ELSE                                               
                                MOVE '3STD'      TO WS-F230-NUM-DESIGN          
                             END-IF                                             
                         END-IF                                                 
                     END-IF                                                     
                   END-IF                                                       
                ELSE                                                            
                   PERFORM 366-CHECK-MODL-DESG-3R THRU 366-EXIT                 
                END-IF                                                          
             WHEN '04'                                                          
             WHEN '05'                                                          
             WHEN '06'                                                          
                IF F800-CDE-TYP-ACTUSE = '04' AND                               
                                     F800-CDE-MODL-DESG = '36'                  
                   MOVE '3STB'             TO WS-F230-NUM-DESIGN                
                ELSE                                                            
                   IF F800-CDE-MODL-DESG = '81' OR '82' OR '86' OR              
                                           '87' OR '88' OR '85' OR '91'         
                      MOVE '1PA '          TO WS-F230-NUM-DESIGN                
                   ELSE                                                         
                      IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR           
                                              '72' OR '73'                      
                         MOVE '2DBP'       TO WS-F230-NUM-DESIGN                
                      ELSE                                                      
                         IF F800-ESTATE = '942'                                 
                            MOVE '1PA '          TO WS-F230-NUM-DESIGN          
                         ELSE                                                   
                            MOVE '3STD'       TO WS-F230-NUM-DESIGN             
                         END-IF                                                 
                     END-IF                                                     
                   END-IF                                                       
                END-IF                                                          
      *         IF  F800-CDE-TYP-ACTUSE = '05' AND                              
      *             F800-CDE-MODL-DESG  = '90' AND                              
      *             F800-DTE-BALLOT     = '201401' AND                          
      *             F800-ESTATE         = '748' AND                             
      *             F800-NEIGHBOURHOOD  = 'S3  ' AND                            
      *             F800-CONTRACT-NO    = 'C33 '                                
      *             MOVE '1PA '   TO WS-F230-NUM-DESIGN                         
      *         END-IF                                                          
           END-EVALUATE.                                                        
                                                                                
       370-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CHECK-NT-ZONE-ALLOCDTE.                                              
      *-------------------------------------------------------------            
                                                                                
           IF ((F205-NUM-NT-ZONE        = F800-CDE-NT)           OR             
               (F205-NUM-NT-ZONE (1:2)  = F800-CDE-ZONE))        AND            
               (F205-DTE-ALLOCN         = F800-DTE-ALLOCN)       AND            
              ((F205-NUM-FLAT-TYPE      = WS-F800-NUM-FLAT-TYPE) OR             
              ((F205-NUM-FLAT-TYPE      = 'SE')                  AND            
               (WS-F800-NUM-FLAT-TYPE   = '3 ' OR '4 ' OR '5 ' OR               
                                          '2 ' OR '1A' OR '2A')) OR             
              ((F205-NUM-NT-ZONE (1:2)  = 'EE' OR 'NN' OR 'WW' OR 'NE')         
                AND                                                             
               (WS-F800-NUM-FLAT-TYPE   = '1A' OR '2A' OR '2 ' OR               
                                          '3 '))                 OR             
              ((F205-NUM-NT-ZONE (1:2)  = '3R')                  AND            
               (WS-F800-NUM-FLAT-TYPE   = '1A' OR '2A' OR '2 ' OR               
                                          '3 '))                 OR             
              ((F800-NUM-COMBINE-SA     = 'Y')                   AND            
               (F205-NUM-FLAT-TYPE      = '1A' OR '2A')          AND            
               (WS-F800-NUM-FLAT-TYPE   = '1A' OR '2A')))                       
              PERFORM 420-PREPARE-DETAIL   THRU   420-EXIT                      
           ELSE                                                                 
              IF (F205-NUM-NT-ZONE   = F800-NUM-BTO-OPTION)   AND               
                 (F205-DTE-ALLOCN    = F800-DTE-ALLOCN)                         
                 PERFORM 420-PREPARE-DETAIL   THRU   420-EXIT                   
              ELSE                                                              
                 IF (F205-NUM-NT-ZONE   = F800-CDE-ZONE)   AND                  
                    (F205-DTE-ALLOCN    = F800-DTE-ALLOCN) AND                  
                    (F205-NUM-FLAT-TYPE = '00')                                 
                    PERFORM 420-PREPARE-DETAIL   THRU   420-EXIT                
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM    320-READ-BM06F800    THRU   320-EXIT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       420-PREPARE-DETAIL.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF F800-NUM-COMBINE-SA     = 'Y'                                     
              PERFORM 421-PREPARE-DETAIL-COMBINE THRU 421-EXIT                  
           ELSE                                                                 
              PERFORM 422-PREPARE-DETAIL-IND     THRU 422-EXIT                  
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       421-PREPARE-DETAIL-COMBINE.                                              
      *-------------------------------------------------------------            
                                                                                
           IF WS-F800-AIX1 NOT =  WS-PREV-F800-AIX1 (1:16)                      
              IF DETAIL-REC-WRITTEN-F230                                        
                 PERFORM   600-WRITE-HEADER     THRU  600-EXIT                  
                 PERFORM   800-WRITE-TRAILER    THRU  800-EXIT                  
                 PERFORM   850-WRITE-BLOCK      THRU  850-EXIT                  
                 PERFORM   605-WRITE-REC05      THRU  605-EXIT                  
                 PERFORM   340-INITIALISE       THRU  340-EXIT                  
                 MOVE      0                    TO WS-READ-CNT-F800             
                                                   WS-READ-CNT-F800-GEN3        
              ELSE                                                              
                 PERFORM   340-INITIALISE       THRU  340-EXIT                  
                 MOVE      0                    TO WS-READ-CNT-F800             
                                                   WS-READ-CNT-F800-GEN3        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-F800-AIX1 = WS-PREV-F800-AIX1 (1:16)                           
              ADD 1 TO WS-READ-CNT-F800                                         
              IF ((F800-CDE-TYP-ACTUSE = '05') AND                              
                  (F800-CDE-MODL-DESG = '90' OR '91' OR '95'))                  
                 ADD 1 TO WS-READ-CNT-F800-GEN3                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 700-WRITE-DETAIL THRU 700-EXIT.                              
                                                                                
       421-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       422-PREPARE-DETAIL-IND.                                                  
      *-------------------------------------------------------------            
                                                                                
           IF WS-F800-EST-FT NOT =  WS-PREV-F800-AIX1                           
              IF DETAIL-REC-WRITTEN-F230                                        
                 PERFORM   600-WRITE-HEADER     THRU  600-EXIT                  
                 PERFORM   800-WRITE-TRAILER    THRU  800-EXIT                  
                 PERFORM   850-WRITE-BLOCK      THRU  850-EXIT                  
                 PERFORM   605-WRITE-REC05      THRU  605-EXIT                  
                 PERFORM   340-INITIALISE       THRU  340-EXIT                  
                 MOVE      0                    TO WS-READ-CNT-F800             
                                                   WS-READ-CNT-F800-GEN3        
              ELSE                                                              
                 PERFORM   340-INITIALISE       THRU  340-EXIT                  
                 MOVE      0                    TO WS-READ-CNT-F800             
                                                   WS-READ-CNT-F800-GEN3        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-F800-EST-FT = WS-PREV-F800-AIX1                                
              ADD 1 TO WS-READ-CNT-F800                                         
              IF ((F800-CDE-TYP-ACTUSE = '05') AND                              
                  (F800-CDE-MODL-DESG = '90' OR '91' OR '95'))                  
                 ADD 1 TO WS-READ-CNT-F800-GEN3                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 700-WRITE-DETAIL THRU 700-EXIT.                              
       422-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-READ-BM06K510.                                                       
      *-------------------------------------------------------------            
           READ BM06K510.                                                       
                                                                                
           IF K510-STATUS = 00                                                  
              IF (F800-NUM-SELECTION = 'BTO')                                   
                 MOVE SPACES TO WS-NT-NAME                                      
                 MOVE SPACES TO K813-KEY-FLD                                    
                 MOVE 'N'    TO WS-K813-FND WS-K813-EOF                         
                 MOVE F800-NUM-BTO-OPTION TO WS-NUM-NT-ZONE                     
                 MOVE WS-NUM-NT-ZONE      TO K813-NUM-ZONE                      
                 PERFORM 890-START-BP13K813 THRU 890-EXIT                       
                 IF WS-K813-FND = 'Y'                                           
                    MOVE K813-NUM-ZONE      TO WS-F230-NUM-BTO-ZONE             
                    MOVE K813-NME-PROJECT-LTR TO WS-NME-PROJECT-LTR             
                 ELSE                                                           
                    DISPLAY 'BP13K813 - ZONE NOT FOUND: ' K813-KEY-FLD          
                 END-IF                                                         
              ELSE                                                              
                 MOVE SPACES         TO WS-NME-PROJECT-LTR                      
              END-IF                                                            
              MOVE K510-NT-NAME      TO WS-NT-NAME                              
              MOVE K510-ALPHA-NTCDE  TO WS-CDE-NT                               
           ELSE                                                                 
           IF K510-STATUS = 23                                                  
              MOVE SPACES         TO WS-NT-NAME WS-CDE-NT                       
                                     WS-NME-PROJECT-LTR                         
              DISPLAY 'READ BM06K510-ERROR ' K510-STATUS ' '                    
                                             K510-ESTCDE                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BM06K510, STATUS = ' K510-STATUS           
                      '      K510-ESTCDE    =  ' K510-ESTCDE                    
              MOVE     K510-STATUS          TO   RETURN-CODE                    
              CLOSE    BM06F800                                                 
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-WRITE-HEADER.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE       SPACES                  TO    F230-HEADER-REC.            
           INITIALIZE F230-HEADER-REC.                                          
           MOVE    'INTV'                     TO                                
                       F230-CDE-INTV          OF    F230-HEADER-REC.            
           MOVE    '01'                       TO    F230-CDE-HEADER             
                                              OF    F230-HEADER-REC.            
           MOVE     WS-PREV-ALLOCN-DATE       TO                                
                       F230-DTE-ALLOC         OF    F230-HEADER-REC.            
           MOVE     WS-F230-NUM-BTO-ZONE      TO                                
                       F230-NUM-BTO-ZONE      OF    F230-HEADER-REC.            
           MOVE     WS-F230-NUM-FLAT-TYPE     TO                                
                       F230-NUM-FLAT-TYPE     OF    F230-HEADER-REC.            
           MOVE     WS-F230-NUM-FLAT-TYPE2    TO                                
                       F230-NUM-FLAT-TYPE2    OF    F230-HEADER-REC             
                       F230-NUM-FLAT-TYPE3    OF    F230-HEADER-REC             
                       F230-NUM-FLAT-TYPE4    OF    F230-HEADER-REC             
                       F230-NUM-FLAT-TYPE5    OF    F230-HEADER-REC.            
           MOVE     WS-F800-NUM-BLK           TO                                
                       F230-NUM-BLK           OF    F230-HEADER-REC             
                       F230-NUM-BLK2          OF    F230-HEADER-REC             
                       F230-NUM-BLK3          OF    F230-HEADER-REC             
                       F230-NUM-BLK4          OF    F230-HEADER-REC.            
           MOVE     WS-NT-NAME                TO                                
                       F230-NUM-NEW-TOWN      OF    F230-HEADER-REC             
                       F230-NUM-NEW-TOWN2     OF    F230-HEADER-REC             
                       F230-NUM-NEW-TOWN3     OF    F230-HEADER-REC             
                       F230-NUM-NEW-TOWN4     OF    F230-HEADER-REC             
                       F230-NUM-NEW-TOWN5     OF    F230-HEADER-REC.            
           MOVE     WS-CDE-NT                 TO                                
                       F230-CDE-NT            OF    F230-HEADER-REC.            
           MOVE     WS-ZONE-NAME              TO                                
                       F230-NUM-ZONE          OF    F230-HEADER-REC             
                       F230-NUM-ZONE2         OF    F230-HEADER-REC             
                       F230-NUM-ZONE3         OF    F230-HEADER-REC.            
           MOVE     WS-F800-DTE-BALLOT        TO                                
                       F230-DTE-BALLOT        OF    F230-HEADER-REC             
                       F230-DTE-BALLOT5       OF    F230-HEADER-REC.            
                                                                                
           IF WS-F230-NUM-DESIGN-3R NOT = SPACES AND LOW-VALUES                 
              MOVE  WS-F230-NUM-DESIGN-3R    TO                                 
                    F230-NUM-DESIGN          OF     F230-DETAIL-REC             
              MOVE  SPACES                   TO                                 
                    F230-NUM-DESIGN2         OF     F230-DETAIL-REC             
           ELSE                                                                 
              MOVE  SPACES                   TO                                 
                    F230-NUM-DESIGN          OF     F230-DETAIL-REC             
              MOVE  WS-F230-NUM-DESIGN       TO                                 
                    F230-NUM-DESIGN2         OF     F230-DETAIL-REC             
           END-IF.                                                              
                                                                                
           MOVE     WS-DATE                   TO    F230-DTE-UPDATE.            
           MOVE     WS-TIME                   TO    F230-TME-UPDATE.            
           MOVE     WS-READ-CNT-F800          TO    F230-NUM-TOTAL OF           
                                                    F230-HEADER-REC.            
           MOVE     WS-READ-CNT-F800-GEN3     TO    F230-NUM-TOTAL-GEN3         
                                                    OF F230-HEADER-REC.         
           MOVE     WS-F800-CDE-SELTY         TO                                
                       F230-NUM-SELECTION     OF    F230-HEADER-REC             
                       F230-NUM-SELECTION2    OF    F230-HEADER-REC             
                       F230-NUM-SELECTION3    OF    F230-HEADER-REC             
                       F230-NUM-SELECTION4    OF    F230-HEADER-REC             
                       F230-NUM-SELECTION5    OF    F230-HEADER-REC.            
           MOVE     WS-F800-NUM-BONUS         TO                                
                       F230-NUM-BONUS         OF    F230-HEADER-REC             
                       F230-NUM-BONUS5        OF    F230-HEADER-REC.            
                                                                                
      ****>> STMNTS ADDED FOR STREET,CNTRCT DETAILS,EST COMPLN DATE             
      ******* LEASE COMMENCE DATE,KEY AVAIL DATE AND UPGRADE STATUS.            
           MOVE  WS-F800-STREET-NAME          TO F230-NME-STREET                
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-NEIGHBOURHOOD        TO F230-NUM-NEIGH                 
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-CONTRACT-NO          TO F230-NUM-CNTRCT                
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-DTE-DLVRY-POSSESSION TO F230-DTE-EST-CMPLTN            
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-DTE-LEASE-COMM       TO F230-DTE-LSE-COMM              
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-DTE-BLK-KEY-AVAIL    TO F230-DTE-KEY-AVAIL             
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-NUM-UPG-STATUS       TO F230-NUM-UPG-STATUS            
                                              OF F230-HEADER-REC.               
           IF    WS-F800-NUM-BTO-NEW-CONTRACT = 'Y'                             
                 MOVE WS-F800-NUM-BTO-NEW-CONTRACT                              
                 TO   F230-NUM-BTO-NEW-CONTRACT    OF F230-HEADER-REC           
           ELSE                                                                 
                 MOVE 'N'                                                       
                 TO   F230-NUM-BTO-NEW-CONTRACT    OF F230-HEADER-REC           
           END-IF.                                                              
           IF    WS-F800-NUM-COMBINE-SA = 'Y'                                   
                 MOVE WS-F800-NUM-COMBINE-SA                                    
                 TO   F230-NUM-COMBINE-SA          OF F230-HEADER-REC           
           ELSE                                                                 
                 MOVE 'N'                                                       
                 TO   F230-NUM-COMBINE-SA          OF F230-HEADER-REC           
           END-IF.                                                              
           PERFORM 601-GET-NEIGH4-CNTRCT4   THRU 601-EXIT.                      
           MOVE WS-NEIGH               TO F230-NUM-NEIGH4  OF                   
                                          F230-HEADER-REC.                      
           MOVE WS-CNTRCT              TO F230-NUM-CNTRCT4 OF                   
                                          F230-HEADER-REC.                      
           MOVE WS-PREV-F800-AIX1(1:3) TO F230-NUM-ESTATE  OF                   
                                          F230-HEADER-REC.                      
           MOVE     WS-PREV-ALLOCN-DATE       TO    K270-ALLOCN-DATE.           
           MOVE     WS-PREV-F800-AIX1 (1:16)  TO    K270-AIX1-KEY.              
           PERFORM  620-READ-BP13K270    THRU  620-EXIT.                        
                                                                                
           IF WS-READ-CNT-F800-GEN3 > ZEROES                                    
              PERFORM 896-GET-ETHNIC-PERCENT   THRU 896-EXIT                    
           END-IF.                                                              
                                                                                
           WRITE F230-HEADER-REC.                                               
                                                                                
           ADD    1   TO  WS-WRITE-CNT-HEADER-F230.                             
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       601-GET-NEIGH4-CNTRCT4.                                                  
      *---------------------------------------------------------------*         
           MOVE   4                    TO WS-CHR-LEN.                           
           MOVE SPACES                 TO WS-RIGHT-JUST-TXT                     
                                          WS-LEFT-JUST-TXT.                     
           IF WS-K100-FND = 'Y'                                                 
              IF K100-NEI-ORIG NOT = SPACES AND LOW-VALUES                      
      *20210506 NOT SURE WHY STILL NEED ORIGINAL NEIGH                          
      *         WE SHLD BE USING CIMS ENC FOR ALL TRANS                         
      *          MOVE K100-NEI-ORIG    TO WS-LEFT-JUST-TXT                      
                 MOVE WS-F800-NEIGHBOURHOOD  TO WS-LEFT-JUST-TXT                
              ELSE                                                              
                 MOVE WS-F800-NEIGHBOURHOOD  TO WS-LEFT-JUST-TXT                
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-F800-NEIGHBOURHOOD  TO WS-LEFT-JUST-TXT                   
           END-IF.                                                              
           PERFORM 603-RIGHT-JUSTIFY-TXT   THRU 603-EXIT.                       
           MOVE WS-RIGHT-JUST-TXT      TO WS-NEIGH.                             
                                                                                
           MOVE SPACES                 TO WS-RIGHT-JUST-TXT                     
                                          WS-LEFT-JUST-TXT.                     
           IF WS-K100-FND = 'Y'                                                 
              IF K100-CONT-ORIG NOT = SPACES AND LOW-VALUES                     
      *20220419 MISSED THIS OVERWRITING LAST YEAR...                            
      *         WE SHLD BE USING CIMS ENC FOR ALL TRANS                         
      *          MOVE K100-CONT-ORIG      TO WS-LEFT-JUST-TXT                   
                 MOVE WS-F800-CONTRACT-NO TO WS-LEFT-JUST-TXT                   
              ELSE                                                              
                 MOVE WS-F800-CONTRACT-NO TO WS-LEFT-JUST-TXT                   
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-F800-CONTRACT-NO    TO WS-LEFT-JUST-TXT                   
           END-IF.                                                              
           PERFORM 603-RIGHT-JUSTIFY-TXT   THRU 603-EXIT.                       
           MOVE WS-RIGHT-JUST-TXT      TO WS-CNTRCT.                            
                                                                                
       601-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       603-RIGHT-JUSTIFY-TXT.                                                   
      *---------------------------------------------------------------*         
           MOVE ZEROES   TO WS-CHR-CNT, WS-START-POSN.                          
           INSPECT WS-LEFT-JUST-TXT TALLYING WS-CHR-CNT                         
                  FOR CHARACTERS BEFORE INITIAL SPACE.                          
           COMPUTE WS-START-POSN = (WS-CHR-LEN  -  WS-CHR-CNT) + 1              
           MOVE WS-LEFT-JUST-TXT(1:WS-CHR-CNT)  TO                              
                WS-RIGHT-JUST-TXT.                                              
                                                                                
       603-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       605-WRITE-REC05.                                                         
      *-------------------------------------------------------------            
                                                                                
      **** >>> WRITE RECORD TYPE 05 <<< ***                                     
           MOVE       SPACES                  TO  F230-START-END-REC.           
           INITIALIZE F230-START-END-REC.                                       
           MOVE    'INTV'                     TO                                
                       F230-CDE-INTV          OF  F230-START-END-REC.           
           MOVE    '05'                       TO  F230-CDE-START-END            
                                              OF  F230-START-END-REC.           
           MOVE     WS-F230-NUM-FLAT-TYPE     TO                                
                       F230-NUM-FLAT-TYPE     OF  F230-START-END-REC.           
           MOVE     WS-NT-NAME                TO                                
                       F230-NUM-NEW-TOWN      OF  F230-START-END-REC.           
           MOVE     WS-ZONE-NAME              TO                                
                       F230-NUM-ZONE          OF  F230-START-END-REC.           
           MOVE     WS-F800-CDE-SELTY         TO                                
                       F230-NUM-SELECTION     OF  F230-START-END-REC.           
                                                                                
           MOVE   F205-NUM-NT-ZONE      TO K222-NUM-NT-ZONE.                    
           MOVE   F205-NUM-FLAT-TYPE    TO K222-NUM-FLAT-TYPE.                  
           MOVE   F205-DTE-ALLOCN       TO K222-DTE-ALLOC.                      
           PERFORM 610-READ-BP13K222  THRU 610-EXIT.                            
                                                                                
           WRITE  P13F230B-REC FROM F230-START-END-REC.                         
           ADD    1   TO  WS-WRITE-CNT-RT05-F230.                               
                                                                                
       605-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       610-READ-BP13K222.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K222.                                                       
                                                                                
           EVALUATE K222-STATUS                                                 
           WHEN 00                                                              
              MOVE  K222-DTE-START-BKAPPMT  TO F230-DTE-START                   
                                            OF F230-START-END-REC               
              IF F205-NUM-OPTION  =  'SE'                                       
                 MOVE  F205-DTE-END         TO F230-DTE-END                     
                                            OF F230-START-END-REC               
              ELSE                                                              
                 MOVE  K222-DTE-END-BKAPPMT TO F230-DTE-END                     
                                            OF F230-START-END-REC               
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              MOVE  ZEROES                  TO F230-DTE-START                   
                                            OF F230-START-END-REC,              
                                               F230-DTE-END                     
                                            OF F230-START-END-REC               
              DISPLAY 'BP13K222 - REC NOT FOUND: '                              
              DISPLAY '                NT ZONE : ' F205-NUM-NT-ZONE             
              DISPLAY '              FLAT TYPE : ' F205-NUM-FLAT-TYPE           
              DISPLAY '             DATE ALLOC : ' F205-DTE-ALLOCN              
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K222, STATUS = ' K222-STATUS           
              MOVE     K222-STATUS         TO  RETURN-CODE                      
              PERFORM  9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       620-READ-BP13K270.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K270.                                                       
                                                                                
           IF K270-STATUS = 00                                                  
              EVALUATE WS-F230-NUM-FLAT-TYPE                                    
                WHEN 'SA'                                                       
                WHEN 'SP'                                                       
                 COMPUTE F230-NUM-MAX-CAT1 =                                    
                         K270-NUM-MAX-CAT1 (1) +                                
                         K270-NUM-MAX-CAT1 (2)                                  
                                                                                
                 COMPUTE F230-NUM-MAX-CAT2 =                                    
                         K270-NUM-MAX-CAT2 (1) +                                
                         K270-NUM-MAX-CAT2 (2)                                  
                                                                                
                 COMPUTE F230-NUM-MAX-CAT3 =                                    
                         K270-NUM-MAX-CAT3 (1) +                                
                         K270-NUM-MAX-CAT3 (2)                                  
                                                                                
                 COMPUTE F230-NUM-MAX-SPR  =                                    
                         K270-NUM-MAX-SPR  (1) +                                
                         K270-NUM-MAX-SPR  (2)                                  
                WHEN '01'                                                       
                WHEN '1A'                                                       
                 MOVE  K270-NUM-MAX-CAT1 (1)  TO F230-NUM-MAX-CAT1              
                 MOVE  K270-NUM-MAX-CAT2 (1)  TO F230-NUM-MAX-CAT2              
                 MOVE  K270-NUM-MAX-CAT3 (1)  TO F230-NUM-MAX-CAT3              
                 MOVE  K270-NUM-MAX-SPR  (1)  TO F230-NUM-MAX-SPR               
                WHEN '02'                                                       
                WHEN '2S'                                                       
                WHEN '2I'                                                       
                WHEN '2N'                                                       
                WHEN '2A'                                                       
                WHEN '2P'                                                       
                WHEN '2D'                                                       
                 MOVE  K270-NUM-MAX-CAT1 (2)  TO F230-NUM-MAX-CAT1              
                 MOVE  K270-NUM-MAX-CAT2 (2)  TO F230-NUM-MAX-CAT2              
                 MOVE  K270-NUM-MAX-CAT3 (2)  TO F230-NUM-MAX-CAT3              
                 MOVE  K270-NUM-MAX-SPR  (2)  TO F230-NUM-MAX-SPR               
                WHEN '03'                                                       
                WHEN '3S'                                                       
                WHEN '3I'                                                       
                WHEN '3N'                                                       
                WHEN '3A'                                                       
                WHEN '3P'                                                       
                WHEN '3D'                                                       
                 MOVE  K270-NUM-MAX-CAT1 (3)  TO F230-NUM-MAX-CAT1              
                 MOVE  K270-NUM-MAX-CAT2 (3)  TO F230-NUM-MAX-CAT2              
                 MOVE  K270-NUM-MAX-CAT3 (3)  TO F230-NUM-MAX-CAT3              
                 MOVE  K270-NUM-MAX-SPR  (3)  TO F230-NUM-MAX-SPR               
                WHEN '04'                                                       
                WHEN '4D'                                                       
                WHEN '4P'                                                       
                WHEN 'S1'                                                       
                 COMPUTE F230-NUM-MAX-CAT1 =                                    
                       K270-NUM-MAX-CAT1 (4) + K270-NUM-MAX-CAT1 (5)            
                 COMPUTE F230-NUM-MAX-CAT2 =                                    
                       K270-NUM-MAX-CAT2 (4) + K270-NUM-MAX-CAT2 (5)            
                 COMPUTE F230-NUM-MAX-CAT3 =                                    
                       K270-NUM-MAX-CAT3 (4) + K270-NUM-MAX-CAT3 (5)            
                 COMPUTE F230-NUM-MAX-SPR  =                                    
                       K270-NUM-MAX-SPR  (4) + K270-NUM-MAX-SPR  (5)            
                WHEN '05'                                                       
                WHEN '5D'                                                       
                WHEN '5P'                                                       
                WHEN 'S2'                                                       
                 MOVE  K270-NUM-MAX-CAT1 (6)  TO F230-NUM-MAX-CAT1              
                 MOVE  K270-NUM-MAX-CAT2 (6)  TO F230-NUM-MAX-CAT2              
                 MOVE  K270-NUM-MAX-CAT3 (6)  TO F230-NUM-MAX-CAT3              
                 MOVE  K270-NUM-MAX-SPR  (6)  TO F230-NUM-MAX-SPR               
                WHEN '06'                                                       
                WHEN '6D'                                                       
                WHEN '6P'                                                       
                 COMPUTE F230-NUM-MAX-CAT1 =                                    
                       K270-NUM-MAX-CAT1 (7) + K270-NUM-MAX-CAT1 (9)            
                 COMPUTE F230-NUM-MAX-CAT2 =                                    
                       K270-NUM-MAX-CAT2 (7) + K270-NUM-MAX-CAT2 (9)            
                 COMPUTE F230-NUM-MAX-CAT3 =                                    
                       K270-NUM-MAX-CAT3 (7) + K270-NUM-MAX-CAT3 (9)            
                 COMPUTE F230-NUM-MAX-SPR  =                                    
                       K270-NUM-MAX-SPR  (7) + K270-NUM-MAX-SPR  (9)            
                WHEN OTHER                                                      
                 MOVE ZEROES                  TO F230-NUM-MAX-CAT1              
                                                 F230-NUM-MAX-CAT2              
                                                 F230-NUM-MAX-CAT3              
                                                 F230-NUM-MAX-SPR               
              END-EVALUATE                                                      
           ELSE                                                                 
           IF K270-STATUS = 23                                                  
              MOVE ZEROES                     TO F230-NUM-MAX-CAT1              
                                                 F230-NUM-MAX-CAT2              
                                                 F230-NUM-MAX-CAT3              
                                                 F230-NUM-MAX-SPR               
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K270, STATUS = ' K270-STATUS           
                      '      K270-AIX1-KEY = ' K270-AIX1-KEY                    
              MOVE     K270-STATUS         TO  RETURN-CODE                      
              CLOSE    BM06F800                                                 
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
       650-BM06K100-READ.                                                       
      *-----------------------------------------------------------              
                                                                                
           READ BM06K100.                                                       
           EVALUATE K100-STATUS                                                 
           WHEN 00                                                              
              MOVE 'Y'                   TO WS-K100-FND                         
           WHEN 23                                                              
              MOVE 'N'                   TO WS-K100-FND                         
              DISPLAY 'RECORD NOT FOUND BM06K100 : ' K100-KEY-FLD               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' K100-STATUS                   
              MOVE K100-STATUS            TO  RETURN-CODE                       
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-WRITE-DETAIL.                                                        
      *-------------------------------------------------------------            
           MOVE  SPACES                  TO F230-DETAIL-REC.                    
           MOVE  'INTV'                  TO                                     
                    F230-CDE-INTV        OF F230-DETAIL-REC.                    
           MOVE '02'                     TO F230-CDE-DETAIL.                    
           MOVE  F800-DTE-ALLOCN         TO                                     
                    F230-DTE-ALLOC       OF F230-DETAIL-REC.                    
           MOVE  WS-F230-NUM-FLAT-TYPE   TO                                     
                   F230-NUM-FLAT-TYPE    OF F230-DETAIL-REC.                    
           MOVE  WS-F230-NUM-FLAT-TYPE2  TO                                     
                   F230-NUM-FLAT-TYPE2   OF F230-DETAIL-REC                     
                   F230-NUM-FLAT-TYPE3   OF F230-DETAIL-REC                     
                   F230-NUM-FLAT-TYPE4   OF F230-DETAIL-REC.                    
           MOVE  WS-F800-NUM-BLK         TO                                     
                   F230-NUM-BLK          OF F230-DETAIL-REC                     
                   F230-NUM-BLK2         OF F230-DETAIL-REC                     
                   F230-NUM-BLK3         OF F230-DETAIL-REC                     
                   F230-NUM-BLK4         OF F230-DETAIL-REC.                    
           MOVE  F800-CDE-FLAT-STATUS    TO                                     
                   F230-NUM-BALANCE      OF F230-DETAIL-REC.                    
           MOVE  F800-SCH-ACC-NO         TO F230-NUM-SCH-ACC.                   
           MOVE  F800-LEVEL-NO           TO F230-NUM-LEVEL.                     
           MOVE  F800-LEVEL-NO           TO F230-NUM-LEVEL2                     
                                            F230-NUM-LEVEL3                     
                                            F230-NUM-LEVEL4.                    
           MOVE  F800-CURR-SELLING-PRICE TO                                     
                                          F230-CURR-SELLING-PRICE.              
           MOVE  F800-CDE-MODL-DESG      TO F230-CDE-MODL-DESG.                 
                                                                                
           MOVE  F800-MAIN-UNIT          TO WS-MAIN-UNIT.                       
           MOVE  F800-SUB-UNIT           TO WS-SUB-UNIT.                        
           MOVE  WS-UNIT-NO              TO F230-NUM-UNIT.                      
           MOVE  WS-UNIT-NO              TO F230-NUM-UNIT2                      
                                            F230-NUM-UNIT3                      
                                            F230-NUM-UNIT4.                     
           MOVE  WS-NT-NAME              TO                                     
                 F230-NUM-NEW-TOWN       OF F230-DETAIL-REC                     
                 F230-NUM-NEW-TOWN2      OF F230-DETAIL-REC                     
                 F230-NUM-NEW-TOWN3      OF F230-DETAIL-REC                     
                 F230-NUM-NEW-TOWN4      OF F230-DETAIL-REC.                    
           MOVE  WS-ZONE-NAME            TO                                     
                 F230-NUM-ZONE           OF F230-DETAIL-REC                     
                 F230-NUM-ZONE2          OF F230-DETAIL-REC                     
                 F230-NUM-ZONE3          OF F230-DETAIL-REC.                    
                                                                                
           MOVE SPACES      TO F230-NUM-GRO-WARD.                               
           MOVE WS-CDE-NT                TO                                     
                 F230-CDE-NT            OF    F230-DETAIL-REC.                  
                                                                                
           IF F800-CDE-SELTY = '3R '                                            
              MOVE 'N'         TO F230-NUM-RP                                   
           ELSE                                                                 
              IF F800-FLAT-STATUS = 'RP' OR                                     
                 (F800-FLAT-STATUS = 'ST' AND                                   
                 (F800-REPOSSESSED-CODE = '40' OR '20'))                        
                 MOVE 'Y'      TO F230-NUM-RP                                   
              ELSE                                                              
                 PERFORM 880-BP13KQ50-READ  THRU 880-EXIT                       
                 IF WS-KQ50-FND = 'Y'                                           
                    IF KQ50-NUM-MGPS = SPACES OR LOW-VALUES                     
                       MOVE 'G'            TO F230-NUM-RP                       
                    ELSE                                                        
                       MOVE KQ50-NUM-MGPS  TO F230-NUM-RP                       
                       MOVE KQ50-NUM-PARENT-FT TO F230-NUM-PARENT-FT            
                    END-IF                                                      
                    MOVE KQ50-NUM-WARD     TO F230-NUM-GRO-WARD                 
                 ELSE                                                           
                    IF F800-FLAT-STATUS = 'CV'                                  
                       MOVE 'V'      TO F230-NUM-RP                             
                       MOVE SPACES   TO F230-NUM-PARENT-FT                      
                    ELSE                                                        
                       MOVE 'N'      TO F230-NUM-RP                             
                       MOVE SPACES   TO F230-NUM-PARENT-FT                      
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           MOVE WS-F800-DTE-BALLOT           TO                                 
                    F230-DTE-BALLOT          OF     F230-DETAIL-REC             
           IF WS-F230-NUM-DESIGN-3R NOT = SPACES AND LOW-VALUES                 
              MOVE  WS-F230-NUM-DESIGN-3R    TO                                 
                    F230-NUM-DESIGN          OF     F230-DETAIL-REC             
              MOVE  SPACES                   TO                                 
                    F230-NUM-DESIGN2         OF     F230-DETAIL-REC             
           ELSE                                                                 
              MOVE  SPACES                   TO                                 
                    F230-NUM-DESIGN          OF     F230-DETAIL-REC             
              MOVE  WS-F230-NUM-DESIGN       TO                                 
                    F230-NUM-DESIGN2         OF     F230-DETAIL-REC             
           END-IF.                                                              
                                                                                
           MOVE  SPACES                  TO F230-NUM-ACCEPT                     
                                            F230-DTE-ACCEPT                     
                                            F230-TME-ACCEPT.                    
           MOVE  F800-CDE-SELTY          TO F230-NUM-SELECTION                  
                                         OF F230-DETAIL-REC                     
                                            F230-NUM-SELECTION2                 
                                         OF F230-DETAIL-REC                     
                                            F230-NUM-SELECTION3                 
                                         OF F230-DETAIL-REC                     
                                            F230-NUM-SELECTION4                 
                                         OF F230-DETAIL-REC.                    
           MOVE  F800-NUM-BONUS          TO F230-NUM-BONUS                      
                                         OF F230-DETAIL-REC.                    
                                                                                
           MOVE  F800-QTY-AREA-TOTAL     TO F230-QTY-AREA-TOTAL                 
                                         OF F230-DETAIL-REC.                    
                                                                                
           MOVE LOW-VALUES              TO K100-KEY-FLD.                        
           MOVE WS-F800-AIX1            TO K100-KEY-FLD.                        
           PERFORM 650-BM06K100-READ     THRU 650-EXIT.                         
           PERFORM 601-GET-NEIGH4-CNTRCT4   THRU 601-EXIT.                      
           MOVE WS-NEIGH               TO F230-NUM-NEIGH4  OF                   
                                          F230-DETAIL-REC.                      
           MOVE WS-CNTRCT              TO F230-NUM-CNTRCT4 OF                   
                                          F230-DETAIL-REC.                      
           MOVE WS-F800-NEIGHBOURHOOD  TO F230-NUM-NEIGH  OF                    
                                          F230-DETAIL-REC.                      
           MOVE WS-F800-CONTRACT-NO    TO F230-NUM-CNTRCT OF                    
                                          F230-DETAIL-REC.                      
                                                                                
           WRITE F230-DETAIL-REC.                                               
           ADD   1    TO  WS-WRITE-CNT-DETAIL-F230.                             
           MOVE 'Y'   TO  WS-DETL-WRT-IND-F230.                                 
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-WRITE-TRAILER.                                                       
      *-------------------------------------------------------------            
           MOVE  SPACES                TO F230-TRAILER-REC.                     
           MOVE 'INTV'                 TO                                       
                    F230-CDE-INTV      OF F230-TRAILER-REC.                     
           MOVE '03'                   TO F230-CDE-TRAILER.                     
           MOVE  WS-PREV-ALLOCN-DATE   TO                                       
                    F230-DTE-ALLOC     OF F230-TRAILER-REC.                     
           MOVE  WS-F800-NEIGHBOURHOOD TO F230-NUM-NEIGH                        
                                       OF F230-TRAILER-REC.                     
           MOVE  WS-F800-CONTRACT-NO   TO F230-NUM-CNTRCT                       
                                       OF F230-TRAILER-REC.                     
           MOVE  WS-F230-NUM-FLAT-TYPE TO                                       
                 F230-NUM-FLAT-TYPE    OF F230-TRAILER-REC.                     
           MOVE  WS-F230-NUM-FLAT-TYPE2 TO                                      
                 F230-NUM-FLAT-TYPE2   OF F230-TRAILER-REC                      
                 F230-NUM-FLAT-TYPE3   OF F230-TRAILER-REC                      
                 F230-NUM-FLAT-TYPE4   OF F230-TRAILER-REC                      
                 F230-NUM-FLAT-TYPE5   OF F230-TRAILER-REC.                     
           MOVE  WS-F800-NUM-BLK       TO                                       
                 F230-NUM-BLK          OF F230-TRAILER-REC                      
                 F230-NUM-BLK2         OF F230-TRAILER-REC                      
                 F230-NUM-BLK3         OF F230-TRAILER-REC                      
                 F230-NUM-BLK4         OF F230-TRAILER-REC.                     
           MOVE  WS-NT-NAME            TO                                       
                 F230-NUM-NEW-TOWN     OF F230-TRAILER-REC                      
                 F230-NUM-NEW-TOWN2    OF F230-TRAILER-REC                      
                 F230-NUM-NEW-TOWN3    OF F230-TRAILER-REC                      
                 F230-NUM-NEW-TOWN4    OF F230-TRAILER-REC                      
                 F230-NUM-NEW-TOWN5    OF F230-TRAILER-REC.                     
           MOVE  WS-ZONE-NAME          TO                                       
                 F230-NUM-ZONE         OF F230-TRAILER-REC                      
                 F230-NUM-ZONE2        OF F230-TRAILER-REC                      
                 F230-NUM-ZONE3        OF F230-TRAILER-REC.                     
                                                                                
           IF WS-F230-NUM-DESIGN-3R NOT = SPACES AND LOW-VALUES                 
              MOVE  WS-F230-NUM-DESIGN-3R    TO                                 
                    F230-NUM-DESIGN          OF     F230-DETAIL-REC             
              MOVE  SPACES                   TO                                 
                    F230-NUM-DESIGN2         OF     F230-DETAIL-REC             
           ELSE                                                                 
              MOVE  SPACES                   TO                                 
                    F230-NUM-DESIGN          OF     F230-DETAIL-REC             
              MOVE  WS-F230-NUM-DESIGN       TO                                 
                    F230-NUM-DESIGN2         OF     F230-DETAIL-REC             
           END-IF.                                                              
                                                                                
           MOVE     WS-F800-CDE-SELTY         TO                                
                       F230-NUM-SELECTION     OF    F230-TRAILER-REC            
                       F230-NUM-SELECTION2    OF    F230-TRAILER-REC            
                       F230-NUM-SELECTION3    OF    F230-TRAILER-REC            
                       F230-NUM-SELECTION4    OF    F230-TRAILER-REC            
                       F230-NUM-SELECTION5    OF    F230-TRAILER-REC.           
                                                                                
           MOVE     WS-F800-DTE-BALLOT        TO                                
                       F230-DTE-BALLOT        OF    F230-TRAILER-REC            
                       F230-DTE-BALLOT5       OF    F230-TRAILER-REC.           
                                                                                
           MOVE WS-NEIGH               TO F230-NUM-NEIGH4  OF                   
                                          F230-TRAILER-REC.                     
           MOVE WS-CNTRCT              TO F230-NUM-CNTRCT4 OF                   
                                          F230-TRAILER-REC.                     
           WRITE F230-TRAILER-REC.                                              
                                                                                
           ADD   1 TO WS-WRITE-CNT-TRAILER-F230.                                
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-WRITE-BLOCK.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE       SPACES                  TO    F230-BLOCK-REC.             
           INITIALIZE F230-BLOCK-REC.                                           
           MOVE    'INTV'                     TO                                
                       F230-CDE-INTV          OF    F230-BLOCK-REC.             
           MOVE    '06'                       TO    F230-CDE-BLOCK              
                                              OF    F230-BLOCK-REC.             
           MOVE     WS-PREV-ALLOCN-DATE       TO                                
                       F230-DTE-ALLOC         OF    F230-BLOCK-REC.             
           MOVE     WS-NT-NAME                TO                                
                       F230-NUM-NEW-TOWN      OF    F230-BLOCK-REC.             
           MOVE     WS-F800-DTE-BALLOT        TO                                
                       F230-DTE-BALLOT        OF    F230-BLOCK-REC.             
           MOVE     WS-F800-CDE-SELTY         TO                                
                       F230-NUM-SELECTION     OF    F230-BLOCK-REC.             
           MOVE     WS-F800-NUM-BLK           TO                                
                       F230-NUM-BLK           OF    F230-BLOCK-REC.             
                                                                                
           MOVE  WS-F800-STREET-NAME          TO F230-NME-STREET                
                                              OF F230-BLOCK-REC.                
           MOVE  WS-NME-PROJECT-LTR           TO F230-NME-PROJECT               
                                              OF F230-BLOCK-REC.                
           IF WS-F800-CDE-SELTY = 'BTO' OR 'SER'                                
              MOVE 'A'                        TO F230-NUM-DISPLAY               
                                              OF F230-BLOCK-REC                 
           ELSE                                                                 
              MOVE 'B'                        TO F230-NUM-DISPLAY               
                                              OF F230-BLOCK-REC                 
           END-IF.                                                              
                                                                                
           WRITE P13F230B-REC FROM F230-BLOCK-REC.                              
                                                                                
           ADD    1   TO  WS-WRITE-CNT-BLOCK-F230.                              
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       860-READ-BP13KQ30.                                                       
      *-------------------------------------------------------------            
           MOVE 'N'                      TO WS-KQ30-FND.                        
           MOVE SPACES                   TO KQ30-ALT-KEY1.                      
           MOVE F800-KEY-FLD             TO KQ30-ALT-KEY1.                      
                                                                                
           READ BP13KQ30 KEY IS KQ30-ALT-KEY1.                                  
                                                                                
           EVALUATE KQ30-STATUS                                                 
           WHEN 00                                                              
              ADD  1       TO   WS-READ-CNT-KQ30                                
              MOVE 'Y'     TO   WS-KQ30-FND                                     
           WHEN 23                                                              
              CONTINUE                                                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KQ30 : ' KQ30-STATUS                   
              MOVE KQ30-STATUS            TO  RETURN-CODE                       
              PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       860-EXIT.                                                                
           EXIT.                                                                
                                                                                
       870-READ-BP13KQ30.                                                       
      *-------------------------------------------------------------            
           MOVE 'N'                      TO WS-KQ30-FND.                        
           MOVE SPACES                   TO KQ30-KEY-FLD.                       
           MOVE F800-KEY-FLD             TO KQ30-KEY-FLD.                       
                                                                                
           READ BP13KQ30.                                                       
                                                                                
           EVALUATE KQ30-STATUS                                                 
           WHEN 00                                                              
              ADD  1       TO   WS-READ-CNT-KQ30                                
              MOVE 'Y'     TO   WS-KQ30-FND                                     
           WHEN 23                                                              
              CONTINUE                                                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KQ30 : ' KQ30-STATUS                   
              MOVE KQ30-STATUS            TO  RETURN-CODE                       
              PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       870-EXIT.                                                                
           EXIT.                                                                
                                                                                
       880-BP13KQ50-READ.                                                       
      *-----------------------------------------------------------              
           MOVE 'N'                  TO WS-KQ50-FND.                            
           MOVE LOW-VALUES           TO KQ50-KEY-FLD.                           
           MOVE F800-ESTATE          TO KQ50-NUM-ESTATE.                        
           MOVE F800-NEIGHBOURHOOD   TO KQ50-NUM-NGHBRD.                        
           MOVE F800-CONTRACT-NO     TO KQ50-NUM-CNTRCT.                        
           MOVE F800-BLK-NO          TO KQ50-NUM-BLOCK.                         
           MOVE F800-UNIT-NO         TO KQ50-NUM-UNIT.                          
                                                                                
           READ BP13KQ50.                                                       
           EVALUATE KQ50-STATUS                                                 
           WHEN 00                                                              
              IF KQ50-DTE-BALLOT = F800-NUM-ALLOCN-QTR                          
                 MOVE  'Y'        TO WS-KQ50-FND                                
              ELSE                                                              
                 MOVE  'N'        TO WS-KQ50-FND                                
              END-IF                                                            
           WHEN 23                                                              
              MOVE  'N'        TO WS-KQ50-FND                                   
              DISPLAY 'RECORD NOT FOUND BP13KQ50 : ' KQ50-KEY-FLD               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KQ50 : ' KQ50-STATUS                   
              MOVE KQ50-STATUS            TO  RETURN-CODE                       
              PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       880-EXIT.                                                                
           EXIT.                                                                
                                                                                
       890-START-BP13K813.                                                      
      *-----------------------------------------------------------              
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD.                                  
                                                                                
           EVALUATE K813-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 895-READ-BP13K813  THRU 895-EXIT                    
                         UNTIL WS-K813-EOF = 'Y' OR                             
                               WS-K813-FND = 'Y'                                
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K813: ' K813-STATUS             
                            ' KEY = ' K813-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       890-EXIT.                                                                
           EXIT.                                                                
                                                                                
       895-READ-BP13K813.                                                       
      *-----------------------------------------------------------              
                                                                                
            READ  BP13K813 NEXT AT END                                          
                  MOVE 'Y'    TO  WS-K813-EOF.                                  
                                                                                
            EVALUATE K813-STATUS                                                
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K813-NUM-ZONE = WS-NUM-NT-ZONE                          
                        MOVE 'Y'      TO  WS-K813-FND                           
                     ELSE                                                       
                        MOVE 'Y'      TO  WS-K813-EOF                           
                     END-IF                                                     
                WHEN 10                                                         
                WHEN 23                                                         
                     MOVE 'Y'         TO  WS-K813-EOF                           
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K813 : ' K813-STATUS            
                             ' KEY = ' K813-KEY-FLD                             
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       895-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       896-GET-ETHNIC-PERCENT.                                                  
      *----------------------*                                                  
                                                                                
           PERFORM VARYING WS-INDEX FROM 1 BY 1 UNTIL WS-INDEX > 8              
              EVALUATE WS-ETHNIC-CODE (WS-INDEX)                                
              WHEN 'NC1'                                                        
                 MOVE WS-ETHNIC-PCT (WS-INDEX)  TO    WS-NEI-CAT-1-PCT-X        
              WHEN 'NC2'                                                        
                 MOVE WS-ETHNIC-PCT (WS-INDEX)  TO    WS-NEI-CAT-2-PCT-X        
              WHEN 'NC3'                                                        
                 MOVE WS-ETHNIC-PCT (WS-INDEX)  TO    WS-NEI-CAT-3-PCT-X        
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
       896-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       897-GET-SERS-SITE.                                                       
      *----------------------*                                                  
           EVALUATE F800-NEIGHBOURHOOD ALSO F800-CONTRACT-NO                    
             WHEN 'N1  ' ALSO  'C3  '                                           
               MOVE 'A'          TO  WS-F800-NUM-BONUS                          
             WHEN 'N1  ' ALSO  'C4  '                                           
               MOVE 'B'          TO  WS-F800-NUM-BONUS                          
             WHEN 'N1  ' ALSO  'C5  '                                           
               MOVE 'C'          TO  WS-F800-NUM-BONUS                          
             WHEN 'N1  ' ALSO  'C6  '                                           
               MOVE 'D'          TO  WS-F800-NUM-BONUS                          
             WHEN 'N1  ' ALSO  'C7  '                                           
               MOVE 'E'          TO  WS-F800-NUM-BONUS                          
             WHEN 'N4  ' ALSO  'RC30'                                           
               MOVE 'F'          TO  WS-F800-NUM-BONUS                          
             WHEN 'N4  ' ALSO  'C12 '                                           
               MOVE 'G'          TO  WS-F800-NUM-BONUS                          
             WHEN 'N8  ' ALSO  'C1  '                                           
               MOVE 'H'          TO  WS-F800-NUM-BONUS                          
           END-EVALUATE.                                                        
                                                                                
       897-EXIT.                                                                
           EXIT.                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C227 CONTROL LIST ----------*'.            
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F205 READ        = ' WS-READ-TOTAL-CNT.           
           DISPLAY 'NO OF F230 HEADER WRITTEN  = '                              
           WS-WRITE-CNT-HEADER-F230.                                            
           DISPLAY 'NO OF F230 DETAIL WRITTEN  = '                              
           WS-WRITE-CNT-DETAIL-F230.                                            
           DISPLAY 'NO OF F230 TRAILER WRITTEN = '                              
           WS-WRITE-CNT-TRAILER-F230.                                           
           DISPLAY 'NO OF F230 BLOCK   WRITTEN = '                              
           WS-WRITE-CNT-BLOCK-F230                                              
           DISPLAY 'NO OF F230 RTYPE05 WRITTEN = '                              
           WS-WRITE-CNT-RT05-F230.                                              
                                                                                
           CLOSE BP13F205                                                       
                 BM06K510                                                       
                 BP13K270                                                       
                 BP13K060                                                       
                 BP13K813                                                       
                 BP13K222                                                       
                 BP13KQ30                                                       
                 BP13F230                                                       
                 BM06K100                                                       
                 BP13KQ50                                                       
                 P13F230B.                                                      
                                                                                
           IF K510-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BM06K510 CLOSE ERROR, K510-STATUS ' K510-STATUS          
              MOVE     K510-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BM06K100 CLOSE ERROR, K100-STATUS ' K100-STATUS          
              MOVE     K100-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K270 CLOSE ERROR, K270-STATUS ' K270-STATUS          
              MOVE     K270-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K060 CLOSE ERROR, K060-STATUS ' K060-STATUS          
              MOVE     K060-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K813 CLOSE ERROR, K813-STATUS ' K813-STATUS          
              MOVE     K813-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K222-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K222 CLOSE ERROR, K222-STATUS ' K222-STATUS          
              MOVE     K222-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF KQ30-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13KQ30 CLOSE ERROR, KQ30-STATUS ' KQ30-STATUS          
              MOVE     KQ30-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF KQ50-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13KQ50 CLOSE ERROR, KQ50-STATUS ' KQ50-STATUS          
              MOVE     KQ50-STATUS      TO       RETURN-CODE.                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
