      *------------------------*                                                
       IDENTIFICATION DIVISION.                                         00010061
      *------------------------*                                                
       PROGRAM-ID.    BP13C388.                                         00020061
       AUTHOR.        MALC.                                             00030061
       DATE-WRITTEN.  11/01/03.                                         00040081
       DATE-COMPILED.                                                   00050061
                                                                                
      ***************************************************************** 00060061
      *                                                               * 00070061
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                         * 00080061
      *                                                               * 00090061
      *    SYSTEM ID : BP13                                           * 00100061
      *                                                               * 00110061
      *    OBJECTIVE : TO PRINT REQUEST FOR CANCELLATION OF SALES     * 00120061
      *                APPLICATION VIA INTERNET                       * 00130061
      *                                                               * 00220061
      *     INPUT/OUTPUT FILE:                                        * 00180061
      *      1. BP13K797                                              * 00190061
      *                                                               * 00210061
      *     OUTPUT:                                                   * 00180061
      *      1. BP13L388                                              * 00190061
      *      2. P13L388B                                              * 00190061
      *                                                               * 00210061
      ***************************************************************** 00240061
      * CHGE REQ BY   DATE   DESCRIPTION                              *         
      * -------- ---- ------ ---------------------------------------- *         
      * BP132163 MALC 110103 NEW PROGRAM                              * 00210061
      * BP135527 ESA1 031214 CATER FOR SENDING OF EMAIL INDIVIDUALLY  * 00210061
      * BP137099 MRN1 151217 EXPAND CANCEL REASON FROM 50 TO 160 BYTES*         
      * BP137170 MRN1 210218 REMOVE 'AFTER 1' WHEN PRINTING PRINT-REC-C         
      * BP137099 MRN1 260318 SEND TO DIFF EMAIL ADR WHEN NO CANCEL CASE,        
      *                      SEND WEEKLY EMAIL OF CASES FOR THE WEEK  *         
      * BP136829 ZS13 220219 ADD CRM NAME                             *         
      * BP137663 MRN1 130319 GET CRM NAME ONLY IF CASE IS TO BE PRINTED         
      ***************************************************************** 00060061
                                                                        00250061
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                            00260061
      *------------------------*                                                
       CONFIGURATION SECTION.                                           00270061
       SOURCE-COMPUTER. IBM-3090.                                       00280061
       OBJECT-COMPUTER. IBM-3090.                                       00290061
                                                                        00300061
       INPUT-OUTPUT SECTION.                                            00310061
                                                                        00320061
       FILE-CONTROL.                                                    00330061
           SELECT BP13K797 ASSIGN TO BP13K797                           00400061
                  ORGANIZATION       INDEXED                            00410061
                  ACCESS   MODE      SEQUENTIAL                         00420061
                  RECORD   KEY       K797-KEY-FLD                       00430061
                  FILE     STATUS    BP13K797-STATUS.                   00440061
                                                                        00450061
           SELECT BP13K022 ASSIGN TO BP13K022                           00400061
                  ORGANIZATION       INDEXED                            00410061
                  ACCESS   MODE      DYNAMIC                            00420061
                  RECORD   KEY       K022-KEY-FLD                       00430061
                  ALTERNATE KEY      K022-NUM-REGN                      00430061
                  FILE     STATUS    BP13K022-STATUS.                   00440061
                                                                        00450061
           SELECT BP13K024 ASSIGN TO BP13K024                           00400061
                  ORGANIZATION       INDEXED                            00410061
                  ACCESS   MODE      DYNAMIC                            00420061
                  RECORD   KEY       K024-KEY-FLD                       00430061
                  FILE     STATUS    BP13K024-STATUS.                   00440061
                                                                        00450061
           SELECT BP13L388 ASSIGN TO BP13L388.                                  
                                                                                
           SELECT P13L388B ASSIGN TO P13L388B.                                  
                                                                                
           SELECT P13L388C ASSIGN TO P13L388C.                                  
                                                                                
           SELECT P13L388D ASSIGN TO P13L388D.                                  
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                   00520061
      *------------------------*                                                
       FILE SECTION.                                                    00530061
                                                                        00540061
       FD   BP13K797                                                    00680061
            RECORD CONTAINS  500 CHARACTERS.                            00700061
       COPY BP13K797.                                                   00720061
                                                                        00730061
       FD   BP13K022                                                    00680061
            RECORD CONTAINS  100 CHARACTERS.                            00700061
       COPY BP13K022.                                                   00720061
                                                                        00730061
       FD   BP13K024                                                    00680061
            RECORD CONTAINS  100 CHARACTERS.                            00700061
       COPY BP13K024.                                                   00720061
                                                                        00730061
       FD   BP13L388                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC                  PIC X(132).                              
                                                                                
       FD   P13L388B                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC-B                PIC X(132).                              
                                                                                
       FD   P13L388C                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC-C                PIC X(132).                              
                                                                                
       FD   P13L388D                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC-D                PIC X(132).                              
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                         00760061
      *------------------------*                                                
                                                                                
       01  WS-FILE-ACCESS-STATUS.                                       00770061
           05  BP13K797-STATUS         PIC X(2)    VALUE SPACES.        00780061
           05  BP13K022-STATUS         PIC X(2)    VALUE SPACES.        00780061
           05  BP13K024-STATUS         PIC X(2)    VALUE SPACES.        00780061
                                                                        00820061
       01  WS-SWITCHES.                                                 00830061
           05  WS-K797-EOF             PIC X       VALUE 'N'.           00850079
           05  WS-WK-HEADER-PRINTED    PIC X       VALUE 'N'.           00850079
                                                                                
       01  WS-COUNTERS.                                                 01020061
           05 WS-K797-REC-PRINT        PIC 9(5)  VALUE ZEROES.          01030061
           05 WS-K797-REC-READ         PIC 9(5)  VALUE ZEROES.          01030061
           05 WS-K797-REC-REWR         PIC 9(5)  VALUE ZEROES.          01030061
           05 WS-PAGE-CNT              PIC 9(5)  VALUE ZEROS.                   
           05 PAGE-CTRL-CTR            PIC 9(6)  VALUE ZEROES.                  
           05 LINE-CTRL-CTR            PIC 9(5)  VALUE 60.                      
                                                                                
       01  WS-CURR-DATE.                                                        
           03  WS-CURR-YYYY.                                                    
               05 WS-CURR-CC              PIC 99.                               
               05 WS-CURR-YY              PIC 99.                               
           03  WS-CURR-MM                 PIC 99.                               
           03  WS-CURR-DD                 PIC 99.                               
                                                                                
       01  WS-DTE-CURRENT                 PIC 9(8)  VALUE ZEROES.               
       01  WS-DTE-REQUESTED               PIC 9(8)  VALUE ZEROES.               
                                                                                
       01 WS-WRP-CTR                        PIC  9(02).                         
       01 WS-WRP-CTR2                       PIC S9(03).                         
       01 WS-START-WRP-STRING               PIC  9(03).                         
       01 WS-END-WRP-STRING                 PIC  9(03).                         
       01 WS-MAX-STRING                     PIC  9(03).                         
       01 WS-DAYS                           PIC  9(03).                         
                                                                                
      *************************************                                     
      * VARIABLES USE TO WRAP STRING      *                                     
      * NOTE: MAX 3 LINES                 *                                     
      *       MAX 60 CHARACTERS PER LINE  *                                     
      *************************************                                     
      * VARIABLES NEEDED TO PASS BEFORE CALLING THE PROCEDURE                   
       01 WS-WRAP-REC.                                                          
          05 WS-UNWRAP-STRING               PIC  X(180).                        
          05 WS-WRP-TOTAL-LINE              PIC  9(02).                         
          05 WS-WRP-LEN-PER-LINE            PIC  9(03).                         
          05 WS-WRP-JUST-FLAG               PIC  X(01).                         
      * VARIABLES PASSED AFTER CALLING THE PROCEDURE                            
          05 WS-WRAP-STRING-ARRAY.                                              
             10 WS-WRAP-STRING OCCURS 3 TIMES PIC  X(60).                       
                                                                                
      *============================================================*            
      *         BP13L388  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L388-HEADER-1.                                                       
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  L388-HEADER-2.                                                       
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  SEND-ID         PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  L388-HEADER-3.                                                       
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-RCPT1      PIC X(71)  VALUE                                 
               'scismail@mailbox.hdb.gov.sg>'.                                  
                                                                                
       01  L388-HEADER-3A.                                                      
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-RCPT1      PIC X(71)  VALUE                                 
               'PEK_Soon_Kang@hdb.gov.sg>'.                                     
                                                                                
       01  L388-HEADER-3B.                                                      
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-RCPT1      PIC X(71)  VALUE                                 
               'CHAN_Soon_Lan@hdb.gov.sg>'.                                     
                                                                                
       01  L388-HEADER-WK1.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-RCPT1      PIC X(71)  VALUE                                 
               'Catherine_WL_TAN@hdb.gov.sg>'.                                  
                                                                                
       01  L388-HEADER-4.                                                       
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  L388-HEADER-5.                                                       
           05  FILLER          PIC X(05)   VALUE 'FROM:'.                       
           05  MAIL-SENDMAILID PIC X(21)   VALUE                                
               'SOC_SYSTEM@HDB.GOV.SG'.                                         
           05  FILLER          PIC X(106)  VALUE SPACES.                        
                                                                                
       01  L388-HEADER-6.                                                       
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  MAIL-RCPT5      PIC X(76)  VALUE                                 
               'scismail@mailbox.hdb.gov.sg>'.                                  
                                                                                
       01  L388-HEADER-6A.                                                      
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  MAIL-RCPT5      PIC X(76)  VALUE                                 
               'PEK_Soon_Kang@hdb.gov.sg>'.                                     
                                                                                
       01  L388-HEADER-6B.                                                      
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  MAIL-RCPT5      PIC X(76)  VALUE                                 
               'CHAN_Soon_Lan@hdb.gov.sg>'.                                     
                                                                                
       01  L388-HEADER-WK2.                                                     
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  MAIL-RCPT5      PIC X(76)  VALUE                                 
               'Catherine_WL_TAN@hdb.gov.sg>'.                                  
                                                                                
       01  L388-HEADER-7.                                                       
           05  FILLER          PIC X(10)  VALUE 'SUBJECT :'.                    
           05  MAIL-SUBJ       PIC X(76)  VALUE                                 
           'REQUEST FOR CANCELLATION OF SALES APPLICATION VIA INTERNET'.        
                                                                                
       01  L388-HEADER-WK3.                                                     
           05  FILLER          PIC X(10)  VALUE 'SUBJECT :'.                    
           05  MAIL-SUBJ       PIC X(76)  VALUE                                 
           'CASES WHICH REQUESTED TO CANCEL WITHIN LAST 7 DAYS        '.        
                                                                                
      *----------------*                                                        
      * HEADING LAYOUT *                                                        
      *----------------*                                                        
       01  L388-HDG1.                                                           
           05  FILLER              PIC X(08)     VALUE 'BP13L388'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(40)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T '.                      
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L388-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L388-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  L388-HDG2.                                                           
           03  FILLER              PIC X(37) VALUE SPACES.                      
           03  FILLER              PIC X(58) VALUE                              
           'REQUEST FOR CANCELLATION OF SALES APPLICATION VIA INTERNET'.        
                                                                                
       01  L388-HDG-LINE.                                                       
           03  FILLER              PIC X(37) VALUE SPACES.                      
           03  FILLER              PIC X(58) VALUE ALL '-'.                     
                                                                                
       01  L388-SUB-HDR1.                                                       
           05  FILLER              PIC X(17) value SPACES.                      
           03  FILLER              PIC X(24) VALUE                              
               'Sales Registration No : '.                                      
           03  L388-SUB-REGN       PIC X(08) VALUE SPACES.                      
                                                                                
       01  L388-SUB-HDR2.                                                       
           05  FILLER              PIC X(17) value SPACES.                      
           03  FILLER              PIC X(24) VALUE                              
               'Date of request       : '.                                      
           03  L388-SUB-DTE-REQ    PIC X(10) VALUE SPACES.                      
                                                                                
       01  L388-SUB-HDR3.                                                       
           05  FILLER              PIC X(17) value SPACES.                      
           03  FILLER              PIC X(24) VALUE                              
               'Time of request       : '.                                      
           03  L388-SUB-TIME-REQ   PIC X(10) VALUE SPACES.                      
                                                                                
       01  L388-SUB-HDR4.                                                       
           05  FILLER              PIC X(17) value SPACES.                      
           03  FILLER              PIC X(24) VALUE                              
               'CRM                   : '.                                      
           03  L388-SUB-CRM        PIC X(32) VALUE SPACES.                      
                                                                                
      **** TRANSFER HEADER                                                      
       01  L388-ERROR-MARK.                                                     
           03  FILLER              PIC X(05) VALUE SPACES.                      
           03  FILLER              PIC X(10) VALUE ALL '*'.                     
           03  L388-ERROR          PIC X(80) VALUE SPACES.                      
           03  FILLER              PIC X(10) VALUE ALL '*'.                     
                                                                                
       01  L388-HDG1-WK.                                                        
           05  FILLER              PIC X(48) VALUE                              
               ' S/N     Reg No.  Date Cancelled  Send Email Tag'.              
           05  FILLER              PIC X(84) VALUE SPACES.                      
                                                                                
       01  L388-HDG2-WK.                                                        
           05  FILLER              PIC X(48) VALUE                              
               '------  --------  --------------  --------------'.              
           05  FILLER              PIC X(84) VALUE SPACES.                      
                                                                                
      *---------------*                                                         
      * DETAIL LAYOUT *                                                         
      *---------------*                                                         
                                                                                
       01  L388-DTL1.                                                           
           05  FILLER              PIC X(17) value SPACES.                      
           05  FILLER              PIC X(02) value 'I '.                        
           05  L388-NAME           PIC X(66) VALUE  SPACES.                     
           05  FILLER              PIC X(09) VALUE  ' NRIC No '.                
           05  L388-NRIC           PIC X(09) VALUE  SPACES.                     
           05  FILLER              PIC X(08) VALUE  ' request'.                 
                                                                                
       01  L388-DTL1-LINE.                                                      
           05  FILLER              PIC X(19) value  SPACES.                     
           05  FILLER              PIC X(66) VALUE  ALL '_'.                    
           05  FILLER              PIC X(09) VALUE  SPACES.                     
           05  FILLER              PIC X(09) VALUE  ALL '_'.                    
                                                                                
       01  L388-DTL2.                                                           
           05  FILLER              PIC X(17) value  SPACES.                     
           05  FILLER              PIC X(38) VALUE                              
               'to cancel my/our application for a/an '.                        
           05  L388-FLAT           PIC X(02) VALUE  SPACES.                     
           05  FILLER              PIC X(09) VALUE  ' flat in '.                
           05  L388-EST-ZONE       PIC X(03) VALUE  SPACES.                     
           05  FILLER              PIC X(33) VALUE                              
               ' Zone/Estate via Registration No '.                             
           05  L388-REGN           PIC X(08) VALUE  SPACES.                     
           05  FILLER              PIC X(01) VALUE  '.'.                        
                                                                                
       01  L388-DTL2-LINE.                                                      
           05  FILLER              PIC X(55) value  SPACES.                     
           05  FILLER              PIC X(02) VALUE  ALL '_'.                    
           05  FILLER              PIC X(09) VALUE  SPACES.                     
           05  FILLER              PIC X(03) VALUE  ALL '_'.                    
           05  FILLER              PIC X(33) VALUE  SPACES.                     
           05  FILLER              PIC X(08) VALUE  ALL '_'.                    
                                                                                
       01  L388-DTL3.                                                           
           05  FILLER              PIC X(17) value SPACES.                      
           05  FILLER              PIC X(55) VALUE                              
           'I understand and accept that my request is irrevocable.'.           
                                                                                
       01  L388-DTL4.                                                           
           05  FILLER              PIC X(17) value SPACES.                      
           05  FILLER              PIC X(32) VALUE                              
               'The reason for cancellation is: '.                              
           05  L388-REASON         PIC X(60) VALUE  SPACES.                     
                                                                                
       01  L388-DTL5.                                                           
           05  FILLER              PIC X(49) VALUE SPACES.                      
           05  L388-REASON2        PIC X(60) VALUE  SPACES.                     
                                                                                
       01  L388-DTL6.                                                           
           05  FILLER              PIC X(49) VALUE SPACES.                      
           05  L388-REASON3        PIC X(60) VALUE  SPACES.                     
                                                                                
       01  L388-DTL-WK.                                                         
           05  WS-SNO              PIC 9(06) VALUE ZEROES.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  WS-WK-REGNO         PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  WS-WK-DTE-REQ       PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(11) VALUE SPACES.                      
           05  WS-WK-SEND-TAG      PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE SPACES.                      
           05  FILLER              PIC X(84) VALUE SPACES.                      
                                                                                
      *---------------*                                                         
      * FOOTER LAYOUT *                                                         
      *---------------*                                                         
                                                                                
       01  L388-FOOTER1.                                                        
           05  FILLER              PIC X(17) value SPACES.                      
           05  FILLER              PIC X(24) value                              
               'Telephone No (H)      : '.                                      
           05  L388-PHONE          PIC X(08) VALUE  SPACES.                     
                                                                                
       01  L388-FOOTER2.                                                        
           05  FILLER              PIC X(17) value SPACES.                      
           05  FILLER              PIC X(24)     value                          
               'Telephone No (O)      : '.                                      
           05  L388-PHONE-OFFICE   PIC X(08) VALUE  SPACES.                     
                                                                                
       01  L388-FOOTER3.                                                        
           05  FILLER              PIC X(17) value SPACES.                      
           05  FILLER              PIC X(24)     value                          
               'Handphone No          : '.                                      
           05  L388-HAND-PHONE     PIC X(08) VALUE  SPACES.                     
                                                                                
       01  L388-FOOTER4.                                                        
           05  FILLER              PIC X(17) VALUE SPACES.                      
           05  FILLER              PIC X(24) VALUE                              
               'Email                 : '.                                      
           05  L388-EMAIL          PIC X(40) VALUE  SPACES.                     
                                                                                
       01  L388-FOOTER5.                                                        
           05  FILLER              PIC X(01) VALUE '.'.                         
                                                                                
       01  L388-FOOTER6.                                                        
           05  FILLER              PIC X(132) VALUE SPACES.                     
                                                                                
      **** CONTROL REPORT                                                       
       01  L388-PRINT-STATUS.                                                   
           05  L388-PRT-STAT       PIC X(110) VALUE ZEROES.                     
                                                                                
       01  L388-CTRL-HDG.                                                       
           05  FILLER              PIC X(28) VALUE SPACES.                      
           05  FILLER              PIC X(64) VALUE                              
           'CONTROL REPORT ON CANCELLATION OF SALES APPLICATION VIA INTE        
      -    'RNET'.                                                              
           05  FILLER              PIC X(28) VALUE SPACES.                      
                                                                                
       01  L388-HDG-CTRL-LINE.                                                  
           05  FILLER              PIC X(28) VALUE SPACES.                      
           05  FILLER              PIC X(64) VALUE ALL '-'.                     
           05  FILLER              PIC X(28) VALUE SPACES.                      
                                                                                
       01  L388-CTRL-SUB-HDG.                                                   
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(60) VALUE                              
               'SERIAL NO     REGN NO      DTE REQ      TME REQ'.               
                                                                                
       01  L388-CTRL-LINE.                                                      
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(60) VALUE                              
               '---------     -------      -------      -------'.               
                                                                                
       01  L388-CTRL-DTL.                                                       
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  L388-SER-NO         PIC ZZ,ZZZ,ZZZ.                              
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  L388-CTRL-REGN      PIC X(08).                                   
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  L388-CTRL-DTE-REQ   PIC X(10).                                   
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  L388-CTRL-TIME-REQ  PIC X(10).                                   
                                                                                
       01  LINK-REC.                                                            
           03  WS-IN-OPTION               PIC X(2).                             
           03  LINK-DATE.                                                       
               05  LINK-CC                PIC 99   VALUE ZEROS.                 
               05  LINK-YY                PIC 99   VALUE ZEROS.                 
               05  LINK-MM                PIC 99   VALUE ZEROS.                 
               05  LINK-DD                PIC 99   VALUE ZEROS.                 
           03  LINK-DAY-WEEK1             PIC X(3) VALUE SPACES.                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                              01060061
      ******************************************************************        
      *-----------------*                                               01080061
       000-MAIN-ROUTINE.                                                01100061
      *-----------------*                                               01080061
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                  01130061
           PERFORM 150-COMPUTE-DAY-OF-WEEK THRU 150-EXIT.                       
           PERFORM 200-PROCESS-BP13K797 THRU 200-EXIT                   01640061
                   UNTIL WS-K797-EOF = 'Y'.                                     
                                                                                
           IF WS-K797-REC-PRINT = ZEROES                                        
              MOVE SPACES TO BP13K797-REC                                       
                             BP13K024-REC                                       
              PERFORM 400-PRINT-HEADING    THRU 400-EXIT                        
                                                                                
              MOVE SPACES TO L388-PRT-STAT                                      
              MOVE '**** NO RECORD(S) TO PRINT ****' TO                         
                   L388-PRT-STAT                                                
              WRITE PRINT-REC FROM   L388-PRINT-STATUS   AFTER 5                
              PERFORM 810-PRINT-CTRL-HDG THRU 810-EXIT                          
              WRITE PRINT-REC-B FROM L388-PRINT-STATUS AFTER 5                  
           ELSE                                                                 
              MOVE SPACES TO L388-PRT-STAT                                      
              MOVE '             **** END OF REPORT ****' TO                    
                   L388-PRT-STAT                                                
              WRITE PRINT-REC-B FROM L388-PRINT-STATUS AFTER 2                  
           END-IF.                                                              
                                                                                
           IF WS-SNO = ZEROES                                                   
              MOVE 'NIL' TO PRINT-REC-D                                         
              WRITE PRINT-REC-D                                                 
           END-IF.                                                              
                                                                                
           PERFORM 999-CLOSE-FILES      THRU 999-EXIT.                  01300061
                                                                        01320061
       000-EXIT.                                                        01330061
           EXIT.                                                        01340061
                                                                        01350061
                                                                                
      *---------------*                                                         
       100-OPEN-FILES.                                                  01380061
      *---------------*                                                         
           OPEN INPUT  BP13K022                                                 
                       BP13K024                                                 
                I-O    BP13K797                                         01430061
               OUTPUT  BP13L388                                         01420061
                       P13L388B                                                 
                       P13L388C                                                 
                       P13L388D.                                                
                                                                        01440061
           IF BP13K797-STATUS  NOT = '00' AND '97'                      01500099
              DISPLAY 'OPEN ERROR FOR BP13K797 - ' BP13K797-STATUS      01510061
              MOVE BP13K797-STATUS TO RETURN-CODE                       01520061
              GO TO 999-CLOSE-FILES.                                    01530099
                                                                        01540061
           IF BP13K022-STATUS  NOT = '00' AND '97'                      01500099
              DISPLAY 'OPEN ERROR FOR BP13K022 - ' BP13K022-STATUS      01510061
              MOVE BP13K022-STATUS TO RETURN-CODE                       01520061
              GO TO 999-CLOSE-FILES.                                    01530099
                                                                        01540061
           IF BP13K024-STATUS  NOT = '00' AND '97'                      01500099
              DISPLAY 'OPEN ERROR FOR BP13K024 - ' BP13K024-STATUS      01510061
              MOVE BP13K024-STATUS TO RETURN-CODE                       01520061
              GO TO 999-CLOSE-FILES.                                    01530099
                                                                        01540061
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DD '/'  WS-CURR-MM  '/'  WS-CURR-YYYY                 
                  DELIMITED BY SIZE INTO L388-RUN-DATE.                         
                                                                                
       100-EXIT.                                                        01600061
           EXIT.                                                        01610061
                                                                                
      *-------------------------------------------------------------            
       150-COMPUTE-DAY-OF-WEEK.                                                 
      *-------------------------------------------------------------            
           MOVE 'DW'              TO WS-IN-OPTION.                              
           MOVE WS-CURR-DD        TO LINK-DD.                                   
           MOVE WS-CURR-MM        TO LINK-MM.                                   
           MOVE WS-CURR-YY        TO LINK-YY.                                   
           IF WS-CURR-YY   < 90                                                 
              MOVE 20 TO LINK-CC                                                
           ELSE                                                                 
              MOVE 19 TO LINK-CC                                                
           END-IF.                                                              
                                                                                
           DISPLAY 'LINK-DATE :' LINK-REC.                                      
                                                                                
           MOVE SPACES            TO LINK-DAY-WEEK1.                            
                                                                                
           CALL  'AV02C001' USING LINK-REC.                                     
                                                                                
           DISPLAY 'LINK-DATE :' LINK-REC.                                      
                                                                                
           MOVE 'N' TO WS-WK-HEADER-PRINTED.                                    
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       200-PROCESS-BP13K797.                                            01750061
      *---------------------*                                                   
           READ BP13K797 AT END MOVE 'Y' TO WS-K797-EOF                 01760061
                            GO TO 200-EXIT.                                     
                                                                                
           EVALUATE BP13K797-STATUS                                             
             WHEN 00                                                            
                ADD 1    TO WS-K797-REC-READ                                    
                PERFORM 300-PRINT-BP13K797   THRU 300-EXIT                      
             WHEN 10                                                            
                MOVE 'Y' TO WS-K797-EOF                                         
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING BP13K797: ' BP13K797-STATUS              
                MOVE BP13K797-STATUS     TO        RETURN-CODE                  
                STRING 'ERROR READING BP13K797: ' BP13K797-STATUS               
                       DELIMITED BY SIZE INTO L388-ERROR                        
                WRITE PRINT-REC FROM     L388-ERROR-MARK AFTER 1                
                PERFORM 999-CLOSE-FILES      THRU 999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       200-EXIT.                                                        01860061
           EXIT.                                                        01870061
                                                                        01880061
      *---------------------*                                                   
       210-GET-CRM.                                                     01750061
      *---------------------*                                                   
           READ BP13K022 KEY IS K022-NUM-REGN                           01760061
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                MOVE SPACES             TO BP13K024-REC                         
                IF K022-NUM-OIC-APPL NOT = SPACES AND LOW-VALUES                
                   MOVE K022-NUM-OIC-APPL    TO K024-NUM-OIC                    
                ELSE                                                            
                   MOVE K022-NUM-OIC         TO K024-NUM-OIC                    
                END-IF                                                          
                PERFORM 220-READ-BP13K024    THRU 220-EXIT                      
             WHEN 23                                                            
                 MOVE SPACES TO BP13K024-REC                                    
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING BP13K022: ' BP13K022-STATUS              
                MOVE BP13K022-STATUS     TO        RETURN-CODE                  
                STRING 'ERROR READING BP13K022: ' BP13K022-STATUS               
                       DELIMITED BY SIZE INTO L388-ERROR                        
                WRITE PRINT-REC FROM     L388-ERROR-MARK AFTER 1                
                PERFORM 999-CLOSE-FILES      THRU 999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       210-EXIT.                                                        01860061
           EXIT.                                                        01870061
                                                                        01880061
      *---------------------*                                                   
       220-READ-BP13K024.                                               01750061
      *---------------------*                                                   
           READ BP13K024                                                01760061
                                                                                
           EVALUATE BP13K024-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                 MOVE SPACES TO BP13K024-REC                                    
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING BP13K024: ' BP13K024-STATUS              
                MOVE BP13K024-STATUS     TO        RETURN-CODE                  
                STRING 'ERROR READING BP13K024: ' BP13K024-STATUS               
                       DELIMITED BY SIZE INTO L388-ERROR                        
                WRITE PRINT-REC FROM     L388-ERROR-MARK AFTER 1                
                PERFORM 999-CLOSE-FILES      THRU 999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       220-EXIT.                                                        01860061
           EXIT.                                                        01870061
                                                                        01880061
                                                                                
      *---------------------*                                                   
       300-PRINT-BP13K797.                                              01890061
      *---------------------*                                                   
           IF (K797-NUM-REGN = SPACES OR LOW-VALUES)                            
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE SPACES            TO BP13K022-REC                               
           MOVE K797-NUM-REGN     TO K022-NUM-REGN                              
                                                                                
           IF K797-NUM-PRINTED NOT = 'Y'                                        
              ADD 1   TO WS-K797-REC-PRINT                                      
              PERFORM 210-GET-CRM          THRU 210-EXIT                        
              PERFORM 400-PRINT-HEADING    THRU 400-EXIT                        
              PERFORM 500-PRINT-DETAIL     THRU 500-EXIT                        
              PERFORM 600-PRINT-FOOTER     THRU 600-EXIT                02210061
              PERFORM 800-PRINT-CTRL-DTL   THRU 800-EXIT                        
              PERFORM 700-REWRITE-BP13K797 THRU 700-EXIT                02210061
           END-IF.                                                              
                                                                                
           IF LINK-DAY-WEEK1 = 'SUN'                                            
              MOVE WS-CURR-DATE TO WS-DTE-CURRENT                               
              IF K797-DTE-REQUESTED NOT = SPACES OR LOW-VALUES                  
                 MOVE K797-DTE-REQUESTED TO WS-DTE-REQUESTED                    
              ELSE                                                              
                 MOVE ZEROES             TO WS-DTE-REQUESTED                    
              END-IF                                                            
              COMPUTE WS-DAYS =                                                 
                 FUNCTION INTEGER-OF-DATE(WS-DTE-CURRENT) -                     
                 FUNCTION INTEGER-OF-DATE(WS-DTE-REQUESTED)                     
              IF WS-DAYS <= 7                                                   
                 PERFORM 900-CREATE-WEEKLY-EMAIL THRU 900-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                        02080061
           EXIT.                                                        02090061
                                                                        02100061
                                                                                
      *------------------*                                                      
       400-PRINT-HEADING.                                                       
      *------------------*                                                      
           MOVE K797-NUM-REGN       TO   L388-SUB-REGN.                         
                                                                                
           STRING K797-DTE-REQUESTED(7:2) '/'                                   
                  K797-DTE-REQUESTED(5:2) '/'                                   
                  K797-DTE-REQUESTED(1:4) DELIMITED BY SIZE                     
                  INTO L388-SUB-DTE-REQ.                                        
                                                                                
           STRING K797-TME-REQUESTED(1:2) ':'                                   
                  K797-TME-REQUESTED(3:2) ':'                                   
                  K797-TME-REQUESTED(5:2) DELIMITED BY SIZE                     
                  INTO L388-SUB-TIME-REQ.                                       
                                                                                
           MOVE K024-NME-OIC     TO L388-SUB-CRM.                               
                                                                                
           ADD    1                            TO   WS-PAGE-CNT.                
           MOVE   WS-PAGE-CNT                  TO   L388-PAGE-NO.               
                                                                                
           WRITE PRINT-REC     FROM L388-HDG1       AFTER PAGE.                 
           WRITE PRINT-REC     FROM L388-HDG2       AFTER 2                     
           WRITE PRINT-REC     FROM L388-HDG-LINE   AFTER 1.                    
           WRITE PRINT-REC     FROM L388-SUB-HDR1   AFTER 4.                    
           WRITE PRINT-REC     FROM L388-SUB-HDR2   AFTER 1.                    
           WRITE PRINT-REC     FROM L388-SUB-HDR3   AFTER 1.                    
           WRITE PRINT-REC     FROM L388-SUB-HDR4   AFTER 1.                    
                                                                                
           MOVE  1             TO L388-PAGE-NO.                                 
           WRITE PRINT-REC-C   FROM L388-HEADER-1.                              
           WRITE PRINT-REC-C   FROM L388-HEADER-2.                              
                                                                                
           IF WS-K797-REC-PRINT = ZEROES                                        
              WRITE PRINT-REC-C   FROM L388-HEADER-3A                           
              WRITE PRINT-REC-C   FROM L388-HEADER-3B                           
           ELSE                                                                 
              WRITE PRINT-REC-C   FROM L388-HEADER-3                            
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC-C   FROM L388-HEADER-4.                              
           WRITE PRINT-REC-C   FROM L388-HEADER-5.                              
                                                                                
           IF WS-K797-REC-PRINT = ZEROES                                        
              WRITE PRINT-REC-C   FROM L388-HEADER-6A                           
              WRITE PRINT-REC-C   FROM L388-HEADER-6B                           
           ELSE                                                                 
              WRITE PRINT-REC-C   FROM L388-HEADER-6                            
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC-C   FROM L388-HEADER-7.                              
           WRITE PRINT-REC-C   FROM L388-HDG1.                                  
           WRITE PRINT-REC-C   FROM L388-HDG2.                                  
           WRITE PRINT-REC-C   FROM L388-HDG-LINE.                              
           WRITE PRINT-REC-C   FROM L388-SUB-HDR1.                              
           WRITE PRINT-REC-C   FROM L388-SUB-HDR2.                              
           WRITE PRINT-REC-C   FROM L388-SUB-HDR3.                              
           WRITE PRINT-REC-C   FROM L388-SUB-HDR4.                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       500-PRINT-DETAIL.                                                        
      *-----------------*                                                       
           MOVE   K797-NME-OCCP        TO L388-NAME.                            
           MOVE   K797-NUM-UIN         TO L388-NRIC.                            
           STRING K797-NUM-ROOM K797-NUM-ROOM-TYPE                              
                  DELIMITED BY SIZE  INTO L388-FLAT.                            
           MOVE   K797-NUM-ESTATE-ZONE TO L388-EST-ZONE.                        
           MOVE   K797-NUM-REGN        TO L388-REGN.                            
           IF K797-NUM-REASON2 = SPACES OR LOW-VALUES                           
              MOVE K797-NUM-REASON      TO L388-REASON                          
              MOVE SPACES               TO L388-REASON2                         
                                           L388-REASON3                         
           ELSE                                                                 
              MOVE SPACES               TO WS-UNWRAP-STRING                     
              MOVE K797-NUM-REASON      TO WS-UNWRAP-STRING(1:50)               
              MOVE K797-NUM-REASON2     TO WS-UNWRAP-STRING(51:110)             
              MOVE 3   TO WS-WRP-TOTAL-LINE                                     
              MOVE 60  TO WS-WRP-LEN-PER-LINE                                   
              MOVE 'Y' TO WS-WRP-JUST-FLAG                                      
              PERFORM 550-WRAP-STRING THRU 550-EXIT                             
              MOVE WS-WRAP-STRING(1) TO L388-REASON                             
              MOVE WS-WRAP-STRING(2) TO L388-REASON2                            
              IF WS-WRAP-STRING(3) NOT = SPACES AND LOW-VALUES                  
                 MOVE WS-WRAP-STRING(3) TO L388-REASON3                         
              ELSE                                                              
                 MOVE SPACES            TO L388-REASON3                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-DTL1 AFTER 6.                             
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-DTL1-LINE AFTER 1.                        
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-DTL2 AFTER 2.                             
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-DTL2-LINE AFTER 1.                        
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-DTL3 AFTER 2.                             
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-DTL4 AFTER 4.                             
                                                                                
           IF L388-REASON2 NOT = SPACES AND LOW-VALUES                          
              MOVE   SPACES       TO   PRINT-REC                                
              WRITE  PRINT-REC FROM L388-DTL5 AFTER 1                           
           END-IF.                                                              
                                                                                
           IF L388-REASON3 NOT = SPACES AND LOW-VALUES                          
              MOVE   SPACES       TO   PRINT-REC                                
              WRITE  PRINT-REC FROM L388-DTL6 AFTER 1                           
           END-IF.                                                              
                                                                                
      ***********************                                                   
      *EMAIL DETAILS.       *                                                   
      ***********************                                                   
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-DTL1.                                   
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-DTL1-LINE.                              
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-DTL2.                                   
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-DTL2-LINE.                              
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-DTL3.                                   
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C  FROM L388-DTL4.                                  
                                                                                
           IF L388-REASON2 NOT = SPACES AND LOW-VALUES                          
              MOVE   SPACES       TO   PRINT-REC-C                              
              WRITE  PRINT-REC-C FROM L388-DTL5                                 
           END-IF.                                                              
                                                                                
           IF L388-REASON3 NOT = SPACES AND LOW-VALUES                          
              MOVE   SPACES       TO   PRINT-REC-C                              
              WRITE  PRINT-REC-C FROM L388-DTL6                                 
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************************************************************           
       550-WRAP-STRING.                                                         
      ***************************************************************           
           PERFORM VARYING WS-WRP-CTR FROM 1 BY 1                               
              UNTIL WS-WRP-CTR > 3                                              
              MOVE SPACES          TO WS-WRAP-STRING(WS-WRP-CTR)                
           END-PERFORM.                                                         
                                                                                
           MOVE 1                  TO WS-START-WRP-STRING.                      
      *                                                                         
      *WRAP THE STRING ON A PER LINE                                            
      *                                                                         
           PERFORM VARYING WS-WRP-CTR FROM 1 BY 1                               
              UNTIL WS-WRP-CTR > WS-WRP-TOTAL-LINE OR                           
                    WS-UNWRAP-STRING(WS-START-WRP-STRING:5) = SPACES            
                                                    OR LOW-VALUES               
              PERFORM 560-GET-END-STRING   THRU 560-EXIT                        
      *                                                                         
      *WRAP THE STRING AFTER DETERMINING THE END CHARACTER                      
      *                                                                         
              MOVE WS-UNWRAP-STRING(WS-START-WRP-STRING:                        
      -                             WS-END-WRP-STRING)                          
                                 TO WS-WRAP-STRING(WS-WRP-CTR)                  
                                                                                
      *                                                                         
      *POSITION TO THE NEXT STARTING CHARACTER TO WRAP                          
      *                                                                         
              COMPUTE WS-START-WRP-STRING = WS-START-WRP-STRING +               
                                            WS-END-WRP-STRING                   
           END-PERFORM.                                                         
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************************************************************           
       560-GET-END-STRING.                                                      
      ***************************************************************           
      *                                                                         
      *DETERMINE THE MAXIMUM CHARACTER POSITION                                 
      *                                                                         
           COMPUTE WS-MAX-STRING = WS-START-WRP-STRING +                        
                                   WS-WRP-LEN-PER-LINE.                         
      *                                                                         
      *CHECK IF THE MAXIMUM CHARACTER POSITION IS SPACE                         
      *                                                                         
           IF WS-UNWRAP-STRING(WS-MAX-STRING:1) = SPACES OR LOW-VALUES          
              MOVE WS-MAX-STRING TO WS-END-WRP-STRING                           
           ELSE                                                                 
      *                                                                         
      *IF NOT; DETERMINE THE POSITION OF THE PREVIOUS SPACE CHARACTER           
      *                                                                         
              PERFORM VARYING WS-WRP-CTR2 FROM WS-MAX-STRING BY -1              
                 UNTIL WS-WRP-CTR2 <= 0                                         
                 IF WS-UNWRAP-STRING(WS-WRP-CTR2:1) = SPACES OR                 
                                                       LOW-VALUES               
                    MOVE WS-WRP-CTR2 TO WS-END-WRP-STRING                       
                    MOVE 0       TO WS-WRP-CTR2                                 
                 END-IF                                                         
              END-PERFORM                                                       
                                                                                
           END-IF.                                                              
      *                                                                         
      *DETERMINE THE LENGTH OF THE STRING TO WRAP                               
      *                                                                         
           COMPUTE WS-END-WRP-STRING = WS-END-WRP-STRING -                      
                                   WS-START-WRP-STRING + 1.                     
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       600-PRINT-FOOTER.                                                02210061
      *-----------------*                                                       
           MOVE   K797-NUM-PHONE        TO L388-PHONE.                          
           MOVE   K797-NUM-PHONE-OFFICE TO L388-PHONE-OFFICE.                   
           MOVE   K797-NUM-HAND-PHONE   TO L388-HAND-PHONE.                     
           MOVE   K797-NUM-EMAIL        TO L388-EMAIL.                          
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-FOOTER1 AFTER 6.                          
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-FOOTER2 AFTER 1.                          
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-FOOTER3 AFTER 1.                          
                                                                                
           MOVE   SPACES       TO   PRINT-REC.                                  
           WRITE  PRINT-REC FROM L388-FOOTER4 AFTER 1.                          
                                                                                
      ***********************                                                   
      *EMAIL DETAILS.       *                                                   
      ***********************                                                   
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-FOOTER1.                                
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-FOOTER2.                                
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-FOOTER3.                                
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-FOOTER4.                                
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-FOOTER5.                                
                                                                                
           MOVE   SPACES       TO   PRINT-REC-C.                                
           WRITE  PRINT-REC-C FROM L388-FOOTER6.                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       700-REWRITE-BP13K797.                                            02210061
      *---------------------*                                                   
           MOVE 'Y' TO K797-NUM-PRINTED.                                        
                                                                                
           REWRITE BP13K797-REC.                                                
                                                                                
           EVALUATE BP13K797-STATUS                                             
             WHEN 00                                                            
                ADD 1                   TO   WS-K797-REC-REWR                   
             WHEN 10                                                            
                DISPLAY 'REC NOTFND IN BP13K797, REGN ' K797-NUM-REGN           
             WHEN OTHER                                                         
                DISPLAY 'ERROR REWRITING BP13K797: ' BP13K797-STATUS            
                MOVE BP13K797-STATUS     TO   RETURN-CODE                       
                PERFORM 999-CLOSE-FILES  THRU 999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       800-PRINT-CTRL-DTL.                                                      
      *-----------------------*                                                 
           IF LINE-CTRL-CTR > 59                                                
              PERFORM 810-PRINT-CTRL-HDG THRU 810-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE SPACES TO L388-CTRL-REGN                                        
                          L388-CTRL-DTE-REQ                                     
                          L388-CTRL-TIME-REQ                                    
           MOVE ZEROES TO L388-SER-NO.                                          
                                                                                
           MOVE  WS-K797-REC-PRINT  TO L388-SER-NO.                             
           MOVE  K797-NUM-REGN      TO L388-CTRL-REGN.                          
                                                                                
           STRING K797-DTE-REQUESTED(7:2) '/'                                   
                  K797-DTE-REQUESTED(5:2) '/'                                   
                  K797-DTE-REQUESTED(1:4) DELIMITED BY SIZE                     
                  INTO L388-CTRL-DTE-REQ.                                       
                                                                                
           STRING K797-TME-REQUESTED(1:2) ':'                                   
                  K797-TME-REQUESTED(3:2) ':'                                   
                  K797-TME-REQUESTED(5:2) DELIMITED BY SIZE                     
                  INTO L388-CTRL-TIME-REQ.                                      
                                                                                
           WRITE PRINT-REC-B FROM L388-CTRL-DTL  AFTER 1.                       
           ADD   1  TO  LINE-CTRL-CTR.                                          
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       810-PRINT-CTRL-HDG.                                                      
      *-----------------------*                                                 
           ADD  1             TO PAGE-CTRL-CTR.                                 
           MOVE PAGE-CTRL-CTR TO L388-PAGE-NO.                                  
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                     
           STRING WS-CURR-DATE(7:2) '/'                                         
                  WS-CURR-DATE(5:2) '/'                                         
                  WS-CURR-DATE(1:4) DELIMITED BY SIZE                           
                  INTO L388-RUN-DATE.                                           
                                                                                
           WRITE PRINT-REC-B FROM L388-HDG1           AFTER PAGE.               
           WRITE PRINT-REC-B FROM L388-CTRL-HDG       AFTER 2.                  
           WRITE PRINT-REC-B FROM L388-HDG-CTRL-LINE  AFTER 1.                  
           WRITE PRINT-REC-B FROM L388-CTRL-SUB-HDG   AFTER 2.                  
           WRITE PRINT-REC-B FROM L388-CTRL-LINE      AFTER 1.                  
                                                                                
           MOVE  SPACES      TO    PRINT-REC-B.                                 
           WRITE PRINT-REC-B AFTER 1.                                           
           MOVE  8           TO    LINE-CTRL-CTR.                               
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       900-CREATE-WEEKLY-EMAIL.                                                 
      *-----------------------*                                                 
           IF WS-WK-HEADER-PRINTED NOT = 'Y'                                    
              INITIALIZE L388-DTL-WK                                            
              WRITE PRINT-REC-D   FROM L388-HEADER-1                            
              WRITE PRINT-REC-D   FROM L388-HEADER-2                            
              WRITE PRINT-REC-D   FROM L388-HEADER-WK1                          
              WRITE PRINT-REC-D   FROM L388-HEADER-4                            
              WRITE PRINT-REC-D   FROM L388-HEADER-5                            
              WRITE PRINT-REC-D   FROM L388-HEADER-WK2                          
              WRITE PRINT-REC-D   FROM L388-HEADER-WK3                          
              MOVE SPACES TO PRINT-REC-D                                        
              WRITE PRINT-REC-D                                                 
              WRITE PRINT-REC-D   FROM L388-HDG1-WK                             
              WRITE PRINT-REC-D   FROM L388-HDG2-WK                             
              MOVE 'Y' TO WS-WK-HEADER-PRINTED                                  
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-SNO                                                      
           MOVE K797-NUM-REGN      TO WS-WK-REGNO.                              
           MOVE K797-DTE-REQUESTED TO WS-WK-DTE-REQ.                            
           MOVE K797-NUM-PRINTED   TO WS-WK-SEND-TAG.                           
                                                                                
           WRITE PRINT-REC-D   FROM L388-DTL-WK.                                
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       999-CLOSE-FILES.                                                 02210061
      *----------------*                                                        
           DISPLAY '****BP13C388 CONTROL REPORT *******'                        
           DISPLAY 'NO OF K797 RECS READ   :' WS-K797-REC-READ.         02280068
           DISPLAY 'NO OF K797 RECS PRINTED:' WS-K797-REC-PRINT.        02280068
           DISPLAY 'NO OF K797 RECS REWRTTN:' WS-K797-REC-REWR.         02280068
           DISPLAY '***********************************'                        
                                                                                
           CLOSE    BP13K797                                            02260061
                    BP13L388                                            02270061
                    BP13K022                                                    
                    BP13K024                                                    
                    P13L388B                                                    
                    P13L388C                                                    
                    P13L388D.                                                   
                                                                                
           IF BP13K797-STATUS NOT = '00'                                02340061
              DISPLAY '***  CLOSING ERROR   BP13K797-STATUS'            02350061
              BP13K797-STATUS.                                          02360061
                                                                                
           IF BP13K022-STATUS NOT = '00'                                02340061
              DISPLAY '***  CLOSING ERROR   BP13K022-STATUS'            02350061
              BP13K022-STATUS.                                          02360061
                                                                                
           IF BP13K024-STATUS NOT = '00'                                02340061
              DISPLAY '***  CLOSING ERROR   BP13K024-STATUS'            02350061
              BP13K024-STATUS.                                          02360061
                                                                                
                                                                                
           STOP RUN.                                                    02410061
                                                                        02420061
       999-EXIT.                                                        02430061
           EXIT.                                                                
