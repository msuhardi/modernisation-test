      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C228.                                                 
       AUTHOR.        JOEL E PARRENO.                                           
       DATE-WRITTEN.  03/22/94.                                                 
       DATE-COMPILED.                                                           
      *================================================================*        
      *    SYSTEM NAME  :   SYSTEM OF COMMITMENT                       *        
      *    SYSTEM ID    :   BP13                                       *        
      *    OBJECTIVE    :   THIS IS A PROGRAM TO DELETE RECORD FROM    *        
      *                     BM06F800 IF IT IS EXISTING IN BP13F205     *        
      *                     CONTROL FILE.                              *        
      *    INPUT FILES  :   1.  BP13F205 - CONTROL RECORDS FILE        *        
      *                     2.  BM06F800 - FLATLIST FILE               *        
      *    OUTPUT FILE  :   1.  BM06F800 - FLATLIST FILE               *        
      *                                                                *        
      *   CHG-NO    BY      DATE     DESCRIPTION                       *        
      *   -------   ---   --------   ------------------------------    *        
      *   SOC-PH9   GJ    16/12/94   TO CHECK FLAT-TYPE BEFORE         *        
      *                              DELETION.                         *        
      *   JAR9297  ERWIN  23/05/95   TO CHANGE WS-REC-PTR TO 9(2).     *        
      *  BP130641  LMS1   07/03/99   TO CATER FOR STUDIO APARTMENT.    *        
      *  BP130778  GBR    13/12/99   TO INCLUDE 3 ROOM FLAT UNDER FLAT *        
      *                              TYPE 'SE'                         *        
      *  BP130970  MBS    08/05/01   WRITE TO A FILE RECORDS IN        *        
      *                              BP13F205 WITH SAME NT OR ZONE AND *        
      *                              FLAT-TYPE AND DTE-ALLOC WITH      *        
      *                              BM06F800.                         *        
      *  BP131059  LMS    23/11/01   TO CATER FOR DELETION OF F800     *        
      *                              RECORDS FOR BTO.                  *        
      *  BP132278  JF4    16/04/03   CHG BM06F800 LRECL FROM 600 TO 800*        
      *  BP132500  LMS1   05/04/04   TO CATER FOR BANDING OF 1A,2A,3,4 *        
      *                              AND 5-ROOM.                       *        
      *  BP132560  MTD1   19/07/04   CATER FOR FORM-TYPE = '4'         *        
      *  BP133026  RB12   15/03/07   CATER FOR DELETION OF 1A          *        
      *  BP133851  ESA1   16/03/10   CATER FOR PAIR UNIT               *        
      *  BP134043  ESA1   03/11/10   CATER FOR PUNGGOL SBF 201008      *        
      *  BP134731  PCL4   09/11/12   TO CATER FOR MODL-DESIGN 89       *        
      *  BP135281  ESA1   30/05/14   TO CATER FOR 3GEN & NO 3GEN UNITS *        
      *  BP135561  KG12   14/01/15   TO INCREASE WS-REC-PTR TO 4 BYTES *        
      *  BP135838  ESA1   01/07/15   TO CATER FOR MODL-DESIGN 92 & 95  *        
      *  BP136002  ESA1   09/11/15   TO CATER FOR 2-ROOM FLEXI         *        
      *  BP136066  ESA1   03/02/16   TO CATER FOR DESIGN-OFFER FOR '3M'*        
      *                              FLAT TYPE                         *        
      *  BP138485  EL27   05/11/20   INCLUDE '85' FOR '3M' FLAT-TYPE   *        
      *  BP138613  MRR5   10/03/21   CATER FOR CCA FLAT                *        
      *  BP139531  MRR5   14/03/23   CATER FOR 3-ROOM FSH              *        
      *================================================================*        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN TO BP13F205.                                 
           SELECT BM06F800  ASSIGN TO BM06F800.                                 
           SELECT BM6F800   ASSIGN TO BM6F800.                                  
           SELECT BM6F800D  ASSIGN TO BM6F800D.                                 
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80  CHARACTERS                                      
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F.                                              
       COPY BP13F205.                                                           
                                                                                
       FD   BM06F800                                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F.                                              
       COPY BM06F800.                                                           
                                                                                
       FD   BM6F800                                                             
            RECORD CONTAINS 800 CHARACTERS                                      
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F.                                              
       01   BM6F800-REC                   PIC X(800).                           
                                                                                
       FD   BM6F800D                                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F.                                              
       01   BM6F800D-REC                  PIC X(800).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05 WS-EOF-F205                 PIC X(01) VALUE 'N'.                  
           05 WS-EOF-F800                 PIC X(01) VALUE 'N'.                  
           05 WS-MATCH-IND                PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-F800-FLAT-TYPE              PIC X(02) VALUE SPACES.               
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05 WS-CNT-READ-F205            PIC 9(06) VALUE ZEROS.                
           05 WS-CNT-READ-F800            PIC 9(06) VALUE ZEROS.                
           05 WS-CNT-WRITE-F800           PIC 9(06) VALUE ZEROS.                
           05 WS-CNT-WRITE-F800D          PIC 9(06) VALUE ZEROS.                
           05 WS-REC-PTR                  PIC 9(04) VALUE ZEROS.                
                                                                                
       01  WS-F205-SAVE-ARRAYS.                                                 
           05 WS-F205-INDEX               PIC S9(4) COMP.                       
           05 WS-F205-ARRAY               OCCURS 1  TO 99 TIMES                 
                                          DEPENDING ON WS-F205-INDEX.           
              10 WS-F205-NT-ZONE          PIC X(03) VALUE SPACES.               
              10 WS-F205-FLAT-TYPE        PIC X(02) VALUE SPACES.               
              10 WS-F205-NT-ZONE-SW       PIC X(01) VALUE SPACES.               
              10 WS-F205-ALLOCN-DTE       PIC 9(08) VALUE ZEROES.               
              10 WS-F205-NUM-GEN3         PIC X     VALUE SPACES.               
                                                                                
       01  WS-NUM-FLAT-TYPE               PIC X(02) VALUE SPACES.               
                                                                                
       01  WS-F800-ALLOCN-DTE             PIC 9(8)  VALUE ZEROES.               
       01  WS-F800-NUM-SITE               PIC X(1)  VALUE SPACES.               
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
      ******************************************************************        
      *    MAIN LOGIC PROCESS SECTION                                  *        
      ******************************************************************        
                                                                                
      *-----------------*                                                       
       000-MAIN-ROUTINE.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM  100-OPEN-ROUTINE        THRU    100-EXIT.                   
           PERFORM  200-READ-BP13F205       THRU    200-EXIT                    
                        UNTIL WS-EOF-F205 = 'Y'.                                
           PERFORM  300-READ-BM06F800       THRU    300-EXIT.                   
           PERFORM  400-PROCESS-RECORD      THRU    400-EXIT                    
                        UNTIL WS-EOF-F800 = 'Y'.                                
           PERFORM  600-DISPLAY-STATISTICS  THRU    600-EXIT.                   
           PERFORM  900-CLOSE-ROUTINE       THRU    900-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                       *        
      ******************************************************************        
                                                                                
      *------------------*                                                      
       100-OPEN-ROUTINE.                                                        
      *------------------*                                                      
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BM06F800                                                 
                OUTPUT BM6F800                                                  
                       BM6F800D.                                                
                                                                                
           MOVE  ZERO  TO WS-F205-INDEX.                                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    READ SEQUENTIAL FILE (BP13F205) AT END SET EOF-IND TO 'Y'   *        
      ******************************************************************        
                                                                                
      *------------------*                                                      
       200-READ-BP13F205.                                                       
      *------------------*                                                      
                                                                                
           READ BP13F205                                                        
                AT END MOVE 'Y' TO WS-EOF-F205                                  
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           IF F205-NUM-FORM-TYPE = '4'                                          
              MOVE '4 ' TO WS-NUM-FLAT-TYPE                                     
              PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                           
              MOVE '5 ' TO WS-NUM-FLAT-TYPE                                     
              PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                           
              GO TO 200-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF F205-NUM-FLAT-TYPE = '1A'                                         
              MOVE '1A' TO WS-NUM-FLAT-TYPE                                     
              PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                           
              MOVE '2A' TO WS-NUM-FLAT-TYPE                                     
              PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                           
           ELSE                                                                 
              IF F205-NUM-FLAT-TYPE = 'SE'                                      
                 MOVE '1A' TO WS-NUM-FLAT-TYPE                                  
                 PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                        
                 MOVE '2A' TO WS-NUM-FLAT-TYPE                                  
                 PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                        
                 MOVE '2 ' TO WS-NUM-FLAT-TYPE                                  
                 PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                        
                 MOVE '3 ' TO WS-NUM-FLAT-TYPE                                  
                 PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                        
                 MOVE '4 ' TO WS-NUM-FLAT-TYPE                                  
                 PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                        
                 MOVE '5 ' TO WS-NUM-FLAT-TYPE                                  
                 PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                        
              ELSE                                                              
                 IF F205-NUM-FLAT-TYPE = '4P'                                   
                    MOVE '1A' TO WS-NUM-FLAT-TYPE                               
                    PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                     
                    MOVE '2A' TO WS-NUM-FLAT-TYPE                               
                    PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                     
                    MOVE '4 ' TO WS-NUM-FLAT-TYPE                               
                    PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                     
                 ELSE                                                           
                    IF F205-NUM-FLAT-TYPE = '5P'                                
                       MOVE '1A' TO WS-NUM-FLAT-TYPE                            
                       PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                  
                       MOVE '2A' TO WS-NUM-FLAT-TYPE                            
                       PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                  
                       MOVE '5 ' TO WS-NUM-FLAT-TYPE                            
                       PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                  
                    ELSE                                                        
                       MOVE F205-NUM-FLAT-TYPE TO WS-NUM-FLAT-TYPE              
                       PERFORM 250-MOVE-TO-ARRAY THRU 250-EXIT                  
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       250-MOVE-TO-ARRAY.                                                       
      *------------------*                                                      
                                                                                
           ADD  1                   TO WS-CNT-READ-F205.                        
           ADD  1                   TO WS-REC-PTR.                              
           ADD  1                   TO WS-F205-INDEX.                           
           MOVE F205-NUM-NT-ZONE    TO WS-F205-NT-ZONE    (WS-REC-PTR).         
           MOVE F205-NUM-NT-ZONE-SW TO WS-F205-NT-ZONE-SW (WS-REC-PTR).         
           MOVE WS-NUM-FLAT-TYPE    TO WS-F205-FLAT-TYPE  (WS-REC-PTR).         
           MOVE F205-DTE-ALLOCN     TO WS-F205-ALLOCN-DTE (WS-REC-PTR).         
           MOVE F205-NUM-GEN3       TO WS-F205-NUM-GEN3   (WS-REC-PTR).         
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    READ SEQUENTIAL FILE BM06F800 AT END SET EOF-IND TO "Y"     *        
      ******************************************************************        
                                                                                
      *------------------*                                                      
       300-READ-BM06F800.                                                       
      *------------------*                                                      
                                                                                
           READ BM06F800                                                        
                AT END MOVE 'Y' TO WS-EOF-F800                                  
                GO TO 300-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD  1  TO WS-CNT-READ-F800.                                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    CHECK FLATLIST FILE RECORD WITH CONTROL FILE RECORDS        *        
      ******************************************************************        
                                                                                
      *------------------*                                                      
       400-PROCESS-RECORD.                                                      
      *------------------*                                                      
                                                                                
           IF F800-DTE-ALLOCN NOT NUMERIC                                       
              MOVE ZEROES          TO WS-F800-ALLOCN-DTE                        
           ELSE                                                                 
              MOVE F800-DTE-ALLOCN TO WS-F800-ALLOCN-DTE                        
           END-IF.                                                              
                                                                                
           MOVE    'N'              TO WS-MATCH-IND.                            
                                                                                
           PERFORM 500-COMPARE-NT-ALLOCN  THRU  500-EXIT                        
                       VARYING WS-REC-PTR FROM  1 BY 1                          
                       UNTIL   WS-REC-PTR  >    WS-F205-INDEX.                  
                                                                                
           IF WS-MATCH-IND =  'N'                                               
              MOVE   F800-REC TO   BM6F800-REC                                  
              WRITE  BM6F800-REC                                                
              ADD    1        TO   WS-CNT-WRITE-F800                            
           ELSE                                                                 
              MOVE   F800-REC TO   BM6F800D-REC                                 
              WRITE  BM6F800D-REC                                               
              ADD    1        TO   WS-CNT-WRITE-F800D                           
           END-IF.                                                              
                                                                                
           PERFORM  300-READ-BM06F800       THRU    300-EXIT.                   
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************                                                   
       450-DECODE-FLAT-TYPE.                                                    
      ***********************                                                   
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE ALSO F800-CDE-MODL-DESG                 
           WHEN '01' ALSO '80'                                                  
           WHEN '01' ALSO '89'                                                  
           WHEN '01' ALSO '92'                                                  
                 MOVE '1A'       TO WS-F800-FLAT-TYPE                           
           WHEN '01' ALSO '74'                                                  
                 MOVE '1F'       TO WS-F800-FLAT-TYPE                           
           WHEN '01' ALSO ANY                                                   
                 MOVE '1 '       TO WS-F800-FLAT-TYPE                           
           WHEN '02' ALSO '80'                                                  
           WHEN '02' ALSO '89'                                                  
           WHEN '02' ALSO '92'                                                  
                 MOVE '2A'       TO WS-F800-FLAT-TYPE                           
           WHEN '02' ALSO ANY                                                   
                 MOVE '2 '       TO WS-F800-FLAT-TYPE                           
           WHEN '03' ALSO ANY                                                   
                 MOVE '3 '       TO WS-F800-FLAT-TYPE                           
           WHEN '04' ALSO ANY                                                   
                 MOVE '4 '       TO WS-F800-FLAT-TYPE                           
           WHEN '05' ALSO ANY                                                   
                 MOVE '5 '       TO WS-F800-FLAT-TYPE                           
           WHEN '06' ALSO ANY                                                   
                 MOVE 'E '       TO WS-F800-FLAT-TYPE                           
           WHEN OTHER                                                           
                 MOVE '  '       TO WS-F800-FLAT-TYPE                           
           END-EVALUATE.                                                        
                                                                                
           IF F800-NUM-DESIGN-OFFER = 'F' AND                                   
              F800-CDE-MODL-DESG NOT = '74' AND                                 
              F800-CDE-TYP-ACTUSE NOT = '03'                                    
              MOVE '2 '          TO WS-F800-FLAT-TYPE                           
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    COMPARE IF NEW-TOWN AND ALLOCATION-DATE IS NOT EQUAL        *        
      ******************************************************************        
                                                                                
      *----------------------*                                                  
       500-COMPARE-NT-ALLOCN.                                                   
      *----------------------*                                                  
                                                                                
           PERFORM 450-DECODE-FLAT-TYPE  THRU 450-EXIT.                         
                                                                                
           IF ((F800-CDE-NT   = WS-F205-NT-ZONE (WS-REC-PTR))                   
           OR  (F800-CDE-ZONE = WS-F205-NT-ZONE (WS-REC-PTR)(1:2)))             
           AND (WS-F800-ALLOCN-DTE = WS-F205-ALLOCN-DTE (WS-REC-PTR))           
              IF WS-F205-FLAT-TYPE(WS-REC-PTR) = '3M'                           
                IF (WS-F800-FLAT-TYPE(1:1)= F205-NUM-FLAT-TYPE(1:1)) AND        
                   (F800-CDE-MODL-DESG = '81' OR '82' OR '83' OR                
                                  '84' OR '85' OR '86' OR '87' OR '88')         
                   MOVE 'Y'    TO WS-MATCH-IND                                  
                END-IF                                                          
                IF (WS-F800-FLAT-TYPE(1:1)= F205-NUM-FLAT-TYPE(1:1)) AND        
                   (F800-NUM-DESIGN-OFFER = 'P')                                
                   MOVE 'Y'    TO WS-MATCH-IND                                  
                END-IF                                                          
              ELSE                                                              
                  IF WS-F205-FLAT-TYPE(WS-REC-PTR) = '3N'                       
                   IF (WS-F800-FLAT-TYPE(1:1) = F205-NUM-FLAT-TYPE(1:1))        
                       AND                                                      
                       (F800-CDE-MODL-DESG NOT = '34' AND '70' AND              
                       '71' AND '72' AND '73' AND '81' AND '82' AND             
                       '83' AND '84' AND '85' AND '86' AND '87' AND             
                       '88')                                                    
                        MOVE    'Y'        TO WS-MATCH-IND                      
                   END-IF                                                       
                  ELSE                                                          
                     IF WS-F205-FLAT-TYPE(WS-REC-PTR) = '2F'                    
                        IF F800-NUM-DESIGN-OFFER = 'F' AND                      
                           F800-CDE-TYP-ACTUSE NOT = '03'                       
                           MOVE    'Y'     TO WS-MATCH-IND                      
                        END-IF                                                  
                     ELSE                                                       
                        IF WS-F800-FLAT-TYPE =                                  
                           WS-F205-FLAT-TYPE(WS-REC-PTR)                        
                           MOVE    'Y'        TO WS-MATCH-IND                   
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
              END-IF                                                            
           ELSE                                                                 
              IF (F800-CDE-SELTY = 'BTO')                                       
                 IF (F800-NUM-SITE = WS-F205-NT-ZONE(WS-REC-PTR)(1:1))          
                    AND (WS-F800-ALLOCN-DTE =                                   
                         WS-F205-ALLOCN-DTE(WS-REC-PTR))                        
                     IF WS-F205-FLAT-TYPE(WS-REC-PTR) = '3M'                    
                        IF (WS-F800-FLAT-TYPE(1:1)=                             
                            F205-NUM-FLAT-TYPE(1:1)) AND                        
                           (F800-CDE-MODL-DESG = '81' OR '82' OR '83' OR        
                                  '84' OR '85' OR '86' OR '87' OR '88')         
                           MOVE 'Y'    TO WS-MATCH-IND                          
                        END-IF                                                  
                        IF (WS-F800-FLAT-TYPE(1:1) =                            
                            F205-NUM-FLAT-TYPE(1:1)) AND                        
                           (F800-NUM-DESIGN-OFFER = 'P')                        
                           MOVE 'Y'    TO WS-MATCH-IND                          
                        END-IF                                                  
                     ELSE                                                       
                        IF WS-F205-FLAT-TYPE(WS-REC-PTR) = '3N'                 
                          IF (WS-F800-FLAT-TYPE(1:1) =                          
                             F205-NUM-FLAT-TYPE(1:1)) AND                       
                            (F800-CDE-MODL-DESG NOT = '34' AND '70' AND         
                            '71' AND '72' AND '73' AND '81' AND '82' AND        
                            '83' AND '84' AND '85' AND '86' AND '87' AND        
                            '88')                                               
                            IF F800-NUM-DESIGN-OFFER NOT = 'P'                  
                               MOVE 'Y'    TO WS-MATCH-IND                      
                            END-IF                                              
                          END-IF                                                
                        ELSE                                                    
                          IF WS-F800-FLAT-TYPE =                                
                             WS-F205-FLAT-TYPE(WS-REC-PTR)                      
                             IF WS-F205-NUM-GEN3 (WS-REC-PTR) = 'Y'             
                                IF F800-CDE-MODL-DESG = '90' OR '91' OR         
                                                        '95'                    
                                   MOVE 'Y'        TO WS-MATCH-IND              
                                END-IF                                          
                             ELSE                                               
                                IF WS-F205-NUM-GEN3 (WS-REC-PTR) = 'N'          
                                   IF F800-CDE-MODL-DESG NOT = '90' AND         
                                                          '91' AND '95'         
                                      MOVE 'Y'        TO WS-MATCH-IND           
                                   END-IF                                       
                                ELSE                                            
                                   MOVE 'Y'        TO WS-MATCH-IND              
                                END-IF                                          
                             END-IF                                             
                        END-IF                                                  
                     END-IF                                                     
                 ELSE                                                           
                    IF (F800-NUM-BTO-OPTION =                                   
                        WS-F205-NT-ZONE (WS-REC-PTR))                           
                    AND (WS-F800-ALLOCN-DTE =                                   
                         WS-F205-ALLOCN-DTE(WS-REC-PTR))                        
                    AND (WS-F205-FLAT-TYPE(WS-REC-PTR) = '00')                  
                         MOVE 'Y'        TO WS-MATCH-IND                        
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    DISPLAY NECESSARY CONTROL LIST STATISTICS                   *        
      ******************************************************************        
                                                                                
      *----------------------*                                                  
       600-DISPLAY-STATISTICS.                                                  
      *----------------------*                                                  
                                                                                
           DISPLAY '                                                 '.         
           DISPLAY '***********  BP13C228 CONTROL LIST  ***********  '.         
           DISPLAY '                                                 '.         
           DISPLAY '                                                 '.         
           DISPLAY 'BP13F205 READ    RECORDS    = ' WS-CNT-READ-F205.           
           DISPLAY 'BM06F800 READ    RECORDS    = ' WS-CNT-READ-F800.           
           DISPLAY 'BM06F800 WRITTEN RECORDS    = ' WS-CNT-WRITE-F800.          
           DISPLAY 'BM06F800 DELETED RECORDS    = ' WS-CNT-WRITE-F800D.         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    CLOSE FILES   AND   STOP RUN                                *        
      ******************************************************************        
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           CLOSE BP13F205                                                       
                 BM06F800                                                       
                 BM6F800                                                        
                 BM6F800D.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
