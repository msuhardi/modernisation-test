       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C352.                                                    
      *AUTHOR.     MTRS.                                                        
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OLD PROGRAM : BP13C304                                    *            
      *                                                            *            
      *  OBJECTIVE :                                               *            
      *    1.  TO TAG THE BP13K885 CONFIRM TAG.                    *            
      *                                                            *            
      *  INPUT     :  BP13F880  (SUB-TRANSFER FILE)                *            
      *               BP13K800  (SOC MASTER FILE)                  *            
      *                                                            *            
      *  OUTPUT    :  BP13K885  (EX-APPLT-FILE)                    *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY   ON      DESCRIPTION                          *            
      * ---------------------------------                          *            
      * SOC-PH8F LSB 950630 - CHG BP13F300 TO BP13F880             *            
      * BP130094 EG1 960226 - REMOVE F120 AND F130. TO TAG         *            
      *                       THE K885-NUM-CONFIRM.                *            
      * BP130418 MPA 980403   YEAR 2000 CHANGES                    *            
      * BP130418 RHB 980604   YEAR 2000 CHANGES                    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F880 ASSIGN TO BP13F880.                                  
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K885-KEY-FLD                               
                  FILE STATUS     IS K885-STATUS.                               
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F880                BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F880.                                                           
                                                                                
       FD  BP13K885                                                             
                                   RECORD CONTAINS 400 CHARACTERS.              
       COPY BP13K885.                                                           
                                                                                
       FD  BP13K800                                                             
                                   RECORD CONTAINS 2000 CHARACTERS.             
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-NO-F880-READ     PIC  9(4)      VALUE ZEROS.                  
           05  WS-NO-K885-REWRITE  PIC  9(4)      VALUE ZEROS.                  
                                                                                
       01  WS-STATUS.                                                           
           05  K885-STATUS         PIC X(2).                                    
           05  K800-STATUS         PIC X(2).                                    
           05  WS-EOF-F880         PIC X(3).                                    
               88  EOF-F880        VALUE 'YES'.                                 
           05  WS-EOF-K885         PIC X(3).                                    
               88  EOF-K885        VALUE 'YES'.                                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
           PERFORM 1000-INIT-RTN  THRU 1000-EXIT.                               
           PERFORM 2000-READ-F880 THRU 2000-EXIT.                               
           PERFORM 3000-MAIN-PROCESS THRU 3000-EXIT                             
                   UNTIL   EOF-F880.                                            
                                                                                
           DISPLAY '-- BP13C352 CONTROL COUNT --  '.                            
           DISPLAY '                              '.                            
           DISPLAY 'TOTAL NO. OF RECORD READ FROM BP13F880          = '         
                    WS-NO-F880-READ.                                            
           DISPLAY 'NO. OF RECORDS UPDATED IN BP13K885              = '         
                    WS-NO-K885-REWRITE.                                         
                                                                                
           PERFORM 9000-CLOSE-FILES                                             
           THRU    9000-EXIT.                                                   
                                                                                
       0000-FINISH.                                                             
           STOP RUN.                                                            
                                                                                
       1000-INIT-RTN.                                                           
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F880                                                 
                       BP13K800                                                 
                I-O    BP13K885.                                                
                                                                                
           MOVE 'NO'   TO WS-EOF-F880.                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F880.                                                          
      *-------------------------------------------------------------            
           READ BP13F880                                                        
               AT END MOVE 'YES' TO WS-EOF-F880                                 
                      MOVE HIGH-VALUES TO F880-NUM-REGN                         
                      GO TO  2000-EXIT.                                         
                                                                                
           ADD 1     TO WS-NO-F880-READ.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           PERFORM 8000-START-K885       THRU 8000-EXIT                         
           IF NOT EOF-K885                                                      
              PERFORM 8100-READ-NEXT-K885   THRU 8100-EXIT                      
           END-IF.                                                              
           PERFORM 3100-UPDATE-K885      THRU 3100-EXIT                         
                   UNTIL EOF-K885.                                              
                                                                                
           PERFORM 2000-READ-F880 THRU 2000-EXIT.                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-UPDATE-K885.                                                        
      *-------------------------------------------------------------            
           MOVE K885-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS  = '00'                                               
           MOVE K800-NUM-ROOM      TO K885-NUM-FLAT                             
           ELSE                                                                 
              DISPLAY 'BP13K800 READ  ERROR CODE(' K800-STATUS ')'              
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
           MOVE 'Y'               TO K885-NUM-CONFIRM.                          
           PERFORM 8200-REWRITE-K885     THRU 8200-EXIT.                        
           PERFORM 8100-READ-NEXT-K885   THRU 8100-EXIT.                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8000-START-K885.                                                         
      *-------------------------------------------------------------            
           MOVE LOW-VALUES           TO WS-EOF-K885.                            
           MOVE LOW-VALUES           TO K885-KEY-FLD.                           
           MOVE F880-NUM-REGN        TO K885-NUM-REGN.                          
           START BP13K885 KEY >= K885-KEY-FLD                                   
                 INVALID KEY MOVE 'YES'      TO WS-EOF-K885                     
                             GO TO 8000-EXIT.                                   
           IF K885-STATUS NOT = '00'                                            
              DISPLAY 'BP13K885 START ERROR CODE(' K885-STATUS ')'              
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8100-READ-NEXT-K885.                                                     
      *-------------------------------------------------------------            
           READ BP13K885 NEXT                                                   
                AT END   MOVE 'YES'      TO WS-EOF-K885                         
                         GO TO 8100-EXIT.                                       
                                                                                
           IF K885-STATUS NOT = '00'                                            
              DISPLAY 'BP13K885 READ ERROR CODE(' K885-STATUS ')'               
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF K885-NUM-REGN NOT = F880-NUM-REGN                                 
              MOVE 'YES'          TO WS-EOF-K885                                
           END-IF.                                                              
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8200-REWRITE-K885.                                                       
      *-------------------------------------------------------------            
           REWRITE BP13K885-REC.                                                
                                                                                
           IF K885-STATUS NOT = '00'                                            
              DISPLAY 'BP13K885 REWRITE ERROR CODE(' K885-STATUS ')'            
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
           ADD 1     TO WS-NO-K885-REWRITE.                                     
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           CLOSE      BP13F880                                                  
                      BP13K800                                                  
                      BP13K885.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
