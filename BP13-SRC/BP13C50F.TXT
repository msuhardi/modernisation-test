       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C50F.                                                 
      *AUTHOR.        KATHRINA VALENZUELA.                                      
      *DATE-WRITTEN.  10/09/2019.                                               
      **************************************************************            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      **************************************************************            
      * OBJECTIVE : TO FORMAT AND SEND EMAIL ALERT TO CRMS THE     *            
      *             REPORT OF ALL NON-MARRIED APPLICANTS (EXCLUDING*            
      *             FS) AND HAVE ALREADY MARRIED                   *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP137791 20190910 KV6   NEW PGM                            *            
      * BP138833 20210930 FP6   SEND ONLY EMAIL TO THE OIC         *            
      * BP138833 20211103 FP6   AMEND TITLE AND DETAILS INCLUDED IN*            
      *                         THE REPORTS                        *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F50C         ASSIGN BP13F50C.                             
                                                                                
           SELECT P13LC50C         ASSIGN P13LC50C.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F50C           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 150 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       01   BP13F50C-REC.                                                       
            05  F50C-REGN            PIC X(08).                                 
            05  FIL01                PIC X(01).                                 
            05  F50C-OIC             PIC X(05).                                 
            05  FIL02                PIC X(01).                                 
            05  F50C-AP-NRIC         PIC X(09).                                 
            05  FIL03                PIC X(01).                                 
            05  F50C-AP-NAME         PIC X(20).                                 
            05  FIL04                PIC X(01).                                 
            05  F50C-NUM-MARRIAGE    PIC X(15).                                 
            05  FIL05                PIC X(01).                                 
            05  F50C-DTE-MARRIAGE    PIC X(08).                                 
            05  FIL06                PIC X(01).                                 
            05  F50C-SP-NRIC         PIC X(09).                                 
            05  FIL07                PIC X(01).                                 
            05  F50C-SP-NAME         PIC X(20).                                 
            05  FIL08                PIC X(01).                                 
            05  FILLER               PIC X(48).                                 
                                                                                
                                                                                
       FD  P13LC50C                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 160 CHARACTERS.                                      
       01  P13L50C-REC                 PIC X(160).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-F50C-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F50C-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-OIC                  PIC X(5)   VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F50C-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-L50C-WRTN                 PIC 9(05)  VALUE ZEROS.             
           05  WS-NUM-SNO-LC50              PIC 9(04)  VALUE ZEROES.            
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
      *---------------------------------------------------------------*         
      *        MAIL HEADER                                            *         
      *---------------------------------------------------------------*         
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(160) VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(160) VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-TO.                                               
           05  FILLER PIC X(160) VALUE SPACES.                                  
                                                                                
       01  MAIL-HDR-MAIL-RCPT2.                                                 
           05  FILLER PIC X(160) VALUE 'RCPT TO:<TAN_SIEW_LAY@HDB.GOV.SG        
      -      '>'.                                                               
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(160) VALUE 'DATA'.                                  
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(160) VALUE 'FROM:Soc System - Email Alert'.         
                                                                                
       01  MAIL-HDR-MAIL-TO.                                                    
           05  FILLER PIC X(160) VALUE SPACES.                                  
                                                                                
       01  MAIL-HDR-MAIL-TO-CC1.                                                
           05  FILLER PIC X(160)                                                
               VALUE 'CC:<tan_siew_lay@hdb.gov.sg>'.                            
                                                                                
       01  MAIL-SUBJECT-LC50C.                                                  
           05  FILLER PIC X(160) VALUE 'SUBJECT : Report for Non-FS and         
      -       'not Married for cases with key avail or 3 mths bef PCD'.         
                                                                                
       01  MAIL-BODY-LC50C             PIC X(160) VALUE SPACES.                 
                                                                                
       01  MAIL-END-LC50C              PIC X(160) VALUE SPACES.                 
                                                                                
       01  MAIL-HDR-DATE.                                                       
           05  FILLER                  PIC X(6)   VALUE 'DATE: '.               
           05  MAIL-DATE               PIC X(10)  VALUE SPACES.                 
           05  FILLER                  PIC X(124) VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(160) VALUE SPACES.                 
                                                                                
      *---------------------------------------------------------------*         
      *                      REPORT DETAILS                           *         
      *---------------------------------------------------------------*         
       01  L50C-HEADER.                                                         
           05  FILLER               PIC X(29)   VALUE SPACES.                   
           05  FILLER               PIC X(97)   VALUE                           
                       'Report for non-FS and marital status not Married        
      -         ' for cases with key-avail or 3 months before PCD'.             
           05  FILLER               PIC X(13)   VALUE SPACES.                   
           05  FILLER               PIC X(6)    VALUE 'DATE: '.                 
           05  L50C-NUM-DTE         PIC X(10)   VALUE SPACES.                   
                                                                                
       01  L50C-LINE                PIC X(160)  VALUE ALL '-'.                  
                                                                                
       01  L50C-HEADER1.                                                        
           05  FILLER              PIC X(04)   VALUE ' S/N'.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'REGN'.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(08)   VALUE 'POST OIC'.                
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'BUYER NRIC'.              
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(20)   VALUE 'BUYER NAME'.              
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'MARRIAGE CERT NO'.        
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'DATE OF MARRIAGE'.        
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(09)   VALUE 'SPOUSE IC'.               
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(20)   VALUE 'SPOUSE NAME'.             
                                                                                
                                                                                
       01  L50C-DETAIL1.                                                        
           05  L50C-SNO             PIC ZZZ9    VALUE ZEROES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  L50C-REGN            PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L50C-OIC             PIC X(05)   VALUE SPACES.                   
           05  FILLER               PIC X(05)   VALUE SPACES.                   
           05  L50C-AP-NRIC         PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  L50C-AP-NAME         PIC X(20)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L50C-NUM-MARRIAGE    PIC X(15)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  L50C-DTE-MARRIAGE    PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(10)   VALUE SPACES.                   
           05  L50C-SP-NRIC         PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  L50C-SP-NAME         PIC X(20)   VALUE SPACES.                   
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13CCNT                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-CCNT.                                                    
           05  WS-LINK-CNTCT-TYPE        PIC X(3).                              
           05  WS-LINK-STAFF-NICK        PIC X(6).                              
           05  WS-LINK-STAFF-NUM         PIC X(6).                              
           05  WS-LINK-STAFF-CNTCT       PIC X(50).                             
           05  WS-LINK-SYSERR            PIC 9(4).                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F50C       THRU 2000-EXIT.                     
           PERFORM 3000-MAIN-ROUTINE        THRU 3000-EXIT                      
                   UNTIL WS-F50C-EOF.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F50C                                                  
               OUTPUT P13LC50C.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8               TO  WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8            TO  WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10                TO  WS-DTE-CURR-10.                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-CHECK-EMAIL.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO WS-LINK-REC-CCNT.                
           MOVE F50C-OIC                    TO WS-LINK-STAFF-NICK.              
           MOVE 'EML'                       TO WS-LINK-CNTCT-TYPE.              
                                                                                
           PERFORM 8000-CALL-BP13CCNT       THRU 8000-EXIT.                     
                                                                                
           MOVE SPACES                      TO MAIL-HDR-MAIL-RCPT-TO            
                                               MAIL-HDR-MAIL-TO.                
                                                                                
           STRING 'RCPT TO:<'          DELIMITED BY SIZE                        
                   WS-LINK-STAFF-CNTCT DELIMITED BY SPACE                       
                  '>'                  DELIMITED BY SIZE                        
                   INTO MAIL-HDR-MAIL-RCPT-TO.                                  
                                                                                
           STRING 'TO:<'               DELIMITED BY SIZE                        
                   WS-LINK-STAFF-CNTCT DELIMITED BY SPACE                       
                  '>'                  DELIMITED BY SIZE                        
                   INTO MAIL-HDR-MAIL-TO.                                       
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1200-PRINT-L50C-HEADER.                                                  
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-10              TO MAIL-DATE                        
                                               L50C-NUM-DTE.                    
                                                                                
           WRITE P13L50C-REC                FROM MAIL-HDR-HELO.                 
           WRITE P13L50C-REC                FROM MAIL-HDR-MAIL-FROM.            
           WRITE P13L50C-REC                FROM MAIL-HDR-MAIL-RCPT-TO.         
      *    WRITE P13L50C-REC                FROM MAIL-HDR-MAIL-RCPT2.           
           WRITE P13L50C-REC                FROM MAIL-HDR-DATA.                 
           WRITE P13L50C-REC                FROM MAIL-HDR-FROM.                 
           WRITE P13L50C-REC                FROM MAIL-HDR-MAIL-TO.              
      *    WRITE P13L50C-REC                FROM MAIL-HDR-MAIL-TO-CC1.          
           WRITE P13L50C-REC                FROM MAIL-SUBJECT-LC50C.            
           WRITE P13L50C-REC                FROM MAIL-HDR-DATE.                 
                                                                                
           WRITE P13L50C-REC                FROM MAIL-SPACES.                   
           WRITE P13L50C-REC                FROM MAIL-SPACES.                   
                                                                                
           WRITE P13L50C-REC                FROM L50C-HEADER.                   
           WRITE P13L50C-REC                FROM MAIL-SPACES.                   
                                                                                
           WRITE P13L50C-REC                FROM L50C-LINE.                     
           WRITE P13L50C-REC                FROM L50C-HEADER1.                  
           WRITE P13L50C-REC                FROM L50C-LINE.                     
           WRITE P13L50C-REC                FROM MAIL-SPACES.                   
                                                                                
           MOVE ZEROES                      TO WS-NUM-SNO-LC50.                 
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F50C.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F50C                                                        
              AT END MOVE 'Y'               TO WS-F50C-EOF-FLAG.                
                                                                                
           IF WS-F50C-EOF-FLAG NOT = 'Y'                                        
              ADD 1                         TO WS-F50C-READ                     
           ELSE                                                                 
              WRITE P13L50C-REC             FROM MAIL-SPACES                    
              WRITE P13L50C-REC             FROM MAIL-SPACES                    
              WRITE P13L50C-REC             FROM MAIL-END                       
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F50C-OIC NOT = SPACES AND LOW-VALUES AND ZEROES                   
              IF F50C-OIC NOT = WS-PREV-OIC                                     
                 IF WS-PREV-OIC NOT = SPACES                                    
                    WRITE P13L50C-REC            FROM MAIL-SPACES               
                    WRITE P13L50C-REC            FROM MAIL-SPACES               
                                                                                
                    WRITE P13L50C-REC            FROM MAIL-END                  
                 END-IF                                                         
                 PERFORM 1100-CHECK-EMAIL        THRU 1100-EXIT                 
                 PERFORM 1200-PRINT-L50C-HEADER THRU 1200-EXIT                  
                 MOVE F50C-OIC                   TO WS-PREV-OIC                 
              END-IF                                                            
              PERFORM 3100-WRITE-L50C-REPORT     THRU 3100-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F50C  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-WRITE-L50C-REPORT.                                                  
      *---------------------------------------------------------------*         
           INITIALIZE P13L50C-REC.                                              
           MOVE SPACES                      TO P13L50C-REC.                     
           ADD 1                            TO WS-L50C-WRTN                     
                                               WS-NUM-SNO-LC50                  
                                                                                
           MOVE WS-NUM-SNO-LC50         TO L50C-SNO.                            
           MOVE F50C-REGN               TO L50C-REGN.                           
           MOVE F50C-OIC                TO L50C-OIC.                            
           MOVE F50C-AP-NRIC            TO L50C-AP-NRIC.                        
           MOVE F50C-AP-NAME            TO L50C-AP-NAME.                        
           MOVE F50C-NUM-MARRIAGE       TO L50C-NUM-MARRIAGE.                   
           MOVE F50C-DTE-MARRIAGE       TO L50C-DTE-MARRIAGE.                   
           MOVE F50C-SP-NRIC            TO L50C-SP-NRIC.                        
           MOVE F50C-SP-NAME            TO L50C-SP-NAME.                        
                                                                                
           WRITE P13L50C-REC               FROM L50C-DETAIL1.                   
           WRITE P13L50C-REC               FROM MAIL-SPACES.                    
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       8000-CALL-BP13CCNT.                                                      
      *------------------------------------------------------------             
                                                                                
           CALL 'BP13CCNT' USING  WS-LINK-REC-CCNT.                             
                                                                                
           EVALUATE WS-LINK-SYSERR                                              
           WHEN 000                                                             
              CONTINUE                                                          
           WHEN 100                                                             
              DISPLAY 'EMAIL ADDRESS NOT FOUND FOR ' F50C-OIC                   
              STRING  F50C-OIC     DELIMITED BY SPACE                           
                     '@hdb.gov.sg' DELIMITED BY SIZE                            
                                           INTO WS-LINK-STAFF-CNTCT             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13CCNT ERROR STATUS : ' WS-LINK-SYSERR                 
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------- P13LC50C -------'.                                  
           DISPLAY ' RECORDS READ    IN BP13F50C : ' WS-F50C-READ.              
           DISPLAY ' RECORDS WRITTEN IN P13LC50C : ' WS-L50C-WRTN.              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F50C                                                       
                 P13LC50C.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
