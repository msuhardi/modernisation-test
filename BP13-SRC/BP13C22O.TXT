      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C22O.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  01 AUGUST 2017.                                           
                                                                                
      * ======================================================== *              
      * SYSTEM OF COMMITMENT  (BP13)                             *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *        1.  PROGRAM TO EXTRACT DATA TO PRINT APPENDIX B   *              
      *            CONSISTING OF ALL UNITS AVAILABLE FOR         *              
      *            SELECTION, DISCARD UNITS THAT ARE SET ASIDE   *              
      *            FOR SAF AND MHA OR FREEZE FROM ALLOCATIONS.   *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *           1. BP13F205  - CONTROL RECORDS FILE            *              
      *           2. BP13K240  - FLAT AVAILABILITY HEADFLAT      *              
      *           3. BM06K110  - FLAT INVENTORY MASTER           *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *           1. BM06F800  - EXTRACT RECORD FOR PRINTING     *              
      *                          SELLING PRICE                   *              
      * -------------------------------------------------------- *              
      * CHG NO   DATE     BY   DESCRIPTION                       *              
      * -------- -------- ---  -----------                       *              
      * BP136836 01/08/17 ESA1 NEW PGM MODELLED AFTER BP13C226   *              
      * BP138485 05/11/20 EL27 INCLUDE '85' FOR '3M' FLAT-TYPE   *              
      * BP139531 10/03/23 MRR5 CATER FOR 3-ROOM FSH              *              
      * BP139531 20/03/23 MRR5 UPDATE CALLING ON BP13C913, BP13C916             
      * ======================================================== *              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K240 ASSIGN TO BP13K240                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K240-KEY-FLD                                    
                FILE STATUS  IS K240-STATUS.                                    
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ORGANIZATION  IS INDEXED                                        
                ACCESS MODE   IS DYNAMIC                                        
                RECORD KEY    IS K110-KEY-FLD                                   
                ALTERNATE KEY IS K110-AIX1                                      
                FILE STATUS   IS K110-STATUS.                                   
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K100-KEY-FLD                                    
                FILE STATUS  IS K100-STATUS.                                    
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K813-KEY-FLD                                    
                FILE STATUS  IS K813-STATUS.                                    
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BM06F800 ASSIGN TO BM06F800.                                  
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
                                                                                
       FD   BP13K240                 RECORD CONTAINS 450 CHARACTERS.            
       COPY BP13K240.                                                           
                                                                                
       FD   BM06K110                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K100.                                                           
                                                                                
       FD   BP13K813                 RECORD CONTAINS 1000 CHARACTERS.           
       COPY BP13K813.                                                           
                                                                                
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD  BM06F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F800.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  FILES-STS.                                                           
           05  K110-STATUS         PIC 99      VALUE ZEROS.                     
           05  K240-STATUS         PIC 99      VALUE ZEROS.                     
           05  K100-STATUS         PIC 99      VALUE ZEROS.                     
           05  K813-STATUS         PIC 99      VALUE ZEROS.                     
           05  F110-STATUS         PIC 99      VALUE ZEROS.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-CTL     PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-AS      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-HOLD    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-RP      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-SD      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K240    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K110    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K100    PIC 9(5)    VALUE ZEROS.                     
           05  WS-SKIP-RECORD      PIC 9(5)    VALUE ZEROS.                     
           05  WS-WRITE-CNT-F800   PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01  WS-NUM-FLAT-TYPE         PIC X(2)    VALUE SPACES.                   
       01  WS-NUM-FLAT-STATUS       PIC X(1)    VALUE SPACES.                   
       01  WS-NUM-UPG-STATUS        PIC X(3)    VALUE SPACES.                   
       01  WS-F800-NME-LOCAL-PRCNCT PIC X(40)   VALUE SPACES.                   
       01  WS-BP13C913              PIC X(8)    VALUE 'BP13C913'.               
       01  WS-BP13C916              PIC X(8)    VALUE 'BP13C916'.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-CTL           PIC X       VALUE SPACES.                   
           05  WS-EOF-K110          PIC X       VALUE SPACES.                   
           05  WS-EOF-K240          PIC X       VALUE SPACES.                   
           05  WS-K100-FND          PIC X       VALUE SPACES.                   
                                                                                
       01  WS-K110-AIX1.                                                        
           05  WS-K110-ESTATE      PIC X(3).                                    
           05  WS-K110-NEIGH       PIC X(4).                                    
           05  WS-K110-CONTRACT-NO PIC X(4).                                    
           05  WS-K110-BLK-NO      PIC X(5).                                    
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C913                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH   PIC X(4).                                  
               10  WS-LINK-NUM-ACC   PIC X(5).                                  
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-----------------------------------------------------------*             
      *      LINK-AREA TO CALL SUB-PROGRAM (DJ02C906)             *             
      *-----------------------------------------------------------*             
       01  LINK-AREA.                                                           
           05  LINK-SQL-RETCODE             PIC S9(4).                          
           05  LINK-NUM-SCHM                PIC X(4).                           
           05  LINK-NUM-ACCNT               PIC X(7).                           
           05  LINK-NUM-BNDRY-STATUS        PIC X(3).                           
                                                                                
       01  WS-SQL-RETCODE                   PIC -9(3).                          
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C916                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-C916.                                                    
           05  WS-LINK-BLDNG-GL             PIC X(5).                           
                                                                                
       01  WS-BP13C916-REC.                                                     
           05  C916-NME-LOCAL-PRCNCT        PIC X(40).                          
           05  C916-CDE-SYSERR              PIC S9(3).                          
           05  C916-CDE-ERROR               PIC X(5).                           
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
      *-----------------------------------------------------------              
       000-MAIN-ROUTINE.                                                        
      *-----------------------------------------------------------              
           PERFORM 100-OPEN-FILES  THRU 100-EXIT.                               
           PERFORM 200-READ-CTL    THRU 200-EXIT.                               
           PERFORM 300-PROCESS-REC THRU 300-EXIT                                
                   UNTIL WS-EOF-CTL = 'Y'.                                      
           PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       100-OPEN-FILES.                                                          
      *-----------------------------------------------------------              
           OPEN INPUT  BP13F205                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K240                                                 
                       BP13K813                                                 
                OUTPUT BM06F800.                                                
                                                                                
           IF K110-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BM06K110-STATUS ' K110-STATUS                 
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR, BM06K100 STATUS ' K100-STATUS                
              MOVE K100-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF K240-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BP13K240-STATUS ' K240-STATUS                 
              MOVE K240-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF K813-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BP13K813-STATUS ' K813-STATUS                 
              MOVE K813-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE ZEROS TO WS-COUNTERS.                                           
           MOVE 'N'   TO WS-EOF-CTL.                                            
           MOVE 'N'   TO WS-EOF-K110.                                           
           MOVE 'N'   TO WS-EOF-K240.                                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       200-READ-CTL.                                                            
      *-----------------------------------------------------------              
           READ BP13F205 AT END                                                 
                MOVE 'Y' TO WS-EOF-CTL                                          
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-CNT-CTL.                                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       300-PROCESS-REC.                                                         
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K240.                                             
           PERFORM 400-START-K240 THRU 400-EXIT.                                
           PERFORM 410-READ-K240  THRU 410-EXIT UNTIL                           
                   WS-EOF-K240 = 'Y'.                                           
                                                                                
           IF WS-EOF-K240 = 'Y'                                                 
              PERFORM 200-READ-CTL THRU 200-EXIT                                
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       400-START-K240.                                                          
      *-----------------------------------------------------------              
           MOVE LOW-VALUES      TO K240-KEY-FLD.                                
           MOVE '01'            TO K240-CDE-HEADER.                             
           MOVE F205-DTE-ALLOCN TO K240-DTE-ALLOC.                              
                                                                                
           START BP13K240 KEY NOT < K240-KEY-FLD INVALID                        
                 DISPLAY 'START ERROR, BP13K240-STATUS ' K240-STATUS            
                 DISPLAY 'K240-KEY-FLD ' K240-KEY-FLD                           
                 MOVE 'Y' TO WS-EOF-K240.                                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       410-READ-K240.                                                           
      *-----------------------------------------------------------              
           READ BP13K240 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K240                                         
                GO TO 410-EXIT.                                                 
                                                                                
           IF K240-CDE-HEADER = '01'                                            
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 410-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF K240-DTE-ALLOC = F205-DTE-ALLOCN                                  
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 410-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF F205-NUM-NT-ZONE = K240-NUM-SELECTION                             
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 410-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF F205-NUM-FLAT-TYPE = '2F'                                         
              IF K240-NUM-FLAT-TYPE2(1:2) = 'SA' OR                             
                 K240-NUM-FLAT-TYPE2 = '2-ROOM   '                              
                 CONTINUE                                                       
              ELSE                                                              
                 GO TO 410-EXIT                                                 
              END-IF                                                            
           ELSE                                                                 
              IF K240-NUM-FLAT-TYPE2 = '3-ROOM   ' OR                           
                 K240-NUM-FLAT-TYPE2 = '3P-ROOM  '                              
                 PERFORM 415-READ-BP13K813 THRU 415-EXIT                        
              END-IF                                                            
              IF F205-NUM-FLAT-TYPE = '3M'                                      
                 IF K240-NUM-FLAT-TYPE2 = '3-ROOM   '                           
                    IF K813-NUM-MATURE-EST-TAG = 'Y'                            
                       CONTINUE                                                 
                    ELSE                                                        
                       GO TO 410-EXIT                                           
                    END-IF                                                      
                 ELSE                                                           
                    IF K240-NUM-FLAT-TYPE2 = '3P-ROOM  '                        
                       CONTINUE                                                 
                    ELSE                                                        
                       GO TO 410-EXIT                                           
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF F205-NUM-FLAT-TYPE = '3N'                                   
                    IF K240-NUM-FLAT-TYPE2 = '3-ROOM   '                        
                       IF K813-NUM-MATURE-EST-TAG = 'Y'                         
                          GO TO 410-EXIT                                        
                       ELSE                                                     
                          CONTINUE                                              
                       END-IF                                                   
                    ELSE                                                        
                       GO TO 410-EXIT                                           
                    END-IF                                                      
                 ELSE                                                           
                   IF F205-NUM-FLAT-TYPE(1:1) = K240-NUM-FLAT-TYPE2(1:1)        
                      CONTINUE                                                  
                   ELSE                                                         
                      GO TO 410-EXIT                                            
                   END-IF                                                       
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-READ-CNT-K240.                                           
                                                                                
           PERFORM 420-DECODE-FLAT-TYPE THRU 420-EXIT.                          
           PERFORM 430-PROCESS-K110     THRU 430-EXIT.                          
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       415-READ-BP13K813.                                                       
      *-----------------------------------------------------------              
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE K240-CDE-NT                     TO K813-NUM-ZONE.               
           IF K240-NUM-FLAT-TYPE2 = '3P-ROOM  '                                 
              MOVE '3P'                         TO K813-NUM-FLAT-TYPE           
           ELSE                                                                 
              IF K240-NUM-FLAT-TYPE2 = 'SA       '                              
                 MOVE '2F'                      TO K813-NUM-FLAT-TYPE           
              ELSE                                                              
                 MOVE K240-NUM-FLAT-TYPE2(1:1)  TO K813-NUM-FLAT-TYPE           
              END-IF                                                            
           END-IF.                                                              
           MOVE K240-DTE-BALLOT                TO K813-DTE-BALLOT.              
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE K813-STATUS                                                 
             WHEN 00                                                            
               CONTINUE                                                         
             WHEN 23                                                            
               MOVE SPACES   TO K813-NUM-MATURE-EST-TAG                         
               DISPLAY 'BP13K813 NOT FND : ' K813-KEY-FLD                       
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BP13K813 : ' K813-STATUS                  
                      ' ZONE = ' K813-KEY-FLD                                   
                    PERFORM  999-CLOSE-ROUTINE      THRU 999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       415-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       420-DECODE-FLAT-TYPE.                                                    
      *-----------------------------------------------------------              
           EVALUATE F205-NUM-FLAT-TYPE                                          
              WHEN '1 '                                                         
                MOVE '01' TO WS-NUM-FLAT-TYPE                                   
              WHEN '1A'                                                         
                MOVE '1A' TO WS-NUM-FLAT-TYPE                                   
              WHEN '2 '                                                         
              WHEN '2F'                                                         
                MOVE '02' TO WS-NUM-FLAT-TYPE                                   
              WHEN '2A'                                                         
                MOVE '2A' TO WS-NUM-FLAT-TYPE                                   
              WHEN '3 '                                                         
              WHEN '3M'                                                         
              WHEN '3N'                                                         
                MOVE '03' TO WS-NUM-FLAT-TYPE                                   
              WHEN '4 '                                                         
              WHEN '4D'                                                         
              WHEN '4P'                                                         
                MOVE '04' TO WS-NUM-FLAT-TYPE                                   
              WHEN '5 '                                                         
              WHEN '5D'                                                         
              WHEN '5P'                                                         
                MOVE '05' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'E '                                                         
              WHEN 'ED'                                                         
                MOVE '06' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'H '                                                         
                MOVE '07' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'MG'                                                         
                MOVE '08' TO WS-NUM-FLAT-TYPE                                   
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       430-PROCESS-K110.                                                        
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K110.                                             
           MOVE K240-NUM-ESTATE     TO WS-K110-ESTATE.                          
           MOVE K240-NUM-NEIGH      TO WS-K110-NEIGH.                           
           MOVE K240-NUM-CNTRCT     TO WS-K110-CONTRACT-NO.                     
           MOVE K240-NUM-BLK        TO WS-K110-BLK-NO.                          
           MOVE WS-K110-AIX1        TO K110-AIX1.                               
           PERFORM 431-START-K110 THRU 431-EXIT.                                
           PERFORM 432-READ-K110  THRU 432-EXIT UNTIL                           
                   WS-EOF-K110 = 'Y'.                                           
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       431-START-K110.                                                          
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K110.                                             
                                                                                
           START BM06K110 KEY = K110-AIX1 INVALID                               
                 DISPLAY 'START ERROR, BM06K110-STATUS ' K110-STATUS            
                 DISPLAY 'K110-AIX1 ' K110-AIX1                                 
                 MOVE 'Y' TO WS-EOF-K110.                                       
                                                                                
       431-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       432-READ-K110.                                                           
      *-----------------------------------------------------------              
           READ BM06K110 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K110                                         
                GO TO 432-EXIT.                                                 
                                                                                
           IF K110-AIX1 = WS-K110-AIX1                                          
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'Y' TO WS-EOF-K110                                           
              GO TO 432-EXIT.                                                   
                                                                                
           IF (K110-DTE-ALLOCN = F205-DTE-ALLOCN) AND                           
              (K110-NUM-SELECTION = F205-NUM-SELECTION) AND                     
              (K110-NUM-ALLOCN-QTR = F205-DTE-BALLOT)                           
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 432-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF K110-FLAT-STATUS = 'SD'                                           
              ADD 1 TO WS-READ-CNT-SD                                           
              GO TO 432-EXIT                                                    
           END-IF.                                                              
                                                                                
           PERFORM 435-PROCESS-DATA  THRU 435-EXIT.                             
                                                                                
       432-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       435-PROCESS-DATA.                                                        
      *-----------------------------------------------------------              
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
                                                                                
           MOVE K110-SCH-ACC-NO      TO WS-LINK-REC.                            
                                                                                
           CALL WS-BP13C913 USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF F205-NUM-FLAT-TYPE = '3M'                                         
              IF K813-NUM-MATURE-EST-TAG = 'Y' AND                              
                 COMM8-CDE-TYP-ACTLUSE = WS-NUM-FLAT-TYPE                       
                 CONTINUE                                                       
              ELSE                                                              
                 IF ((COMM8-CDE-TYP-ACTLUSE = WS-NUM-FLAT-TYPE) AND             
                    ((COMM8-CDE-MODL-DESG = '81' OR '82' OR '83' OR             
                             '84' OR '85' OR '86' OR '87' OR '88') OR           
                      K110-NUM-DESIGN-OFFER = 'P'))                             
                    CONTINUE                                                    
                 ELSE                                                           
                    GO TO 432-EXIT                                              
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F205-NUM-FLAT-TYPE = '3N'                                      
                 IF K813-NUM-MATURE-EST-TAG = 'Y'                               
                    GO TO 432-EXIT                                              
                 ELSE                                                           
                    IF (COMM8-CDE-TYP-ACTLUSE = WS-NUM-FLAT-TYPE) AND           
                       (COMM8-CDE-MODL-DESG NOT = '34' AND '70' AND             
                       '71' AND '72' AND '73' AND '81' AND '82' AND             
                       '83' AND '84' AND '85' AND '86' AND '87' AND             
                       '88')                                                    
                       IF K110-NUM-DESIGN-OFFER NOT = 'P'                       
                          CONTINUE                                              
                       ELSE                                                     
                          GO TO 432-EXIT                                        
                       END-IF                                                   
                    ELSE                                                        
                       GO TO 432-EXIT                                           
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF F205-NUM-FLAT-TYPE = '2F'                                   
                    IF K110-NUM-DESIGN-OFFER = 'F' AND                          
                       COMM8-CDE-TYP-ACTLUSE NOT = '03'                         
                       CONTINUE                                                 
                    ELSE                                                        
                       IF COMM8-CDE-MODL-DESG = '96' OR '97'                    
                          CONTINUE                                              
                       ELSE                                                     
                          GO TO 432-EXIT                                        
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    IF F205-NUM-GEN3 = 'Y'                                      
                       IF (COMM8-CDE-TYP-ACTLUSE =  WS-NUM-FLAT-TYPE)           
                                             AND                                
                          (COMM8-CDE-MODL-DESG = '90' OR '91' OR '95')          
                          CONTINUE                                              
                       ELSE                                                     
                          GO TO 432-EXIT                                        
                       END-IF                                                   
                    ELSE                                                        
                       IF F205-NUM-GEN3 = 'N'                                   
                          IF (COMM8-CDE-TYP-ACTLUSE =                           
                              WS-NUM-FLAT-TYPE)  AND                            
                             (COMM8-CDE-MODL-DESG NOT = '90' AND                
                                                        '91' AND '95')          
                             CONTINUE                                           
                          ELSE                                                  
                             GO TO 432-EXIT                                     
                          END-IF                                                
                       ELSE                                                     
                          IF WS-NUM-FLAT-TYPE = '06'                            
                             IF COMM8-CDE-TYP-ACTLUSE = '06' OR '08'            
                                CONTINUE                                        
                             ELSE                                               
                                GO TO 432-EXIT                                  
                             END-IF                                             
                          ELSE                                                  
                             IF (COMM8-CDE-TYP-ACTLUSE =                        
                                 WS-NUM-FLAT-TYPE)  AND                         
                                (COMM8-CDE-MODL-DESG NOT = '80'                 
                                                     AND '89' AND '92')         
                                CONTINUE                                        
                             ELSE                                               
                                GO TO 432-EXIT                                  
                             END-IF                                             
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
      **---> GET UPGRADING STATUS                                               
           MOVE SPACES      TO LINK-AREA.                                       
           INITIALIZE          LINK-AREA.                                       
           MOVE K110-SCH-ACC-NO(1:4) TO LINK-NUM-SCHM.                          
           MOVE K110-SCH-ACC-NO(5:7) TO LINK-NUM-ACCNT.                         
                                                                                
           MOVE SPACES        TO WS-NUM-UPG-STATUS.                             
           CALL 'DJ02C906' USING LINK-AREA.                                     
                                                                                
           IF LINK-SQL-RETCODE = 0                                              
              MOVE LINK-NUM-BNDRY-STATUS  TO WS-NUM-UPG-STATUS                  
           ELSE                                                                 
              IF LINK-SQL-RETCODE = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB : '                         
                                                K110-SCH-ACC-NO(1:11)           
              ELSE                                                              
                 MOVE LINK-SQL-RETCODE TO WS-SQL-RETCODE                        
                 DISPLAY 'RETCODE : ' WS-SQL-RETCODE                            
                 DISPLAY 'ERROR IN CALLING DJ02C906'.                           
                                                                                
           IF (K110-DTE-ACCEPTANCE = ZEROS OR SPACES OR LOW-VALUES) AND         
              (K110-DTE-KEY-ISSUED  = ZEROS OR SPACES OR LOW-VALUES)            
               CONTINUE                                                         
           ELSE                                                                 
              ADD 1 TO WS-READ-CNT-AS                                           
              GO TO 432-EXIT.                                                   
                                                                                
           IF K110-ALLOCN-TAG NOT = SPACES AND LOW-VALUES AND                   
                                    'BFS'                                       
              ADD 1 TO WS-READ-CNT-HOLD                                         
              GO TO 432-EXIT.                                                   
                                                                                
           IF K110-FLAT-STATUS = 'RP' OR 'RR'                                   
              ADD   1  TO WS-READ-CNT-RP.                                       
                                                                                
           ADD 1 TO WS-READ-CNT-K110.                                           
                                                                                
           PERFORM 440-BM06K100-READ   THRU 440-EXIT.                           
                                                                                
           IF WS-K100-FND = 'Y'                                                 
              MOVE K100-CDE-BLK TO WS-LINK-BLDNG-GL                             
              DISPLAY 'BLDNG : ' WS-LINK-BLDNG-GL                               
                                                                                
              CALL WS-BP13C916 USING WS-LINK-REC-C916 WS-BP13C916-REC           
              MOVE C916-NME-LOCAL-PRCNCT TO WS-F800-NME-LOCAL-PRCNCT            
              DISPLAY 'PRCNCT: ' C916-NME-LOCAL-PRCNCT                          
           END-IF.                                                              
                                                                                
           PERFORM 600-WRITE-F800 THRU 600-EXIT.                                
                                                                                
       435-EXIT.                                                                
           EXIT.                                                                
                                                                                
       440-BM06K100-READ.                                                       
      *-----------------------------------------------------------              
           MOVE 'N'                  TO WS-K100-FND.                            
           MOVE LOW-VALUES           TO K100-KEY-FLD.                           
           MOVE K110-AIX1            TO K100-KEY-FLD.                           
                                                                                
           READ BM06K100.                                                       
           EVALUATE K100-STATUS                                                 
           WHEN 00                                                              
              ADD  1       TO   WS-READ-CNT-K100                                
              MOVE  'Y '   TO   WS-K100-FND                                     
           WHEN 23                                                              
              MOVE  'N'    TO   WS-K100-FND                                     
              MOVE SPACES  TO   WS-F800-NME-LOCAL-PRCNCT                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' K100-STATUS                   
              MOVE K100-STATUS            TO  RETURN-CODE                       
              PERFORM 999-CLOSE-ROUTINE   THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       600-WRITE-F800.                                                          
      *-----------------------------------------------------------              
           MOVE SPACES                 TO F800-REC.                             
           INITIALIZE F800-REC.                                                 
                                                                                
           IF K110-REGN-NO(1:2) = '##'                                          
               MOVE SPACES TO K110-REGN-NO                                      
           END-IF.                                                              
                                                                                
           MOVE K110-REC               TO F800-REC (1:500).                     
                                                                                
           MOVE COMM8-NME-STREET       TO F800-STREET-NAME.                     
           MOVE COMM8-NUM-POSTAL-CODE  TO F800-POSTAL-CODE.                     
           MOVE COMM8-NUM-LEVEL        TO F800-LEVEL-NO.                        
           MOVE COMM8-NUM-UNIT-MAIN    TO F800-MAIN-UNIT.                       
           MOVE COMM8-NUM-UNIT-SUB     TO F800-SUB-UNIT.                        
           MOVE COMM8-CDE-BUC          TO F800-CDE-BUC.                         
           MOVE COMM8-QTY-AREA-TOTAL   TO F800-QTY-AREA-TOTAL.                  
           MOVE COMM8-QTY-AREA-ORGNL   TO F800-QTY-AREA-ORGNL.                  
           MOVE K240-CDE-NT            TO F800-CDE-NT.                          
           MOVE 'RF'                   TO F800-CDE-ZONE                         
           MOVE K240-NUM-SELECTION     TO F800-CDE-SELTY.                       
           MOVE SPACES                 TO F800-NUM-SITE                         
                                          F800-NUM-COMBINE-SA                   
                                          F800-CDE-FLAT-STATUS.                 
           MOVE K240-DTE-BALLOT        TO F800-DTE-BALLOT.                      
           MOVE WS-NUM-UPG-STATUS      TO F800-NUM-UPG-STATUS.                  
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
              WHEN '01'                                                         
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '1A' TO F800-NUM-FT                                     
                ELSE                                                            
                   MOVE '01' TO F800-NUM-FT                                     
                END-IF                                                          
              WHEN '02'                                                         
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '2A' TO F800-NUM-FT                                     
                ELSE                                                            
                   MOVE '02' TO F800-NUM-FT                                     
                END-IF                                                          
              WHEN '03'                                                         
                MOVE '03' TO F800-NUM-FT                                        
              WHEN '04'                                                         
                MOVE '04' TO F800-NUM-FT                                        
              WHEN '05'                                                         
                MOVE '05' TO F800-NUM-FT                                        
              WHEN '06'                                                         
                MOVE '06' TO F800-NUM-FT                                        
           END-EVALUATE.                                                        
                                                                                
      ** MOVE 02 TO FLAT TYPE WHEN DESG OFFER = F                               
      ** DO NOT OVERWRITE WITH 02 IF 3-ROOM FSH CASE                            
                                                                                
           IF K110-NUM-DESIGN-OFFER = 'F' AND                                   
              F800-CDE-TYP-ACTUSE NOT = '03'                                    
              MOVE '02'                    TO F800-CDE-TYP-ACTUSE               
                                              F800-NUM-FT                       
           END-IF.                                                              
                                                                                
           MOVE WS-F800-NME-LOCAL-PRCNCT   TO F800-NME-LOCAL-PRCNCT.            
                                                                                
           WRITE F800-REC.                                                      
           ADD 1 TO WS-WRITE-CNT-F800.                                          
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       999-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------              
           DISPLAY '*----------  BP13C22O SUMMARY LIST  ----------*'.           
           DISPLAY ' NO OF BP13F205 READ    = ' WS-READ-CNT-CTL.                
           DISPLAY ' NO OF BP13K240 READ    = ' WS-READ-CNT-K240.               
           DISPLAY ' NO OF BM06K110 READ    = ' WS-READ-CNT-K110.               
           DISPLAY ' NO OF BM06K100 READ    = ' WS-READ-CNT-K100.               
           DISPLAY ' NO OF BM06F800 WRITE   = ' WS-WRITE-CNT-F800.              
           DISPLAY ' REC NOT FOR ALLOCN     = ' WS-SKIP-RECORD.                 
           DISPLAY ' NO OF RECORD AS        = ' WS-READ-CNT-AS.                 
           DISPLAY ' NO OF RECORD SD        = ' WS-READ-CNT-SD.                 
           DISPLAY ' REC FREEZE FR ALLOCN   = ' WS-READ-CNT-HOLD.               
           DISPLAY ' NO OF RECORD RP        = ' WS-READ-CNT-RP.                 
                                                                                
           CLOSE BP13F205                                                       
                 BM06F800                                                       
                 BP13K240                                                       
                 BP13K813                                                       
                 BM06K110                                                       
                 BM06K100.                                                      
                                                                                
           IF K240-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BP13K240-STATUS ' K240-STATUS               
           END-IF.                                                              
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BP13K813-STATUS ' K813-STATUS               
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' K110-STATUS               
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BM06K100-STATUS ' K100-STATUS               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
