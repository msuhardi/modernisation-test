      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CC25.                                                 
       AUTHOR.        ABH.                                                      
       DATE-WRITTEN.  12/07/2006.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  TO CHECK DATE BALANCE CLOSE AGAINST THE       *        
      *                  CURRENT DATE (CCYYMM). RETURNS '99' IF THE    *        
      *                  DATES ARE NOT EQUAL.                          *        
      *                                                                *        
      *   INPUT FILES :  1.  SY02F001                                  *        
      *                  2.  BP13KD65                                  *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132845  BA3  12/07/2006 NEW PROGRAM                        *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13KD65  ASSIGN        TO BP13KD65                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD65-CDE-FILEID                    
                            FILE STATUS   IS WS-KD65-STATUS.                    
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13KD65                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
                                                                                
       COPY BP13KD65.                                                           
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-KD65-STATUS               PIC 9(2)    VALUE 0.                    
       01  CNT-KD65-READ                PIC 9(8)    VALUE 0.                    
       01  CNT-KD65-NOTFOUND            PIC 9(8)    VALUE 0.                    
       01  WS-SYS-DATE                  PIC X(8)    VALUE SPACES.               
       01  WS-LAST-DAY-PREV             PIC X(8)    VALUE SPACES.               
       01  WS-KD65-FD                   PIC X(1)    VALUE SPACES.               
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
           PERFORM  1000-OPEN-FILES      THRU    1000-EXIT.                     
           PERFORM  3000-PROCESS         THRU    3000-EXIT.                     
           PERFORM  9000-CLOSE-ROUTINE   THRU    9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13KD65.                                                
                                                                                
           IF WS-KD65-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD65-STATUS ' WS-KD65-STATUS         
               MOVE     WS-KD65-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-SY02F001   THRU 2000-EXIT.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-SYS-DATE.                           
           MOVE F001-DTE-LAST-DAY-PREV TO WS-LAST-DAY-PREV.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE 'N'                            TO WS-KD65-FD.                   
           PERFORM 4000-READ-KD65              THRU 4000-EXIT.                  
                                                                                
           IF WS-KD65-FD = 'Y'                                                  
              IF KD65-DTE-BAL-CLOSE(1:6) NOT = WS-LAST-DAY-PREV(1:6)            
                 MOVE '99'                     TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-READ-KD65.                                                          
      *----------------------------------------------------------------*        
           MOVE 'KD65'        TO KD65-CDE-FILEID.                               
                                                                                
           READ BP13KD65                                                        
           EVALUATE WS-KD65-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD65-FD                                         
                 ADD 1 TO CNT-KD65-READ                                         
              WHEN 23                                                           
                 ADD 1 TO CNT-KD65-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD65 - STATUS '                  
                             WS-KD65-STATUS                                     
                 MOVE        WS-KD65-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
           CLOSE SY02F001                                                       
                 BP13KD65.                                                      
                                                                                
           IF WS-KD65-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD65-STATUS ' WS-KD65-STATUS.         
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CC25 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS FOUND FROM (BP13KD65) = '                     
                    CNT-KD65-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD65) = '                     
                    CNT-KD65-NOTFOUND.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
