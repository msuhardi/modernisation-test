       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC12.                                                 
       AUTHOR. BALA AMUTHAN.                                                    
       DATE-WRITTEN.  08/05/06.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS PROGRAMME WILL VALIDATE GIRO PAYMENT,  *                
      *            AGAINST BP13FD00, OUTPUT VALID FILE BP13F136*                
      *            IF THE RECORD DOES NOT MATCH, THE           *                
      *            OUTPUT RECORD WILL MOVE TO THE KIV FILE     *                
      *            BP13K140.                                   *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. AC08F685 (TRANSACTION FILE)               *                
      *           2. BP13FD00 (SUB-TRANSFER FILE)              *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13K140 (KIV FILE)                       *                
      *           2. BP13F136 (CASH DAILY TRANSACTION FILE)    *                
      *                                                        *                
      *     LISTINGS:                                          *                
      *           1. BP13LC12 (CONTROL LIST)                   *                
      *                                                        *                
      * BP132845 BA2 080506 - SAME PGM AS BP13C101             *                
      * BP133051 BA3 270407 - INCLUDE LUMP SUM PAYMENT- GIRO-LS*                
      * BP133115 BA3 221007 - MOVE CDE-PAYMENT-TYPE            *                
      * BP133091 RB12 161107  EXPAND FD00 LNGTH FR 1200 TO 1600*                
      * BP133535 JB8 110209 - OUTPUT C08F685A IF KD00 NOT FOUND*                
      *                       BUT KD03 FOUND                   *                
      * BP133876 PCL 040610 - ADD CHECK ON FD00-AMT-INSTAL AND *                
      *                       FD00-AMT-LOAN-BAL                *                
      * BP134149 JB8  230211  OUTPUT AB03F001 IF KD00 NOT FOUND*                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AC08F685 ASSIGN TO AC08F685.                                  
                                                                                
           SELECT BP13FD00 ASSIGN TO BP13FD00.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K140-KEY-FLD                                      
                FILE STATUS IS K140-STATUS.                                     
                                                                                
           SELECT BP13KD03 ASSIGN TO BP13KD03                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS KD03-KEY-FLD                                      
                ALTERNATE KEY IS KD03-ALT-KEY1                                  
                FILE STATUS IS KD03-STATUS.                                     
                                                                                
           SELECT BP13F136 ASSIGN TO BP13F136.                                  
           SELECT C08F685A ASSIGN TO C08F685A.                                  
           SELECT AB03F001 ASSIGN TO AB03F001.                                  
                                                                                
           SELECT BP13LC12 ASSIGN TO BP13LC12.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AC08F685                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AC08F685.                                                           
                                                                                
       FD  BP13F136                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F136.                                                           
                                                                                
       FD  C08F685A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  C08F685A-REC          PIC X(100).                                    
                                                                                
       FD   AB03F001                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AB03F001.                                                           
                                                                                
       FD  BP13K140                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K140.                                                           
                                                                                
       FD  BP13KD03                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD03.                                                           
                                                                                
       FD  BP13FD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13FD00.                                                           
                                                                                
       FD  BP13LC12                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-CONTROL-LIST             PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  K140-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD03-STATUS                    PIC 99      VALUE ZEROES.             
                                                                                
       01  WS-K140-STATUS.                                                      
           05  WS-K140-STATUS1            PIC 9.                                
           05  WS-K140-STATUS2            PIC 9.                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F685-EOF-FLAG           PIC X       VALUE 'N'.                
           05  WS-FD00-EOF-FLAG           PIC X       VALUE 'N'.                
                                                                                
       01  WS-KD03-FND                    PIC X(01)   VALUE SPACE.              
           88  KD03-FND                   VALUE 'Y'.                            
       01  WS-CNT-AC08F685-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-KIV                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F136-VALID              PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-FD00-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD03-FND                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F685A-WRT               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F001-WRT                PIC 9(5)    VALUE ZEROES.             
       01  WS-K140-COUNT-HISTORY          PIC 9(2)    VALUE ZEROES.             
       01  WS-CDE-APPLT                   PIC X       VALUE SPACES.             
       01  WS-NUM-NRIC                    PIC X(9)    VALUE SPACES.             
       01  WS-LINE-CNT                    PIC 9(2)    VALUE 54.                 
       01  WS-PAGENO                      PIC 9(5)    VALUE ZEROES.             
       01  WS-SNO                         PIC 9(5)    VALUE ZEROES.             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
      ***************************************************************           
      *    WS  OR CONTROL-LIST                                      *           
      ***************************************************************           
                                                                                
       01  WS-CONTROL-HEAD.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC12'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'HDBCAT3'.               
           05  FILLER                  PIC X(25) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  CL-DATE-HD              PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  CL-PAGENO               PIC ZZZZ9 .                              
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
                                                                                
       01  WS-CONTROL-LIST-1.                                                   
           05  FILLER              PIC X(50)         VALUE SPACES.              
           05  FILLER              PIC X(29)         VALUE                      
               'ERROR REPORT ON GIRO PAYMENT '.                                 
           05  FILLER              PIC X(53)         VALUE SPACES.              
                                                                                
       01  WS-CONTROL-LIST-2.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE ' S/NO'.                    
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE 'REG NO'.                   
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'SCH-ACC'.                  
           05  FILLER              PIC X(08)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAYMENT'.                  
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(11)  VALUE  'AMT-RECEIPT'.             
           05  FILLER              PIC X(69)  VALUE  SPACES.                    
                                                                                
       01  WS-CONTROL-LIST-3.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-SNO              PIC ZZZZ9  VALUE ZEROES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  CL-RENO             PIC X(08)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  CL-SCH-ACC          PIC X(11)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  CL-PAY-TY           PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  CL-AMT-RECEIPT      PIC ZZZZZ9.99.                               
           05  FILLER              PIC X(71)  VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-4.                                                   
           05  FILLER              PIC X(132) VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-5.                                                   
           05  FILLER              PIC X(132) VALUE ALL '-'.                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-AC08F685            THRU 100-EXIT.                  
           PERFORM 200-READ-BP13FD00            THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
               UNTIL WS-FD00-EOF-FLAG = 'Y' AND                                 
                     WS-F685-EOF-FLAG = 'Y'.                                    
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       000-OPEN-FILE.                                                           
           OPEN INPUT  AC08F685                                                 
                       BP13FD00                                                 
                       BP13KD03                                                 
                I-O    BP13K140                                                 
                OUTPUT BP13F136                                                 
                       AB03F001                                                 
                       C08F685A                                                 
                       BP13LC12.                                                
                                                                                
           IF K140-STATUS NOT = 0  AND                                          
              K140-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K140 STATUS ' K140-STATUS            
              MOVE K140-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD03-STATUS NOT = 0  AND                                          
              KD03-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13KD03 STATUS ' KD03-STATUS            
              MOVE KD03-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
                  DELIMITED BY SIZE INTO CL-DATE-HD.                            
                                                                                
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE AC08F685                         *              
      ************************************************************              
                                                                                
       100-READ-AC08F685.                                                       
                                                                                
           READ AC08F685                                                        
                AT END  MOVE HIGH-VALUE TO F685-NUM-HDB-REF                     
                MOVE 'Y'                TO WS-F685-EOF-FLAG                     
                GO TO 100-EXIT.                                                 
           ADD  1                       TO WS-CNT-AC08F685-READ.                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13FD00.                                                       
                                                                                
           READ BP13FD00                                                        
                AT END  MOVE HIGH-VALUE TO FD00-ALT-KEY1                        
                MOVE 'Y'                TO WS-FD00-EOF-FLAG                     
                GO TO 200-EXIT.                                                 
           ADD  1                       TO WS-CNT-FD00-READ.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-SEQUENTIAL-FILES.                                            
                                                                                
           IF F685-NUM-HDB-REF = FD00-ALT-KEY1                                  
               PERFORM 400-GIRO-PAYMENT  THRU 400-EXIT                          
               PERFORM 100-READ-AC08F685 THRU 100-EXIT                          
           ELSE                                                                 
               IF F685-NUM-HDB-REF > FD00-ALT-KEY1                              
                  PERFORM 200-READ-BP13FD00        THRU 200-EXIT                
               ELSE                                                             
                   PERFORM 800-CHECK-KD03          THRU 800-EXIT                
                   PERFORM 100-READ-AC08F685       THRU 100-EXIT                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-GIRO-PAYMENT.                                                        
                                                                                
           IF FD00-AMT-INSTAL > ZEROS OR FD00-AMT-LOAN-BAL > ZEROS              
              PERFORM 500-OUTPUT-F136-FILE THRU 500-EXIT                        
           ELSE                                                                 
              PERFORM 600-WRITE-KIV-REC    THRU 600-EXIT                        
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-OUTPUT-F136-FILE.                                                    
                                                                                
           MOVE SPACES                TO BP13F136-CASHTRAN.                     
           INITIALIZE                    BP13F136-CASHTRAN.                     
           MOVE '64'                  TO F136-CDE-TRANS-TYPE.                   
                                                                                
           IF F685-TXT-DESC = 'GIRO-DBS'                                        
              MOVE 'INS'              TO F136-CDE-PAYMENT-TYPE                  
           ELSE                                                                 
              IF F685-TXT-DESC = 'GIRO-LS '                                     
                 MOVE 'LS '           TO F136-CDE-PAYMENT-TYPE                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE FD00-NUM-REGN         TO F136-NUM-REGN.                         
           MOVE F685-DTE-TRANS        TO F136-DTE-POST.                         
           MOVE ZEROS                 TO F136-COUNT-HISTORY.                    
           MOVE F685-DTE-TRANS        TO F136-DTE-TRANS.                        
           MOVE F685-TXT-DESC         TO F136-NUM-RECPT-JRNO                    
           MOVE F685-NUM-HDB-REF      TO F136-NUM-SCH-ACCT.                     
           MOVE F685-CDE-ERROR        TO F136-CDE-ERROR.                        
           MOVE F685-AMT-INST-LOAN    TO F136-AMT-RECEIPT.                      
           MOVE F685-CDE-CR-DR        TO F136-CDE-CRDR.                         
           MOVE FD00-CDE-LOAN-SCH     TO F136-NUM-LOAN-SCH.                     
                                                                                
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1 TO WS-CNT-F136-VALID.                                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      * OUTPUT ERROR RECORD TO KIV FILE                                         
       600-WRITE-KIV-REC.                                                       
                                                                                
           MOVE SPACES                TO BP13K140-KIVTRAN.                      
           INITIALIZE                    BP13K140-KIVTRAN.                      
           MOVE FD00-NUM-REGN         TO K140-NUM-REGN.                         
           MOVE '64'                  TO K140-CDE-TRANS-TYPE.                   
                                                                                
           IF F685-TXT-DESC = 'GIRO-DBS'                                        
              MOVE 'INS'              TO K140-CDE-PAYMENT-TYPE                  
           ELSE                                                                 
              IF F685-TXT-DESC = 'GIRO-LS '                                     
                 MOVE 'LS '           TO K140-CDE-PAYMENT-TYPE                  
              END-IF                                                            
           END-IF.                                                              
           MOVE F685-DTE-TRANS        TO K140-DTE-POST.                         
           ADD  1                     TO WS-K140-COUNT-HISTORY.                 
           MOVE 'N'                   TO K140-COUNT-HIST-CHAR.                  
           MOVE WS-K140-COUNT-HISTORY TO K140-COUNT-HIST-NUM.                   
           MOVE F685-DTE-TRANS        TO K140-DTE-TRANS.                        
                                                                                
           MOVE F685-NUM-RECEIPT      TO K140-NUM-RECPT-JRNO.                   
           MOVE F685-AMT-INST-LOAN    TO K140-AMT-RECEIPT.                      
           MOVE F685-NUM-HDB-REF      TO K140-NUM-SCH-ACCT.                     
           MOVE 'A'                   TO K140-CDE-ERROR.                        
                                                                                
           WRITE BP13K140-KIVTRAN.                                              
                                                                                
           MOVE K140-STATUS TO  WS-K140-STATUS.                                 
                                                                                
           IF WS-K140-STATUS1 = 0                                               
              ADD 1 TO WS-CNT-K140-KIV                                          
           ELSE                                                                 
              IF K140-STATUS = 22                                               
                 DISPLAY ' ERROR WRITING DUP REC : ' K140-KEY-FLD               
              ELSE                                                              
                 DISPLAY ' OPENING ERROR, BP13K140 STATUS '                     
                         K140-STATUS                                            
                 MOVE K140-STATUS           TO   RETURN-CODE                    
                 PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
           PERFORM 700-PRINT-CONTROL-LIST  THRU 700-EXIT.                       
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINT CONTROL-LIST                                         *         
      *****************************************************************         
                                                                                
       700-PRINT-CONTROL-LIST.                                                  
                                                                                
           IF WS-LINE-CNT > 53                                                  
              MOVE  ZEROES              TO   WS-LINE-CNT                        
              PERFORM 710-PRINT-HEADING THRU 710-EXIT                           
           END-IF.                                                              
           ADD +1                       TO   WS-SNO.                            
           MOVE WS-SNO                  TO   CL-SNO.                            
           MOVE FD00-NUM-REGN           TO   CL-RENO.                           
           MOVE K140-NUM-SCH-ACCT       TO   CL-SCH-ACC.                        
           MOVE F136-CDE-PAYMENT-TYPE   TO   CL-PAY-TY.                         
           MOVE K140-AMT-RECEIPT        TO   CL-AMT-RECEIPT.                    
                                                                                
           PERFORM 720-PRINT-REC        THRU 720-EXIT.                          
                                                                                
        700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       710-PRINT-HEADING.                                                       
           ADD +1            TO WS-PAGENO.                                      
           MOVE WS-PAGENO    TO CL-PAGENO.                                      
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4 AFTER PAGE.          
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-HEAD.                       
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-1.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-2.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-5.                     
           ADD +9   TO WS-LINE-CNT.                                             
                                                                                
        710-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       720-PRINT-REC.                                                           
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-3.                     
           ADD +1   TO WS-LINE-CNT.                                             
                                                                                
        720-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       800-CHECK-KD03.                                                          
                                                                                
           PERFORM 810-READ-BP13KD03        THRU 810-EXIT                       
           IF KD03-FND                                                          
              WRITE C08F685A-REC FROM AC08F685-REC                              
              ADD 1 TO WS-CNT-F685A-WRT                                         
              IF KD03-DTE-TRANSFER-SMS NOT = SPACES AND LOW-VALUES              
                 PERFORM 850-WRITE-BP13F001 THRU 850-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              PERFORM 600-WRITE-KIV-REC     THRU 600-EXIT                       
           END-IF.                                                              
                                                                                
        800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       810-READ-BP13KD03.                                                       
                                                                                
           MOVE F685-NUM-HDB-REF              TO KD03-NUM-SCH-ACCNT             
           READ BP13KD03 KEY IS KD03-ALT-KEY1.                                  
                                                                                
           IF KD03-STATUS = 00 OR 97                                            
              MOVE 'Y'                        TO WS-KD03-FND                    
           ELSE                                                                 
              MOVE 'N'                        TO WS-KD03-FND                    
              DISPLAY 'ERROR READING BP13KD03 , ALT KEY : '                     
                                                KD03-ALT-KEY1                   
           END-IF.                                                              
                                                                                
        810-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       850-WRITE-BP13F001.                                                      
      *****************************************************************         
                                                                                
           IF F685-TXT-DESC = 'GIRO-DBS' OR 'GIRO-LS '                          
              MOVE SPACES                     TO AB03F001-REC                   
              INITIALIZE                         AB03F001-REC                   
              MOVE F685-NUM-HDB-REF           TO F001-NUM-HDB-REF               
              MOVE F685-DTE-TRANS             TO F001-DTE-TRANS                 
              MOVE ZEROS                      TO F001-NUM-SEQ                   
              MOVE ZEROS                      TO F001-NUM-SEQ                   
              MOVE F685-AMT-INST-LOAN         TO F001-AMT-INST-LOAN             
              MOVE F685-AMT-INST-LOAN         TO F001-AMT-TOTL                  
              MOVE F685-CDE-CR-DR             TO F001-CDE-CR-DR                 
              MOVE WS-CURRENT-DATE(5:2)       TO F001-DTE-PYMT-MTH              
              MOVE F685-TXT-DESC(1:8)         TO F001-NUM-REF                   
              MOVE F685-CDE-BO                TO F001-CDE-BO                    
              MOVE F685-CDE-LOAN-SCH          TO F001-CDE-LOAN-SCH              
              MOVE SPACES                     TO F001-CDE-INT                   
              MOVE SPACES                     TO F001-CDE-ERROR                 
              MOVE 'BP13CC12'                 TO F001-NUM-USERID                
              MOVE F685-DTE-TRANS             TO F001-DTE-DATA-ENTRY            
              IF F685-TXT-DESC = 'GIRO-DBS'                                     
                 MOVE 'GIRO    '              TO F001-TXT-DESC                  
                 MOVE 'GO'                    TO F001-CDE-TRANS-POSTNG          
              ELSE                                                              
                 IF F685-TXT-DESC = 'GIRO-LS '                                  
                    MOVE 'GIRO-L  '           TO F001-TXT-DESC                  
                    MOVE 'GO'                 TO F001-CDE-TRANS-POSTNG          
                 END-IF                                                         
              END-IF                                                            
                                                                                
              WRITE AB03F001-REC                                                
              ADD 1                           TO WS-CNT-F001-WRT                
           ELSE                                                                 
              DISPLAY 'F685-TXT-DESC ' F685-TXT-DESC                            
           END-IF.                                                              
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13CC12 *************'.                            
           DISPLAY 'NO OF F685 READ  (AC08F685) : '                             
                                                 WS-CNT-AC08F685-READ.          
           DISPLAY 'NO OF FD00 REC READ  (FD00) : ' WS-CNT-FD00-READ.           
           DISPLAY 'NO OF F136 REC WRITE (F121) : ' WS-CNT-F136-VALID.          
           DISPLAY 'NO OF K140 REC WRITE (K140) : ' WS-CNT-K140-KIV.            
           DISPLAY 'NO OF F685A REC WRITE (K140): ' WS-CNT-F685A-WRT.           
           DISPLAY 'NO OF F001  REC WRITE (F001): ' WS-CNT-F001-WRT.            
                                                                                
           CLOSE AC08F685                                                       
                 BP13F136                                                       
                 BP13K140                                                       
                 BP13FD00                                                       
                 BP13KD03                                                       
                 C08F685A                                                       
                 AB03F001                                                       
                 BP13LC12.                                                      
                                                                                
           IF K140-STATUS NOT = 0 AND K140-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13K140 STATUS ' K140-STATUS           
               MOVE K140-STATUS TO RETURN-CODE.                                 
                                                                                
           IF KD03-STATUS NOT = 0 AND KD03-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD03 STATUS ' KD03-STATUS           
               MOVE KD03-STATUS TO RETURN-CODE.                                 
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
