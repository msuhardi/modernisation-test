       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C600.                                                    
      *AUTHOR. SGK.                                                             
      *DATE-WRITTEN.   07 MAY   1998.                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  THIS PROGRAM COMPARES RECORDS FROM            *                
      *          AB02F875 AND BP13K800. IF THE NUM-REGN        *                
      *          IN F875 MATCH THE NUM REGN IN K800 AND        *                
      *          K800-DTE-SO-APPT NOT = SPACES OR              *                
      *          K800-DTE-AO-APPT NOT = SPACES THEN F875-      *                
      *          DTE-LOAN-COMM IS UPDATED WITH K800-DTE-APPT.  *                
      *                                                        *                
      * BP130302 07/05/1998 SGK  NEW PROGRAM.                  *                
      * BP130522 27/07/1998 SGK  TO CHANGE AB02F875 FILE SIZE  *                
      *                          FROM 1000 BYTES TO 1500.      *                
      * BP130418 26/08/1998 LSB  Y2K CHANGES.                  *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F875   ASSIGN  TO AB02F875.                               
                                                                                
           SELECT BP13K800   ASSIGN  TO BP13K800                                
                        ACCESS MODE  IS RANDOM                                  
                        ORGANIZATION IS INDEXED                                 
                        RECORD KEY   IS K800-NUM-REGN                           
                        FILE STATUS  IS BP13K800-STATUS.                        
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   AB02F875                                                            
            RECORD CONTAINS 1500 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY AB02F875.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WORK-AREAS.                                                          
           05  BP13K800-STATUS     PIC 9(2)    VALUE 0.                         
           05  WS-F875-EOF         PIC X       VALUE 'N'.                       
           05  WS-READ-K800        PIC 9(8)    VALUE 0.                         
           05  WS-READ-F875        PIC 9(8)    VALUE 0.                         
           05  WS-UPDTD-F875       PIC 9(8)    VALUE 0.                         
           05  WS-CUR-DATE.                                                     
               10  WS-CUR-DATE-CC  PIC 9(2)    VALUE 0.                         
               10  WS-CUR-DATE-YY  PIC 9(2)    VALUE 0.                         
               10  WS-CUR-DATE-MM  PIC 9(2)    VALUE 0.                         
               10  WS-CUR-DATE-DD  PIC 9(2)    VALUE 0.                         
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN.                                                                
           OPEN I-O    AB02F875                                                 
                INPUT  BP13K800.                                                
                                                                                
           IF BP13K800-STATUS NOT = ZERO AND NOT = 97                           
              DISPLAY  'BP13K800 FILE OPEN ERROR , STATUS : '                   
                               BP13K800-STATUS                                  
              GO TO 900-CLOSE-ROUTINE                                           
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-F875     THRU 100-EXIT.                             
                                                                                
           PERFORM 300-PROCESS       THRU 300-EXIT                              
                   UNTIL WS-F875-EOF = 'Y'.                                     
                                                                                
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F875.                                                           
           READ AB02F875                                                        
                AT END MOVE 'Y'         TO WS-F875-EOF                          
                   GO TO 100-EXIT.                                              
           ADD 1 TO WS-READ-F875.                                               
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-K800.                                                           
                                                                                
           MOVE  F875-NUM-REGN       TO    K800-NUM-REGN.                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
            WHEN 00                                                             
              ADD 1 TO WS-READ-K800                                             
            WHEN 23                                                             
              DISPLAY 'BP13K800 RECORD NOT FOUND , KEY : '                      
                                       K800-NUM-REGN                            
              MOVE   LOW-VALUES      TO    K800-DTE-AO-APPT                     
                                           K800-DTE-SO-APPT                     
            WHEN OTHER                                                          
              DISPLAY 'BP13K800 FILE ERROR, STATUS : '  BP13K800-STATUS         
              DISPLAY '    KEY  : '  K800-NUM-REGN                              
              GO TO 900-CLOSE-ROUTINE                                           
           END-EVALUATE.                                                        
                                                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS.                                                             
                                                                                
           PERFORM 200-READ-K800  THRU  200-EXIT.                               
                                                                                
           IF  K800-DTE-AO-APPT IS NOT NUMERIC                                  
               MOVE  '00000000'  TO     K800-DTE-AO-APPT                        
           END-IF.                                                              
                                                                                
           IF  K800-DTE-SO-APPT IS NOT NUMERIC                                  
               MOVE  '00000000'  TO     K800-DTE-SO-APPT                        
           END-IF.                                                              
                                                                                
                                                                                
           IF  K800-DTE-SO-APPT  IS NOT = '00000000'                            
               MOVE  K800-DTE-SO-APPT   TO  F875-DTE-LOAN-COMM                  
               REWRITE AB02F875-REC                                             
               ADD   1            TO    WS-UPDTD-F875                           
           ELSE                                                                 
           IF  K800-DTE-AO-APPT  IS NOT = '00000000'                            
               MOVE  K800-DTE-AO-APPT   TO  F875-DTE-LOAN-COMM                  
               REWRITE AB02F875-REC                                             
               ADD   1            TO    WS-UPDTD-F875                           
           END-IF                                                               
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-F875     THRU 100-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *                                                                         
       900-CLOSE-ROUTINE.                                                       
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
                                                                                
           DISPLAY '********************  '.                                    
           DISPLAY '                      '.                                    
           DISPLAY 'PROGRAM-ID : BP13C600   '.                                  
           DISPLAY '                       '.                                   
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ    :'                                    
           DISPLAY '   AB02F875           : ' WS-READ-F875                      
           DISPLAY '   BP13K800           : ' WS-READ-K800                      
           DISPLAY 'NO OF RECORDS UPDATED :'                                    
           DISPLAY '   AB02F875           : ' WS-UPDTD-F875                     
           DISPLAY '                      '.                                    
           DISPLAY '********************  '.                                    
                                                                                
           CLOSE AB02F875                                                       
                 BP13K800.                                                      
                                                                                
           IF BP13K800-STATUS NOT = ZERO                                        
              DISPLAY  'BP13K800 CLOSE ERROR , STATUS : '                       
                             BP13K800-STATUS                                    
              MOVE  BP13K800-STATUS      TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
