      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C197.                                                 
       AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  11/08/98.                                                 
                                                                                
      *================================================================*        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT   (SOC)                  *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS THE PROGRAM TO PRINT OUT REPORTS  *        
      *                      WHERE THE BLOCK IS DUE.                   *        
      *                      (FOR UNITS OPTED FOR OCS AT BOOKING ONLY).*        
      *                                                                *        
      *   INPUT FILES :  1.  BM06K100  -  BLOCK FILE                   *        
      *                  2.  BM06K110  -  FLATINV FILE                 *        
      *                  3.  BP13K800  -  SOC MASTER FILE              *        
      *                                                                *        
      *   LISTING     :  1.  REPORT FOR OCS WITH TIMBER DOORS ONLY.    *        
      *                                                                *        
      *   CHGE-NO   DATE   BY   DESCRIPTION                            *        
      *   -------  ------  ---  -------------------------------------  *        
      *  BP130488  110898  LMS  NEW PROGRAM.                           *        
      *  BP130488  021198  LMS  TO BYPASS CHECKING OF RECORD WITH      *        
      *                         OCS-DATE = '00000000'.                 *        
      *  BP130488  031298  GEO  MOVE COMM-BLK INSTEAD OF K110-BLK      *        
      *  BP130488  040199  GEO  TO USE INTEGER FOR CALCULATION.        *        
      *  BP130883  040800  RASM TO CHANGE BP13C904 TO BP13C913         *        
      *================================================================*        
                                                                                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS K100-STATUS.                       
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE RECORD KEY IS K110-AIX1                   
                            FILE STATUS   IS K110-STATUS.                       
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS K800-STATUS.                       
           SELECT BP13L197  ASSIGN        TO BP13L197.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
            COPY BM06K100.                                                      
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
            COPY BM06K110.                                                      
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13L197                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC                  PIC X(132).                              
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
       01  WS-FILE-STATUS.                                                      
           05  K100-STATUS                      PIC 99    VALUE 00.             
           05  K110-STATUS                      PIC 99    VALUE 00.             
           05  K800-STATUS                      PIC 99    VALUE 00.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-K100                      PIC X(1)  VALUE 'N'.            
           05  WS-EOF-K110                      PIC X(1)  VALUE 'N'.            
           05  WS-EOF-K800                      PIC X(1)  VALUE 'N'.            
                                                                                
       01  FOUND-FLAG.                                                          
           05  K100-REC-FOUND                   PIC X     VALUE 'Y'.            
           05  K110-REC-FOUND                   PIC X     VALUE 'Y'.            
           05  K800-REC-FOUND                   PIC X     VALUE 'Y'.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-PAGE                          PIC 9(4)  VALUE ZEROES.         
           05  WS-REC-CNT                       PIC 99    VALUE ZEROES.         
           05  WS-SEQ-CNT                       PIC 9(5)  VALUE ZEROES.         
           05  WS-INDEX                         PIC 99    VALUE ZEROES.         
           05  WS-DIFF-DAY                      PIC S9(6) VALUE ZEROES.         
           05  WS-READ-K100-CNT                 PIC 9(5)  VALUE ZEROES.         
           05  WS-READ-K110-CNT                 PIC 9(5)  VALUE ZEROES.         
           05  WS-READ-K800-CNT                 PIC 9(5)  VALUE ZEROES.         
                                                                                
       01  WS-CURR-DATE                         PIC 9(8).                       
       01  WS-CURR-DATE-INTEGER                 PIC S9(9).                      
       01  WS-OCS-DATE-INTEGER                  PIC S9(9).                      
                                                                                
       01  WS-OCS-DATE                          PIC 9(8).                       
                                                                                
       01  WS-UNIT-NO.                                                          
           05  WS-LEVEL                         PIC X(2)  VALUE SPACES.         
           05  FILLER                           PIC X     VALUE '-'.            
           05  WS-MAIN-UNIT                     PIC X(4)  VALUE SPACES.         
           05  WS-SLASH1                        PIC X     VALUE '-'.            
           05  WS-SUB-UNIT                      PIC X(3)  VALUE SPACES.         
                                                                                
       01  WS-SCH-ACC-NO.                                                       
           05  WS-SCHEME                        PIC X(4)  VALUE SPACES.         
           05  WS-ACCOUNT                       PIC X(4)  VALUE SPACES.         
           05  WS-CHK                           PIC X     VALUE SPACE.          
           05  WS-LESSEE                        PIC X(2)  VALUE SPACES.         
                                                                                
       01  WS-PREV-VALUES.                                                      
           05  WS-PREV-BLOCK                    PIC X(5)  VALUE SPACES.         
                                                                                
      *------------*                                                            
      *   HEADER   *                                                            
      *------------*                                                            
       01  L197-HDG1.                                                           
           05  PRT-REPORT-TITLE         PIC X(9)  VALUE 'BP13L197'.             
           05  FILLER                   PIC X(30) VALUE SPACES.                 
           05  FILLER                   PIC X(65) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(7)  VALUE 'DATE :'.               
           05  PRT-DATE                 PIC X(10).                              
                                                                                
       01  L197-HDG2.                                                           
           05  FILLER                   PIC X(43) VALUE SPACES.                 
           05  FILLER                   PIC X(61) VALUE                         
               'LIST OF OCSD CASES WHICH IS DUE'.                               
           05  FILLER                   PIC X(7)  VALUE 'PAGE : '.              
           05  PRT-PAGE                 PIC ZZZ9.                               
                                                                                
       01  L197-HDG3.                                                           
           05  FILLER                   PIC X(35) VALUE SPACES.                 
           05  FILLER                   PIC X(69) VALUE                         
               'OCSD WITH TIMBER DOORS ONLY - OPTION AT BOOKING'.               
           05  FILLER          PIC X(7)  VALUE 'USER : '.                       
           05  FILLER          PIC X(15) VALUE 'ALLOCATION TEAM'.               
                                                                                
       01  L197-HDG4.                                                           
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(46) VALUE                         
               'T O T A L   N U M B E R   O F   U N I T S = '.                  
           05  PRT-TOTAL                PIC ZZZZ9 VALUE ZEROES.                 
                                                                                
       01  L197-HDG5.                                                           
           05  FILLER                   PIC X(98) VALUE SPACES.                 
           05  FILLER                   PIC X(34) VALUE                         
               'BOOKING     REQUEST FOR'.                                       
                                                                                
       01  L197-HDG6.                                                           
           05  FILLER                   PIC X(74) VALUE SPACES.                 
           05  FILLER                   PIC X(24) VALUE 'SALES REGN'.           
           05  FILLER                   PIC X(34) VALUE                         
               'APPOINTMENT INSTALLATION'.                                      
                                                                                
       01  L197-HDG7.                                                           
           05  FILLER                   PIC X(78) VALUE                         
               ' S/NO   SCHEME A/C NO   BLOCK   ADDRESS'.                       
           05  FILLER                   PIC X(54) VALUE                         
               'NO       DUE DATE   DATE        OF COMPENTS (Y/N)'.             
                                                                                
       01  L197-HDG8.                                                           
           05  FILLER                   PIC X(132) VALUE ALL '-'.               
                                                                                
                                                                                
      *-------------------*                                                     
      *   DETAIL RECORD   *                                                     
      *-------------------*                                                     
       01  L197-DTL.                                                            
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  PRT-SEQ-CNT              PIC ZZZ9  VALUE ZEROS.                  
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-SCH-ACC-NO           PIC X(14) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-BLOCK                PIC X(5)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE '  #'.                  
           05  PRT-HOUSE-NO             PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  PRT-STREET               PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE ' S('.                  
           05  PRT-POSTAL-CODE          PIC X(6)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE ') '.                   
           05  PRT-CURR-REGN-NO         PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(3)  VALUE SPACES.                 
           05  PRT-DTE-OCS              PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  PRT-DTE-BOOKING          PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(9)  VALUE SPACES.                 
           05  PRT-CDE-OCS-TAG          PIC X     VALUE SPACE.                  
                                                                                
      *----------------------------------------------------------------*        
      *   LINKAGE VARIABLES PASS TO SUB-PROGRAM  BP13C913              *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(4)  VALUE SPACES.                 
               10  WS-LINK-NUM-ACC      PIC X(5)  VALUE SPACES.                 
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ******************************************************************        
       000-MAIN.                                                                
      ******************************************************************        
           PERFORM   100-OPEN-ROUTINE      THRU    100-EXIT.                    
           PERFORM   150-START-K100        THRU    150-EXIT.                    
           PERFORM   200-READ-K100         THRU    200-EXIT.                    
                                                                                
           IF WS-EOF-K100 NOT = 'Y'                                             
              MOVE    K100-BLK-NO          TO      WS-PREV-BLOCK                
                                                                                
              PERFORM   300-PROCESS-REPORT THRU    300-EXIT                     
                        UNTIL WS-EOF-K100 = 'Y'.                                
                                                                                
           PERFORM   900-PRINT-TOTAL       THRU    900-EXIT.                    
           PERFORM   999-CLOSE-ROUTINE     THRU    999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
      ******************************************************************        
           OPEN INPUT BM06K100                                                  
                      BM06K110                                                  
                      BP13K800                                                  
               OUTPUT BP13L197.                                                 
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K100-STATUS = ' K100-STATUS               
              MOVE     K100-STATUS TO RETURN-CODE                               
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K110-STATUS = ' K110-STATUS               
              MOVE     K110-STATUS TO RETURN-CODE                               
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR, K800-STATUS = ' K800-STATUS               
              MOVE     K800-STATUS TO RETURN-CODE                               
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
           MOVE ZEROES TO  WS-COUNTERS.                                         
           MOVE 50     TO  WS-REC-CNT.                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       150-START-K100.                                                          
      ******************************************************************        
           MOVE LOW-VALUES TO K100-KEY-FLD.                                     
           START BM06K100 KEY NOT < K100-KEY-FLD INVALID                        
                 DISPLAY 'START ERROR, BM06K100-STATUS = ' K100-STATUS          
                 MOVE     K100-STATUS  TO RETURN-CODE                           
                 PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                      
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-K100.                                                           
      ******************************************************************        
           MOVE SPACES TO WS-EOF-K100.                                          
                                                                                
           READ BM06K100 NEXT                                                   
                AT END MOVE 'Y'  TO  WS-EOF-K100                                
                       GO TO 200-EXIT.                                          
                                                                                
           ADD  1  TO  WS-READ-K100-CNT.                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-REPORT.                                                      
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    TO CHECK WHETHER THE THE BLOCK IS DUE IN 5 DAYS TIME   *             
      *-----------------------------------------------------------*             
           IF   K100-DTE-OCS-DUE =  SPACES OR LOW-VALUES                        
                MOVE ZEROES       TO K100-DTE-OCS-DUE.                          
                                                                                
           IF K100-DTE-OCS-DUE NOT = ZEROES AND SPACES AND LOW-VALUES           
              MOVE    K100-DTE-OCS-DUE  TO   WS-OCS-DATE                        
              PERFORM 400-COMPARE-DATES THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-K100     THRU  200-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-COMPARE-DATES.                                                       
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    TO CALCULATE THE OCS DUE DATE                          *             
      *-----------------------------------------------------------*             
           MOVE FUNCTION CURRENT-DATE (1:8) TO WS-CURR-DATE.                    
                                                                                
           COMPUTE WS-CURR-DATE-INTEGER =                                       
                   FUNCTION INTEGER-OF-DATE (WS-CURR-DATE).                     
           COMPUTE WS-OCS-DATE-INTEGER =                                        
                   FUNCTION INTEGER-OF-DATE (WS-OCS-DATE).                      
                                                                                
           COMPUTE WS-DIFF-DAY = WS-OCS-DATE-INTEGER -                          
                                 WS-CURR-DATE-INTEGER.                          
                                                                                
           IF WS-DIFF-DAY > 5  OR WS-DIFF-DAY < -5                              
              CONTINUE                                                          
           ELSE                                                                 
              MOVE    SPACES         TO   WS-EOF-K110                           
              PERFORM 500-START-K110 THRU 500-EXIT                              
              PERFORM 550-READ-K110  THRU 550-EXIT                              
                UNTIL WS-EOF-K110 = 'Y'                                         
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-START-K110.                                                          
      ******************************************************************        
           MOVE K100-KEY-FLD TO K110-AIX1.                                      
           START BM06K110 KEY = K110-AIX1 INVALID                               
                 MOVE 'Y' TO WS-EOF-K110                                        
                 DISPLAY 'START ERROR, BM06K110-STATUS = ' K110-STATUS          
                 DISPLAY '                 K110-AIX1   = ' K110-AIX1.           
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       550-READ-K110.                                                           
      ******************************************************************        
           MOVE SPACES TO WS-EOF-K110.                                          
                                                                                
           READ BM06K110 NEXT                                                   
                AT END MOVE 'Y'  TO  WS-EOF-K110                                
                       GO TO 550-EXIT.                                          
                                                                                
           IF (K110-AIX1        > K100-KEY-FLD)                                 
               MOVE 'Y' TO WS-EOF-K110                                          
           ELSE                                                                 
              IF (K110-AIX1        = K100-KEY-FLD) AND                          
                (K110-REGN-NO(1:2) NOT = SPACES AND LOW-VALUES AND '##')        
                 AND                                                            
                 (K110-CDE-OCS-TAG = 'N')                                       
                  ADD     1                     TO    WS-READ-K110-CNT          
                  MOVE    K110-REGN-NO          TO    K800-NUM-REGN             
                  MOVE    SPACES                TO    K800-REC-FOUND            
                  PERFORM 600-READ-K800         THRU  600-EXIT                  
                  IF (K800-REC-FOUND   = 'Y') AND                               
                     (K800-DTE-BALLOT >= '199804')                              
                      PERFORM  700-PRINT-REPORT-L197 THRU 700-EXIT              
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-READ-K800.                                                           
      ******************************************************************        
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              MOVE 'Y' TO K800-REC-FOUND                                        
           ELSE                                                                 
           IF K800-STATUS = 23                                                  
              MOVE 'N' TO K800-REC-FOUND                                        
              DISPLAY 'RECORD NOT FOUND IN BP13K800'                            
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
           ELSE                                                                 
              MOVE K800-STATUS TO RETURN-CODE                                   
              DISPLAY 'ERROR READING BP13K800 FILE : ' K800-STATUS              
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              PERFORM  999-CLOSE-ROUTINE THRU 999-EXIT.                         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-PRINT-REPORT-L197.                                                   
      ******************************************************************        
           MOVE  SPACES               TO BP13COMM8-REC.                         
           INITIALIZE                    BP13COMM8-REC.                         
                                                                                
           MOVE  K110-SCH-ACC-NO      TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC BP13COMM8-REC.                     
                                                                                
           IF COMM8-NUM-BLK = WS-PREV-BLOCK                                     
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 900-PRINT-TOTAL   THRU   900-EXIT                         
              PERFORM 800-PRINT-HEADER  THRU   800-EXIT.                        
                                                                                
           IF WS-REC-CNT > 44                                                   
              PERFORM 800-PRINT-HEADER  THRU   800-EXIT.                        
                                                                                
           MOVE   SPACES                TO     PRINT-REC.                       
           ADD    1                     TO     WS-SEQ-CNT.                      
           MOVE   WS-SEQ-CNT            TO     PRT-SEQ-CNT.                     
           MOVE   K110-SCH-ACC-NO       TO     WS-SCH-ACC-NO.                   
           STRING WS-SCHEME '-' WS-ACCOUNT '-' WS-CHK '-' WS-LESSEE             
                   DELIMITED BY SIZE   INTO     PRT-SCH-ACC-NO.                 
                                                                                
           MOVE   COMM8-NUM-BLK         TO     PRT-BLOCK.                       
           MOVE   COMM8-NUM-LEVEL       TO     WS-LEVEL.                        
           MOVE   COMM8-NUM-UNIT-MAIN   TO     WS-MAIN-UNIT.                    
                                                                                
           IF COMM8-NUM-UNIT-SUB = SPACES OR LOW-VALUES                         
              MOVE SPACES               TO     WS-SLASH1 WS-SUB-UNIT            
           ELSE                                                                 
              MOVE '-'                  TO     WS-SLASH1                        
              MOVE COMM8-NUM-UNIT-SUB   TO     WS-SUB-UNIT.                     
                                                                                
           MOVE   WS-UNIT-NO            TO     PRT-HOUSE-NO.                    
           MOVE   COMM8-NME-STREET      TO     PRT-STREET.                      
           MOVE   COMM8-NUM-POSTAL-CODE TO     PRT-POSTAL-CODE.                 
                                                                                
           MOVE   K110-REGN-NO          TO     PRT-CURR-REGN-NO.                
           STRING WS-OCS-DATE (7:2) '/'                                         
                  WS-OCS-DATE (5:2) '/'                                         
                  WS-OCS-DATE (1:4)                                             
                  DELIMITED BY SIZE   INTO     PRT-DTE-OCS.                     
           STRING K800-DTE-BK-APPT (7:2) '/'                                    
                  K800-DTE-BK-APPT (5:2) '/'                                    
                  K800-DTE-BK-APPT (1:4)                                        
                  DELIMITED BY SIZE   INTO     PRT-DTE-BOOKING.                 
           MOVE   K110-CDE-OCS-TAG      TO     PRT-CDE-OCS-TAG.                 
                                                                                
           WRITE  PRINT-REC             FROM   L197-DTL AFTER 1.                
           ADD    1                     TO     WS-REC-CNT.                      
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PRINT-HEADER.                                                        
      ******************************************************************        
           ADD    1              TO    WS-PAGE.                                 
           MOVE   WS-PAGE        TO    PRT-PAGE.                                
           STRING WS-CURR-DATE (7:2) '/'                                        
                  WS-CURR-DATE (5:2) '/'                                        
                  WS-CURR-DATE (1:4)                                            
           DELIMITED BY SIZE                                                    
           INTO PRT-DATE.                                                       
           MOVE   'BP13L197'     TO    PRT-REPORT-TITLE.                        
                                                                                
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC   FROM L197-HDG1   AFTER PAGE.                      
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC   FROM L197-HDG2.                                   
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC   FROM L197-HDG3   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC   FROM L197-HDG5   AFTER 2.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC   FROM L197-HDG6   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC   FROM L197-HDG7   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC   FROM L197-HDG8   AFTER 1.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC.                                                    
           MOVE   ZEROES      TO   WS-REC-CNT.                                  
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-PRINT-TOTAL.                                                         
      ******************************************************************        
           IF WS-SEQ-CNT > ZEROES                                               
              MOVE   WS-SEQ-CNT    TO    PRT-TOTAL                              
              MOVE   SPACES        TO    PRINT-REC                              
              WRITE  PRINT-REC     FROM  L197-HDG4   AFTER 3.                   
                                                                                
           MOVE      ZEROES        TO    WS-SEQ-CNT.                            
           MOVE      ZEROES        TO    WS-REC-CNT.                            
           MOVE      COMM8-NUM-BLK TO    WS-PREV-BLOCK.                         
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BM06K100                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13L197.                                                      
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K100-STATUS = ' K100-STATUS.              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K110-STATUS = ' K110-STATUS.              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K800-STATUS = ' K800-STATUS.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
