      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CD29.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  APRIL 29, 2009.                                           
      *===============================================================*         
      * OBJECTIVE  :  PRINT DBSS SUB TRANSFER CASES W/ GRANT REPORT   *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. SY02F001                                     *         
      *               2. BP13FD00                                     *         
      *               3. BP13KD05                                     *         
      *               4. BP13K130                                     *         
      *               5. BM06K110                                     *         
      *               6. BP13K022                                     *         
      *               7. BP13K024                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13LD29                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP133522 29/04/2009 PCL3 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  BP13FD00  ASSIGN        TO BP13FD00.                         
           SELECT  BP13KD05  ASSIGN        TO BP13KD05                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD05-KEY-FLD                      
                             FILE STATUS   IS WS-KD05-STATUS.                   
           SELECT  BP13K130  ASSIGN        TO BP13K130                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K130-KEY-FLD                      
                             FILE STATUS   IS WS-K130-STATUS.                   
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
           SELECT  BP13K022  ASSIGN       TO BP13K022                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K022-KEY-FLD                       
                             ALTERNATE RECORD KEY IS K022-NUM-REGN              
                             FILE STATUS  IS WS-K022-STATUS.                    
           SELECT  BP13K024  ASSIGN        TO BP13K024                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K024-KEY-FLD                      
                             FILE STATUS   IS WS-K024-STATUS.                   
           SELECT  BP13LD29  ASSIGN        TO BP13LD29.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD00                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 1600 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD00.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   BP13KD05                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD   BP13K130                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K130.                                                           
                                                                                
       FD   BP13LD29                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13LD29-REC               PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F001-EOF             PIC X(1)  VALUE 'N'.                     
               88 EOF-F001                       VALUE 'Y'.                     
           05  WS-EOF-FD00             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-KD05             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K130             PIC X(1)  VALUE 'N'.                     
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-KD05-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K130-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K024-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FD00-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD05-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-LD29-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-K130-FOUND            PIC X(1)  VALUE 'N'.                     
           05 WS-REGN-NO               PIC X(8)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
                                                                                
       01  LD29-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LD29'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LD29-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD29-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LD29-HEADER02.                                                       
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               'REPORT ON DBSS SUB TRANSFER CASES W/ HOUSING GRANT'.            
                                                                                
       01  LD29-HEADER03.                                                       
           05  FILLER              PIC X(18)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'BOOKING   '.            
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'APPROVAL  '.            
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'GRNT'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'HSGNT '.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'AHG   '.                
           05  FILLER              PIC X(26)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NAME'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
                                                                                
       01  LD29-HEADER04.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'DATE'.                   
           05  FILLER              PIC X(08)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'NRIC NO  '.              
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'DATE'.                   
           05  FILLER              PIC X(08)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'TYPE'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(06)    VALUE 'AMOUNT'.                 
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(06)    VALUE 'AMOUNT'.                 
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(06)    VALUE 'PAY-TY'.                 
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'CR CODE  '.              
           05  FILLER              PIC X(07)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE 'CSM'.                    
           05  FILLER              PIC X(02)    VALUE SPACES.                   
                                                                                
       01  LD29-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD29-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-BOOKING        PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-HSGNT-APPROVE  PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-GRANT-TYPE     PIC X(04)        VALUE SPACE.                
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-HG-AMT         PIC ZZ,ZZ9       VALUE ZEROES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-AHG-AMT        PIC ZZ,ZZ9       VALUE ZEROES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-PAY-TY         PIC X(06)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-CR-CODE        PIC X(14)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD29-NME-OCCP       PIC X(30)        VALUE SPACES.               
                                                                                
       01  LD29-DETAIL02.                                                       
           05  FILLER              PIC X(99)    VALUE SPACES.                   
           05  LD29-CSM            PIC X(30)     VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13FD00      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-FD00 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD00                                                 
                       SY02F001                                                 
                       BP13KD05                                                 
                       BM06K110                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K130                                                 
              OUTPUT   BP13LD29.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD05 - ERROR OPENING : ' WS-KD05-STATUS              
              MOVE WS-KD05-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K130 - ERROR OPENING : ' WS-K130-STATUS              
              MOVE WS-K130-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              MOVE WS-K022-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR OPENING : ' WS-K024-STATUS              
              MOVE WS-K024-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
                                                                                
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FD00.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD00           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FD00                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-FD00-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF FD00-NUM-REGN > SPACES                                            
              PERFORM 3400-READ-BM06K110    THRU 3400-EXIT                      
              PERFORM 4000-START-BP13KD05   THRU 4000-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD00       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           MOVE FD00-NUM-SCH-ACCNT                     TO K110-KEY-FLD.         
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' SCHEME ACCT = ' K110-KEY-FLD                      
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-START-BP13KD05.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13KD05-REC.          
           INITIALIZE                                    BP13KD05-REC.          
                                                                                
           MOVE 'N'                                   TO WS-EOF-KD05.           
           MOVE FD00-NUM-REGN                         TO KD05-NUM-REGN.         
                                                                                
           START BP13KD05 KEY = KD05-NUM-REGN.                                  
                                                                                
           EVALUATE WS-KD05-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4200-READNEXT-BP13KD05  THRU 4200-EXIT              
                    PERFORM 4400-PROCESS-BP13KD05   THRU 4400-EXIT              
                            UNTIL WS-EOF-KD05 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    ADD 1                             TO WS-KD05-NFND           
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13KD05 : ' WS-KD05-STATUS              
                          ' REGN : ' KD05-NUM-REGN                              
                  PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-READNEXT-BP13KD05.                                                  
      ******************************************************************        
                                                                                
           READ BP13KD05 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-KD05.                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4400-PROCESS-BP13KD05.                                                   
      ******************************************************************        
                                                                                
           IF KD05-NUM-REGN = FD00-NUM-REGN                                     
              PERFORM 5000-GET-FIELDS        THRU 5000-EXIT                     
              PERFORM 4200-READNEXT-BP13KD05 THRU 4200-EXIT                     
           ELSE                                                                 
             MOVE 'Y'                          TO WS-EOF-KD05                   
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-GET-FIELDS.                                                         
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO LD29-DETAIL01          
                                                         LD29-DETAIL02.         
           INITIALIZE                                    LD29-DETAIL01          
                                                         LD29-DETAIL02.         
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO LD29-DATE-HDR.                             
                                                                                
           IF FD00-DTE-HSGNT-APPROVE NOT = SPACES AND LOW-VALUES AND            
                                                      ZEROES                    
              STRING FD00-DTE-HSGNT-APPROVE(7:2) '/'                            
                     FD00-DTE-HSGNT-APPROVE(5:2) '/'                            
                     FD00-DTE-HSGNT-APPROVE(1:4)                                
                 DELIMITED BY SIZE INTO LD29-HSGNT-APPROVE                      
           END-IF.                                                              
                                                                                
           IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES                   
                                               AND ZEROES                       
              STRING K110-DTE-ACCEPTANCE(7:2) '/'                               
                     K110-DTE-ACCEPTANCE(5:2) '/'                               
                     K110-DTE-ACCEPTANCE(1:4)                                   
                     DELIMITED BY SIZE INTO LD29-BOOKING                        
           END-IF.                                                              
                                                                                
                                                                                
           MOVE FD00-NUM-REGN                  TO LD29-REGN.                    
           MOVE KD05-NME-OCCP                  TO LD29-NME-OCCP.                
           MOVE KD05-NUM-NRIC                  TO LD29-NRIC.                    
                                                                                
           IF KD05-AMT-AHG-GRANT NOT NUMERIC                                    
              MOVE ZEROES                      TO KD05-AMT-AHG-GRANT            
           END-IF.                                                              
           MOVE KD05-AMT-AHG-GRANT             TO LD29-AHG-AMT.                 
                                                                                
           IF KD05-AMT-HSGNT NOT NUMERIC                                        
              MOVE ZEROES                      TO KD05-AMT-AHG-GRANT            
           END-IF.                                                              
           MOVE KD05-AMT-HSGNT                 TO LD29-HG-AMT.                  
                                                                                
           MOVE FD00-NUM-HSGNT-ELIG            TO LD29-GRANT-TYPE.              
                                                                                
           PERFORM 5200-GET-CSM-NAME         THRU 5200-EXIT.                    
           MOVE 'N'                            TO WS-K130-FOUND.                
           PERFORM 5500-START-BP13K130       THRU 5500-EXIT.                    
                                                                                
           IF WS-K130-FOUND = 'N'                                               
              PERFORM 6000-PRINT-REPORT      THRU 6000-EXIT                     
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-GET-CSM-NAME.                                                       
      ******************************************************************        
                                                                                
            MOVE SPACES                               TO BP13K022-REC.          
            INITIALIZE                                   BP13K022-REC.          
                                                                                
            MOVE FD00-NUM-REGN                        TO K022-NUM-REGN.         
                                                                                
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
                WHEN 00                                                         
                     PERFORM 5400-READ-BP13K024       THRU 5400-EXIT            
                WHEN 23                                                         
                     MOVE SPACES TO LD29-CSM                                    
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K022 : ' WS-K022-STATUS         
                             ' REGN NO = ' K022-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5400-READ-BP13K024.                                                      
      ******************************************************************        
                                                                                
            MOVE SPACES                                TO BP13K024-REC.         
            INITIALIZE                                    BP13K024-REC.         
                                                                                
            MOVE K022-NUM-OIC                          TO K024-NUM-OIC.         
                                                                                
            READ BP13K024.                                                      
                                                                                
            EVALUATE WS-K024-STATUS                                             
                WHEN 00                                                         
                     MOVE K024-NME-OIC                 TO LD29-CSM              
                WHEN 23                                                         
                     MOVE SPACES                       TO LD29-CSM              
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K024 : ' WS-K024-STATUS         
                             ' OIC  = ' K024-NUM-OIC                            
                     PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT             
            END-EVALUATE.                                                       
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5500-START-BP13K130.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K130-CDHIST.            
           INITIALIZE                               BP13K130-CDHIST.            
           MOVE 'N'                              TO WS-EOF-K130.                
                                                                                
           MOVE KD05-NUM-REGN                    TO K130-NUM-ORIG-REGN.         
                                                                                
           START BP13K130 KEY = K130-NUM-ORIG-REGN.                             
                                                                                
           EVALUATE WS-K130-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 5600-READNEXT-BP13K130 THRU 5600-EXIT               
                    PERFORM 5700-PROCESS-BP13K130  THRU 5700-EXIT               
                            UNTIL WS-EOF-K130 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE SPACES                      TO LD29-PAY-TY             
                                                        LD29-CR-CODE            
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K130 : ' WS-K130-STATUS              
                          ' REGN : ' K130-NUM-ORIG-REGN                         
                  PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5600-READNEXT-BP13K130.                                                  
      ******************************************************************        
                                                                                
           READ BP13K130 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K130.                        
                                                                                
       5600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5700-PROCESS-BP13K130.                                                   
      ******************************************************************        
                                                                                
           IF (KD05-NUM-REGN     = K130-NUM-ORIG-REGN)                          
              IF (KD05-NUM-NRIC     = K130-NUM-NRIC) AND                        
                 (K130-PAYMENT-TYPE = 'AHG' OR 'HGT')                           
                  MOVE 'Y'                            TO WS-K130-FOUND          
                  MOVE K130-PAYMENT-TYPE              TO LD29-PAY-TY            
                  MOVE K130-NUM-RECPT-JRNO            TO LD29-CR-CODE           
                  PERFORM 6000-PRINT-REPORT         THRU 6000-EXIT              
              END-IF                                                            
              PERFORM 5600-READNEXT-BP13K130        THRU 5600-EXIT              
           ELSE                                                                 
               MOVE 'Y'                               TO WS-EOF-K130            
           END-IF.                                                              
                                                                                
       5700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           IF (FD00-NUM-REGN = WS-REGN-NO)                                      
               MOVE SPACES                       TO LD29-SN                     
                                                    LD29-REGN                   
                                                    LD29-BOOKING                
           ELSE                                                                 
               ADD 1                              TO WS-SNO                     
               MOVE WS-SNO                        TO WS-SNO-ZZZZ9               
               MOVE WS-SNO-ZZZZ9                  TO LD29-SN                    
               MOVE FD00-NUM-REGN                 TO WS-REGN-NO                 
           END-IF.                                                              
                                                                                
           PERFORM 6100-CHECK-WRITE-HEADER      THRU 6100-EXIT.                 
                                                                                
           WRITE BP13LD29-REC                   FROM LD29-DETAIL01.             
           WRITE BP13LD29-REC                   FROM LD29-DETAIL02.             
                                                                                
           ADD 1                                  TO WS-LINE-CTR.               
           ADD 1                                  TO WS-CNT-LD29-WRITE.         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6100-CHECK-WRITE-HEADER.                                                 
      ******************************************************************        
                                                                                
           IF (WS-LINE-CTR + WS-CNT-WRITE) > WS-MAX-LINE OR                     
                                             WS-FIRST-WRITE = 'Y'               
              MOVE 'N'                         TO WS-FIRST-WRITE                
              PERFORM 7000-PRINT-HEADER      THRU 7000-EXIT                     
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO LD29-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO LD29-PAGE-HDR.                
                                                                                
           WRITE BP13LD29-REC    FROM LD29-HEADER01 AFTER PAGE.                 
           WRITE BP13LD29-REC    FROM LD29-HEADER02.                            
           WRITE BP13LD29-REC    FROM LD29-HEADER03 AFTER 2.                    
           WRITE BP13LD29-REC    FROM LD29-HEADER04.                            
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD29'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13FD00 RECORDS READ............. : '            
                    WS-FD00-READ.                                               
           DISPLAY '(2) NO OF BP13KD05 RECORDS NOT FOUND........ : '            
                    WS-KD05-NFND.                                               
           DISPLAY '(3) NO OF S/N ROWS FOUND.................... : '            
                    WS-SNO.                                                     
           DISPLAY '(4) NO OF BP13LD29 RECORDS WRITTEN.......... : '            
                    WS-CNT-LD29-WRITE.                                          
                                                                                
           CLOSE    BP13FD00                                                    
                    SY02F001                                                    
                    BM06K110                                                    
                    BP13KD05                                                    
                    BP13K022                                                    
                    BP13K024                                                    
                    BP13K130                                                    
                    BP13LD29.                                                   
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K130 - ERROR CLOSING : ' WS-K130-STATUS              
              MOVE WS-K130-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD05 - ERROR CLOSING : ' WS-KD05-STATUS              
              MOVE WS-KD05-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
              MOVE WS-K022-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR CLOSING : ' WS-K024-STATUS              
              MOVE WS-K024-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
