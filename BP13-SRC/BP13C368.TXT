      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13C368.                                                 
       AUTHOR.        SK.                                                       
       DATE-WRITTEN.  07/03/2003.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  PRINT REPORT OF SURRENDER OF STUDIO APPARTMENT*        
      *                  WHEN AMT-COMPENSATION UPDATED FROM PAYMENT    *        
      *                  SYSTEM AND DTE-SURRENDER UPDATED FROM SLS     *        
      *                  WRITE TO BP13F120 FOR GL INTERFACE            *        
      *                  WRITE TO BP13F690 AS  HISTORY REC             *        
      *                                                                *        
      *   INPUT FILES :  1.  BR05F150  -  AMORTISE FILE                *        
      *                                                                *        
      *   I-O         :  1.  BP13K690  -  AMORTISE DEFER INCOME        *        
      *                                                                *        
      *   OUPUT FILES :  1.  BP13F120                                  *        
      *                  2.  BP13F690                                  *        
      *                  3.  BP13L368                                  *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132292  SK   07/03/2003 NEW PGM                            *        
      *   BP132996  ED1  27/02/2007 CHANGED READ OF BP13K690 TO STARTBR*        
      *                             OF FIRST 9-BYTES OF SCH ACC & ADDED*        
      *                             REPORT BP13L368                    *        
      *   BP135903  FNP1 30/10/2015 CATER FOR LBS/FLEXI CASES          *        
      *   BP136398  FNP1 17/08/2016 EXPAND F690 FROM 250 TO 600        *        
      *   BP137539  FNP1 22/10/2018 CHANGE SIZE OF DEFER INCOME        *        
      *   BP138944  FP6  13/10/2021 EXPAND F690 FROM 600 TO 800        *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F120  ASSIGN        TO BP13F120.                          
           SELECT BR05F150  ASSIGN        TO BR05F150.                          
                                                                                
           SELECT BP13F690  ASSIGN        TO BP13F690.                          
           SELECT BP13L368  ASSIGN        TO BP13L368.                          
                                                                                
           SELECT BP13K690  ASSIGN        TO BP13K690                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K690-KEY-FLD                       
                            FILE STATUS   IS WS-K690-STATUS.                    
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F120                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F120.                                                           
                                                                                
       FD  BR05F150                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       COPY BR05F150.                                                           
                                                                                
       FD  BP13F690                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F690.                                                           
                                                                                
       FD  BP13K690                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
       COPY BP13K690.                                                           
                                                                                
       FD  BP13L368                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORD IS OMITTED                                              
           RECORDING MODE IS F.                                                 
       01  L368-PRTREC  PIC X(132).                                             
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F150-READ        PIC 9(8)  VALUE 0.                        
           05 WS-CNT-F120-WRITE       PIC 9(8)  VALUE 0.                        
           05 WS-CNT-F690-WRITE       PIC 9(8)  VALUE 0.                        
           05 WS-CNT-K690-NFND        PIC 9(8)  VALUE 0.                        
           05 WS-CNT-K690-DELETE      PIC 9(8)  VALUE 0.                        
           05 WS-CNT-K690-UPDATE      PIC 9(8)  VALUE 0.                        
           05 WS-CNT-L368-WRITE       PIC 9(8)  VALUE 0.                        
           05 WS-SNO                  PIC 9(5)  VALUE 0.                        
           05 WS-PAGE-CTR             PIC 9(4)  VALUE 0.                        
           05 WS-LINE-CTR             PIC 9(2)  VALUE 0.                        
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F150-EOF             PIC X(1)  VALUE 'N'.                      
           05 WS-K690-EOF             PIC X(1)  VALUE 'N'.                      
           05 WS-K690-FOUND           PIC X(1)  VALUE 'N'.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-K690-STATUS          PIC 9(2)  VALUE 0.                        
           05 WS-SEQ                  PIC 9(3)  VALUE 0.                        
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-INC-SURR-AMT           PIC 9(8)V99 VALUE ZEROS.                
           05 WS-AMOUNT                 PIC 9(6)V99 VALUE ZEROS.                
                                                                                
       01  WS-EDITED-DATE              PIC X(10)  VALUE SPACES.                 
       01  WS-MAX-LINE                 PIC 99     VALUE 58.                     
                                                                                
       01  WS-BLANK-LINE                PIC X(132) VALUE SPACES.                
       01  WS-DASH-LINE                 PIC X(132) VALUE ALL '-'.               
                                                                                
      *---> DETAIL LINES FOR REPORT                                             
       01  L368-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L368'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L368-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L368-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L368-HEADER02.                                                       
           05  FILLER              PIC X(44)     VALUE SPACES.                  
           05  FILLER              PIC X(42)     VALUE                          
               'REPORT ON SURRENDER OF STUDIO APARTMENT'.                       
                                                                                
       01  L368-HEADER03.                                                       
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'AMORTISE'.              
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'SLS   '.                
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'TRANS'.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'SURRENDER'.             
                                                                                
       01  L368-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'SCH-ACC '.              
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE ' RMK'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'DATE COMP'.             
                                                                                
       01 L368-DETAILS.                                                         
          05 L368-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L368-SCH-ACC         PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L368-SCH-ACC2        PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L368-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 L368-TRANS-RMK       PIC X(02)     VALUE SPACES.                   
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 L368-DTE-SURNDR      PIC X(10)     VALUE SPACES.                   
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES      THRU    1000-EXIT.                     
           PERFORM  2000-READ-BR05F150   THRU    2000-EXIT.                     
           PERFORM  3000-PROCESS         THRU    3000-EXIT                      
                    UNTIL WS-F150-EOF = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE   THRU    9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BR05F150                                                 
                OUTPUT BP13F120                                                 
                       BP13F690                                                 
                       BP13L368                                                 
                I-O    BP13K690.                                                
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K690-STATUS    TO RETURN-CODE                          
                 DISPLAY 'OPEN BP13K690 FAIL. STATUS=' WS-K690-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BR05F150.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BR05F150                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F150-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-CNT-F150-READ                                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 3100-STARTBR-BP13K690 THRU 3100-EXIT.                        
                                                                                
           PERFORM 2000-READ-BR05F150 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-STARTBR-BP13K690.                                                   
      *----------------------------------------------------------------*        
           MOVE 'N'                   TO WS-K690-EOF.                           
           MOVE SPACES                TO K690-REC.                              
           MOVE F150-NUM-HDB-REF(1:9) TO K690-NUM-SCH-ACC.                      
                                                                                
           START BP13K690 KEY >= K690-KEY-FLD                                   
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3200-READNXT-BP13K690 THRU 3200-EXIT                   
                         UNTIL WS-K690-EOF = 'Y' OR                             
                 F150-NUM-HDB-REF(1:9) NOT = K690-NUM-SCH-ACC(1:9)              
                                                                                
              WHEN 23                                                           
                 DISPLAY 'REC NOT FND IN BP13K690'                              
                         ' KEY ' F150-NUM-HDB-REF                               
                 GO TO 3100-EXIT                                                
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN STARTBR BP13K690 - STATUS '                  
                 WS-K690-STATUS ' KEY ' F150-NUM-HDB-REF                        
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       3200-READNXT-BP13K690.                                                   
      *----------------------------------------------------------------*        
           READ BP13K690 NEXT RECORD                                            
                  AT END MOVE 'Y' TO WS-K690-EOF                                
                     GO TO 3200-EXIT.                                           
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                 IF F150-NUM-HDB-REF(1:9) = K690-NUM-SCH-ACC(1:9)               
                    PERFORM 3300-PROCESS-RECORD   THRU 3300-EXIT                
                 END-IF                                                         
                                                                                
              WHEN 10                                                           
                 MOVE 'Y' TO WS-K690-EOF                                        
                                                                                
              WHEN 23                                                           
                 MOVE 'Y' TO WS-K690-EOF                                        
                 DISPLAY 'REC NOT FND IN BP13K690'                              
                         ' KEY ' F150-NUM-HDB-REF                               
                                                                                
              WHEN OTHER                                                        
                 MOVE 'Y' TO WS-K690-EOF                                        
                 DISPLAY 'ERROR IN READNXT BP13K690 - STATUS '                  
                 WS-K690-STATUS ',KEY ' F150-NUM-HDB-REF                        
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       3300-PROCESS-RECORD.                                                     
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 3500-INIT-PARA       THRU 3500-EXIT.                         
                                                                                
           PERFORM 3600-WRITE-BP13F120  THRU 3600-EXIT.                         
                                                                                
           PERFORM 3800-FIND-REMARK     THRU 3800-EXIT.                         
           PERFORM 3900-UPDATE-K690     THRU 3900-EXIT.                         
                                                                                
           PERFORM 4000-DELETE-BP13K690 THRU 4000-EXIT.                         
           PERFORM 3700-WRITE-BP13F690  THRU 3700-EXIT.                         
                                                                                
           IF F150-NUM-HDB-REF(10:2) NOT = K690-NUM-SCH-ACC(10:2)               
              PERFORM 5000-WRITE-BP13L368  THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-INIT-PARA.                                                          
      *----------------------------------------------------------------*        
                                                                                
           IF K690-AMT-DEFER-INCOME NOT NUMERIC                                 
              MOVE ZEROS TO K690-AMT-DEFER-INCOME                               
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-INCOME-MTH NOT NUMERIC                             
              MOVE ZEROS TO K690-AMT-AMORT-INCOME-MTH                           
           END-IF.                                                              
                                                                                
           IF K690-AMT-AMORT-INCOME-ACCUM NOT NUMERIC                           
              MOVE ZEROS TO K690-AMT-AMORT-INCOME-ACCUM                         
           END-IF.                                                              
                                                                                
           IF K690-AMT-SURRENDER-COMPUTED NOT NUMERIC                           
              MOVE ZEROS TO K690-AMT-SURRENDER-COMPUTED                         
           END-IF.                                                              
                                                                                
           IF K690-AMT-COMPENSATION NOT NUMERIC                                 
              MOVE ZEROS TO K690-AMT-COMPENSATION                               
           END-IF.                                                              
                                                                                
           IF K690-AMT-SURRENDER-BAL NOT NUMERIC                                
              MOVE ZEROS TO K690-AMT-SURRENDER-BAL                              
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3600-WRITE-BP13F120.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE 0 TO WS-AMOUNT.                                                 
           COMPUTE WS-AMOUNT = K690-AMT-DEFER-INCOME -                          
                               K690-AMT-AMORT-INCOME-ACCUM.                     
                                                                                
           IF WS-AMOUNT < K690-AMT-COMPENSATION                                 
              MOVE SPACES             TO BP13F120-CASHTRAN                      
              INITIALIZE                 BP13F120-CASHTRAN                      
                                                                                
              IF K690-NUM-AMORTISE-TRANS = 'S'                                  
                 IF K690-CDE-TYP-ACTUSE = '01'                                  
                    MOVE  '35'           TO F120-CDE-TRANS-TYPE                 
                    MOVE  'SA1'          TO F120-CDE-PAYMENT-TYPE               
                 ELSE                                                           
                    IF K690-CDE-TYP-ACTUSE = '02'                               
                       MOVE  '36'        TO F120-CDE-TRANS-TYPE                 
                       MOVE  'SA2'       TO F120-CDE-PAYMENT-TYPE               
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF K690-NUM-AMORTISE-TRANS = 'F' OR 'L'                        
                    MOVE '38'            TO F120-CDE-TRANS-TYPE                 
                    MOVE 'VRL'           TO F120-CDE-PAYMENT-TYPE               
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE  'D'               TO F120-CDE-CRDR                          
                                                                                
              COMPUTE F120-AMT-RECEIPT = (WS-AMOUNT -                           
                                          K690-AMT-COMPENSATION)                
                                                                                
              MOVE K690-NUM-REGN      TO F120-NUM-REGN                          
              MOVE K690-NUM-SCH-ACC   TO F120-NUM-SCH-ACCT                      
              MOVE K690-DTE-KEY-ISSUE TO F120-DTE-POST                          
                                         F120-DTE-TRANS                         
              ADD 1                   TO WS-SEQ                                 
              MOVE WS-SEQ             TO F120-COUNT-HISTORY                     
                                                                                
              WRITE BP13F120-CASHTRAN                                           
              ADD 1                   TO WS-CNT-F120-WRITE                      
           END-IF.                                                              
                                                                                
           IF WS-AMOUNT > K690-AMT-COMPENSATION                                 
              MOVE SPACES             TO BP13F120-CASHTRAN                      
              INITIALIZE                 BP13F120-CASHTRAN                      
                                                                                
              IF K690-NUM-AMORTISE-TRANS = 'S'                                  
                 IF K690-CDE-TYP-ACTUSE = '01'                                  
                    MOVE  '27'           TO F120-CDE-TRANS-TYPE                 
                    MOVE  'SA1'          TO F120-CDE-PAYMENT-TYPE               
                 ELSE                                                           
                    IF K690-CDE-TYP-ACTUSE = '02'                               
                       MOVE  '28'        TO F120-CDE-TRANS-TYPE                 
                       MOVE  'SA2'       TO F120-CDE-PAYMENT-TYPE               
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF K690-NUM-AMORTISE-TRANS = 'F' OR 'L'                        
                    MOVE '37'            TO F120-CDE-TRANS-TYPE                 
                    MOVE 'VRL'           TO F120-CDE-PAYMENT-TYPE               
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE  'C'               TO F120-CDE-CRDR                          
                                                                                
              COMPUTE F120-AMT-RECEIPT = (WS-AMOUNT -                           
                                          K690-AMT-COMPENSATION)                
                                                                                
              MOVE K690-NUM-REGN      TO F120-NUM-REGN                          
              MOVE K690-NUM-SCH-ACC   TO F120-NUM-SCH-ACCT                      
              MOVE K690-DTE-KEY-ISSUE TO F120-DTE-POST                          
                                         F120-DTE-TRANS                         
              ADD 1                   TO WS-SEQ                                 
              MOVE WS-SEQ             TO F120-COUNT-HISTORY                     
                                                                                
              WRITE BP13F120-CASHTRAN                                           
              ADD 1                   TO WS-CNT-F120-WRITE                      
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3700-WRITE-BP13F690.                                                     
      *----------------------------------------------------------------*        
                                                                                
           WRITE F690-REC FROM K690-REC.                                        
           ADD 1 TO WS-CNT-F690-WRITE.                                          
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3800-FIND-REMARK.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF K690-AMT-COMPENSATION > K690-AMT-SURRENDER-COMPUTED               
              MOVE 'EC'        TO K690-TRANS-RMK                                
           ELSE                                                                 
           IF (K690-DTE-SURRENDER-REQUEST = SPACES OR LOW-VALUES                
                                            OR ALL '0')                         
              MOVE 'ND'        TO K690-TRANS-RMK                                
           ELSE                                                                 
           IF (K690-AMT-COMPENSATION = ZEROS)                                   
              MOVE 'NC'        TO K690-TRANS-RMK                                
           END-IF.                                                              
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3900-UPDATE-K690.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE F150-DTE-COMPLETION  TO K690-DTE-SURRENDER-COMPLETED.           
                                                                                
           REWRITE K690-REC.                                                    
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
                 ADD 1  TO WS-CNT-K690-UPDATE                                   
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13K690 - STATUS '                 
                             WS-K690-STATUS                                     
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-DELETE-BP13K690.                                                    
      *----------------------------------------------------------------*        
                                                                                
           DELETE BP13K690.                                                     
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
                 ADD 1  TO WS-CNT-K690-DELETE                                   
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN DELETING BP13K690 - STATUS '                 
                             WS-K690-STATUS                                     
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-BP13L368.                                                     
      *----------------------------------------------------------------*        
      *------>  MOVE DETAILS                                                    
           INITIALIZE     L368-DETAILS.                                         
                                                                                
           STRING K690-NUM-SCH-ACC(1:4) '-'                                     
                  K690-NUM-SCH-ACC(5:4) '-'                                     
                  K690-NUM-SCH-ACC(9:1) '-'                                     
                  K690-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO L368-SCH-ACC.                          
                                                                                
           STRING F150-NUM-HDB-REF(1:4) '-'                                     
                  F150-NUM-HDB-REF(5:4) '-'                                     
                  F150-NUM-HDB-REF(9:1) '-'                                     
                  F150-NUM-HDB-REF(10:2)                                        
                  DELIMITED BY SIZE INTO L368-SCH-ACC2.                         
                                                                                
           MOVE K690-NUM-REGN               TO L368-REGNO.                      
           MOVE K690-TRANS-RMK              TO L368-TRANS-RMK.                  
                                                                                
           IF K690-DTE-SURRENDER-COMPLETED NOT = SPACES AND LOW-VALUES          
                                               AND ZEROES                       
              STRING K690-DTE-SURRENDER-COMPLETED(7:2) '/'                      
                     K690-DTE-SURRENDER-COMPLETED(5:2) '/'                      
                     K690-DTE-SURRENDER-COMPLETED(1:4)                          
                     DELIMITED BY SIZE INTO L368-DTE-SURNDR                     
           END-IF.                                                              
                                                                                
      *------>  PRINT REPORT                                                    
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  L368-DTL-SERIAL.                    
           MOVE  WS-EDITED-DATE         TO  L368-DATE-HDR.                      
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO L368-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO L368-PAGE-HDR                               
              PERFORM 6000-PRINT-HEADING THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE L368-PRTREC FROM L368-DETAILS.                                 
                                                                                
           ADD   1   TO   WS-LINE-CTR.                                          
           ADD   1   TO   WS-CNT-L368-WRITE.                                    
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE L368-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L368-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L368-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L368-PRTREC FROM L368-HEADER01 AFTER PAGE.                    
            WRITE L368-PRTREC FROM L368-HEADER02.                               
            WRITE L368-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L368-PRTREC FROM L368-HEADER03.                               
            WRITE L368-PRTREC FROM L368-HEADER04.                               
            WRITE L368-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BR05F150                                                       
                 BP13F120                                                       
                 BP13F690                                                       
                 BP13K690                                                       
                 BP13L368.                                                      
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K690-STATUS      TO RETURN-CODE                        
                 DISPLAY 'CLOSING BP13K690 ERROR. STATUS='                      
                                             WS-K690-STATUS                     
           END-EVALUATE.                                                        
                                                                                
           DISPLAY '                                  '.                        
           DISPLAY '************ BP13C368 ************'.                        
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS WRITE      (BP13F120) = '                     
                    WS-CNT-F120-WRITE.                                          
           DISPLAY 'NO OF RECORDS DELETED    (BP13K690) = '                     
                    WS-CNT-K690-DELETE.                                         
           DISPLAY 'NO OF RECORDS UPDATED    (BP13K690) = '                     
                    WS-CNT-K690-UPDATE.                                         
           DISPLAY 'NO OF RECORDS WRITE      (BP13F690) = '                     
                    WS-CNT-F690-WRITE.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
