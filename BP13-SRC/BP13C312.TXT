      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C312.                                                 
       AUTHOR.        SHAQUILLE.                                                
       DATE-WRITTEN.  08/07/95.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  THIS IS A PROGRAM USE TO FILTER OUT THE   *        
      *                      OCS CASES.                                *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F310  - TRANSACTION FILE              *        
      *                  2.  BM06K100  - BLOCK FILE                    *        
      *                  3.  BM06K110  - FLATINV FILE                  *        
      *                                                                *        
      *   OUTPUT FILES:  1.  BP13F311  - OCSD CASES                    *        
      *                  2.  BP13F312  - OTHER CASES                   *        
      *                                                                *        
      * CHGE-NO   BY DATE    DESCRIPTION                               *        
      * -------  --- ------   ---------------------------------        *        
      * BP130418 SIA 980305  CHGE BM06K211 TO BM06K110, K210 TO K100   *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
           SELECT BP13F311  ASSIGN        TO BP13F311.                          
           SELECT BP13F312  ASSIGN        TO BP13F312.                          
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS BM06K100-STATUS.                   
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE   IS  F                                               
           RECORD CONTAINS  150 CHARACTERS.                                     
       01  F310-IN-REC      PIC X(150).                                         
                                                                                
       FD  BP13F311                                                             
           RECORDING MODE   IS  F                                               
           RECORD CONTAINS  150 CHARACTERS.                                     
       01  F311-REC         PIC X(150).                                         
                                                                                
       FD  BP13F312                                                             
           RECORDING MODE   IS  F                                               
           RECORD CONTAINS  150 CHARACTERS.                                     
       01  F312-REC         PIC X(150).                                         
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      **********************************************************                
      *        COPYBOOK AREAS                                  *                
      **********************************************************                
       COPY BP13F310.                                                           
                                                                                
      **********************************************************                
      *        MISCELLEANEOUS VARIABLES                        *                
      **********************************************************                
       01  BM06K100-STATUS              PIC 99    VALUE 0.                      
       01  BM06K110-STATUS              PIC 99    VALUE 0.                      
       01  CNT-F310-READ                PIC 9(7)  VALUE 0.                      
       01  CNT-K100-ERR                 PIC 9(7)  VALUE 0.                      
       01  CNT-K110-ERR                 PIC 9(7)  VALUE 0.                      
       01  CNT-F311-WRITE               PIC 9(7)  VALUE 0.                      
       01  CNT-F312-WRITE               PIC 9(7)  VALUE 0.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F310-EOF              PIC X(1)  VALUE 'N'.                    
                                                                                
       01  LT-SUCCESS                   PIC X(2)  VALUE '00'.                   
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM  1000-INITIALIZATION   THRU   1000-EXIT.                     
           PERFORM  3100-READ-BP13F310    THRU   3100-EXIT.                     
           PERFORM  2000-PROCESS-F310     THRU   2000-EXIT                      
                    UNTIL WS-F310-EOF = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE    THRU   9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13F310                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                OUTPUT BP13F311                                                 
                       BP13F312.                                                
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BM06K100-STATUS ' BM06K100-STATUS        
               MOVE     BM06K100-STATUS TO RETURN-CODE                          
               PERFORM  9900-HANDLE-ABEND     THRU 9900-EXIT                    
           END-IF.                                                              
           IF BM06K110-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR, BM06K110-STATUS ' BM06K110-STATUS        
               MOVE     BM06K110-STATUS TO RETURN-CODE                          
               PERFORM  9900-HANDLE-ABEND     THRU 9900-EXIT                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-F310.                                                       
      *================================================================*        
                                                                                
           MOVE  F310-SCH-ACC              TO K110-KEY-FLD.                     
           PERFORM 3200-READ-K110          THRU 3200-EXIT.                      
                                                                                
           IF  BM06K110-STATUS = LT-SUCCESS                                     
               MOVE K110-AIX1              TO   K100-KEY-FLD                    
               PERFORM 3300-READ-K100      THRU 3300-EXIT                       
               IF BM06K100-STATUS = LT-SUCCESS                                  
                  IF K100-NUM-OCS-TYPE = 'D'                                    
                     MOVE F310-IN-REC      TO F311-REC                          
                     PERFORM 3400-WRITE-F311 THRU 3400-EXIT                     
                  ELSE                                                          
                     MOVE F310-IN-REC      TO F312-REC                          
                     PERFORM 3500-WRITE-F312 THRU 3500-EXIT                     
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 3100-READ-BP13F310      THRU 3100-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-READ-BP13F310.                                                      
      *================================================================*        
           READ BP13F310                                                        
                AT END MOVE 'Y'  TO  WS-F310-EOF                                
                       GO TO 3100-EXIT.                                         
           MOVE F310-IN-REC      TO BP13F310-TRANS-REC.                         
           ADD  1  TO CNT-F310-READ.                                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3200-READ-K110.                                                          
      *================================================================*        
           READ BM06K110 KEY K110-KEY-FLD                                       
           INVALID KEY DISPLAY ' RECORD NOT FOUND: ' F310-KEY-FLD               
                               '  ' F310-SCH-ACC                                
                       ADD  1  TO CNT-K110-ERR                                  
           END-READ.                                                            
           IF BM06K110-STATUS NOT = 00 AND 97 AND 23                            
               DISPLAY 'READ ERROR, BM06K110-STATUS ' BM06K110-STATUS           
               MOVE     BM06K110-STATUS TO RETURN-CODE                          
               PERFORM  9900-HANDLE-ABEND     THRU 9900-EXIT                    
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3300-READ-K100.                                                          
      *================================================================*        
           READ BM06K100 KEY K100-KEY-FLD                                       
           INVALID KEY DISPLAY ' RECORD NOT FOUND: ' F310-KEY-FLD               
                               '  ' K110-AIX1                                   
                       ADD  1  TO CNT-K100-ERR                                  
           END-READ.                                                            
           IF BM06K100-STATUS NOT = 00 AND 97 AND 23                            
               DISPLAY 'READ ERROR, BM06K100-STATUS ' BM06K100-STATUS           
               MOVE     BM06K100-STATUS TO RETURN-CODE                          
               PERFORM  9900-HANDLE-ABEND     THRU 9900-EXIT                    
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3400-WRITE-F311.                                                         
      *================================================================*        
                                                                                
           WRITE F311-REC.                                                      
           ADD   1        TO CNT-F311-WRITE.                                    
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-WRITE-F312.                                                         
      *================================================================*        
                                                                                
           WRITE F312-REC.                                                      
           ADD   1        TO CNT-F312-WRITE.                                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
           CLOSE BP13F310                                                       
                 BM06K100                                                       
                 BM06K110                                                       
                 BP13F311                                                       
                 BP13F312.                                                      
                                                                                
           IF BM06K100-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K100-STATUS ' BM06K100-STATUS.        
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS.        
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C312 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM DAILY TRANS   (BP13F310) = '        
                    CNT-F310-READ.                                              
           DISPLAY 'NO OF RECORDS NOTFND IN BLOCK SUMMARY (BM06K100) = '        
                    CNT-K100-ERR.                                               
           DISPLAY 'NO OF RECORDS NOTFND IN FIS (BM06K110) = '                  
                    CNT-K110-ERR.                                               
           DISPLAY 'NO OF OCSD CASES            (BP13F311) = '                  
                    CNT-F311-WRITE.                                             
           DISPLAY 'NO OF NON-OCSD CASES        (BP13F312) = '                  
                    CNT-F312-WRITE.                                             
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9900-HANDLE-ABEND.                                                       
      ******************************************************************        
           CLOSE BP13F310                                                       
                 BM06K100                                                       
                 BM06K110                                                       
                 BP13F311                                                       
                 BP13F312.                                                      
                                                                                
           IF BM06K100-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K100-STATUS ' BM06K100-STATUS.        
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS.        
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C312 ABEND   TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM DAILY TRANS   (BP13F310) = '        
                    CNT-F310-READ.                                              
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
