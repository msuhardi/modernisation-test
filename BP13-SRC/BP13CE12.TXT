      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CE12.                                                 
       AUTHOR.        MRN1.                                                     
       DATE-WRITTEN.  17072013.                                                 
      *===============================================================*         
      *   OBJECTIVE:  REPORT 2: DISBURSEMENT & RECOVERY OF MAIN GRANT *         
      *                         FOR DBSS CASES                        *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13FD09                                  *         
      *                                                               *         
      *     REPORT FILE:                                              *         
      *                  1. P13LE12A                                  *         
      *                  2. P13LE12B                                  *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134970 17/07/2013 MRN1 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FD09  ASSIGN        TO BP13FD09.                         
                                                                                
           SELECT  P13LE12A ASSIGN        TO P13LE12A.                          
                                                                                
           SELECT  P13LE12B ASSIGN        TO P13LE12B.                          
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FD09                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13KD09.                                                           
                                                                                
       FD  P13LE12A                                                             
           RECORD CONTAINS 900 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13LE12A-REC                 PIC X(900).                             
                                                                                
       FD  P13LE12B                                                             
           RECORD CONTAINS 900 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13LE12B-REC                 PIC X(900).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-FD09-EOF             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FD09-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE-TIME.                                              
              10 WS-SYSTEM-DATE        PIC X(08) VALUE ZEROES.                  
              10 WS-SYSTEM-TIME        PIC X(08) VALUE ZEROES.                  
           05 WS-DATE                  PIC X(08) VALUE SPACES.                  
           05 WS-TOT-OUTSTDG-A         PIC S9(8)V99 VALUE ZEROES.               
           05 WS-TOT-OUTSTDG-B         PIC S9(8)V99 VALUE ZEROES.               
           05 WS-TOT-GRANT             PIC S9(8)V99 VALUE ZEROES.               
           05 WS-TOT-RECOVER           PIC S9(8)V99 VALUE ZEROES.               
                                                                                
       01  WS-HEADER-LE12A1.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '  S/N  GRANT                   GRANT              '.            
           05  FILLER                 PIC X(50) VALUE                           
               '               REG NO     AMOUNT      AMOUNT    EF'.            
           05  FILLER                 PIC X(50) VALUE                           
               'F-DATE    EFF-DATE  TOT OUTSTANDING  CRM          '.            
                                                                                
       01  WS-HEADER-LE12A2.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '       CODE                    TYPE               '.            
           05  FILLER                 PIC X(50) VALUE                           
               '                       DISBURSEMENT  RECOVERED REP'.            
           05  FILLER                 PIC X(50) VALUE                           
               'UDIATION  TRANSFER      AMOUNT      (OIC)         '.            
                                                                                
       01  WS-HEADER-LE12A3.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '------ ----- -------------------------------------'.            
           05  FILLER                 PIC X(50) VALUE                           
               '------------- -------- ------------- --------- ---'.            
           05  FILLER                 PIC X(50) VALUE                           
               '-------- ---------- --------------- -----         '.            
                                                                                
       01  WS-SPACES.                                                           
           05  FILLER       PIC X(900)   VALUE SPACES.                          
                                                                                
       01  WS-LE12A-DETAIL.                                                     
           05 WS-LE12A-SNO                  PIC ZZZZZ9.                         
           05 FILLER                        PIC X(03) VALUE SPACES.             
           05 WS-LE12A-CDE-GRANT            PIC X(01).                          
           05 FILLER                        PIC X(03) VALUE SPACES.             
           05 WS-LE12A-TYP-GRANT            PIC X(50).                          
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE12A-NUM-REGN             PIC X(08).                          
           05 FILLER                        PIC X(02) VALUE SPACES.             
           05 WS-LE12A-AMT-DISB             PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(02) VALUE SPACES.             
           05 WS-LE12A-AMT-RECOVER          PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE12A-DTE-CANCEL           PIC X(10).                          
           05 FILLER                        PIC X(02) VALUE SPACES.             
           05 WS-LE12A-DTE-TRANSFER         PIC X(10).                          
           05 FILLER                        PIC X(04) VALUE SPACES.             
           05 WS-LE12A-AMT-TOT-OUTSTDG      PIC -ZZZ,ZZ9.99.                    
           05 FILLER                        PIC X(03) VALUE SPACES.             
           05 WS-LE12A-OIC                  PIC X(05).                          
           05 FILLER                        PIC X(20) VALUE SPACES.             
                                                                                
       01  WS-HEADER-LE12B1.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '  S/N   REG NO      AMOUNT     AMOUNT    EFF-DATE '.            
           05  FILLER                 PIC X(50) VALUE                           
               '   EFF-DATE  TOT OUTSTANDING  CRM                 '.            
                                                                                
       01  WS-HEADER-LE12B2.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '                 DISBURSEMENT RECOVERED REPUDIATIO'.            
           05  FILLER                 PIC X(50) VALUE                           
               'N  TRANSFER      AMOUNT      (OIC)                '.            
                                                                                
       01  WS-HEADER-LE12B3.                                                    
           05  FILLER                 PIC X(50) VALUE                           
               '------ -------- ------------- --------- ----------'.            
           05  FILLER                 PIC X(50) VALUE                           
               '- ---------- --------------- -----                '.            
                                                                                
       01  WS-LE12B-DETAIL.                                                     
           05 WS-LE12B-SNO                  PIC ZZZZZ9.                         
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE12B-NUM-REGN             PIC X(08).                          
           05 FILLER                        PIC X(03) VALUE SPACES.             
           05 WS-LE12B-AMT-DISB             PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE12B-AMT-RECOVER          PIC ZZZ,ZZ9.99.                     
           05 FILLER                        PIC X(01) VALUE SPACES.             
           05 WS-LE12B-DTE-CANCEL           PIC X(10).                          
           05 FILLER                        PIC X(02) VALUE SPACES.             
           05 WS-LE12B-DTE-TRANSFER         PIC X(10).                          
           05 FILLER                        PIC X(04) VALUE SPACES.             
           05 WS-LE12B-AMT-TOT-OUTSTDG      PIC -ZZZ,ZZ9.99.                    
           05 FILLER                        PIC X(03) VALUE SPACES.             
           05 WS-LE12B-OIC                  PIC X(05).                          
           05 FILLER                        PIC X(20) VALUE SPACES.             
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13FD09      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-FD09-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD09                                                 
               OUTPUT  P13LE12A                                                 
                       P13LE12B.                                                
                                                                                
           WRITE P13LE12A-REC FROM WS-HEADER-LE12A1.                            
           WRITE P13LE12A-REC FROM WS-HEADER-LE12A2.                            
           WRITE P13LE12A-REC FROM WS-HEADER-LE12A3.                            
                                                                                
           WRITE P13LE12B-REC FROM WS-HEADER-LE12B1.                            
           WRITE P13LE12B-REC FROM WS-HEADER-LE12B2.                            
           WRITE P13LE12B-REC FROM WS-HEADER-LE12B3.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FD09.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD09           AT END                                       
                MOVE 'Y'           TO WS-FD09-EOF                               
                GO                 TO 2000-EXIT.                                
                                                                                
           IF KD09-NUM-REGN = SPACES OR LOW-VALUES                              
              GO TO 2000-READ-BP13FD09.                                         
                                                                                
           ADD  1                  TO WS-FD09-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 3500-CHECK-NON-NUMERIC THRU 3500-EXIT.                       
           INITIALIZE WS-LE12A-DETAIL.                                          
           INITIALIZE WS-LE12B-DETAIL.                                          
           PERFORM 4000-MOVE-VALUES-TO-REPORTS THRU 4000-EXIT.                  
                                                                                
           ADD 1 TO WS-SNO.                                                     
           MOVE WS-SNO                   TO WS-LE12A-SNO                        
                                            WS-LE12B-SNO.                       
           WRITE P13LE12A-REC FROM WS-LE12A-DETAIL.                             
           WRITE P13LE12B-REC FROM WS-LE12B-DETAIL.                             
                                                                                
           PERFORM 2000-READ-BP13FD09 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-CHECK-NON-NUMERIC.                                                  
      ******************************************************************        
           IF KD09-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-SHG-GRANT                                 
           END-IF.                                                              
                                                                                
           IF KD09-AMT-SHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-SHG-RECOVER                               
           END-IF.                                                              
                                                                                
           IF KD09-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROES TO KD09-AMT-AHG-GRANT                                 
           END-IF.                                                              
                                                                                
           IF KD09-AMT-AHG-RECOVER IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-AHG-RECOVER                               
           END-IF.                                                              
                                                                                
           IF KD09-AMT-HSGNT-GRANT IS NOT NUMERIC                               
              MOVE ZEROES TO KD09-AMT-HSGNT-GRANT                               
           END-IF.                                                              
                                                                                
           IF KD09-AMT-HSGNT-RECOVER IS NOT NUMERIC                             
              MOVE ZEROES TO KD09-AMT-HSGNT-RECOVER                             
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-MOVE-VALUES-TO-REPORTS.                                             
      ******************************************************************        
           MOVE KD09-CODE-GRANT         TO WS-LE12A-CDE-GRANT.                  
           PERFORM 5000-GET-GRANT-TYPE  THRU 5000-EXIT.                         
           MOVE KD09-NUM-REGN           TO WS-LE12A-NUM-REGN                    
                                           WS-LE12B-NUM-REGN.                   
           IF KD09-DTE-CANCEL NOT = SPACES AND LOW-VALUES                       
              STRING KD09-DTE-CANCEL(7:2) '/'                                   
                     KD09-DTE-CANCEL(5:2) '/'                                   
                     KD09-DTE-CANCEL(1:4)                                       
                     DELIMITED BY SIZE  INTO WS-LE12A-DTE-CANCEL                
              STRING KD09-DTE-CANCEL(7:2) '/'                                   
                     KD09-DTE-CANCEL(5:2) '/'                                   
                     KD09-DTE-CANCEL(1:4)                                       
                     DELIMITED BY SIZE  INTO WS-LE12B-DTE-CANCEL                
           END-IF.                                                              
           MOVE SPACES                  TO WS-LE12A-DTE-TRANSFER                
                                           WS-LE12B-DTE-TRANSFER.               
           MOVE KD09-NUM-OIC            TO WS-LE12A-OIC                         
                                           WS-LE12B-OIC.                        
                                                                                
           COMPUTE WS-TOT-GRANT    = KD09-AMT-AHG-GRANT +                       
                                     KD09-AMT-HSGNT-GRANT                       
           COMPUTE WS-TOT-RECOVER  = KD09-AMT-AHG-RECOVER +                     
                                     KD09-AMT-HSGNT-RECOVER                     
           MOVE WS-TOT-GRANT             TO WS-LE12A-AMT-DISB                   
           MOVE WS-TOT-RECOVER           TO WS-LE12A-AMT-RECOVER                
           COMPUTE WS-TOT-OUTSTDG-A = WS-TOT-GRANT - WS-TOT-RECOVER.            
           MOVE WS-TOT-OUTSTDG-A         TO WS-LE12A-AMT-TOT-OUTSTDG.           
                                                                                
           MOVE KD09-AMT-AHG-GRANT       TO WS-LE12B-AMT-DISB                   
           MOVE KD09-AMT-AHG-RECOVER     TO WS-LE12B-AMT-RECOVER                
           COMPUTE WS-TOT-OUTSTDG-B = KD09-AMT-AHG-GRANT -                      
                                      KD09-AMT-AHG-RECOVER.                     
           MOVE WS-TOT-OUTSTDG-B         TO WS-LE12B-AMT-TOT-OUTSTDG.           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-GET-GRANT-TYPE.                                                     
      ******************************************************************        
           EVALUATE KD09-CODE-GRANT                                             
             WHEN 'F'                                                           
               MOVE 'FAMILY GRANT'         TO WS-LE12A-TYP-GRANT                
             WHEN 'M'                                                           
               MOVE 'FAMILY GRANT (LIVING NEAR PARENTS/MARRIED CHILD)'          
                                           TO WS-LE12A-TYP-GRANT                
             WHEN 'S'                                                           
               MOVE 'SINGLES GRANT'        TO WS-LE12A-TYP-GRANT                
             WHEN 'H'                                                           
               MOVE 'SINGLES GRANT LIVING WITH PARENTS'                         
                                           TO WS-LE12A-TYP-GRANT                
             WHEN 'L'                                                           
               MOVE 'HALF GRANT'           TO WS-LE12A-TYP-GRANT                
             WHEN 'K'                                                           
               MOVE 'HALF GRANT (LIVING NEAR PARENTS/MARRIED CHILD)'            
                                           TO WS-LE12A-TYP-GRANT                
             WHEN 'T'                                                           
               MOVE 'TOP-UP GRANT'         TO WS-LE12A-TYP-GRANT                
             WHEN 'P'                                                           
               MOVE 'TOP- UP GRANT (LIVING NEAR PARENTS/MARRIED CHILD)'         
                                           TO WS-LE12A-TYP-GRANT                
             WHEN 'C'                                                           
               MOVE 'CITIZEN TOP-UP GRANT' TO WS-LE12A-TYP-GRANT                
             WHEN OTHER                                                         
               MOVE SPACES                 TO WS-LE12A-TYP-GRANT                
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CE12'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13FD09 RECORDS READ............. : '            
                    WS-FD09-READ.                                               
                                                                                
           CLOSE    BP13FD09                                                    
                    P13LE12A                                                    
                    P13LE12B.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
