       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C041.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN. 18/11/2003.                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE :                                                *            
      *      MATCH BP13F800 WITH BP13F825 TO GET THE HOUSEHOLD     *            
      *      SIZE, AGE OF MAIN APPLICANT, MARRIED COUPLES AND      *            
      *      HOUSEHOLD INCOME                                      *            
      *                                                            *            
      * INPUT  : BP13F800                                          *            
      *          BP13F825                                          *            
      *          BP13K820                                          *            
      *                                                            *            
      * OUTPUT : P13F800A                                          *            
      *                                                            *            
      * CHG REF  BY   ON        DESCRIPTION                        *            
      * ------   ---- --------  -----------------------------------*            
      * BP132442 SK   20031118  NEW PROGRAM.                       *            
      * BP132558 SK   20040712  TO CATER FOR ETHNIC CODE           *            
      * BP132598 SK   20040922  TO CATER UNTIL HIGH-VALUES         *            
      * BP132908 SSS2 20060823  READ HA1 MS                        *            
      * BP132892 SSS2 20060926  READ BP13K820                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800   ASSIGN TO BP13F800.                                
           SELECT BP13K820   ASSIGN TO BP13K820                                 
                  ACCESS MODE       IS DYNAMIC                                  
                  ORGANIZATION      IS INDEXED                                  
                  RECORD KEY        IS K820-KEY-FLD                             
                  FILE STATUS       IS WS-K820-STATUS.                          
                                                                                
           SELECT BP13F825   ASSIGN  TO BP13F825.                               
           SELECT P13F800A   ASSIGN  TO P13F800A.                               
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F825                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F825.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F800A-REC.                                                       
            05 FILLER               PIC X(1979).                                
            05 F800A-HH-SIZE        PIC 9(2).                                   
            05 F800A-APPLN-AGE      PIC 9(4).                                   
            05 F800A-HA1-DOB        PIC 9(8).                                   
            05 F800A-HH-INCOME      PIC 9(5).                                   
            05 F800A-COUPLES        PIC X(1).                                   
            05 F800A-HA1-MS         PIC X(1).                                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-F800-READ-CNT        PIC 9(8) VALUE ZEROES.                        
       01 WS-F825-READ-CNT        PIC 9(8) VALUE ZEROES.                        
       01 WS-F800A-WRITE-CNT      PIC 9(8) VALUE ZEROES.                        
                                                                                
       01 WS-F800-EOF             PIC X     VALUE 'N'.                          
          88 F800-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F825-EOF             PIC X     VALUE 'N'.                          
          88 F825-END-OF-FILE   VALUE 'Y'.                                      
                                                                                
       01 WS-K820-EOF             PIC X(1)  VALUE 'N'.                          
       01 WS-K820-STATUS          PIC 9(2)  VALUE 0.                            
                                                                                
       01 WS-CURR-DATE            PIC X(8)  VALUE ZEROES.                       
       01 WS-NUM-CURR-DATE  REDEFINES WS-CURR-DATE.                             
          05 WS-CURR-CCYYMM       PIC 9(6).                                     
          05 WS-CURR-DD           PIC 9(2).                                     
       01 WS-BIRTH-DATE.                                                        
          05 WS-BIRTH-CCYYMM      PIC 9(6).                                     
          05 WS-BIRTH-DD          PIC 9(2).                                     
                                                                                
       01 WS-AMT-INCOME           PIC 9(5)  VALUE ZEROES.                       
       01 WS-COUNTER              PIC 9(2)  VALUE ZEROES.                       
       01 WS-AGE                  PIC 9(6)  VALUE ZEROES.                       
       01 WS-NUM-AGE  REDEFINES WS-AGE.                                         
          05 WS-APPLN-AGE         PIC 9(4).                                     
          05 WS-NUM-REMNG         PIC 9(2).                                     
       01 WS-HH-SIZE              PIC 9(2)  VALUE ZEROES.                       
       01 WS-HA1-MS               PIC X     VALUE SPACE.                        
                                                                                
       01 WS-APPLICANT            PIC X     VALUE SPACE.                        
       01 WS-SPOUSE               PIC X     VALUE SPACE.                        
                                                                                
       01 WS-SPOUSE-FND           PIC X     VALUE SPACE.                        
       01 WS-FATHER-FND           PIC X     VALUE SPACE.                        
       01 WS-MOTHER-FND           PIC X     VALUE SPACE.                        
                                                                                
       01 WS-CAT1                 PIC X     VALUE SPACE.                        
       01 WS-CAT2                 PIC X     VALUE SPACE.                        
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-EDIT1                PIC Z(7)9.                                    
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F800   THRU 2000-EXIT.                         
           PERFORM 3000-READ-BP13F825   THRU 3000-EXIT.                         
           PERFORM 4000-PROCESS-DATA    THRU 4000-EXIT                          
                   UNTIL F800-NUM-REGN = HIGH-VALUES                            
                     AND F825-NUM-REGN = HIGH-VALUES.                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F825                                                 
                       BP13K820                                                 
               OUTPUT  P13F800A.                                                
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K820-STATUS      TO RETURN-CODE                      
                   DISPLAY 'OPEN BP13K820 FAIL. STATUS ' WS-K820-STATUS         
                   PERFORM 9000-CLOSE-ROUTINE                                   
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CURR-DATE.                     
           STRING WS-CURR-DATE(7:2) '/'                                         
                  WS-CURR-DATE(5:2) '/'                                         
                  WS-CURR-DATE(1:4)                                             
                  DELIMITED BY SIZE INTO WS-EDT-DATE.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
                            MOVE HIGH-VALUES  TO F800-NUM-REGN                  
                         NOT AT END                                             
                            ADD 1    TO WS-F800-READ-CNT                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F825.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F825 AT END                                                 
                            MOVE HIGH-VALUES  TO F825-NUM-REGN                  
                         NOT AT END                                             
                            ADD 1    TO WS-F825-READ-CNT                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE  ZEROES      TO WS-HH-SIZE                                      
                                WS-AMT-INCOME                                   
                                WS-BIRTH-DATE                                   
                                WS-AGE.                                         
           MOVE  SPACES      TO WS-APPLICANT                                    
                                WS-SPOUSE.                                      
           MOVE  SPACES      TO WS-SPOUSE-FND WS-FATHER-FND                     
                                WS-MOTHER-FND                                   
                                WS-CAT1 WS-CAT2                                 
                                WS-HA1-MS.                                      
                                                                                
           EVALUATE TRUE                                                        
              WHEN F800-NUM-REGN  =  F825-NUM-REGN                              
                                                                                
                 PERFORM 5000-COMPUTE-HOUSEHOLD-SIZE THRU 5000-EXIT             
                       UNTIL (F825-NUM-REGN  NOT =  F800-NUM-REGN) OR           
                             (F825-NUM-REGN = HIGH-VALUES)                      
                                                                                
                 PERFORM 6000-WRITE-P13F800A   THRU 6000-EXIT                   
                 PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                   
                                                                                
              WHEN F800-NUM-REGN  <  F825-NUM-REGN                              
                                                                                
                 MOVE 'N' TO WS-K820-EOF                                        
                 MOVE 0   TO WS-COUNTER                                         
      *          PERFORM 6000-WRITE-P13F800A   THRU 6000-EXIT                   
                 PERFORM 5100-READ-BP13K820        THRU 5100-EXIT               
                       UNTIL WS-K820-EOF = 'Y'                                  
                                                                                
                 IF WS-COUNTER > 0                                              
                    PERFORM 6000-WRITE-P13F800A   THRU 6000-EXIT                
                 END-IF                                                         
                 PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                   
                                                                                
              WHEN F825-NUM-REGN  <  F800-NUM-REGN                              
                                                                                
                 PERFORM 3000-READ-BP13F825    THRU 3000-EXIT                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-COMPUTE-HOUSEHOLD-SIZE.                                             
      *-------------------------------------------------------------            
                                                                                
           ADD  1      TO WS-HH-SIZE.                                           
                                                                                
           IF F825-NUM-NRIC  =  F800-NUM-NRIC1                                  
                                                                                
             MOVE F825-NUM-MARITAL-STATUS TO WS-HA1-MS                          
                                                                                
             IF F825-DTE-BIRTH  IS NUMERIC                                      
                MOVE F825-DTE-BIRTH    TO WS-BIRTH-DATE                         
                COMPUTE WS-AGE  =  WS-CURR-CCYYMM - WS-BIRTH-CCYYMM             
             ELSE                                                               
                MOVE ZEROES          TO WS-AGE                                  
             END-IF                                                             
           ELSE                                                                 
             EVALUATE F825-NUM-RELATIONSHIP                                     
                WHEN '20'                                                       
                   MOVE 'Y'   TO WS-SPOUSE-FND                                  
                WHEN '02'                                                       
                   MOVE 'Y'   TO WS-FATHER-FND                                  
                WHEN '12'                                                       
                   MOVE 'Y'   TO WS-MOTHER-FND                                  
             END-EVALUATE                                                       
           END-IF.                                                              
                                                                                
           IF F825-AMT-INCOME IS NUMERIC                                        
              ADD F825-AMT-INCOME    TO WS-AMT-INCOME                           
           END-IF.                                                              
                                                                                
           IF F825-NUM-NRIC  =  F800-NUM-NRIC1                                  
              IF F825-NUM-ETHNIC NOT = SPACES AND LOW-VALUES                    
                 EVALUATE F825-NUM-ETHNIC                                       
                    WHEN '0'                                                    
                       MOVE '2'   TO WS-CAT1                                    
                    WHEN '6'                                                    
                       MOVE '3'   TO WS-CAT1                                    
                    WHEN '5'                                                    
                       MOVE '1'   TO WS-CAT1                                    
                    WHEN '7'                                                    
                       MOVE '4'   TO WS-CAT1                                    
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F825-NUM-NRIC  =  F800-NUM-NRIC2                                  
              IF (F825-NUM-ETHNIC NOT = SPACES AND LOW-VALUES) AND              
                 (WS-CAT1 NOT = SPACES AND LOW-VALUES)                          
                 EVALUATE F825-NUM-ETHNIC                                       
                    WHEN '0'                                                    
                       MOVE '2'   TO WS-CAT2                                    
                    WHEN '6'                                                    
                       MOVE '3'   TO WS-CAT2                                    
                    WHEN '5'                                                    
                       MOVE '1'   TO WS-CAT2                                    
                    WHEN '7'                                                    
                       MOVE '4'   TO WS-CAT2                                    
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F825    THRU 3000-EXIT.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5100-READ-BP13K820.                                                      
      *---------------------------------------------------------------*         
                                                                                
           MOVE LOW-VALUES    TO K820-KEY-FLD.                                  
           MOVE F800-NUM-REGN TO K820-NUM-REGN.                                 
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
                WHEN 00                                                         
                    PERFORM 5200-READNEXT-K820 THRU 5200-EXIT                   
                            UNTIL K820-NUM-REGN NOT = F800-NUM-REGN             
                WHEN 23                                                         
                     MOVE 'Y'         TO WS-K820-EOF                            
                WHEN OTHER                                                      
                 DISPLAY 'ERRROR READING  BP13K820, STATUS = '                  
                          WS-K820-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE                                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5200-READNEXT-K820.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13K820 NEXT RECORD                                            
                AT END MOVE HIGH-VALUES TO K820-NUM-REGN.                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
                WHEN 00                                                         
                    IF F800-NUM-REGN = K820-NUM-REGN                            
                       ADD 1 TO WS-COUNTER                                      
                       PERFORM 5300-COMPUTE-HOUSEHOLD-SIZE1                     
                               THRU 5300-EXIT                                   
                    ELSE                                                        
                       MOVE 'Y'         TO WS-K820-EOF                          
                    END-IF                                                      
                WHEN 23                                                         
                     MOVE 'Y'         TO WS-K820-EOF                            
                WHEN OTHER                                                      
                 DISPLAY 'ERRROR READING BP13K820, STATUS = '                   
                          WS-K820-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE                                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5300-COMPUTE-HOUSEHOLD-SIZE1.                                            
      *---------------------------------------------------------------*         
                                                                                
           ADD  1      TO WS-HH-SIZE.                                           
           IF K820-NUM-NRIC  =  F800-NUM-NRIC1                                  
             IF K820-DTE-BIRTH  IS NUMERIC                                      
                MOVE K820-DTE-BIRTH    TO WS-BIRTH-DATE                         
                COMPUTE WS-AGE  =  WS-CURR-CCYYMM - WS-BIRTH-CCYYMM             
             ELSE                                                               
                MOVE ZEROES          TO WS-AGE                                  
             END-IF                                                             
           ELSE                                                                 
             EVALUATE K820-NUM-RELATIONSHIP                                     
                WHEN '20'                                                       
                   MOVE 'Y'   TO WS-SPOUSE-FND                                  
                WHEN '02'                                                       
                   MOVE 'Y'   TO WS-FATHER-FND                                  
                WHEN '12'                                                       
                   MOVE 'Y'   TO WS-MOTHER-FND                                  
             END-EVALUATE                                                       
           END-IF.                                                              
                                                                                
           IF K820-AMT-INCOME IS NUMERIC                                        
              ADD K820-AMT-INCOME    TO WS-AMT-INCOME                           
           END-IF.                                                              
                                                                                
           IF K820-NUM-NRIC  =  F800-NUM-NRIC1                                  
              IF K820-NUM-ETHNIC NOT = SPACES AND LOW-VALUES                    
                 EVALUATE K820-NUM-ETHNIC                                       
                    WHEN '0'                                                    
                       MOVE '2'   TO WS-CAT1                                    
                    WHEN '6'                                                    
                       MOVE '3'   TO WS-CAT1                                    
                    WHEN '5'                                                    
                       MOVE '1'   TO WS-CAT1                                    
                    WHEN '7'                                                    
                       MOVE '4'   TO WS-CAT1                                    
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K820-NUM-NRIC  =  F800-NUM-NRIC2                                  
              IF (K820-NUM-ETHNIC NOT = SPACES AND LOW-VALUES) AND              
                 (WS-CAT1 NOT = SPACES AND LOW-VALUES)                          
                 EVALUATE K820-NUM-ETHNIC                                       
                    WHEN '0'                                                    
                       MOVE '2'   TO WS-CAT2                                    
                    WHEN '6'                                                    
                       MOVE '3'   TO WS-CAT2                                    
                    WHEN '5'                                                    
                       MOVE '1'   TO WS-CAT2                                    
                    WHEN '7'                                                    
                       MOVE '4'   TO WS-CAT2                                    
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-WRITE-P13F800A.                                                     
      *---------------------------------------------------------------*         
                                                                                
           MOVE  SPACES   TO P13F800A-REC.                                      
           INITIALIZE        P13F800A-REC.                                      
                                                                                
           IF F800-NUM-CAT = SPACES OR LOW-VALUES                               
              IF WS-CAT1 NOT = SPACES AND LOW-VALUE                             
                 MOVE WS-CAT1 TO F800-NUM-CAT                                   
              ELSE                                                              
                 MOVE WS-CAT2 TO F800-NUM-CAT                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE  BP13F800-MASTER  TO P13F800A-REC.                              
           MOVE  WS-HH-SIZE       TO F800A-HH-SIZE.                             
           MOVE  WS-APPLN-AGE     TO F800A-APPLN-AGE.                           
           MOVE  WS-BIRTH-DATE    TO F800A-HA1-DOB.                             
           MOVE  WS-AMT-INCOME    TO F800A-HH-INCOME.                           
           MOVE  WS-HA1-MS        TO F800A-HA1-MS.                              
                                                                                
           IF F800-NUM-ELIG-SCHEME = 'FS '                                      
              MOVE '2'  TO  F800A-COUPLES                                       
           ELSE                                                                 
              IF (WS-SPOUSE-FND = 'Y') OR                                       
                 (WS-FATHER-FND = 'Y' AND WS-MOTHER-FND = 'Y')                  
                   MOVE '1'  TO  F800A-COUPLES                                  
              ELSE                                                              
                   MOVE '3'  TO  F800A-COUPLES                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE  P13F800A-REC.                                                 
           ADD 1  TO WS-F800A-WRITE-CNT.                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
                                                                                
           DISPLAY 'BP13C041 CONTROL TOTAL        ' WS-EDT-DATE.                
           DISPLAY '******************************************'.                
           DISPLAY ' '.                                                         
           MOVE WS-F800-READ-CNT   TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS READ FROM BP13F800  : ' WS-EDIT1.                
           MOVE WS-F825-READ-CNT   TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS READ FROM BP13F825  : ' WS-EDIT1.                
           MOVE WS-F800A-WRITE-CNT TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO P13F800A : ' WS-EDIT1.                
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F825                                                       
                 BP13F800                                                       
                 BP13K820                                                       
                 P13F800A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
