       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         BP13C03B.                                            
      *AUTHOR.             ZARITA SARMIENTO.                                    
      *DATE-WRITTEN.       13 JUL 2009.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO DELETE CANCELLED CASES ON BP13K853       *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F740                                 *            
      *  OUTPUT FILE:  1. BP13K853                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133410 ZDD1 07042009 NEW PROGRAM                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BP13F740 ASSIGN TO BP13F740.                                 
                                                                                
            SELECT BP13K853 ASSIGN TO BP13K853                                  
                      ORGANIZATION IS INDEXED                                   
                      RECORD KEY IS K853-KEY-FLD                                
                      FILE STATUS IS WS-BP13K853-STAT                           
                      ACCESS MODE IS DYNAMIC.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F740   RECORD CONTAINS 500 CHARACTERS.                          
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K853   RECORD CONTAINS 250  CHARACTERS.                         
       COPY BP13K853.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 WS-VARIABLES.                                                         
          05 WS-BP13K853-STAT            PIC X(02) VALUE SPACES.                
          05 EOF-FLAG-BP13F740           PIC X(01) VALUE 'N'.                   
          05 EOF-FLAG-BP13K853           PIC X(01) VALUE 'N'.                   
          05 WS-REC-DELETED              PIC X(01) VALUE 'N'.                   
          05 WS-PREV-REGN                PIC X(08) VALUE SPACES.                
          05 WS-F740-READ                PIC 9(06) VALUE ZEROES.                
          05 WS-K853-READ                PIC 9(06) VALUE ZEROES.                
          05 WS-K853-NOT-EXIST           PIC 9(06) VALUE ZEROES.                
          05 WS-K853-DELETED             PIC 9(06) VALUE ZEROES.                
          05 WS-REG-NUM-FOUND            PIC 9(06) VALUE ZEROES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-MAIN-PROCESS      THRU 2000-EXIT                        
                   UNTIL EOF-FLAG-BP13F740 = 'Y'.                               
           PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT   BP13F740                                                
                I-O     BP13K853.                                               
                                                                                
                                                                                
           IF WS-BP13K853-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BP13K853   STATUS - '                   
              WS-BP13K853-STAT                                                  
              MOVE WS-BP13K853-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-MAIN-PROCESS.                                                       
      ****************************                                              
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO EOF-FLAG-BP13F740                                   
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                 TO WS-F740-READ.                               
                                                                                
           MOVE SPACES           TO K853-KEY-FLD.                               
           MOVE F740-NUM-REGN    TO K853-NUM-REGN.                              
           MOVE 'N'              TO EOF-FLAG-BP13K853                           
                                    WS-REC-DELETED.                             
                                                                                
           PERFORM 3000-START-K853   THRU 3000-EXIT.                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-START-K853.                                                         
      ****************************                                              
                                                                                
           START BP13K853 KEY IS >= K853-KEY-FLD.                               
                                                                                
           EVALUATE WS-BP13K853-STAT                                            
             WHEN 00                                                            
             WHEN 02                                                            
                 PERFORM 3100-READ-NEXT-K853  THRU 3100-EXIT                    
                      UNTIL F740-NUM-REGN NOT = K853-NUM-REGN OR                
                         EOF-FLAG-BP13K853 = 'Y'                                
             WHEN 23                                                            
                 ADD 1 TO WS-K853-NOT-EXIST                                     
             WHEN OTHER                                                         
                  DISPLAY 'ERROR IN START BP13K853..' WS-BP13K853-STAT          
                  MOVE WS-BP13K853-STAT             TO RETURN-CODE              
                  PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3100-READ-NEXT-K853.                                                     
      ****************************                                              
                                                                                
           READ BP13K853 NEXT RECORD                                            
                                                                                
           EVALUATE WS-BP13K853-STAT                                            
             WHEN 00                                                            
             WHEN 02                                                            
                 IF F740-NUM-REGN = K853-NUM-REGN                               
                   IF WS-PREV-REGN NOT = F740-NUM-REGN                          
                     ADD 1 TO WS-REG-NUM-FOUND                                  
                   END-IF                                                       
                                                                                
                   DELETE BP13K853                                              
                                                                                
                   IF WS-BP13K853-STAT NOT  = 00                                
                     DISPLAY 'ERROR DELETING THE RECORD..STAT'                  
                          WS-BP13K853-STAT                                      
                     MOVE WS-BP13K853-STAT             TO RETURN-CODE           
                     PERFORM 9999-CLOSE-FILES  THRU 9999-EXIT                   
                   END-IF                                                       
                                                                                
                   ADD 1 TO WS-K853-DELETED                                     
                   MOVE F740-NUM-REGN TO WS-PREV-REGN                           
                   MOVE 'Y' TO WS-REC-DELETED                                   
                 ELSE                                                           
                   IF WS-REC-DELETED = 'N'                                      
                     ADD 1 TO WS-K853-NOT-EXIST                                 
                   END-IF                                                       
                 END-IF                                                         
             WHEN 10                                                            
                 MOVE 'Y' TO EOF-FLAG-BP13K853                                  
             WHEN OTHER                                                         
                 DISPLAY 'ERROR IN READING BP13K853..' WS-BP13K853-STAT         
                 MOVE WS-BP13K853-STAT             TO RETURN-CODE               
                 PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9999-CLOSE-FILES.                                                        
      ****************************                                              
                                                                                
           DISPLAY '9999-CLOSE-FILES'.                                          
           DISPLAY SPACES.                                                      
           DISPLAY 'REPORT DESCRIPTION'.                                        
           DISPLAY '------------------'.                                        
           DISPLAY SPACES.                                                      
           DISPLAY 'NO BP13F740 READ          :' WS-F740-READ                   
           DISPLAY 'NO BP13K853 NOT EXIST     :' WS-K853-NOT-EXIST              
           DISPLAY 'NO REG# FOUND IN K853     :' WS-REG-NUM-FOUND               
           DISPLAY 'NO OF TOTAL REG# DELETED  :' WS-K853-DELETED                
           DISPLAY ' '                                                          
           CLOSE BP13K853                                                       
                 BP13F740.                                                      
                                                                                
           IF WS-BP13K853-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN CLOSING BP13K853   STATUS - '                   
              WS-BP13K853-STAT                                                  
              MOVE WS-BP13K853-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
