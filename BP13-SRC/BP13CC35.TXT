      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13CC35.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  11/06/2010.                                               
      *****************************************************************         
      *                                                               *         
      *  SYSTEM NAME  :    SYSTEM OF COMMITMENT   (SOC)               *         
      *                                                               *         
      *  SYSTEM ID    :    BP13                                       *         
      *                                                               *         
      *  OBJECTIVE    :                                               *         
      *                                                               *         
      *  INPUT FILES  :    1. BP13.FD45            (BP13FD45)         *         
      *                                                               *         
      *  CHGE-NO  BY    DATE    DESCRIPTION                           *         
      *  -------- ---- -------- ------------------------------------  *         
      *  BP133907 JB8  20100611 CONVERT FROM BP13S038                 *         
      ******************************************************************        
                                                                                
                                                                                
      ***************************                                               
       ENVIRONMENT    DIVISION.                                                 
      ***************************                                               
                                                                                
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD38  ASSIGN        TO BP13FD38.                          
           SELECT BP13FD00  ASSIGN        TO BP13FD00.                          
           SELECT BP13KA10  ASSIGN        TO BP13KA10                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KA10-KEY-FLD                       
                            FILE STATUS   IS WS-KA10-STATUS.                    
           SELECT P13FD38A  ASSIGN        TO P13FD38A.                          
                                                                                
      *******************                                                       
       DATA DIVISION.                                                           
      *******************                                                       
                                                                                
      *============*                                                            
       FILE SECTION.                                                            
      *============*                                                            
                                                                                
       FD   BP13FD38                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 400  CHARACTERS.                                    
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13FD00                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13KA10                                                            
            RECORD CONTAINS 800  CHARACTERS.                                    
       COPY BP13KA10.                                                           
                                                                                
       FD  P13FD38A                                                             
           RECORDING MODE   IS   F                                              
           RECORD CONTAINS  400  CHARACTERS.                                    
       01  P13FD38A-REC     PIC  X(400).                                        
                                                                                
      *=======================*                                                 
       WORKING-STORAGE SECTION.                                                 
      *=======================*                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-FD38-READ              PIC 9(8)   VALUE ZEROES.              
           05  CNT-FD00-READ              PIC 9(8)   VALUE ZEROES.              
           05  CNT-FD38A-WRITE            PIC 9(8)   VALUE ZEROES.              
           05  CNT-FD00-NOTFND            PIC 9(8)   VALUE ZEROES.              
           05  WS-KA10-STATUS             PIC 99     VALUE ZEROES.              
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 3000-MAIN-PROCESS        THRU 3000-EXIT                      
             UNTIL FD38-NUM-REGN = HIGH-VALUES                                  
               AND FD00-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD38                                                 
                       BP13FD00                                                 
                       BP13KA10                                                 
                OUTPUT P13FD38A.                                                
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 - OPEN ERROR '                                  
              MOVE WS-KA10-STATUS               TO RETURN-CODE                  
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           PERFORM 8001-READ-BP13FD38       THRU 8001-EXIT.                     
           PERFORM 8002-READ-BP13FD00       THRU 8002-EXIT.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           IF FD38-NUM-REGN = FD00-NUM-REGN                                     
              MOVE FD00-NUM-PROJECT           TO KA10-NUM-PRJCT                 
              PERFORM 8003-READ-BP13KA10    THRU 8003-EXIT                      
              MOVE KA10-NME-PRJCT          TO BP13FD38-TRANS-REC(329:30)        
              WRITE P13FD38A-REC FROM BP13FD38-TRANS-REC                        
              ADD 1                           TO CNT-FD38A-WRITE                
              PERFORM 8001-READ-BP13FD38    THRU 8001-EXIT                      
           ELSE                                                                 
              IF FD38-NUM-REGN < FD00-NUM-REGN                                  
                 MOVE SPACES               TO BP13FD38-TRANS-REC(329:30)        
                 WRITE P13FD38A-REC FROM BP13FD38-TRANS-REC                     
                 ADD 1                        TO CNT-FD38A-WRITE                
                 ADD 1                        TO CNT-FD00-NOTFND                
                 PERFORM 8001-READ-BP13FD38 THRU 8001-EXIT                      
              ELSE                                                              
                 PERFORM 8002-READ-BP13FD00 THRU 8002-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8001-READ-BP13FD38.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD38                                                        
             AT END                                                             
                MOVE HIGH-VALUES        TO FD38-NUM-REGN                        
                GO                      TO 8001-EXIT                            
           END-READ.                                                            
                                                                                
           ADD 1                        TO CNT-FD38-READ.                       
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8002-READ-BP13FD00.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD00                                                        
             AT END                                                             
                MOVE HIGH-VALUES        TO FD00-NUM-REGN                        
                GO                      TO 8002-EXIT                            
           END-READ.                                                            
                                                                                
           ADD 1                        TO CNT-FD00-READ.                       
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8003-READ-BP13KA10.                                                      
      ******************************************************************        
                                                                                
           READ BP13KA10.                                                       
                                                                                
           IF WS-KA10-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KA10-STATUS = 23                                            
                 MOVE 'INVALID PROJECT'   TO KA10-NME-PRJCT                     
                 DISPLAY 'INVALID PROJECT - ' KA10-NUM-PRJCT                    
              ELSE                                                              
                 DISPLAY 'BP13KA10 - READ ERROR (' WS-KA10-STATUS ')'           
                 MOVE WS-KA10-STATUS      TO RETURN-CODE                        
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY '                                           '.               
           DISPLAY '*****     BP13CC35 CONTROL LISTING    *****'.               
           DISPLAY '                                           '.               
           DISPLAY 'NO OF RECORDS READ FROM BP13FD38 : ' CNT-FD38-READ          
           DISPLAY 'NO OF RECORDS READ FROM BP13FD00 : ' CNT-FD00-READ          
           DISPLAY 'NO OF RECORDS WRITE TO  P13FD38A : ' CNT-FD38A-WRITE        
                                                                                
           CLOSE BP13FD38                                                       
                 BP13FD00                                                       
                 BP13KA10                                                       
                 P13FD38A.                                                      
                                                                                
           IF WS-KA10-STATUS NOT = 00                                           
              DISPLAY 'BP13KA10 - CLOSE ERROR '                                 
              MOVE WS-KA10-STATUS            TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP  RUN.                                                           
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
