       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C718.                                                 
       AUTHOR. BALA AMUTHAN.                                                    
       DATE-WRITTEN.  19/03/2007.                                               
                                                                                
      *****************************************************************         
      *                                                               *         
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                         *         
      *                                                               *         
      *    SYSTEM ID : BP13                                           *         
      *                                                               *         
      *    OBJECTIVE  : TO MATCH THE RECORDS IN BP13F764 WITH THE     *         
      *                 RECORD IN BP13F757.                           *         
      *               : THOSE WITH THE SAME SALE-MODE WILL BE THE     *         
      *                 RECORDS TO BE SHORTLISTED.                    *         
      *               : UPDATE BP13K764                               *         
      *                                                               *         
      *     INPUT FILES:                                              *         
      *           1. BP13F764                                         *         
      *           2. BP13F757                                         *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *           2. BP13K764                                         *         
      *                                                               *         
      *----------------------------------------------------------------         
      * REF NO     DATE    BY   AMENDMENTS/ENHANCEMENTS               *         
      * -------    ----    ---  -----------------------               *         
      * BP133030   190307  BA2  NEW PROG.                             *         
      * BP133017   270407  RB12 ADD CHECKING OF K757-DTE-REQUESTED    *         
      * BP133129   180607  RB12 REMOVE PREFIX FOR WIC                 *         
      * BP133193   121107  RB12 ADD EVALUATION OF K764 STATUS ON READ *         
      * BP133430   100908  LMS1 NEW SELECTION TYPE WIH                *         
      * BP133071   061008  RB12 ADD LAST QUEUE FOR 3R SA              *         
      * BP133430   151008  RB12 CATER FOR NEW PREFIX FOR WIH          *         
      * BP139280   161123  EAA2 EXPAND BP13F757 FROM 3000 TO 4000     *         
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F764  ASSIGN TO BP13F764.                                 
                                                                                
           SELECT BP13K764  ASSIGN                TO  BP13K764                  
                            ACCESS                IS  RANDOM                    
                            ORGANIZATION          IS  INDEXED                   
                            RECORD KEY            IS  K764-KEY-FLD              
                            FILE STATUS           IS  WS-K764-STATUS.           
                                                                                
           SELECT BP13F757  ASSIGN TO BP13F757.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K764                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K764.                                                           
                                                                                
       FD  BP13F764                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F764.                                                           
                                                                                
       FD  BP13F757                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 4000  CHARACTERS                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F757.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-K764-STATUS                 PIC 9(2)    VALUE ZEROES.             
       01  WS-CNT-F764-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F757-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K764-WRIT               PIC 9(5)    VALUE ZEROES.             
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
       01  WS-TOTAL-CNT                   PIC 9(5)    VALUE ZEROES.             
       01  WS-TOTAL-CNT-SA                PIC 9(5)    VALUE ZEROES.             
       01  WS-NUM-FINAL-Q                 PIC X(5)    VALUE SPACES.             
       01  WS-PREFIX                      PIC X(1)    VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-F764                THRU 100-EXIT.                  
           PERFORM 200-READ-F757                THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
               UNTIL F757-NUM-SALES-MODE = HIGH-VALUES.                         
           PERFORM 400-PROCESS-WRITE            THRU 400-EXIT.                  
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       000-OPEN-FILE.                                                           
           OPEN INPUT  BP13F764                                                 
                       BP13F757                                                 
                I-O    BP13K764.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F764                         *              
      ************************************************************              
                                                                                
      ************************************************************              
       100-READ-F764.                                                           
      ************************************************************              
                                                                                
           READ BP13F764                                                        
                AT END MOVE HIGH-VALUE  TO F764-NUM-SALES-MODE                  
                GO TO 100-EXIT.                                                 
           ADD  1                      TO WS-CNT-F764-READ.                     
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F757                         *              
      ************************************************************              
                                                                                
      ************************************************************              
       200-READ-F757.                                                           
      ************************************************************              
           READ BP13F757                                                        
                AT END MOVE HIGH-VALUE  TO F757-NUM-SALES-MODE                  
                GO TO 200-EXIT.                                                 
           ADD  1                       TO WS-CNT-F757-READ.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
       300-PROCESS-SEQUENTIAL-FILES.                                            
      ************************************************************              
                                                                                
           IF ((F764-NUM-SALES-MODE = F757-NUM-SALES-MODE) AND                  
               (F764-DTE-BALLOT = F757-DTE-BALLOT) AND                          
               (F764-DTE-SALES-START <= F757-DTE-REQUESTED))                    
                ADD 1 TO WS-TOTAL-CNT                                           
                IF F757-NUM-FLAT-TYPE = '1A' OR '2A'                            
                   ADD 1 TO WS-TOTAL-CNT-SA                                     
                END-IF                                                          
                PERFORM 200-READ-F757         THRU 200-EXIT                     
           ELSE                                                                 
                PERFORM 200-READ-F757         THRU 200-EXIT                     
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       400-PROCESS-WRITE.                                                       
      ************************************************************              
           EVALUATE F764-NUM-SALES-MODE                                         
            WHEN '3R '                                                          
              MOVE 'R'          TO WS-PREFIX                                    
            WHEN OTHER                                                          
              MOVE SPACES       TO WS-PREFIX                                    
           END-EVALUATE.                                                        
                                                                                
           MOVE  F764-NUM-SALES-MODE TO K764-NUM-SALES-MODE.                    
           READ  BP13K764.                                                      
                                                                                
           EVALUATE WS-K764-STATUS                                              
           WHEN 00                                                              
                CONTINUE                                                        
           WHEN 23                                                              
                DISPLAY 'BP13K764 - REC NOT FOUND  ' K764-KEY-FLD               
                MOVE    WS-K764-STATUS       TO   RETURN-CODE                   
           WHEN OTHER                                                           
                DISPLAY 'BP13K764 - READ ERROR FOR ' K764-KEY-FLD               
                DISPLAY 'FILE STATUS IS ' WS-K764-STATUS                        
                MOVE    WS-K764-STATUS       TO   RETURN-CODE                   
                PERFORM 1000-CLOSE-ROUTINE   THRU 1000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           IF F764-NUM-SALES-MODE = '3R '                                       
              COMPUTE WS-TOTAL-CNT = WS-TOTAL-CNT - WS-TOTAL-CNT-SA             
           END-IF.                                                              
                                                                                
           MOVE WS-TOTAL-CNT    TO K764-NUM-APPLICATION.                        
           MOVE WS-TOTAL-CNT-SA TO K764-NUM-APPLICATION-SA.                     
                                                                                
           ADD K764-NUM-SETASIDE-QUEUE TO WS-TOTAL-CNT.                         
                                                                                
           IF K764-NUM-SALES-MODE = 'WIH'                                       
              MOVE WS-TOTAL-CNT TO WS-NUM-FINAL-Q                               
           ELSE                                                                 
              STRING   WS-PREFIX                                                
                       WS-TOTAL-CNT (2:4)                                       
                       DELIMITED BY SIZE                                        
                       INTO    WS-NUM-FINAL-Q                                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-FINAL-Q  TO K764-LAST-QUEUE.                             
                                                                                
           IF K764-NUM-SALES-MODE = '3R '                                       
              ADD K764-NUM-SETASIDE-QUEUE-SA TO WS-TOTAL-CNT-SA                 
              STRING   'S'                                                      
                       WS-TOTAL-CNT-SA (2:4)                                    
                       DELIMITED BY SIZE                                        
                       INTO    WS-NUM-FINAL-Q                                   
              END-STRING                                                        
              MOVE WS-NUM-FINAL-Q            TO K764-LAST-QUEUE-SA              
           END-IF.                                                              
                                                                                
           REWRITE BP13K764-REC.                                                
           ADD +1               TO WS-CNT-K764-WRIT.                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13C718 *************'.                            
           DISPLAY 'NO OF BP13K764 READ    : '                                  
                                             WS-CNT-F764-READ.                  
           DISPLAY 'NO OF BP13F757 READ    : '                                  
                                             WS-CNT-F757-READ.                  
           DISPLAY 'NO OF BP13K764 UPDATED : '                                  
                                             WS-CNT-K764-WRIT.                  
                                                                                
           CLOSE BP13F757                                                       
                 BP13F764                                                       
                 BP13K764.                                                      
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
