       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C146.                                                 
      *AUTHOR.        CKK7                                                      
      *DATE-WRITTEN.  20MAY2003.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. UPDATE OF OIC FILE.                              *                
      *    2. TO ASSIGN REGN TO OIC'S FOR WIS APPLICATION      *                
      *       APPLIED FFOR THE DAY.                            *                
      *                                                        *                
      *    INPUT FILES  : BP13F800 - TRANSACTION FILE          *                
      *    I-O            BP13K022 - OIC FILE                  *                
      *                   BP13K025 - CONTROL FILE              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP132352 CKK 20030502  NEW PGM                          *                
      *BP132432 MJ1620031021  REMOVED PROCESSING FOR NON-BANK  *                
      *                       LOAN CASES                       *                
      *BP13     JB8 20080723  TO CATER FOR NEW FIELD IN        *                
      *                       BP13K025(K025-NUM-PRJCT)         *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K025 ASSIGN TO BP13K025                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K025-KEY-FLD                               
                  FILE STATUS     IS WS-K025-STATUS.                            
                                                                                
           SELECT P13L146A ASSIGN TO P13L146A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K025                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K025.                                                           
                                                                                
       FD   P13L146A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13L146A-REC    PIC X(132).                                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F800-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K022-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K025-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K022-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K025-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-CNT-READ-F800               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRIT-L146               PIC 9(6)  VALUE ZEROS.            
           05  WS-SKIPPED                     PIC 9(6)  VALUE ZEROS.            
                                                                                
       01  WS-L146-CONTROLS.                                                    
           05  WS-L146-LINECTR-A              PIC 9(2)  VALUE 60.               
           05  WS-L146-PAGECTR-A              PIC 9(5)  VALUE ZEROS.            
           05  WS-L146-SNO-A                  PIC 9(5)  VALUE 0.                
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-BLANK-LINE                  PIC X(132) VALUE SPACES.          
           05  WS-REGN-ALLOC-CTR              PIC 9(6)  VALUE ZEROS.            
           05  WS-PREV-OIC                    PIC X(5).                         
           05  WS-ALLO-CAT-PREV               PIC X(1).                         
           05  WS-CNT                         PIC 9(3)  VALUE 0.                
           05  WS-SKIP                        PIC 9(1)  VALUE 0.                
           05  WS-FLAG                        PIC X(1)  VALUE 'N'.              
           05  WS-DONE                        PIC X(1)  VALUE 'N'.              
           05  WS-READ-FLAG-BL                PIC X(1)  VALUE 'N'.              
           05  WS-FOUND                       PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      *============================================================*            
      *         BP13L146  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L146-HEADER-1A.                                                      
           05  FILLER              PIC X(08)     VALUE 'P13L146A'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L146-RUN-DATE-A     PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L146-PAGE-NO-A      PIC ZZZZ9 .                                  
                                                                                
       01  L146-HEADER-2A.                                                      
           05  FILLER              PIC X(41)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
           '     REPORT ON DBSS CASES ASSIGNED TO CSM        '.                 
                                                                                
                                                                                
       01  L146-HEADER-03.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'NT '.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'ZONE'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'FT '.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGNO   '.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'REGN DATE '.            
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'OIC  '.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(30)     VALUE 'OIC NAME'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'ALLOC CAT'.             
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'LOAN TAG'.              
                                                                                
       01  L146-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L146-DETAIL.                                                         
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L146-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L146-NT             PIC X(03).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L146-ZONE           PIC X(04).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L146-FT             PIC X(03).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L146-REGNO          PIC X(08).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L146-DTE-REGN       PIC X(10).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L146-OIC            PIC X(05).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L146-NME-OIC        PIC X(30).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L146-ALLO-CAT       PIC X(03).                                   
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  L146-LOAN-TAG       PIC X(03).                                   
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-F800      THRU 3000-EXIT                        
                  UNTIL WS-F800-EOF-SW = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F800                                                 
                I-O    BP13K022                                                 
                       BP13K025                                                 
                OUTPUT P13L146A.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K025-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K025 OPEN ERROR, STATUS IS ' WS-K025-STATUS          
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
           MOVE   WS-EDITED-DATE      TO L146-RUN-DATE-A.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F800.                                                          
      ****************************************************************          
                                                                                
           READ BP13F800                                                        
              AT END MOVE 'Y' TO WS-F800-EOF-SW                                 
              GO TO 3000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-F800.                          
                                                                                
           MOVE 'N'               TO WS-FOUND.                                  
                                                                                
           PERFORM 2100-READ-K021 THRU 2100-EXIT.                               
                                                                                
           IF WS-FOUND = 'N'                                                    
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1 TO WS-SKIPPED                                               
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-K021.                                                          
      ****************************************************************          
                                                                                
           MOVE LOW-VALUES           TO K022-KEY-FLD.                           
           MOVE F800-NUM-REGN        TO K022-NUM-REGN.                          
                                                                                
           START BP13K022 KEY >= K022-NUM-REGN.                                 
                                                                                
           IF WS-K022-STATUS = 00 OR 02                                         
              READ BP13K022 NEXT RECORD                                         
              IF WS-K022-STATUS = 00 OR 02                                      
                 IF K022-NUM-REGN = F800-NUM-REGN                               
                    MOVE 'Y' TO WS-FOUND                                        
                 ELSE                                                           
                    MOVE 'N' TO WS-FOUND                                        
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
           IF WS-K022-STATUS = 23                                               
              MOVE 'N' TO WS-FOUND                                              
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR WITH START READ OF BP13K022. STATUS IS '           
                       WS-K022-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2500-READ-K025.                                                          
      ****************************************************************          
                                                                                
           MOVE  LOW-VALUES                    TO K025-KEY-FLD.                 
           MOVE  F800-NUM-DBSS-PRJCT           TO K025-NUM-PRJCT.               
           MOVE  'N'                           TO WS-FLAG.                      
           START BP13K025 KEY >  K025-KEY-FLD.                                  
                                                                                
           IF WS-K025-STATUS = 00                                               
              PERFORM UNTIL WS-FLAG = 'Y'                                       
                 READ BP13K025 NEXT RECORD                                      
                      AT END MOVE 'Y'       TO WS-FLAG                          
                 END-READ                                                       
                 IF WS-K025-STATUS = 00        AND                              
                    K025-NUM-PRJCT = F800-NUM-DBSS-PRJCT                        
                    IF K025-CDE-LAST-REC-BL = 'Y'                               
                       MOVE 'N'             TO K025-CDE-LAST-REC-BL             
                       PERFORM 5100-REWRITE-K025 THRU 5100-EXIT                 
                       MOVE 'Y'       TO WS-FLAG                                
                    END-IF                                                      
                 ELSE                                                           
                    DISPLAY 'ERROR READING CURRENT OIC IN BP13K025'             
                    MOVE 99                      TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              DISPLAY 'ERROR WITH START READ OF BP13K025. STATUS IS '           
                       WS-K025-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F800.                                                       
      ****************************************************************          
                                                                                
           PERFORM 2000-READ-F800    THRU 2000-EXIT.                            
           PERFORM 3100-PROCESS-BANKLOAN  THRU 3100-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-PROCESS-BANKLOAN.                                                   
      ****************************************************************          
                                                                                
           PERFORM 2500-READ-K025  THRU 2500-EXIT.                              
                                                                                
           MOVE  0                    TO WS-CNT.                                
           MOVE 'N'                   TO WS-FLAG.                               
                                                                                
           PERFORM UNTIL WS-FLAG = 'Y'                                          
           PERFORM 4500-READNEXT-K025  THRU 4500-EXIT                           
           IF WS-K025-EOF-SW = 'Y'                                              
              ADD 1                      TO WS-CNT                              
              MOVE  LOW-VALUES           TO K025-KEY-FLD                        
              MOVE  F800-NUM-DBSS-PRJCT  TO K025-NUM-PRJCT                      
              START BP13K025 KEY >  K025-KEY-FLD                                
              PERFORM 4500-READNEXT-K025 THRU 4500-EXIT                         
              MOVE  'N'                  TO WS-K025-EOF-SW                      
              IF WS-DONE = 'Y'                                                  
                 MOVE  'N'               TO WS-DONE                             
                 WRITE P13L146A-REC    FROM WS-BLANK-LINE                       
                 ADD 1                 TO WS-L146-LINECTR-A                     
              END-IF                                                            
           END-IF                                                               
              IF K025-CDE-BLOCK NOT = 'Y'                                       
                 IF K025-NUM-RATIO = K025-NUM-CUR-CNT                           
                    SUBTRACT K025-NUM-RATIO FROM K025-NUM-MAX-CNT               
                             GIVING WS-SKIP                                     
                    IF WS-SKIP <= K025-NUM-SKIP                                 
                       MOVE 1                     TO K025-NUM-CUR-CNT           
                       MOVE ZEROES                TO K025-NUM-SKIP              
                       MOVE 'Y'           TO K025-CDE-LAST-REC-BL               
                       PERFORM 5100-REWRITE-K025  THRU 5100-EXIT                
                       PERFORM 5000-WRITE-K022    THRU 5000-EXIT                
                       MOVE 'Y'                   TO WS-FLAG                    
                       MOVE 'Y'                   TO WS-DONE                    
                    ELSE                                                        
                       ADD  1                     TO K025-NUM-SKIP              
                       PERFORM 5100-REWRITE-K025  THRU 5100-EXIT                
                    END-IF                                                      
                 ELSE                                                           
                    ADD   1                    TO K025-NUM-CUR-CNT              
                    MOVE 'Y'              TO K025-CDE-LAST-REC-BL               
                    PERFORM 5000-WRITE-K022    THRU 5000-EXIT                   
                    PERFORM 5100-REWRITE-K025  THRU 5100-EXIT                   
                    MOVE 'Y'                   TO WS-FLAG                       
                    MOVE 'Y'                   TO WS-DONE                       
                 END-IF                                                         
              END-IF                                                            
              IF WS-CNT > 1                                                     
                 MOVE 'Y'                         TO WS-FLAG                    
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-CNT > 1                                                        
              DISPLAY 'ERROR: ALL OIC''S MARKED AS BLOCK.'                      
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4500-READNEXT-K025.                                                      
      ****************************************************************          
                                                                                
            READ BP13K025 NEXT RECORD                                           
               AT END MOVE 'Y' TO WS-K025-EOF-SW                                
               GO TO 4500-EXIT                                                  
            END-READ.                                                           
                                                                                
            IF WS-K025-STATUS = 00                                              
               IF K025-NUM-PRJCT = F800-NUM-DBSS-PRJCT                          
                  CONTINUE                                                      
               ELSE                                                             
                  MOVE 'Y'                    TO WS-K025-EOF-SW                 
               END-IF                                                           
            ELSE                                                                
            IF WS-K025-STATUS = 46                                              
               MOVE 'Y'                       TO WS-K025-EOF-SW                 
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K025. STATUS IS '                     
                        WS-K025-STATUS                                          
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-WRITE-K022.                                                         
      ****************************************************************          
                                                                                
            MOVE K025-NUM-OIC              TO K022-NUM-OIC.                     
            MOVE F800-NUM-REGN             TO K022-NUM-REGN.                    
            MOVE F800-NUM-ALLO-CAT         TO K022-CDE-ALLO-CAT.                
            MOVE F800-NUM-LOAN-TAG         TO K022-CDE-BANKLOAN.                
            MOVE 'BP13C146'                TO K022-NUM-USERID                   
            MOVE WS-SYSTEM-DATE            TO K022-DTE-UPDATE.                  
                                                                                
            WRITE BP13K022-REC.                                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
            WHEN 00                                                             
               ADD 1 TO WS-REGN-ALLOC-CTR                                       
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K022. STATUS IS '                     
                        WS-K022-STATUS                                          
               PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
            END-EVALUATE.                                                       
                                                                                
            PERFORM 8000-PRINT-RPT-A    THRU 8000-EXIT.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5100-REWRITE-K025.                                                       
      ****************************************************************          
                                                                                
            MOVE 'BP13C146'            TO K025-NUM-USERID.                      
            MOVE WS-SYSTEM-DATE        TO K025-DTE-UPDATE.                      
                                                                                
            REWRITE BP13K025-REC.                                               
                                                                                
            EVALUATE WS-K025-STATUS                                             
            WHEN 00                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K025. STATUS IS '                     
                        WS-K025-STATUS                                          
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       8000-PRINT-RPT-A.                                                        
      ****************************************************************          
                                                                                
           ADD  1                             TO WS-L146-SNO-A.                 
           MOVE WS-L146-SNO-A                 TO L146-SNO.                      
                                                                                
           PERFORM 8500-MOVE-VALUES     THRU 8500-EXIT.                         
                                                                                
           IF WS-L146-LINECTR-A >= 55                                           
              ADD                   1   TO WS-L146-PAGECTR-A                    
              MOVE WS-L146-PAGECTR-A    TO L146-PAGE-NO-A                       
              WRITE P13L146A-REC    FROM L146-HEADER-1A  AFTER PAGE             
              WRITE P13L146A-REC    FROM L146-HEADER-2A                         
              WRITE P13L146A-REC    FROM L146-HEADER-03  AFTER 2                
              WRITE P13L146A-REC    FROM L146-HEADER-04                         
              WRITE P13L146A-REC    FROM L146-DETAIL                            
              MOVE                  6 TO WS-L146-LINECTR-A                      
           ELSE                                                                 
              ADD                   1 TO WS-L146-LINECTR-A                      
              WRITE P13L146A-REC    FROM L146-DETAIL                            
           END-IF.                                                              
                                                                                
           ADD 1                    TO WS-CNT-WRIT-L146.                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       8500-MOVE-VALUES.                                                        
      ****************************************************************          
                                                                                
           MOVE F800-NUM-NEW-TOWN             TO L146-NT.                       
           MOVE F800-NUM-ZONE                 TO L146-ZONE                      
           MOVE F800-NUM-FLAT-TYPE            TO L146-FT.                       
           STRING F800-DTE-REGN(7:2) '/'                                        
                  F800-DTE-REGN(5:2) '/'                                        
                  F800-DTE-REGN(1:4)                                            
                  DELIMITED BY SIZE         INTO L146-DTE-REGN.                 
           MOVE F800-NUM-REGN                 TO L146-REGNO.                    
           MOVE K022-NUM-OIC                  TO L146-OIC.                      
           MOVE K025-NME-OIC                  TO L146-NME-OIC.                  
           MOVE F800-NUM-ALLO-CAT             TO L146-ALLO-CAT.                 
           MOVE F800-NUM-LOAN-TAG             TO L146-LOAN-TAG.                 
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F800                                                       
                 BP13K025                                                       
                 BP13K022                                                       
                 P13L146A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C146 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F800 RECORDS READ        = ' WS-CNT-READ-F800.          
           DISPLAY 'NO OF REGNO ALLOCATED TO OIC = ' WS-REGN-ALLOC-CTR.         
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF SKIPPED CASES          = ' WS-SKIPPED.                
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
