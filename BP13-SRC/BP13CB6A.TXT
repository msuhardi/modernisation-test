      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB6A.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  11 APRIL 2016.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM CREATES HOUSING GRANT CREDITING*           
      *                 FILE                                        *           
      *                                                             *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F633 -- VOUCHER TRANSACTION FILE                  *           
      *   2.  BP13KD05 -- SOC GRANT FILE                            *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  AB02F930 -- HDB GRANT FILE                            *           
      *   2.  B02F930A -- HDB GRANT FILE - DAILY                    *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136182 11/04/2016 FNP1 NEW PROGRAM                        *           
      * BP136181 25/04/2016 FNP1 FOR AHG/SHG GRANT, MOVE Y TO CHANNEL           
      *                          TAG IF THRESHOLD AMT NOT = 0, UPDATE           
      *                          KD05-NUM-CPF-SEND-STATUS TO P      *           
      * BP136181 25/04/2016 FNP1 ADD AHG AND SHG GRANT IF SAME UIN  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD05 ASSIGN TO BP13KD05                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD05-KEY-FLD                               
                  FILE STATUS     IS BP13KD05-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13F633   ASSIGN       TO BP13F633.                          
                                                                                
           SELECT AB02F930   ASSIGN       TO AB02F930.                          
           SELECT B02F930A   ASSIGN       TO B02F930A.                          
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD05                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F633                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  450 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F633.                                                           
                                                                                
       FD   AB02F930                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AB02F930.                                                   00055007
                                                                                
       FD   B02F930A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  B02F930A-REC         PIC X(150).                             00055007
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-F633-EOF                PIC X       VALUE SPACE.              
           05  WS-KD05-EOF                PIC X       VALUE SPACE.              
           05  WS-KD05-FND                PIC X       VALUE SPACE.              
           05  BP13KD05-STATUS            PIC 9(2)    VALUE ZEROES.             
           05  BP13K800-STATUS            PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT                     PIC 9       VALUE ZEROES.             
           05  WS-CTR                     PIC 9       VALUE ZEROES.             
           05  CNT-F633-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-KD05-FND               PIC 9(5)    VALUE ZEROES.             
           05  CNT-KD05-UPDATED           PIC 9(5)    VALUE ZEROES.             
           05  CNT-K800-FND               PIC 9(5)    VALUE ZEROES.             
           05  WS-WRITE-CNT-F930A         PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-DETAIL-F930   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-TRAILER-F930  PIC 9(5)    VALUE ZEROS.              
           05  WS-CNT-REC                 PIC 9(5)    VALUE ZEROS.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SCH-ACC                 PIC X(11).                            
           05  WS-AMT-THRESHOLD           PIC 9(7)V99.                          
           05  WS-UIN-GRANT OCCURS 4 TIMES.                                     
               10  WS-NUM-CPF             PIC X(9).                             
               10  WS-AMT-CPF-GRANT       PIC 9(6).                             
               10  WS-CHANNEL-TAG         PIC X(1).                             
                                                                                
       01  WS-DATE                        PIC X(08)   VALUE SPACES.             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           02  WS-CURRENT-DATE1.                                                
               05  WS-DTE-CCYY            PIC X(4).                             
               05  WS-DTE-MM              PIC X(2).                             
               05  WS-DTE-DD              PIC X(2).                             
           02  WS-CURRENT-TIME            PIC X(9).                             
           02  FILLER                     PIC X(4).                             
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F633    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F633-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F633                                                 
                       BP13K800                                                 
                I-O    BP13KD05                                                 
                OUTPUT AB02F930                                                 
                       B02F930A.                                                
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD05 : ' BP13KD05-STATUS             
              MOVE BP13KD05-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F633.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F633 AT END                                                 
                MOVE HIGH-VALUES TO BP13F633-REC                                
                MOVE 'Y' TO WS-F633-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F633-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           MOVE ZEROES       TO WS-CNT.                                         
           MOVE SPACES       TO WS-SCH-ACC.                                     
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                  
              MOVE SPACES    TO WS-NUM-CPF(WS-CTR)                              
                                WS-CHANNEL-TAG(WS-CTR)                          
              MOVE ZEROES    TO WS-AMT-CPF-GRANT(WS-CTR)                        
           END-PERFORM.                                                         
                                                                                
           IF F633-NUM-REGN(1:1) NOT = '5' AND '6'                              
              PERFORM 3100-PROCESS-REC     THRU 3100-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F633    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3500-READ-BP13K800   THRU 3500-EXIT.                         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 2                  
              IF F633-NUM-CPF-SUG(WS-CTR) NOT = SPACES                          
                 IF F633-AMT-CPF-GRANT-SUG(WS-CTR) NOT NUMERIC                  
                    MOVE ZEROES TO F633-AMT-CPF-GRANT-SUG(WS-CTR)               
                 END-IF                                                         
                 IF F633-AMT-CPF-GRANT-SUG(WS-CTR) NOT = ZEROES                 
                    ADD 1 TO WS-CNT                                             
                    MOVE F633-NUM-CPF-SUG(WS-CTR)   TO                          
                                              WS-NUM-CPF(WS-CNT)                
                    MOVE F633-AMT-CPF-GRANT-SUG(WS-CTR)   TO                    
                                              WS-AMT-CPF-GRANT(WS-CNT)          
                    MOVE 'N'              TO  WS-CHANNEL-TAG(WS-CNT)            
                 END-IF                                                         
              END-IF                                                            
              IF F633-NUM-CPF-PAYEE(WS-CTR) NOT = SPACES                        
                 IF F633-AMT-CPF-GRANT-PAYEE(WS-CTR) NOT NUMERIC                
                    MOVE ZEROES TO F633-AMT-CPF-GRANT-PAYEE(WS-CTR)             
                 END-IF                                                         
                 IF F633-AMT-CPF-GRANT-PAYEE(WS-CTR) NOT = ZEROES               
                    ADD 1 TO WS-CNT                                             
                    MOVE F633-NUM-CPF-PAYEE(WS-CTR) TO                          
                                              WS-NUM-CPF(WS-CNT)                
                    MOVE F633-AMT-CPF-GRANT-PAYEE(WS-CTR) TO                    
                                              WS-AMT-CPF-GRANT(WS-CNT)          
                    MOVE 'Y'              TO  WS-CHANNEL-TAG(WS-CNT)            
                 END-IF                                                         
              END-IF                                                            
              IF F633-NUM-CPF-SHG(WS-CTR) NOT = SPACES                          
                 IF F633-AMT-CPF-GRANT-SHG(WS-CTR) NOT NUMERIC                  
                    MOVE ZEROES TO F633-AMT-CPF-GRANT-SHG(WS-CTR)               
                 END-IF                                                         
                 IF F633-AMT-CPF-GRANT-SHG(WS-CTR) NOT = ZEROES                 
                    IF F633-NUM-CPF-SHG(WS-CTR) =                               
                                     F633-NUM-CPF-PAYEE(WS-CTR)                 
                       IF F633-AMT-CPF-GRANT-PAYEE(WS-CTR) = ZEROES             
                          ADD 1 TO WS-CNT                                       
                          MOVE F633-NUM-CPF-SHG(WS-CTR)   TO                    
                                              WS-NUM-CPF(WS-CNT)                
                          MOVE 'Y'           TO  WS-CHANNEL-TAG(WS-CNT)         
                       END-IF                                                   
                       ADD F633-AMT-CPF-GRANT-SHG(WS-CTR)   TO                  
                                              WS-AMT-CPF-GRANT(WS-CNT)          
                    ELSE                                                        
                       ADD 1 TO WS-CNT                                          
                       MOVE F633-NUM-CPF-SHG(WS-CTR)   TO                       
                                              WS-NUM-CPF(WS-CNT)                
                       MOVE F633-AMT-CPF-GRANT-SHG(WS-CTR)   TO                 
                                              WS-AMT-CPF-GRANT(WS-CNT)          
                       MOVE 'Y'              TO  WS-CHANNEL-TAG(WS-CNT)         
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > WS-CNT             
              PERFORM 3200-READ-BP13KD05      THRU 3200-EXIT                    
              PERFORM 7000-WRITE-DETAILS      THRU 7000-EXIT                    
           END-PERFORM.                                                         
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13KD05.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                       TO KD05-KEY-FLD.                   
           MOVE F633-NUM-REGN                TO KD05-NUM-REGN.                  
           MOVE  WS-NUM-CPF(WS-CTR)          TO KD05-NUM-NRIC.                  
                                                                                
           READ BP13KD05.                                                       
                                                                                
           IF BP13KD05-STATUS = 00 OR 02                                        
              IF KD05-AMT-THRESHOLD NOT NUMERIC                                 
                 MOVE ZEROES TO KD05-AMT-THRESHOLD                              
              END-IF                                                            
              MOVE KD05-AMT-THRESHOLD    TO WS-AMT-THRESHOLD                    
              PERFORM 5000-REWRITE-BP13KD05   THRU 5000-EXIT                    
              ADD 1 TO CNT-KD05-FND                                             
           ELSE                                                                 
              IF BP13KD05-STATUS = 23                                           
                 DISPLAY 'REGNO NOT FOUND IN BP13KD05 : ' KD05-NUM-REGN         
                 MOVE ZEROES  TO WS-AMT-THRESHOLD                               
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD05 : ' BP13KD05-STATUS          
                 MOVE BP13KD05-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3500-READ-BP13K800.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13K800-MASTER.                            
           MOVE F633-NUM-REGN    TO K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
           IF BP13K800-STATUS = 0                                               
              MOVE K800-NUM-SCH-ACC TO WS-SCH-ACC                               
              ADD 1 TO CNT-K800-FND                                             
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 MOVE SPACES    TO WS-SCH-ACC                                   
                 DISPLAY 'RECORD NOT FOUND IN BP13K800: ' K800-NUM-REGN         
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS          
                 MOVE BP13K800-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       5000-REWRITE-BP13KD05.                                                   
      *-----------------*                                                       
                                                                                
           MOVE 'P'               TO KD05-NUM-CPF-SEND-STATUS.                  
           MOVE 'BP13CB6A'        TO KD05-NUM-USERID.                           
           MOVE WS-CURRENT-DATE1  TO KD05-DTE-UPDATE.                           
           MOVE WS-CURRENT-TIME   TO KD05-TME-UPDATE.                           
                                                                                
           REWRITE BP13KD05-REC.                                                
                                                                                
           IF BP13KD05-STATUS = 00 OR 02                                        
              ADD 1 TO CNT-KD05-UPDATED                                         
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING- BP13KD05 : ' BP13KD05-STATUS             
              MOVE BP13KD05-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       7000-WRITE-DETAILS.                                                      
      *-----------------*                                                       
                                                                                
           MOVE SPACES           TO AB02F930-DETAIL-REC.                        
           INITIALIZE               AB02F930-DETAIL-REC.                        
                                                                                
           MOVE '1'                    TO F930-REC-TYPE-D.                      
           MOVE WS-NUM-CPF(WS-CTR)     TO F930-NUM-CPF-ACCT.                    
                                                                                
           IF WS-SCH-ACC = SPACES                                               
              MOVE SPACES              TO F930-NUM-PREFIX                       
           ELSE                                                                 
              MOVE '0'                 TO F930-NUM-PREFIX                       
           END-IF.                                                              
                                                                                
           MOVE WS-SCH-ACC             TO F930-NUM-HDB-REF.                     
                                                                                
           IF WS-CHANNEL-TAG(WS-CTR) = 'Y'                                      
              IF WS-AMT-THRESHOLD = ZEROES                                      
                 MOVE 'N'              TO F930-TAG-CHANNEL                      
              ELSE                                                              
                 MOVE 'Y'              TO F930-TAG-CHANNEL                      
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-CHANNEL-TAG(WS-CTR) TO F930-TAG-CHANNEL                   
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-CPF-GRANT(WS-CTR) TO F930-AMT-GRNT.                      
           MOVE WS-AMT-THRESHOLD       TO F930-AMT-THRSHOLD.                    
           MOVE '+'                    TO F930-SIGN-GRNT                        
                                          F930-SIGN-GRNT-EXCS.                  
           MOVE 'BP13'                 TO F930-CDE-SYSID.                       
           MOVE F633-NUM-REGN          TO F930-NUM-REGN.                        
                                                                                
           ADD 1 TO WS-CNT-REC.                                                 
                                                                                
           MOVE AB02F930-DETAIL-REC  TO B02F930A-REC.                           
           WRITE AB02F930-DETAIL-REC.                                           
           WRITE B02F930A-REC.                                                  
           ADD 1 TO WS-WRITE-CNT-DETAIL-F930.                                   
           ADD 1 TO WS-WRITE-CNT-F930A.                                         
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB6A *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13F633  : ' CNT-F633-READ.          
           DISPLAY 'NO OF RECS FOUND IN   BP13KD05  : ' CNT-KD05-FND.           
           DISPLAY 'NO OF RECS UPDATED IN BP13KD05  : ' CNT-KD05-UPDATED        
           DISPLAY 'NO OF RECS FOUND IN   BP13K800  : ' CNT-K800-FND.           
           DISPLAY 'NO OF RECS WRITTEN TO AB02F930  : '                         
                                               WS-WRITE-CNT-DETAIL-F930.        
           DISPLAY 'NO OF RECS WRITTEN TO B02F930A  : '                         
                                               WS-WRITE-CNT-F930A.              
           CLOSE BP13K800                                                       
                 BP13KD05                                                       
                 BP13F633                                                       
                 AB02F930                                                       
                 B02F930A.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD05-STATUS ' BP13KD05-STATUS          
            MOVE BP13KD05-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
