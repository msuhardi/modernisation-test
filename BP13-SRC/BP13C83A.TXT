       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C83A.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  15 NOV 2017.                                              
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO UPDATE FREEZE OCCUPIER TAG BASED ON ALLOC SCH*                
      *           AND RELATIONSHIP OF INDIVIDUAL REQUESTS :    *                
      *                                                        *                
      *    INPUT  FILE  : BP13F800 - REQUEST DATA ENTRY FILE   *                
      *                   BP13K060 - CODE TRANSCRIPTION FILE   *                
      *                                                        *                
      *    I/O    FILE  : BP13K820 - REQUEST OCCUPIER FILE     *                
      *                                                        *                
      *    OUTPUT FILES : NIL                                  *                
      *                                                        *                
      *    LISTINGS     : BP13L83A - REPORT ON OCCUPIERS AFTER *                
      *                               FREEZE OCCP TAG CHECK    *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ====== ================================== *                
      *BP137074 FNP1 151117 NEW PROGRAM                        *                
      *BP137074 FNP1 221118 FIXED PROBLEM OF RECORD UPDATED IN-*                
      *                     CORRECTLY                          *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS WS-K060-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K820-KEY-FLD                                  
                  FILE STATUS  IS WS-K820-STATUS.                               
                                                                                
           SELECT BP13L83A ASSIGN TO BP13L83A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25 CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
       FD   BP13L83A                                                            
            RECORDING MODE F                                                    
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L83A-REC    PIC X(132).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F800-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K060-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-K820-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  K820-EOF                       PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F800               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-UPD-K820                PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-READ-K820               PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-L83A-CONTROLS.                                                    
           05  WS-L83A-LINECTR                PIC 9(2)  VALUE 50.               
           05  WS-L83A-PAGECTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-L83A-S-NO                   PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-K820-ARRAY-VARS.                                                  
           05  WS-OCCUPIER                    OCCURS    16 TIMES.               
               10  WS-NRIC                    PIC X(09).                        
               10  WS-NAME                    PIC X(66).                        
               10  WS-RELN                    PIC X(02).                        
               10  WS-CTZN                    PIC X(10).                        
               10  WS-MS                      PIC X(01).                        
               10  WS-FRZ-TAG                 PIC X(02).                        
               10  WS-HA                      PIC X(01).                        
           05  WS-CNT                         PIC 9(2).                         
           05  WS-I                           PIC 9(2).                         
                                                                                
       01  WS-NUCLEUS-TAGS.                                                     
           05  WS-HA-GRAND                    PIC X(1) VALUE 'N'.               
           05  WS-HA-PARENT                   PIC X(1) VALUE 'N'.               
           05  WS-HA-SIBLING                  PIC X(1) VALUE 'N'.               
           05  WS-HA-CHILD                    PIC X(1) VALUE 'N'.               
           05  WS-HA1-MARRIED                 PIC X(1) VALUE 'N'.               
           05  WS-SC-SP-CHILD                 PIC X(1) VALUE 'N'.               
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-HA1                         PIC X(1) VALUE 'N'.               
           05  WS-HA2                         PIC X(1) VALUE 'N'.               
           05  WS-HA3                         PIC X(1) VALUE 'N'.               
           05  WS-HA4                         PIC X(1) VALUE 'N'.               
           05  WS-AGE                         PIC S9(3).                        
           05  WS-BIRTH-YY                    PIC 9(4).                         
           05  WS-RELATIONSHIP-SW             PIC X(02).                        
               88 APPLICANT   VALUE '00'.                                       
               88 PARENT VALUE '02' '12' '19' '22' '32' '38' '97' '98'.         
               88 SIBLING     VALUE '06' '07' '26' '27'.                        
               88 SPOUSE      VALUE '20' '10'.                                  
               88 FIANCEE     VALUE '09'.                                       
               88 ADP-CHILD   VALUE '39'.                                       
               88 CHILD       VALUE '41'.                                       
               88 GRANDPARENT VALUE '01' '21'.                                  
                                                                                
           05  WS-MARITAL-STAT-SW             PIC X(01).                        
               88 SINGLE      VALUE '1' 'S'.                                    
               88 MARRIED     VALUE '2' 'M'.                                    
               88 DIV-WID-SEP VALUE '3' '4' '5' 'W' 'D' 'P'.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      *============================================================*            
      *         BP13L83A  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L83A-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L83A'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L83A-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L83A-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  L83A-HEADER-02.                                                      
           05  FILLER              PIC X(43)     VALUE SPACES.                  
           05  FILLER              PIC X(47)     VALUE                          
           'REPORT ON OCCUPIERS AFTER FREEZE OCCP TAG CHECK'.                   
                                                                                
       01  L83A-HEADER-03.                                                      
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REG NO'.                
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'ALC SCH'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'ELG SCH'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'NT'.                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'FT'.                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CAT'.                   
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'APPL/'.                 
           05  FILLER              PIC X(42)     VALUE SPACES.                  
                                                                                
       01  L83A-HEADER-04.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(56)     VALUE 'NAME'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'OCC '.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'REL'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE 'RELATIONSHIP'.          
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'MS'.                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CIT'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'FRZ OCP TAG'.           
                                                                                
       01  L83A-HEADER-05.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L83A-DETAIL-01.                                                      
           05  L83A-S-NO           PIC ZZZZ9.                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L83A-REF-NO         PIC X(08).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L83A-ALLOC-SCH      PIC X(03).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L83A-ELIG-SCH       PIC X(03).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L83A-NT             PIC X(03).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L83A-FT             PIC X(02).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L83A-CAT            PIC X(03).                                   
                                                                                
       01  L83A-DETAIL-02.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L83A-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L83A-NAME           PIC X(56).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L83A-HA-OCCP        PIC X(04).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L83A-REL-CODE       PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L83A-REL-DESC       PIC X(20).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L83A-MARITAL-STAT   PIC X(01).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L83A-CITIZENSHIP    PIC X(02).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L83A-FRZ-OCCP-TAG   PIC X(02).                                   
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-F800         THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F800      THRU 3000-EXIT                        
             UNTIL WS-F800-EOF-SW = 'Y'.                                        
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K060                                                 
                I-O    BP13K820                                                 
                OUTPUT BP13L83A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 OPEN ERROR, STATUS IS ' WS-K060-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPEN ERROR, STATUS IS ' WS-K820-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           MOVE   WS-EDITED-DATE      TO L83A-RUN-DATE.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F800.                                                          
      ****************************************************************          
                                                                                
           READ BP13F800                                                        
              AT END MOVE 'Y'   TO WS-F800-EOF-SW.                              
                                                                                
           ADD  +1                 TO WS-CNT-READ-F800.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F800.                                                       
      ****************************************************************          
                                                                                
           MOVE ZEROS               TO WS-CNT.                                  
           MOVE SPACES              TO WS-NUCLEUS-TAGS.                         
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 16                     
              MOVE SPACES           TO WS-OCCUPIER(WS-I)                        
           END-PERFORM.                                                         
                                                                                
           PERFORM 3500-STARTBR-BP13K820  THRU 3500-EXIT.                       
                                                                                
           PERFORM 4000-PROCESS-NUCLEUS   THRU 4000-EXIT                        
              VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-CNT.                     
                                                                                
           PERFORM 5150-TAG-FREEZE-OCCP   THRU 5150-EXIT                        
              VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-CNT.                     
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-CNT                 
              IF WS-FRZ-TAG(WS-I) = '77'                                        
                 PERFORM 5200-READ-BP13K820  THRU 5200-EXIT                     
                 PERFORM 5300-PRINT-L83A     THRU 5300-EXIT                     
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM 2000-READ-F800         THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3500-STARTBR-BP13K820.                                                   
      ****************************************************************          
                                                                                
           MOVE F800-NUM-REGN      TO K820-NUM-REGN.                            
           MOVE SPACES             TO K820-NUM-NRIC                             
                                      K820-EOF.                                 
                                                                                
           START BP13K820 KEY > K820-KEY-FLD.                                   
           IF WS-K820-STATUS = 0                                                
              PERFORM 3600-READ-NEXT-K820  THRU 3600-EXIT                       
              PERFORM 3700-GET-OCCUPIERS   THRU 3700-EXIT                       
                UNTIL (K820-NUM-REGN NOT = F800-NUM-REGN) OR                    
                       K820-EOF = 'Y'                                           
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY ' RECORD NOT FOUND IN BP13K820, KEY ' K820-KEY-FLD        
           ELSE                                                                 
              DISPLAY ' ERROR READING BP13K820, STATUS ' WS-K820-STATUS         
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3600-READ-NEXT-K820.                                                     
      ****************************************************************          
                                                                                
           READ BP13K820 NEXT AT END                                            
                MOVE 'Y' TO K820-EOF                                            
                GO TO 3600-EXIT.                                                
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3700-GET-OCCUPIERS.                                                      
      ****************************************************************          
                                                                                
           IF K820-NUM-REGN = F800-NUM-REGN                                     
              ADD 1   TO WS-CNT                                                 
              IF K820-NUM-NRIC = F800-NUM-NRIC1 OR                              
                                 F800-NUM-NRIC2 OR                              
                                 F800-NUM-NRIC3 OR                              
                                 F800-NUM-NRIC4                                 
                 MOVE 'Y'    TO WS-HA(WS-CNT)                                   
              END-IF                                                            
              MOVE K820-NUM-NRIC             TO WS-NRIC(WS-CNT)                 
              MOVE K820-NUM-RELATIONSHIP     TO WS-RELN(WS-CNT)                 
              MOVE K820-NUM-MARITAL-STATUS   TO WS-MS(WS-CNT)                   
              MOVE K820-NUM-CITIZENSHIP      TO WS-CTZN(WS-CNT)                 
              MOVE K820-NME-OCCP             TO WS-NAME(WS-CNT)                 
           END-IF.                                                              
                                                                                
           PERFORM 3600-READ-NEXT-K820       THRU 3600-EXIT.                    
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-PROCESS-NUCLEUS.                                                    
      ****************************************************************          
                                                                                
      *------------>  DETERMINE IF SC/SP CHILD                                  
           IF (WS-RELN(WS-I) = '41') AND                                        
              (WS-CTZN(WS-I) = '10' OR '20')                                    
                 MOVE 'Y'          TO WS-SC-SP-CHILD                            
           END-IF.                                                              
                                                                                
      *------------>  DETERMINE MARRIED HA1                                     
           IF WS-RELN(WS-I) = '20' OR '09' OR '10'                              
              MOVE 'Y'             TO WS-HA1-MARRIED                            
           END-IF.                                                              
                                                                                
                                                                                
      *------------>  DETERMINE APPLN BY HA RELATIONSHIP                        
           IF WS-RELN(WS-I) = '01' OR '11' OR '21' OR '31'                      
              MOVE 'Y'             TO WS-HA-GRAND                               
           ELSE                                                                 
           IF WS-RELN(WS-I) = '02' OR '12' OR '19' OR '22' OR                   
                                      '32' OR '38' OR '97' OR '98'              
              MOVE 'Y'             TO WS-HA-PARENT                              
           ELSE                                                                 
           IF WS-RELN(WS-I) = '06' OR '07' OR '26' OR '27'                      
              MOVE 'Y'             TO WS-HA-SIBLING                             
           ELSE                                                                 
           IF WS-RELN(WS-I) = '41' OR '42'                                      
              MOVE 'Y'             TO WS-HA-CHILD                               
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5150-TAG-FREEZE-OCCP.                                                    
      *************************************************************             
            MOVE SPACES                  TO WS-FRZ-TAG(WS-I).                   
            MOVE WS-RELN(WS-I)           TO WS-RELATIONSHIP-SW.                 
            MOVE WS-MS(WS-I)             TO WS-MARITAL-STAT-SW.                 
                                                                                
            IF WS-HA(WS-I) = 'Y'                                                
               GO TO 5150-EXIT                                                  
            END-IF.                                                             
                                                                                
            IF PARENT                                                           
               CONTINUE                                                         
            ELSE                                                                
               GO TO 5150-EXIT                                                  
            END-IF.                                                             
                                                                                
      *--< FOR PAA SCHEME                                                       
           IF F800-NUM-ELIG-SCHEME = 'PAA' AND PARENT                           
              MOVE '77'             TO WS-FRZ-TAG(WS-I)                         
              GO TO 5150-EXIT                                                   
           END-IF.                                                              
                                                                                
      *--> FOR PAS SCHEME                                                       
           IF F800-NUM-ELIG-SCHEME = 'PAS' AND                                  
              SINGLE AND PARENT                                                 
              MOVE '77'             TO WS-FRZ-TAG(WS-I)                         
              GO TO 5150-EXIT                                                   
           END-IF.                                                              
                                                                                
      *--->APPLICANT IS S/D/W/P AND WITH NO SC/SP CHILD                         
           IF (F800-NUM-ELIG-SCHEME = 'PA ' OR 'TCP')                           
              AND (WS-HA1-MARRIED NOT = 'Y')                                    
              AND NOT(APPLICANT OR GRANDPARENT)                                 
              AND (PARENT OR MARRIED)                                           
              AND (WS-SC-SP-CHILD NOT = 'Y')                                    
              MOVE '77'             TO WS-FRZ-TAG(WS-I)                         
              GO TO 5150-EXIT                                                   
           END-IF.                                                              
                                                                                
      *--->FOR SMP SCHEME                                                       
           IF (F800-NUM-ELIG-SCHEME = 'SMP') AND (MARRIED OR PARENT)            
              MOVE '77'             TO WS-FRZ-TAG(WS-I)                         
              GO TO 5150-EXIT                                                   
           END-IF.                                                              
                                                                                
      *--->FOR NCS SCHEME                                                       
           IF (F800-NUM-ELIG-SCHEME = 'NCS') AND PARENT                         
              MOVE '77'             TO WS-FRZ-TAG(WS-I)                         
              GO TO 5150-EXIT                                                   
           END-IF.                                                              
                                                                                
       5150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5200-READ-BP13K820.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                 TO K820-KEY-FLD.                         
           MOVE F800-NUM-REGN          TO K820-NUM-REGN.                        
           MOVE WS-NRIC(WS-I)          TO K820-NUM-NRIC.                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = ZEROS                                            
              PERFORM 5250-REWRITE-K820  THRU 5250-EXIT                         
              ADD +1  TO WS-CNT-READ-K820                                       
           ELSE                                                                 
              IF WS-K820-STATUS = 10 OR 23                                      
                 DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY IS '                
                      K820-KEY-FLD                                              
              ELSE                                                              
                 DISPLAY 'BP13K820 READ ERROR, STATUS IS '                      
                       WS-K820-STATUS ' KEY IS ' K820-KEY-FLD                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5250-REWRITE-K820.                                                       
      *************************************************************             
                                                                                
           MOVE WS-FRZ-TAG(WS-I)       TO K820-NUM-FREEZE-OCCP-TAG.             
           MOVE 'P13C83A'              TO K820-NUM-USERID.                      
           MOVE WS-SYSTEM-DATE         TO K820-DTE-UPDATE.                      
                                                                                
                                                                                
           REWRITE BP13K820-REC.                                                
                                                                                
           IF WS-K820-STATUS = ZEROS                                            
              ADD +1  TO WS-CNT-UPD-K820                                        
           ELSE                                                                 
              DISPLAY 'BP13K820 UPDATE ERROR, STATUS IS ' WS-K820-STATUS        
              DISPLAY 'BP13K820 RECORD KEY IS ' F800-NUM-REGN                   
           END-IF.                                                              
                                                                                
       5250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5300-PRINT-L83A.                                                         
      *************************************************************             
                                                                                
           IF WS-L83A-LINECTR > 49                                              
              PERFORM 5310-PRINT-HEADING   THRU 5310-EXIT                       
           END-IF.                                                              
                                                                                
           ADD +1        TO WS-L83A-LINECTR.                                    
                                                                                
      *    IF WS-I = 1                                                          
              ADD +1                       TO WS-L83A-S-NO                      
              MOVE WS-L83A-S-NO            TO L83A-S-NO                         
              MOVE F800-NUM-REGN           TO L83A-REF-NO                       
              MOVE F800-NUM-ALLO-SCHEME    TO L83A-ALLOC-SCH                    
              MOVE F800-NUM-ELIG-SCHEME    TO L83A-ELIG-SCH                     
              MOVE F800-NUM-NEW-TOWN       TO L83A-NT                           
              MOVE F800-NUM-FLAT-TYPE      TO L83A-FT                           
              MOVE F800-NUM-ALLO-CAT       TO L83A-CAT                          
                                                                                
              WRITE BP13L83A-REC         FROM L83A-DETAIL-01                    
                                        AFTER ADVANCING 2 LINES                 
              ADD   +2                     TO WS-L83A-LINECTR                   
      *    END-IF.                                                              
                                                                                
           PERFORM 5320-GET-REL-DESC     THRU 5320-EXIT.                        
           PERFORM 5325-GET-CITIZENSHIP  THRU 5325-EXIT.                        
           PERFORM 5330-GET-MARITAL-STAT THRU 5330-EXIT.                        
                                                                                
           EVALUATE TRUE                                                        
           WHEN WS-NRIC(WS-I) = F800-NUM-NRIC1                                  
              MOVE 'HA1'                   TO L83A-HA-OCCP                      
           WHEN WS-NRIC(WS-I) = F800-NUM-NRIC2                                  
              MOVE 'HA2'                   TO L83A-HA-OCCP                      
           WHEN WS-NRIC(WS-I) = F800-NUM-NRIC3                                  
              MOVE 'HA3'                   TO L83A-HA-OCCP                      
           WHEN WS-NRIC(WS-I) = F800-NUM-NRIC4                                  
              MOVE 'HA4'                   TO L83A-HA-OCCP                      
           WHEN OTHER                                                           
              MOVE 'OCCP'                  TO L83A-HA-OCCP                      
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-NRIC(WS-I)              TO L83A-NRIC.                        
           MOVE WS-NAME(WS-I)              TO L83A-NAME.                        
           MOVE WS-RELN(WS-I)              TO L83A-REL-CODE.                    
           MOVE WS-FRZ-TAG(WS-I)           TO L83A-FRZ-OCCP-TAG.                
                                                                                
           WRITE BP13L83A-REC            FROM L83A-DETAIL-02                    
                                        AFTER ADVANCING 1 LINE.                 
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       5310-PRINT-HEADING.                                                      
      *************************************************************             
                                                                                
           ADD +1                  TO WS-L83A-PAGECTR.                          
           MOVE ZERO               TO WS-L83A-LINECTR.                          
                                                                                
           MOVE WS-L83A-PAGECTR    TO L83A-PAGE-NO.                             
           MOVE WS-EDITED-DATE     TO L83A-RUN-DATE.                            
                                                                                
           WRITE BP13L83A-REC    FROM L83A-HEADER-01                            
                                AFTER ADVANCING PAGE.                           
                                                                                
           WRITE BP13L83A-REC    FROM L83A-HEADER-02                            
                                AFTER ADVANCING 1 LINE.                         
                                                                                
           WRITE BP13L83A-REC    FROM L83A-HEADER-03                            
                                AFTER ADVANCING 2 LINES.                        
                                                                                
           WRITE BP13L83A-REC    FROM L83A-HEADER-04                            
                                AFTER ADVANCING 1 LINE.                         
                                                                                
           WRITE BP13L83A-REC    FROM L83A-HEADER-05                            
                                AFTER ADVANCING 1 LINE.                         
                                                                                
       5310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       5320-GET-REL-DESC.                                                       
      ****************************************************************          
                                                                                
           MOVE SPACES                TO BP13K060-REC.                          
           MOVE '35'                  TO K060-SERIAL-NO.                        
           MOVE WS-RELN(WS-I)         TO K060-CODE.                             
           PERFORM 5400-READ-K060     THRU 5400-EXIT.                           
           MOVE K060-DESC             TO L83A-REL-DESC.                         
                                                                                
       5320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5325-GET-CITIZENSHIP.                                                    
      ****************************************************************          
                                                                                
           MOVE SPACES                TO BP13K060-REC.                          
           MOVE '41'                  TO K060-SERIAL-NO.                        
           MOVE WS-CTZN(WS-I)         TO K060-CODE.                             
           PERFORM 5400-READ-K060     THRU 5400-EXIT.                           
           MOVE K060-DESC             TO L83A-CITIZENSHIP.                      
                                                                                
       5325-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5330-GET-MARITAL-STAT.                                                   
      ****************************************************************          
                                                                                
           IF WS-MS(WS-I) = 'P' OR '5'                                          
              MOVE 'P'                  TO L83A-MARITAL-STAT                    
              GO TO 5330-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO BP13K060-REC.                        
           MOVE '21'                    TO K060-SERIAL-NO.                      
           MOVE WS-MS(WS-I)             TO K060-CODE.                           
           PERFORM 5400-READ-K060       THRU 5400-EXIT.                         
           MOVE K060-DESC               TO L83A-MARITAL-STAT.                   
                                                                                
       5330-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5400-READ-K060.                                                          
      ****************************************************************          
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
                                                                                
           IF WS-K060-STATUS = 0                                                
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-K060-STATUS = 23                                               
              DISPLAY 'BP13K060 RECORD NOT FOUND, KEY IS ' K060-KEY-FLD         
              DISPLAY 'F800-NUM-REGN ' F800-NUM-REGN                            
           ELSE                                                                 
              DISPLAY 'BP13K060 READ ERROR, STATUS IS ' WS-K060-STATUS          
              DISPLAY 'K060-KEY-FLD-' K060-KEY-FLD                              
                    '& F800-NUM-REGN ' F800-NUM-REGN                            
           END-IF.                                                              
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F800                                                       
                 BP13K820                                                       
                 BP13K060                                                       
                 BP13L83A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C83A CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F800 RECORDS READ        = ' WS-CNT-READ-F800.          
           DISPLAY 'BP13K820 RECORDS UPDATED     = ' WS-CNT-UPD-K820.           
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
