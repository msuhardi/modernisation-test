       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C419.                                                 
      *AUTHOR.        TMK.                                                      
      *DATE-WRITTEN.  27/10/89.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMENCEMENT                *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  TO DELETE LAST MONTHS AGREEMENT ORDER       *                
      *            RECORDS FROM K330 FILE IF THEY ALREADY      *                
      *            EXIST IN K410 FILE.                         *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13K410  :  ACCUMULATED SALES ORDERS FILE    *                
      *                                                        *                
      *     I-O   FILES :                                      *                
      *       1. BP13K330  :  AGREEMENT ORDER FILE             *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *       1. BP13F330  :  AGREEMENT ORDER FILE (FOR DELETED*                
      *                       CASES)                           *                
      * CHG-NO   BY   ON                                       *                
      * ----------------                                       *                
      * SOC-PH8B LSB 930713 - EXPAND REGN                      *                
      * BP130418 CLT 980826 - Y2K COMPLIANCE CHANGE            *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                ACCESS MODE  IS SEQUENTIAL                                      
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K410-KEY-FLD                                    
                FILE STATUS  IS K410-STATUS.                                    
                                                                                
           SELECT BP13K330 ASSIGN TO BP13K330                                   
                ACCESS MODE  IS DYNAMIC                                         
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K330-KEY-FLD                                    
                FILE STATUS  IS K330-STATUS.                                    
                                                                                
           SELECT BP13F330 ASSIGN TO BP13F330.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K410         BLOCK CONTAINS 0 RECORDS                            
                            RECORD CONTAINS 2000 CHARACTERS                     
                            LABEL RECORDS ARE STANDARD.                         
       COPY BP13K410.                                                           
                                                                                
       FD  BP13K330         BLOCK CONTAINS 0 RECORDS                            
                            RECORD CONTAINS 2000 CHARACTERS                     
                            LABEL RECORDS ARE STANDARD.                         
       COPY BP13K330.                                                           
                                                                                
       FD  BP13F330         BLOCK CONTAINS 0 RECORDS                            
                            RECORD CONTAINS 2000 CHARACTERS                     
                            LABEL RECORDS ARE STANDARD                          
                            RECORDING MODE IS F.                                
       COPY BP13F330.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  K330-STATUS         PIC 99      VALUE ZEROS.                     
               88  K330-REC-FOUND  VALUE ZEROS.                                 
               88  K330-REC-NOTFND VALUE 23.                                    
           05  K410-STATUS         PIC 99      VALUE ZEROS.                     
               88  K410-REC-FOUND  VALUE ZEROS.                                 
               88  K410-REC-NOTFND VALUE 23.                                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-DELETED      PIC 9(5)    VALUE ZERO.                      
           05  WS-CNT-READ         PIC 9(5)    VALUE ZERO.                      
           05  WS-CNT-WRITE        PIC 9(5)    VALUE ZERO.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  EOF-K410-IND        PIC X       VALUE SPACES.                    
               88 EOF-K410         VALUE 'Y'.                                   
                                                                                
       01  WS-DTE-SO-X             PIC X(08).                                   
       01  WS-DTE-SO   REDEFINES   WS-DTE-SO-X.                                 
           05  WS-DTE-SO-CCYY      PIC 9(4).                                    
           05  WS-DTE-SO-MM        PIC 99.                                      
           05  WS-DTE-SO-DD        PIC 99.                                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9(4).                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       01  WS-MISC-DATE-VARIABLES.                                              
           05  WS-MTH-DIFF             PIC S9(04) VALUE +0.                     
                                                                                
      **************************************************************            
       PROCEDURE DIVISION.                                                      
      **************************************************************            
       000-MAIN-PROCESS.                                                        
                                                                                
           PERFORM 010-OPEN-FILES     THRU  010-EXIT.                           
                                                                                
           PERFORM 100-PROCESS-INDATA THRU 100-EXIT                             
                    UNTIL EOF-K410.                                             
                                                                                
           PERFORM 020-END-ROUTINE    THRU  020-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       010-OPEN-FILES.                                                          
      *-----------------*                                                       
           OPEN INPUT  BP13K410                                                 
                I-O    BP13K330                                                 
                OUTPUT BP13F330.                                                
                                                                                
           IF K410-STATUS NOT = 0 AND 97                                        
              DISPLAY ' OPENING ERROR, K410-STATUS '                            
                        K410-STATUS                                             
              MOVE K410-STATUS TO RETURN-CODE                                   
              PERFORM 020-END-ROUTINE.                                          
                                                                                
           IF K330-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR, K330-STATUS ' K330-STATUS                 
              PERFORM 020-END-ROUTINE.                                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
                                                                                
       010-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       020-END-ROUTINE.                                                         
      *---------------*                                                         
           CLOSE BP13K410                                                       
                 BP13K330                                                       
                 BP13F330.                                                      
                                                                                
           IF K410-STATUS NOT = 0 AND 97                                        
              DISPLAY ' CLOSING ERROR, K410-STATUS ' K410-STATUS                
              MOVE K410-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K330-STATUS NOT = 0 AND 97                                        
            DISPLAY ' CLOSING ERROR, BM06K330-STATUS ' K330-STATUS              
            MOVE K330-STATUS TO RETURN-CODE.                                    
                                                                                
           DISPLAY ' ** BP13C419 ** '.                                          
           DISPLAY ' NO. OF RECORDS READ FROM K410    = '                       
                     WS-CNT-READ.                                               
           DISPLAY ' NO. OF RECORDS DELETED FROM K330 = '                       
                     WS-CNT-DELETED.                                            
           DISPLAY ' NO. OF RECORDS WRITTEN TO F330 = '                         
                     WS-CNT-WRITE.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       020-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *  1.  READ K410 FILE AT END EXIT                         *               
      *  2.  IF K410 RECORD IS THAT OF LAST MONTH               *               
      *         (UPDATE-DATE = SYSTEM-DATE - 2)                 *               
      *       . READ K330 FILE                                  *               
      *       . DELETE ALL RECORD WITH THE SAME CURRENT REG NO. *               
      *      ELSE                                               *               
      *         ERROR                                           *               
      ***********************************************************               
       100-PROCESS-INDATA.                                                      
                                                                                
           READ BP13K410 AT END                                                 
                MOVE 'Y' TO EOF-K410-IND                                        
                GO TO 100-EXIT.                                                 
                                                                                
           ADD 1 TO WS-CNT-READ.                                                
                                                                                
           IF K410-DTE-SO-EFFECTIVE = SPACES OR LOW-VALUES                      
              MOVE ZEROES TO WS-DTE-SO                                          
           ELSE                                                                 
              MOVE  K410-DTE-UPDATE TO WS-DTE-SO-X.                             
                                                                                
           COMPUTE WS-MTH-DIFF = (( WS-CUR-CCYY * 12 ) + WS-CUR-MM) -           
                                 (( WS-DTE-SO-CCYY * 12 ) +                     
                                    WS-DTE-SO-MM ).                             
                                                                                
           IF WS-MTH-DIFF > 3                                                   
              NEXT SENTENCE                                                     
           ELSE                                                                 
              GO TO 100-EXIT.                                                   
                                                                                
           MOVE K410-NUM-REGN     TO K330-CURR-REG.                             
           MOVE SPACE             TO K330-SEQ-NO.                               
                                                                                
           START BP13K330 KEY NOT < K330-CURR-REG.                              
                                                                                
           IF K330-REC-FOUND                                                    
              PERFORM 200-DELETE-K330 THRU 200-EXIT                             
                 UNTIL K330-CURR-REG > K410-NUM-REGN                            
           ELSE                                                                 
              IF K330-REC-NOTFND                                                
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 DISPLAY ' ERROR READING, BP13K330 ' K330-STATUS                
                 MOVE K330-STATUS TO RETURN-CODE                                
                 PERFORM 020-END-ROUTINE.                                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *  DELETE ALL K330 RECORDS WITH THE SAME CURRENT REG NO  *                
      *                                                        *                
      **********************************************************                
       200-DELETE-K330.                                                         
                                                                                
           PERFORM 300-READNEXT-K330 THRU 300-EXIT.                             
                                                                                
           IF K330-KEY-FLD = HIGH-VALUE OR                                      
              K330-CURR-REG > K410-NUM-REGN                                     
              GO TO 200-EXIT.                                                   
                                                                                
           PERFORM 250-WRITE-F330 THRU 250-EXIT.                                
                                                                                
           DELETE BP13K330 RECORD.                                              
                                                                                
           IF K330-STATUS = ZEROES                                              
              ADD 1 TO WS-CNT-DELETED                                           
           ELSE                                                                 
              DISPLAY ' ERROR DELETING, BP13K330 ' K330-STATUS                  
              MOVE K330-STATUS TO RETURN-CODE                                   
              PERFORM 020-END-ROUTINE.                                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       250-WRITE-F330.                                                          
           MOVE SPACES TO BP13F330-REC.                                         
           MOVE BP13K330-REC TO BP13F330-REC.                                   
           WRITE BP13F330-REC.                                                  
           ADD 1 TO WS-CNT-WRITE.                                               
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READNEXT-K330.                                                       
                                                                                
           READ BP13K330 NEXT                                                   
                AT END MOVE HIGH-VALUE TO K330-KEY-FLD.                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
