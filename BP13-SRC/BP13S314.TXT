 /*===========================================================*/                
 /* SAS PROGRAM TO PRINT SUMMARY AGEING REPORT ON SOC DEBTOR  */                
 /* CHG-NO   BY   ON    - DESCRIPTION                         */                
 /* ---------------------------------                         */                
 /* BP130084 LSB 960238 - NEW PGM                             */                
 /* BP130130 EVM 960716 - ADD HEADING TITLE                   */                
 /* BP130130 LJL 960826 - TO REMOVE THE '<' AND '>' SIGNS.     */               
 /* BP130447 LSB 990104 - CHG BP13F142                         */               
 /*===========================================================*/                
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
    PROC FORMAT;                                                                
      PICTURE AMTEDT LOW-<0='000,000,000,000.00CR'                              
                         (FILL=' ' PREFIX='$' MULT=100.00000000001)             
                     0-HIGH='000,000,000,000.00DR'                              
                         (FILL=' ' PREFIX='$' MULT=100.00000000001);            
                                                                                
  DATA BP13F142 ;                                                               
     INFILE BP13F142;                                                           
     INPUT  @01  REGNO    $CHAR8.                                               
            @223 AMTDEBT  ZD9.2                                                 
            @249 MONTH    3.;                                                   
                                                                                
     FORMAT AMTDEBT AMTEDT.;                                                    
                                                                                
     PROC SORT; BY MONTH;                                                       
                                                                                
     DATA _NULLS_;                                                              
       SET BP13F142 END=EOF;                                                    
       BY MONTH;                                                                
                                                                                
       FILE BP13L314 NOTITLES HEADER=HH;                                        
                                                                                
       IF FIRST.MONTH THEN DO;                                                  
          CASE_NO=0; TOT_AMT=0;                                                 
          END;                                                                  
                                                                                
       CASE_NO+1; TOT_AMT+AMTDEBT;                                              
                                                                                
       IF LAST.MONTH THEN                                                       
          DO;                                                                   
              MAX_MTH=MONTH+1;                                                  
              PUT @26  ' '                                                      
                  @28  MONTH  3.                                                
                  @33  '  '                                                     
                  @58  CASE_NO 6.                                               
                  @94  TOT_AMT AMTEDT.;                                         
          END;                                                                  
       CASE_TOT+1;                                                              
       GRD_AMT+AMTDEBT;                                                         
       IF EOF THEN DO;                                                          
          PUT @54  '================='  @89  '=====================';           
          PUT @10  'GRAND TOTAL :'                                              
              @56   CASE_TOT 8.  @94  GRD_AMT AMTEDT.;                          
          END;                                                                  
        RETURN;                                                                 
     HH:                                                                        
       PAGE+1;                                                                  
       CURRENT=DATE();                                                          
       PUT @1   'BP13L314      HDB3'                                            
           @41  'S Y S T E M   O F   C O M M I T M E N T'                       
           @90  'DATE : '      @98  CURRENT DDMMYY10.                           
           @120 'PAGE : '      @127 PAGE 3.;                                    
       PUT @41  'SUMMARY  AGEING  REPORT  ON  SOC  DEBTOR';                     
       PUT @48  'FROM SALES AGREEMENT DATE'                                     
           @90  'USER : FINANCE DEPT'/;                                         
       PUT @25  'BALANCE (MONTH)'                                               
           @55  'NUMBER OF CASES'                                               
           @92  'AMT-SOC-DEBTOR';                                               
       PUT @24  '-----------------'                                             
           @54  '-----------------'                                             
           @89  '---------------------';                                        
                                                                                
       RETURN;                                                                  
