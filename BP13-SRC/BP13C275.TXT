      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C275                                                  
       AUTHOR.        RHB.                                                      
      *DATE-WRITTEN : 30/06/1997.                                               
      *----------------------------------------------------------------*        
      *                                                                *        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *                                                                *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  OBJECTIVE   : PROGRAM TO MATCH F200 TO F205                   *        
      *                                                                *        
      *  INPUT FILE  :                                                 *        
      *     1. BP13F200                                                *        
      *     2. BP13F205                                                *        
      *     3. BP13K800                                                *        
      *                                                                *        
      *  OUTPUT FILES:                                                 *        
      *     1. P13F200A                                                *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *  REQ NO   BY    DATE    DESCRIPTION                            *        
      *  ======   ===   ====    ===========                            *        
      * BP130250  RHB 19970630  NEW PROGRAM                            *        
      * BP130250  LMS 19970812  TO ADD ADDITIONAL COND, K200-NUM-REOPT.*        
      * BP130418  MPA 19980414  TO CATER FOR Y2000.                    *        
      * BP132095  MTD 20020423  EXPAND LENGTH OF BP13F200/P13F200A     *        
      *                         (FROM 100 TO 300)                      *        
      *----------------------------------------------------------------*        
                                                                                
      *-------------------------*                                               
       ENVIRONMENT DIVISION.                                                    
      *-------------------------*                                               
       CONFIGURATION SECTION.                                                   
      *-------------------------*                                               
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *-------------------------*                                               
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------*                                               
       FILE-CONTROL.                                                            
      *-------------------------*                                               
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS       IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS WS-K800-STATUS.                               
                                                                                
                                                                                
           SELECT P13F200A ASSIGN TO P13F200A.                                  
                                                                                
                                                                                
      *-------------------------*                                               
       DATA DIVISION.                                                           
      *-------------------------*                                               
       FILE SECTION.                                                            
      *-------------------------*                                               
                                                                                
       FD   BP13F200                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F205.                                                           
                                                                                
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD   P13F200A                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F200A-REC           PIC X(300).                                  
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *-------------------------*                                               
                                                                                
       01  WS-F200-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F205-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F200A-WRITE          PIC 9(8)    VALUE ZERO.                      
       01  WS-F200-NMR             PIC 9(8)    VALUE ZERO.                      
       01  WS-F205-NMR             PIC 9(8)    VALUE ZERO.                      
       01  WS-F205-BYPASS          PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH-CTR            PIC 9(8)    VALUE ZERO.                      
       01  WS-K800-STATUS          PIC 9(2)    VALUE ZERO.                      
       01  WS-K800-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-K800-FOUND           PIC X       VALUE SPACES.                    
                                                                                
       01  WS-F200-KEY-FLD.                                                     
           05  WS-F200-DTE-ALLOC      PIC X(08)  VALUE SPACES.                  
           05  WS-F200-NUM-NT-ZONE    PIC X(03)  VALUE SPACES.                  
           05  WS-F200-NUM-FLAT-TYPE  PIC X(02)  VALUE SPACES.                  
                                                                                
       01  WS-F205-KEY-FLD.                                                     
           05  WS-F205-DTE-ALLOC      PIC X(08)  VALUE SPACES.                  
           05  WS-F205-NUM-NT-ZONE    PIC X(03)  VALUE SPACES.                  
           05  WS-F205-NUM-FLAT-TYPE  PIC X(02)  VALUE SPACES.                  
                                                                                
       01  WS-RUN-DATE.                                                         
           05  WS-YY               PIC X(4).                                    
           05  WS-MM               PIC X(2).                                    
           05  WS-DD               PIC X(2).                                    
                                                                                
       01  WS-EOF-F200             PIC X(1) VALUE SPACES.                       
       01  WS-EOF-F205             PIC X(1) VALUE SPACES.                       
                                                                                
       01  WS-DTE-REGN             PIC X(8).                                    
                                                                                
      *--------------------*                                                    
       PROCEDURE DIVISION.                                                      
      *--------------------*                                                    
                                                                                
      *--------------------*                                                    
       000-MAIN-LOGIC.                                                          
      *--------------------*                                                    
                                                                                
           PERFORM 100-INITIALIZATION                                           
              THRU 100-EXIT.                                                    
           PERFORM 200-READ-F200                                                
              THRU 200-EXIT.                                                    
           PERFORM 300-READ-F205                                                
              THRU 300-EXIT.                                                    
           PERFORM 400-PROCESS                                                  
              THRU 400-EXIT UNTIL WS-EOF-F200 = 'Y'.                            
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       100-INITIALIZATION.                                                      
      *-------------------------*                                               
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13F205                                                 
                       BP13K800                                                 
                OUTPUT P13F200A.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K800 OPEN ERROR '                                        
              DISPLAY 'FILE STATUS IS :' WS-K800-STATUS                         
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE 'N' TO WS-EOF-F200.                                             
           MOVE 'N' TO WS-EOF-F205.                                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------*                                               
       200-READ-F200.                                                           
      *-------------------------*                                               
                                                                                
           READ BP13F200 AT END MOVE 'Y' TO WS-EOF-F200                         
                GO TO 200-EXIT.                                                 
                                                                                
           MOVE F200-DTE-ALLOC     TO WS-F200-DTE-ALLOC.                        
           MOVE F200-NUM-NT-ZONE   TO WS-F200-NUM-NT-ZONE.                      
           MOVE F200-NUM-FLAT-TYPE TO WS-F200-NUM-FLAT-TYPE.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       300-READ-F205.                                                           
      *-------------------------*                                               
                                                                                
           READ BP13F205 AT END MOVE 'Y' TO WS-EOF-F205                         
               GO TO 300-EXIT.                                                  
                                                                                
           MOVE F205-DTE-ALLOCN    TO WS-F205-DTE-ALLOC.                        
           MOVE F205-NUM-NT-ZONE   TO WS-F205-NUM-NT-ZONE.                      
           MOVE F205-NUM-FLAT-TYPE TO WS-F205-NUM-FLAT-TYPE.                    
                                                                                
           ADD 1 TO WS-F205-READ.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       400-PROCESS.                                                             
      *-------------------------*                                               
                                                                                
           IF WS-F200-KEY-FLD = WS-F205-KEY-FLD                                 
              ADD 1 TO WS-F200-READ                                             
              PERFORM 500-READ-K800  THRU 500-EXIT                              
              IF WS-K800-FOUND        = 'Y'                                     
                 PERFORM 410-CHECK-K800 THRU 410-EXIT                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F200  THRU 200-EXIT.                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       410-CHECK-K800.                                                          
      *-------------------------*                                               
                                                                                
           IF K800-NUM-SCH-ACC        = SPACES                 AND              
              K800-NUM-STATUS  NOT    = 'C'                    AND              
              K800-NUM-CANCEL-REASON  = '61'                   AND              
              K800-DTE-BALLOT         = F205-DTE-BALLOT        AND              
              K800-NUM-REOPT          = 'Y'                    AND              
              F200-NUM-REOPT          = 'Y'                    AND              
              (K800-NUM-CUR-REGN NOT  = SPACES AND LOW-VALUES  AND              
               K800-NUM-CUR-REGN      > K800-NUM-REGN)                          
               WRITE P13F200A-REC FROM BP13F200-REC                             
               ADD   1  TO WS-F200A-WRITE                                       
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       500-READ-K800.                                                           
      *-----------------*                                                       
                                                                                
           MOVE 'Y'            TO WS-K800-FOUND.                                
           MOVE  F200-NUM-REGN TO K800-NUM-REGN.                                
                                                                                
           READ  BP13K800.                                                      
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD  1         TO WS-K800-READ                                  
           WHEN OTHER                                                           
                MOVE 'N'       TO WS-K800-FOUND                                 
                DISPLAY 'K800-FILE STATUS IS  : ' WS-K800-STATUS                
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
             MOVE FUNCTION CURRENT-DATE(1:8) TO WS-RUN-DATE.                    
             DISPLAY '*-------- BP13C275 ---------*'.                           
             DISPLAY ' '.                                                       
             DISPLAY ' RUN DATE          : ' WS-DD '/' WS-MM '/' WS-YY.         
             DISPLAY ' '.                                                       
             DISPLAY ' BP13F200-READ     : ' WS-F200-READ.                      
             DISPLAY ' BP13F205-READ     : ' WS-F205-READ.                      
             DISPLAY ' P13F200A-WRITE    : ' WS-F200A-WRITE.                    
                                                                                
           CLOSE      BP13F200                                                  
                      BP13F205                                                  
                      BP13K800                                                  
                      P13F200A.                                                 
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K800 OPEN ERROR '                                        
              DISPLAY 'FILE STATUS IS :' WS-K800-STATUS                         
              MOVE WS-K800-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
