       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C477.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  11/05/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO DELETE SENT CPF CASES TO   *            
      *                CPF REJECT FILE                             *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F600                               *            
      * I-O    FILES :  1.) BP13K607                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136711 KSJ3 11/05/2017 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F600  ASSIGN        TO BP13F600.                          
                                                                                
           SELECT BP13K607  ASSIGN        TO BP13K607                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K607-KEY-FLD                       
                            ALTERNATE KEY IS K607-ALT-KEY2                      
                            FILE STATUS   IS WS-K607-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F600                                                             
           RECORD CONTAINS 1800 CHARACTERS.                                     
           COPY BP13F600.                                                       
                                                                                
       FD  BP13K607.                                                            
           COPY BP13K607.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K607-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F600-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F600-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K607-DEL           PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F600-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K607-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F600-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F600-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F600                                                 
                I-O    BP13K607.                                                
                                                                                
           IF WS-K607-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K607 ERROR ' WS-K607-STATUS                  
              MOVE     WS-K607-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F600-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F600 NEXT                                                   
              AT END MOVE 'Y'              TO   WS-F600-EOF.                    
                                                                                
           IF WS-F600-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F600-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K607-REC.                   
           MOVE F600-NUM-ORIG-REGN         TO   K607-NUM-REGN.                  
           MOVE F600-NUM-CPF               TO   K607-NUM-CPF.                   
           MOVE F600-CDE-CPF               TO   K607-CDE-CPF.                   
           MOVE F600-NUM-SNO               TO   K607-NUM-SNO.                   
                                                                                
           PERFORM 3010-BP13K607-READ      THRU 3010-EXIT.                      
                                                                                
           IF WS-K607-FND = 'Y'                                                 
              PERFORM 3020-DELETE-BP13K607 THRU 3020-EXIT                       
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-F600-SKIP                
           END-IF.                                                              
                                                                                
       3000-CONTINUE-NEXT-REC.                                                  
                                                                                
           PERFORM 2000-BP13F600-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13K607-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K607 KEY IS K607-ALT-KEY2.                                  
                                                                                
           EVALUATE WS-K607-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K607-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K607-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K607 ERROR '    WS-K607-STATUS                  
                      ' / '                     K607-ALT-KEY2                   
              MOVE     WS-K607-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3020-DELETE-BP13K607.                                                    
      *=============================================================            
                                                                                
           DELETE BP13K607.                                                     
                                                                                
           EVALUATE WS-K607-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-K607-DEL                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'DELETE BP13K607 ERROR '  WS-K607-STATUS                  
                      ' / '                     K607-ALT-KEY2                   
              MOVE     WS-K607-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F600                                                       
                 BP13K607.                                                      
                                                                                
           IF WS-K607-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K607 ERROR ' WS-K607-STATUS                  
              MOVE     WS-K607-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C477 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F600 READ      : ' WS-CNT-F600-READ.          
           DISPLAY ' 2. NO OF BP13F600 SKIPPED   : ' WS-CNT-F600-READ.          
           DISPLAY ' 3. NO OF BP13K607 DELETED   : ' WS-CNT-K607-DEL.           
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
