      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C490.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  08-04-2009.                                               
      *--------------------------------------------------------------*          
      *             SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)        *          
      *--------------------------------------------------------------*          
      *                                                              *          
      *    OBJECTIVES :  MATCH BP13F205 WITH BM06K110 FOR PRINTING   *          
      *                    OF PCD NOTIFICATION                       *          
      *                                                              *          
      *    INPUT FILES  : BP13F205                                   *          
      *                 : BM06K110                                   *          
      *    OUTPUT       : BM06F110                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP133570 ESA1 08042009 NEW PGM                                *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BM06F110 ASSIGN TO BM06F110.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE RECORD KEY  IS K110-AIX1                            
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BM06F110                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       COPY BM06F110.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F205-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K110-EOF                PIC X(01) VALUE 'N'.                  
           05  BM06K110-STATUS            PIC 9(02).                            
                                                                                
       01  WS-MISC-VARIABLES.                                                   
           05  WS-EDITED-DATE              PIC X(10) VALUE SPACES.              
           05  WS-CNT-F205-READ            PIC 9(08) VALUE 0.                   
           05  WS-CNT-F110-WRITE           PIC 9(08) VALUE 0.                   
                                                                                
       01  WS-F205-KEY.                                                         
           05  WS-F205-ESTATE              PIC X(03) VALUE SPACES.              
           05  WS-F205-NEIGH               PIC X(04) VALUE SPACES.              
           05  WS-F205-CNTRACT             PIC X(04) VALUE SPACES.              
           05  WS-F205-BLKNO               PIC X(05) VALUE SPACES.              
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(04).                            
           05  WS-SYS-MM                  PIC 9(02).                            
           05  WS-SYS-DD                  PIC 9(02).                            
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 1100-READ-F205         THRU 1100-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS           THRU 2000-EXIT.                       
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BM06K110                                                 
                OUTPUT BM06F110.                                                
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BM06K110 OPEN ERROR, STATUS IS ' BM06K110-STATUS         
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-READ-F205.                                                          
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES   TO  WS-F205-KEY.                                       
                                                                                
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE F205-NUM-NT-ZONE        TO WS-F205-ESTATE.                      
           MOVE F205-NUM-FLAT-TYPE      TO WS-F205-NEIGH.                       
           MOVE F205-DTE-ALLOCN         TO WS-F205-CNTRACT.                     
           MOVE F205-DTE-ALLOCN-NEW     TO WS-F205-BLKNO.                       
                                                                                
           ADD 1 TO WS-CNT-F205-READ.                                           
                                                                                
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
                                                                                
           INITIALIZE K110-REC.                                                 
           MOVE WS-F205-KEY             TO K110-AIX1.                           
                                                                                
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                  PERFORM 2100-READNEXT-BM06K110   THRU 2100-EXIT               
                        UNTIL WS-K110-EOF = 'Y'                                 
             WHEN OTHER                                                         
                  DISPLAY 'START BROWSE ERROR BM06K110,STATUS IS '              
                           BM06K110-STATUS                                      
                  MOVE BM06K110-STATUS         TO RETURN-CODE                   
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2100-READNEXT-BM06K110.                                                  
      *--------------------------------------------------------------*          
                                                                                
           READ BM06K110 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'  TO  WS-K110-EOF                                    
           END-READ.                                                            
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                  IF WS-F205-KEY = K110-AIX1                                    
                     IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES         
                                              AND ZEROES                        
                        WRITE F110-REC FROM K110-REC                            
                        ADD 1 TO WS-CNT-F110-WRITE                              
                     END-IF                                                     
                  ELSE                                                          
                     MOVE 'Y'       TO    WS-K110-EOF                           
                  END-IF                                                        
            WHEN 10                                                             
            WHEN 23                                                             
                 MOVE 'Y'       TO    WS-K110-EOF                               
            WHEN OTHER                                                          
                 DISPLAY 'ERROR READING BM06K110,STATUS IS '                    
                         BM06K110-STATUS                                        
                 MOVE BM06K110-STATUS        TO  RETURN-CODE                    
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F205                                                       
                 BM06K110                                                       
                 BM06F110.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE BM06K110 FAIL. STATUS IS ' BM06K110-STATUS         
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C490 CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECS WRITE      = '.                                 
           DISPLAY '    (BM06F110)         = ' WS-CNT-F110-WRITE.               
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
