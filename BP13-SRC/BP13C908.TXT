       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C908.                                                 
      *AUTHOR.        MTD.                                                      
      *DATE-WRITTEN.  27MAR2002.                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  TO COMPUTE THE CHECK DIGIT OF A GIVEN       *           
      *                 REGISTRATION NUMBER                         *           
      *              :  TO GET THE BARCODE EQUIVALENT OF A GIVEN    *           
      *                 REGISTRATION NUMBER                         *           
      *                                                             *           
      * CALLING MODULE:                                             *           
      *                                                             *           
      * COPYBOOK: P13C908A (CHARACTER-VALUE TABLE)                  *           
      *         : P13C908B (EBCDIC-VALUE TABLE)                     *           
      *                                                             *           
      * PARAMETERS: NUM-REGN (FROM CALLING PROGRAM)                 *           
      *             NUM-REGN W/ CHECK DIGIT                         *           
      *             RETURN CODE                                     *           
      *                                                             *           
      * OUTPUT : NUM-REGN WITH CHECK DIGIT (IN EBCDIC)              *           
      *          {START CHAR}{NUM-REGN}{CHECK DIGIT}{STOP CHAR}     *           
      *                                                             *           
      * RETURN CODE : +0 - SUCCESSFUL                               *           
      *               +8 - NOT SUCCESSFUL                           *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      * -------- ---  ------  ------------------------------------- *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       COPY P13C908A.                                                           
                                                                                
       COPY P13C908B.                                                           
                                                                                
       01  WS-WORK-VARIABLES.                                                   
           05  WS-NUM-TXT-INPUT          PIC X(30).                             
           05  WS-NUM-TXT-CHAR REDEFINES WS-NUM-TXT-INPUT                       
                                         PIC X(01) OCCURS 30.                   
           05  WS-NUM-TXT-VALUE          PIC 9(03) OCCURS 30.                   
           05  WS-WEIGHT-ARRAY           PIC X(60) VALUE '01020304050607        
      -        '0809101112131415161718192021222324252627282930'.                
           05  FILLER    REDEFINES       WS-WEIGHT-ARRAY.                       
               10  WS-WEIGHT             PIC 9(02)  OCCURS 30.                  
           05  WS-PRODUCT                PIC 9(05)  OCCURS 30.                  
           05  WS-ACC-PRODUCT            PIC 9(07).                             
           05  WS-TOTAL                  PIC 9(07).                             
           05  WS-DIVIDEND               PIC 9(05).                             
           05  WS-REMAINDER              PIC 9(03).                             
           05  WS-REMAINDER-R REDEFINES  WS-REMAINDER                           
                                         PIC X(03).                             
           05  WS-EBC-CHKDIGIT           PIC X(01).                             
           05  WS-SUB                    PIC 9(02).                             
                                                                                
       01  WS-CONSTANTS.                                                        
           05  WS-MODULUS-103            PIC 9(03)  VALUE 103.                  
           05  WS-START-VALUE            PIC 9(03)  VALUE 104.                  
           05  WS-EBCDIC-START-A9        PIC X(01)  VALUE X'8A'.                
           05  WS-EBCDIC-STOP-AB         PIC X(01)  VALUE X'75'.                
                                                                                
      *------------------------------------------------------------*            
       LINKAGE SECTION.                                                         
      *------------------------------------------------------------*            
       01  WS-C908-PARAMETERS.                                                  
           05  WS-INPUT-PARAMETERS.                                             
               10  WS-C908-TXT-IN-SIZE       PIC 9(2).                          
               10  WS-C908-TXT-INPUT         PIC X(30).                         
           05  WS-OUTPUT-PARAMETERS.                                            
               10  WS-C908-TXT-OUT-SIZE      PIC 9(2).                          
               10  WS-C908-TXT-OUTPUT        PIC X(40).                         
           05  WS-C908-RETCODE               PIC S9(04)  COMP.                  
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION     USING WS-C908-PARAMETERS.                         
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            PERFORM 1000-INITIALIZATION       THRU  1000-EXIT.                  
            PERFORM 2000-GET-CHECK-DIGIT      THRU  2000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           GOBACK.                                                              
                                                                                
      *-------------------------------------------------------------            
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROS                 TO WS-C908-RETCODE.                       
           MOVE LOW-VALUES            TO WS-EBC-CHKDIGIT.                       
                                                                                
           MOVE ZEROS                 TO WS-ACC-PRODUCT                         
                                         WS-TOTAL                               
                                         WS-DIVIDEND                            
                                         WS-REMAINDER.                          
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 30                 
              MOVE ZEROS               TO WS-PRODUCT (WS-SUB)                   
                                          WS-NUM-TXT-VALUE (WS-SUB)             
           END-PERFORM.                                                         
                                                                                
           MOVE WS-C908-TXT-INPUT     TO WS-NUM-TXT-INPUT.                      
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-GET-CHECK-DIGIT.                                                    
      *-------------------------------------------------------------            
                                                                                
           PERFORM   2100-GET-CHAR-VALUES     THRU  2100-EXIT                   
           VARYING WS-SUB FROM 1 BY 1                                           
             UNTIL WS-SUB > WS-C908-TXT-IN-SIZE.                                
                                                                                
           PERFORM 2200-MULTIPLY-WEIGHTS         THRU  2200-EXIT                
           VARYING  WS-SUB FROM 1 BY 1                                          
             UNTIL WS-SUB > WS-C908-TXT-IN-SIZE.                                
                                                                                
           COMPUTE WS-TOTAL = WS-START-VALUE + WS-ACC-PRODUCT.                  
                                                                                
           DIVIDE WS-TOTAL BY WS-MODULUS-103                                    
           GIVING WS-DIVIDEND                                                   
           REMAINDER WS-REMAINDER.                                              
                                                                                
      *****--- GET THE EBCDIC VALUE OF COMPUTED CHECK DIGIT(REMAINDER)          
           SET WS-EBC-IDX    TO 1.                                              
           SEARCH WS-EBCDIC-TABLE                                               
              AT END                                                            
                 MOVE +8     TO WS-C908-RETCODE                                 
                 GO TO 2000-EXIT                                                
              WHEN WS-REMAINDER-R = WS-EBC-CHAR-VALUE (WS-EBC-IDX)              
                 MOVE WS-EBC-EBCDIC  (WS-EBC-IDX)  TO                           
                      WS-EBC-CHKDIGIT                                           
           END-SEARCH.                                                          
                                                                                
           MOVE SPACES TO WS-C908-TXT-OUTPUT.                                   
                                                                                
           STRING WS-EBCDIC-START-A9,                                           
                  WS-NUM-TXT-INPUT(1:WS-C908-TXT-IN-SIZE),                      
                  WS-EBC-CHKDIGIT,                                              
                  WS-EBCDIC-STOP-AB DELIMITED BY SIZE                           
             INTO WS-C908-TXT-OUTPUT.                                           
                                                                                
           COMPUTE  WS-C908-TXT-OUT-SIZE =  WS-C908-TXT-IN-SIZE + 3.            
                                                                                
           MOVE +0      TO WS-C908-RETCODE.                                     
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2100-GET-CHAR-VALUES.                                                    
      *-------------------------------------------------------------            
                                                                                
           SET WS-CHAR-IDX   TO  1.                                             
           SEARCH WS-CHAR-TABLE                                                 
              AT END                                                            
                 MOVE +8     TO WS-C908-RETCODE                                 
                 GO TO 0000-EXIT                                                
              WHEN WS-NUM-TXT-CHAR (WS-SUB) = WS-CHAR (WS-CHAR-IDX)             
                   MOVE WS-CHAR-VALUE (WS-CHAR-IDX)  TO                         
                   WS-NUM-TXT-VALUE (WS-SUB)                                    
           END-SEARCH.                                                          
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2200-MULTIPLY-WEIGHTS.                                                   
      *-------------------------------------------------------------            
                                                                                
           COMPUTE WS-PRODUCT (WS-SUB) = WS-NUM-TXT-VALUE (WS-SUB) *            
                                         WS-WEIGHT (WS-SUB).                    
                                                                                
           COMPUTE WS-ACC-PRODUCT = WS-ACC-PRODUCT +                            
                                    WS-PRODUCT (WS-SUB).                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
