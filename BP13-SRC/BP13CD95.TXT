       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD95.                                                 
       AUTHOR.        EFC.                                                      
       DATE-WRITTEN.  27/06/2012.                                               
                                                                                
      *****************************************************************         
      * SYSTEM NAME :                                                 *         
      *===============================================================*         
      * OBJECTIVE : 1. TO COMPUTE AND UPDATE BP13KB25                 *         
      *           :    NUMBERS OF DAYS FROM OTP DATE                  *         
      *           :    NUMBERS OF DAYS FROM DATE COMMIT               *         
      *                                                               *         
      * I/  FILES : I -   BP13FB25                                    *         
      *  /O FILES : I -   BP13KB25                                    *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001 ASSIGN       TO  SY02F001.                           
           SELECT BP13FB25 ASSIGN       TO  BP13FB25.                           
           SELECT BP13KB25 ASSIGN       TO  BP13KB25                            
                           ORGANIZATION IS  INDEXED                             
                           ACCESS MODE  IS  DYNAMIC                             
                           RECORD KEY   IS  KB25-KEY-FLD                        
                           FILE STATUS  IS  KB25-STATUS.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13FB25                                                              
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 3400 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13FB25.                                                           
                                                                                
       FD  BP13KB25                                                             
           RECORD CONTAINS 3400 CHARACTERS.                                     
                                                                                
       COPY BP13KB25.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-INTEGERS.                                                          
          05 WS-OTP-GAP             PIC 9(3) VALUE ZEROES.                      
          05 WS-COMMIT-GAP          PIC 9(3) VALUE ZEROES.                      
          05 WS-CURR-DTE            PIC 9(8) VALUE ZEROES.                      
          05 WS-COMMIT-DTE          PIC 9(8) VALUE ZEROES.                      
          05 WS-ACCEPT-DTE          PIC 9(8) VALUE ZEROES.                      
                                                                                
       01 WS-VARIABLES.                                                         
          05  KB25-STATUS           PIC X(2) VALUE ZEROES.                      
          05  FB25-EOF              PIC X(1) VALUE 'N'.                         
          05  WS-UPDATE             PIC X(1) VALUE 'N'.                         
                                                                                
       01 WS-COUNTERS.                                                          
          05  WS-FB25-READ-CTR      PIC 9(6) VALUE ZEROES.                      
          05  WS-KB25-FND-CTR       PIC 9(6) VALUE ZEROES.                      
          05  WS-KB25-NOTFND-CTR    PIC 9(6) VALUE ZEROES.                      
          05  WS-KB25-UPD-CTR       PIC 9(6) VALUE ZEROES.                      
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *=================================================================        
       0000-MAIN.                                                               
      *=================================================================        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1500-READ-BP13FB25 THRU 1500-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
               UNTIL FB25-EOF = 'Y'.                                            
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       1000-OPEN-FILES.                                                         
      *=================================================================        
           OPEN INPUT     BP13FB25                                              
                          SY02F001                                              
                I-O       BP13KB25.                                             
                                                                                
           IF   KB25-STATUS NOT = ZEROES                                        
                DISPLAY 'ERR OPEN KB25  ' KB25-STATUS                           
                MOVE KB25-STATUS          TO RETURN-CODE                        
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       1500-READ-BP13FB25.                                                      
      *=================================================================        
           READ BP13FB25 AT END                                                 
               MOVE 'Y'  TO  FB25-EOF                                           
               GO TO  2000-EXIT.                                                
           ADD 1                          TO WS-FB25-READ-CTR.                  
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       2000-PROCESS.                                                            
      *=================================================================        
           MOVE SPACE TO WS-UPDATE                                              
           MOVE SPACES                        TO BP13KB25-REC.                  
           INITIALIZE                         BP13KB25-REC                      
                                              WS-INTEGERS.                      
                                                                                
           MOVE FB25-KEY-FLD                  TO KB25-KEY-FLD.                  
                                                                                
           READ BP13KB25.                                                       
           EVALUATE KB25-STATUS                                                 
                                                                                
           WHEN 00                                                              
              ADD 1                           TO WS-KB25-FND-CTR                
              PERFORM 2100-CHECK-TO-PROCESS   THRU 2100-EXIT                    
                                                                                
           WHEN 23                                                              
              ADD 1                           TO WS-KB25-NOTFND-CTR             
              DISPLAY 'RECORD NOT FOUND: '    FB25-KEY-FLD                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KB25 : ' KB25-STATUS                   
              MOVE KB25-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                    
                                                                                
           END-EVALUATE.                                                        
                                                                                
           PERFORM 1500-READ-BP13FB25         THRU 1500-EXIT.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       2100-CHECK-TO-PROCESS.                                                   
      *=================================================================        
           IF  KB25-DTE-ACCEPT           NOT =  SPACES AND ZEROS                
                                                                                
               IF KB25-DTE-DBSS-CONFIRM      =  SPACES OR ZEROES                
                   PERFORM 3100-COMPUTE         THRU 3100-EXIT                  
                   MOVE 'Y' TO WS-UPDATE                                        
                                                                                
               ELSE                                                             
                   IF KB25-NUM-DAY-OTP = SPACES OR ZEROS                        
                      CONTINUE                                                  
                   ELSE                                                         
                      MOVE ZEROES               TO KB25-NUM-DAY-OTP             
                      MOVE 'Y' TO WS-UPDATE                                     
                   END-IF                                                       
                                                                                
                   IF KB25-NUM-DAY-COMMIT = SPACES OR ZEROS                     
                      CONTINUE                                                  
                   ELSE                                                         
                      MOVE ZEROES               TO KB25-NUM-DAY-COMMIT          
                      MOVE 'Y' TO WS-UPDATE                                     
                   END-IF                                                       
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF WS-UPDATE = 'Y'                                                   
               PERFORM 5000-REWRITE-KB25        THRU 5000-EXIT                  
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       3100-COMPUTE.                                                            
      *=================================================================        
           PERFORM 3200-COMPUTE-DAYOTP          THRU 3200-EXIT.                 
                                                                                
           IF  KB25-DTE-COMMIT    NOT =  SPACES AND ZEROES                      
               PERFORM 3300-COMPUTE-DAYCOMMIT   THRU 3300-EXIT                  
                                                                                
           ELSE                                                                 
               MOVE ZEROES                      TO KB25-NUM-DAY-COMMIT          
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       3200-COMPUTE-DAYOTP.                                                     
      *=================================================================        
           MOVE F001-DTE-CURRENT                TO WS-CURR-DTE.                 
           MOVE KB25-DTE-ACCEPT                 TO WS-ACCEPT-DTE.               
                                                                                
           IF WS-CURR-DTE <= WS-ACCEPT-DTE                                      
              MOVE ZEROES                       TO KB25-NUM-DAY-OTP             
                                                                                
           ELSE                                                                 
              COMPUTE WS-OTP-GAP =                                              
                      FUNCTION INTEGER-OF-DATE (WS-CURR-DTE)                    
                    - FUNCTION INTEGER-OF-DATE (WS-ACCEPT-DTE)                  
                                                                                
              MOVE    WS-OTP-GAP                TO KB25-NUM-DAY-OTP             
                                                                                
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=================================================================        
       3300-COMPUTE-DAYCOMMIT.                                                  
      *=================================================================        
           MOVE F001-DTE-CURRENT                TO WS-CURR-DTE.                 
           MOVE KB25-DTE-COMMIT                 TO WS-COMMIT-DTE.               
                                                                                
           IF WS-COMMIT-DTE <= WS-CURR-DTE                                      
              MOVE ZEROES                       TO KB25-NUM-DAY-COMMIT          
                                                                                
           ELSE                                                                 
              COMPUTE WS-COMMIT-GAP =                                           
                      FUNCTION INTEGER-OF-DATE (WS-COMMIT-DTE)                  
                    - FUNCTION INTEGER-OF-DATE (WS-CURR-DTE)                    
                                                                                
              MOVE    WS-COMMIT-GAP             TO KB25-NUM-DAY-COMMIT          
                                                                                
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       5000-REWRITE-KB25.                                                       
      *=================================================================        
           REWRITE BP13KB25-REC.                                                
                                                                                
           EVALUATE KB25-STATUS                                                 
                                                                                
               WHEN 00                                                          
                   ADD 1                      TO WS-KB25-UPD-CTR                
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'ERROR REWRITE ON BP13KB25:  ' KB25-STATUS           
                   MOVE KB25-STATUS           TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
                                                                                
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================================================================        
       9000-CLOSE-FILES.                                                        
      *=================================================================        
           CLOSE BP13FB25                                                       
                 SY02F001                                                       
                 BP13KB25.                                                      
                                                                                
           IF KB25-STATUS NOT = ZEROES                                          
              DISPLAY 'ERROR CLOSE FILE  ' KB25-STATUS                          
           END-IF.                                                              
                                                                                
           DISPLAY 'TOTAL FB25 READ         ' WS-FB25-READ-CTR                  
           DISPLAY 'TOTAL KB25 FOUND        ' WS-KB25-FND-CTR                   
           DISPLAY 'TOTAL KB25 NOT FOUND    ' WS-KB25-NOTFND-CTR                
           DISPLAY 'TOTAL KB25 UPDATED      ' WS-KB25-UPD-CTR                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
