      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID.   BP13CH39.                                                  
      *AUTHOR.       ZARITA DANE SARMIENTO.                                     
      *DATE-WRITTEN. 06/08/2014.                                                
      *------------------------------------------------------------*            
      *                   SYSTEM OF COMMITMENT  (BP13)             *            
      *------------------------------------------------------------*            
      *                                                            *            
      *  OBJECTIVE   : 1.  TO ENSURE EACH CASES WITH SAME NRIC     *            
      *                    WILL BE ASSIGNED TO ONLY 1 OIC          *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F022  -  OIC RE-ASSIGN FILE         *            
      *                2.  BP13K024  -  LIST OF OIC                *            
      *                3.  BP13K022  -  OIC FILE                   *            
      *                6.  BP13K028  -  OIC HISTORY FILE           *            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *------------------------------------------------------------*            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F022 ASSIGN TO BP13F022.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K028 ASSIGN TO BP13K028                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K028-KEY-FLD                               
                  FILE STATUS     IS WS-K028-STATUS.                            
                                                                                
           SELECT BP13K024 ASSIGN TO BP13K024                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K024-KEY-FLD                               
                  FILE STATUS     IS WS-K024-STATUS.                            
                                                                                
                                                                                
           SELECT P13F022A ASSIGN TO P13F022A.                                  
           SELECT P13F022B ASSIGN TO P13F022B.                                  
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------*            
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   BP13K028                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K028.                                                           
                                                                                
       FD   BP13F022                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       COPY BP13F022.                                                           
                                                                                
       FD   P13F022A                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13F022A-REC   PIC X(100).                                          
                                                                                
       FD   P13F022B                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS    0 RECORDS                                         
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
                                                                                
       01   P13F022B-REC   PIC X(100).                                          
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WORK-AREAS.                                                          
           05  WS-F022-READ             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-RPT1-CNT              PIC 9(08)  VALUE 0.                     
           05  WS-TOTAL-CASES           PIC 9(08)  VALUE 0.                     
           05  WS-TOTAL-CASES-CTR       PIC 9(08)  VALUE 0.                     
           05  WS-1-CASE-ONLY           PIC 9(08)  VALUE 0.                     
           05  WS-CTR-WRITE             PIC 9(02)  VALUE 0.                     
           05  WS-F022B-WRITE           PIC 9(08)  VALUE 0.                     
           05  WS-F022A-WRITE           PIC 9(08)  VALUE 0.                     
           05  WS-K022-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-K022-REWRITE          PIC 9(08)  VALUE 0.                     
           05  WS-K022-DELETE           PIC 9(08)  VALUE 0.                     
           05  WS-K028-WRITE            PIC 9(08)  VALUE 0.                     
           05  WS-MATCH                 PIC 9(08)  VALUE 0.                     
           05  WS-K022-NFND             PIC 9(08)  VALUE 0.                     
                                                                                
           05  WS-F022-EOF              PIC X(01)  VALUE 'N'.                   
           05  WS-FND                   PIC X(01)  VALUE 'N'.                   
           05  WS-K022-STATUS           PIC X(02)  VALUE SPACES.                
           05  WS-K028-STATUS           PIC X(02)  VALUE SPACES.                
           05  WS-K024-STATUS           PIC X(02)  VALUE SPACES.                
                                                                                
                                                                                
                                                                                
           05  WS-COUNTER               PIC 9(03)  VALUE 1.                     
           05  WS-COUNTER1              PIC 9(03)  VALUE 0.                     
           05  WS-INTERVAL-OIC          PIC 9(03)  VALUE 0.                     
           05  WS-CTR-CHECK             PIC 9(03)  VALUE 0.                     
           05  WS-COUNT                 PIC 9(03)  VALUE 0.                     
           05  WS-COUNT1                PIC 9(03)  VALUE 0.                     
           05  WS-CTR                   PIC 9(03)  VALUE 0.                     
           05  WS-INTERVAL              PIC 9(02)   VALUE 1.                    
           05  WS-TOT                   PIC 9(04)   VALUE 0.                    
           05  WS-MAX-CASE              PIC 9(04)   VALUE 0.                    
                                                                                
           05  WS-ROUND                 PIC 9(03)  VALUE 0.                     
           05  WS-ROUND-ED              PIC Z(03).                              
                                                                                
           05  WS-LAST                  PIC X(06)  VALUE SPACES.                
           05  WS-OIC-FOUND             PIC X(05)  VALUE SPACES.                
           05  WS-FLAG                  PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY          PIC 9(04).                              
               10  WS-SYS-MM            PIC 9(02).                              
               10  WS-SYS-DD            PIC 9(02).                              
                                                                                
           05  WS-EDITED-DATE           PIC X(10)  VALUE SPACES.                
                                                                                
       01  WS-F026-VAR.                                                         
           05  WS-F026-KEY-FLD          PIC X(11)  VALUE SPACES.                
           05  WS-F026-CDE-COMPLETE     PIC X(01)  VALUE SPACES.                
           05  WS-F026-NUM-INTVL        PIC 9(02)  VALUE 0.                     
           05  WS-F026-NUM-MAX-INTVL    PIC 9(04)  VALUE 0.                     
           05  WS-F026-CDE-ROUND        PIC X(01)  VALUE SPACES.                
                                                                                
       01  WS-F022-KEY.                                                         
           05  WS-F022-NUM-OIC          PIC X(05)  VALUE SPACES.                
           05  WS-F022-NUM-REGN         PIC X(08)  VALUE SPACES.                
           05  WS-F022-NUM-NRIC         PIC X(09)  VALUE SPACES.                
                                                                                
       01  WS-OIC-TABLE.                                                        
           05 WS-OIC-ASGN  OCCURS 10 TIMES.                                     
              10 WS-K022-NUM-OIC         PIC X(05).                             
              10 WS-K022-NUM-REGN        PIC X(08).                             
              10 WS-K022-CDE-ALLO-CAT    PIC X(03).                             
              10 WS-K022-CDE-BANKLOAN    PIC X(01).                             
              10 WS-K022-CDE-ALERT       PIC X(01).                             
              10 WS-K022-CDE-ALERT-TYPE  PIC X(02).                             
              10 WS-K022-NUM-OIC-APPL    PIC X(05).                             
              10 WS-K022-NUM-PRE-OIC-TAG PIC X(01).                             
              10 WS-K022-NUM-NRIC        PIC X(09).                             
              10 FILLER                  PIC X(49).                             
              10 WS-K022-NUM-USERID      PIC X(08).                             
              10 WS-K022-DTE-UPDATE      PIC X(08).                             
                                                                                
       01 WS-K022-REC                   PIC X(100)   VALUE SPACES.              
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2100-READ-F022         THRU 2100-EXIT                        
           PERFORM 2600-RE-ASSIGN         THRU 2600-EXIT                        
                   UNTIL WS-F022-EOF = 'Y'.                                     
                                                                                
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT  BP13F022                                                 
                       BP13K024                                                 
                I-O    BP13K022                                                 
                       BP13K028                                                 
                OUTPUT P13F022A                                                 
                       P13F022B.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 OPEN ERROR, STATUS IS ' WS-K024-STATUS          
              MOVE WS-K024-STATUS              TO RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K028-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K028 OPEN ERROR, STATUS IS ' WS-K028-STATUS          
              MOVE WS-K028-STATUS              TO RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/'                                      
                   WS-SYSTEM-DATE(5:2) '/'                                      
                   WS-SYSTEM-DATE(1:4)                                          
                   DELIMITED BY SIZE                                            
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
           MOVE  'OLD-OIC;REGN-NUM;NEW-OIC;NRIC;' TO P13F022A-REC               
           WRITE P13F022A-REC.                                                  
           MOVE SPACES   TO P13F022A-REC.                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       2100-READ-F022.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13F022 NEXT RECORD                                            
                AT END MOVE 'Y' TO  WS-F022-EOF                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F022-READ.                                               
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2500-WRITE-TABLE.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE 1 TO WS-CTR WS-COUNTER.                                         
           MOVE 0 TO WS-COUNTER1.                                               
           MOVE 0 TO WS-TOTAL-CASES.                                            
                                                                                
           MOVE SPACES   TO   WS-OIC-TABLE                                      
           INITIALIZE         WS-OIC-TABLE.                                     
           MOVE SPACES   TO   P13F022A-REC                                      
           INITIALIZE         P13F022A-REC.                                     
                                                                                
           MOVE SPACES   TO   P13F022B-REC                                      
           INITIALIZE         P13F022B-REC.                                     
                                                                                
           MOVE F022-NUM-NRIC   TO WS-F022-NUM-NRIC.                            
                                                                                
           PERFORM UNTIL WS-F022-EOF = 'Y' OR                                   
                         WS-CTR > 20   OR                                       
                         WS-F022-NUM-NRIC NOT = F022-NUM-NRIC                   
                                                                                
              MOVE BP13F022-REC     TO WS-OIC-ASGN(WS-CTR)                      
                                                                                
              ADD 1 TO WS-CTR WS-COUNTER1                                       
              PERFORM 2100-READ-F022     THRU 2100-EXIT                         
           END-PERFORM.                                                         
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2600-RE-ASSIGN.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'           TO WS-OIC-FOUND.                                  
           MOVE SPACES        TO WS-F022-NUM-OIC.                               
           PERFORM 2500-WRITE-TABLE       THRU 2500-EXIT.                       
                                                                                
           IF WS-COUNTER1 = 1                                                   
              ADD 1 TO WS-1-CASE-ONLY                                           
              GO TO 2600-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE 1    TO WS-CTR                                                  
                                                                                
           PERFORM UNTIL WS-OIC-FOUND = 'Y' OR                                  
                         WS-CTR    >  WS-COUNTER1                               
              PERFORM 2800-GET-PRE-OIC   THRU 2800-EXIT                         
             ADD 1 TO WS-CTR                                                    
             DISPLAY 'PRE OIC = ' K024-NUM-OIC                                  
           END-PERFORM,                                                         
                                                                                
           MOVE 1    TO WS-CTR                                                  
                                                                                
           IF WS-F022-NUM-OIC NOT = SPACES AND LOW-VALUES                       
             PERFORM UNTIL WS-CTR    >  WS-COUNTER1                             
               PERFORM 2700-CHECK-OIC   THRU 2700-EXIT                          
               ADD 1 TO WS-CTR                                                  
             END-PERFORM                                                        
           END-IF.                                                              
                                                                                
      *    PERFORM 2100-READ-F022    THRU 2100-EXIT.                            
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2700-CHECK-OIC.                                                          
      *------------------------------------------------------------*            
                                                                                
           IF WS-K022-NUM-OIC-APPL(WS-CTR)  = SPACES OR  LOW-VALUES             
              IF WS-K022-NUM-OIC(WS-CTR) NOT = WS-F022-NUM-OIC                  
                 MOVE SPACES   TO BP13K022-REC                                  
                 MOVE WS-K022-NUM-OIC(WS-CTR)  TO K022-NUM-OIC                  
                 MOVE WS-K022-NUM-REGN(WS-CTR) TO K022-NUM-REGN                 
                 STRING  K022-NUM-OIC ';' K022-NUM-REGN ';'                     
                         WS-F022-NUM-OIC  ';' WS-K022-NUM-NRIC(WS-CTR)          
                 DELIMITED BY SIZE INTO P13F022A-REC                            
                 WRITE P13F022A-REC                                             
                 ADD 1 TO WS-F022A-WRITE                                        
                 PERFORM 3100-READ-K022   THRU 3100-EXIT                        
                 IF WS-FND = 'Y'                                                
                   PERFORM 4600-WRITE-K028   THRU 4600-EXIT                     
                   PERFORM 4000-DELETE-K022 THRU 4000-EXIT                      
                   MOVE WS-K022-REC         TO BP13K022-REC                     
                   MOVE WS-F022-NUM-OIC     TO K022-NUM-OIC                     
                   PERFORM 4100-WRITE-K022  THRU 4100-EXIT                      
                 ELSE                                                           
                   ADD 1 TO WS-K022-NFND                                        
                 END-IF                                                         
              ELSE                                                              
                 ADD 1 TO WS-MATCH                                              
              END-IF                                                            
           ELSE                                                                 
              IF WS-K022-NUM-OIC-APPL(WS-CTR) NOT  = WS-F022-NUM-OIC            
                 MOVE SPACES   TO BP13K022-REC                                  
                 MOVE WS-K022-NUM-OIC(WS-CTR) TO K022-NUM-OIC                   
                 MOVE WS-K022-NUM-REGN(WS-CTR) TO K022-NUM-REGN                 
                 ADD 1 TO WS-F022A-WRITE                                        
                 PERFORM 3100-READ-K022 THRU 3100-EXIT                          
                 IF WS-FND = 'Y'                                                
                   STRING  K022-NUM-OIC-APPL ';' K022-NUM-REGN ';'              
                           WS-F022-NUM-OIC ';' WS-K022-NUM-NRIC(WS-CTR)         
                   DELIMITED BY SIZE INTO P13F022A-REC                          
                   WRITE P13F022A-REC                                           
                   PERFORM 4600-WRITE-K028   THRU 4600-EXIT                     
                   MOVE WS-F022-NUM-OIC  TO K022-NUM-OIC-APPL                   
                   PERFORM 4700-REWRITE-K022 THRU 4700-EXIT                     
                 END-IF                                                         
              ELSE                                                              
                 ADD 1 TO WS-MATCH                                              
              END-IF                                                            
           END-IF.                                                              
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2800-GET-PRE-OIC.                                                        
      *------------------------------------------------------------*            
                                                                                
           IF WS-K022-NUM-OIC-APPL(WS-CTR) NOT = SPACES AND LOW-VALUES          
                 MOVE SPACES   TO BP13K024-REC                                  
                 MOVE WS-K022-NUM-OIC-APPL(WS-CTR) TO K024-NUM-OIC              
                 PERFORM 3200-READ-K024 THRU 3200-EXIT                          
                 IF K024-CDE-BLOCK = 'N'                                        
                    MOVE K024-NUM-OIC TO WS-F022-NUM-OIC                        
                    MOVE 'Y'   TO WS-OIC-FOUND                                  
                 END-IF                                                         
           ELSE                                                                 
                 MOVE SPACES   TO BP13K024-REC                                  
                 MOVE WS-K022-NUM-OIC(WS-CTR) TO K024-NUM-OIC                   
                 PERFORM 3200-READ-K024 THRU 3200-EXIT                          
                 IF K024-CDE-BLOCK = 'N'                                        
                    MOVE K024-NUM-OIC TO WS-F022-NUM-OIC                        
                    MOVE 'Y'   TO WS-OIC-FOUND                                  
                 END-IF                                                         
           END-IF.                                                              
       2800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-READ-K022.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13K022.                                                       
                                                                                
           EVALUATE WS-K022-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y'   TO WS-FND                                            
             WHEN 23                                                            
                MOVE 'N'   TO WS-FND                                            
                MOVE SPACES    TO BP13K022-REC                                  
             WHEN OTHER                                                         
                 DISPLAY 'READ BP13K022 FAIL. STATUS - '                        
                                            WS-K022-STATUS                      
                 MOVE  WS-K022-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3200-READ-K024.                                                          
      *------------------------------------------------------------*            
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE WS-K024-STATUS                                              
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE SPACES    TO BP13K024-REC                                  
             WHEN OTHER                                                         
                 DISPLAY 'READ BP13K024 FAIL. STATUS - '                        
                                            WS-K024-STATUS                      
                 MOVE  WS-K024-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-DELETE-K022.                                                        
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES         TO WS-K022-REC.                                  
           INITIALIZE             WS-K022-REC.                                  
                                                                                
           MOVE BP13K022-REC   TO WS-K022-REC.                                  
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K022-DELETE                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR DELETING BP13K022, STATUS - '                  
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4100-WRITE-K022.                                                         
      *------------------------------------------------------------*            
                                                                                
           WRITE BP13K022-REC.                                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-K022-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING BP13K022, STATUS - '                   
                                            WS-K022-STATUS                      
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4600-WRITE-K028.                                                         
      *------------------------------------------------------------*            
                                                                                
           MOVE SPACES               TO BP13K028-REC.                           
           INITIALIZE                   BP13K028-REC.                           
                                                                                
           MOVE K022-NUM-OIC         TO K028-NUM-OIC.                           
           MOVE K022-NUM-REGN        TO K028-NUM-REGN.                          
           MOVE WS-SYSTEM-DATE       TO K028-DTE-HISTORY.                       
           MOVE K022-CDE-ALLO-CAT    TO K028-CDE-ALLO-CAT.                      
           MOVE K022-CDE-BANKLOAN    TO K028-CDE-BANKLOAN.                      
           MOVE K022-CDE-ALERT       TO K028-CDE-ALERT.                         
           MOVE K022-NUM-USERID      TO K028-NUM-USERID.                        
           MOVE K022-DTE-UPDATE      TO K028-DTE-UPDATE.                        
                                                                                
           WRITE  BP13K028-REC.                                                 
                                                                                
           EVALUATE WS-K028-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
              WHEN 22                                                           
                 ADD 1 TO WS-K028-WRITE                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR WRITING  BP13K028, STATUS - '                  
                                            WS-K028-STATUS                      
                 MOVE WS-K028-STATUS        TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------***          
       4700-REWRITE-K022.                                                       
      *------------------------------------------------------------***          
                                                                                
            MOVE 'BP13CH39'            TO K022-NUM-USERID.                      
            MOVE WS-SYSTEM-DATE        TO K022-DTE-UPDATE.                      
                                                                                
            REWRITE BP13K022-REC.                                               
                                                                                
            EVALUATE WS-K022-STATUS                                             
            WHEN 00                                                             
            WHEN 02                                                             
               ADD 1 TO WS-K022-REWRITE                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR REWRITING BP13K022. STATUS IS '                   
                        WS-K022-STATUS                                          
               MOVE WS-K022-STATUS              TO RETURN-CODE                  
               PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13K022                                                       
                 BP13K024                                                       
                 BP13K028                                                       
                 BP13F022                                                       
                 P13F022A.                                                      
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BP13K022 FAIL. STATUS-'                         
                                         WS-K022-STATUS                         
                 MOVE WS-K022-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BP13K024 FAIL. STATUS-'                         
                                         WS-K024-STATUS                         
                 MOVE WS-K024-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K028-STATUS NOT = 00 AND 97                                    
                 DISPLAY 'CLOSE BP13K028 FAIL. STATUS-'                         
                                         WS-K028-STATUS                         
                 MOVE WS-K028-STATUS  TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
                                                                                
           DISPLAY '*----------- BP13CH39 -------------*'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY ' 1. BP13F022 READ      : '  WS-F022-READ.                   
           DISPLAY ' 2. WITH 1 CASE ONLY   : '  WS-1-CASE-ONLY.                 
           DISPLAY ' 3. MORE THAN 3 CASE   : '  WS-F022B-WRITE.                 
           DISPLAY ' 4. >3 CASES WRT F022B : '  WS-F022B-WRITE.                 
           DISPLAY ' 5. UPDATED WRT F022A  : '  WS-F022A-WRITE.                 
           DISPLAY ' 6. BP13K028 WRITE(HIS): '  WS-K022-WRITE.                  
           DISPLAY ' 7. DELETED BP13K022   : '  WS-K022-DELETE.                 
           DISPLAY ' 8. UPD(OICAPPL) K022  : '  WS-K022-REWRITE.                
           DISPLAY ' 9. MATCH OIC          : '  WS-MATCH.                       
           DISPLAY '10. NOT FOUND IN K022  : '  WS-K022-NFND.                   
           DISPLAY SPACES.                                                      
           DISPLAY '*----------------------------------*'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
