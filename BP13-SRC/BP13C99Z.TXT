       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C99Z.                                                  
       AUTHOR.       RJB1.                                                      
      *DATE-WRITTEN. 30/11/2016.                                                
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO CREATE IN-TRAY ALERT TO OIC FOR STATUS   *            
      *                REPORT OF SATP/TP CASES PENDING > 3 MONTHS  *            
      *                USING BE08C008                              *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136525 20161130 RJB1  NEW PROGRAM                        *            
      * BP136525 20161207 RJB1  CHANGED THE URL                    *            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13FC40 ASSIGN           TO BP13FC40.                       
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FC40                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 100 CHARACTERS           
                                       RECORDING MODE IS F                      
                                       LABEL RECORDS ARE STANDARD.              
       COPY BP13FC40.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONSTANTS.                                                        
           05  WS-TXN-CREATE           PIC X(02)  VALUE 'C'.                    
           05  WS-SYSTEM-ID            PIC X(04)  VALUE 'BP13'.                 
           05  WS-NUM-PRCS-CTGRY       PIC X(04)  VALUE '0137'.                 
           05  WS-NUM-BRNCH            PIC X(06)  VALUE '000001'.               
           05  WS-DEFAULT-DTE          PIC X(10)  VALUE '0001-01-01'.           
           05  WS-PROGRAM-ID           PIC X(08)  VALUE 'BP13C99Z'.             
           05  WS-STATUS-NEW           PIC X(03)  VALUE '000'.                  
           05  WS-PLANNED-INSPTN       PIC X(03)  VALUE '101'.                  
           05  WS-14-DAYS              PIC 9(02)  VALUE 14.                     
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURRENT-DTE-TME      PIC 9(14)  VALUE ZEROES.                 
           05  WS-DTE-CURRENT          PIC X(10)  VALUE SPACES.                 
           05  WS-TME-CURRENT          PIC X(08)  VALUE SPACES.                 
           05  WS-DTE-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-INT-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE             PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE-TXT         PIC X(10)  VALUE SPACES.                 
           05  WS-DTE-DUE.                                                      
               10 WS-DTE-CCYY          PIC X(04).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-MM            PIC X(02).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-DD            PIC X(02).                               
                                                                                
       01  WS-STATUS.                                                           
           05  WS-OIC-EXIST-TAG        PIC X(01)  VALUE 'N'.                    
           05  WS-BP13FC40-EOF         PIC X(01)  VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT             PIC 9(07)  VALUE ZEROES.                 
           05  WS-WRITE-CNT            PIC 9(07)  VALUE ZEROES.                 
                                                                                
       01  WS-FIELDS.                                                           
           05  WS-STAFF                PIC X(06)  VALUE SPACES.                 
           05  WS-OIC                  PIC X(05)  VALUE SPACES.                 
                                                                                
      ******************************************************************        
      *  TEXT URL VARIABLES                                                     
      ******************************************************************        
       01  WS-INBOX-TXT-TARGET-URL.                                             
           05  FILLER              PIC X(67) VALUE                              
              '/be10/home/Newsales/sssCrm/pendingKeyIssue/updateReasonCo        
      -       'de?regnNo='.                                                     
           05  WS-CASE-REF-URL     PIC X(12).                                   
                                                                                
       01  WS-INBOX-TXT-REMARK.                                                 
           05  FILLER              PIC X(51) VALUE                              
              'Status Report of SATP/TP Cases Pending > 3 months. '.            
           05  WS-REGN-REMARK      PIC X(08) VALUE SPACES.                      
                                                                                
      ******************************************************************        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      ******************************************************************        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
                                                                                
       COPY BE08W008.                                                           
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-LOGIC.                                                         
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FC40       THRU 2000-EXIT.                     
           IF WS-BP13FC40-EOF = 'Y'                                             
              DISPLAY 'FILE BP13FC40 IS  EMPTY'                                 
           END-IF                                                               
           PERFORM 3000-INSERT-INBOX-INTRY  THRU 3000-EXIT                      
             UNTIL WS-BP13FC40-EOF = 'Y'.                                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-FILES.                                                         
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FC40.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-BP13FC40.                                                      
      ****************************************************************          
                                                                                
           READ BP13FC40 AT END                                                 
                         MOVE 'Y'  TO WS-BP13FC40-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-CNT                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2200-CALL-BP13CCNT.                                                      
      ****************************************************************          
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROS                                                           
              CONTINUE                                                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                  WS-OIC ', REGN : ' FC40-NUM-REGN                              
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-INSERT-INBOX-INTRY.                                                 
      ****************************************************************          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:14) TO WS-CURRENT-DTE-TME.              
                                                                                
           STRING WS-CURRENT-DTE-TME(1:4) '-'                                   
                  WS-CURRENT-DTE-TME(5:2) '-'                                   
                  WS-CURRENT-DTE-TME(7:2) DELIMITED BY SIZE                     
                  INTO WS-DTE-CURRENT.                                          
                                                                                
           STRING WS-CURRENT-DTE-TME(9:2)                                       
                  '.'                                                           
                  WS-CURRENT-DTE-TME(11:2)                                      
                  '.'                                                           
                  WS-CURRENT-DTE-TME(13:2) DELIMITED BY SIZE                    
                  INTO  WS-TME-CURRENT.                                         
                                                                                
           MOVE SPACES                      TO  BE08W008-COMMAREA.              
           INITIALIZE                           BE08W008-COMMAREA.              
                                                                                
           PERFORM 3010-CREATE-URL-REMARK THRU  3010-EXIT.                      
           PERFORM 3020-COMPUTE-DATE-DUE  THRU  3020-EXIT.                      
           PERFORM 3030-GET-OIC           THRU  3030-EXIT.                      
                                                                                
           MOVE WS-TXN-CREATE               TO  W008-IN-TXN-TYPE.               
           MOVE WS-SYSTEM-ID                TO  W008-IN-SYSTEM-ID.              
           MOVE WS-NUM-PRCS-CTGRY           TO  W008-IN-NUM-PRCS-CTGRY.         
           MOVE WS-NUM-BRNCH                TO  W008-IN-NUM-BRNCH.              
           MOVE FC40-NUM-REGN               TO  W008-IN-NUM-CASE-REF.           
           MOVE WS-STAFF                    TO  W008-IN-NUM-STAFF.              
           MOVE WS-INBOX-TXT-TARGET-URL     TO  W008-IN-TXT-TARGET-URL.         
           MOVE WS-INBOX-TXT-REMARK         TO  W008-IN-TXT-REMARK.             
           MOVE WS-DTE-CURRENT              TO  W008-IN-DTE-INBOX.              
           MOVE WS-TME-CURRENT              TO  W008-IN-TME-INBOX.              
           MOVE WS-DTE-DUE                  TO  W008-IN-DUE-DATE.               
                                                                                
           CALL 'BE08C008'    USING BE08W008-COMMAREA.                          
           IF W008-OUT-RTN-MSGCDE = '0' OR 'W'                                  
              ADD 1                         TO  WS-WRITE-CNT                    
           ELSE                                                                 
              DISPLAY 'ERROR RETURNED CALLING BE08C008 SUBR (C) : '             
                                          W008-IN-NUM-CASE-REF                  
              DISPLAY '{ ' W008-OUT-RTN-MSGCDE ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-MSGNO  ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-DESC   ' }'                             
           END-IF.                                                              
           PERFORM 2000-READ-BP13FC40       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3010-CREATE-URL-REMARK.                                                  
      ****************************************************************          
                                                                                
           MOVE SPACES           TO  WS-REGN-REMARK                             
                                     WS-CASE-REF-URL.                           
                                                                                
           MOVE FC40-NUM-REGN    TO  WS-CASE-REF-URL                            
                                     WS-REGN-REMARK.                            
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3020-COMPUTE-DATE-DUE.                                                   
      ****************************************************************          
                                                                                
           MOVE WS-CURRENT-DTE-TME(1:8) TO WS-DTE-TEMP.                         
                                                                                
           COMPUTE WS-INT-TEMP =                                                
                   FUNCTION INTEGER-OF-DATE (WS-DTE-TEMP) + WS-14-DAYS.         
                                                                                
           COMPUTE WS-COMP-DTE =                                                
                   FUNCTION DATE-OF-INTEGER (WS-INT-TEMP).                      
                                                                                
           MOVE WS-COMP-DTE             TO  WS-COMP-DTE-TXT.                    
                                                                                
           MOVE WS-COMP-DTE-TXT(1:4)    TO  WS-DTE-CCYY.                        
           MOVE WS-COMP-DTE-TXT(5:2)    TO  WS-DTE-MM.                          
           MOVE WS-COMP-DTE-TXT(7:2)    TO  WS-DTE-DD.                          
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3030-GET-OIC.                                                            
      ****************************************************************          
                                                                                
           MOVE SPACES                 TO WS-BP13CCNT-LINK                      
           MOVE 'EML'                  TO LINK-CNTCT-TYPE                       
           MOVE FC40-NUM-OIC           TO LINK-STAFF-NICK                       
           MOVE SPACES                 TO LINK-STAFF-NUM                        
           PERFORM 2200-CALL-BP13CCNT  THRU 2200-EXIT                           
           MOVE LINK-STAFF-NUM         TO WS-STAFF.                             
                                                                                
       3030-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY '---- BP13C99Z CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BP13FC40   = '                         
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF RECS SENT TO BE08        = '                         
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13FC40.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
