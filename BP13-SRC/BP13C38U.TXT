      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38U.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  OCTOBER 10, 2017.                                         
      *===============================================================*         
      * OBJECTIVE  :  PRINT APPOINTMENT DETAILS ON SOCA , SATP & TP   *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F454                                     *         
      *               2. BP13K800                                     *         
      *               2. BP13K022                                     *         
      * OUTPUT FILE:  1. BP13L38U                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137027 10/10/2017 PCL4 NEW PROGRAM.                         *         
      * BP137053 10/11/2017 PCL4 TO UPDATE EMAIL FORMAT               *         
      * BP137172 31/01/2018 RJB1 TO INCLUDE ADDRESS IN REPORT         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F454  ASSIGN        TO BP13F454.                         
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K022  ASSIGN        TO BP13K022                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE KEY IS K022-NUM-REGN                     
                             FILE STATUS   IS WS-K022-STATUS.                   
                                                                                
           SELECT  BP13L38U ASSIGN         TO BP13L38U.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F454                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F454.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  BP13L38U                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS V                              
                               LABEL RECORD IS STANDARD.                        
       01  BP13L38U-REC                  PIC X(200).                            
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F454             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F454-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38U-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-REGN              PIC X(8)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-PRINT-DATE            PIC 9(8)  VALUE ZEROES.                  
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-PRIO-DTE1.                                                         
          05  WS-PRIO-CCYY1                PIC 9(04).                           
          05  WS-PRIO-MM1                  PIC 9(02).                           
          05  WS-PRIO-DD1                  PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
                                                                                
       01  WS-L38U-HEADER          PIC X(130)    VALUE SPACES.                  
                                                                                
       01  L38U-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38U'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38U-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38U-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  L38U-HEADER02.                                                       
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(42)     VALUE                          
               'APPOINTMENT DETAILS FOR SOCA, SATP AND TP '.                    
           05  FILLER              PIC X(18)     VALUE                          
               'FILELESS CASES - ('.                                            
           05  L38U-DATE-HDR2      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' - '.                   
           05  L38U-DATE-HDR3      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE ')'.                     
                                                                                
       01  L38U-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(07)    VALUE 'DTE-BAL'.                
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(04)    VALUE 'MODE'.                   
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(02)    VALUE 'FT'.                     
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(02)    VALUE 'LT'.                     
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(07)    VALUE 'ALC SCH'.                
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(09)    VALUE 'APPT TYPE'.              
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(09)    VALUE 'APPT CODE'.              
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(09)    VALUE 'APPT DATE'.              
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(09)    VALUE 'APPT TIME'.              
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(05)    VALUE 'BLOCK'.                  
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(09)    VALUE 'UNIT'.                   
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(30)    VALUE 'STREET'.                 
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(05)    VALUE 'CRM'.                    
           05  FILLER              PIC X(01)    VALUE ' '.                      
                                                                                
       01  L38U-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38U-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER1             PIC X(01)        VALUE SPACES.               
           05  L38U-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER2             PIC X(01)        VALUE SPACES.               
           05  L38U-DTE-BALLOT     PIC X(07)        VALUE SPACES.               
           05  FILLER3             PIC X(01)        VALUE SPACES.               
           05  L38U-MODE           PIC X(04)        VALUE SPACES.               
           05  FILLER4             PIC X(01)        VALUE SPACES.               
           05  L38U-FT             PIC X(02)        VALUE SPACES.               
           05  FILLER5             PIC X(01)        VALUE SPACES.               
           05  L38U-LT             PIC X(02)        VALUE SPACES.               
           05  FILLER6             PIC X(04)        VALUE SPACES.               
           05  L38U-ALLO-SCH       PIC X(03)        VALUE SPACES.               
           05  FILLER7             PIC X(06)        VALUE SPACES.               
           05  L38U-APPT-TYPE      PIC X(02)        VALUE SPACES.               
           05  FILLER8             PIC X(08)        VALUE SPACES.               
           05  L38U-APPT-CODE      PIC X(01)        VALUE SPACES.               
           05  FILLER9             PIC X(05)        VALUE SPACES.               
           05  L38U-APPT-DATE      PIC X(09)        VALUE SPACES.               
           05  FILLER10            PIC X(04)        VALUE SPACES.               
           05  L38U-APPT-TIME      PIC X(04)        VALUE SPACES.               
           05  FILLER11            PIC X(03)        VALUE SPACES.               
           05  L38U-BLOCK          PIC X(05)        VALUE SPACES.               
           05  FILLER12            PIC X(01)        VALUE SPACES.               
           05  L38U-UNIT           PIC X(09)        VALUE SPACES.               
           05  FILLER13            PIC X(01)        VALUE SPACES.               
           05  L38U-STREET         PIC X(30)        VALUE SPACES.               
           05  FILLER14            PIC X(01)        VALUE SPACES.               
           05  L38U-CRM            PIC X(05)        VALUE SPACES.               
           05  FILLER15            PIC X(01)        VALUE SPACES.               
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH PIC X(4).                                    
               10  WS-LINK-NUM-ACC PIC X(5).                                    
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F454      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F454 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F454                                                 
                       BP13K022                                                 
                       BP13K800                                                 
              OUTPUT   BP13L38U.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              MOVE WS-K022-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7100-GET-DATE           THRU 7100-EXIT.                      
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F454.                                                      
      ******************************************************************        
                                                                                
           READ BP13F454           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F454                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F454-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER                     
                                            BP13K022-REC.                       
           INITIALIZE                       BP13K800-MASTER                     
                                            BP13K022-REC.                       
                                                                                
           MOVE F454-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD  1                       TO WS-K800-FND                 
                                                                                
                    PERFORM 2200-READ-BP13K022   THRU 2200-EXIT                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                       TO WS-K800-NFND                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2200-READ-BP13K022.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K022-REC.                       
           INITIALIZE                       BP13K022-REC.                       
                                                                                
           MOVE SPACES                   TO K022-KEY-FLD.                       
           MOVE F454-NUM-REGN            TO K022-NUM-REGN.                      
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K022 READ ERROR ' WS-K022-STATUS               
                    DISPLAY 'K022 KEY: ' K022-NUM-REGN                          
                    MOVE WS-K022-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF (F454-DTE-APPT >= WS-PRIO-DTE1) AND                               
              (F454-DTE-APPT <= WS-PRIO-DTE )                                   
              PERFORM 2100-READ-BP13K800        THRU 2100-EXIT                  
                                                                                
              IF K800-DTE-BALLOT >= '201702'                                    
                 PERFORM 4000-PROCESS-RECORDS   THRU 4000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F454           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                TO L38U-DETAIL01                          
           INITIALIZE                    L38U-DETAIL01.                         
                                                                                
           MOVE SPACES                TO FILLER1  FILLER2  FILLER3              
                                         FILLER4  FILLER5  FILLER6              
                                         FILLER7  FILLER8  FILLER9              
                                         FILLER10 FILLER11 FILLER12             
                                         FILLER13 FILLER14 FILLER15.            
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L38U-SN.                           
           MOVE F454-NUM-REGN             TO L38U-REGN.                         
                                                                                
           MOVE K800-NUM-ALLO-SCHEME      TO L38U-ALLO-SCH.                     
           MOVE K800-NUM-LOAN-TAG         TO L38U-LT.                           
           MOVE K800-NUM-FLAT-TYPE        TO L38U-FT.                           
           MOVE K800-NUM-ALLO-CAT         TO L38U-MODE.                         
           MOVE K800-DTE-BALLOT           TO L38U-DTE-BALLOT.                   
           MOVE K800-DTE-BALLOT           TO L38U-DTE-BALLOT.                   
                                                                                
           MOVE F454-CDE-APPT-TYPE        TO L38U-APPT-TYPE.                    
           MOVE F454-CDE-APPT             TO L38U-APPT-CODE.                    
           MOVE F454-DTE-APPT             TO L38U-APPT-DATE.                    
           MOVE F454-TME-APPT             TO L38U-APPT-TIME                     
                                                                                
           MOVE K022-NUM-OIC              TO L38U-CRM.                          
                                                                                
           PERFORM 4500-CALL-BP13C913   THRU 4500-EXIT.                         
                                                                                
           PERFORM 5000-PRINT-REPORT    THRU 5000-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4500-CALL-BP13C913.                                                      
      ******************************************************************        
           MOVE K800-NUM-SCH-ACC           TO WS-LINK-REC.                      
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN 000                                                             
              MOVE COMM8-NUM-BLK           TO L38U-BLOCK                        
              STRING '#' COMM8-NUM-LEVEL '-'                                    
                         COMM8-NUM-UNIT-MAIN DELIMITED BY SIZE                  
                                         INTO L38U-UNIT                         
              MOVE COMM8-NME-STREET        TO L38U-STREET                       
           WHEN 100                                                             
              DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                       
                       K800-NUM-SCH-ACC ', REGN : ' K800-NUM-REGN               
              MOVE SPACES                  TO L38U-BLOCK L38U-UNIT              
                                              L38U-STREET                       
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR                
              MOVE SPACES                  TO L38U-BLOCK L38U-UNIT              
                                              L38U-STREET                       
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO BP13L38U-REC.                  
           INITIALIZE                            BP13L38U-REC.                  
                                                                                
           WRITE BP13L38U-REC               FROM L38U-DETAIL01.                 
           ADD 1                              TO WS-CNT-L38U-WRITE.             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           STRING WS-PRIO-DTE1(7:2) '/'                                         
                  WS-PRIO-DTE1(5:2) '/'                                         
                  WS-PRIO-DTE1(1:4)                                             
              DELIMITED BY SIZE INTO L38U-DATE-HDR.                             
                                                                                
           MOVE L38U-DATE-HDR                  TO L38U-DATE-HDR2.               
                                                                                
           STRING WS-PRIO-DTE(7:2) '/'                                          
                  WS-PRIO-DTE(5:2) '/'                                          
                  WS-PRIO-DTE(1:4)                                              
              DELIMITED BY SIZE INTO L38U-DATE-HDR3.                            
                                                                                
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38U-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38U-PAGE-HDR.                
                                                                                
           MOVE SPACES             TO WS-L38U-HEADER.                           
           WRITE BP13L38U-REC    FROM WS-L38U-HEADER.                           
                                                                                
           WRITE BP13L38U-REC    FROM L38U-HEADER01 AFTER PAGE.                 
           WRITE BP13L38U-REC    FROM L38U-HEADER02.                            
           WRITE BP13L38U-REC    FROM L38U-HEADER03 AFTER 2.                    
                                                                                
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-GET-DATE.                                                           
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-SYSTEM-DATE                
                                                  WS-CURR-DATE.                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-PRIOR-DTE.                 
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           ADD 04 TO  WS-NUM-DTE-APPT.                                          
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
                                                                                
      ***** +1 DAY                                                              
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-PRIOR-DTE.                
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           ADD 1 TO  WS-NUM-DTE-APPT.                                           
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE1.                            
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38U'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F454 RECORDS READ............. : '            
                    WS-F454-READ.                                               
           DISPLAY '(2) NO OF BP13L38U RECORDS WRITTEN.......... : '            
                    WS-CNT-L38U-WRITE.                                          
                                                                                
           CLOSE    BP13F454                                                    
                    BP13K800                                                    
                    BP13K022                                                    
                    BP13L38U.                                                   
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
