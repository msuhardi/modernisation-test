       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF84.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  03/02/2015.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES  : OMIT GRO-PA & MGPS CASES AND WRITE OUTPUT   *            
      *                FILE IN BP13F800 FORMAT                     *            
      *                INCLUDE FIRST 200% ONLY                     *            
      *                                                            *            
      *  INPUT FILE  : BP13F740                                    *            
      *                BP13K800                                    *            
      *                BP13K813                                    *            
      *  OUTPUT FILE : BP13F800                                    *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP135656 SMR2 03022015 NEW PROGRAM                         *            
      * BP135656 SMR2 06032015 BYPASS CASES THAT EXIST ON BP13K76D *            
      * BP135656 IL5  02042015 WRITE CASES INTO BP13F800 WHEN      *            
      *                        LOAN-TAG = H OR P                   *            
      * BP135829 IL5  10062015 ADD STATEMENT IN PARA 5000 TO       *            
      *                        MOVE F740-NUM-PPO TO K800-NUM-PPO   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS WS-K813-STATUS.                            
                                                                                
           SELECT BP13K76D ASSIGN TO BP13K76D                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K76D-KEY-FLD                               
                  FILE STATUS     IS WS-K76D-STATUS.                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740   RECORD CONTAINS 500  CHARACTERS                           
                      RECORDING MODE IS F.                                      
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K800   RECORD CONTAINS 2000 CHARACTERS.                          
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K813   RECORD CONTAINS 1000 CHARACTERS.                          
       COPY BP13K813.                                                           
                                                                                
       FD  BP13K76D   RECORD CONTAINS 100  CHARACTERS.                          
       COPY BP13K76D.                                                           
                                                                                
       FD  BP13F800   RECORD CONTAINS 2000 CHARACTERS                           
                      RECORDING MODE IS F.                                      
       COPY BP13F800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K800-STATUS              PIC X(02) VALUE SPACES.              
           05  WS-K813-STATUS              PIC X(02) VALUE SPACES.              
           05  WS-K76D-STATUS              PIC X(02) VALUE SPACES.              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                 PIC X(01) VALUE 'N'.                 
           05  WS-K76D-FOUND               PIC X(01) VALUE 'N'.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                PIC 9(08) VALUE ZEROES.              
           05  WS-F800-WRITE               PIC 9(08) VALUE ZEROES.              
           05  WS-K800-CANCEL              PIC 9(08) VALUE ZEROES.              
           05  WS-NO-200-PRCNT             PIC 9(08) VALUE ZEROES.              
           05  WS-K76D-EXIST               PIC 9(08) VALUE ZEROES.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-QUEUE-END            PIC X(05) VALUE SPACES.              
                                                                                
       PROCEDURE DIVISION.                                                      
      ********************                                                      
       0000-MAIN-CONTROL.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K800                                                 
                       BP13K813                                                 
                       BP13K76D                                                 
                OUTPUT BP13F800.                                                
                                                                                
           IF WS-K800-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K800 : ' WS-K800-STATUS             
              MOVE WS-K800-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K813 : ' WS-K813-STATUS             
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K76D-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K76D : ' WS-K76D-STATUS             
              MOVE WS-K76D-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F740.                                                      
      *********************                                                     
                                                                                
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F740-EOF                       
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                           TO WS-F740-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       3000-PROCESS-RECORDS.                                                    
      ***********************                                                   
                                                                                
           IF F740-NUM-RANDOM(28:1) NOT = 'G' AND                               
              F740-NUM-MGPS = SPACES                                            
              PERFORM 3100-READ-BP13K76D    THRU 3100-EXIT                      
              IF WS-K76D-FOUND = 'Y'                                            
                 ADD 1                      TO WS-K76D-EXIST                    
              ELSE                                                              
                 PERFORM 4000-READ-BP13K813 THRU 4000-EXIT                      
                 IF (F740-NUM-NT-FT-QUEUE NOT = SPACES AND LOW-VALUES)          
                    AND (F740-NUM-NT-FT-QUEUE <= WS-NUM-QUEUE-END)              
                    PERFORM 5000-READ-BP13K800 THRU 5000-EXIT                   
                 ELSE                                                           
                    ADD 1                   TO WS-NO-200-PRCNT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3100-READ-BP13K76D.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K76D-REC.                     
           INITIALIZE                         BP13K76D-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K76D-FOUND.                    
                                                                                
           MOVE F740-NUM-ALLO-CAT          TO K76D-NUM-SALES-MODE.              
           MOVE F740-DTE-BALLOT            TO K76D-NUM-DTE-BALLOT.              
           MOVE F740-NUM-REGN              TO K76D-NUM-REGN.                    
                                                                                
           READ BP13K76D.                                                       
                                                                                
           EVALUATE WS-K76D-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K76D-FOUND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K76D-FOUND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K76D : ' WS-K76D-STATUS             
              MOVE WS-K76D-STATUS         TO RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K813.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                    TO BP13K813-REC.                      
           INITIALIZE                        BP13K813-REC.                      
                                                                                
           MOVE F740-CDE-NT1              TO K813-NUM-ZONE.                     
           MOVE F740-CDE-FLAT-TYPE        TO K813-NUM-FLAT-TYPE.                
           MOVE F740-DTE-BALLOT           TO K813-DTE-BALLOT.                   
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              MOVE K813-NUM-QUEUE-END     TO WS-NUM-QUEUE-END                   
              IF K813-NUM-GEN3-TAG = 'Y'                                        
                 IF F740-NUM-GEN3 = 'Y'                                         
                    CONTINUE                                                    
                 ELSE                                                           
                    IF K813-NUM-QUEUE-END-MGPSGEN3 IS NOT NUMERIC               
                       MOVE ZEROES        TO K813-NUM-QUEUE-END-MGPSGEN3        
                    END-IF                                                      
                    MOVE K813-NUM-QUEUE-END-MGPSGEN3                            
                                          TO WS-NUM-QUEUE-END                   
                 END-IF                                                         
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K813 : ' K813-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K813 : ' WS-K813-STATUS             
              MOVE WS-K813-STATUS         TO RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-READ-BP13K800.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K800-MASTER.                  
           INITIALIZE                         BP13K800-MASTER.                  
                                                                                
           MOVE F740-NUM-REGN              TO K800-NUM-REGN.                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              IF (K800-NUM-STATUS NOT = 'C') AND                                
                 (K800-NUM-LOAN-TAG = 'P' OR 'H')                               
                 MOVE F740-NUM-PPO         TO K800-NUM-PPO                      
                 WRITE BP13F800-MASTER     FROM BP13K800-MASTER                 
                 ADD 1                     TO WS-F800-WRITE                     
              ELSE                                                              
                 ADD 1                     TO WS-K800-CANCEL                    
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K800 : ' K800-NUM-REGN           
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K800 : ' WS-K800-STATUS             
              MOVE WS-K800-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY '*-----------------------------*'.                           
           DISPLAY ' REPORT DESCRIPTION : BP13CF84 '.                           
           DISPLAY '*-----------------------------*'.                           
           DISPLAY SPACES.                                                      
           DISPLAY 'NO BP13F740 RECORDS READ        : ' WS-F740-READ.           
           DISPLAY 'NO BP13F800 RECORDS WRITTEN     : ' WS-F800-WRITE.          
           DISPLAY 'NO BP13K800 RECORDS CANCELLED   : ' WS-K800-CANCEL.         
           DISPLAY 'NO BP13F740 RECORDS NOT IN 200% : ' WS-NO-200-PRCNT.        
           DISPLAY 'NO BP13K76D RECORDS FOUND       : ' WS-K76D-EXIST.          
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F740                                                       
                 BP13K800                                                       
                 BP13K813                                                       
                 BP13K76D                                                       
                 BP13F800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN CLOSING BP13K800 : ' WS-K800-STATUS             
              MOVE WS-K800-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN CLOSING BP13K813 : ' WS-K813-STATUS             
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K76D-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN CLOSING BP13K76D : ' WS-K76D-STATUS             
              MOVE WS-K76D-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
