       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C89E.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  19/03/2009.                                               
      * ========================================================= *             
      *  SYSTEM OF COMMITMENT (BP13)                              *             
      * ========================================================= *             
      *  OBJECTIVE  :  CREATE FLAT FILE FOR PENDING DOCUMENTS     *             
      *                                                           *             
      *  INPUT      :  BP13F740 - BP13.F740.MATUREBA              *             
      *                BP13K800 - BP13.K800.APPLN                 *             
      *                BP13K820 - BP13.K820.SOCOCCP               *             
      *  OUTPUT     :  BP13FP00                                   *             
      *                BP13FQ10                                   *             
      *                                                           *             
      * ========================================================= *             
      *  CHG REF  OIC   DATE        DESCRIPTION                   *             
      *  -------- ----  ----------  ----------------------------  *             
      *  BP133551 RB12  19/03/2009  NEW PROGRAM                   *             
      *  BP133551 RB12  26/08/2009  WRITE TO BP13FQ10 PER REGN    *             
      *  BP133551 RB12  14/09/2009  MODIFY BP13FQ10               *             
      *  BP133817 RB12  21/01/2010  POPULATE LATEST DOC DUE DATES *             
      * ========================================================= *             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F740     ASSIGN       TO BP13F740.                        
                                                                                
           SELECT BP13K800     ASSIGN       TO BP13K800                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K800-NUM-REGN                    
                               FILE STATUS  IS WS-K800-STATUS.                  
                                                                                
           SELECT BP13K820     ASSIGN       TO BP13K820                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS DYNAMIC                          
                               RECORD KEY   IS K820-KEY-FLD                     
                               FILE STATUS  IS WS-K820-STATUS.                  
                                                                                
           SELECT BP13FP00     ASSIGN       TO BP13FP00.                        
                                                                                
           SELECT BP13FQ10     ASSIGN       TO BP13FQ10.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740           IS GLOBAL                                        
                               RECORD CONTAINS 500 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS F.                             
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K800           IS GLOBAL                                        
                               RECORD CONTAINS 2000 CHARACTERS.                 
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820           IS GLOBAL                                        
                               RECORD CONTAINS 400 CHARACTERS.                  
                                                                                
       COPY BP13K820.                                                           
                                                                                
       FD   BP13FP00           IS GLOBAL                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 3800 CHARACTERS.                                    
                                                                                
       COPY BP13FP00.                                                           
                                                                                
       FD   BP13FQ10           IS GLOBAL                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 400 CHARACTERS.                                     
                                                                                
       COPY BP13FQ10.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-SWITCHES.                                                    
           05  WS-F740-EOF               PIC X(01)  VALUE 'N'.                  
           05  WS-K800-EOF               PIC X(01)  VALUE 'N'.                  
           05  WS-K820-EOF               PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F740-READ-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K820-READ-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-FND-CNT           PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-NFND-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-FND               PIC X(01)  VALUE 'N'.                  
           05  WS-K820-FND               PIC X(01)  VALUE 'N'.                  
           05  WS-FP00-WRITE-CNT         PIC 9(06)  VALUE ZEROES.               
           05  WS-FQ10-WRITE-CNT         PIC 9(06)  VALUE ZEROES.               
           05  WS-SPOUSE-FND             PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-AGE                        PIC 9(08) VALUE ZEROES.                
       01  WS-NUM-AGE  REDEFINES WS-AGE.                                        
           05 WS-AGE-CMPLTD              PIC 9(04).                             
           05 WS-NUM-REMNG               PIC 9(04).                             
                                                                                
       01  WS-DTE-LETTER                 PIC 9(08) VALUE ZEROES.                
       01  WS-NUM-DTE-LETTER             PIC 9(07) VALUE ZEROES.                
                                                                                
       01  WS-MONTH.                                                            
           05  WS-YR                     PIC 9(04)  VALUE ZEROES.               
           05  WS-MM                     PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  WS-NRIC-AREA.                                                        
           05  WS-NRIC OCCURS 12 TIMES   PIC X(09)  VALUE SPACES.               
                                                                                
       01  WS-FILE-STATUS-G GLOBAL.                                             
           05  WS-K800-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES-G GLOBAL.                                               
           05  WS-CURRENT-DATE           PIC X(08)  VALUE SPACES.               
           05  WS-DATE                   PIC 9(08)  VALUE ZEROES.               
           05  WS-BIRTH-DATE             PIC 9(08)  VALUE ZEROES.               
           05  WS-TOT-PAGE-CNT           PIC 9(06)  VALUE ZEROES.               
           05  WS-CASE-PAGE-CNT          PIC 9(03)  VALUE ZEROES.               
           05  WS-CNT-WRITE-L89DA        PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-WRITE-L89DB        PIC 9(08)  VALUE ZEROES.               
           05  WS-REC-SKIPPED            PIC 9(08)  VALUE ZEROES.               
           05  WS-TOT-LET-PRINT          PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-LINE-ON-PAGE-A     PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-LINE-ON-PAGE-B     PIC 9(08)  VALUE ZEROES.               
           05  WS-CURR-DATE              PIC X(10)  VALUE SPACES.               
           05  WS-HH                     PIC X(01)  VALUE SPACES.               
           05  WS-FT                     PIC X(01)  VALUE SPACES.               
           05  WS-CTR                    PIC 9(02)  VALUE ZEROES.               
           05  CTR                       PIC 9(02)  VALUE ZEROES.               
           05  WS-NRIC-CNT               PIC 9(02)  VALUE ZEROES.               
           05  WS-APPL-CNT               PIC 9(02)  VALUE ZEROES.               
           05  WS-OCCP-CNT               PIC 9(02)  VALUE ZEROES.               
           05  WS-DOC-CNT                PIC 9(03)  VALUE ZEROES.               
                                                                                
       PROCEDURE DIVISION.                                                      
      ************************************************************              
       0000-MAIN.                                                               
      ************************************************************              
           PERFORM 1000-OPEN-ROUTINE   THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F740  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ************************************************************              
       1000-OPEN-ROUTINE.                                                       
      ************************************************************              
           OPEN INPUT  BP13F740                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                OUTPUT BP13FP00                                                 
                       BP13FQ10.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS ' WS-K800-STATUS             
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPEN ERROR, STATUS ' WS-K820-STATUS             
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
                                                                                
           STRING WS-CURRENT-DATE(7:2) '/'                                      
                  WS-CURRENT-DATE(5:2) '/'                                      
                  WS-CURRENT-DATE(1:4)                                          
                  DELIMITED BY SIZE                                             
             INTO WS-CURR-DATE.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       2000-READ-BP13F740.                                                      
      ************************************************************              
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F740-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F740-READ-CNT                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       3000-PROCESS-RECORD.                                                     
      ************************************************************              
           PERFORM 3100-INITIALIZE     THRU 3100-EXIT.                          
           PERFORM 3200-READ-BP13K800  THRU 3200-EXIT.                          
                                                                                
           IF WS-K800-FND = 'N'                                                 
              DISPLAY 'NOT FOUND K800-NUM-REGN: ' K800-NUM-REGN                 
              ADD 1 TO WS-K800-NFND-CNT                                         
           ELSE                                                                 
              DISPLAY 'FOUND K800-NUM-REGN: ' K800-NUM-REGN                     
              ADD 1 TO WS-K800-FND-CNT                                          
              IF K800-DTE-BK-APPT NOT = SPACES AND ZEROES AND LOW-VALUES        
                 PERFORM 3400-PROCESS-BP13K820   THRU 3400-EXIT                 
                 PERFORM 3600-CREATE-BP13FQ10    THRU 3600-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       3100-INITIALIZE.                                                         
      ************************************************************              
           MOVE  ZEROS      TO WS-APPL-CNT.                                     
           MOVE   4         TO WS-OCCP-CNT.                                     
           MOVE   'N'       TO WS-K820-EOF.                                     
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3200-READ-BP13K800.                                                      
      ************************************************************              
           MOVE F740-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
             WHEN   00                                                          
                    IF K800-NUM-STATUS = 'C'                                    
                       MOVE 'N' TO WS-K800-FND                                  
                    ELSE                                                        
                       MOVE 'Y' TO WS-K800-FND                                  
                    END-IF                                                      
             WHEN   23                                                          
                    MOVE 'N' TO WS-K800-FND                                     
             WHEN   OTHER                                                       
                    DISPLAY 'READ BP13K800 ERROR ' K800-NUM-REGN                
                    DISPLAY 'STATUS ' WS-K800-STATUS                            
                    MOVE WS-K800-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3400-PROCESS-BP13K820.                                                   
      ************************************************************              
           MOVE 'N'             TO WS-K820-FND.                                 
                                                                                
           MOVE ZEROES          TO WS-NRIC-CNT.                                 
           MOVE LOW-VALUES      TO K820-KEY-FLD.                                
           MOVE K800-NUM-REGN   TO K820-NUM-REGN.                               
                                                                                
           PERFORM 3410-START-BP13K820    THRU 3410-EXIT.                       
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3410-START-BP13K820.                                                     
      *-------------------------------------------------------------            
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
             WHEN   00                                                          
                    PERFORM 3420-READNEXT-BP13K820 THRU 3420-EXIT               
                       UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                  
                        OR   WS-K820-EOF = 'Y'                                  
             WHEN   23                                                          
                    MOVE 'N' TO WS-K820-FND                                     
             WHEN   46                                                          
                    MOVE 'Y' TO WS-K820-EOF                                     
             WHEN   OTHER                                                       
                    DISPLAY 'ERROR BROWSE BP13K820, STAT = '                    
                           K820-KEY-FLD ' ' WS-K820-STATUS                      
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3420-READNEXT-BP13K820.                                                  
      *---------------------------------------------------------------*         
           READ BP13K820 NEXT                                                   
                         AT END MOVE 'Y' TO WS-K820-EOF                         
                         GO TO 3420-EXIT.                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
             WHEN 00                                                            
                  IF K820-NUM-REGN = K800-NUM-REGN                              
                     ADD    1   TO WS-NRIC-CNT                                  
                     PERFORM 3430-MOVE-K820-FLDS   THRU 3430-EXIT               
                     ADD    1   TO WS-K820-READ-CNT                             
                     MOVE 'Y'   TO WS-K820-FND                                  
                END-IF                                                          
             WHEN 23                                                            
                  MOVE 'Y'   TO WS-K820-FND                                     
             WHEN 46                                                            
                  MOVE 'Y'   TO WS-K820-EOF                                     
             WHEN OTHER                                                         
                DISPLAY 'BP18K820 - READNXT ERROR, STATUS: '                    
                                                     WS-K820-STATUS             
                MOVE     WS-K820-STATUS    TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3420-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3430-MOVE-K820-FLDS.                                                     
      *---------------------------------------------------------------*         
      *-----------------------------------------*                               
      * CALCULATE COMPLETED AGE FROM BIRTH DATE *                               
      *-----------------------------------------*                               
           IF K820-DTE-BIRTH IS NOT NUMERIC                                     
              MOVE ZEROES                TO K820-DTE-BIRTH                      
           END-IF.                                                              
                                                                                
           MOVE K820-DTE-BIRTH        TO WS-BIRTH-DATE.                         
           COMPUTE WS-AGE  =  WS-DATE  -  WS-BIRTH-DATE.                        
                                                                                
      *---------------------------------------------------*                     
      * INITIALIZE BP13FP00 RECORD                        *                     
      *---------------------------------------------------*                     
           MOVE SPACES        TO BP13FP00-REC.                                  
           INITIALIZE            BP13FP00-REC.                                  
                                                                                
           MOVE K820-NUM-REGN TO FP00-NUM-REGN.                                 
           MOVE K820-NUM-NRIC TO FP00-NUM-NRIC.                                 
                                                                                
      *---------------------------------------------------*                     
      * IF NRIC FOUND IN BP13K800, PERFORM APPLICANT PARA *                     
      * ELSE PERFORM OCCUPIER PARA                        *                     
      *---------------------------------------------------*                     
           IF  (K820-NUM-NRIC  =  K800-NUM-NRIC1)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC2)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC3)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC4)                               
               PERFORM 3440-PROCESS-APPLICANT THRU 3440-EXIT                    
               PERFORM 3500-CREATE-FP00-APPL  THRU 3500-EXIT                    
           ELSE                                                                 
               ADD   1                        TO   WS-OCCP-CNT                  
               MOVE K820-NUM-NRIC             TO   WS-NRIC(WS-OCCP-CNT)         
               PERFORM 3550-CREATE-FP00-OCCUP THRU 3550-EXIT                    
           END-IF.                                                              
                                                                                
       3430-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3440-PROCESS-APPLICANT.                                                  
      *---------------------------------------------------------------*         
           IF  (K820-NUM-NRIC = K800-NUM-NRIC1)                                 
                MOVE  1     TO WS-APPL-CNT                                      
           END-IF.                                                              
                                                                                
           IF  (K820-NUM-NRIC = K800-NUM-NRIC2)                                 
                MOVE  2     TO WS-APPL-CNT                                      
           END-IF.                                                              
                                                                                
           IF  (K820-NUM-NRIC = K800-NUM-NRIC3)                                 
                MOVE  3     TO WS-APPL-CNT                                      
           END-IF.                                                              
                                                                                
           IF  (K820-NUM-NRIC = K800-NUM-NRIC4)                                 
                MOVE  4     TO WS-APPL-CNT                                      
           END-IF.                                                              
                                                                                
           MOVE K820-NUM-NRIC TO WS-NRIC(WS-APPL-CNT).                          
                                                                                
       3440-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3500-CREATE-FP00-APPL.                                                   
      ************************************************************              
                                                                                
           MOVE ZEROES TO WS-DOC-CNT.                                           
                                                                                
      *** NRIC                                                                  
           ADD 1            TO WS-DOC-CNT.                                      
           MOVE '001'       TO FP00-NUM-DOC     (WS-DOC-CNT).                   
           MOVE  'Y'        TO FP00-NUM-DOC-REQ (WS-DOC-CNT).                   
           MOVE  'N'        TO FP00-NUM-DOC-RECVD(WS-DOC-CNT).                  
                                                                                
      *** BIRTH CERTIFICATE                                                     
                                                                                
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06'                      
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '002'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE 'Y'      TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE 'N'      TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME =  'MTF'                                     
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 ADD 1      TO WS-DOC-CNT                                       
                 MOVE '002' TO FP00-NUM-DOC     (WS-DOC-CNT)                    
                 MOVE 'Y'   TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
                 MOVE 'N'   TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *** MARRIAGE CERTIFICATE                                                  
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
              MOVE 'N'       TO WS-SPOUSE-FND                                   
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL                          
                                            WS-CTR = WS-NRIC-CNT                
                 IF K820-NUM-NRIC-SPOUSE = WS-NRIC(WS-CTR)                      
                    MOVE 'Y' TO WS-SPOUSE-FND                                   
                 END-IF                                                         
              END-PERFORM                                                       
              IF WS-SPOUSE-FND = 'N'                                            
                 ADD 1       TO WS-DOC-CNT                                      
                 MOVE '003'  TO FP00-NUM-DOC     (WS-DOC-CNT)                   
                 MOVE 'Y'    TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                   
                 MOVE 'N'    TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *** PAYSLIP                                                               
           IF K820-AMT-INCOME  >  0                                             
              ADD 1           TO WS-DOC-CNT                                     
              MOVE '004'      TO FP00-NUM-DOC      (WS-DOC-CNT)                 
              MOVE 'Y'        TO FP00-NUM-DOC-REQ  (WS-DOC-CNT)                 
              MOVE 'N'        TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                 
              MOVE WS-DATE(1:6)                                                 
                              TO FP00-NUM-DOC-DATE (WS-DOC-CNT)                 
                                 WS-MONTH                                       
              PERFORM VARYING CTR FROM 1 BY 1                                   
                UNTIL CTR > 11                                                  
                SUBTRACT 1 FROM WS-MM                                           
                IF WS-MM = 0                                                    
                   MOVE 12 TO WS-MM                                             
                   SUBTRACT 1 FROM WS-YR                                        
                END-IF                                                          
                IF WS-MONTH >= K800-DTE-BALLOT                                  
                   ADD 1      TO WS-DOC-CNT                                     
                   MOVE '004' TO FP00-NUM-DOC      (WS-DOC-CNT)                 
                   MOVE 'Y'   TO FP00-NUM-DOC-REQ  (WS-DOC-CNT)                 
                   MOVE 'N'   TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                 
                   MOVE WS-MONTH                                                
                              TO FP00-NUM-DOC-DATE (WS-DOC-CNT)                 
                ELSE                                                            
                   MOVE 12 TO CTR                                               
                END-IF                                                          
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
      *** UNEMPLOYMENT/SELF-EMPLOYED DECLARATION FORM                           
           IF K820-AMT-INCOME = 0                                               
               IF ((WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)) OR         
                  ((WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30))            
                  ADD 1      TO WS-DOC-CNT                                      
                  MOVE '008' TO FP00-NUM-DOC     (WS-DOC-CNT)                   
                  MOVE  'Y'  TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                   
                  MOVE  'N'  TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *** DEATH CERTIFICATE                                                     
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '010'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
              MOVE K820-NME-SPOUSE                                              
                            TO FP00-TXT-DEATH-CERT1                             
           END-IF.                                                              
                                                                                
      *** DIVORCE CERTIFICATE                                                   
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '011'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** APPLICATION FORM                                                      
           IF (K800-NUM-APPLN-TYPE = 'I') AND                                   
              (K820-NUM-NRIC = K800-NUM-NRIC1)                                  
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '019'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** STUDENT PASS                                                          
           IF (K820-AMT-INCOME = 0) AND                                         
              (WS-AGE-CMPLTD >= 18) AND (WS-AGE-CMPLTD <= 30)                   
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '020'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** ESSENTIAL OCCUPIER FORM                                               
           IF K820-NUM-NRIC = K800-NUM-NRIC1                                    
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '022'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** MCPS FORM                                                             
           IF (K800-NUM-ALLO-SCHEME = 'MCP') AND                                
              (K820-NUM-NRIC = K800-NUM-NRIC1)                                  
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '023'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** ORPHAN SCHEME FORM                                                    
           IF (K800-NUM-ELIG-SCHEME = 'OR ') AND                                
              (K820-NUM-NRIC = K800-NUM-NRIC1)                                  
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '024'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
           MOVE WS-CURRENT-DATE TO FP00-DTE-UPDATE.                             
           MOVE 'BP13C89E'      TO FP00-NUM-USERID.                             
           ACCEPT FP00-TME-UPDATE FROM TIME.                                    
           WRITE BP13FP00-REC.                                                  
           ADD   1              TO WS-FP00-WRITE-CNT.                           
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3550-CREATE-FP00-OCCUP.                                                  
      ************************************************************              
                                                                                
           MOVE ZEROES TO WS-DOC-CNT.                                           
                                                                                
      *** NRIC                                                                  
           IF (K820-NUM-CITIZENSHIP = '10' OR '20')  AND                        
              (WS-AGE-CMPLTD >= 15)                                             
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '001'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** BIRTH CERTIFICATE                                                     
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06'                      
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '002'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME =  'MTF'                                     
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 ADD 1      TO WS-DOC-CNT                                       
                 MOVE '002' TO FP00-NUM-DOC     (WS-DOC-CNT)                    
                 MOVE  'Y'  TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
                 MOVE  'N'  TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *** MARRIAGE CERTIFICATE                                                  
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
              MOVE 'N'      TO WS-SPOUSE-FND                                    
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL                          
                                            WS-CTR = WS-NRIC-CNT                
                 IF K820-NUM-NRIC-SPOUSE = WS-NRIC(WS-CTR)                      
                    MOVE 'Y' TO WS-SPOUSE-FND                                   
                 END-IF                                                         
              END-PERFORM                                                       
              IF WS-SPOUSE-FND = 'N'                                            
              ADD 1          TO WS-DOC-CNT                                      
              MOVE '003'     TO FP00-NUM-DOC     (WS-DOC-CNT)                   
              MOVE  'Y'      TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                   
              MOVE  'N'      TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *** PAYSLIP                                                               
                                                                                
           IF K820-AMT-INCOME  >  0                                             
              ADD 1          TO WS-DOC-CNT                                      
              MOVE '004'     TO FP00-NUM-DOC     (WS-DOC-CNT)                   
              MOVE  'Y'      TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                   
              MOVE  'N'      TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                  
              MOVE WS-DATE(1:6)                                                 
                             TO FP00-NUM-DOC-DATE (WS-DOC-CNT)                  
                                WS-MONTH                                        
              PERFORM VARYING CTR FROM 1 BY 1                                   
                UNTIL CTR > 11                                                  
                SUBTRACT 1 FROM WS-MM                                           
                IF WS-MM = 0                                                    
                   MOVE 12 TO WS-MM                                             
                   SUBTRACT 1 FROM WS-YR                                        
                END-IF                                                          
                IF WS-MONTH >= K800-DTE-BALLOT                                  
                   ADD 1      TO WS-DOC-CNT                                     
                   MOVE '004' TO FP00-NUM-DOC      (WS-DOC-CNT)                 
                   MOVE 'Y'   TO FP00-NUM-DOC-REQ  (WS-DOC-CNT)                 
                   MOVE 'N'   TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                 
                   MOVE WS-MONTH                                                
                           TO FP00-NUM-DOC-DATE (WS-DOC-CNT)                    
                ELSE                                                            
                   MOVE 12 TO CTR                                               
                END-IF                                                          
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
      *** UNEMPLOYMENT/SELF-EMPLOYED DECLARATION FORM                           
           IF K820-AMT-INCOME = 0                                               
               IF ((WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)) OR         
                  ((WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30))            
                  ADD 1      TO WS-DOC-CNT                                      
                  MOVE '008' TO FP00-NUM-DOC      (WS-DOC-CNT)                  
                  MOVE  'Y'  TO FP00-NUM-DOC-REQ  (WS-DOC-CNT)                  
                  MOVE  'N'  TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *** DEATH CERTIFICATE                                                     
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '010'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
              MOVE K820-NME-SPOUSE                                              
                            TO FP00-TXT-DEATH-CERT1                             
           END-IF.                                                              
                                                                                
      *** DIVORCE CERTIFICATE                                                   
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '011'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** STUDENT PASS                                                          
           IF (K820-AMT-INCOME = 0) AND                                         
              (WS-AGE-CMPLTD >= 18) AND (WS-AGE-CMPLTD <= 30)                   
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '020'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** EMPLOYMENT PASS/WORK PERMIT                                           
           IF (K820-NUM-CITIZENSHIP  NOT =  '10')                               
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '021'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
      *** MARRIAGE CONSENT FORM                                                 
           IF (K820-NUM-RELATIONSHIP  =  '09')  AND                             
              (WS-AGE-CMPLTD >= 18  AND  WS-AGE-CMPLTD < 21)                    
              ADD 1         TO WS-DOC-CNT                                       
              MOVE '025'    TO FP00-NUM-DOC     (WS-DOC-CNT)                    
              MOVE  'Y'     TO FP00-NUM-DOC-REQ (WS-DOC-CNT)                    
              MOVE  'N'     TO FP00-NUM-DOC-RECVD(WS-DOC-CNT)                   
           END-IF.                                                              
                                                                                
           MOVE WS-CURRENT-DATE TO FP00-DTE-UPDATE.                             
           MOVE 'BP13C89E'      TO FP00-NUM-USERID.                             
           ACCEPT FP00-TME-UPDATE FROM TIME.                                    
           WRITE BP13FP00-REC.                                                  
           ADD   1              TO WS-FP00-WRITE-CNT.                           
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3600-CREATE-BP13FQ10.                                                    
      ************************************************************              
                                                                                
           MOVE SPACES               TO BP13FQ10-REC.                           
           INITIALIZE                   BP13FQ10-REC.                           
           MOVE K800-NUM-REGN        TO FQ10-NUM-REGN                           
                                        FQ10-NUM-REGN1                          
                                        FQ10-NUM-REGN2.                         
           MOVE ALL '9'              TO FQ10-DTE-LATEST-DOC-DUE                 
                                        FQ10-DTE-LATEST-DOC-DUE2.               
           MOVE K800-NUM-NEW-TOWN    TO FQ10-NUM-NEW-TOWN.                      
           MOVE K800-NUM-FLAT-TYPE   TO FQ10-NUM-FLAT-TYPE.                     
           MOVE K800-NUM-ELIG-SCHEME TO FQ10-NUM-ELIG-SCHEME.                   
           MOVE K800-NUM-ALLO-SCHEME TO FQ10-NUM-ALLO-SCHEME.                   
           MOVE K800-NUM-ALLO-CAT    TO FQ10-NUM-SEL-TYPE.                      
           MOVE 'BP13C89E'           TO FQ10-NUM-USERID.                        
           MOVE WS-CURRENT-DATE      TO FQ10-DTE-UPDATE.                        
           ACCEPT FQ10-TME-UPDATE FROM TIME.                                    
           WRITE BP13FQ10-REC.                                                  
           ADD   1                   TO WS-FQ10-WRITE-CNT.                      
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       9000-CLOSE-ROUTINE.                                                      
      ************************************************************              
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C89E CONTROL TOTAL --------------'.                   
           DISPLAY '                RUN DATE : ' WS-CURR-DATE.                  
           DISPLAY '---------------------------------------'.                   
           DISPLAY ' NO OF RECS READ        (F740): ' WS-F740-READ-CNT.         
           DISPLAY ' NO OF RECS FOUND       (K800): ' WS-K800-FND-CNT.          
           DISPLAY ' NO OF RECS NFND/CANCEL (K800): ' WS-K800-NFND-CNT.         
           DISPLAY ' NO OF RECS WRITTEN     (FP00): ' WS-FP00-WRITE-CNT.        
           DISPLAY ' NO OF RECS WRITTEN     (FQ10): ' WS-FQ10-WRITE-CNT.        
                                                                                
           CLOSE BP13F740                                                       
                 BP13FP00                                                       
                 BP13FQ10                                                       
                 BP13K800                                                       
                 BP13K820.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K800, STATUS ' WS-K800-STATUS            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K820, STATUS ' WS-K820-STATUS            
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       END PROGRAM BP13C89E.                                                    
