      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13C14C.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  27/11/08.                                                 
                                                                                
      ******************************************************************        
      *  PROGRAM NAME : BP13C14C                                       *        
      *  OBJECTIVE    : ASSIGN CASES IN BALLOT FILE TO OIC - OML1      *        
      *                                                                *        
      *  INPUT/OUTPUT FILES :                                          *        
      *  ====================                                          *        
      *  TYPE      FILE NAME                                           *        
      *  ------    ---------                                           *        
      *  INPUT     BP13F740                                            *        
      *            SY02F001                                            *        
      *  I-O       BP13K022                                            *        
      *================================================================*        
      * MODIFICATION HISTORY:                                          *        
      * ---------------------                                          *        
      * CHG REQ. AUTHOR DATE      DESCRIPTIONS                         *        
      * ======== ====== =======   =====================================*        
      * BP133465 JB8    20081127  NEW PROGRAM.                         *        
      * BP134045 ZDD    20101015  INCLUDE CCK16                        *        
      * BP134104 IL5    20111111  INCLUDE LET4                         *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS RANDOM                                     
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    FLAG VARIABLES                                              *        
      *----------------------------------------------------------------*        
       01 WS-FILE-VARIBLES.                                                     
          05 WS-K022-FND                 PIC 9(07) VALUE ZEROES.                
          05 WS-K022-NOTFND              PIC 9(07) VALUE ZEROES.                
          05 WS-K022-DELETE              PIC 9(07) VALUE ZEROES.                
          05 WS-K022-WRITE               PIC 9(07) VALUE ZEROES.                
          05 WS-F740-READ                PIC 9(07) VALUE ZEROES.                
          05 WS-K022-STATUS              PIC 9(02) VALUE ZEROES.                
          05 WS-EOF-F740                 PIC X(01) VALUE 'N'.                   
          05 WS-OIC                      PIC X(05) VALUE SPACES.                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
                   UNTIL WS-EOF-F740 = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       SY02F001                                                 
                I-O    BP13K022.                                                
                                                                                
           IF WS-K022-STATUS NOT EQUAL 00 AND 97                                
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-READ-BP13F740.                                                      
      *================================================================*        
                                                                                
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y' TO WS-EOF-F740                                         
                GO TO 2000-EXIT                                                 
           END-READ                                                             
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-RECORDS.                                                    
      *================================================================*        
                                                                                
           MOVE SPACES                           TO BP13K022-REC.               
           INITIALIZE                               BP13K022-REC.               
                                                                                
           MOVE F740-NUM-REGN                    TO K022-NUM-REGN.              
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF WS-K022-STATUS = 00 OR 02                                         
              ADD 1                              TO WS-K022-FND                 
              PERFORM 4000-DELETE-BP13K022     THRU 4000-EXIT                   
              PERFORM 5000-WRITE-BP13K022      THRU 5000-EXIT                   
           ELSE                                                                 
              IF WS-K022-STATUS = 23                                            
                 ADD 1                           TO WS-K022-NOTFND              
                 PERFORM 5000-WRITE-BP13K022   THRU 5000-EXIT                   
              ELSE                                                              
                 DISPLAY 'BP13K022 - ERROR DELETING : ' WS-K022-STATUS          
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-DELETE-BP13K022.                                                    
      *================================================================*        
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           IF WS-K022-STATUS = 00                                               
              ADD 1                            TO WS-K022-DELETE                
           ELSE                                                                 
              DISPLAY 'BP13K022 - ERROR DELETING : ' WS-K022-STATUS             
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       5000-WRITE-BP13K022.                                                     
      *================================================================*        
                                                                                
           MOVE SPACES                         TO BP13K022-REC.                 
           INITIALIZE                             BP13K022-REC.                 
                                                                                
           MOVE F740-NUM-REGN                  TO K022-NUM-REGN.                
           EVALUATE WS-OIC                                                      
             WHEN 'CCK16'                                                       
              MOVE 'OML1'                      TO K022-NUM-OIC                  
                                                  K022-NUM-OIC-APPL             
                                                  WS-OIC                        
             WHEN 'OML1'                                                        
              MOVE 'LET4'                      TO K022-NUM-OIC                  
                                                  K022-NUM-OIC-APPL             
                                                  WS-OIC                        
             WHEN 'LET4'                                                        
              MOVE 'CCK16'                     TO K022-NUM-OIC                  
                                                  K022-NUM-OIC-APPL             
                                                  WS-OIC                        
             WHEN OTHER                                                         
              MOVE 'CCK16'                     TO K022-NUM-OIC                  
                                                  K022-NUM-OIC-APPL             
                                                  WS-OIC                        
           END-EVALUATE.                                                        
                                                                                
           MOVE F740-NUM-ALLO-CAT              TO K022-CDE-ALLO-CAT             
           MOVE 'BP13C14C'                     TO K022-NUM-USERID               
           MOVE F001-DTE-CURRENT               TO K022-DTE-UPDATE               
                                                                                
           WRITE BP13K022-REC.                                                  
                                                                                
           IF WS-K022-STATUS = 00                                               
              ADD 1                            TO WS-K022-WRITE                 
           ELSE                                                                 
              DISPLAY 'BP13K022 - ERROR DELETING : ' WS-K022-STATUS             
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
                                                                                
           CLOSE   BP13K022                                                     
                   SY02F001                                                     
                   BP13F740.                                                    
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '********* BP13C14C CONTROL TOTALS *********'.               
           DISPLAY '*'.                                                         
           DISPLAY '* NO. OF F740 READ .............. : '                       
                    WS-F740-READ.                                               
           DISPLAY '* NO. OF K022 FOUND ............. : '                       
                    WS-K022-FND.                                                
           DISPLAY '* NO. OF K022 NOT FOUND ......... : '                       
                    WS-K022-NOTFND.                                             
           DISPLAY '* NO. OF K022 DELETE ............ : '                       
                    WS-K022-DELETE.                                             
           DISPLAY '* NO. OF K022 WRITE ............. : '                       
                    WS-K022-WRITE.                                              
                                                                                
           IF WS-K022-STATUS NOT = 00                                           
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
