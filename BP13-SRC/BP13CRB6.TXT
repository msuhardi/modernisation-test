       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CRB6.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  15 JAN 2014.                                              
      *============================================================*            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      *============================================================*            
      * OBJECTIVE :  TO CREATE A RAW FILE FOR BATCH UPLOAD OF HH  **            
      *              STRUCTURE.                                    *            
      *              THIS FILE WILL BE USED AS INPUT BY BP13CRB7 TO*            
      *              CREATE AN XML CONVERTED RECORDS/FILE.         *            
      * INPUT     :  BP13F200  -  BKAPPMT FILE                     *            
      *           :  BP13F740  -  REMOVED                          *            
      *           :  BP13K800  -  MASTER FILE                      *            
      *           :  BP13K820  -  OCCUPIER FILE                    *            
      *           :  BP13KP00  -  DOCUMENT FILE - REMOVED          *            
      *           :  BP13K060  -  CODE TRANSCRIPTION FILE          *            
      *           :  BP13K734  -  APPLICANT'S INCOME FILE          *            
      *           :  DOCCODE   -  DOCUMENT CODE TABLE              *            
      * OUTPUT    :  BP13F0B6  -  OUTPUT FILE                      *            
      *----------------------------------------------------------- *            
      * CHG REF  DATE     BY     DESCRIPTION                       *            
      * -------- -------- ----   ------------------------------    *            
      * BP135151 15012014 KAM4   NEW PROGRAM                       *            
      * BP135219 04032014 KAM4   TO RETRIEVE INCOME FROM K734      *            
      *          13032014 KAM4   ADDED DTE-REGN AND EMPLY-STATUS   *            
      * BP135304 17042014 KAM4   REMOVED RETRIEVAL OF DOCS         *            
      * BP135517 23102014 KAM4   REMOVED F740 AND REPLACED BY F200 *            
      *          27102014 KAM4   ALSO REMOVE CALLING OF BP13C913   *            
      * BP135198 22012015 KAM4   ADDED DTE-KEY-ISSUED, DTE-CANCEL &*            
      *                          SCH-ACC.                          *            
      *                          ADDED READING OF BM06K110.        *            
      * BP136696 20031017 MRN1   CALL BP13CB4A TO READ DB2 GRANT TABLE          
      *                          AND REPLACE K800 GRANT FIELDS     *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                      ACCESS MODE IS RANDOM                                     
                     ORGANIZATION IS INDEXED                                    
                       RECORD KEY IS K800-NUM-REGN                              
                     FILE STATUS  IS K800-STATUS.                               
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                      ACCESS MODE IS RANDOM                                     
                     ORGANIZATION IS INDEXED                                    
                       RECORD KEY IS K022-KEY-FLD                               
                    ALTERNATE KEY IS K022-NUM-REGN                              
                      FILE STATUS IS K022-STATUS.                               
                                                                                
                                                                                
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                       ACCESS MODE IS DYNAMIC                                   
                      ORGANIZATION IS INDEXED                                   
                        RECORD KEY IS K820-KEY-FLD                              
                       FILE STATUS IS K820-STATUS.                              
                                                                                
           SELECT BP13K060  ASSIGN TO BP13K060                                  
                       ACCESS MODE IS RANDOM                                    
                      ORGANIZATION IS INDEXED                                   
                        RECORD KEY IS K060-KEY-FLD                              
                       FILE STATUS IS K060-STATUS.                              
                                                                                
           SELECT BP13K734  ASSIGN TO  BP13K734                                 
                       ACCESS MODE IS  RANDOM                                   
                      ORGANIZATION IS  INDEXED                                  
                        RECORD KEY IS  K734-KEY-FLD                             
                       FILE STATUS IS  K734-STATUS.                             
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS K110-STATUS.                               
                                                                                
           SELECT BP13F0B6  ASSIGN       TO BP13F0B6.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD  BP13F200                                                             
           RECORDING  MODE  IS F                                                
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL  RECORDS  ARE  STANDARD.                                       
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K734                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K734.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F0B6                                                             
           RECORD CONTAINS 20000 CHARACTERS                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORD ARE STANDARD                                            
           RECORDING  MODE  IS F.                                               
       COPY BP13F0B6.                                                           
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
       COPY DOCCODE.                                                            
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13CB4A.               *                 
      *-------------------------------------------------------*                 
       COPY P13COM73.                                                           
                                                                                
       01  WS-SQL-CODE                 PIC 9(4)  VALUE ZEROS.                   
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-SYSTEM-DATE          PIC X(08) VALUE SPACES.                  
           05  WS-NUM-AHG-STATUS         PIC X(1)   VALUE SPACES.               
           05  WS-NUM-SHG-STATUS         PIC X(1)   VALUE SPACES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ                   PIC 9(06)  VALUE ZEROES.               
           05  WS-CTR                    PIC 9(06)  VALUE ZEROES.               
           05  WS-CTR2                   PIC 9(06)  VALUE ZEROES.               
           05  WS-IDX                    PIC 9(06)  VALUE ZEROES.               
           05  WS-WRITE                  PIC 9(06)  VALUE ZEROES.               
           05  WS-LINEB-CNT              PIC 9(02)  VALUE ZEROES.               
           05  WS-READ-F200              PIC 9(06)  VALUE ZEROES.               
           05  WS-WRITE-F0B6             PIC 9(06)  VALUE ZEROES.               
           05  WS-K110-NOTFND            PIC 9(06)  VALUE ZEROES.               
           05  WS-INCOME-MONTHS          PIC 9(02)  VALUE ZEROES.               
           05  WS-A                      PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS               PIC 9(02)  VALUE ZEROES.               
           05  K022-STATUS               PIC 9(02)  VALUE ZEROES.               
           05  K820-STATUS               PIC 9(02)  VALUE ZEROES.               
           05  K060-STATUS               PIC 9(02)  VALUE ZEROES.               
           05  K734-STATUS               PIC 9(02)  VALUE ZEROES.               
           05  K110-STATUS               PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  WS-SWITCH.                                                           
           05  WS-K820-EOF               PIC X(01).                             
           05  WS-F200-EOF               PIC X(01)   VALUE 'N'.                 
                                                                                
      *-------------------------------------------------------------            
      *    BP13C913 PARAMETERS                                                  
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH            PIC X(4).                           
              10 WS-LINK-NUM-ACC            PIC X(5).                           
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 1500-READ-BP13F200 THRU 1500-EXIT.                           
           PERFORM 2000-PROCESS-DATA  THRU 2000-EXIT                            
                   UNTIL WS-F200-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13F200                                                  
                      BP13K800                                                  
                      BP13K022                                                  
                      BP13K820                                                  
                      BP13K060                                                  
                      BP13K734                                                  
                      BM06K110                                                  
               OUTPUT BP13F0B6.                                                 
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K800 ERROR ' K800-STATUS                     
              MOVE     K800-STATUS           TO RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K022-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K022 ERROR ' K022-STATUS                     
              MOVE     K022-STATUS           TO RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K820 ERROR ' K820-STATUS                     
              MOVE     K820-STATUS           TO RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K060 ERROR ' K060-STATUS                     
              MOVE     K060-STATUS           TO RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K734-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13K734 ERROR ' K734-STATUS                     
              MOVE     K734-STATUS           TO RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BM06K110 ERROR ' K110-STATUS                     
              MOVE     K110-STATUS           TO RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1500-READ-BP13F200.                                                      
      *-------------------------------------------------------------            
           READ BP13F200                                                        
             AT END                                                             
                IF WS-READ-F200 = ZERO                                          
                   DISPLAY 'BP13F200 FILE IS EMPTY.'                            
                ELSE                                                            
                   DISPLAY 'END OF FILE REACH!'                                 
                END-IF                                                          
                MOVE 'Y'   TO WS-F200-EOF                                       
                GO         TO 1500-EXIT.                                        
                                                                                
           ADD 1    TO WS-READ-F200.                                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                        TO BP13K800-MASTER.               
           MOVE F200-NUM-REGN                 TO K800-NUM-REGN.                 
           PERFORM 2100-READ-BP13K800       THRU 2100-EXIT.                     
                                                                                
           PERFORM 1500-READ-BP13F200       THRU 1500-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           READ BP13K800                                                        
                                                                                
           EVALUATE K800-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2500-MOVE-K800-DATA THRU 2500-EXIT                  
               WHEN 23                                                          
                    DISPLAY 'BP13K800 REC NOTFND ' K800-NUM-REGN                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR, REGN: ' K800-NUM-REGN         
                    MOVE K800-STATUS           TO RETURN-CODE                   
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2500-MOVE-K800-DATA.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                        TO BP13F0B6-RECORD.               
           MOVE K800-NUM-REGN                 TO F0B6-REFNO.                    
           MOVE K800-NUM-STATUS               TO F0B6-STATUS.                   
                                                                                
                                                                                
           MOVE K800-DTE-BALLOT               TO F0B6-DTE-BALLOT.               
           MOVE K800-NUM-HOUSEHOLD            TO F0B6-HH-TYPE.                  
           MOVE K800-NUM-ALLO-CAT             TO F0B6-ALLOC-MODE.               
           MOVE K800-NUM-ALLO-SCHEME          TO F0B6-ALLOC-SCH.                
           MOVE K800-NUM-ELIG-SCHEME         TO F0B6-ELIG-SCH.                  
           IF K800-AMT-HSE-INCOME NOT NUMERIC                                   
              MOVE ZEROES                     TO F0B6-HH-INCOME                 
           ELSE                                                                 
              MOVE K800-AMT-HSE-INCOME        TO F0B6-HH-INCOME                 
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-FLAT-TYPE            TO F0B6-FLAT-TYPE.                
           PERFORM 2550-CALL-BP13CB4A         THRU 2550-EXIT.                   
           MOVE WS-NUM-AHG-STATUS             TO F0B6-AHG-STATUS.               
           MOVE WS-NUM-SHG-STATUS             TO F0B6-SHG-STATUS.               
           MOVE K800-NUM-LOAN-TAG             TO F0B6-LOAN-TAG.                 
                                                                                
      ****GET BOOKED ADDR AND FLAT DESIGN IN PIDB******                         
      *    IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
      *       PERFORM 2700-GET-FLAT-ADDR    THRU 2700-EXIT                      
      *       MOVE COMM8-NME-STREET           TO F0B6-BK-ADDRESS                
      *       MOVE COMM8-CDE-MODL-DESG        TO F0B6-FLAT-DESIGN               
      *    END-IF.                                                              
           MOVE SPACES                        TO F0B6-BK-ADDRESS                
                                                 F0B6-FLAT-DESIGN.              
                                                                                
           MOVE K800-NUM-SERS                 TO F0B6-AFFECTED-SERS.            
                                                                                
           IF K800-DTE-ACCEPTANCE = ZEROES OR LOW-VALUES OR SPACES              
              MOVE '0001-01-01'               TO F0B6-DTE-ACCEPTANCE            
           ELSE                                                                 
              STRING K800-DTE-ACCEPTANCE(1:4) '-'                               
                     K800-DTE-ACCEPTANCE(5:2) '-'                               
                     K800-DTE-ACCEPTANCE(7:2)                                   
                          DELIMITED BY SIZE INTO F0B6-DTE-ACCEPTANCE            
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-SYSTEM-DATE.                
           STRING WS-SYSTEM-DATE(1:4) '-'                                       
                  WS-SYSTEM-DATE(5:2) '-'                                       
                  WS-SYSTEM-DATE(7:2)                                           
                  DELIMITED BY SIZE INTO F0B6-DTE-FORWARDED.                    
                                                                                
           PERFORM 2800-GET-OIC             THRU 2800-EXIT.                     
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE K022-NUM-OIC               TO F0B6-OIC1                      
           ELSE                                                                 
              MOVE K022-NUM-OIC-APPL          TO F0B6-OIC1                      
           END-IF.                                                              
                                                                                
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE SPACES                     TO F0B6-OIC2                      
           ELSE                                                                 
              MOVE K022-NUM-OIC               TO F0B6-OIC2                      
           END-IF.                                                              
                                                                                
           MOVE 'TRUE'                        TO F0B6-REF-HH-STRUCT.            
           MOVE K800-DTE-REGN                 TO F0B6-DTE-REGN.                 
                                                                                
           IF K800-DTE-CANCEL = SPACES OR LOW-VALUES                            
              MOVE ZEROES                     TO K800-DTE-CANCEL                
           END-IF.                                                              
                                                                                
           MOVE K800-DTE-CANCEL               TO F0B6-DTE-CANCEL.               
           MOVE K800-NUM-SCH-ACC              TO F0B6-NUM-SCH-ACCT.             
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              MOVE K800-NUM-SCH-ACC           TO K110-SCH-ACC-NO                
              PERFORM 2900-READ-BM06K110    THRU 2900-EXIT                      
              IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES                     
                 MOVE ZEROES                  TO K110-DTE-KEY-ISSUED            
              END-IF                                                            
              MOVE K110-DTE-KEY-ISSUED        TO F0B6-DTE-KEY-ISSUE             
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 3000-GET-OCCUPIERS       THRU 3000-EXIT.                     
           PERFORM 2600-WRITE-OUTFILE       THRU 2600-EXIT.                     
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2550-CALL-BP13CB4A.                                                      
      ******************************************************************        
           INITIALIZE COM73-COMMAREA.                                           
                                                                                
           MOVE 'R'                  TO  COM73-NUM-CALL-ACTION.                 
           MOVE F200-NUM-REGN        TO  COM73-NUM-RGSTRN-INPUT.                
           CALL 'BP13CB4A' USING COM73-COMMAREA.                                
                                                                                
           IF COM73-CDE-ERROR = SPACES OR ZEROES                                
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE SPACES                TO COM73-COMMAREA                      
              INITIALIZE                    COM73-COMMAREA                      
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-A FROM 1 BY 1 UNTIL WS-A > 5                      
             IF COM73-NUM-GRANT-TYPE(WS-A) = 'AHG'                              
                MOVE COM73-GRANT-STATUS(WS-A) TO WS-NUM-AHG-STATUS              
             END-IF                                                             
             IF COM73-NUM-GRANT-TYPE(WS-A) = 'SHG'                              
                MOVE COM73-GRANT-STATUS(WS-A) TO WS-NUM-SHG-STATUS              
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
       2550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2600-WRITE-OUTFILE.                                                      
      *-------------------------------------------------------------            
           WRITE BP13F0B6-RECORD.                                               
           MOVE SPACES                        TO BP13F0B6-RECORD.               
           ADD 1                              TO WS-WRITE-F0B6.                 
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2800-GET-OIC.                                                            
      *-------------------------------------------------------------            
           MOVE SPACES                        TO BP13K022-REC.                  
           INITIALIZE                            BP13K022-REC.                  
                                                                                
           MOVE K800-NUM-REGN                 TO K022-NUM-REGN.                 
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE K022-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE SPACES               TO K022-NUM-OIC                   
                                                 K022-NUM-OIC-APPL              
               WHEN OTHER                                                       
                    DISPLAY 'BP13K022 - READ FILE ERROR.'                       
                    DISPLAY 'RETURN CODE : (' K022-STATUS ')'                   
                    MOVE K022-STATUS          TO RETURN-CODE                    
                    PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       2800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2900-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           READ BM06K110.                                                       
           EVALUATE K110-STATUS                                                 
             WHEN 00                                                            
                  CONTINUE                                                      
             WHEN 23                                                            
                  MOVE ZEROES                     TO K110-DTE-KEY-ISSUED        
             WHEN OTHER                                                         
                  DISPLAY 'BM06K110 - READ ERROR (' K110-STATUS ').'            
                  DISPLAY 'KEY READ : ' K110-SCH-ACC-NO                         
                  PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-GET-OCCUPIERS.                                                      
      *-------------------------------------------------------------            
           MOVE 'N'                       TO WS-K820-EOF.                       
           MOVE 5                             TO WS-CTR2.                       
           MOVE SPACES                        TO BP13K820-REC.                  
           MOVE K800-NUM-REGN                 TO K820-NUM-REGN.                 
           MOVE SPACES                        TO K820-NUM-NRIC.                 
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD                                   
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 3100-READ-BP13K820  THRU 3100-EXIT                  
                       UNTIL K820-NUM-REGN NOT = F200-NUM-REGN                  
                          OR WS-K820-EOF = 'Y'                                  
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 - STARTREAD ERROR.'                       
                    DISPLAY 'READ KEY : ' K820-KEY-FLD                          
                    DISPLAY 'FILE STATUS : ' K820-STATUS                        
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
           READ BP13K820 NEXT                                                   
           AT END                                                               
              MOVE 'Y'                        TO WS-K820-EOF                    
           END-READ                                                             
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K820-NUM-REGN = K800-NUM-REGN                            
                       PERFORM 3200-EVALUATE-NRIC THRU 3200-EXIT                
                    ELSE                                                        
                       MOVE 'Y'               TO WS-K820-EOF                    
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                  TO WS-K820-EOF                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 - STARTREAD ERROR.'                       
                    DISPLAY 'READ KEY : ' K820-KEY-FLD                          
                    DISPLAY 'FILE STATUS : ' K820-STATUS                        
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-EVALUATE-NRIC.                                                      
      *-------------------------------------------------------------            
           IF K820-NUM-NRIC = K800-NUM-NRIC1                                    
              MOVE 1                      TO WS-CTR                             
              MOVE K820-NUM-PHONE         TO F0B6-CONTACT-NO                    
              MOVE 'HA'                   TO F0B6-PERSONAL-TYPE(WS-CTR)         
              PERFORM 3300-MOVE-OTHER-DATA   THRU 3300-EXIT                     
           ELSE                                                                 
           IF K820-NUM-NRIC = K800-NUM-NRIC2                                    
              MOVE 2                      TO WS-CTR                             
              MOVE 'HA'                   TO F0B6-PERSONAL-TYPE(WS-CTR)         
              PERFORM 3300-MOVE-OTHER-DATA   THRU 3300-EXIT                     
           ELSE                                                                 
           IF K820-NUM-NRIC = K800-NUM-NRIC3                                    
              MOVE 3                      TO WS-CTR                             
              MOVE 'HA'                   TO F0B6-PERSONAL-TYPE(WS-CTR)         
              PERFORM 3300-MOVE-OTHER-DATA   THRU 3300-EXIT                     
           ELSE                                                                 
           IF K820-NUM-NRIC = K800-NUM-NRIC4                                    
              MOVE 4                      TO WS-CTR                             
              MOVE 'HA'                   TO F0B6-PERSONAL-TYPE(WS-CTR)         
              PERFORM 3300-MOVE-OTHER-DATA   THRU 3300-EXIT                     
           ELSE                                                                 
           IF WS-CTR2 <= 8                                                      
              MOVE WS-CTR2                TO WS-CTR                             
              MOVE 'OC'                   TO F0B6-PERSONAL-TYPE(WS-CTR)         
              PERFORM 3300-MOVE-OTHER-DATA   THRU 3300-EXIT                     
              ADD 1                       TO WS-CTR2                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-MOVE-OTHER-DATA.                                                    
      *-------------------------------------------------------------            
           IF K820-NUM-NRIC = K800-NUM-NRIC1                                    
              MOVE 'TRUE'                 TO F0B6-MAIN-APPLNT(WS-CTR)           
           ELSE                                                                 
              MOVE 'FALSE'                TO F0B6-MAIN-APPLNT(WS-CTR)           
           END-IF.                                                              
                                                                                
           MOVE 'FALSE'                   TO F0B6-NUM-DEL    (WS-CTR).          
           MOVE K820-NUM-CSTMR-SOURCE     TO F0B6-CUSTOMER-ID(WS-CTR).          
           MOVE K820-NUM-NRIC             TO F0B6-NUM-NRIC   (WS-CTR).          
           MOVE K820-NME-OCCP             TO F0B6-NAME       (WS-CTR).          
           MOVE K820-NUM-MARITAL-STATUS   TO F0B6-MARITAL-STAT(WS-CTR).         
           IF K820-NUM-MARITAL-STATUS IS NOT ALPHABETIC                         
              MOVE 44                        TO K060-SERIAL-NO                  
              MOVE K820-NUM-MARITAL-STATUS   TO K060-CODE                       
              PERFORM 3350-READ-BP13K060   THRU 3350-EXIT                       
           END-IF.                                                              
                                                                                
           STRING K820-DTE-BIRTH(1:4) '-'                                       
                  K820-DTE-BIRTH(5:2) '-'                                       
                  K820-DTE-BIRTH(7:2)                                           
                  DELIMITED BY SIZE INTO F0B6-DTE-BIRTH(WS-CTR).                
                                                                                
           MOVE K820-NUM-RELATIONSHIP      TO F0B6-RELTNSHP   (WS-CTR).         
           MOVE K820-NUM-CITIZENSHIP       TO F0B6-CITIZENSHIP(WS-CTR).         
                                                                                
      ** GET DATE/AMT INCOME BP13K734**                                         
           MOVE SPACES                     TO K734-KEY-FLD.                     
           MOVE K820-NUM-REGN              TO K734-NUM-REGN.                    
           MOVE K820-NUM-NRIC              TO K734-NUM-NRIC.                    
           PERFORM 3305-READ-BP13K734    THRU 3305-EXIT.                        
                                                                                
           MOVE K800-CDE-MCPS-REQ          TO F0B6-MCPS(WS-CTR).                
           MOVE K800-NUM-PPO               TO F0B6-PPO (WS-CTR).                
                                                                                
      ****INITIALLY MOVE SPACES TO THIS 2 FIELDS BELOW****                      
           MOVE SPACES                     TO F0B6-NIRC-SOC (WS-CTR)            
                                              F0B6-NME-SOC  (WS-CTR).           
                                                                                
           MOVE WS-INCOME-MONTHS           TO F0B6-INCOME-MON(WS-CTR).          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3305-READ-BP13K734.                                                      
      *-------------------------------------------------------------            
           READ BP13K734.                                                       
                                                                                
           EVALUATE K734-STATUS                                                 
              WHEN 00                                                           
                   MOVE    ZERO                 TO WS-INCOME-MONTHS             
                   PERFORM 3306-MOVE-INCOME   THRU 3306-EXIT                    
                                                                                
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K734.'                      
                   DISPLAY 'KEY : ' K734-KEY-FLD                                
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K734, STAT : ' K734-STATUS        
                   DISPLAY 'KEY: ' K734-KEY-FLD                                 
                   PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3305-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3306-MOVE-INCOME.                                                        
      *-------------------------------------------------------------            
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
             UNTIL WS-IDX > 12                                                  
             IF (K734-DTE-INCOME(WS-IDX) NOT = SPACES AND LOW-VALUES            
                                               AND ZEROES)                      
                IF (K734-NUM-INCOME(WS-IDX) = ZEROES) AND                       
                   (K734-NUM-INCOME(1) NOT = SPACES AND LOW-VALUES              
                                         AND ZEROES) AND                        
                   (K734-NUM-EMPLY-STATUS = '5  ' OR '7  ' OR '9  ')            
                    MOVE K734-DTE-INCOME(WS-IDX)                                
                      TO F0B6-DTE-INCOME(WS-CTR, WS-IDX)                        
                    MOVE K734-NUM-INCOME(1)                                     
                      TO F0B6-NUM-INCOME(WS-CTR, WS-IDX)                        
                ELSE                                                            
                    MOVE K734-DTE-INCOME(WS-IDX)                                
                      TO F0B6-DTE-INCOME(WS-CTR, WS-IDX)                        
                    MOVE K734-NUM-INCOME(WS-IDX)                                
                      TO F0B6-NUM-INCOME(WS-CTR, WS-IDX)                        
                END-IF                                                          
                ADD  1                TO WS-INCOME-MONTHS                       
             ELSE                                                               
                MOVE SPACES           TO F0B6-DTE-INCOME(WS-CTR, WS-IDX)        
                MOVE ZEROES           TO F0B6-NUM-INCOME(WS-CTR, WS-IDX)        
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           MOVE K734-NUM-EMPLY-STATUS TO F0B6-EMPLY-STATUS(WS-CTR).             
                                                                                
       3306-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3350-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
           READ BP13K060.                                                       
                                                                                
           EVALUATE K060-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K060-DESC(1:2)   TO F0B6-MARITAL-STAT(WS-CTR)          
               WHEN 23                                                          
                    MOVE SPACES           TO F0B6-MARITAL-STAT(WS-CTR)          
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING FILE, BP13K060!!'                    
                    DISPLAY 'ERROR CODE :' K060-STATUS                          
                    MOVE K060-STATUS      TO RETURN-CODE                        
                    PERFORM  9000-CLOSE-FILES THRU 9000-EXIT                    
           END-EVALUATE.                                                        
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '*************************************'.                     
           DISPLAY '*----- BP13CRB6 CONTROL TOTALS -----*'.                     
           DISPLAY '*************************************'.                     
           DISPLAY '*NO OF RECS READ    IN F200: ' WS-READ-F200                 
           DISPLAY '*NO OF RECS WRITE   IN F0B6: ' WS-WRITE-F0B6                
           DISPLAY '****************************************'.                  
                                                                                
           CLOSE BP13F200                                                       
                 BP13K800                                                       
                 BP13K022                                                       
                 BP13K820                                                       
                 BP13K820                                                       
                 BP13K060                                                       
                 BP13K734                                                       
                 BM06K110                                                       
                 BP13F0B6.                                                      
           STOP RUN.                                                            
       9000-EXIT.                                                               
