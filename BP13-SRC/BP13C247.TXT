       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C247.                                                 
      *AUTHOR.        SOH ENG GHEE.                                             
      *DATE-WRITTEN.  22/06/89.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE  :                                        *                
      *            TO UPDATE ALL THE FOLLOWING FILES WITH THE  *                
      *            REVISED ALLOCATION DATE:                    *                
      *            1.  BP13K200.                               *                
      *            2.  BP13K210.                               *                
      *            3.  BP13K222.                               *                
      *            4.  BP13K270.                               *                
      *            5.  BM06K210 CHGE TO BM06K100               *                
      *            6.  BM06K110 CHGE TO BM06K110               *                
      *            7.  BM06F800.                               *                
      *            8.  BP13K230.                               *                
      *                                                        *                
      *    INPUT    :  BP13F205                                *                
      *                BM06K510                                *                
      *    I-O      :  BP13K200                                *                
      *                BP13K210                                *                
      *                BP13K222                                *                
      *                BM06K100                                *                
      *                BM06K110                                *                
      *                BP13K270                                *                
      *                BP13K230                                *                
      *                BP13K813                                *                
      *    OUTPUT   :  BP13L247 (CONTROL LIST)                 *                
      *                BP13F230 (FLAT FILE)                    *                
      *                P13F222D (FLAT FILE-DELETE)             *                
      *                P13F222W (FLAT FILE-WRITE)              *                
      *                                                        *                
      *--------------------------------------------------------*                
      *  CHG-NO BY    ON     DESCRIPTION                       *                
      *  -------------------------------                       *                
      * C901227 LSB 900118 - TAKE OUT BM06K450, BM06K480 FILES *                
      * C910153 LSB 910201 - TAKE OUT BM06K440 FOR STEADY STAGE*                
      * C9107B9 LSB 910805 - TAKE OUT K010-TRANSFER-IN-DATE    *                
      *         RAZ 921123 - TAKE OUT BM06K901 LOG FILE        *                
      * N940009 BAL 940328 - REPLACE BM06CTL  W/ BP13F205 FILE *                
      *                      REPLACE BM06K010 W/ BP13K200 FILE *                
      *                      TAKE OUT BM06K460 FILE            *                
      *                      EXPAND K270-ALLOCN-DTE TO 8 BYTES *                
      * N940009 LMS 940607 - TO INCLUDE UPDATE TO BP13K210 &   *                
      *                      BP13K222                          *                
      * C930933 OCT4 940722 - TO UPDATE NEW ALLOCATION DATE TO *                
      *                       BM06F800                         *                
      * SOC-PH9 GD   941110 - TO CATER FOR ZONE CODES          *                
      * C941228 GJ   950119 - TO UPDATE NEW ALLOCATION DATE TO *                
      *                       BP13K230 & BP13K240              *                
      *BP130067 LMS  951219 - CHANGE IN FILE LAYOUT OF BP13K222*                
      *BP130164 LMS  960903 - TO CHANGE THE READING KEY OF     *                
      *                       BP13K222                         *                
      *BP130340 SSS  971120 - INCLUDE PG ZONE                  *                
      *BP130418 SIA  980301 - CHGE BM06K210 TO BM06K100        *                
      *                       CHGE BM06K211 TO BM06K110        *                
      *BP130956 AKT  001118 - CHGE BP13K230/BP13K240 LRECL     *                
      *                       FROM 130 TO 250                  *                
      *BP131017 JAM  010612 - MAKE SEPARATE FLAT FILES FOR     *                
      *                       DELETED RECORDS & WRITTEN RECORDS*                
      *                       (P13F222D & P13F222W)FOR BP13K222*                
      *                       TO MOVE UPDATING OF BP13K240 TO  *                
      *                       BP13C218.                        *                
      *BP131059 JAM  010910 - TO UPDATE NEW ALLOCATION FOR BTO *                
      *                       FLATS.                           *                
      *BP132095 MTD  020423 - EXPAND LENGTH OF BP13K200        *                
      *                       (FROM 100 TO 300)                *                
      *BP132278 JF4  280303 - CHGE BP13F230/F240 LRECL TO 450. *                
      *                       CHGE BM06F800 LRECL TO 800       *                
      *BP133610 ESA1 280409 - TO REPLACE BP13K767 W/ BP13K816  *                
      *BP135305 ESA1 170614 - TO REPLACE BP13K816 W/ BP13K813  *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13F230 ASSIGN TO BP13F230.                                  
           SELECT P13F222D ASSIGN TO P13F222D.                                  
           SELECT P13F222W ASSIGN TO P13F222W.                                  
           SELECT BM06F800 ASSIGN TO BM06F800.                                  
                                                                                
           SELECT BP13K200 ASSIGN TO BP13K200                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K200-KEY-FLD                               
                  FILE STATUS     IS BP13K200-STATUS.                           
                                                                                
           SELECT BP13K210 ASSIGN TO BP13K210                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K210-KEY-FLD OF BP13K210-REC               
                  FILE STATUS     IS BP13K210-STATUS.                           
                                                                                
           SELECT BP13K222 ASSIGN TO BP13K222                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K222-KEY-FLD                               
                  FILE STATUS     IS BP13K222-STATUS.                           
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K100-KEY-FLD                               
                  FILE STATUS     IS BM06K100-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K110-AIX1                             
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K270 ASSIGN TO BP13K270                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K270-KEY-FLD                               
                  FILE STATUS     IS BP13K270-STATUS.                           
                                                                                
           SELECT BP13K230 ASSIGN TO BP13K230                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K230-KEY-FLD                               
                  FILE STATUS     IS BP13K230-STATUS.                           
                                                                                
           SELECT BM06K510 ASSIGN TO BM06K510                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K510-ESTCDE                                
                  FILE STATUS     IS BM06K510-STATUS.                           
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS BP13K813-STATUS.                           
                                                                                
           SELECT BP13L247 ASSIGN TO BP13L247.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F205                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F230                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 450 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F230.                                                           
                                                                                
       FD  P13F222D                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F222D-REC        PIC X(100).                                      
                                                                                
       FD  P13F222W                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F222W-REC        PIC X(100).                                      
                                                                                
       FD  BM06F800                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 800  CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F800.                                                           
                                                                                
       FD  BP13K200                                                             
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY BP13K200.                                                           
                                                                                
       FD  BP13K210                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K210.                                                           
                                                                                
       FD  BP13K222                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K222.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60 CHARACTERS.                                       
       COPY BM06K510.                                                           
                                                                                
       FD  BP13K270                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K270.                                                           
                                                                                
       FD  BP13K230                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K230.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13L247                                                             
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC-1              PIC X(132).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K200-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BP13K210-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BP13K222-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BM06K100-STATUS.                                                     
           03 BM06K100-STATUS-1     PIC 9(1)  VALUE ZEROS.                      
           03 BM06K100-STATUS-2     PIC 9(1)  VALUE ZEROS.                      
       01  BM06K110-STATUS.                                                     
           03 BM06K110-STATUS-1     PIC 9(1)  VALUE ZEROS.                      
           03 BM06K110-STATUS-2     PIC 9(1)  VALUE ZEROS.                      
       01  BP13K270-STATUS.                                                     
           03 BP13K270-STATUS-1     PIC 9(1)  VALUE ZEROS.                      
           03 BP13K270-STATUS-2     PIC 9(1)  VALUE ZEROS.                      
       01  BP13K230-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BM06K510-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  BP13K813-STATUS          PIC 9(2)  VALUE ZEROES.                     
       01  WS-K813-FOUND            PIC X(1)  VALUE SPACES.                     
       01  K200-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K200-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  K210-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K210-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  K222-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K222-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  K100-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K100-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  K110-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K110-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  K270-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K270-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  K230-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K230-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  K813-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  F800-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  F800-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  F230-WRITE-CNT           PIC 9(6)  VALUE ZEROS.                      
       01  F222-WRITE-CNT           PIC 9(6)  VALUE ZEROS.                      
       01  F222-DELETE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  WS-YEAR                  PIC 9(2)  VALUE ZEROS.                      
       01  WS-MONTH                 PIC 9(2)  VALUE ZEROS.                      
       01  WS-PRINT-PAGE            PIC 9(3)  VALUE ZEROS.                      
       01  CTRL-CDE-FLAT-TYPE       PIC X(02) VALUE SPACES.                     
       01  CTRL-NUM-SELECTION       PIC X(03) VALUE SPACES.                     
                                                                                
       01  WS-REASON.                                                           
           05 WS-REASON-1           PIC X(4)  VALUE SPACES.                     
           05 WS-REASON-2           PIC X(6)  VALUE SPACES.                     
                                                                                
       01  WS-CTRL-KEY.                                                         
           05 CTRL-OLD-DATE.                                                    
              10 WS-CC              PIC 9(02) VALUE ZEROS.                      
              10 WS-YY              PIC 9(02) VALUE ZEROS.                      
              10 WS-MM              PIC 9(02) VALUE ZEROS.                      
              10 WS-DD              PIC 9(02) VALUE ZEROS.                      
           05 CTRL-CDE-NT-ZONE      PIC X(03) VALUE SPACES.                     
                                                                                
       01  CTRL-NEW-DATE.                                                       
              05 WS-CC              PIC 9(02) VALUE ZEROS.                      
              05 WS-YY              PIC 9(02) VALUE ZEROS.                      
              05 WS-MM              PIC 9(02) VALUE ZEROS.                      
              05 WS-DD              PIC 9(02) VALUE ZEROS.                      
                                                                                
       01  WS-K100-KEY.                                                         
           05 WS-EST                PIC X(03) VALUE SPACES.                     
           05 WS-NEIGH              PIC X(04) VALUE SPACES.                     
           05 WS-CONTR              PIC X(04) VALUE SPACES.                     
           05 WS-BLK                PIC X(05) VALUE SPACES.                     
                                                                                
       01  EOF-FLAG.                                                            
           05 EOF-F205              PIC X     VALUE 'N'.                        
           05 EOF-F800              PIC X     VALUE 'N'.                        
           05 EOF-K200              PIC X     VALUE 'N'.                        
           05 EOF-K210              PIC X     VALUE 'N'.                        
           05 EOF-K222              PIC X     VALUE 'N'.                        
           05 EOF-K100              PIC X     VALUE 'N'.                        
           05 EOF-K110              PIC X     VALUE 'N'.                        
           05 EOF-K270              PIC X     VALUE 'N'.                        
           05 EOF-K230              PIC X     VALUE 'N'.                        
                                                                                
       01  WS-DATE.                                                             
           05 WS-YY                 PIC 9(2).                                   
           05 WS-MM                 PIC 9(2).                                   
           05 WS-DD                 PIC 9(2).                                   
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-DD                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-MM                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-CC                 PIC 9(2)  VALUE ZEROS.                      
           05 WS-YY                 PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-TIME.                                                             
           05 WS-HH                 PIC X(2).                                   
           05 WS-MM                 PIC X(2).                                   
           05 WS-SS                 PIC X(2).                                   
                                                                                
       01  WS-EDITED-TIME.                                                      
           05 WS-SYS-TIME           PIC X(6).                                   
           05 WS-MICRO-SEC          PIC 9(1)  VALUE 0.                          
                                                                                
       01  L247-LINE1.                                                          
           05 FILLER                PIC X(5)  VALUE SPACES.                     
           05 FILLER                PIC X(7)  VALUE 'PAGE : '.                  
           05 PRINT-PAGE            PIC ZZ9.                                    
           05 FILLER                PIC X(28) VALUE SPACES.                     
           05 FILLER                PIC X(24) VALUE                             
             'SYSTEM   OF   COMMITMENT'.                                        
           05 FILLER                PIC X(36) VALUE SPACES.                     
           05 FILLER                PIC X(07) VALUE 'DATE : '.                  
           05 PRINT-DATE.                                                       
              10 WS-DD              PIC 9(2).                                   
              10 FILLER             PIC X     VALUE '/'.                        
              10 WS-MM              PIC 9(2).                                   
              10 FILLER             PIC X     VALUE '/'.                        
              10 WS-CC              PIC 9(2).                                   
              10 WS-YY              PIC 9(2).                                   
           05 FILLER                PIC X(5)  VALUE SPACES.                     
                                                                                
       01  L247-LINE2.                                                          
           05 FILLER                PIC X(35) VALUE SPACES.                     
           05 FILLER                PIC X(43) VALUE                             
             'UPDATING   OF   REVISED   ALLOCATION   DATE'.                     
           05 FILLER                PIC X(25) VALUE SPACES.                     
           05 FILLER                PIC X(22) VALUE                             
             'LISTING-ID  : BP13L247'.                                          
           05 FILLER                PIC X(7)  VALUE SPACES.                     
                                                                                
       01  L247-LINE3.                                                          
           05 FILLER                PIC X(08) VALUE SPACES.                     
           05 FILLER                PIC X(22) VALUE                             
             'OLD ALLOCATION DATE : '.                                          
           05 L-OLD-DATE            PIC X(10)  VALUE SPACES.                    
           05 FILLER                PIC X(27)                                   
              VALUE '     NEW ALLOCATION DATE : '.                              
           05 L-NEW-DATE            PIC X(10)  VALUE SPACES.                    
           05 FILLER                PIC X(5)  VALUE SPACES.                     
           05 FILLER                PIC X(15) VALUE 'CODE-NT-ZONE : '.          
           05 L-CDE-NT              PIC X(3)   VALUE SPACES.                    
           05 FILLER                PIC X(29) VALUE SPACES.                     
                                                                                
       01  L247-LINE4.                                                          
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BOOKING APPOINTMENT RECORDS READ     : '.                     
           05 L-K200-READ-CNT       PIC ZZZZZ9  VALUE ZEROS.                    
                                                                                
       01  L247-LINE5.                                                          
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BOOKING APPOINTMENT RECORDS UPDATED  : '.                     
           05 L-K200-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE6.                                                          
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BOOKING WORKPLAN RECORDS READ        : '.                     
           05 L-K210-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE7.                                                          
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BOOKING WORKPLAN RECORDS UPDATED     : '.                     
           05 L-K210-UPDATE-CNT PIC ZZZZZ9 VALUE ZEROS.                         
                                                                                
       01  L247-LINE8.                                                          
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BOOKING SUMMARY RECORDS READ         : '.                     
           05 L-K222-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE9.                                                          
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BOOKING SUMMARY RECORDS UPDATED      : '.                     
           05 L-K222-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE12.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF ALLOCATION RECORDS READ              : '.                     
           05 L-K270-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE13.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF ALLOCATION RECORDS UPDATED           : '.                     
           05 L-K270-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE14.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BLOCK SUMMARY RECORDS READ           : '.                     
           05 L-K100-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE15.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BLOCK SUMMARY RECORDS UPDATED        : '.                     
           05 L-K100-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE16.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF FLAT INVENTORY RECORDS READ          : '.                     
           05 L-K110-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE17.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF FLAT INVENTORY RECORDS UPDATED       : '.                     
           05 L-K110-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE18.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BM06F800 RECORDS READ                : '.                     
           05 L-F800-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE19.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF BM06F800 RECORDS UPDATED             : '.                     
           05 L-F800-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE20.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF DETAIL INTV RECORDS READ             : '.                     
           05 L-K230-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L247-LINE21.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF DETAIL INTV RECORDS UPDATED          : '.                     
           05 L-K230-UPDATE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-CTRL-REC THRU 200-EXIT                              
                   UNTIL EOF-F205 = 'Y'.                                        
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BM06K510                                                 
                       BP13K222                                                 
                       BP13K230                                                 
                       BP13K813                                                 
                OUTPUT BP13L247                                                 
                       BP13F230                                                 
                       P13F222D                                                 
                       P13F222W                                                 
                I-O    BP13K200                                                 
                       BP13K210                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BP13K270.                                                
                                                                                
           IF BP13K200-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K200 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K200-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K210-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K210 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K210-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K222-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K222 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K222-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BM06K100 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BM06K100-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BM06K110 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BM06K110-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K270-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K270 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K270-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K230-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K230 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K230-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BM06K510-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BM06K510 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BM06K510-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K813 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K813-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-CTRL-REC.                                                       
      *-------------------------------------------------------------            
           MOVE ZEROS TO  K200-READ-CNT,     K200-UPDATE-CNT,                   
                          K210-READ-CNT,     K210-UPDATE-CNT,                   
                          K222-READ-CNT,     K222-UPDATE-CNT,                   
                          K100-READ-CNT,     K100-UPDATE-CNT,                   
                          K110-READ-CNT,     K110-UPDATE-CNT,                   
                          K270-READ-CNT,     K270-UPDATE-CNT,                   
                          K230-READ-CNT,     K230-UPDATE-CNT,                   
                          F800-READ-CNT,     F800-UPDATE-CNT,                   
                          CTRL-OLD-DATE,     CTRL-NEW-DATE,                     
                          F222-WRITE-CNT,    F222-DELETE-CNT,                   
                          K813-READ-CNT.                                        
                                                                                
           MOVE SPACES TO CTRL-CDE-NT-ZONE, CTRL-CDE-FLAT-TYPE,                 
                          CTRL-NUM-SELECTION,                                   
                          EOF-FLAG,          WS-REASON.                         
                                                                                
           READ BP13F205 AT END                                                 
                MOVE 'Y' TO EOF-F205                                            
                GO TO 200-EXIT                                                  
                END-READ.                                                       
                                                                                
           MOVE F205-NUM-NT-ZONE    TO CTRL-CDE-NT-ZONE.                        
           MOVE F205-NUM-FLAT-TYPE  TO CTRL-CDE-FLAT-TYPE.                      
           MOVE F205-DTE-ALLOCN     TO CTRL-OLD-DATE.                           
           MOVE F205-DTE-ALLOCN-NEW TO CTRL-NEW-DATE.                           
           MOVE F205-NUM-SELECTION  TO CTRL-NUM-SELECTION.                      
                                                                                
           PERFORM 800-PRINT-CONTROL THRU 800-EXIT.                             
           PERFORM 202-ACCESS-K270   THRU 202-EXIT.                             
                                                                                
           MOVE K270-READ-CNT       TO L-K270-READ-CNT.                         
           MOVE K270-UPDATE-CNT     TO L-K270-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE12 AFTER 2.                     
           WRITE PRINT-REC-1      FROM L247-LINE13 AFTER 2.                     
                                                                                
           MOVE K100-READ-CNT       TO L-K100-READ-CNT.                         
           MOVE K100-UPDATE-CNT     TO L-K100-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE14 AFTER 2.                     
           WRITE PRINT-REC-1      FROM L247-LINE15 AFTER 2.                     
                                                                                
           MOVE K110-READ-CNT       TO L-K110-READ-CNT.                         
           MOVE K110-UPDATE-CNT     TO L-K110-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE16 AFTER 2.                     
           WRITE PRINT-REC-1      FROM L247-LINE17 AFTER 2.                     
                                                                                
           MOVE K230-READ-CNT       TO L-K230-READ-CNT.                         
           MOVE K230-UPDATE-CNT     TO L-K230-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE20    AFTER 2.                  
           WRITE PRINT-REC-1      FROM L247-LINE21    AFTER 2.                  
                                                                                
           PERFORM 400-READ-K200-REC THRU 400-EXIT.                             
                                                                                
           MOVE K200-READ-CNT       TO L-K200-READ-CNT.                         
           MOVE K200-UPDATE-CNT     TO L-K200-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE4  AFTER 2.                     
           WRITE PRINT-REC-1      FROM L247-LINE5  AFTER 2.                     
                                                                                
           PERFORM 500-READ-BP13K210-REC THRU 500-EXIT.                         
                                                                                
           MOVE K210-READ-CNT       TO L-K210-READ-CNT.                         
           MOVE K210-UPDATE-CNT     TO L-K210-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE6    AFTER 2.                   
           WRITE PRINT-REC-1      FROM L247-LINE7    AFTER 2.                   
                                                                                
           PERFORM 600-READ-K222-REC THRU 600-EXIT.                             
                                                                                
           MOVE K222-READ-CNT       TO L-K222-READ-CNT.                         
           MOVE K222-UPDATE-CNT     TO L-K222-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE8    AFTER 2.                   
           WRITE PRINT-REC-1      FROM L247-LINE9    AFTER 2.                   
                                                                                
           PERFORM 700-READ-F800-REC THRU 700-EXIT.                             
                                                                                
           MOVE F800-READ-CNT       TO L-F800-READ-CNT.                         
           MOVE F800-UPDATE-CNT     TO L-F800-UPDATE-CNT.                       
                                                                                
           WRITE PRINT-REC-1      FROM L247-LINE18    AFTER 2.                  
           WRITE PRINT-REC-1      FROM L247-LINE19    AFTER 2.                  
                                                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       202-ACCESS-K270.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES              TO K270-KEY-FLD.                            
           MOVE F205-DTE-ALLOCN     TO K270-ALDATE-ZONE.                        
           START BP13K270 KEY NOT < K270-KEY-FLD.                               
                                                                                
           IF BP13K270-STATUS NOT = ZEROS                                       
              DISPLAY 'BP13K270 START ERROR , KEY = ' K270-KEY-FLD              
              DISPLAY 'BP13K270 STATUS = ' BP13K270-STATUS                      
              MOVE 'Y' TO EOF-K270                                              
           ELSE                                                                 
              READ BP13K270 NEXT AT END MOVE 'Y' TO EOF-K270                    
           END-IF.                                                              
                                                                                
           PERFORM 210-READ-K270 THRU 210-EXIT                                  
             UNTIL EOF-K270 = 'Y'                                               
                OR K270-ALLOCN-DATE > F205-DTE-ALLOCN.                          
                                                                                
       202-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       210-READ-K270.                                                           
      *-------------------------------------------------------------            
           ADD 1 TO K270-READ-CNT.                                              
                                                                                
           MOVE SPACES TO WS-K813-FOUND.                                        
                                                                                
           IF CTRL-NUM-SELECTION = 'BTO'                                        
              PERFORM 760-READ-K813-REC THRU 760-EXIT                           
           END-IF.                                                              
                                                                                
           IF F205-DTE-ALLOCN = K270-ALLOCN-DATE                                
              IF (WS-K813-FOUND = 'Y' AND K813-NUM-ZONE NOT = '00') OR          
                 (F205-NUM-NT-ZONE = K270-CDE-NT OR K270-CDE-APPL-ZONE)         
                                                                                
                 MOVE K270-ESTATE        TO WS-EST                              
                 MOVE K270-NEIGHBOURHOOD TO WS-NEIGH                            
                 MOVE K270-CONTRACT-NO   TO WS-CONTR                            
                 MOVE K270-NUM-BLK       TO WS-BLK                              
                 PERFORM 250-K100-REC THRU 250-EXIT                             
                 PERFORM 260-K110-REC THRU 260-EXIT                             
                                                                                
                 DELETE BP13K270                                                
                                                                                
                 IF BP13K270-STATUS NOT = ZEROS                                 
                    DISPLAY 'K270 DELETE ERROR, STATUS ' BP13K270-STATUS        
                 ELSE                                                           
                    MOVE CTRL-NEW-DATE TO K270-ALLOCN-DATE                      
                    WRITE K270-REC                                              
                    IF BP13K270-STATUS = 00 OR 02                               
                       ADD 1 TO K270-UPDATE-CNT                                 
                    ELSE                                                        
                       DISPLAY 'BP13K270 WRITE ERROR =' BP13K270-STATUS         
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 212-READ-NEXT-K270-REC THRU 212-EXIT.                        
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       212-READ-NEXT-K270-REC.                                                  
      *-------------------------------------------------------------            
           READ BP13K270 NEXT AT END MOVE 'Y' TO EOF-K270.                      
                                                                                
       212-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       250-K100-REC.                                                            
      *-------------------------------------------------------------            
           MOVE WS-K100-KEY TO K100-KEY-FLD OF K100-REC.                        
           READ BM06K100.                                                       
                                                                                
           IF BM06K100-STATUS NOT = ZEROS                                       
              DISPLAY 'READ BM06K100 ERROR, STATUS ' BM06K100-STATUS            
              DISPLAY 'BM06K100-KEY-FLD IS ' K100-KEY-FLD OF K100-REC           
              GO TO 250-EXIT                                                    
           END-IF.                                                              
                                                                                
           ADD 1 TO K100-READ-CNT.                                              
                                                                                
           IF K100-DTE-ALLOCN = CTRL-OLD-DATE                                   
              MOVE CTRL-NEW-DATE TO K100-DTE-ALLOCN                             
           END-IF.                                                              
                                                                                
           IF K100-DTE-1ST-SALE = CTRL-OLD-DATE                                 
              MOVE CTRL-NEW-DATE TO K100-DTE-1ST-SALE                           
           END-IF.                                                              
           REWRITE K100-REC.                                                    
                                                                                
           ADD 1 TO K100-UPDATE-CNT.                                            
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       260-K110-REC.                                                            
      *-------------------------------------------------------------            
           MOVE WS-K100-KEY TO K110-AIX1.                                       
           START BM06K110 KEY NOT < K110-AIX1.                                  
                                                                                
           IF BM06K110-STATUS-1 NOT = 0                                         
              DISPLAY 'BM06K110 START ERROR, STATUS ' BM06K110-STATUS           
              GO TO 260-EXIT                                                    
           END-IF.                                                              
                                                                                
           READ BM06K110 NEXT AT END                                            
                              MOVE 'Y' TO EOF-K110                              
                              GO TO 260-EXIT.                                   
                                                                                
           PERFORM 270-READ-K110 THRU 270-EXIT                                  
                   UNTIL EOF-K110 = 'Y' OR                                      
                   WS-K100-KEY NOT = K110-AIX1.                                 
                                                                                
       260-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       270-READ-K110.                                                           
      *-------------------------------------------------------------            
           IF K110-AIX1 = WS-K100-KEY AND                                       
              K110-DTE-ALLOCN = CTRL-OLD-DATE                                   
              ADD  1                 TO K110-READ-CNT                           
              MOVE CTRL-NEW-DATE     TO K110-DTE-ALLOCN                         
              ADD  1                 TO K110-UPDATE-CNT                         
                                                                                
              REWRITE K110-REC                                                  
                                                                                
              PERFORM 750-READ-K230-REC THRU 750-EXIT                           
           END-IF.                                                              
                                                                                
           READ BM06K110 NEXT                                                   
                AT END MOVE 'Y' TO EOF-K110                                     
                       GO TO 270-EXIT.                                          
                                                                                
           IF BM06K110-STATUS-1 NOT = 0                                         
              DISPLAY 'BM06K110 READ ERROR, STATUS ' BM06K110-STATUS            
              DISPLAY 'K110-AIX1 ' K110-AIX1                                    
           END-IF.                                                              
                                                                                
       270-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-K200-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES      TO K200-KEY-FLD.                                    
           MOVE WS-CTRL-KEY TO K200-KEY-FLD.                                    
           START BP13K200 KEY NOT < K200-KEY-FLD.                               
                                                                                
           IF BP13K200-STATUS NOT = 00                                          
              DISPLAY 'BP13K200 START ERROR ' BP13K200-STATUS                   
              GO TO 400-EXIT                                                    
           ELSE                                                                 
              READ BP13K200 NEXT AT END MOVE 'Y' TO EOF-K200                    
           END-IF.                                                              
                                                                                
           PERFORM 410-CHECK-K200-REC THRU 410-EXIT                             
             UNTIL EOF-K200           = 'Y'           OR                        
                   K200-DTE-ALLOC NOT = CTRL-OLD-DATE OR                        
                   K200-NUM-NT-ZONE NOT = CTRL-CDE-NT-ZONE.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       410-CHECK-K200-REC.                                                      
      *-------------------------------------------------------------            
             ADD 1 TO K200-READ-CNT.                                            
                                                                                
             IF (K200-DTE-ALLOC NOT = CTRL-OLD-DATE OR                          
                 K200-NUM-NT-ZONE NOT = CTRL-CDE-NT-ZONE)                       
                GO TO 410-EXIT                                                  
             END-IF.                                                            
                                                                                
             DELETE BP13K200.                                                   
                                                                                
             IF BP13K200-STATUS NOT = 00                                        
                DISPLAY 'K200 DELETE ERROR, STATUS ' BP13K200-STATUS            
                DISPLAY 'K200-KEY-FLD              ' K200-KEY-FLD               
             ELSE                                                               
                MOVE CTRL-NEW-DATE TO K200-DTE-ALLOC                            
                WRITE BP13K200-REC                                              
                IF BP13K200-STATUS NOT = 00                                     
                   DISPLAY 'K200 WRITE ERROR, STATUS ' BP13K200-STATUS          
                   DISPLAY 'K200-KEY-FLD             ' K200-KEY-FLD             
                END-IF                                                          
                ADD 1 TO K200-UPDATE-CNT                                        
             END-IF.                                                            
                                                                                
             READ BP13K200 NEXT AT END MOVE 'Y' TO EOF-K200.                    
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-READ-BP13K210-REC.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES       TO K210-KEY-FLD OF BP13K210-REC.                   
           MOVE CTRL-CDE-NT-ZONE TO K210-NUM-NT-ZONE.                           
                                                                                
           START BP13K210 KEY NOT < K210-KEY-FLD OF BP13K210-REC.               
                                                                                
           IF BP13K210-STATUS NOT = ZEROS                                       
              DISPLAY 'BP13K210-START ERROR ' BP13K210-STATUS                   
              DISPLAY 'BP13K210-KEY-FLD     ' K210-KEY-FLD OF                   
                                              BP13K210-REC                      
              GO TO 500-EXIT                                                    
           END-IF.                                                              
                                                                                
           PERFORM 510-CHECK-BP13K210-REC THRU 510-EXIT                         
             UNTIL EOF-K210           = 'Y'.                                    
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       510-CHECK-BP13K210-REC.                                                  
      *-------------------------------------------------------------            
           READ BP13K210 NEXT AT END                                            
                MOVE 'Y' TO EOF-K210                                            
                GO TO 510-EXIT.                                                 
                                                                                
           ADD 1 TO K210-READ-CNT.                                              
                                                                                
           IF (K210-DTE-ALLOC OF BP13K210-REC NOT = CTRL-OLD-DATE OR            
               K210-NUM-NT-ZONE NOT = CTRL-CDE-NT-ZONE)                         
               GO TO 510-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE CTRL-NEW-DATE TO K210-DTE-ALLOC OF BP13K210-REC.                
                                                                                
           REWRITE BP13K210-REC.                                                
                                                                                
           IF BP13K210-STATUS NOT = 00                                          
              DISPLAY 'BP13K210-WRITE ERROR ' BP13K210-STATUS                   
              DISPLAY 'BP13K210-KEY-FLD     ' K210-KEY-FLD OF                   
                                              BP13K210-REC                      
           END-IF.                                                              
                                                                                
           ADD 1 TO K210-UPDATE-CNT.                                            
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-READ-K222-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES           TO K222-KEY-FLD.                               
           MOVE CTRL-CDE-NT-ZONE TO K222-NUM-NT-ZONE.                           
           START BP13K222 KEY NOT < K222-KEY-FLD.                               
                                                                                
           IF BP13K222-STATUS NOT = ZEROS                                       
              DISPLAY 'BP13K222-START ERROR ' BP13K222-STATUS                   
              DISPLAY 'BP13K222-KEY-FLD     ' K222-KEY-FLD                      
              GO TO 600-EXIT                                                    
           ELSE                                                                 
              READ BP13K222 NEXT AT END MOVE 'Y' TO EOF-K222                    
           END-IF.                                                              
                                                                                
           PERFORM 610-CHECK-K222-REC THRU 610-EXIT                             
             UNTIL EOF-K222           = 'Y'           OR                        
                   K222-NUM-NT-ZONE NOT = CTRL-CDE-NT-ZONE.                     
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       610-CHECK-K222-REC.                                                      
      *-------------------------------------------------------------            
             ADD 1 TO K222-READ-CNT.                                            
                                                                                
             IF (K222-DTE-ALLOC   = CTRL-OLD-DATE) AND                          
                (K222-NUM-NT-ZONE = CTRL-CDE-NT-ZONE)                           
                 PERFORM 620-DELETE-BP13K222 THRU 620-EXIT                      
             ELSE                                                               
                READ BP13K222 NEXT AT END MOVE 'Y' TO EOF-K222                  
             END-IF.                                                            
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       620-DELETE-BP13K222.                                                     
      *-------------------------------------------------------------            
      *                                                                         
      *   ROUTINE TO SAVE DELETED RECORD                                        
      *                                                                         
           WRITE P13F222D-REC FROM BP13K222-REC.                                
           ADD 1 TO F222-DELETE-CNT.                                            
      *                                                                         
      *   ROUTINE TO SAVE UPDATED RECORD (W/ NEW DATE ALLOC)                    
      *                                                                         
           MOVE CTRL-NEW-DATE TO K222-DTE-ALLOC.                                
           WRITE P13F222W-REC FROM BP13K222-REC.                                
           ADD 1 TO F222-WRITE-CNT.                                             
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-READ-F800-REC.                                                       
      *-------------------------------------------------------------            
           OPEN I-O  BM06F800.                                                  
           MOVE 'N' TO EOF-F800.                                                
           READ BM06F800 AT END MOVE 'Y' TO EOF-F800.                           
                                                                                
           IF EOF-F800 NOT = 'Y'                                                
              PERFORM 710-CHECK-F800  THRU 710-EXIT                             
                UNTIL EOF-F800 = 'Y'                                            
           END-IF.                                                              
                                                                                
           CLOSE BM06F800.                                                      
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       710-CHECK-F800.                                                          
      *-------------------------------------------------------------            
           ADD 1 TO F800-READ-CNT.                                              
           IF (CTRL-CDE-NT-ZONE = F800-CDE-NT OR                                
                                  F800-CDE-ZONE OR                              
                                  F800-NUM-BTO-OPTION) AND                      
              (F800-DTE-ALLOCN = CTRL-OLD-DATE)                                 
              MOVE CTRL-NEW-DATE TO F800-DTE-ALLOCN                             
              REWRITE F800-REC                                                  
              ADD 1 TO F800-UPDATE-CNT                                          
           END-IF.                                                              
                                                                                
           READ BM06F800 AT END MOVE 'Y' TO EOF-F800.                           
                                                                                
       710-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       750-READ-K230-REC.                                                       
      *-------------------------------------------------------------            
           MOVE K110-KEY-FLD  TO K230-KEY-FLD.                                  
           READ BP13K230.                                                       
                                                                                
           IF BP13K230-STATUS NOT = ZEROES                                      
              DISPLAY 'READ ERROR K230 : ' BP13K230-STATUS                      
              DISPLAY 'K230-KEY-FLD : ' K230-KEY-FLD                            
              GO TO 750-EXIT                                                    
           END-IF.                                                              
                                                                                
           ADD 1 TO K230-READ-CNT.                                              
           MOVE CTRL-NEW-DATE TO K230-DTE-ALLOC.                                
                                                                                
           WRITE F230-HEADER-REC FROM K230-DETAIL-REC.                          
           ADD 1 TO F230-WRITE-CNT.                                             
                                                                                
       750-EXIT.                                                                
           EXIT.                                                                
                                                                                
       760-READ-K813-REC.                                                       
                                                                                
           MOVE SPACES                    TO BP13K813-REC.                      
           INITIALIZE                        BP13K813-REC.                      
                                                                                
           MOVE CTRL-CDE-NT-ZONE          TO K813-NUM-ZONE.                     
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE BP13K813-STATUS                                             
               WHEN 00                                                          
                    READ BP13K813 NEXT RECORD                                   
                    IF K813-NUM-ZONE = CTRL-CDE-NT-ZONE                         
                       ADD 1 TO K813-READ-CNT                                   
                       MOVE 'Y' TO WS-K813-FOUND                                
                    ELSE                                                        
                     DISPLAY 'BP13K813 REC NOT FOUND: ' BP13K813-STATUS         
                     DISPLAY 'K813-KEY-FLD : ' K813-KEY-FLD                     
                     GO TO 760-EXIT                                             
                    END-IF                                                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K813 REC NOT FOUND: ' BP13K813-STATUS          
                    DISPLAY 'K813-KEY-FLD : ' K813-KEY-FLD                      
                    GO TO 760-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       760-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PRINT-CONTROL.                                                       
      *-------------------------------------------------------------            
           MOVE CTRL-CDE-NT-ZONE   TO L-CDE-NT.                                 
           ACCEPT WS-DATE        FROM DATE.                                     
           MOVE CORR WS-DATE       TO PRINT-DATE.                               
           IF WS-YY OF WS-DATE > 85                                             
              MOVE '19' TO WS-CC OF PRINT-DATE                                  
           ELSE                                                                 
              MOVE '20' TO WS-CC OF PRINT-DATE.                                 
                                                                                
           MOVE CORR CTRL-OLD-DATE TO WS-EDITED-DATE.                           
           MOVE WS-EDITED-DATE     TO L-OLD-DATE.                               
           MOVE CORR CTRL-NEW-DATE TO WS-EDITED-DATE.                           
           MOVE WS-EDITED-DATE     TO L-NEW-DATE.                               
           ADD  1                  TO WS-PRINT-PAGE.                            
           MOVE WS-PRINT-PAGE      TO PRINT-PAGE.                               
                                                                                
           WRITE PRINT-REC-1 FROM L247-LINE1 AFTER PAGE.                        
           WRITE PRINT-REC-1 FROM L247-LINE2.                                   
           WRITE PRINT-REC-1 FROM L247-LINE3    AFTER 5.                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
            CLOSE      BP13F205                                                 
                       BP13F230                                                 
                       P13F222D                                                 
                       P13F222W                                                 
                       BP13L247                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BP13K270                                                 
                       BP13K200                                                 
                       BP13K210                                                 
                       BP13K222                                                 
                       BP13K230                                                 
                       BM06K510                                                 
                       BP13K813.                                                
                                                                                
            IF   BM06K100-STATUS NOT = 00 OR                                    
                 BM06K110-STATUS NOT = 00 OR                                    
                 BP13K270-STATUS NOT = 00 OR                                    
                 BP13K200-STATUS NOT = 00 OR                                    
                 BP13K210-STATUS NOT = 00 OR                                    
                 BP13K222-STATUS NOT = 00 OR                                    
                 BP13K230-STATUS NOT = 00 OR                                    
                 BP13K813-STATUS NOT = 00 OR                                    
                 BM06K510-STATUS NOT = 00                                       
                 DISPLAY 'BM06K100-STATUS ' BM06K100-STATUS                     
                 DISPLAY 'BM06K110-STATUS ' BM06K110-STATUS                     
                 DISPLAY 'BP13K270-STATUS ' BP13K270-STATUS                     
                 DISPLAY 'BP13K200-STATUS ' BP13K200-STATUS                     
                 DISPLAY 'BP13K210-STATUS ' BP13K210-STATUS                     
                 DISPLAY 'BP13K222-STATUS ' BP13K222-STATUS                     
                 DISPLAY 'BP13K230-STATUS ' BP13K230-STATUS                     
                 DISPLAY 'BP13K813-STATUS ' BP13K813-STATUS                     
                 DISPLAY 'BM06K510-STATUS ' BM06K510-STATUS.                    
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
