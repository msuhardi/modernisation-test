      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21Q.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  01 JULY 2008.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO ADD RECORDS TO BP13KQ00                  *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F200 -- SORTED BOOKING APPOINTMENT FILE           *           
      *   2.  BP13K813 -- BTO SITE FILE                             *           
      *   3.  BP13K060 -- CODE TABLE                                *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KQ00 -- SOC BOOKING REGN/APPT FILE                *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133446 01/07/2008 RB12 NEW PROGRAM                        *           
      * BP133610 27/04/2009 ESA1 TO REPLACE BP13K767 W/ BP13K816    *           
      * BP134513 18/04/2012 ESA1 TO CATER FOR MGPS                  *           
      * BP134991 12/07/2013 ESD1 ADD 3P-ROOM LOGIC                  *           
      * BP135095 07/12/2013 ESD1 CATER TO READ K813 WITH NUM-ZONE   *           
      *                          ONLY.                              *           
      * BP135190 04/12/2013 SMR2 EXPAND BP13K813 TO 1000            *           
      * BP135415 07/08/2014 ESA1 TO CATER FOR 3P-ROOM FOR SBF 201405*           
      * BP135411 16/09/2014 ESA1 TO CORRECT LOGIC FOR 3P AND 3-ROOM *           
      * BP136836 22/08/2017 ESA1 TO CATER FOR RF FLAT TYPE FOR 'ROF'*           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KQ00 ASSIGN TO BP13KQ00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KQ00-KEY-FLD                               
                  FILE STATUS     IS BP13KQ00-STATUS.                           
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS BP13K813-STATUS.                           
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K060-KEY-FLD                               
                  FILE STATUS     IS BP13K060-STATUS.                           
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KQ00                                                            
            RECORD CONTAINS  150 CHARACTERS.                                    
       COPY BP13KQ00.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25  CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  300 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F200.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-KQ00-ERR-CNT        PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ00-FND-SW         PIC X     VALUE SPACES.                       
       01  WS-KQ00-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ00-DUPL-REC       PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ00-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-K813-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-K813-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K060-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-K060-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F200-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F200-EOF            PIC X     VALUE SPACES.                       
       01  WS-FLAG                PIC X     VALUE SPACES.                       
       01  BP13KQ00-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K813-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K060-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-FLAT-TYPE-2         PIC X(02) VALUE SPACES.                       
       01  WS-NEW-TOWN-3          PIC X(03) VALUE SPACES.                       
       01  WS-FLAT-TYPE-9         PIC X(09) VALUE SPACES.                       
       01  WS-NEW-TOWN-20         PIC X(20) VALUE SPACES.                       
       01  WS-K813-EOF            PIC X      VALUE 'N'.                         
       01  WS-K813-FND            PIC X      VALUE 'N'.                         
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F200    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F200-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13K813                                                 
                       BP13K060                                                 
                I-O    BP13KQ00.                                                
                                                                                
           IF BP13KQ00-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KQ00 : ' BP13KQ00-STATUS             
              MOVE BP13KQ00-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K813-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K813 : ' BP13K813-STATUS             
              MOVE BP13K813-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K060-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K060 : ' BP13K060-STATUS             
              MOVE BP13K060-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F200.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F200 AT END                                                 
                MOVE HIGH-VALUES TO BP13F200-REC                                
                MOVE 'Y' TO WS-F200-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F200-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F200-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES              
              PERFORM 3100-MOVE-KQ00 THRU 3100-EXIT                             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F200        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       3100-MOVE-KQ00.                                                          
      *---------------*                                                         
                                                                                
           MOVE SPACES                     TO   BP13KQ00-REC.                   
           INITIALIZE                           BP13KQ00-REC.                   
           IF F200-NUM-FLAT-TYPE = 'SE'                                         
              MOVE 'SER'                   TO   KQ00-NUM-ALLO-MODE              
                                                KQ00-NUM-ALLO-MODE2             
           ELSE                                                                 
              MOVE F200-CDE-PROJECT        TO   KQ00-NUM-ALLO-MODE              
                                                KQ00-NUM-ALLO-MODE2             
           END-IF.                                                              
           IF F200-CDE-PROJECT = 'BTO'                                          
              PERFORM 3450-STARTBR-BP13K813 THRU 3450-EXIT                      
              MOVE K813-CDE-NT             TO   WS-NEW-TOWN-3                   
              MOVE K813-NUM-FLAT-TYPE      TO   WS-FLAT-TYPE-2                  
           ELSE                                                                 
              IF F200-NUM-FLAT-TYPE = 'SE'                                      
                 MOVE F200-NUM-NT-ZONE     TO   WS-NEW-TOWN-3                   
                 MOVE F200-NUM-ACTUAL-FT   TO   WS-FLAT-TYPE-2                  
              ELSE                                                              
                 MOVE F200-NUM-NT-ZONE     TO   WS-NEW-TOWN-3                   
                 MOVE F200-NUM-FLAT-TYPE   TO   WS-FLAT-TYPE-2                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F200-NUM-NT-ZONE = 'WIH' OR '3R '                                 
              MOVE SPACES TO WS-NEW-TOWN-20                                     
           ELSE                                                                 
              PERFORM 4000-DECODE-NEW-TOWN THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE WS-NEW-TOWN-20             TO   KQ00-NUM-NEW-TOWN               
                                                KQ00-NUM-NEW-TOWN2.             
                                                                                
           IF F200-NUM-NT-ZONE = 'WIH'                                          
              MOVE '4-ROOM' TO WS-FLAT-TYPE-9                                   
           ELSE                                                                 
              IF F200-NUM-NT-ZONE = '3R '                                       
                 MOVE '3-ROOM' TO WS-FLAT-TYPE-9                                
              ELSE                                                              
                 PERFORM 5000-DECODE-FLAT-TYPE THRU 5000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-FLAT-TYPE-9 = '3-ROOM'                                         
              IF ((F200-NUM-SELTYPE  = 'SBF' AND                                
                   F200-DTE-BALLOT = '201305')                                  
                 OR                                                             
                  (F200-NUM-SELTYPE = 'BTO'  AND                                
                    K813-NUM-FT-PREMIUM = 'P'))                                 
                 MOVE '3P-ROOM' TO WS-FLAT-TYPE-9                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-FLAT-TYPE-9             TO   KQ00-NUM-FLAT-TYPE              
                                                KQ00-NUM-FLAT-TYPE2.            
                                                                                
           MOVE F200-NUM-REGN              TO   KQ00-NUM-REGN.                  
           MOVE F200-DTE-BALLOT            TO   KQ00-DTE-BALLOT                 
                                                KQ00-DTE-BALLOT2.               
           IF F200-NUM-MGPS-TAG = 'P' OR 'C'                                    
              MOVE F200-DTE-BKAPPMT-DATE-MGPS TO   KQ00-DTE-APPMT               
              MOVE F200-TME-BKAPPMT-MGPS      TO   KQ00-TME-APPMT               
              MOVE F200-NUM-MGPS-QUEUE-SERIAL TO   KQ00-NUM-QUEUE               
           ELSE                                                                 
              MOVE F200-DTE-BKAPPMT-DATE      TO   KQ00-DTE-APPMT               
              MOVE F200-TME-BKAPPMT           TO   KQ00-TME-APPMT               
              MOVE F200-NUM-HHTY-QUEUE-SERIAL TO   KQ00-NUM-QUEUE               
            END-IF.                                                             
                                                                                
           IF F200-CDE-PROJECT = 'BTO'                                          
              IF K813-NUM-ADDITIONAL-FLAT NOT = SPACES AND LOW-VALUES           
                 MOVE K813-NUM-ADDITIONAL-FLAT   TO   KQ00-NUM-BONUS            
              ELSE                                                              
                 MOVE 'N'                        TO   KQ00-NUM-BONUS            
              END-IF                                                            
           ELSE                                                                 
              MOVE 'N'                        TO   KQ00-NUM-BONUS               
           END-IF.                                                              
                                                                                
           PERFORM 3500-WRITE-BP13KQ00     THRU 3500-EXIT.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3450-STARTBR-BP13K813.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
           MOVE 'N'                            TO WS-K813-EOF                   
                                                  WS-K813-FND.                  
                                                                                
           MOVE F200-NUM-NT-ZONE   TO K813-NUM-ZONE.                            
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K813-STATUS                                             
           WHEN 00                                                              
                PERFORM 3451-READNEXT-BP13K813 THRU 3451-EXIT                   
                   UNTIL WS-K813-EOF = 'Y'                                      
                IF WS-K813-FND = 'Y'                                            
                   DISPLAY 'RECORD FOUND KEY IS ' K813-KEY-FLD                  
                   ADD   1  TO WS-K813-READ-CNT                                 
                ELSE                                                            
                   DISPLAY 'RECORD NOT FOUND IN BP13K813:' K813-KEY-FLD         
                   ADD   1     TO WS-K813-READ-ERR                              
                   MOVE SPACES TO K813-CDE-NT                                   
                                  K813-NUM-FLAT-TYPE                            
                   MOVE 'N'    TO K813-NUM-ADDITIONAL-FLAT                      
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
           WHEN 46                                                              
                MOVE 'Y'                   TO   WS-K813-EOF                     
           WHEN OTHER                                                           
                DISPLAY 'BP13K813 - STARTBR ERROR ' BP13K813-STATUS             
                MOVE BP13K813-STATUS       TO   RETURN-CODE                     
                ADD   1   TO WS-K813-READ-ERR                                   
                GO        TO 9000-CLOSE-FILES                                   
           END-EVALUATE.                                                        
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3451-READNEXT-BP13K813.                                                  
      *-------------------------------------------------------------            
           READ BP13K813 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13K813-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
                IF K813-NUM-ZONE = F200-NUM-NT-ZONE                             
                   IF K813-DTE-BALLOT = F200-DTE-BALLOT                         
                      MOVE 'Y'                 TO   WS-K813-EOF                 
                                                    WS-K813-FND                 
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'                    TO   WS-K813-EOF                 
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
           WHEN 46                                                              
                MOVE 'Y'                       TO   WS-K813-EOF                 
           WHEN OTHER                                                           
                DISPLAY 'BP13K813 - READ NEXT ERROR ' BP13K813-STATUS           
                MOVE BP13K813-STATUS           TO   RETURN-CODE                 
                ADD   1   TO WS-K813-READ-ERR                                   
                GO        TO 9000-CLOSE-FILES                                   
           END-EVALUATE.                                                        
                                                                                
       3451-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3500-WRITE-BP13KQ00.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13KQ00-REC.                                                  
                                                                                
           IF BP13KQ00-STATUS = 0                                               
              ADD   1  TO WS-KQ00-WRITE-CNT                                     
           ELSE                                                                 
           IF BP13KQ00-STATUS = 2 OR 22                                         
              ADD   1  TO WS-KQ00-DUPL-REC                                      
              DISPLAY 'DUPLICATE RECORD IN BP13KQ00:'                           
              DISPLAY ' KEY     : ' KQ00-KEY-FLD                                
           ELSE                                                                 
           IF BP13KQ00-STATUS NOT = 0                                           
              MOVE BP13KQ00-STATUS TO RETURN-CODE                               
              DISPLAY 'WRITE ERROR, STATUS : ' BP13KQ00-STATUS                  
              DISPLAY ' KEY     : ' KQ00-KEY-FLD                                
              ADD   1   TO WS-KQ00-WRITE-ERR                                    
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       4000-DECODE-NEW-TOWN.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES             TO K060-KEY-FLD.                             
           MOVE 02                 TO K060-SERIAL-NO.                           
           MOVE WS-NEW-TOWN-3      TO K060-CODE.                                
                                                                                
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
                                                                                
           IF BP13K060-STATUS = 0                                               
              MOVE K060-DESC TO WS-NEW-TOWN-20                                  
              ADD   1  TO WS-K060-READ-CNT                                      
           ELSE                                                                 
           IF BP13K060-STATUS = 23                                              
              MOVE SPACES    TO WS-NEW-TOWN-20                                  
              DISPLAY 'ESTATE NOT FOUND IN BP13K060:' K060-KEY-FLD              
              ADD   1     TO WS-K060-READ-ERR                                   
           ELSE                                                                 
              MOVE BP13K060-STATUS TO RETURN-CODE                               
              DISPLAY 'READ ERROR IN BP13K060 ' K060-KEY-FLD                    
              ADD   1   TO WS-K060-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       5000-DECODE-FLAT-TYPE.                                                   
      *---------------------*                                                   
                                                                                
           EVALUATE WS-FLAT-TYPE-2(1:1)                                         
              WHEN '1'                                                          
                 IF WS-FLAT-TYPE-2(2:1) = 'A'                                   
                    MOVE 'SA       ' TO WS-FLAT-TYPE-9                          
                 ELSE                                                           
                    MOVE '1-ROOM   ' TO WS-FLAT-TYPE-9                          
                 END-IF                                                         
              WHEN '2'                                                          
                 MOVE '2-ROOM   '    TO WS-FLAT-TYPE-9                          
              WHEN '3'                                                          
                 IF WS-FLAT-TYPE-2(2:1) = 'P'                                   
                    MOVE '3P-ROOM  '    TO WS-FLAT-TYPE-9                       
                 ELSE                                                           
                    MOVE '3-ROOM   '    TO WS-FLAT-TYPE-9                       
                 END-IF                                                         
              WHEN '4'                                                          
                 MOVE '4-ROOM   '    TO WS-FLAT-TYPE-9                          
              WHEN '5'                                                          
                 MOVE '5-ROOM   '    TO WS-FLAT-TYPE-9                          
              WHEN 'E'                                                          
                 MOVE 'EXECUTIVE'    TO WS-FLAT-TYPE-9                          
              WHEN 'R'                                                          
                 MOVE 'RF       '    TO WS-FLAT-TYPE-9                          
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21Q  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F200    : ' WS-F200-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13KQ00   : ' WS-KQ00-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13KQ00   : ' WS-KQ00-WRITE-ERR.          
           DISPLAY 'RECS FOUND    IN BP13KQ00   : ' WS-KQ00-DUPL-REC.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K813   : ' WS-K813-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K813   : ' WS-K813-READ-ERR.           
                                                                                
           CLOSE BP13KQ00                                                       
                 BP13K813                                                       
                 BP13K060                                                       
                 BP13F200.                                                      
                                                                                
           IF BP13KQ00-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KQ00-STATUS ' BP13KQ00-STATUS          
            MOVE BP13KQ00-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K813-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K813-STATUS ' BP13K813-STATUS          
            MOVE BP13K813-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K060-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K060-STATUS ' BP13K060-STATUS          
            MOVE BP13K060-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
