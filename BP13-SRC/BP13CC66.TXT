       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC66.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN : 15 FEBRUARY 2007.                                         
      *===============================================================*         
      *  SYSTEM OF COMMITMENT (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE   : TO READ BP13F136 AND WRITE ALL RECORDS WITH    *         
      *                EQUAL AMT IN P13F136A AND NOT EQUAL AMT IN     *         
      *                P13F136B.                                      *         
      *                                                               *         
      *---------------------------------------------------------------*         
      *  CHG-NO   BY    DATE   DESCRIPTION                            *         
      * -------- ---- -------- ---------------------------------------*         
      * BP132973 SD10 20070215 NEW PROGRAM                            *         
      * BP132888 BA3  20070226 USE DUE DATE FROM WS-F136-REC TABLE TO *         
      *                        COMPUTE FOR THE MONTH AGE              *         
      * BP132888 SD10 20070303 CHANGE OCCURRENCE OF WS-F136-REC TO 20 *         
      * BP133052 BA3  20070606 REMOVE CREATING 'C' RECORD (MORE THAN  *         
      *                          ONE MONTH) TO BE PASSED TO BP13CC34. *         
      * BP133360 ED1  20080812 CATER FOR JVP (69). ADD AMT TO RECPT   *         
      *                        WHEN CDE-CR = 'C', ELSE ADD TO COLL    *         
      * BP133545 JB8  20090223 MOVE 0 TO WS-AGE IF DUE-DTE IS BLANK,  *         
      *                        CATER FOR JVP (69). ADD AMT*-1 TO RECPT*         
      *                        WHEN CDE-CR = 'D'                      *         
      * BP133572 JB8  20090406 TO SEPARATE BY PP                      *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
           SELECT BP13F136 ASSIGN TO BP13F136.                                  
           SELECT P13F136A ASSIGN TO P13F136A.                                  
           SELECT P13F136B ASSIGN TO P13F136B.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13F136                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F136.                                                           
                                                                                
       FD  P13F136A                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       01  P13F136A-REC            PIC X(150).                                  
                                                                                
       FD  P13F136B                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       01  P13F136B-REC            PIC X(150).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-AGE-MTH              PIC 999      VALUE ZEROS.                
           05  WS-F136-REC OCCURS 20   PIC X(150)   VALUE SPACES.               
           05  WS-F136-NUM-REGN        PIC X(8)     VALUE SPACES.               
           05  WS-F136-NUM-PP          PIC X(3)     VALUE SPACES.               
           05  WS-F136-READ            PIC 9(5)     VALUE ZERO.                 
           05  WS-AMT-RECEIPT          PIC S9(7)V99 VALUE ZERO.                 
           05  WS-AMT-COLL             PIC S9(7)V99 VALUE ZERO.                 
           05  WS-CTR                  PIC 9(2)     VALUE ZERO.                 
           05  WS-MATCH                PIC 9(5)     VALUE ZERO.                 
           05  WS-NOT-MATCH            PIC 9(5)     VALUE ZERO.                 
           05  WS-F136-REF             PIC X(6)     VALUE SPACES.               
           05  WS-F136-NOTMATCH        PIC 9(5)     VALUE ZERO.                 
           05  WS-F136-SUB             PIC 9(2)     VALUE ZERO.                 
           05  WS-F136-STORED          PIC 9(2)     VALUE ZERO.                 
           05  WS-RESALE-FND           PIC X(1)     VALUE 'N'.                  
           05  WS-TRF-FND              PIC X(1)     VALUE 'N'.                  
           05  WS-EOF-F136             PIC X(3).                                
               88  EOF-OF-F136                      VALUE 'YES'.                
           05  WS-PREVIOUS-DATE.                                                
               10  WS-PREVIOUS-CCYYMM  PIC X(6).                                
               10  WS-PREVIOUS-CCYYMM-B REDEFINES WS-PREVIOUS-CCYYMM.           
                   15  WS-PREV-CCYYMM  PIC 9(6).                                
                   15  WS-PREV-CCYYMM-C REDEFINES WS-PREV-CCYYMM.               
                       20 WS-PREV-CCYY PIC 9(4).                                
                       20 WS-PREV-MM   PIC 9(2).                                
               10  WS-PREVIOUS-DD      PIC 99.                                  
                                                                                
           05  WS-DUE-DATE.                                                     
               10  WS-DUE-CCYYMM-A     PIC X(8)     VALUE SPACES.               
               10  WS-DUE-CCYYMM-B REDEFINES WS-DUE-CCYYMM-A.                   
                   15  WS-DUE-CCYYMM   PIC 9(6).                                
                   15  WS-DUE-CCYYMM-B REDEFINES WS-DUE-CCYYMM.                 
                       20  WS-DUE-CCYY     PIC 9(4).                            
                       20  WS-DUE-MM       PIC 9(2).                            
                   15  WS-DUE-DD       PIC 9(2).                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 150-READ-SY02F001  THRU 150-EXIT.                            
           PERFORM 200-INITIALIZE-REC THRU 200-EXIT.                            
           PERFORM 300-READ-F136      THRU 300-EXIT.                            
           MOVE F136-NUM-REGN         TO WS-F136-NUM-REGN.                      
           MOVE F136-NUM-PP-SCHEDULE  TO WS-F136-NUM-PP.                        
           PERFORM 400-PROCESS        THRU 400-EXIT                             
                   UNTIL EOF-OF-F136.                                           
           PERFORM 900-FINALIZATION   THRU 900-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BP13F136                                                 
                       SY02F001                                                 
                OUTPUT P13F136A                                                 
                       P13F136B.                                                
           MOVE 'NO' TO WS-EOF-F136.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-READ-SY02F001.                                                       
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-LAST-DAY-PREV TO WS-PREVIOUS-DATE.                     
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-INITIALIZE-REC.                                                      
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 19                 
              MOVE SPACES TO WS-F136-REC (WS-CTR)                               
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES    TO WS-F136-NUM-REGN.                                  
           MOVE SPACES    TO WS-F136-NUM-PP.                                    
           MOVE ZEROES    TO WS-AMT-RECEIPT                                     
                             WS-AMT-COLL.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-F136.                                                           
           READ BP13F136 AT END MOVE 'YES' TO WS-EOF-F136                       
                                MOVE HIGH-VALUE TO F136-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F136-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PROCESS.                                                             
           MOVE 1      TO WS-F136-SUB.                                          
           MOVE 0      TO WS-F136-STORED.                                       
                                                                                
           IF F136-NUM-REGN = WS-F136-NUM-REGN AND                              
              F136-NUM-PP-SCHEDULE = WS-F136-NUM-PP                             
              PERFORM 450-STORE-F136   THRU 450-EXIT                            
                UNTIL EOF-OF-F136 OR                                            
                F136-NUM-REGN NOT = WS-F136-NUM-REGN OR                         
                F136-NUM-PP-SCHEDULE NOT = WS-F136-NUM-PP                       
                                                                                
              IF WS-AMT-COLL = WS-AMT-RECEIPT                                   
                 PERFORM 500-OUTPUT-EQUAL-AMT THRU 500-EXIT                     
                 VARYING WS-F136-SUB FROM 1 BY 1 UNTIL                          
                         WS-F136-SUB > WS-F136-STORED                           
              ELSE                                                              
                 PERFORM 600-OUTPUT-NOT-EQUAL-AMT THRU 600-EXIT                 
                 VARYING WS-F136-SUB FROM 1 BY 1 UNTIL                          
                         WS-F136-SUB > WS-F136-STORED                           
              END-IF                                                            
              PERFORM 200-INITIALIZE-REC THRU 200-EXIT                          
           ELSE                                                                 
              MOVE F136-NUM-REGN         TO WS-F136-NUM-REGN                    
              MOVE F136-NUM-PP-SCHEDULE  TO WS-F136-NUM-PP                      
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-STORE-F136.                                                          
           MOVE BP13F136-CASHTRAN TO WS-F136-REC (WS-F136-SUB).                 
           IF F136-CDE-TRANS-TYPE = '11' OR '12' OR                             
             (F136-CDE-TRANS-TYPE = '69' AND F136-CDE-CRDR = 'C')               
              COMPUTE WS-AMT-RECEIPT = WS-AMT-RECEIPT +                         
                                       F136-AMT-RECEIPT                         
           ELSE                                                                 
              IF F136-CDE-TRANS-TYPE = '69' AND F136-CDE-CRDR = 'D'             
                 COMPUTE WS-AMT-COLL = WS-AMT-COLL +                            
                                       F136-AMT-RECEIPT * -1                    
              ELSE                                                              
                 COMPUTE WS-AMT-COLL = WS-AMT-COLL + F136-AMT-RECEIPT           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-F136-SUB.                                                
           ADD 1 TO WS-F136-STORED.                                             
           PERFORM 300-READ-F136 THRU 300-EXIT.                                 
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-OUTPUT-EQUAL-AMT.                                                    
           ADD 1 TO WS-MATCH.                                                   
           MOVE 'A'             TO WS-F136-REC (WS-F136-SUB)(149:1).            
           MOVE WS-F136-REC (WS-F136-SUB) TO P13F136A-REC.                      
           WRITE P13F136A-REC.                                                  
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-OUTPUT-NOT-EQUAL-AMT.                                                
           ADD 1 TO WS-NOT-MATCH.                                               
           MOVE 0 TO WS-AGE-MTH.                                                
           PERFORM 700-COMPUTE-MO-AGE THRU 700-EXIT.                            
           MOVE WS-AGE-MTH      TO WS-F136-REC (WS-F136-SUB)(132:3)             
           MOVE 'B'             TO WS-F136-REC (WS-F136-SUB)(149:1).            
           MOVE WS-F136-REC (WS-F136-SUB) TO P13F136B-REC.                      
           WRITE P13F136B-REC.                                                  
      *    IF WS-AGE-MTH > 01                                                   
      *       MOVE 'C'          TO WS-F136-REC (WS-F136-SUB)(149:1)             
      *       MOVE WS-F136-REC (WS-F136-SUB) TO P13F136C-REC                    
      *       WRITE P13F136C-REC                                                
      *    END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-COMPUTE-MO-AGE.                                                      
                                                                                
           MOVE ZEROES                     TO WS-AGE-MTH.                       
           MOVE WS-F136-REC(WS-F136-SUB)(122:8)                                 
                                           TO WS-DUE-DATE.                      
           IF WS-DUE-DATE NOT NUMERIC                                           
              MOVE ZEROES               TO WS-AGE-MTH                           
              GO TO 700-EXIT                                                    
           END-IF.                                                              
                                                                                
           COMPUTE WS-AGE-MTH = (WS-PREV-CCYY * 12 + WS-PREV-MM) -              
                                (WS-DUE-CCYY  * 12 + WS-DUE-MM).                
                                                                                
           IF WS-DUE-CCYYMM > F001-DTE-LAST-DAY-PREV(1:6)                       
              MOVE ZEROES               TO WS-AGE-MTH                           
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*-------- BP13CC66 -----------*'.                         
             DISPLAY ' BP13F136-READ      ' WS-F136-READ.                       
             DISPLAY ' EQUAL              ' WS-MATCH.                           
             DISPLAY ' NOT EQUAL          ' WS-NOT-MATCH.                       
                                                                                
           CLOSE      BP13F136                                                  
                      SY02F001                                                  
                      P13F136A                                                  
                      P13F136B.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
