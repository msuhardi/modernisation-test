       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC10.                                                 
      *AUTHOR.        ABH.                                                      
      *DATE-WRITTEN.  10/04/06.                                                 
      *REMARKS.                                                                 
      *========================================================*                
      *                                                        *                
      *   PROGRAM TO OUTPUT BP13F130 FOR CLEARED CASES AND     *                
      *              OUTSTANDING CASES FOR PROGRESS PAYMENT    *                
      *              LUMPSUM.                                  *                
      *                                                        *                
      *     INPUT FILE :                                       *                
      *           1. BP13KD30                                  *                
      *           2. BP13K130                                  *                
      *           3. SY02F001                                  *                
      *                                                        *                
      *     OUTPUT :                                           *                
      *           1. BP13F130                                  *                
      *                                                        *                
      *                                                        *                
      *  CHG-NO   BY    NO          DESCRIPTION                *                
      *  -------  ---   --------    -------------------------  *                
      *  BP132914 BA3   10/04/06    NEW PROGRAM.               *                
      *  BP132914 LSB   07/11/06    CHK DUE-DTE, RUN-MTH       *                
      *  BP132888 BA3   12/12/06    INCLUDE ALL RECORDS FROM   *                
      *                             REPORT C TO REPORT B       *                
      *                 12/02/07    INCLUDE ONLY TRANACTIONS   *                
      *                             WHERE TRANS-DATE = PREV    *                
      *                             MONTH. PRINT CLEARED CASES *                
      *                             ONLY WHEN DATE CHEQUE =    *                
      *                             PREV MONTH.                *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13KD30 ASSIGN       TO BP13KD30                             
                           ACCESS MODE  IS SEQUENTIAL                           
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD30-KEY-FLD                         
                           FILE STATUS  IS KD30-STATUS.                         
                                                                                
           SELECT BP13K130 ASSIGN       TO BP13K130                             
                           ACCESS MODE  IS SEQUENTIAL                           
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K130-KEY-FLD                         
                           FILE STATUS  IS K130-STATUS.                         
                                                                                
           SELECT BP13F130 ASSIGN       TO BP13F130.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13KD30                                                             
                                   RECORD CONTAINS 2000 CHARACTERS.             
       COPY BP13KD30.                                                           
                                                                                
       FD  BP13K130                                                             
                                   RECORD CONTAINS 150 CHARACTERS.              
       COPY BP13K130.                                                           
                                                                                
       FD  BP13F130                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F130.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-AGE-MTH              PIC 999  VALUE ZEROS.                    
           05  WS-CNT-KD30-READ        PIC 9(5) VALUE ZEROS.                    
           05  WS-CNT-F130-WRITE       PIC 9(5) VALUE ZEROS.                    
           05  WS-REPORT               PIC X    VALUE SPACE.                    
           05  WS-LUMPSUM-REQ          PIC S9(6)V99   VALUE ZEROES.             
                                                                                
       01  WS-EOF.                                                              
           05  WS-EOF-KD30             PIC X   VALUE 'N'.                       
           05  WS-EOF-K130             PIC X   VALUE 'N'.                       
                                                                                
       01  FILE-STATUS.                                                         
           05  KD30-STATUS             PIC 99  VALUE ZEROS.                     
           05  K130-STATUS             PIC 99  VALUE ZEROS.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-COUNT                PIC 9(5)     VALUE ZEROS.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           03  WS-CURRENT-CCYYMM       PIC X(6).                                
           03  WS-CURRENT-CCYYMM-B REDEFINES WS-CURRENT-CCYYMM.                 
               05  WS-CURR-CCYYMM      PIC 9(6).                                
               05  WS-CURR-CCYYMM-C REDEFINES WS-CURR-CCYYMM.                   
                   10 WS-CURR-CCYY     PIC 9(4).                                
                   10 WS-CURR-MM       PIC 9(2).                                
           03  WS-CURRENT-DD           PIC 99.                                  
                                                                                
       01  WS-PREVIOUS-DATE.                                                    
           03  WS-PREVIOUS-CCYYMM      PIC X(6).                                
           03  WS-PREVIOUS-CCYYMM-B REDEFINES WS-PREVIOUS-CCYYMM.               
               05  WS-PREV-CCYYMM      PIC 9(6).                                
               05  WS-PREV-CCYYMM-C REDEFINES WS-PREV-CCYYMM.                   
                   10 WS-PREV-CCYY     PIC 9(4).                                
                   10 WS-PREV-MM       PIC 9(2).                                
           03  WS-PREVIOUS-DD          PIC 99.                                  
                                                                                
       01  WS-DUE-DATE.                                                         
           03  WS-DUE-CCYYMM-A         PIC X(6).                                
           03  WS-DUE-CCYYMM-B REDEFINES WS-DUE-CCYYMM-A.                       
               05  WS-DUE-CCYY         PIC 9(4).                                
               05  WS-DUE-MM           PIC 9(2).                                
           03  WS-DUE-DD               PIC 99.                                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0010-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13KD30                                                 
                       BP13K130                                                 
                OUTPUT BP13F130.                                                
                                                                                
           IF KD30-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR KD30  ' KD30-STATUS                        
              PERFORM 0900-CLOSE-ROUTINE THRU 0900-EXIT                         
           END-IF.                                                              
                                                                                
           IF K130-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K130  ' K130-STATUS                        
              PERFORM 0900-CLOSE-ROUTINE THRU 0900-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 0050-READ-SY02F001    THRU 0050-EXIT.                        
           PERFORM 0100-INVALID-KEY-SECT THRU 0100-EXIT.                        
           PERFORM 0150-READ-KD30        THRU 0150-EXIT.                        
           PERFORM 0200-PROCESS-KD30     THRU 0200-EXIT                         
                                         UNTIL WS-EOF-KD30 = 'Y'.               
                                                                                
           PERFORM 0900-CLOSE-ROUTINE THRU 0900-EXIT.                           
                                                                                
       0010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0050-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT       TO WS-CURRENT-DATE.                      
           MOVE F001-DTE-LAST-DAY-PREV TO WS-PREVIOUS-DATE.                     
                                                                                
       0050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0100-INVALID-KEY-SECT.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE LOW-VALUES                TO KD30-NUM-REGN.                     
           START BP13KD30 KEY > KD30-NUM-REGN.                                  
           IF KD30-STATUS NOT = 00                                              
              DISPLAY 'START ERROR KD30 = ' KD30-STATUS                         
              PERFORM 0900-CLOSE-ROUTINE THRU 0900-EXIT                         
           END-IF.                                                              
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0150-READ-KD30.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD30 NEXT                                                   
                         AT END MOVE 'Y' TO WS-EOF-KD30                         
                         GO TO 0150-EXIT.                                       
                                                                                
           ADD 1         TO WS-CNT-KD30-READ.                                   
                                                                                
       0150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0200-PROCESS-KD30.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM VARYING WS-COUNT FROM 1 BY 1 UNTIL WS-COUNT > 15             
             IF KD30-AMT-PP-REQ(WS-COUNT)  IS NOT NUMERIC                       
                MOVE ZEROS               TO KD30-AMT-PP-REQ(WS-COUNT)           
             END-IF                                                             
             IF KD30-AMT-PP-PAID(WS-COUNT) IS NOT NUMERIC                       
                MOVE ZEROS               TO KD30-AMT-PP-PAID(WS-COUNT)          
             END-IF                                                             
             IF KD30-AMT-PP-LOAN(WS-COUNT) IS NOT NUMERIC                       
                MOVE ZEROS               TO KD30-AMT-PP-LOAN(WS-COUNT)          
             END-IF                                                             
             PERFORM 0210-CHECK-DUE-FILE THRU 0210-EXIT                         
           END-PERFORM.                                                         
                                                                                
           PERFORM 0150-READ-KD30        THRU 0150-EXIT.                        
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0210-CHECK-DUE-FILE.                                                     
      *-------------------------------------------------------------            
           IF KD30-DTE-PP-DUE(WS-COUNT) = ZEROS OR SPACES OR                    
                                                  LOW-VALUES                    
              GO TO 0210-EXIT.                                                  
                                                                                
           MOVE ZEROES                       TO WS-LUMPSUM-REQ.                 
           MOVE SPACES                       TO WS-REPORT.                      
                                                                                
           IF KD30-AMT-PP-LOAN(WS-COUNT) > ZEROS                                
              COMPUTE WS-LUMPSUM-REQ =                                          
              KD30-AMT-PP-REQ(WS-COUNT) - KD30-AMT-PP-LOAN(WS-COUNT)            
           ELSE                                                                 
              COMPUTE WS-LUMPSUM-REQ = KD30-AMT-PP-REQ(WS-COUNT)                
           END-IF.                                                              
      *                                                                         
      * CHECK CHEQUE CLEAR IN PREV MONTH                                        
      *                                                                         
           IF WS-LUMPSUM-REQ = KD30-AMT-PP-PAID(WS-COUNT) AND                   
              KD30-DTE-CHEQUE-SENT(WS-COUNT)(1:6) = WS-PREVIOUS-CCYYMM          
                                                                                
              MOVE 'A'                       TO WS-REPORT                       
              PERFORM 0250-PROCESS-CLEARED THRU 0250-EXIT                       
                                                                                
           ELSE                                                                 
              IF WS-LUMPSUM-REQ NOT = KD30-AMT-PP-PAID(WS-COUNT)                
                                                                                
      *---       DETERMINE 'B' OR 'C' ON NEXT PARA BASED ON MO AGE              
                 PERFORM 0250-PROCESS-CLEARED THRU 0250-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0250-PROCESS-CLEARED.                                                    
      *-------------------------------------------------------------            
                                                                                
           PERFORM 0300-START-K130          THRU 0300-EXIT.                     
           PERFORM 0350-READ-NEXT-K130-C10A THRU 0350-EXIT UNTIL                
                   K130-NUM-ORIG-REGN NOT = KD30-NUM-REGN                       
                   OR WS-EOF-K130 = 'Y'.                                        
                                                                                
       0250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0300-START-K130.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                        TO WS-EOF-K130.                      
           MOVE SPACES                     TO K130-KEY-FLD.                     
                                                                                
           MOVE KD30-NUM-REGN              TO K130-NUM-ORIG-REGN.               
           START BP13K130 KEY >= K130-KEY-FLD.                                  
           IF K130-STATUS NOT = 00                                              
              DISPLAY 'START ERROR K130 = ' K130-STATUS                         
              PERFORM 0900-CLOSE-ROUTINE THRU 0900-EXIT                         
           END-IF.                                                              
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0350-READ-NEXT-K130-C10A.                                                
      *-------------------------------------------------------------            
           READ BP13K130 NEXT                                                   
                         AT END MOVE 'Y' TO WS-EOF-K130                         
                         GO TO 0350-EXIT.                                       
                                                                                
           IF KD30-NUM-REGN NOT = K130-NUM-ORIG-REGN                            
              MOVE 'Y'  TO  WS-EOF-K130                                         
              GO TO 0350-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF KD30-DTE-PP-DUE(WS-COUNT) = K130-DTE-PP-DUE                       
                                                                                
              IF K130-AMT-CASH IS NOT NUMERIC                                   
                 MOVE ZEROS TO K130-AMT-CASH                                    
              END-IF                                                            
                                                                                
              IF K130-PAYMENT-TYPE = 'PP '                                      
                 PERFORM 0360-PROCESS-REPORT THRU 0360-EXIT                     
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       0350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0360-PROCESS-REPORT.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF K130-DTE-TRANS(1:6) NOT = F001-DTE-LAST-DAY-PREV(1:6)             
              GO TO 0360-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 0600-COMPUTE-MO-AGE         THRU 0600-EXIT.                  
                                                                                
           IF WS-REPORT = 'A'                                                   
               PERFORM 0700-OUTPUT-BP13F130    THRU 0700-EXIT                   
           ELSE                                                                 
               MOVE 'B' TO WS-REPORT                                            
               PERFORM 0700-OUTPUT-BP13F130 THRU 0700-EXIT                      
               IF WS-AGE-MTH > 01                                               
                  MOVE 'C' TO WS-REPORT                                         
                  PERFORM 0700-OUTPUT-BP13F130 THRU 0700-EXIT                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
       0360-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       0600-COMPUTE-MO-AGE.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROES                     TO WS-AGE-MTH.                       
           MOVE K130-DTE-PP-DUE            TO WS-DUE-DATE.                      
           IF WS-DUE-CCYY IS NOT NUMERIC                                        
              MOVE ZEROES                  TO WS-DUE-CCYY                       
           END-IF.                                                              
           IF WS-DUE-MM   IS NOT NUMERIC                                        
              MOVE ZEROES                  TO WS-DUE-MM                         
           END-IF.                                                              
                                                                                
           COMPUTE WS-AGE-MTH = (WS-PREV-CCYY * 12 + WS-PREV-MM) -              
                                (WS-DUE-CCYY  * 12 + WS-DUE-MM).                
                                                                                
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0700-OUTPUT-BP13F130.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES          TO BP13F130-REC.                                
           INITIALIZE              BP13F130-REC.                                
                                                                                
           MOVE BP13K130-CDHIST TO BP13F130-REC                                 
           MOVE WS-AGE-MTH      TO BP13F130-REC (132:3)                         
           MOVE WS-REPORT       TO BP13F130-REC (149:1)                         
                                                                                
           WRITE BP13F130-REC.                                                  
           ADD   1              TO WS-CNT-F130-WRITE.                           
                                                                                
       0700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0900-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' ********** BP13CC10 ********** '.                          
           DISPLAY ' *** CC10 LISTING :'.                                       
           DISPLAY ' ***  BP13KD30 READ   - ' WS-CNT-KD30-READ.                 
           DISPLAY ' ***  BP13F130 WRITE  - ' WS-CNT-F130-WRITE.                
           DISPLAY ' ****************************** '.                          
           CLOSE SY02F001                                                       
                 BP13KD30                                                       
                 BP13K130                                                       
                 BP13F130.                                                      
                                                                                
           IF KD30-STATUS NOT = 00 AND 97                                       
              DISPLAY 'KD30 CLOSING ERROR  ' KD30-STATUS.                       
                                                                                
           IF K130-STATUS NOT = 00 AND 97                                       
              DISPLAY 'K130 CLOSING ERROR  ' K130-STATUS.                       
                                                                                
           STOP RUN.                                                            
                                                                                
       0900-EXIT.                                                               
           EXIT.                                                                
                                                                                
