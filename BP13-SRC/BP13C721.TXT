       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C721.                                                 
      *AUTHOR.        NINO ANGELO GARILLO.                                      
      *DATE-WRITTEN.  23/09/2005.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :  MATCH REQUEST CASES IN BP13F730 WITH HIST  *            
      *                 RECORDS IN BP13K595 BY NRIC1 & NRIC2, IF   *            
      *                 FOUND DIFFERENCE IN HOUSEHOLD TYPE OR 5-YR *            
      *                 TAG, PRINT REPORTS TO SOU.                 *            
      *                                                            *            
      *  INPUT FILES :  BP13F730                                   *            
      *                 BP13K595                                   *            
      *                                                            *            
      *  OUT FILE    :  BP13L721                                   *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP13DDDD 20050923 GAN1  NEW PGM                            *            
      * BP135279 20140403 SMR2  BALLOT DATE SHOULD BE WITHIN 1 YR  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F730   ASSIGN TO BP13F730.                                
           SELECT BP13L721   ASSIGN TO BP13L721.                                
           SELECT BP13K595   ASSIGN TO BP13K595                                 
                  ORGANIZATION      IS INDEXED                                  
                  ACCESS MODE       IS DYNAMIC                                  
                  RECORD KEY        IS K595-KEY-FLD                             
                  ALTERNATE RECORD KEY IS K595-ALT1-KEY                         
                                    WITH DUPLICATES                             
                  ALTERNATE RECORD KEY IS K595-ALT2-KEY                         
                                    WITH DUPLICATES                             
                  FILE STATUS       IS WS-K595-STATUS.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE IS F                                                 
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD BP13L721                                                              
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01 BP13L721-REC    PIC X(132).                                           
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WORK-AREAS.                                                          
           05  WS-READ-F730           PIC 9(8)    VALUE 0.                      
           05  WS-NOTFD               PIC 9(8)    VALUE 0.                      
                                                                                
       01  WS-K595-STATUS             PIC 9(2)    VALUE ZEROES.                 
       01  WS-K595-EOF                PIC X(1)    VALUE 'N'.                    
       01  WS-PRINT                   PIC X(1)    VALUE 'N'.                    
       01  WS-FIRST-READ              PIC X(1)    VALUE 'Y'.                    
       01  WS-TEMP-NRIC               PIC X(9)    VALUE SPACES.                 
       01  WS-CNT-LINE-ON-PAGE        PIC 9(8)    VALUE 66.                     
       01  WS-NO-OF-PAGE              PIC 9(5)    VALUE 0.                      
       01  WS-COUNT                   PIC 9(6)    VALUE 0.                      
       01  WS-TEMP-FIELDS.                                                      
           05  TEMP-NUM-F-REGN        PIC X(08)   VALUE SPACES.                 
           05  TEMP-NUM-F-NRIC1       PIC X(09)   VALUE SPACES.                 
           05  TEMP-NUM-F-NRIC2       PIC X(09)   VALUE SPACES.                 
           05  TEMP-NUM-F-NT          PIC X(03)   VALUE SPACES.                 
           05  TEMP-NUM-F-FT          PIC X(02)   VALUE SPACES.                 
           05  TEMP-NUM-F-HH          PIC X(01)   VALUE SPACES.                 
           05  TEMP-NUM-F-5YR         PIC X(01)   VALUE SPACES.                 
           05  TEMP-NUM-K-REGN        PIC X(08)   VALUE SPACES.                 
           05  TEMP-NUM-K-NRIC1       PIC X(09)   VALUE SPACES.                 
           05  TEMP-NUM-K-NRIC2       PIC X(09)   VALUE SPACES.                 
           05  TEMP-NUM-K-HH          PIC X(01)   VALUE SPACES.                 
           05  TEMP-NUM-K-5YR         PIC X(01)   VALUE SPACES.                 
           05  TEMP-NUM-K-BALLT       PIC X(06)   VALUE SPACES.                 
       01  WS-EDITED-DATE             PIC X(10)   VALUE SPACES.                 
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4)    VALUE 0.                      
           05  WS-SYS-MM              PIC 9(2)    VALUE 0.                      
           05  WS-SYS-DD              PIC 9(2)    VALUE 0.                      
       01  WS-NUM-DTE-BALLOT.                                                   
           05  WS-NUM-CCYY            PIC 9(04)   VALUE ZEROES.                 
           05  WS-NUM-MM              PIC 9(02)   VALUE ZEROES.                 
                                                                                
      *-----------------------------------------------------------*             
      *                 BP13L721 - LAYOUT                         *             
      *-----------------------------------------------------------*             
                                                                                
       01  L721-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L721'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(22) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M  O F  C O M M I T M E N T'.                         
           05  FILLER                   PIC X(16) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L721-PRINT-DATE          PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L721-PRINT-PAGE          PIC ZZZZ9.                              
                                                                                
       01  L721-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(37) VALUE SPACES.                 
           05  FILLER                   PIC X(80) VALUE                         
                'HOUSEHOLD TYPE/5-YR TAG FOUND DIFF FROM HISTORY REC'.          
                                                                                
       01  L721-PR-HEAD-03.                                                     
           05  FILLER                   PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(53) VALUE 'F730'.                 
           05  FILLER                   PIC X(50) VALUE 'K595'.                 
                                                                                
       01  L721-PR-HEAD-04.                                                     
           05  FILLER           PIC X(06)  VALUE '   SEQ'.                      
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(08)  VALUE 'REGN NO'.                     
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(09)  VALUE 'NRIC1'.                       
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(09)  VALUE 'NRIC2'.                       
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(03)  VALUE 'NT'.                          
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE 'FT'.                          
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE 'HH'.                          
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(03)  VALUE '5YR'.                         
           05  FILLER           PIC X(05)  VALUE SPACES.                        
           05  FILLER           PIC X(08)  VALUE 'REGN NO'.                     
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(09)  VALUE 'NRIC1'.                       
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(09)  VALUE 'NRIC2'.                       
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE 'HH'.                          
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(03)  VALUE '5YR'.                         
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(06)  VALUE 'BALLOT'.                      
                                                                                
       01  L721-PR-HEAD-05.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L721-PR-DETAILS.                                                     
           05  L721-NUM-SEQ     PIC ZZZZZ9.                                     
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-F-REGN  PIC X(08)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-F-NRIC1 PIC X(09)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-F-NRIC2 PIC X(09)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-F-NT    PIC X(03)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-F-FT    PIC X(02)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-F-HH    PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(04)  VALUE SPACES.                        
           05  L721-NUM-F-5YR   PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(06)  VALUE SPACES.                        
           05  L721-NUM-K-REGN  PIC X(08)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-K-NRIC1 PIC X(09)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-K-NRIC2 PIC X(09)  VALUE SPACES.                        
           05  FILLER           PIC X(02)  VALUE SPACES.                        
           05  L721-NUM-K-HH    PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(04)  VALUE SPACES.                        
           05  L721-NUM-K-5YR   PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(03)  VALUE SPACES.                        
           05  L721-NUM-K-BALLT PIC X(06)  VALUE SPACES.                        
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F730     THRU 2000-EXIT                            
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL F730-ALT1-KEY = HIGH-VALUES.                           
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K595                                                 
                OUTPUT BP13L721.                                                
                                                                                
           IF WS-K595-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'FILE BP13K595 OPEN ERROR'                                
              DISPLAY 'ERROR STATUS = ' WS-K595-STATUS                          
              MOVE WS-K595-STATUS TO RETURN-CODE                                
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/' WS-SYSTEM-DATE(5:2) '/'              
                   WS-SYSTEM-DATE(1:4) DELIMITED BY SIZE                        
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
           COMPUTE WS-NUM-CCYY = WS-SYS-CCYY - 1.                               
           MOVE WS-SYS-MM              TO WS-NUM-MM.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F730.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13F730                                                        
              AT END MOVE HIGH-VALUES TO F730-ALT1-KEY                          
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F730.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO K595-ALT1-KEY.                                        
           MOVE F730-NUM-NRIC1 TO WS-TEMP-NRIC.                                 
                                                                                
           MOVE 'N'                      TO   WS-K595-EOF.                      
           MOVE 'N'                      TO   WS-PRINT.                         
           MOVE 'Y'                      TO WS-FIRST-READ                       
           PERFORM 3300-START-K595-NRIC1 THRU 3300-EXIT.                        
           MOVE 'N'                      TO   WS-K595-EOF.                      
           PERFORM 3600-START-K595-NRIC2 THRU 3600-EXIT.                        
                                                                                
           IF F730-NUM-NRIC2(1:1) = '#' OR SPACE OR LOW-VALUE                   
              PERFORM 2000-READ-F730     THRU 2000-EXIT                         
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES TO K595-ALT2-KEY.                                        
           MOVE F730-NUM-NRIC2 TO WS-TEMP-NRIC.                                 
                                                                                
           MOVE 'N'                      TO   WS-K595-EOF.                      
           PERFORM 3300-START-K595-NRIC1 THRU 3300-EXIT.                        
           MOVE 'N'                      TO   WS-K595-EOF.                      
           PERFORM 3600-START-K595-NRIC2 THRU 3600-EXIT.                        
                                                                                
           IF WS-PRINT = 'Y'                                                    
              IF TEMP-NUM-K-BALLT >= WS-NUM-DTE-BALLOT                          
                 IF F730-CDE-HOUSEHOLD NOT = TEMP-NUM-K-HH                      
                    PERFORM 4600-MOVE-TO-PRINT THRU 4600-EXIT                   
                 ELSE                                                           
                    IF F730-NUM-5YR-OCCUPN NOT = TEMP-NUM-K-5YR                 
                       PERFORM 4600-MOVE-TO-PRINT THRU 4600-EXIT                
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F730        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3300-START-K595-NRIC1.                                                   
                                                                                
           MOVE WS-TEMP-NRIC   TO K595-NUM-NRIC1.                               
                                                                                
           START BP13K595 KEY >= K595-ALT1-KEY.                                 
                                                                                
           IF WS-K595-STATUS = ZEROES                                           
              PERFORM 4000-READ-BP13K595 THRU 4000-EXIT                         
                UNTIL WS-K595-EOF = 'Y'                                         
                   OR (K595-NUM-NRIC1 NOT = WS-TEMP-NRIC)                       
           ELSE                                                                 
              IF WS-K595-STATUS = 23                                            
                 ADD 1 TO WS-NOTFD                                              
                 DISPLAY ' NRIC NOTFD=' WS-TEMP-NRIC                            
                         ' REGN=' F730-NUM-REGN                                 
              ELSE                                                              
                 DISPLAY ' BP13K595 START ERROR : ' WS-K595-STATUS              
                         ' F730-NRIC=' WS-TEMP-NRIC                             
                 MOVE     WS-K595-STATUS      TO RETURN-CODE                    
                 PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3600-START-K595-NRIC2.                                                   
                                                                                
           MOVE WS-TEMP-NRIC   TO K595-NUM-NRIC2.                               
                                                                                
           START BP13K595 KEY >= K595-ALT2-KEY.                                 
                                                                                
           IF WS-K595-STATUS = ZEROES                                           
              PERFORM 4001-READ-BP13K597 THRU 4001-EXIT                         
                UNTIL WS-K595-EOF = 'Y'                                         
                   OR (K595-NUM-NRIC2 NOT = WS-TEMP-NRIC)                       
           ELSE                                                                 
              IF WS-K595-STATUS = 23                                            
                 ADD 1 TO WS-NOTFD                                              
                 DISPLAY ' NRIC NOTFD=' WS-TEMP-NRIC                            
                         ' REGN=' F730-NUM-REGN                                 
              ELSE                                                              
                 DISPLAY ' BP13K595 START ERROR : ' WS-K595-STATUS              
                         ' F730-NRIC=' WS-TEMP-NRIC                             
                 MOVE     WS-K595-STATUS      TO RETURN-CODE                    
                 PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BP13K595.                                                      
                                                                                
           READ BP13K595 NEXT AT END MOVE 'Y' TO WS-K595-EOF                    
                                     GO TO 4000-EXIT.                           
                                                                                
           IF K595-NUM-NRIC1 = WS-TEMP-NRIC                                     
              IF WS-FIRST-READ = 'Y'                                            
                 PERFORM 4300-MOVE-TO-TEMP  THRU 4300-EXIT                      
                 MOVE 'N' TO WS-FIRST-READ                                      
              END-IF                                                            
              IF K595-DTE-BALLOT > TEMP-NUM-K-BALLT                             
                 IF K595-NUM-REGN NOT = F730-NUM-REGN                           
                    IF K595-NUM-RANDOM NOT = SPACES AND LOW-VALUES              
                       PERFORM 4300-MOVE-TO-TEMP  THRU 4300-EXIT                
                       MOVE 'Y'     TO WS-PRINT                                 
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF WS-FIRST-READ = 'Y'                                            
                 ADD 1 TO WS-NOTFD                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4001-READ-BP13K597.                                                      
                                                                                
           READ BP13K595 NEXT AT END MOVE 'Y' TO WS-K595-EOF                    
                                     GO TO 4001-EXIT.                           
                                                                                
           IF K595-NUM-NRIC2 = WS-TEMP-NRIC                                     
              IF K595-DTE-BALLOT > TEMP-NUM-K-BALLT                             
                 IF K595-NUM-REGN NOT = F730-NUM-REGN                           
                    PERFORM 4300-MOVE-TO-TEMP  THRU 4300-EXIT                   
                    MOVE 'Y'     TO WS-PRINT                                    
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF WS-FIRST-READ = 'Y'                                            
                 ADD 1 TO WS-NOTFD                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4300-MOVE-TO-TEMP.                                                       
      *---------------------------------------------------------------*         
                                                                                
           MOVE F730-NUM-REGN            TO TEMP-NUM-F-REGN.                    
           MOVE F730-NUM-NRIC1           TO TEMP-NUM-F-NRIC1.                   
           MOVE F730-NUM-NRIC2           TO TEMP-NUM-F-NRIC2.                   
           MOVE F730-CDE-NT1             TO TEMP-NUM-F-NT.                      
           MOVE F730-CDE-FLAT-TYPE       TO TEMP-NUM-F-FT.                      
           MOVE F730-CDE-HOUSEHOLD       TO TEMP-NUM-F-HH.                      
           MOVE F730-NUM-5YR-OCCUPN      TO TEMP-NUM-F-5YR.                     
                                                                                
           MOVE K595-NUM-REGN            TO TEMP-NUM-K-REGN.                    
           MOVE K595-NUM-NRIC1           TO TEMP-NUM-K-NRIC1.                   
           MOVE K595-NUM-NRIC2           TO TEMP-NUM-K-NRIC2.                   
           MOVE K595-CDE-HOUSEHOLD       TO TEMP-NUM-K-HH.                      
           MOVE K595-NUM-5YR-OCCUPN      TO TEMP-NUM-K-5YR.                     
           IF K595-DTE-BALLOT = LOW-VALUES OR SPACES                            
              MOVE ZEROES                TO TEMP-NUM-K-BALLT                    
           ELSE                                                                 
              MOVE K595-DTE-BALLOT       TO TEMP-NUM-K-BALLT                    
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4600-MOVE-TO-PRINT.                                                      
      *---------------------------------------------------------------*         
                                                                                
           MOVE TEMP-NUM-F-REGN          TO L721-NUM-F-REGN.                    
           MOVE TEMP-NUM-F-NRIC1         TO L721-NUM-F-NRIC1.                   
           MOVE TEMP-NUM-F-NRIC2         TO L721-NUM-F-NRIC2.                   
           MOVE TEMP-NUM-F-NT            TO L721-NUM-F-NT.                      
           MOVE TEMP-NUM-F-FT            TO L721-NUM-F-FT.                      
           MOVE TEMP-NUM-F-HH            TO L721-NUM-F-HH.                      
           MOVE TEMP-NUM-F-5YR           TO L721-NUM-F-5YR.                     
                                                                                
           MOVE TEMP-NUM-K-REGN          TO L721-NUM-K-REGN.                    
           MOVE TEMP-NUM-K-NRIC1         TO L721-NUM-K-NRIC1.                   
           MOVE TEMP-NUM-K-NRIC2         TO L721-NUM-K-NRIC2.                   
           MOVE TEMP-NUM-K-HH            TO L721-NUM-K-HH.                      
           MOVE TEMP-NUM-K-5YR           TO L721-NUM-K-5YR.                     
           MOVE TEMP-NUM-K-BALLT         TO L721-NUM-K-BALLT.                   
                                                                                
           PERFORM 5000-WRITE-L721     THRU 5000-EXIT.                          
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-WRITE-L721.                                                         
      *---------------------------------------------------------------*         
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 53                                          
              PERFORM 5500-PRINT-HEADING THRU 5500-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           ADD   1                 TO    WS-COUNT.                              
           MOVE WS-COUNT           TO    L721-NUM-SEQ.                          
           WRITE BP13L721-REC      FROM  L721-PR-DETAILS   AFTER 1.             
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE.                   
           MOVE  SPACES            TO    L721-PR-DETAILS.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5500-PRINT-HEADING.                                                      
      *---------------------------------------------------------------*         
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L721-PRINT-PAGE.                        
           MOVE WS-EDITED-DATE   TO     L721-PRINT-DATE.                        
                                                                                
           MOVE SPACES           TO     BP13L721-REC.                           
                                                                                
           WRITE BP13L721-REC    AFTER  PAGE.                                   
           WRITE BP13L721-REC    FROM   L721-PR-HEAD-01 AFTER 1.                
           WRITE BP13L721-REC    FROM   L721-PR-HEAD-02 AFTER 1.                
           WRITE BP13L721-REC    FROM   L721-PR-HEAD-03 AFTER 1.                
           WRITE BP13L721-REC    FROM   L721-PR-HEAD-04 AFTER 1.                
           WRITE BP13L721-REC    FROM   L721-PR-HEAD-05 AFTER 1.                
                                                                                
           MOVE 5  TO WS-CNT-LINE-ON-PAGE.                                      
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '--- BP13C721 CONTROL TOTAL --------------'.                 
           DISPLAY '                    RUN DATE  : ' WS-EDITED-DATE.           
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC READ FROM BP13F730  : ' WS-READ-F730.             
           DISPLAY 'NO OF F730 REC PRINTED        : ' WS-COUNT.                 
           DISPLAY 'NO OF F730 REC NOT FOUND      : ' WS-NOTFD.                 
           DISPLAY '-----------------------------------------'.                 
                                                                                
           CLOSE BP13F730                                                       
                 BP13K595                                                       
                 BP13L721.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
