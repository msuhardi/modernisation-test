       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C267.                                                  
      *AUTHOR.       SONNY A.                                                   
      *DATE-WRITTEN. 26/11/96.                                                  
      ***********************************************************               
      *                                                         *               
      *  INPUT FILE :  BP13F800                                 *               
      *                BM06K110                                 *               
      *  OUTPUT FILE:  BP13F80A                                 *               
      *                                                         *               
      ***********************************************************               
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  TO OMIT TP CASES FROM F800                    *                
      *                                                        *                
      **********************************************************                
      * CHG-NO   BY  DATE    DESCRIPTION                         *              
      * -------- --- ------  -----------                         *              
      * BP130418 SIA 980304  CHGE BM06K211 TO BM06K110           *              
      * ======================================================== *              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT BP13F80A ASSIGN TO BP13F80A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F800.                                                           
                                                                                
       FD  BM06K110                                                             
                      RECORD CONTAINS 500 CHARACTERS.                           
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F80A   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       01  WS-F800A-REC                   PIC X(2000).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F800-EOF               PIC X    VALUE 'N'.                    
               88 F800-EOF                        VALUE 'Y'.                    
           05  WS-IS-OK                  PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F800          PIC 9(6) VALUE ZEROS.                  
           05  WS-K110-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K110-NOTFD             PIC 9(6) VALUE ZEROS.                  
           05  WS-F80A-WRITTEN           PIC 9(6) VALUE ZEROS.                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE                  PIC X(8).                              
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F800    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-K110 THRU 300-EXIT                              
                    UNTIL F800-EOF.                                             
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                OUTPUT BP13F80A.                                                
                                                                                
           IF WS-K110-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       200-READ-F800.                                                           
      *-----------------                                                        
           READ BP13F800 AT END                                                 
           MOVE 'Y'                      TO WS-F800-EOF                         
           GO TO 200-EXIT.                                                      
           ADD 1                         TO WS-READ-CNT-F800.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       300-PROCESS-K110.                                                        
      *-----------------                                                        
                                                                                
           MOVE 'Y' TO WS-IS-OK.                                                
                                                                                
           IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES                           
              CONTINUE                                                          
           ELSE                                                                 
              MOVE F800-NUM-SCH-ACC TO K110-SCH-ACC-NO                          
              PERFORM 400-READ-K110  THRU 400-EXIT                              
           END-IF.                                                              
                                                                                
           IF WS-IS-OK = 'Y'                                                    
              WRITE WS-F800A-REC FROM BP13F800-MASTER                           
              ADD 1 TO WS-F80A-WRITTEN.                                         
                                                                                
           PERFORM 200-READ-F800    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       400-READ-K110.                                                           
      *--------------------                                                     
                                                                                
           READ BM06K110                                                        
                INVALID KEY                                                     
                DISPLAY 'RECORD NOT FOUND IN BM06K110 : ' F800-NUM-REGN         
                DISPLAY '             BM06K110 STATUS : ' WS-K110-STATUS        
                ADD 1 TO WS-K110-NOTFD                                          
                GO TO 400-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD  1  TO WS-K110-READ                                              
                                                                                
           IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR ZEROES              
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'N' TO WS-IS-OK.                                             
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BP13F80A                                                       
                 BM06K110.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K110, STATUS ' WS-K110-STATUS          
              MOVE WS-K110-STATUS        TO RETURN-CODE.                        
                                                                                
                                                                                
           DISPLAY '*------------------ BP13C267 -------------------'.          
           DISPLAY '                  ' WS-CURR-DATE                            
           DISPLAY 'NO OF REC READ        BP13F800   = '                        
                    WS-READ-CNT-F800.                                           
           DISPLAY 'NO OF REC READ        BM06K110   = '                        
                    WS-K110-READ.                                               
           DISPLAY 'NO OF REC NOTFOUND    BM06K110   = '                        
                    WS-K110-NOTFD.                                              
           DISPLAY 'NO OF REC WRITTEN     BP13F80A   = '                        
                    WS-F80A-WRITTEN.                                            
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
